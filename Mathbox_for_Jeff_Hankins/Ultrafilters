$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Ultrafilters
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c UFil $.
  $( Extend class notation with the class of all ultrafilters. $)
  cufil $a class UFil $.

  ${
    $d f x $.
    $( Define the class of all ultrafilters. $)
    df-ufil $a |- UFil = { f e. Fil | A. x e. ~P U. f ( x e. f \/ ( U. f \ x )
    e. f ) } $.
  $}

  ${
    $d f t w x y z F $.  $d f t w x y z X $.
    isufil.1 $e |- X = U. F $.
    $( The property of being an ultrafilter.  (Contributed by Jeff Hankins,
       30-Nov-2009.) $)
    isufil $p |- ( F e. UFil <-> ( F e. Fil /\ A. x e. ~P X ( x e. F \/ ( X \ x
    ) e. F ) ) ) $=
      ( vf cv wcel cuni cdif wo cpw wral cfil cufil wceq unieq syl6eqr pweqd
      eleq2 difeq1d id eleq12d orbi12d raleqbidv df-ufil elrab2 ) AFZEFZGZUHHZU
      GIZUHGZJZAUJKZLUGBGZCUGIZBGZJZACKZLEBMNUHBOZUMURAUNUSUTUJCUTUJBHCUHBPDQZR
      UTUIUOULUQUHBUGSUTUKUPUHBUTUJCUGVATUTUAUBUCUDAEUEUF $.
      $( [30-Nov-2009] $)
  $}

  ${
    $d x F $.
    $( An ultrafilter is a filter.  (Contributed by Jeff Hankins,
       1-Dec-2009.) $)
    ufilfil $p |- ( F e. UFil -> F e. Fil ) $=
      ( vx cufil wcel cfil cv cuni cdif wo cpw wral eqid isufil simplbi ) ACDAE
      DBFZADAGZOHADIBPJKBAPPLMN $.
      $( [1-Dec-2009] $)
  $}

  ${
    $d f x y F $.  $d x S $.  $d f x y X $.
    ufilss.1 $e |- X = U. F $.
    $( For any subset of the base set of an ultrafilter, either the set is in
       the ultrafilter or the complement is.  (Contributed by Jeff Hankins,
       1-Dec-2009.) $)
    ufilss $p |- ( ( F e. UFil /\ S C_ X ) -> ( S e. F \/ ( X \ S ) e. F ) ) $=
      ( vx cufil wcel wss cdif wo cpw cvv wb cuni uniexg syl5eqel elpw2g syl
      cv wral wi cfil isufil simprbi wceq eleq1 difeq2 eleq1d orbi12d rcla4cv
      sylbird imp ) BFGZACHZABGZCAIZBGZJZUMUNACKZGZURUMCLGUTUNMUMCBNLDBFOPACLQR
      UMESZBGZCVAIZBGZJZEUSTZUTURUAUMBUBGVFEBCDUCUDVEUREAUSVAAUEZVBUOVDUQVAABUF
      VGVCUPBVAACUGUHUIUJRUKUL $.
      $( [1-Dec-2009] $)

    $( The complement is in an ultrafilter iff the set is not.  (Contributed by
       Mario Carneiro, 8-Dec-2013.) $)
    ufilb $p |- ( ( F e. UFil /\ S C_ X ) ->
                  ( -. S e. F <-> ( X \ S ) e. F ) ) $=
      ( cufil wcel wss wa wn cdif ufilss ord wi cfil cfbas ufilfil filfbas
      fbncp ex con2d 3syl adantr impbid ) BEFZACGZHZABFZIZCAJBFZUFUGUIABCDKLUDU
      IUHMZUEUDBNFBOFZUJBPBQUKUGUIUKUGUIIACBRSTUAUBUC $.
      $( [11-Dec-2013] $)

    ${
      $d x G $.  $d x Y $.
      ufilmax.2 $e |- Y = U. G $.
      $( Any filter finer than an ultrafilter is actually equal to it.
         (Contributed by Jeff Hankins, 1-Dec-2009.) $)
      ufilmax $p |- ( ( ( F e. UFil /\ G e. Fil ) /\ ( X = Y /\ F C_ G ) ) ->
                      F = G ) $=
        ( vx cufil wcel cfil wa wceq wss simprr cv wi cuni elssuni syl6sseqr
        a1i wb sseq2 ad2antrl sylibrd wn cdif simpl simpr ufilb syl2an simprlr
        sseld cfbas filfbas fbncp ex syl con2d ad2antlr syld sylbid con4d expr
        com23 mpdd ssrdv eqssd ) AHIZBJIZKZCDLZABMZKZKZABVJVKVLNVNGBAVNGOZBIZVO
        CMZVOAIZVNVPVODMZVQVPVSPVNVPVOBQDVOBRFSTVKVQVSUAVJVLCDVOUBUCUDVNVQVPVRV
        JVMVQVPVRPVJVMVQKZKZVRVPWAVRUEZCVOUFZAIZVPUEZVJVHVQWBWDUAVTVHVIUGVMVQUH
        VOACEUIUJWAWDWCBIZWEWAABWCVJVKVLVQUKULVIWFWEPVHVTVIVPWFVIBUMIZVPWFUEZPB
        UNWGVPWHVOCBUOUPUQURUSUTVAVBVCVDVEVFVG $.
        $( [8-Dec-2013] $) $( [1-Dec-2009] $)
    $}

    $( The maximal property of an ultrafilter.  (Contributed by Jeff Hankins,
       30-Nov-2009.)  (The proof was shortened by Mario Carneiro,
       8-Dec-2013.) $)
    isufil2 $p |- ( F e. UFil <-> ( F e. Fil /\ A. f e. Fil ( ( X = U. f /\ F
    C_ f ) -> F = f ) ) ) $=
      ( vx vy cufil wcel cfil cv cuni wceq wss wa wi wral ufilfil eqid ufilmax
      ex r19.21aiva jca cdif wo cpw simpl elpwi wn cin c0 wne csn cun cfi cfv
      cfg uniun visset unisn eqcomi uneq12i eqtr4i simplr ssequn2 sylib
      syl5req cvv simpll snex unexg sylancl fiuni syl cfbas simpr wsbc ralsn
      ineq2 neeq1d sbcie bitr2i ralbii wb filfbas ad2antrr inss2 filusb adantr
      ineq1 rcla4va sylan ssn0 sylancr snfil fbunfip syl11anc mpbird ssun2
      snnz mp2an jctil fsubbas fgbas 3eqtrd ssun1 a1i ssfii fbssfg sstrd a1d
      fgfil unieq eqeq2d sseq2 anbi12d eqeq2 imbi12d rcla4v mpdd snid sselii
      sseldd eleq2 syl5ibrcom syld impancom an32s con3d wrex rexnal nne
      elssuni syl6sseqr reldisj adantl difss fillsb 3expd mpii imp sylbid
      syl5bi r19.23adva syl5bir orrd sylan2 isufil sylanbrc impbii ) BGHZBIHZCA
      JZKZLZBUULMZNZBUULLZOZAIPZNZUUJUUKUUSBQUUJUURAIUUJUULIHNUUPUUQBUULCUUMDUU
      MRSTUAUBUUTUUKEJZBHZCUVAUCZBHZUDZECUEZPUUJUUKUUSUFUUTUVEEUVFUVAUVFHUUTUVA
      CMZUVEUVACUGUUTUVGNZUVBUVDUVHUVBUHFJZUVAUIZUJUKZFBPZUHZUVDUVHUVLUVBUUKUVG
      UUSUVLUVBOUUKUVGNZUVLUUSUVBUVNUVLNZUUSBBUVAULZUMZUNUOZUPUOZLZUVBUVOUUSCUV
      SKZLZBUVSMZNZUVTUVOUWDUUSUVOUWBUWCUVOCUVQKZUVRKZUWAUVOUWECUVAUMZCUWEBKZUV
      PKZUMUWGBUVPUQCUWHUVAUWIDUWIUVAUVAEURZUSUTVAVBUVOUVGUWGCLUUKUVGUVLVCUVACV
      DVEVFUVOUVQVGHZUWEUWFLUVOUUKUVPVGHUWKUUKUVGUVLVHUVAVIBUVPIVGVJVKZUVQVGVLV
      MUVOUVRVNHZUWFUWALUVOUWMUVQUJUKZUJUVRHUHZNZUVOUWOUWNUVOUWOUVIUULUIZUJUKZA
      UVPPZFBPZUVOUVLUWTUVNUVLVOUVKUWSFBUWSUWRAUVAVPUVKUWRAUVAUWJVQUWRUVKAUVAUW
      JUULUVALUWQUVJUJUULUVAUVIVRVSVTWAWBVEUVOBVNHZUVPVNHZUWOUWTWCUUKUXAUVGUVLB
      WDWEUVOUVAVGHZUVAUJUKZUXBUWJUVOCUVAUIZUVAMUXEUJUKZUXDCUVAWFUVNCBHZUVLUXFU
      UKUXGUVGBCDWGWHUVKUXFFCBUVICLUVJUXEUJUVICUVAWIVSWJWKUXEUVAWLWMUXCUXDNUVPI
      HUXBUVAVGWNUVPWDVMWMFABUVPWOWPWQUVPUVQMUVPUJUKUWNUVPBWRZUVAUWJWSUVPUVQWLW
      TXAUVOUWKUWMUWPWCUWLUVQVGXBVMWQZUVRUWFUWFRXCVMXDUVOBUVQUVSBUVQMUVOBUVPXEX
      FUVOUVQUVRUVSUVOUWKUVQUVRMUWLUVQVGXGVMUVOUWMUVRUVSMUXIUVRXHVMXIZXIUBXJUVO
      UVSIHZUUSUWDUVTOZOUVOUWMUXKUXIUVRXKVMUURUXLAUVSIUULUVSLZUUPUWDUUQUVTUXMUU
      NUWBUUOUWCUXMUUMUWACUULUVSXLXMUULUVSBXNXOUULUVSBXPXQXRVMXSUVOUVBUVTUVAUVS
      HUVOUVQUVSUVAUXJUVAUVQHUVOUVPUVQUVAUXHUVAUWJXTYAXFYBBUVSUVAYCYDYEYFYGYHUU
      KUVMUVDOUUSUVGUVMUVKUHZFBYIUUKUVDUVKFBYJUUKUXNUVDFBUXNUVJUJLZUUKUVIBHZNZU
      VDUVJUJYKUXQUXOUVIUVCMZUVDUXPUXOUXRWCZUUKUXPUVICMUXSUXPUVIUWHCUVIBYLDYMUV
      IUVACYNVMYOUUKUXPUXRUVDOZUUKUXPUVCCMZUXTCUVAYPUUKUXPUYAUXRUVDUVIUVCBCDYQY
      RYSYTUUAUUBUUCUUDWEYEUUEUUFUAEBCDUUGUUHUUI $.
      $( [8-Dec-2013] $) $( [30-Nov-2009] $)

    $( An ultrafilter is a prime filter.  (Contributed by Jeff Hankins,
       1-Jan-2010.) $)
    ufprim $p |- ( ( F e. UFil /\ A C_ X /\ B C_ X ) -> ( ( A e. F \/ B e. F )
    <-> ( A u. B ) e. F ) ) $=
      ( cufil wcel wss w3a wo cun wa cfil ufilfil 3ad2ant1 adantr simpr unss
      biimpi 3adant1 ssun1 a1i 3jca fillsb sylc ex ssun2 jaod wn cdif wb ufilb
      3adant3 cin difun2 uncom difeq1i eqtr3i ineq2i indifcom 3eqtr4i filint
      syl3an1 syl5eqel simp3 inss1 3expia sylbid orrd impbid ) CFGZADHZBDHZIZAC
      GZBCGZJZABKZCGZVNVOVSVPVNVOVSVNVOLZCMGZVOVRDHZAVRHZIVSVNWAVOVKVLWAVMCNOZP
      VTVOWBWCVNVOQVNWBVOVLVMWBVKVLVMLWBABDRSTZPWCVTABUAUBUCAVRCDEUDUEUFVNVPVSV
      NVPLZWAVPWBBVRHZIVSVNWAVPWDPWFVPWBWGVNVPQVNWBVPWEPWGWFBAUGUBUCBVRCDEUDUEU
      FUHVNVSVQVNVSLZVOVPWHVOUIZDAUJZCGZVPVNWIWKUKZVSVKVLWLVMACDEULUMPVNVSWKVPV
      NVSWKIZWABWJUNZCGZVMWNBHZIVPVNVSWAWKWDOWMWOVMWPWMWNVRWJUNZCDBAUJZUNDVRAUJ
      ZUNWNWQWRWSDBAKZAUJWRWSBAUOWTVRABAUPUQURUSBDAUTVRDAUTVAVNWAVSWKWQCGWDVRWJ
      CVBVCVDVNVSVMWKVKVLVMVEOWPWMBWJVFUBUCWNBCDEUDUEVGVHVIUFVJ $.
      $( [8-Dec-2013] $) $( [1-Jan-2010] $)
  $}

  ${
    $d f g h x y F $.  $d f g h x y X $.
    filssufil.1 $e |- X = U. F $.
    $( A filter is contained in some ultrafilter.  (Requires the Axiom of
       Choice, via ~ zornn0 .)  (Contributed by Jeff Hankins, 2-Dec-2009.)
       (The proof was shortened by Mario Carneiro, 11-Dec-2013.) $)
    filssufil $p |- ( F e. Fil -> E. f e. UFil ( X = U. f /\ F C_ f ) ) $=
      ( vh vg vx vy cfil wcel cv cuni wceq wss wa wpss wn wi wral wrex cufil
      crab cvv c0 wne wo w3a wal cpw rabss eqimss2 adantr visset elpw sspwuni
      bitri sylibr a1i mprgbir uniexg syl5eqel pwexg 3syl ssexg sylancr id
      ssid pm3.2i jctir unieq eqeq2d sseq2 anbi12d elrab ne0i syl cun simpr1
      ssrab sylib simpld simpr2 simpl eqcomd ralimi adantl simpr3 ssequn1
      eleq1 biimprcd syl5bi ssequn2 jaod ralimdva ralimia uneq1 eleq1d ralbidv
      cbvralv jca r19.26 filuni syl111anc ciun wex eluni rexbii eliun eluni2
      anbi2i r19.42v bitr4i exbii rexcom4 3bitr4i eqriv iuneq2 syl5eqr
      iunconst eqtr2d n0 ssel2 simprr ssuni sylancom ex 19.23adv sylc jca32
      19.21aiv neeq1 3anbi1d eleq2 syl6bb imbi12d albidv raleq ralrab
      rexeqbi1dv rexrab zornn0 vtoclg eqeq1 anbi1d biimprd sstr2 anim2d sylan9
      imim1d df-pss biimpri necon1bd a2i syl6 ralimdv imp anim2i eqid isufil2
      simprl reximi2 ) BIJZCAKZLZMZBUVCNZOZCEKZLZMZBUVHNZOZUVCUVHPZQZRZEISZOZAI
      TZUVGAUATUVBCFKZLZMZBUVSNZOZFIUBZUCJZUWDUDUEZGKZUWDNZUWGUDUEZUVCUVHNZUVHU
      VCNZUFZEUWGSZAUWGSZUGZUWGLZIJZCUWPLZMZBUWPNZOZOZRZGUHZOZUVRUVBUWDCUIZUIZN
      ZUXGUCJZUWEUXHUWCUVSUXGJZRZFIUWCFIUXGUJUXKUVSIJUWCUVTCNZUXJUWAUXLUWBUVTCU
      KULUXJUVSUXFNUXLUVSUXFFUMUNUVSCUOUPUQURUSUVBCUCJUXFUCJUXIUVBCBLZUCDBIUTVA
      CUCVBUXFUCVBVCUWDUXGUCVDVEUVBUWFUXDUVBBUWDJZUWFUVBUVBCUXMMZBBNZOZOUXNUVBU
      VBUXQUVBVFUXOUXPDBVGVHVIUWCUXQFBIUVSBMZUWAUXOUWBUXPUXRUVTUXMCUVSBVJVKUVSB
      BVLVMVNUQUWDBVOVPUVBUXCGUVBUWOUXBUVBUWOOZUWQUWSUWTUXSUWQUWSUXSUWGINZUWIUV
      TCMZUVSUVHVQZUWGJZEUWGSZOFUWGSZUWQUWSOUXSUXTUWCFUWGSZUXSUWHUXTUYFOZUVBUWH
      UWIUWNVRZUWCFIUWGVSVTZWAUVBUWHUWIUWNWBZUXSUYAFUWGSZUYDFUWGSZOUYEUXSUYKUYL
      UXSUYGUYKUYIUYFUYKUXTUWCUYAFUWGUWCCUVTUWAUWBWCWDWEWFVPZUXSUWNUYLUVBUWHUWI
      UWNWGUWNUVCUVHVQZUWGJZEUWGSZAUWGSUYLUWMUYPAUWGUVCUWGJZUWLUYOEUWGUYQUVHUWG
      JZOUWJUYOUWKUYRUWJUYORUYQUWJUYNUVHMZUYRUYOUVCUVHWHUYSUYOUYRUYNUVHUWGWIWJW
      KWFUYQUWKUYORUYRUWKUYNUVCMZUYQUYOUVHUVCWLUYTUYOUYQUYNUVCUWGWIWJWKULWMWNWO
      UYPUYDAFUWGUVCUVSMZUYOUYCEUWGVUAUYNUYBUWGUVCUVSUVHWPWQWRWSVTVPWTUYAUYDFUW
      GXAUQFEUWGCXBXCWAUXSUWRFUWGCXDZCUXSUWRFUWGUVTXDZVUBEVUCUWRUVHUVTJZFUWGTUV
      HUVCJZUVCUVSJZOZAXEZFUWGTZUVHVUCJUVHUWRJZVUDVUHFUWGAUVHUVSXFXGFUVHUWGUVTX
      HVUEUVCUWPJZOZAXEVUGFUWGTZAXEVUJVUIVULVUMAVULVUEVUFFUWGTZOVUMVUKVUNVUEFUV
      CUWGXIXJVUEVUFFUWGXKXLXMAUVHUWPXFVUGFAUWGXNXOXOXPUXSUYKVUCVUBMUYMFUWGUVTC
      XQVPXRUXSUWIVUBCMUYJFUWGCXSVPXTUXSUWHUWIUWTUYHUYJUWIUYREXEUWHUWTEUWGYAUWH
      UYRUWTEUWHUYRUWTUWHUYRUVKUWTUWHUYROZUVHIJZUVLOZUVKVUOUVHUWDJVUQUWGUWDUVHY
      BUWCUVLFUVHIUVSUVHMZUWAUVJUWBUVKVURUVTUVICUVSUVHVJVKUVSUVHBVLVMZVNVTVUPUV
      JUVKYCVPBUVHUWGYDYEYFYGWKYHYIYFYJWTHKZUDUEZUWGVUTNZUWIUWNUGZUWPVUTJZRZGUH
      ZOZUVNEVUTSZAVUTTZRUXEUVRRHUWDUCVUTUWDMZVVGUXEVVIUVRVVJVVAUWFVVFUXDVUTUWD
      UDYKVVJVVEUXCGVVJVVCUWOVVDUXBVVJVVBUWHUWIUWNVUTUWDUWGVLYLVVJVVDUWPUWDJUXB
      VUTUWDUWPYMUWCUXAFUWPIUVSUWPMZUWAUWSUWBUWTVVKUVTUWRCUVSUWPVJVKUVSUWPBVLVM
      VNYNYOYPVMVVJVVIUVPAUWDTUVRVVHUVPAVUTUWDVVJVVHUVNEUWDSUVPUVNEVUTUWDYQUWCU
      VLUVNEFIVUSYRYNYSUWCUVGUVPFAIUVSUVCMZUWAUVEUWBUVFVVLUVTUVDCUVSUVCVJVKUVSU
      VCBVLVMYTYNYOAEGVUTHUMUUAUUBYHUVQUVGAIUAUVCIJZUVQOZUVCUAJZUVGVVNVVMUVDUVI
      MZUWJOZUVCUVHMZRZEISZOVVOUVQVVTVVMUVGUVPVVTUVGUVOVVSEIUVGUVOVVQUVNRVVSUVG
      VVQUVLUVNUVEVVQUVJUWJOZUVFUVLUVEVWAVVQUVEUVJVVPUWJCUVDUVIUUCUUDUUEUVFUWJU
      VKUVJBUVCUVHUUFUUGUUHUUIVVQUVNVVRUWJUVNVVRRVVPUWJUVMUVCUVHUWJUVCUVHUEZUVM
      UVMUWJVWBOUVCUVHUUJUUKYFUULWFUUMUUNUUOUUPUUQEUVCUVDUVDUURUUSUQVVMUVGUVPUU
      TWTUVAVP $.
      $( [11-Dec-2013] $) $( [2-Dec-2009] $)
  $}

  ${
    $d f g x F $.  $d f g x X $.
    ufileu.1 $e |- X = U. F $.
    $( If the ultrafilter containing a given filter is unique, the filter is an
       ultrafilter.  (Contributed by Jeff Hankins, 3-Dec-2009.)  (The proof was
       shortened by Mario Carneiro, 12-Dec-2013.) $)
    ufileu $p |- ( F e. Fil ->
        ( F e. UFil <-> E! f e. UFil ( X = U. f /\ F C_ f ) ) ) $=
      ( vg vx cfil wcel cufil cv cuni wceq wss wa wb wral wrex wreu wi ufilfil
      eqid ufilmax eqcomd ex sylan2 unieq syl6reqr eqimss2 jca impbid1
      r19.21aiva eqeq2 bibi2d ralbidv rcla4ev mpdan ibibr pm5.74ri eqeq2d
      sseq2 anbi12d bitr3d rcla4va adantll simprd elssuni simpld sseq2d
      syl5ibr wn cdif adantr difeq1d csn cun cfi cfv cfg cfbas c0 wne cvv
      simpll snex unexg sylancl fsubbas syl emnfil ssun1 ssn0 mpan 3syl cin
      syl6sseqr df-ss sylib ad2antll sseq1d fillsb 3expd com23 imp3a imp
      sylbid con3d expr impr filusb ad2antrr difexg wsbc ralsng ineq2 neeq1d
      sbcieg bitrd inssdif0 necon3bbii syl6bbr mpbird filfbas ssdif0 eqss
      biimpri eleq1 notbid biimpcd sylan9 adantl syl5bir necon2ad mpd snfil
      syl11anc fbunfip mpbir2and fgfil filssufil a1i unisng uneq12d difss
      ssequn2 mpbi syl6req uniun syl6eqr fiuni fgbas 3eqtrd eqeq1d anbi1d
      rexbidv r19.29 bi1 ssfii fbssfg sstrd sstr2 anim2d imim1d a2i syl6 syl5
      r19.23adva mpan2d an32s snidg elun2 sseldd adantlr eqeltrrd simpllr
      biimpa adantrr ufilb con4d mpdd ssrdv eqssd simplr eqeltrd impbid2 reu6
      ) BGHZBIHZCAJZKZLZBUWHMZNZUWHEJZLZOZAIPZEIQZUWLAIRUWFUWGUWQUWGUWLUWHBLZOZ
      AIPZUWQUWGUWSAIUWGUWHIHZNUWLUWRUXAUWGUWHGHZUWLUWRSUWHTUWGUXBNZUWLUWRUXCUW
      LNBUWHBUWHCUWIDUWIUAUBUCUDUEUWRUWJUWKUWRUWIBKZCUWHBUFDUGBUWHUHUIUJUKUWPUW
      TEBIUWMBLZUWOUWSAIUXEUWNUWRUWLUWMBUWHULUMUNUOUPUWFUWPUWGEIUWFUWMIHZNZUWPU
      WGUXGUWPNZBUWMIUXHBUWMUXHCUWMKZLZBUWMMZUXFUWPUXJUXKNZUWFUWOUXLAUWMIUWNUWL
      UWOUXLUWNUWLUWOUWNUWLUQURUWNUWJUXJUWKUXKUWNUWIUXICUWHUWMUFUSUWHUWMBUTVAVB
      VCVDZVEUXHFUWMBUXHFJZUWMHZUXNCMZUXNBHZUXOUXPUXHUXNUXIMZUXNUWMVFUXHCUXIUXN
      UXHUXJUXKUXMVGZVHZVIUXHUXPUXOUXQUXHUXPUXOUXQSUXHUXPNUXQUXOUXHUXPUXQVJZUXO
      VJZUXHUXPUYANZNZUYBUXIUXNVKZUWMHZUYDCUXNVKZUYEUWMUYDCUXIUXNUXHUXJUYCUXSVL
      VMUYDBUYGVNZVOZVPVQZVRVQZUWMUYGUXGUYCUWPUYKUWMMZUXGUYCNZUWPUYLUYMUWPUWJUY
      KUWHMZNZAIQZUYLUYMUYPUYKKZUWILZUYNNZAIQZUYMUYKGHZUYTUYMUYJVSHZVUAUYMVUBUY
      IVTWAZVTUYJHVJZUYMUYIWBHZVUBVUCVUDNOUYMUWFUYHWBHVUEUWFUXFUYCWCZUYGWDBUYHG
      WBWEWFZUYIWBWGWHUYMUWFBVTWAZVUCVUFBWIBUYIMZVUHVUCBUYHWJZBUYIWKWLWMUYMVUDU
      WHUWMWNZVTWAZEUYHPZABPZUYMVUMABUYMUWHBHZNVUMUWHCWNZUXNMZVJZUYMVUOVURUXGUX
      PUYAVUOVURSUXGUXPNVUOUYAVURUXGUXPVUOUYAVURSUXGUXPVUONZNZVUQUXQVUTVUQUWHUX
      NMZUXQVUTVUPUWHUXNVUOVUPUWHLZUXGUXPVUOUWHCMVVBVUOUWHUXDCUWHBVFDWOUWHCWPWQ
      WRWSUXGVUSVVAUXQSZUWFVUSVVCSUXFUWFUXPVUOVVCUWFVUOUXPVVCUWFVUOUXPVVAUXQUWH
      UXNBCDWTXAXBXCVLXDXEXFXGXBXHXDUYMVUMVUROZVUOUYMUYGWBHZVVDUYMCBHZVVEUWFVVF
      UXFUYCBCDXIXJZCUXNBXKWHZVVEVUMUWHUYGWNZVTWAZVURVVEVUMVULEUYGXLVVJVULEUYGW
      BXMVULVVJEUYGWBUWMUYGLVUKVVIVTUWMUYGUWHXNXOXPXQVUQVVIVTUWHCUXNXRXSXTWHVLY
      AUKUYMBVSHZUYHVSHZVUDVUNOUWFVVKUXFUYCBYBXJUYMUYHGHZVVLUYMVVEUYGVTWAZVVMVV
      HUYMVVFVVNVVGUYMVVFUYGVTUYGVTLCUXNMZUYMVVFVJZCUXNYCUYCVVOVVPSUXGUXPVVOUXN
      CLZUYAVVPUXPVVOVVQVVQUXPVVONUXNCYDYEUDVVQUYAVVPVVQUXQVVFUXNCBYFYGYHYIYJYK
      YLYMUYGWBYNYOUYHYBWHAEBUYHYPYOYAYQZUYJYRWHAUYKUYQUYQUAYSWHUYMUYOUYSAIUYMU
      WJUYRUYNUYMCUYQUWIUYMCUYIKZUYJKZUYQUYMCUXDUYHKZVOZVVSUYMVWBCUYGVOZCUYMUXD
      CVWAUYGUYMCUXDCUXDLUYMDYTUCUYMVVEVWAUYGLVVHUYGWBUUAWHUUBUYGCMVWCCLCUXNUUC
      UYGCUUDUUEUUFBUYHUUGUUHUYMVUEVVSVVTLVUGUYIWBUUIWHUYMVUBVVTUYQLVVRUYJVVTVV
      TUAUUJWHUUKUULUUMUUNYAUWPUYPNUWOUYONZAIQUYMUYLUWOUYOAIUUOUYMVWDUYLAIUYMVW
      DUYLSUXAUYMUWOUYOUYLUWOUWLUWNSZUYMUYOUYLSZUWLUWNUUPUYMVWEUYOUWNSVWFUYMUYO
      UWLUWNUYMUYNUWKUWJUYMBUYKMUYNUWKSUYMBUYIUYKVUIUYMVUJYTUYMUYIUYJUYKUYMVUEU
      YIUYJMVUGUYIWBUUQWHUYMVUBUYJUYKMVVRUYJUURWHUUSZUUSBUYKUWHUUTWHUVAUVBUYOUW
      NUYLUYNUWNUYLSUWJUWNUYNUYLUWHUWMUYKUTYHYJUVCUVDUVEXCVLUVFUVEUVGXDUVHUXGUY
      CUYGUYKHUWPUYMUYIUYKUYGVWGUYMVVEUYGUYHHUYGUYIHVVHUYGWBUVIUYGUYHBUVJWMUVKU
      VLUVKUVMUYDUXFUXRUYBUYFOUWFUXFUWPUYCUVNUXHUXPUXRUYAUXHUXPUXRUXTUVOUVPUXNU
      WMUXIUXIUAUVQYOYAXGUVRUDXBUVSUVTUWAUWFUXFUWPUWBUWCUDUVFUWDUWLAEIUWEXT $.
      $( [12-Dec-2013] $) $( [3-Dec-2009] $)
  $}

  ${
    $d f x y z F $.  $d f x y z X $.
    filufint.1 $e |- X = U. F $.
    $( A filter is equal to the intersection of the ultrafilters containing
       it.  (Contributed by Jeff Hankins, 1-Jan-2010.) $)
    filufint $p |- ( F e. Fil -> |^| { f e. UFil | ( X = U. f /\ F C_ f ) } = F
    ) $=
      ( vx vy vz cfil wcel cv cuni wceq wss wa cufil crab cint wi wral visset
      elintrab wn wrex w3a cdif csn cun cfi cfv cfg difss ssequn2 mpbi a1i cvv
      uniexg syl5eqel difexg unisng 3syl eqcomd 3ad2ant1 uneq12d syl5eqr uniun
      syl6eqr snex unexg mpan2 fiuni syl cfbas c0 wne wb fsubbas filusb ne0i
      ssun1 ssn0 mpan cin elsni elssuni syl6sseqr reldisj 3ad2ant2 dfss4
      biimpi sseq2d 3ad2ant3 bitrd fillsb 3expd 3imp sylbid necon3bd 3exp
      com24 3imp1 ineq2 neeq1d syl5ibrcom expimpd sylan2i r19.21aivv filfbas
      difeq2 dif0 syl6eq eqtr3d eqeltrd 3expia ex com23 snfil syl11anc fbunfip
      mpbird mpbir2and eqid fgbas 3eqtrd fgfil filssufil simpl2 simprl eqtrd
      ssfii sstrd fbssfg adantr simprr ufilfil filesn disjdif simpr3 ssun2
      simpr2 snidg sseldd filint syl111anc syl5eqelr 3exp2 imp32 mtod jca31
      reximdvai syl5 mp2d sseq2 syl5ibr con3rr3 adantrd ancld reximdv syl5com
      pm2.61d rexanali syl6ib con4d syl5bi ssrdv cab wal df-rab inteqi sseq2i
      ssintab bitr2i mpgbi eqssd ) BHIZCAJZKZLZBUVOMZNZAOPZQZBUVNEUWABEJZUWAIUV
      SUWBUVOIZRAOSZUVNUWBBIZUVSAUWBOETUAUVNUWEUWDUVNUWEUBZUVSUWCUBZNZAOUCZUWDU
      BUVNUWFUWIUVNUWFNUWBCMZUWIUVNUWFUWJUWIUVNUWFUWJUDZCBCUWBUEZUFZUGZUHUIZUJU
      IZKZLZUWPHIZUWIUWKCUWNKZUWOKZUWQUWKCBKZUWMKZUGZUWTUWKCCUWLUGZUXDUWLCMUXEC
      LCUWBUKUWLCULUMUWKCUXBUWLUXCCUXBLUWKDUNUVNUWFUWLUXCLUWJUVNUXCUWLUVNCUOIZU
      WLUOIZUXCUWLLUVNCUXBUODBHUPUQZCUWBUOURZUWLUOUSUTVAVBVCVDBUWMVEVFUVNUWFUWT
      UXALZUWJUVNUWNUOIZUXJUVNUWMUOIUXKUWLVGBUWMHUOVHVIZUWNUOVJVKVBUWKUWOVLIZUX
      AUWQLUWKUXMUWNVMVNZVMUWOIUBZUVNUWFUXMUXNUXONVOZUWJUVNUXKUXPUXLUWNUOVPVKVB
      UVNUWFUXNUWJUVNCBIZBVMVNZUXNBCDVQZBCVRBUWNMZUXRUXNBUWMVSZBUWNVTWAUTVBUWKU
      XOFJZGJZWBZVMVNZGUWMSFBSZUWKUYEFGBUWMUYCUWMIUWKUYBBIZUYCUWLLZUYEUYCUWLWCU
      WKUYGUYHUYEUWKUYGNUYEUYHUYBUWLWBZVMVNZUVNUWFUWJUYGUYJUVNUYGUWJUWFUYJUVNUY
      GUWJUWFUYJRUVNUYGUWJUDZUWEUYIVMUYKUYIVMLZUYBUWBMZUWEUYKUYLUYBCUWLUEZMZUYM
      UYGUVNUYLUYOVOZUWJUYGUYBCMUYPUYGUYBUXBCUYBBWDDWEUYBUWLCWFVKWGUWJUVNUYOUYM
      VOUYGUWJUYNUWBUYBUWJUYNUWBLZUWBCWHWIZWJWKWLUVNUYGUWJUYMUWERUVNUYGUWJUYMUW
      EUYBUWBBCDWMWNWOWPWQWRWSWTUYHUYDUYIVMUYCUWLUYBXAXBXCXDXEXFUWKBVLIZUWMVLIZ
      UXOUYFVOUVNUWFUYSUWJBXGVBUWKUWMHIZUYTUWKUXGUWLVMVNZVUAUVNUWFUXGUWJUVNUXFU
      XGUXHUXIVKVBUVNUWFUWJVUBUVNUWJUWFVUBUVNUWJUWFVUBRUVNUWJNUWEUWLVMUVNUWJUWL
      VMLZUWEUVNUWJVUCUDZUWBCBVUDUYNUWBCUWJUVNUYQVUCUYRWGVUCUVNUYNCLUWJVUCUYNCV
      MUECUWLVMCXHCXIXJWKXKUVNUWJUXQVUCUXSVBXLXMWQXNXOWOUWLUOXPXQUWMXGVKFGBUWMX
      RXQXSXTZUWOUXAUXAYAYBVKYCUWKUXMUWSVUEUWOYDVKUWKUWRUWSUWIRUWSUWQUVPLZUWPUV
      OMZNZAOUCUWKUWRNZUWIAUWPUWQUWQYAYEVUIVUHUWHAOUWKUWRUVOOIZVUHUWHRUWKUWRVUJ
      UDZVUHUWHVUKVUHNZUVQUVRUWGVULCUWQUVPUWKUWRVUJVUHYFVUKVUFVUGYGYHVULBUWPUVO
      VUKBUWPMZVUHUWKUWRVUMVUJUWKBUWOUWPUWKBUWNUWOUXTUWKUYAUNUVNUWFUWNUWOMZUWJU
      VNUXKVUNUXLUWNUOYIVKZVBYJUWKUXMUWOUWPMZVUEUWOYKZVKYJVBYLVUKVUFVUGYMYJVULU
      WCVMUVOIZVUKVURUBZVUHVUJUWKVUSUWRVUJUVOHIZVUSUVOYNZUVOYOVKWKYLVUKVUFVUGUW
      CVURRVUKVUFVUGUWCVURVUKVUFVUGUWCUDZNZVMUWBUWLWBZUVOUWBCYPVVCVUTUWCUWLUVOI
      VVDUVOIVUKVUTVVBVUJUWKVUTUWRVVAWKYLVUKVUFVUGUWCYQVVCUWMUVOUWLVVCUWMUWPUVO
      VUKUWMUWPMVVBVUKUWMUWOUWPUWKUWRUWMUWOMZVUJUVNUWFVVEUWJUVNUWMUWNUWOUWMUWNM
      UVNUWMBYRUNVUOYJVBVBVUKUXMVUPUWKUWRUXMVUJVUEVBVUQVKYJYLVUKVUFVUGUWCYSYJVU
      KUWLUWMIZVVBUWKUWRVVFVUJUVNUWFVVFUWJUVNUXFUXGVVFUXHUXIUWLUOYTUTVBVBYLUUAU
      WBUWLUVOUUBUUCUUDUUEUUFUUGUUHXNXMUUIUUJXNUUKXMUVNUWJUBZUWIRUWFUVNUVSAOUCV
      VGUWIABCDYEVVGUVSUWHAOVVGUVSUWGVVGUVQUWGUVRUVQUWCUWJUWCUWJUVQUWBUVPMUWBUV
      OWDCUVPUWBUULUUMUUNUUOUUPUUQUURYLUUSXNUVSUWCAOUUTUVAUVBUVCUVDBUWAMZUVNVUJ
      UVSNZUVRRZVVHAVVHBVVIAUVEZQZMVVJAUVFUWAVVLBUVTVVKUVSAOUVGUVHUVIVVIABUVJUV
      KVUJUVQUVRYMUVLUNUVM $.
      $( [14-Dec-2013] $) $( [1-Jan-2010] $)
  $}

  ${
    $d x y A $.  $d x y X $.  $d x y V $.
    $( Lemma for ~ fixufil and ~ uffixfr .  (Contributed by Mario Carneiro,
       12-Dec-2013.) $)
    uffix $p |- ( ( X e. V /\ A e. X ) -> ( { { A } , X } e. fBas /\
                  { x e. ~P X | A e. x } = ( filGen ` { { A } , X } ) ) ) $=
      ( vy wcel wa csn cpr cfbas cv cpw crab cfg cfv wceq cun df-pr wss cfil
      cvv c0 wne snex snnzg snfil sylancr filfbas syl snssi cuni unisn eqcomi
      extbas1 syl11anc adantl syl5eqel wrex visset elpw simpl uniprg ssequn1
      sylib eqtr2d sseq2d syl5bb prid1 sseq1 rcla4ev ex mpsyl cint wi intss1
      sstr2 snidg cin intprg df-ss eleqtrd ssel syl5com sylan9r r19.23adva
      impbid2 anbi12d wb eleq2 elrab a1i eqid elfg 3bitr4d eqrdv jca ) DCFZBDFZ
      GZBHZDIZJFZBAKZFZADLZMZXANOZPWSXAWTHZDHQZJWTDRWRXIJFZWQWRXHJFZWTDSZXJWRXH
      TFZXKWRWTUAFZWTUBUCXMBUDZBDUEWTUAUFUGXHUHUIBDUJZDXHWTXHUKWTWTXOULUMUNUOUP
      UQZWSEXFXGWSEKZXEFZBXRFZGZXRXAUKZSZXCXRSZAXAURZGZXRXFFZXRXGFZWSXSYCXTYEXS
      XRDSWSYCXRDEUSUTWSDYBXRWSYBWTDQZDWSXNWQYBYIPXOWQWRVAZWTDUACVBUGWSXLYIDPWR
      XLWQXPUPZWTDVCVDVEVFVGWSXTYEWTXAFZXTWTXRSZYEWTDXOVHBXRUJYLYMYEYDYMAWTXAXC
      WTXRVIVJVKVLWSYDXTAXAXCXAFZYDXAVMZXRSZWSXTYNYOXCSYDYPVNXCXAVOYOXCXRVPUIWS
      BYOFYPXTWSBWTYOWRBWTFWQBDVQUPWSYOWTDVRZWTWSXNWQYOYQPXOYJWTDUACVSUGWSXLYQW
      TPYKWTDVTVDVEWAYOXRBWBWCWDWEWFWGYGYAWHWSXDXTAXRXEXCXRBWIWJWKWSXBYHYFWHXQA
      XRXAYBYBWLWMUIWNWOWP $.
      $( [12-Dec-2013] $)

    $( The condition describing a fixed ultrafilter always produces an
       ultrafilter.  (Contributed by Jeff Hankins, 9-Dec-2009.)  (Revised by
       Mario Carneiro, 12-Dec-2013.) $)
    fixufil $p |- ( ( X e. V /\ A e. X ) -> { x e. ~P X | A e. x } e. UFil ) $=
      ( vy wcel wa cv cpw crab cfil cuni cdif wo wral cufil csn cpr cfg cfv
      cfbas wceq uffix simprd simpld fgfil syl eqeltrd cun undif2 wss elpwi
      ssequn1 sylib syl5req eleq2d biimpac elun adantll wb ibar adantl difss
      elpw2g mpbiri ad2antrr biantrurd orbi12d mpbid r19.21aiva ssrab2 uniss
      ax-mp unipw sseqtri a1i ssid elpwg anim1i eleq2 elrab sylibr elssuni
      eqssd pweq difeq1d eleq1d syl6bb raleqbidv mpbird eqid isufil sylanbrc )
      DCFZBDFZGZBAHZFZADIZJZKFEHZWTFZWTLZXAMZWTFZNZEXCIZOZWTPFWPWTBQDRZSTZKWPXI
      UAFZWTXJUBZABCDUCZUDWPXKXJKFWPXKXLXMUEXIUFUGUHWPXHXAWSFZBXAFZGZDXAMZWSFZB
      XQFZGZNZEWSOWPYAEWSWPXNGZXOXSNZYAWOXNYCWNWOXNGBXAXQUIZFZYCXNWOYEXNDYDBXNY
      DXADUIZDXADUJXNXADUKYFDUBXADULXADUMUNUOUPUQBXAXQURUNUSYBXOXPXSXTXNXOXPUTW
      PXNXOVAVBYBXRXSWNXRWOXNWNXRXQDUKDXAVCXQDCVDVEVFVGVHVIVJWPXFYAEXGWSWPXCDUB
      XGWSUBWPXCDXCDUKWPXCWSLZDWTWSUKXCYGUKWRAWSVKWTWSVLVMDVNVOVPWPDWTFZDXCUKWP
      DWSFZWOGYHWNYIWOWNYIDDUKDVQDDCVRVEVSWRWOADWSWQDBVTWAWBDWTWCUGWDZXCDWEUGWP
      XBXPXEXTXBXPUTWPWRXOAXAWSWQXABVTWAVPWPXEXQWTFXTWPXDXQWTWPXCDXAYJWFWGWRXSA
      XQWSWQXQBVTWAWHVHWIWJEWTXCXCWKWLWM $.
      $( [12-Dec-2013] $) $( [9-Dec-2009] $)
  $}

  ${
    $d x A $.  $d x F $.  $d x X $.
    uffixfr.1 $e |- X = U. F $.
    $( An ultrafilter is either fixed or free.  A fixed ultrafilter is called
       principal (generated by a single element ` A ` ), and a free ultrafilter
       is called nonprincipal (having empty intersection).  Note that free
       ultrafilters cannot be constructed.  (Contributed by Jeff Hankins,
       4-Dec-2009.)  (Revised by Mario Carneiro, 12-Dec-2013.) $)
    uffixfr $p |- ( ( F e. UFil /\ A e. |^| F ) ->
                    F = { x e. ~P X | A e. x } ) $=
      ( cufil wcel cint wa cv cpw crab cfil cuni wceq wss simpl csn cpr cfg
      cfv cfbas ufilfil filusb syl adantr c0 wne emnfil intssuni 3syl
      syl6sseqr sseld imp uffix syl11anc simprd simpld fgfil eqeltrd ssid
      elpwg mpbiri eleq2 elrab sylanbrc elssuni ssrab2 uniss ax-mp unipw
      sseqtri a1i eqssd wral pwuni pweq sseqtr4i elintg ibi adantl ssrab eqid
      ufilmax syl22anc ) CFGZBCHZGZIZWFBAJZGZADKZLZMGDWMNZOCWMPZCWMOWFWHQWIWMBR
      DSZTUAZMWIWPUBGZWMWQOZWIDCGZBDGZWRWSIWFWTWHWFCMGZWTCUCZCDEUDUEUFZWFWHXAWF
      WGDBWFWGCNZDWFXBCUGUHWGXEPXCCUICUJUKEULUMUNZABCDUOUPZUQWIWRWQMGWIWRWSXGUR
      WPUSUEUTWIDWNWIDWMGZDWNPWIDWLGZXAXHWIWTXIXDWTXIDDPDVADDCVBVCUEXFWKXAADWLW
      JDBVDVEVFDWMVGUEWNDPWIWNWLNZDWMWLPWNXJPWKAWLVHWMWLVIVJDVKVLVMVNWICWLPZWKA
      CVOZWOXKWICXEKZWLCVPDXEOWLXMOEDXEVQVJVRVMWHXLWFWHXLABCWGVSVTWAWKAWLCWBVFC
      WMDWNEWNWCWDWE $.
      $( [12-Dec-2013] $) $( [4-Dec-2009] $)
  $}

  ${
    $d x y F $.  $d x y X $.
    uffinfix.1 $e |- X = U. F $.
    $( An ultrafilter containing a finite element is fixed.  (Contributed by
       Jeff Hankins, 5-Dec-2009.) $)
    uffinfix $p |- ( ( F e. UFil /\ S e. F /\ S e. Fin ) ->
                     E. x e. X F = { y e. ~P X | x e. y } ) $=
      ( cufil wcel cfn w3a cv cint wex cpw crab wceq wrex cfbas cfil ufilfil
      filfbas syl c0 wne fbfinnfr n0 sylib syl3an1 wi wa cuni wss fbasne0
      intssuni 3syl syl6sseqr sseld uffixfr ex jcad eximdv df-rex syl6ibr
      3ad2ant1 mpd ) DGHZCDHZCIHZJAKZDLZHZAMZDVIBKHBENOPZAEQZVFDRHZVGVHVLVFDSHV
      ODTDUAUBZVOVGVHJVJUCUDVLCDUEAVJUFUGUHVFVGVLVNUIVHVFVLVIEHZVMUJZAMVNVFVKVR
      AVFVKVQVMVFVJEVIVFVJDUKZEVFVODUCUDVJVSULVPDUMDUNUOFUPUQVFVKVMBVIDEFURUSUT
      VAVMAEVBVCVDVE $.
      $( [13-Dec-2013] $) $( [5-Dec-2009] $)
  $}

  ${
    $d f u v x y z A $.  $d f u v x y z B $.  $d f u v x y z X $.
    $( An infinite subset is contained in a free ultrafilter.  (Contributed by
       Jeff Hankins, 6-Dec-2009.) $)
    ufinffr $p |- ( ( X e. B /\ A C_ X /\ om ~<_ A ) -> E. f e. UFil ( X = U. f
    /\ A e. f /\ |^| f = (/) ) ) $=
      ( vx vy wcel wss com cdom wbr w3a cv cdif cfn cpw crab cuni wceq wa
      cufil wrex cint c0 cfil wn csdm domnsym isfinite1b nsyl rcfpfil syl3an3
      simpld eqid filssufil syl simprd eqeq1d biimprd elpw2g biimpar 3adant3
      0fin a1i difeq2 difid syl6eq eleq1d elrab sylanbrc ssel syl5com intss
      wal csn visset snid eldifn mt2 elinti mtoi difss simp2 sstrd wb 3ad2ant1
      mpbird snfi wi eldif notbii iman bitr4i anbi2i bitri pm3.35 sylbi ssriv
      ssfi mp2an nsyl3 19.21aiv eq0 sylibr sseq2d syl5ib ss0 syl6 jcad anim12d
      3anass syl6ibr reximdv mpd ) DBGZADHZIAJKZLZAEMZNZOGZEDPZQZRZCMZRZSZYCYEH
      ZTZCUAUBZDYFSZAYEGZYEUCZUDSZLZCUAUBXRYCUEGZYJXRYPDYDSZXQXOXPAOGZUFYPYQTXQ
      AIUGKYRIAUHAUIUJEABDUKULZUMCYCYDYDUNUOUPXRYIYOCUAXRYIYKYLYNTZTYOXRYGYKYHY
      TXRYKYGXRDYDYFXRYPYQYSUQURUSXRYHYLYNXRAYCGZYHYLXRAYBGZUDOGZUUAXOXPUUBXQXO
      UUBXPADBUTVAVBUUCXRVCVDYAUUCEAYBXSASZXTUDOUUDXTAANUDXSAAVEAVFVGVHVIVJYCYE
      AVKVLXRYHYMUDHZYNYHYMYCUCZHXRUUEYCYEVMXRUUFUDYMXRFMZUUFGZUFZFVNUUFUDSXRUU
      IFUUHAUUGVOZNZYCGZXRUUHUULUUGUUKGZUUMUUGUUJGUUGFVPVQUUGAUUJVRVSUUGYCUUKVT
      WAXRUUKYBGZAUUKNZOGZUULXRUUNUUKDHZXRUUKADUUKAHXRAUUJWBVDXOXPXQWCWDXOXPUUN
      UUQWEXQUUKDBUTWFWGUUPXRUUJOGUUOUUJHUUPUUGWHEUUOUUJXSUUOGZXSAGZUUSXSUUJGZW
      IZTZUUTUURUUSXSUUKGZUFZTUVBXSAUUKWJUVDUVAUUSUVDUUSUUTUFTZUFUVAUVCUVEXSAUU
      JWJWKUUSUUTWLWMWNWOUUSUUTWPWQWRUUJUUOWSWTVDYAUUPEUUKYBXSUUKSXTUUOOXSUUKAV
      EVHVIVJXAXBFUUFXCXDXEXFYMXGXHXIXJYKYLYNXKXLXMXN $.
      $( [4-Dec-2013] $) $( [6-Dec-2009] $)
  $}

  ${
    $d f u v x y z A $.  $d f u v x y z F $.  $d f u v x y z X $.
    $( Any infinite set has an ultrafilter on it whose elements are of the same
       cardinality as the set.  Any such ultrafilter is necessarily free.
       (Contributed by Jeff Hankins, 7-Dec-2009.)  (The proof was shortened by
       Mario Carneiro, 14-Dec-2013.) $)
    ufilen $p |- ( ( X e. A /\ om ~<_ X ) -> E. f e. UFil ( X = U. f /\ A. x e.
    f x ~~ X ) ) $=
      ( vy wcel com cdom wbr wa cv cdif csdm cpw crab cuni wceq wss cufil wrex
      cen wral cfil csdfil simpld eqid filssufil syl wb simprd eqeq1d anbi1d
      adantr wi wn simplll elssuni ad2antll simprl sseqtr4d cfbas ufilfil
      filfbas adantl simpr fbncp syl2an w3a difss elpw2g mpbiri 3ad2ant1 simp2
      dfss4 sylib simp3 eqbrtrd difeq2 breq1d elrab sylanbrc ssel syl5com
      3expa impancom con3d syl21anc cvv visset ssdomg ax-mp bren2 biimpri ex
      3syl syld expr com23 r19.21adv imdistanda sylbird reximdva mpd ) DBFZGDHI
      ZJZDEKZLZDMIZEDNZOZPZCKZPZQZXKXMRZJZCSTZDXNQZAKZDUAIZAXMUBZJZCSTXFXKUCFZX
      RXFYDDXLQZEBDUDZUECXKXLXLUFUGUHXFXQYCCSXFXMSFZJZXQXSXPJZYCXFYIXQUIYGXFXSX
      OXPXFDXLXNXFYDYEYFUJUKULUMYHXSXPYBYHXSJZXPYAAXMYJXTXMFZXPYAYHXSYKXPYAUNYH
      XSYKJZJZXPXTDMIZUOZYAYMXDXTDRZDXTLZXMFZUOZXPYOUNXDXEYGYLUPYMXTXNDYKXTXNRY
      HXSXTXMUQURYHXSYKUSUTZYHXMVAFZYKYSYLYGUUAXFYGXMUCFUUAXMVBXMVCUHVDXSYKVEXT
      DXMVFVGXDYPJZXPYSYOUUBXPJYNYRUUBYNXPYRXDYPYNXPYRUNXDYPYNVHZYQXKFZXPYRUUCY
      QXJFZDYQLZDMIZUUDXDYPUUEYNXDUUEYQDRDXTVIYQDBVJVKVLUUCUUFXTDMUUCYPUUFXTQXD
      YPYNVMXTDVNVOXDYPYNVPVQXIUUGEYQXJXGYQQXHUUFDMXGYQDVRVSVTWAXKXMYQWBWCWDWEW
      FWEWGYMYPXTDHIZYOYAUNYTXTWHFYPUUHUNAWIXTDWHWJWKUUHYOYAYAUUHYOJXTDWLWMWNWO
      WPWQWRWSWTXAXBXC $.
      $( [14-Dec-2013] $) $( [7-Dec-2009] $)
  $}

  ${
    $d x F $.  $d x J $.  $d x X $.
    ufildr.1 $e |- J = ( F u. { (/) } ) $.
    ufildr.2 $e |- X = U. F $.
    $( An ultrafilter gives rise to a connected door topology.  (Contributed by
       Jeff Hankins, 6-Dec-2009.)  (Revised by Mario Carneiro, 14-Dec-2013.) $)
    ufildr $p |- ( F e. UFil -> ( J u. ( Clsd ` J ) ) = ~P X ) $=
      ( vx cufil wcel ccld cfv cun cpw cv wo wss wi cuni elssuni c0 csn uniun
      0ex unisn uneq2i un0 3eqtri unieqi 3eqtr4ri syl6sseqr a1i ctop cfil ccon
      ufilfil filcon contop 3syl syl5eqel cldss ex syl jaod wa cdif ufilss
      ssun1 sseqtr4i sseld wb iscld2 sylan sylibrd orim12d mpd impbid elun
      visset elpw 3bitr4g eqrdv ) AGHZFBBIJZKZCLZWAFMZBHZWEWBHZNZWECOZWEWCHWEWD
      HWAWHWIWAWFWIWGWFWIPWAWFWEBQZCWEBRASTZKZQZAQZWJCWMWNWKQZKWNSKWNAWKUAWOSWN
      SUBUCUDWNUEUFBWLDUGEUHZUIUJWABUKHZWGWIPWABWLUKDWAAULHWLUMHWLUKHAUNAUOWLUP
      UQURZWQWGWIWEBCWPUSUTVAVBWAWIWHWAWIVCZWEAHZCWEVDZAHZNWHWEACEVEWSWTWFXBWGW
      SABWEABOWSAWLBAWKVFDVGUJZVHWSXBXABHZWGWSABXAXCVHWAWQWIWGXDVIWRWEBCWPVJVKV
      LVMVNUTVOWEBWBVPWECFVQVRVSVT $.
      $( [14-Dec-2013] $) $( [6-Dec-2009] $)
  $}

