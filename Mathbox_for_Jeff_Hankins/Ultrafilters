$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Ultrafilters
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c UFil $.
  $( Extend class notation with the class of all ultrafilters. $)
  cufil $a class UFil $.

  ${
    $d f x $.
    $( Define the class of all ultrafilters. $)
    df-ufil $a |- UFil = { f e. Fil | A. x e. ~P U. f ( x e. f \/ ( U. f \ x )
    e. f ) } $.
  $}

  ${
    $d f t w x y z F $.  $d f t w x y z X $.
    isufil.1 $e |- X = U. F $.
    $( The property of being an ultrafilter.  (Contributed by Jeff Hankins,
       30-Nov-2009.) $)
    isufil $p |- ( F e. UFil <-> ( F e. Fil /\ A. x e. ~P X ( x e. F \/ ( X \ x
    ) e. F ) ) ) $=
      ( vf cv wcel cuni cdif cpw wral cfil cufil wceq unieq syl6eqr pweqd eleq2
      wo difeq1d id eleq12d orbi12d raleqbidv df-ufil elrab2 ) AFZEFZGZUHHZUGIZ
      UHGZSZAUJJZKUGBGZCUGIZBGZSZACJZKEBLMUHBNZUMURAUNUSUTUJCUTUJBHCUHBODPZQUTU
      IUOULUQUHBUGRUTUKUPUHBUTUJCUGVATUTUAUBUCUDAEUEUF $.
      $( [30-Nov-2009] $)
  $}

  ${
    $d x F $.
    $( An ultrafilter is a filter.  (Contributed by Jeff Hankins,
       1-Dec-2009.) $)
    ufilfil $p |- ( F e. UFil -> F e. Fil ) $=
      ( vx cufil wcel cfil cv cuni cdif wo cpw wral eqid isufil simplbi ) ACDAE
      DBFZADAGZOHADIBPJKBAPPLMN $.
      $( [1-Dec-2009] $)
  $}

  ${
    $d f x y F $.  $d x S $.  $d f x y X $.
    ufilss.1 $e |- X = U. F $.
    $( For any subset of the base set of an ultrafilter, either the set is in
       the ultrafilter or the complement is.  (Contributed by Jeff Hankins,
       1-Dec-2009.) $)
    ufilss $p |- ( ( F e. UFil /\ S C_ X ) -> ( S e. F \/ ( X \ S ) e. F ) ) $=
      ( vx cufil wcel wss cdif wo cpw cvv wb cuni uniexg syl5eqel elpw2g syl cv
      wral cfil isufil simprbi wceq eleq1 difeq2 eleq1d orbi12d rcla4cv sylbird
      wi imp ) BFGZACHZABGZCAIZBGZJZUMUNACKZGZURUMCLGUTUNMUMCBNLDBFOPACLQRUMESZ
      BGZCVAIZBGZJZEUSTZUTURUKUMBUAGVFEBCDUBUCVEUREAUSVAAUDZVBUOVDUQVAABUEVGVCU
      PBVAACUFUGUHUIRUJUL $.
      $( [1-Dec-2009] $)

    $( The complement is in an ultrafilter iff the set is not.  (Contributed by
       Mario Carneiro, 8-Dec-2013.) $)
    ufilb $p |- ( ( F e. UFil /\ S C_ X ) ->
                  ( -. S e. F <-> ( X \ S ) e. F ) ) $=
      ( cufil wcel wss wa wn cdif ufilss wi cfil cfbas ufilfil filfbas fbncp ex
      ord con2d 3syl adantr impbid ) BEFZACGZHZABFZIZCAJBFZUFUGUIABCDKSUDUIUHLZ
      UEUDBMFBNFZUJBOBPUKUGUIUKUGUIIACBQRTUAUBUC $.
      $( [11-Dec-2013] $)

    ${
      $d x G $.  $d x Y $.
      ufilmax.2 $e |- Y = U. G $.
      $( Any filter finer than an ultrafilter is actually equal to it.
         (Contributed by Jeff Hankins, 1-Dec-2009.) $)
      ufilmax $p |- ( ( ( F e. UFil /\ G e. Fil ) /\ ( X = Y /\ F C_ G ) ) ->
                      F = G ) $=
        ( vx cufil wcel cfil wa wceq wss simprr cv wi cuni elssuni wb wn syl2an
        syl6sseqr a1i sseq2 ad2antrl sylibrd cdif simpl simpr ufilb sseld cfbas
        simprlr filfbas fbncp ex syl con2d ad2antlr syld sylbid con4d expr mpdd
        com23 ssrdv eqssd ) AHIZBJIZKZCDLZABMZKZKZABVJVKVLNVNGBAVNGOZBIZVOCMZVO
        AIZVNVPVODMZVQVPVSPVNVPVOBQDVOBRFUBUCVKVQVSSVJVLCDVOUDUEUFVNVQVPVRVJVMV
        QVPVRPVJVMVQKZKZVRVPWAVRTZCVOUGZAIZVPTZVJVHVQWBWDSVTVHVIUHVMVQUIVOACEUJ
        UAWAWDWCBIZWEWAABWCVJVKVLVQUMUKVIWFWEPVHVTVIVPWFVIBULIZVPWFTZPBUNWGVPWH
        VOCBUOUPUQURUSUTVAVBVCVEVDVFVG $.
        $( [8-Dec-2013] $) $( [1-Dec-2009] $)
    $}

    $( The maximal property of an ultrafilter.  (Contributed by Jeff Hankins,
       30-Nov-2009.)  (The proof was shortened by Mario Carneiro,
       8-Dec-2013.) $)
    isufil2 $p |- ( F e. UFil <-> ( F e. Fil /\ A. f e. Fil ( ( X = U. f /\ F
    C_ f ) -> F = f ) ) ) $=
      ( vx vy wcel cfil cv cuni wceq wss wa wi wral wn c0 wne cvv syl cufil jca
      ufilfil eqid ufilmax ex r19.21aiva wo cpw simpl elpwi cin csn cun cfi cfv
      cfg uniun visset unisn eqcomi uneq12i eqtr4i simplr ssequn2 sylib syl5req
      cdif simpll snex unexg sylancl fiuni cfbas simpr ralsn ineq2 neeq1d sbcie
      bitr2i ralbii wb filfbas ad2antrr inss2 filusb adantr ineq1 rcla4va sylan
      wsbc ssn0 sylancr snfil fbunfip syl11anc mpbird ssun2 mp2an jctil fsubbas
      snnz fgbas 3eqtrd ssun1 a1i ssfii fbssfg sstrd fgfil unieq eqeq2d anbi12d
      a1d sseq2 eqeq2 imbi12d rcla4v mpdd snid sselii eleq2 syl5ibrcom impancom
      sseldd syld an32s con3d rexnal nne elssuni syl6sseqr reldisj adantl difss
      wrex fillsb 3expd mpii imp sylbid syl5bi r19.23adva syl5bir sylan2 isufil
      orrd sylanbrc impbii ) BUAGZBHGZCAIZJZKZBUULLZMZBUULKZNZAHOZMZUUJUUKUUSBU
      CUUJUURAHUUJUULHGMUUPUUQBUULCUUMDUUMUDUEUFUGUBUUTUUKEIZBGZCUVAVHZBGZUHZEC
      UIZOUUJUUKUUSUJUUTUVEEUVFUVAUVFGUUTUVACLZUVEUVACUKUUTUVGMZUVBUVDUVHUVBPFI
      ZUVAULZQRZFBOZPZUVDUVHUVLUVBUUKUVGUUSUVLUVBNUUKUVGMZUVLUUSUVBUVNUVLMZUUSB
      BUVAUMZUNZUOUPZUQUPZKZUVBUVOUUSCUVSJZKZBUVSLZMZUVTUVOUWDUUSUVOUWBUWCUVOCU
      VQJZUVRJZUWAUVOUWECUVAUNZCUWEBJZUVPJZUNUWGBUVPURCUWHUVAUWIDUWIUVAUVAEUSZU
      TVAVBVCUVOUVGUWGCKUUKUVGUVLVDUVACVEVFVGUVOUVQSGZUWEUWFKUVOUUKUVPSGUWKUUKU
      VGUVLVIUVAVJBUVPHSVKVLZUVQSVMTUVOUVRVNGZUWFUWAKUVOUWMUVQQRZQUVRGPZMZUVOUW
      OUWNUVOUWOUVIUULULZQRZAUVPOZFBOZUVOUVLUWTUVNUVLVOUVKUWSFBUWSUWRAUVAWKUVKU
      WRAUVAUWJVPUWRUVKAUVAUWJUULUVAKUWQUVJQUULUVAUVIVQVRVSVTWAVFUVOBVNGZUVPVNG
      ZUWOUWTWBUUKUXAUVGUVLBWCWDUVOUVASGZUVAQRZUXBUWJUVOCUVAULZUVALUXEQRZUXDCUV
      AWEUVNCBGZUVLUXFUUKUXGUVGBCDWFWGUVKUXFFCBUVICKUVJUXEQUVICUVAWHVRWIWJUXEUV
      AWLWMUXCUXDMUVPHGUXBUVASWNUVPWCTWMFABUVPWOWPWQUVPUVQLUVPQRUWNUVPBWRZUVAUW
      JXBUVPUVQWLWSWTUVOUWKUWMUWPWBUWLUVQSXATWQZUVRUWFUWFUDXCTXDUVOBUVQUVSBUVQL
      UVOBUVPXEXFUVOUVQUVRUVSUVOUWKUVQUVRLUWLUVQSXGTUVOUWMUVRUVSLUXIUVRXHTXIZXI
      UBXNUVOUVSHGZUUSUWDUVTNZNUVOUWMUXKUXIUVRXJTUURUXLAUVSHUULUVSKZUUPUWDUUQUV
      TUXMUUNUWBUUOUWCUXMUUMUWACUULUVSXKXLUULUVSBXOXMUULUVSBXPXQXRTXSUVOUVBUVTU
      VAUVSGUVOUVQUVSUVAUXJUVAUVQGUVOUVPUVQUVAUXHUVAUWJXTYAXFYEBUVSUVAYBYCYFYDY
      GYHUUKUVMUVDNUUSUVGUVMUVKPZFBYPUUKUVDUVKFBYIUUKUXNUVDFBUXNUVJQKZUUKUVIBGZ
      MZUVDUVJQYJUXQUXOUVIUVCLZUVDUXPUXOUXRWBZUUKUXPUVICLUXSUXPUVIUWHCUVIBYKDYL
      UVIUVACYMTYNUUKUXPUXRUVDNZUUKUXPUVCCLZUXTCUVAYOUUKUXPUYAUXRUVDUVIUVCBCDYQ
      YRYSYTUUAUUBUUCUUDWDYFUUGUUEUGEBCDUUFUUHUUI $.
      $( [8-Dec-2013] $) $( [30-Nov-2009] $)

    $( An ultrafilter is a prime filter.  (Contributed by Jeff Hankins,
       1-Jan-2010.) $)
    ufprim $p |- ( ( F e. UFil /\ A C_ X /\ B C_ X ) -> ( ( A e. F \/ B e. F )
    <-> ( A u. B ) e. F ) ) $=
      ( wcel wss w3a cun wa 3ad2ant1 adantr simpr a1i 3jca fillsb sylc cdif cin
      ex cufil wo cfil ufilfil unss biimpi 3adant1 ssun1 ssun2 jaod wn wb ufilb
      3adant3 difun2 uncom difeq1i eqtr3i ineq2i indifcom 3eqtr4i syl3an1 simp3
      filint syl5eqel inss1 3expia sylbid orrd impbid ) CUAFZADGZBDGZHZACFZBCFZ
      UBZABIZCFZVNVOVSVPVNVOVSVNVOJZCUCFZVOVRDGZAVRGZHVSVNWAVOVKVLWAVMCUDKZLVTV
      OWBWCVNVOMVNWBVOVLVMWBVKVLVMJWBABDUEUFUGZLWCVTABUHNOAVRCDEPQTVNVPVSVNVPJZ
      WAVPWBBVRGZHVSVNWAVPWDLWFVPWBWGVNVPMVNWBVPWELWGWFBAUINOBVRCDEPQTUJVNVSVQV
      NVSJZVOVPWHVOUKZDARZCFZVPVNWIWKULZVSVKVLWLVMACDEUMUNLVNVSWKVPVNVSWKHZWABW
      JSZCFZVMWNBGZHVPVNVSWAWKWDKWMWOVMWPWMWNVRWJSZCDBARZSDVRARZSWNWQWRWSDBAIZA
      RWRWSBAUOWTVRABAUPUQURUSBDAUTVRDAUTVAVNWAVSWKWQCFWDVRWJCVDVBVEVNVSVMWKVKV
      LVMVCKWPWMBWJVFNOWNBCDEPQVGVHVITVJ $.
      $( [8-Dec-2013] $) $( [1-Jan-2010] $)
  $}

  ${
    $d f g h x y F $.  $d f g h x y X $.
    filssufil.1 $e |- X = U. F $.
    $( A filter is contained in some ultrafilter.  (Requires the Axiom of
       Choice, via ~ zornn0 .)  (Contributed by Jeff Hankins, 2-Dec-2009.)
       (The proof was shortened by Mario Carneiro, 11-Dec-2013.) $)
    filssufil $p |- ( F e. Fil -> E. f e. UFil ( X = U. f /\ F C_ f ) ) $=
      ( vh vg vx cfil wcel cv cuni wceq wss wa wi wral wrex cvv sseq2 syl vy wn
      wpss cufil crab c0 wne wo w3a wal cpw rabss eqimss2 adantr visset sspwuni
      elpw bitri sylibr a1i mprgbir uniexg syl5eqel pwexg 3syl ssexg sylancr id
      ssid pm3.2i jctir unieq eqeq2d anbi12d elrab ne0i cun simpr1 ssrab simpld
      sylib simpr2 simpl eqcomd ralimi adantl simpr3 eleq1 biimprcd syl5bi jaod
      ssequn1 ssequn2 ralimdva ralimia eleq1d ralbidv cbvralv jca r19.26 filuni
      uneq1 syl111anc wex eluni rexbii eliun eluni2 anbi2i r19.42v bitr4i exbii
      ciun rexcom4 3bitr4i eqriv iuneq2 syl5eqr iunconst eqtr2d n0 ssel2 simprr
      ssuni sylancom ex 19.23adv sylc jca32 19.21aiv neeq1 3anbi1d eleq2 syl6bb
      imbi12d albidv raleq ralrab rexeqbi1dv rexrab zornn0 vtoclg eqeq1 biimprd
      anbi1d sstr2 anim2d sylan9 imim1d df-pss biimpri necon1bd a2i ralimdv imp
      syl6 anim2i eqid isufil2 simprl reximi2 ) BHIZCAJZKZLZBUVCMZNZCEJZKZLZBUV
      HMZNZUVCUVHUCZUBZOZEHPZNZAHQZUVGAUDQUVBCFJZKZLZBUVSMZNZFHUEZRIZUWDUFUGZGJ
      ZUWDMZUWGUFUGZUVCUVHMZUVHUVCMZUHZEUWGPZAUWGPZUIZUWGKZHIZCUWPKZLZBUWPMZNZN
      ZOZGUJZNZUVRUVBUWDCUKZUKZMZUXGRIZUWEUXHUWCUVSUXGIZOZFHUWCFHUXGULUXKUVSHIU
      WCUVTCMZUXJUWAUXLUWBUVTCUMUNUXJUVSUXFMUXLUVSUXFFUOUQUVSCUPURUSUTVAUVBCRIU
      XFRIUXIUVBCBKZRDBHVBVCCRVDUXFRVDVEUWDUXGRVFVGUVBUWFUXDUVBBUWDIZUWFUVBUVBC
      UXMLZBBMZNZNUXNUVBUVBUXQUVBVHUXOUXPDBVIVJVKUWCUXQFBHUVSBLZUWAUXOUWBUXPUXR
      UVTUXMCUVSBVLVMUVSBBSVNVOUSUWDBVPTUVBUXCGUVBUWOUXBUVBUWONZUWQUWSUWTUXSUWQ
      UWSUXSUWGHMZUWIUVTCLZUVSUVHVQZUWGIZEUWGPZNFUWGPZUWQUWSNUXSUXTUWCFUWGPZUXS
      UWHUXTUYFNZUVBUWHUWIUWNVRZUWCFHUWGVSWAZVTUVBUWHUWIUWNWBZUXSUYAFUWGPZUYDFU
      WGPZNUYEUXSUYKUYLUXSUYGUYKUYIUYFUYKUXTUWCUYAFUWGUWCCUVTUWAUWBWCWDWEWFTZUX
      SUWNUYLUVBUWHUWIUWNWGUWNUVCUVHVQZUWGIZEUWGPZAUWGPUYLUWMUYPAUWGUVCUWGIZUWL
      UYOEUWGUYQUVHUWGIZNUWJUYOUWKUYRUWJUYOOUYQUWJUYNUVHLZUYRUYOUVCUVHWLUYSUYOU
      YRUYNUVHUWGWHWIWJWFUYQUWKUYOOUYRUWKUYNUVCLZUYQUYOUVHUVCWMUYTUYOUYQUYNUVCU
      WGWHWIWJUNWKWNWOUYPUYDAFUWGUVCUVSLZUYOUYCEUWGVUAUYNUYBUWGUVCUVSUVHXBWPWQW
      RWATWSUYAUYDFUWGWTUSFEUWGCXAXCVTUXSUWRFUWGCXMZCUXSUWRFUWGUVTXMZVUBEVUCUWR
      UVHUVTIZFUWGQUVHUVCIZUVCUVSIZNZAXDZFUWGQZUVHVUCIUVHUWRIZVUDVUHFUWGAUVHUVS
      XEXFFUVHUWGUVTXGVUEUVCUWPIZNZAXDVUGFUWGQZAXDVUJVUIVULVUMAVULVUEVUFFUWGQZN
      VUMVUKVUNVUEFUVCUWGXHXIVUEVUFFUWGXJXKXLAUVHUWPXEVUGFAUWGXNXOXOXPUXSUYKVUC
      VUBLUYMFUWGUVTCXQTXRUXSUWIVUBCLUYJFUWGCXSTXTUXSUWHUWIUWTUYHUYJUWIUYREXDUW
      HUWTEUWGYAUWHUYRUWTEUWHUYRUWTUWHUYRUVKUWTUWHUYRNZUVHHIZUVLNZUVKVUOUVHUWDI
      VUQUWGUWDUVHYBUWCUVLFUVHHUVSUVHLZUWAUVJUWBUVKVURUVTUVICUVSUVHVLVMUVSUVHBS
      VNZVOWAVUPUVJUVKYCTBUVHUWGYDYEYFYGWJYHYIYFYJWSUAJZUFUGZUWGVUTMZUWIUWNUIZU
      WPVUTIZOZGUJZNZUVNEVUTPZAVUTQZOUXEUVROUAUWDRVUTUWDLZVVGUXEVVIUVRVVJVVAUWF
      VVFUXDVUTUWDUFYKVVJVVEUXCGVVJVVCUWOVVDUXBVVJVVBUWHUWIUWNVUTUWDUWGSYLVVJVV
      DUWPUWDIUXBVUTUWDUWPYMUWCUXAFUWPHUVSUWPLZUWAUWSUWBUWTVVKUVTUWRCUVSUWPVLVM
      UVSUWPBSVNVOYNYOYPVNVVJVVIUVPAUWDQUVRVVHUVPAVUTUWDVVJVVHUVNEUWDPUVPUVNEVU
      TUWDYQUWCUVLUVNEFHVUSYRYNYSUWCUVGUVPFAHUVSUVCLZUWAUVEUWBUVFVVLUVTUVDCUVSU
      VCVLVMUVSUVCBSVNYTYNYOAEGVUTUAUOUUAUUBYHUVQUVGAHUDUVCHIZUVQNZUVCUDIZUVGVV
      NVVMUVDUVILZUWJNZUVCUVHLZOZEHPZNVVOUVQVVTVVMUVGUVPVVTUVGUVOVVSEHUVGUVOVVQ
      UVNOVVSUVGVVQUVLUVNUVEVVQUVJUWJNZUVFUVLUVEVWAVVQUVEUVJVVPUWJCUVDUVIUUCUUE
      UUDUVFUWJUVKUVJBUVCUVHUUFUUGUUHUUIVVQUVNVVRUWJUVNVVROVVPUWJUVMUVCUVHUWJUV
      CUVHUGZUVMUVMUWJVWBNUVCUVHUUJUUKYFUULWFUUMUUPUUNUUOUUQEUVCUVDUVDUURUUSUSV
      VMUVGUVPUUTWSUVAT $.
      $( [11-Dec-2013] $) $( [2-Dec-2009] $)
  $}

  ${
    $d f g x F $.  $d f g x X $.
    ufileu.1 $e |- X = U. F $.
    $( If the ultrafilter containing a given filter is unique, the filter is an
       ultrafilter.  (Contributed by Jeff Hankins, 3-Dec-2009.)  (The proof was
       shortened by Mario Carneiro, 12-Dec-2013.) $)
    ufileu $p |- ( F e. Fil ->
        ( F e. UFil <-> E! f e. UFil ( X = U. f /\ F C_ f ) ) ) $=
      ( vg cfil wcel cufil cuni wceq wss wa wb wral wi wn c0 wne cvv syl eqcomd
      vx cv wrex wreu ufilfil eqid ufilmax ex sylan2 unieq syl6reqr eqimss2 jca
      impbid1 r19.21aiva eqeq2 bibi2d ralbidv mpdan ibibr pm5.74ri eqeq2d sseq2
      rcla4ev anbi12d bitr3d rcla4va adantll simprd elssuni simpld syl5ibr cdif
      sseq2d adantr difeq1d csn cun cfi cfv cfg cfbas simpll snex unexg sylancl
      fsubbas emnfil ssun1 ssn0 mpan 3syl syl6sseqr df-ss sylib ad2antll sseq1d
      cin fillsb 3expd com23 imp3a imp sylbid con3d expr filusb ad2antrr difexg
      impr ralsng ineq2 neeq1d sbcieg bitrd inssdif0 necon3bbii syl6bbr filfbas
      mpbird ssdif0 biimpri eleq1 notbid biimpcd sylan9 adantl syl5bir necon2ad
      wsbc eqss mpd snfil syl11anc a1i sstrd syl5 r19.23adva sseldd fgfil difss
      fbunfip mpbir2and unisng uneq12d ssequn2 mpbi syl6req uniun syl6eqr fiuni
      filssufil fgbas 3eqtrd eqeq1d anbi1d r19.29 bi1 ssfii fbssfg sstr2 anim2d
      rexbidv imim1d a2i syl6 mpan2d an32s snidg elun2 adantlr eqeltrrd simpllr
      biimpa adantrr ufilb con4d mpdd ssrdv eqssd simplr eqeltrd impbid2 reu6 )
      BFGZBHGZCAUCZIZJZBUWHKZLZUWHEUCZJZMZAHNZEHUDZUWLAHUEUWFUWGUWQUWGUWLUWHBJZ
      MZAHNZUWQUWGUWSAHUWGUWHHGZLUWLUWRUXAUWGUWHFGZUWLUWROUWHUFUWGUXBLZUWLUWRUX
      CUWLLBUWHBUWHCUWIDUWIUGUHUAUIUJUWRUWJUWKUWRUWIBIZCUWHBUKDULBUWHUMUNUOUPUW
      PUWTEBHUWMBJZUWOUWSAHUXEUWNUWRUWLUWMBUWHUQURUSVEUTUWFUWPUWGEHUWFUWMHGZLZU
      WPUWGUXGUWPLZBUWMHUXHBUWMUXHCUWMIZJZBUWMKZUXFUWPUXJUXKLZUWFUWOUXLAUWMHUWN
      UWLUWOUXLUWNUWLUWOUWNUWLVAVBUWNUWJUXJUWKUXKUWNUWIUXICUWHUWMUKVCUWHUWMBVDV
      FVGVHVIZVJUXHUBUWMBUXHUBUCZUWMGZUXNCKZUXNBGZUXOUXPUXHUXNUXIKZUXNUWMVKUXHC
      UXIUXNUXHUXJUXKUXMVLZVOZVMUXHUXPUXOUXQUXHUXPUXOUXQOUXHUXPLUXQUXOUXHUXPUXQ
      PZUXOPZUXHUXPUYALZLZUYBUXIUXNVNZUWMGZUYDCUXNVNZUYEUWMUYDCUXIUXNUXHUXJUYCU
      XSVPVQUYDBUYGVRZVSZVTWAZWBWAZUWMUYGUXGUYCUWPUYKUWMKZUXGUYCLZUWPUYLUYMUWPU
      WJUYKUWHKZLZAHUDZUYLUYMUYPUYKIZUWIJZUYNLZAHUDZUYMUYKFGZUYTUYMUYJWCGZVUAUY
      MVUBUYIQRZQUYJGPZUYMUYISGZVUBVUCVUDLMUYMUWFUYHSGVUEUWFUXFUYCWDZUYGWEBUYHF
      SWFWGZUYISWHTUYMUWFBQRZVUCVUFBWIBUYIKZVUHVUCBUYHWJZBUYIWKWLWMUYMVUDUWHUWM
      WSZQRZEUYHNZABNZUYMVUMABUYMUWHBGZLVUMUWHCWSZUXNKZPZUYMVUOVURUXGUXPUYAVUOV
      UROUXGUXPLVUOUYAVURUXGUXPVUOUYAVUROUXGUXPVUOLZLZVUQUXQVUTVUQUWHUXNKZUXQVU
      TVUPUWHUXNVUOVUPUWHJZUXGUXPVUOUWHCKVVBVUOUWHUXDCUWHBVKDWNUWHCWOWPWQWRUXGV
      USVVAUXQOZUWFVUSVVCOUXFUWFUXPVUOVVCUWFVUOUXPVVCUWFVUOUXPVVAUXQUWHUXNBCDWT
      XAXBXCVPXDXEXFXGXBXKXDUYMVUMVURMZVUOUYMUYGSGZVVDUYMCBGZVVEUWFVVFUXFUYCBCD
      XHXIZCUXNBXJTZVVEVUMUWHUYGWSZQRZVURVVEVUMVULEUYGYKVVJVULEUYGSXLVULVVJEUYG
      SUWMUYGJVUKVVIQUWMUYGUWHXMXNXOXPVUQVVIQUWHCUXNXQXRXSTVPYAUPUYMBWCGZUYHWCG
      ZVUDVUNMUWFVVKUXFUYCBXTXIUYMUYHFGZVVLUYMVVEUYGQRZVVMVVHUYMVVFVVNVVGUYMVVF
      UYGQUYGQJCUXNKZUYMVVFPZCUXNYBUYCVVOVVPOUXGUXPVVOUXNCJZUYAVVPUXPVVOVVQVVQU
      XPVVOLUXNCYLYCUIVVQUYAVVPVVQUXQVVFUXNCBYDYEYFYGYHYIYJYMUYGSYNYOUYHXTTAEBU
      YHUUCYOYAUUDZUYJUUATAUYKUYQUYQUGUUMTUYMUYOUYSAHUYMUWJUYRUYNUYMCUYQUWIUYMC
      UYIIZUYJIZUYQUYMCUXDUYHIZVSZVVSUYMVWBCUYGVSZCUYMUXDCVWAUYGUYMCUXDCUXDJUYM
      DYPUAUYMVVEVWAUYGJVVHUYGSUUETUUFUYGCKVWCCJCUXNUUBUYGCUUGUUHUUIBUYHUUJUUKU
      YMVUEVVSVVTJVUGUYISUULTUYMVUBVVTUYQJVVRUYJVVTVVTUGUUNTUUOUUPUUQUVDYAUWPUY
      PLUWOUYOLZAHUDUYMUYLUWOUYOAHUURUYMVWDUYLAHUYMVWDUYLOUXAUYMUWOUYOUYLUWOUWL
      UWNOZUYMUYOUYLOZUWLUWNUUSUYMVWEUYOUWNOVWFUYMUYOUWLUWNUYMUYNUWKUWJUYMBUYKK
      UYNUWKOUYMBUYIUYKVUIUYMVUJYPUYMUYIUYJUYKUYMVUEUYIUYJKVUGUYISUUTTUYMVUBUYJ
      UYKKVVRUYJUVATYQZYQBUYKUWHUVBTUVCUVEUYOUWNUYLUYNUWNUYLOUWJUWNUYNUYLUWHUWM
      UYKVDYFYHUVFUVGYRXCVPYSYRUVHXDUVIUXGUYCUYGUYKGUWPUYMUYIUYKUYGVWGUYMVVEUYG
      UYHGUYGUYIGVVHUYGSUVJUYGUYHBUVKWMYTUVLYTUVMUYDUXFUXRUYBUYFMUWFUXFUWPUYCUV
      NUXHUXPUXRUYAUXHUXPUXRUXTUVOUVPUXNUWMUXIUXIUGUVQYOYAXGUVRUIXBUVSUVTUWAUWF
      UXFUWPUWBUWCUIYSUWDUWLAEHUWEXS $.
      $( [12-Dec-2013] $) $( [3-Dec-2009] $)
  $}

  ${
    $d f x y z F $.  $d f x y z X $.
    filufint.1 $e |- X = U. F $.
    $( A filter is equal to the intersection of the ultrafilters containing
       it.  (Contributed by Jeff Hankins, 1-Jan-2010.) $)
    filufint $p |- ( F e. Fil -> |^| { f e. UFil | ( X = U. f /\ F C_ f ) } = F
    ) $=
      ( cfil wcel cuni wceq wss wa cufil wi wn w3a cvv 3ad2ant1 c0 sstrd adantr
      syl vx vy vz crab cint wral visset elintrab wrex cdif csn cun cfi cfv cfg
      difss ssequn2 mpbi a1i uniexg syl5eqel difexg unisng 3syl uneq12d syl5eqr
      eqcomd uniun syl6eqr snex unexg mpan2 fiuni cfbas wne fsubbas filusb ne0i
      cv wb ssun1 ssn0 mpan cin elsni elssuni syl6sseqr reldisj 3ad2ant2 biimpi
      dfss4 sseq2d 3ad2ant3 bitrd fillsb 3expd 3imp sylbid necon3bd com24 3imp1
      3exp ineq2 neeq1d syl5ibrcom expimpd sylan2i filfbas difeq2 syl6eq eqtr3d
      r19.21aivv dif0 eqeltrd 3expia ex com23 syl11anc fbunfip mpbird mpbir2and
      snfil eqid fgbas 3eqtrd fgfil filssufil simpl2 simprl eqtrd fbssfg simprr
      ssfii ufilfil filesn disjdif simpr3 ssun2 simpr2 snidg sseldd 3exp2 imp32
      filint syl111anc syl5eqelr mtod jca31 reximdvai syl5 mp2d syl5ibr con3rr3
      sseq2 adantrd ancld reximdv syl5com pm2.61d syl6ib con4d syl5bi ssrdv cab
      rexanali wal df-rab inteqi sseq2i ssintab bitr2i mpgbi eqssd ) BEFZCAVSZG
      ZHZBUVOIZJZAKUDZUEZBUVNUAUWABUAVSZUWAFUVSUWBUVOFZLAKUFZUVNUWBBFZUVSAUWBKU
      AUGUHUVNUWEUWDUVNUWEMZUVSUWCMZJZAKUIZUWDMUVNUWFUWIUVNUWFJUWBCIZUWIUVNUWFU
      WJUWIUVNUWFUWJNZCBCUWBUJZUKZULZUMUNZUOUNZGZHZUWPEFZUWIUWKCUWNGZUWOGZUWQUW
      KCBGZUWMGZULZUWTUWKCCUWLULZUXDUWLCIUXECHCUWBUPUWLCUQURUWKCUXBUWLUXCCUXBHU
      WKDUSUVNUWFUWLUXCHUWJUVNUXCUWLUVNCOFZUWLOFZUXCUWLHUVNCUXBODBEUTVAZCUWBOVB
      ZUWLOVCVDVGPVEVFBUWMVHVIUVNUWFUWTUXAHZUWJUVNUWNOFZUXJUVNUWMOFUXKUWLVJBUWM
      EOVKVLZUWNOVMTPUWKUWOVNFZUXAUWQHUWKUXMUWNQVOZQUWOFMZUVNUWFUXMUXNUXOJVTZUW
      JUVNUXKUXPUXLUWNOVPTPUVNUWFUXNUWJUVNCBFZBQVOZUXNBCDVQZBCVRBUWNIZUXRUXNBUW
      MWAZBUWNWBWCVDPUWKUXOUBVSZUCVSZWDZQVOZUCUWMUFUBBUFZUWKUYEUBUCBUWMUYCUWMFU
      WKUYBBFZUYCUWLHZUYEUYCUWLWEUWKUYGUYHUYEUWKUYGJUYEUYHUYBUWLWDZQVOZUVNUWFUW
      JUYGUYJUVNUYGUWJUWFUYJUVNUYGUWJUWFUYJLUVNUYGUWJNZUWEUYIQUYKUYIQHZUYBUWBIZ
      UWEUYKUYLUYBCUWLUJZIZUYMUYGUVNUYLUYOVTZUWJUYGUYBCIUYPUYGUYBUXBCUYBBWFDWGU
      YBUWLCWHTWIUWJUVNUYOUYMVTUYGUWJUYNUWBUYBUWJUYNUWBHZUWBCWKWJZWLWMWNUVNUYGU
      WJUYMUWELUVNUYGUWJUYMUWEUYBUWBBCDWOWPWQWRWSXBWTXAUYHUYDUYIQUYCUWLUYBXCXDX
      EXFXGXLUWKBVNFZUWMVNFZUXOUYFVTUVNUWFUYSUWJBXHPUWKUWMEFZUYTUWKUXGUWLQVOZVU
      AUVNUWFUXGUWJUVNUXFUXGUXHUXITPUVNUWFUWJVUBUVNUWJUWFVUBUVNUWJUWFVUBLUVNUWJ
      JUWEUWLQUVNUWJUWLQHZUWEUVNUWJVUCNZUWBCBVUDUYNUWBCUWJUVNUYQVUCUYRWIVUCUVNU
      YNCHUWJVUCUYNCQUJCUWLQCXICXMXJWMXKUVNUWJUXQVUCUXSPXNXOWSXPXQWQUWLOYBXRUWM
      XHTUBUCBUWMXSXRXTYAZUWOUXAUXAYCYDTYEUWKUXMUWSVUEUWOYFTUWKUWRUWSUWILUWSUWQ
      UVPHZUWPUVOIZJZAKUIUWKUWRJZUWIAUWPUWQUWQYCYGVUIVUHUWHAKUWKUWRUVOKFZVUHUWH
      LUWKUWRVUJNZVUHUWHVUKVUHJZUVQUVRUWGVULCUWQUVPUWKUWRVUJVUHYHVUKVUFVUGYIYJV
      ULBUWPUVOVUKBUWPIZVUHUWKUWRVUMVUJUWKBUWOUWPUWKBUWNUWOUXTUWKUYAUSUVNUWFUWN
      UWOIZUWJUVNUXKVUNUXLUWNOYMTZPRUWKUXMUWOUWPIZVUEUWOYKZTRPSVUKVUFVUGYLRVULU
      WCQUVOFZVUKVURMZVUHVUJUWKVUSUWRVUJUVOEFZVUSUVOYNZUVOYOTWMSVUKVUFVUGUWCVUR
      LVUKVUFVUGUWCVURVUKVUFVUGUWCNZJZQUWBUWLWDZUVOUWBCYPVVCVUTUWCUWLUVOFVVDUVO
      FVUKVUTVVBVUJUWKVUTUWRVVAWMSVUKVUFVUGUWCYQVVCUWMUVOUWLVVCUWMUWPUVOVUKUWMU
      WPIVVBVUKUWMUWOUWPUWKUWRUWMUWOIZVUJUVNUWFVVEUWJUVNUWMUWNUWOUWMUWNIUVNUWMB
      YRUSVUORPPVUKUXMVUPUWKUWRUXMVUJVUEPVUQTRSVUKVUFVUGUWCYSRVUKUWLUWMFZVVBUWK
      UWRVVFVUJUVNUWFVVFUWJUVNUXFUXGVVFUXHUXIUWLOYTVDPPSUUAUWBUWLUVOUUDUUEUUFUU
      BUUCUUGUUHXPXOUUIUUJXPUUKXOUVNUWJMZUWILUWFUVNUVSAKUIVVGUWIABCDYGVVGUVSUWH
      AKVVGUVSUWGVVGUVQUWGUVRUVQUWCUWJUWCUWJUVQUWBUVPIUWBUVOWFCUVPUWBUUNUULUUMU
      UOUUPUUQUURSUUSXPUVSUWCAKUVEUUTUVAUVBUVCBUWAIZUVNVUJUVSJZUVRLZVVHAVVHBVVI
      AUVDZUEZIVVJAUVFUWAVVLBUVTVVKUVSAKUVGUVHUVIVVIABUVJUVKVUJUVQUVRYLUVLUSUVM
      $.
      $( [14-Dec-2013] $) $( [1-Jan-2010] $)
  $}

  ${
    $d x y A $.  $d x y X $.  $d x y V $.
    $( Lemma for ~ fixufil and ~ uffixfr .  (Contributed by Mario Carneiro,
       12-Dec-2013.) $)
    uffix $p |- ( ( X e. V /\ A e. X ) -> ( { { A } , X } e. fBas /\
                  { x e. ~P X | A e. x } = ( filGen ` { { A } , X } ) ) ) $=
      ( vy wcel wa csn cfbas cv wceq cun wss cvv sylancr syl snssi adantl sylib
      cuni cpr cpw crab cfg cfv df-pr cfil c0 wne snex snnzg snfil unisn eqcomi
      filfbas extbas1 syl11anc syl5eqel wrex visset simpl uniprg ssequn1 eqtr2d
      elpw sseq2d syl5bb prid1 sseq1 rcla4ev ex mpsyl wi intss1 sstr2 snidg cin
      cint intprg df-ss eleqtrd ssel syl5com sylan9r r19.23adva impbid2 anbi12d
      wb eleq2 elrab a1i eqid elfg 3bitr4d eqrdv jca ) DCFZBDFZGZBHZDUAZIFZBAJZ
      FZADUBZUCZXAUDUEZKWSXAWTHZDHLZIWTDUFWRXIIFZWQWRXHIFZWTDMZXJWRXHUGFZXKWRWT
      NFZWTUHUIXMBUJZBDUKWTNULOXHUOPBDQZDXHWTXHTWTWTXOUMUNUPUQRURZWSEXFXGWSEJZX
      EFZBXRFZGZXRXATZMZXCXRMZAXAUSZGZXRXFFZXRXGFZWSXSYCXTYEXSXRDMWSYCXRDEUTVEW
      SDYBXRWSYBWTDLZDWSXNWQYBYIKXOWQWRVAZWTDNCVBOWSXLYIDKWRXLWQXPRZWTDVCSVDVFV
      GWSXTYEWTXAFZXTWTXRMZYEWTDXOVHBXRQYLYMYEYDYMAWTXAXCWTXRVIVJVKVLWSYDXTAXAX
      CXAFZYDXAVRZXRMZWSXTYNYOXCMYDYPVMXCXAVNYOXCXRVOPWSBYOFYPXTWSBWTYOWRBWTFWQ
      BDVPRWSYOWTDVQZWTWSXNWQYOYQKXOYJWTDNCVSOWSXLYQWTKYKWTDVTSVDWAYOXRBWBWCWDW
      EWFWGYGYAWHWSXDXTAXRXEXCXRBWIWJWKWSXBYHYFWHXQAXRXAYBYBWLWMPWNWOWP $.
      $( [12-Dec-2013] $)

    $( The condition describing a fixed ultrafilter always produces an
       ultrafilter.  (Contributed by Jeff Hankins, 9-Dec-2009.)  (Revised by
       Mario Carneiro, 12-Dec-2013.) $)
    fixufil $p |- ( ( X e. V /\ A e. X ) -> { x e. ~P X | A e. x } e. UFil ) $=
      ( vy wcel wa cv cpw cfil cuni cdif wo wral wceq syl cun wss eleq2 elrab
      crab cufil csn cpr cfg cfv cfbas uffix simprd simpld fgfil eqeltrd undif2
      elpwi ssequn1 sylib syl5req eleq2d biimpac elun adantll ibar adantl difss
      wb elpw2g mpbiri ad2antrr biantrurd orbi12d mpbid r19.21aiva ssrab2 uniss
      ax-mp unipw sseqtri ssid elpwg anim1i sylibr elssuni eqssd difeq1d eleq1d
      a1i pweq syl6bb raleqbidv mpbird eqid isufil sylanbrc ) DCFZBDFZGZBAHZFZA
      DIZUAZJFEHZWTFZWTKZXALZWTFZMZEXCIZNZWTUBFWPWTBUCDUDZUEUFZJWPXIUGFZWTXJOZA
      BCDUHZUIWPXKXJJFWPXKXLXMUJXIUKPULWPXHXAWSFZBXAFZGZDXALZWSFZBXQFZGZMZEWSNW
      PYAEWSWPXNGZXOXSMZYAWOXNYCWNWOXNGBXAXQQZFZYCXNWOYEXNDYDBXNYDXADQZDXADUMXN
      XADRYFDOXADUNXADUOUPUQURUSBXAXQUTUPVAYBXOXPXSXTXNXOXPVEWPXNXOVBVCYBXRXSWN
      XRWOXNWNXRXQDRDXAVDXQDCVFVGVHVIVJVKVLWPXFYAEXGWSWPXCDOXGWSOWPXCDXCDRWPXCW
      SKZDWTWSRXCYGRWRAWSVMWTWSVNVODVPVQWFWPDWTFZDXCRWPDWSFZWOGYHWNYIWOWNYIDDRD
      VRDDCVSVGVTWRWOADWSWQDBSTWADWTWBPWCZXCDWGPWPXBXPXEXTXBXPVEWPWRXOAXAWSWQXA
      BSTWFWPXEXQWTFXTWPXDXQWTWPXCDXAYJWDWEWRXSAXQWSWQXQBSTWHVJWIWJEWTXCXCWKWLW
      M $.
      $( [12-Dec-2013] $) $( [9-Dec-2009] $)
  $}

  ${
    $d x A $.  $d x F $.  $d x X $.
    uffixfr.1 $e |- X = U. F $.
    $( An ultrafilter is either fixed or free.  A fixed ultrafilter is called
       principal (generated by a single element ` A ` ), and a free ultrafilter
       is called nonprincipal (having empty intersection).  Note that free
       ultrafilters cannot be constructed.  (Contributed by Jeff Hankins,
       4-Dec-2009.)  (Revised by Mario Carneiro, 12-Dec-2013.) $)
    uffixfr $p |- ( ( F e. UFil /\ A e. |^| F ) ->
                    F = { x e. ~P X | A e. x } ) $=
      ( cufil wcel cint wa cpw crab cfil cuni wceq wss syl sylanbrc ax-mp a1i
      cv simpl csn cpr cfg cfv cfbas ufilfil filusb adantr c0 wne intssuni 3syl
      emnfil syl6sseqr sseld imp uffix syl11anc simprd fgfil eqeltrd ssid elpwg
      simpld mpbiri eleq2 elrab elssuni uniss unipw sseqtri eqssd wral sseqtr4i
      ssrab2 pwuni pweq elintg ibi adantl ssrab eqid ufilmax syl22anc ) CFGZBCH
      ZGZIZWFBATZGZADJZKZLGDWMMZNCWMOZCWMNWFWHUAWIWMBUBDUCZUDUEZLWIWPUFGZWMWQNZ
      WIDCGZBDGZWRWSIWFWTWHWFCLGZWTCUGZCDEUHPUIZWFWHXAWFWGDBWFWGCMZDWFXBCUJUKWG
      XEOXCCUNCULUMEUOUPUQZABCDURUSZUTWIWRWQLGWIWRWSXGVEWPVAPVBWIDWNWIDWMGZDWNO
      WIDWLGZXAXHWIWTXIXDWTXIDDODVCDDCVDVFPXFWKXAADWLWJDBVGVHQDWMVIPWNDOWIWNWLM
      ZDWMWLOWNXJOWKAWLVPWMWLVJRDVKVLSVMWICWLOZWKACVNZWOXKWICXEJZWLCVQDXENWLXMN
      EDXEVRRVOSWHXLWFWHXLABCWGVSVTWAWKAWLCWBQCWMDWNEWNWCWDWE $.
      $( [12-Dec-2013] $) $( [4-Dec-2009] $)
  $}

  ${
    $d x y F $.  $d x y X $.
    uffinfix.1 $e |- X = U. F $.
    $( An ultrafilter containing a finite element is fixed.  (Contributed by
       Jeff Hankins, 5-Dec-2009.) $)
    uffinfix $p |- ( ( F e. UFil /\ S e. F /\ S e. Fin ) ->
                     E. x e. X F = { y e. ~P X | x e. y } ) $=
      ( cufil wcel cfn w3a cv cint wex cpw crab wceq wrex cfbas c0 wne cfil syl
      ufilfil filfbas fbfinnfr n0 sylib syl3an1 wi wa cuni wss fbasne0 intssuni
      3syl syl6sseqr sseld uffixfr ex jcad eximdv df-rex syl6ibr 3ad2ant1 mpd )
      DGHZCDHZCIHZJAKZDLZHZAMZDVIBKHBENOPZAEQZVFDRHZVGVHVLVFDUAHVODUCDUDUBZVOVG
      VHJVJSTVLCDUEAVJUFUGUHVFVGVLVNUIVHVFVLVIEHZVMUJZAMVNVFVKVRAVFVKVQVMVFVJEV
      IVFVJDUKZEVFVODSTVJVSULVPDUMDUNUOFUPUQVFVKVMBVIDEFURUSUTVAVMAEVBVCVDVE $.
      $( [13-Dec-2013] $) $( [5-Dec-2009] $)
  $}

  ${
    $d f u v x y z A $.  $d f u v x y z B $.  $d f u v x y z X $.
    $( An infinite subset is contained in a free ultrafilter.  (Contributed by
       Jeff Hankins, 6-Dec-2009.) $)
    ufinffr $p |- ( ( X e. B /\ A C_ X /\ om ~<_ A ) -> E. f e. UFil ( X = U. f
    /\ A e. f /\ |^| f = (/) ) ) $=
      ( vx vy wcel wss com wbr w3a cv cdif cfn wceq wa cufil c0 wn a1i cdom cpw
      crab cuni wrex cint cfil csdm domnsym isfinite1b nsyl rcfpfil simpld eqid
      syl3an3 filssufil syl simprd eqeq1d biimprd elpw2g biimpar 3adant3 difeq2
      0fin difid syl6eq eleq1d elrab sylanbrc ssel syl5com intss wal csn visset
      snid eldifn mt2 elinti mtoi difss simp2 sstrd wb 3ad2ant1 mpbird wi eldif
      snfi notbii iman bitr4i anbi2i bitri pm3.35 sylbi ssriv ssfi 19.21aiv eq0
      mp2an nsyl3 sylibr sseq2d syl5ib syl6 jcad anim12d 3anass syl6ibr reximdv
      ss0 mpd ) DBGZADHZIAUAJZKZAELZMZNGZEDUBZUCZUDZCLZUDZOZYCYEHZPZCQUEZDYFOZA
      YEGZYEUFZROZKZCQUEXRYCUGGZYJXRYPDYDOZXQXOXPANGZSYPYQPXQAIUHJYRIAUIAUJUKEA
      BDULUOZUMCYCYDYDUNUPUQXRYIYOCQXRYIYKYLYNPZPYOXRYGYKYHYTXRYKYGXRDYDYFXRYPY
      QYSURUSUTXRYHYLYNXRAYCGZYHYLXRAYBGZRNGZUUAXOXPUUBXQXOUUBXPADBVAVBVCUUCXRV
      ETYAUUCEAYBXSAOZXTRNUUDXTAAMRXSAAVDAVFVGVHVIVJYCYEAVKVLXRYHYMRHZYNYHYMYCU
      FZHXRUUEYCYEVMXRUUFRYMXRFLZUUFGZSZFVNUUFROXRUUIFUUHAUUGVOZMZYCGZXRUUHUULU
      UGUUKGZUUMUUGUUJGUUGFVPVQUUGAUUJVRVSUUGYCUUKVTWAXRUUKYBGZAUUKMZNGZUULXRUU
      NUUKDHZXRUUKADUUKAHXRAUUJWBTXOXPXQWCWDXOXPUUNUUQWEXQUUKDBVAWFWGUUPXRUUJNG
      UUOUUJHUUPUUGWJEUUOUUJXSUUOGZXSAGZUUSXSUUJGZWHZPZUUTUURUUSXSUUKGZSZPUVBXS
      AUUKWIUVDUVAUUSUVDUUSUUTSPZSUVAUVCUVEXSAUUJWIWKUUSUUTWLWMWNWOUUSUUTWPWQWR
      UUJUUOWSXBTYAUUPEUUKYBXSUUKOXTUUONXSUUKAVDVHVIVJXCWTFUUFXAXDXEXFYMXMXGXHX
      IYKYLYNXJXKXLXN $.
      $( [4-Dec-2013] $) $( [6-Dec-2009] $)
  $}

  ${
    $d f u v x y z A $.  $d f u v x y z F $.  $d f u v x y z X $.
    $( Any infinite set has an ultrafilter on it whose elements are of the same
       cardinality as the set.  Any such ultrafilter is necessarily free.
       (Contributed by Jeff Hankins, 7-Dec-2009.)  (The proof was shortened by
       Mario Carneiro, 14-Dec-2013.) $)
    ufilen $p |- ( ( X e. A /\ om ~<_ X ) -> E. f e. UFil ( X = U. f /\ A. x e.
    f x ~~ X ) ) $=
      ( vy wcel cdom wbr wa cv cdif csdm cuni wceq wss cufil wrex cfil syl wi
      com cpw crab cen wral csdfil simpld filssufil simprd eqeq1d anbi1d adantr
      eqid wb wn simplll elssuni ad2antll simprl sseqtr4d cfbas ufilfil filfbas
      adantl simpr fbncp syl2an w3a difss elpw2g mpbiri simp2 dfss4 sylib simp3
      3ad2ant1 eqbrtrd difeq2 breq1d elrab sylanbrc ssel syl5com 3expa impancom
      con3d syl21anc cvv visset ssdomg ax-mp bren2 biimpri 3syl syld expr com23
      ex r19.21adv imdistanda sylbird reximdva mpd ) DBFZUADGHZIZDEJZKZDLHZEDUB
      ZUCZMZCJZMZNZXKXMOZIZCPQZDXNNZAJZDUDHZAXMUEZIZCPQXFXKRFZXRXFYDDXLNZEBDUFZ
      UGCXKXLXLUMUHSXFXQYCCPXFXMPFZIZXQXSXPIZYCXFYIXQUNYGXFXSXOXPXFDXLXNXFYDYEY
      FUIUJUKULYHXSXPYBYHXSIZXPYAAXMYJXTXMFZXPYAYHXSYKXPYATYHXSYKIZIZXPXTDLHZUO
      ZYAYMXDXTDOZDXTKZXMFZUOZXPYOTXDXEYGYLUPYMXTXNDYKXTXNOYHXSXTXMUQURYHXSYKUS
      UTZYHXMVAFZYKYSYLYGUUAXFYGXMRFUUAXMVBXMVCSVDXSYKVEXTDXMVFVGXDYPIZXPYSYOUU
      BXPIYNYRUUBYNXPYRXDYPYNXPYRTXDYPYNVHZYQXKFZXPYRUUCYQXJFZDYQKZDLHZUUDXDYPU
      UEYNXDUUEYQDODXTVIYQDBVJVKVPUUCUUFXTDLUUCYPUUFXTNXDYPYNVLXTDVMVNXDYPYNVOV
      QXIUUGEYQXJXGYQNXHUUFDLXGYQDVRVSVTWAXKXMYQWBWCWDWEWFWEWGYMYPXTDGHZYOYATYT
      XTWHFYPUUHTAWIXTDWHWJWKUUHYOYAYAUUHYOIXTDWLWMWRWNWOWPWQWSWTXAXBXC $.
      $( [14-Dec-2013] $) $( [7-Dec-2009] $)
  $}

  ${
    $d x F $.  $d x J $.  $d x X $.
    ufildr.1 $e |- J = ( F u. { (/) } ) $.
    ufildr.2 $e |- X = U. F $.
    $( An ultrafilter gives rise to a connected door topology.  (Contributed by
       Jeff Hankins, 6-Dec-2009.)  (Revised by Mario Carneiro, 14-Dec-2013.) $)
    ufildr $p |- ( F e. UFil -> ( J u. ( Clsd ` J ) ) = ~P X ) $=
      ( vx cufil wcel ccld cfv cun wo wss wi cuni c0 a1i ctop ex sseld cv uniun
      cpw elssuni csn 0ex uneq2i un0 3eqtri unieqi 3eqtr4ri syl6sseqr cfil ccon
      unisn ufilfil filcon contop 3syl syl5eqel cldss jaod wa cdif ufilss ssun1
      syl sseqtr4i wb iscld2 sylan sylibrd orim12d mpd impbid elun elpw 3bitr4g
      visset eqrdv ) AGHZFBBIJZKZCUCZWAFUAZBHZWEWBHZLZWECMZWEWCHWEWDHWAWHWIWAWF
      WIWGWFWINWAWFWEBOZCWEBUDAPUEZKZOZAOZWJCWMWNWKOZKWNPKWNAWKUBWOPWNPUFUOUGWN
      UHUIBWLDUJEUKZULQWABRHZWGWINWABWLRDWAAUMHWLUNHWLRHAUPAUQWLURUSUTZWQWGWIWE
      BCWPVASVGVBWAWIWHWAWIVCZWEAHZCWEVDZAHZLWHWEACEVEWSWTWFXBWGWSABWEABMWSAWLB
      AWKVFDVHQZTWSXBXABHZWGWSABXAXCTWAWQWIWGXDVIWRWEBCWPVJVKVLVMVNSVOWEBWBVPWE
      CFVSVQVRVT $.
      $( [14-Dec-2013] $) $( [6-Dec-2009] $)
  $}

