$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Directed sets, nets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d d x D $.  $d x X $.  $d x A $.
    tailfval.1 $e |- X = dom D $.
    $( The tail function for a directed set.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    tailfval $p |- ( D e. DirRel -> ( tail ` D ) =
                     ( x e. X |-> ( D " { x } ) ) ) $=
      ( vd cdir wcel ctail cfv cuni cv csn cima cmpt cvv wceq uniexg syl mptexg
      mpteq12dv unieq unieqd imaeq1 df-tail mpdan cdm dirdm syl5req eqidd eqtrd
      fvmptg ) BFGZBHIZABJZJZBAKLZMZNZACUQNULUROGZUMURPULUOOGZUSULUNOGUTBFQUNOQ
      RAUOUQOSREBAEKZJZJZVAUPMZNURFOHVABPZAVCVDUOUQVEVBUNVABUAUBVABUPUCTAEUDUKU
      EULAUOUQCUQULCBUFUODBUGUHULUQUITUJ $.
      $( [24-Nov-2013] $) $( [25-Nov-2009] $)

    $( The tail of an element in a directed set.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    tailval $p |- ( ( D e. DirRel /\ A e. X ) ->
                   ( ( tail ` D ) ` A ) = ( D " { A } ) ) $=
      ( vx cdir wcel wa ctail cfv csn cima cmpt wceq tailfval fveq1d adantr cvv
      cv id imaexg sneq imaeq2d eqid fvmptg syl2anc eqtrd ) BFGZACGZHABIJZJZAEC
      BESZKZLZMZJZBAKZLZUHUKUPNUIUHAUJUOEBCDOPQUIUIURRGUPURNUHUITBUQFUAEAUNURCR
      UOULANUMUQBULAUBUCUOUDUEUFUG $.
      $( [24-Nov-2013] $) $( [25-Nov-2009] $)

    $( An element of a tail.  (Contributed by Jeff Hankins, 25-Nov-2009.) $)
    eltail $p |- ( ( D e. DirRel /\ A e. X /\ B e. C ) ->
                   ( B e. ( ( tail ` D ) ` A ) <-> A D B ) ) $=
      ( cdir wcel w3a ctail cfv csn cima wbr wb wa tailval eleq2d 3adant3 cop
      elimasng df-br syl6bbr 3adant1 bitrd ) DGHZAEHZBCHZIBADJKKZHZBDALMZHZABDN
      ZUFUGUJULOUHUFUGPUIUKBADEFQRSUGUHULUMOUFUGUHPULABTDHUMDABECUAABDUBUCUDUE
      $.
      $( [24-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  ${
    $d s x y z D $.  $d s x y z X $.
    tailf.1 $e |- X = dom D $.
    $( The tail function of a directed set sends its elements to its subsets.
       (Contributed by Jeff Hankins, 25-Nov-2009.) $)
    tailf $p |- ( D e. DirRel -> ( tail ` D ) : X --> ~P X ) $=
      ( vx cdir wcel cpw ctail cfv wf cv csn cima cmpt wral wss cuni cvv mpbird
      sstri crn imassrn cdm cun ssun2 dmrnssfld dirdm syl5req syl5sseq wb dmexg
      syl5eqel elpw2g syl a1d r19.21aiv eqid fmpt sylib tailfval feq1d ) AEFZBB
      GZAHIZJBVCDBADKZLZMZNZJZVBVGVCFZDBOVIVBVJDBVBVJVEBFVBVJVGBPZVBAQQZVGBVGAU
      AZVLAVFUBVMAUCZVMUDVLVMVNUEAUFTTVBBVNVLCAUGUHUIVBBRFVJVKUJVBBVNRCAEUKULVG
      BRUMUNSUOUPDBVCVGVHVHUQURUSVBBVCVDVHDABCUTVAS $.
      $( [24-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  ${
    tailini.1 $e |- X = dom D $.
    $( A tail contains its initial element.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    tailini $p |- ( ( D e. DirRel /\ A e. X ) -> A e. ( ( tail ` D ) ` A ) ) $=
      ( cdir wcel wa ctail cfv wbr dirref wb eltail 3anidm23 mpbird ) BEFZACFZG
      AABHIIFZAABJZABCDKPQRSLAACBCDMNO $.
      $( [25-Nov-2009] $)
  $}

  ${
    $d t x D $.  $d t x X $.
    tailuni.1 $e |- X = dom D $.
    $( The union of all the tails of a directed set.  (Contributed by Jeff
       Hankins, 13-Dec-2009.) $)
    tailuni $p |- ( D e. DirRel -> U. ran ( tail ` D ) = X ) $=
      ( vx vt cdir wcel ctail cfv crn cuni cv wss wral cpw wf wi syl sylibr wa
      tailf frn sseld visset elpw syl6ib r19.21aiv wex tailini wfn ffn fnfvelrn
      unissb sylan fvex wceq eleq2 eleq1 anbi12d cla4ev syl11anc eluni ex ssrdv
      eqssd ) AFGZAHIZJZKZBVFDLZBMZDVHNVIBMVFVKDVHVFBBOZVGPZVJVHGZVKQABCUAZVMVN
      VJVLGVKVMVHVLVJBVLVGUBUCVJBDUDUEUFRUGDVHBUMSVFDBVIVFVJBGZVJVIGZVFVPTZVJEL
      ZGZVSVHGZTZEUHZVQVRVJVJVGIZGZWDVHGZWCVJABCUIVFVGBUJZVPWFVFVMWGVOBVLVGUKRB
      VJVGULUNWBWEWFTEWDVJVGUOVSWDUPVTWEWAWFVSWDVJUQVSWDVHURUSUTVAEVJVHVBSVCVDV
      E $.
      $( [13-Dec-2009] $)
  $}

  ${
    $d u v w x y z D $.  $d u v w x y z X $.
    tailfb.1 $e |- X = dom D $.
    $( The collection of tails of a directed set is a filter base.
       (Contributed by Jeff Hankins, 25-Nov-2009.) $)
    tailfb $p |- ( ( D e. DirRel /\ X =/= (/) )
          -> ran ( tail ` D ) e. fBas ) $=
      ( vz vx vy vu vv vw wcel c0 wa cfv cv wss wrex wi wb wbr cvv cdir wne crn
      ctail wnel cin wral w3a cfbas wex n0 cpw wfn tailf ffn fnfvelrn 3syl ne0i
      wf ex syl6 19.23adv syl5bi imp wceq tailini n0i syl nrexdv adantr fvelrnb
      mtbird df-nel sylibr reeanv dirge 3expb sylan ad2ant2r visset dirtr exp32
      wn anbi12d mpan2 com23 ad2ant2rl anim12d expr impr eltail adantrr adantrl
      mp3an3 3imtr4d elin syl6ibr ssrdv sseq1 rcla4ev syl11anc r19.23adv sseq2d
      mpd ineq1 rexbidv sylan9bb syl5ibcom r19.23advv syl5bir sylbid r19.21aivv
      ineq2 3jca fvex rnex isfbas2 ax-mp ) AUAJZBKUBZLZAUDMZUCZKUBZKYCUEZDNZENZ
      FNZUFZOZDYCPZFYCUGEYCUGZUHZYCUIJZYAYDYEYLXSXTYDXTYGBJZEUJXSYDEBUKXSYOYDEX
      SYOYGYBMZYCJZYDXSBBULZYBUSZYBBUMZYOYQQABCUNZBYRYBUOZYTYOYQBYGYBUPUTUQYCYP
      URVAVBVCVDYAKYCJZWCYEYAUUCYPKVEZEBPZXSUUEWCXTXSUUDEBXSYOLYGYPJUUDWCYGABCV
      FYPYGVGVHVIVJXSUUCUUERZXTXSYSYTUUFUUAUUBEBKYBVKUQVJVLKYCVMVNYAYKEFYCYCXSY
      GYCJZYHYCJZLZYKQXTXSUUIGNZYBMZYGVEZGBPZHNZYBMZYHVEZHBPZLZYKXSYSYTUUIUURRU
      UAUUBYTUUGUUMUUHUUQGBYGYBVKHBYHYBVKWDUQUURUULUUPLZHBPGBPXSYKUULUUPGHBBVOX
      SUUSYKGHBBXSUUJBJZUUNBJZLZUUSYKQXSUVBLZYFUUKUUOUFZOZDYCPZUUSYKUVCUUJINZAS
      ZUUNUVGASZLZIBPZUVFXSUUTUVAUVKIUUJUUNABCVPVQUVCUVJUVFIBUVCUVGBJZUVJUVFUVC
      UVLUVJLZLZUVGYBMZYCJZUVOUVDOZUVFXSUVLUVPUVBUVJXSYTUVLUVPXSYSYTUUAUUBVHBUV
      GYBUPVRVSUVNEUVOUVDUVNYGUVOJZYGUUKJZYGUUOJZLZYGUVDJUVNUVGYGASZUUJYGASZUUN
      YGASZLZUVRUWAUVCUVLUVJUWBUWEQUVCUVLLUWBUVJUWEUVCUVLUWBUVJUWEQUVCUVLUWBLLU
      VHUWCUVIUWDXSUWBUVHUWCQZUVBUVLXSUWBUWFXSUVHUWBUWCXSYGTJZUVHUWBUWCQQEVTZXS
      UWGLZUVHUWBUWCUUJUVGYGATWAWBWEWFVDWGXSUWBUVIUWDQZUVBUVLXSUWBUWJXSUVIUWBUW
      DXSUWGUVIUWBUWDQQUWHUWIUVIUWBUWDUUNUVGYGATWAWBWEWFVDWGWHWIWFWJXSUVLUVRUWB
      RZUVBUVJXSUVLUWGUWKUWHUVGYGTABCWKWNVSUVCUWAUWERUVMUVCUVSUWCUVTUWDXSUUTUVS
      UWCRZUVAXSUUTUWGUWLUWHUUJYGTABCWKWNWLXSUVAUVTUWDRZUUTXSUVAUWGUWMUWHUUNYGT
      ABCWKWNWMWDVJWOYGUUKUUOWPWQWRUVEUVQDUVOYCYFUVOUVDWSWTXAWBXBXDUULUVFYFYGUU
      OUFZOZDYCPUUPYKUULUVEUWODYCUULUVDUWNYFUUKYGUUOXEXCXFUUPUWOYJDYCUUPUWNYIYF
      UUOYHYGXMXCXFXGXHUTXIXJXKVJXLXNYCTJYNYMRYBAUDXOXPEFDTYCXQXRVN $.
      $( [25-Nov-2009] $)
  $}

  ${
    $d d f m n t v x y w z F $.  $d d f m n t w z X $.
    filnet.1 $e |- X = U. F $.
    ${
      $d t v w x y z H $.  $d t v w z D $.
      filnet.2 $e |- H = { <. m , n >. | ( n e. F /\ m e. n ) } $.
      filnet.3 $e |- D = { <. x , y >. |
        ( ( x e. H /\ y e. H ) /\ ( 2nd ` y ) C_ ( 2nd ` x ) ) } $.
      ${
        $d A x y $.  $d B x y $.
        filnetlem1.4 $e |- A e. _V $.
        filnetlem1.5 $e |- B e. _V $.
        $( Lemma for ~ filnet .  Change variables. $)
        filnetlem1 $p |- ( A D B <-> ( ( A e. H /\ B e. H ) /\
          ( 2nd ` B ) C_ ( 2nd ` A ) ) ) $=
          ( wcel wa c2nd cfv wss wceq eleq1 anbi1d sseq2d anbi12d anbi2d sseq1d
          cv fveq2 brab ) AUHZIPZBUHZIPZQZUMRSZUKRSZTZQCIPZUNQZUPCRSZTZQUSDIPZQ
          ZDRSZVATZQABCDENOUKCUAZUOUTURVBVGULUSUNUKCIUBUCVGUQVAUPUKCRUIUDUEUMDU
          AZUTVDVBVFVHUNVCUSUMDIUBUFVHUPVEVAUMDRUIUGUEMUJ $.
          $( [23-Nov-2013] $) $( [13-Dec-2009] $)
      $}

      $( Lemma for ~ filnet .  The field of the direction. $)
      filnetlem2 $p |- ( H = U. U. D /\ H = dom D ) $=
        ( vz cuni cv wcel wa c2nd cfv wss sstri wceq cdm crn cun wbr filnetlem1
        visset mpbiran2 breldm sylbir anidms ssriv ssun1 dmrnssfld cxp opabssxp
        ssid copab eqsstri uniss mp2b unixpss unidm sseqtri eqssi dmxpid pm3.2i
        dmss ax-mp ) GCMZMZUAGCUBZUAGVKGVLCUCZUDZVKGVLVNLGVLLNZGOZVOVLOZVPVPPZV
        OVOCUEZVQVSVRVOQRZVTSABVOVOCDEFGHIJKLUGZWAUFVTUQUHVOVOCWAUIUJUKULZVLVMU
        MTCUNTVKGGUDZGVKGGUOZMZMZWCCWDSZVJWESVKWFSCANZGOBNZGOPWIQRWHQRSZPABURWD
        KWJABGGUPUSZCWDUTVJWEUTVAGGVBTGVCVDVEGVLWBVLWDUBZGWGVLWLSWKCWDVHVIGVFVD
        VEVG $.
        $( [23-Nov-2013] $) $( [13-Dec-2009] $)

      $( Lemma for ~ filnet . $)
      filnetlem3 $p |- ( H C_ ( X X. F ) /\ ( F e. Fil -> D e. DirRel ) ) $=
        ( vv vw vz wss wcel wa wbr c2nd cvv vt cxp cfil cdir wi cv copab elunii
        cuni syl6eleqr simpl jca ssopab2i df-xp 3sstr4i wrel cid cres ccom ccnv
        ancoms wex wral cfv cin c0 c1st wceq eleq1 anbi2d eleq2 anbi12d elopabi
        wne eleq2s simpld fveq2 eleq1d vtoclga anim12i fipfil imp n0 w3a simp2l
        syl5 cop filint 3expb sylan2 3adant3 simp3 df-br visset fvex inex1 brab
        bitr3i sylanbrc op2nd inss1 syl5eqss opex filnetlem1 simp2r inss2 breq2
        a1i cla4ev syl11anc 3expia 19.23adv syl5bi mpd r19.21aivva codir sylibr
        wal simplll simprlr sstr ad2ant2l jca31 anbi12i 3imtr4i gen2 cotr mpbir
        ax-gen jctil relopab releqi ssid mpbiran2 biimpri anidms pm3.2i sylancr
        xpexg ssexg rgen ref4w mpbi wb opabssxp eqsstri uniexg syl5eqel mpancom
        cdm filnetlem2 simpli isdir syl mpbird ) GHFUBZOZFUCPZCUDPZUEEUFZFPZDUF
        ZUUTPZQZDEUGZUVBHPZUVAQZDEUGGUUPUVDUVGDEUVDUVFUVAUVDUVBFUIZHUVCUVAUVBUV
        HPUVBUUTFUHVAIUJUVAUVCUKULUMJDEHFUNUOZUURUUSCUPZUQGURCOZQZCCUSCOZGGUBZC
        UTCUSOZQZQZUURUVPUVLUURUVOUVMUURUAUFZLUFZCRZMUFZUVSCRZQZLVBZMGVCUAGVCUV
        OUURUWDUAMGGUURUVRGPZUWAGPZQZQZUVRSVDZUWASVDZVEZVFVNZUWDUURUWGUWLUWGUWI
        FPZUWJFPZQZUURUWLUWEUWMUWFUWNUWEUWMUVRVGVDZUWIPZUWMUWQQZUVRUVEGUVDUVAUW
        PUUTPZQUWRDEUVRUVBUWPVHUVCUWSUVAUVBUWPUUTVIVJUUTUWIVHUVAUWMUWSUWQUUTUWI
        FVIUUTUWIUWPVKVLVMJVOVPZUWMUWNUAUWAGUVRUWAVHUWIUWJFUVRUWASVQVRUWTVSVTZU
        WIUWJFWAWFWBUWLNUFZUWKPZNVBUWHUWDNUWKWCUWHUXCUWDNUURUWGUXCUWDUURUWGUXCW
        DZUVRUXBUWKWGZCRZUWAUXECRZUWDUXDUWEUXEGPZQUXESVDZUWIOUXFUXDUWEUXHUURUWE
        UWFUXCWEUXDUWKFPZUXCUXHUURUWGUXJUXCUWGUURUWOUXJUXAUURUWMUWNUXJUWIUWJFWH
        WIWJWKUURUWGUXCWLUXHUXBUWKGRUXJUXCQZUXBUWKGWMUVDUVAUXBUUTPZQUXKDEUXBUWK
        GNWNZUWIUWJUVRSWOWPZUVBUXBVHUVCUXLUVAUVBUXBUUTVIVJUUTUWKVHUVAUXJUXLUXCU
        UTUWKFVIUUTUWKUXBVKVLJWQWRWSZULUXDUXIUWKUWIUXBUWKUXMUXNWTZUWKUWIOUXDUWI
        UWJXAXHXBABUVRUXECDEFGHIJKUAWNUXBUWKXCZXDWSUXDUWFUXHQUXIUWJOUXGUXDUWFUX
        HUURUWEUWFUXCXEUXOULUXDUXIUWKUWJUXPUWKUWJOUXDUWIUWJXFXHXBABUWAUXECDEFGH
        IJKMWNZUXQXDWSUWCUXFUXGQLUXEUXQUVSUXEVHUVTUXFUWBUXGUVSUXEUVRCXGUVSUXEUW
        ACXGVLXIXJXKXLXMXNXOUAMLGGCXPXQUVMUVSUWACRZUWAUXBCRZQZUVSUXBCRZUEZNXRZM
        XRLXRUYDLMUYCNUVSGPZUWFQZUWJUVSSVDZOZQZUWFUXBGPZQZUXBSVDZUWJOZQZQZUYEUY
        JQUYLUYGOZQUYAUYBUYOUYEUYJUYPUYEUWFUYHUYNXSUYIUWFUYJUYMXTUYHUYMUYPUYFUY
        KUYMUYHUYPUYLUWJUYGYAVAYBYCUXSUYIUXTUYNABUVSUWACDEFGHIJKLWNZUXRXDABUWAU
        XBCDEFGHIJKUXRUXMXDYDABUVSUXBCDEFGHIJKUYQUXMXDYEYIYFLMNCYGYHYJUVJUVKUVJ
        AUFZGPBUFZGPQUYSSVDUYRSVDOZQZABUGZUPVUAABYKCVUBKYLYHUXBUXBCRZNGVCUVKVUC
        NGUYJVUCVUCUYJUYJQZVUCVUDUYLUYLOABUXBUXBCDEFGHIJKUXMUXMXDUYLYMYNYOYPUUA
        NGCUUBUUCYQYJUURCTPZUUSUVQUUDUURCUVNOUVNTPZVUECVUBUVNKUYTABGGUUEUUFUURG
        TPZVUGVUFUURUUQUUPTPZVUGUVIHTPUURVUHUURHUVHTIFUCUUGUUHHFTUCYSUUIGUUPTYT
        YRZVUIGGTTYSXJCUVNTYTYRGCTGCUIUIVHGCUUJVHABCDEFGHIJKUUKUULUUMUUNUUOYQ
        $.
        $( [24-Nov-2013] $) $( [13-Dec-2009] $)

      $( Lemma for ~ filnet . $)
      filnetlem4 $p |- ( F e. Fil -> E. z e. DirRel E. w ( w : dom z --> X
       /\ F = ( ( X FilMap ran ( tail ` z ) ) ` w ) ) ) $=
        ( wcel cfv wceq wa wss c1st cvv vt vv cfil cdir cv wf ctail crn cfm wex
        co cdm wrex cxp wi filnetlem3 simpri cres wfun wfo fo1st fof ax-mp ffun
        simpli cuni syl5eqel xpexg mpancom ssexg sylancr resfunexg cima elssuni
        uniexg syl6sseqr a1i c0 wne filesn eleq1 notbid syl5ibrcom necon2ad imp
        wn n0 sylib w3a cop wfn cpw filnetlem2 tailf ffn 3ad2ant1 3simpc visset
        3syl wbr anbi2d eleq2 anbi12d brab bitr3i fnfvelrn syl11anc wral eltail
        df-br mp3an3 opex brelrn sseli syl syl6bi c2nd filnetlem1 op2nd eqeltrd
        wb fveq2 ssrdv funimass4 mpbird imaeq2 sseq1d rcla4ev 3expia mpd adantr
        19.23adv ex eleq1d sylan impancom sylbid r19.23adv feq2d eqeq2d simprbi
        cun ssun2 dmrnssfld sstri sseqtr4i fvres biimpa syl6sseq eleq12d eleq2i
        copab elopabi sylbi simprd vtoclga sseldd r19.21aiva ssv mp2an fdm jcad
        fssres fvelrnb biimprcd reximdv syl6 sseld ralbidva bitrd simpll simpld
        ad2antlr simprl simpl anim1i fvex eqimssi mpbiran2 adantll op1st syl6eq
        sylanbrc rcla4v adantrl fillsb syl13anc exp3a syl9r com23 expimpd cfbas
        impbid emnfil ne0i tailfb ffnfv xp1st rgen mpbir2an eqid elfm syl111anc
        tailuni bitr4d eqrdv jctil feq1 cla4egv sylc dmeq syl6eqr rneqd opreq2d
        fveq1d exbidv ) HUCNZEUDNZIJDUEZUFZHUXSJEUGOZUHZUIUKZOZPZQZDUJZCUEZULZJ
        UXSUFZHUXSJUYHUGOZUHZUIUKZOZPZQZDUJZCUDUMIJHUNZRZUXQUXRUOZABEFGHIJKLMUP
        ZUQZUXQSIURZTNZIJVUCUFZHVUCUYCOZPZQZUYGUXQSUSZITNZVUDTTSUFZVUITTSUTVUKV
        ATTSVBVCZTTSVDVCUXQUYSUYRTNZVUJUYSUYTVUAVEZJTNZUXQVUMUXQJHVFZTKHUCVOVGZ
        JHTUCVHVIIUYRTVJVKSITVLVKUXQVUGVUEUXQUAHVUFUXQUAUEZHNZVURJRZVUCUYHVMZVU
        RRZCUYBUMZQZVURVUFNZUXQVUSVVDUXQVUSVUTVVCVUSVUTUOUXQVUSVURVUPJVURHVNKVP
        VQUXQVUSVVCUXQVUSQZUBUEZVURNZUBUJZVVCVVFVURVRVSZVVIUXQVUSVVJUXQVUSVURVR
        UXQVUSWFVURVRPZVRHNZWFHVTVVKVUSVVLVURVRHWAWBWCWDWEUBVURWGWHZVVFVVHVVCUB
        UXQVUSVVHVVCUXQVUSVVHWIZVVGVURWJZUYAOZUYBNZVUCVVPVMZVURRZVVCVVNUYAIWKZV
        VOINZVVQUXQVUSVVTVVHUXQUXRIIWLZUYAUFZVVTVUBEIIEVFVFZPZIEULZPZABEFGHIJKL
        MWMZUQZWNZIVWBUYAWOZWSWPVVNVUSVVHQZVWAUXQVUSVVHWQVWLVVGVURIWTVWAGUEZHNZ
        FUEZVWMNZQZVWNVVGVWMNZQZVWLFGVVGVURIUBWRZUAWRZVWOVVGPVWPVWRVWNVWOVVGVWM
        WAXAZVWMVURPVWNVUSVWRVVHVWMVURHWAVWMVURVVGXBXCLXDVVGVURIXJXEWHZIVVOUYAX
        FXGVVNVVSUYHVUCOZVURNZCVVPXHZVVNVXECVVPVVNUYHVVPNZQZVXDUYHSOZVURVXHUYHI
        NZVXDVXIPZVVNVXGVXJVVNVXGVVOUYHEWTZVXJVVNUXRVWAVXGVXLYAZUXQVUSUXRVVHVUB
        WPVXCUXRVWAUYHTNZVXMCWRZVVOUYHTEIVWIXIXKXGZVXLUYHEUHZNZVXJVVOUYHEVVGVUR
        XLZVXOXMVXQIUYHVXQVWDIVXQVWFVXQUUBVWDVXQVWFUUCEUUDUUEVWEVWGVWHVEUUFXNZX
        OXPZWEZUYHISUUGZXOVXHUYHXQOZVURVXIVXHVXLVYDVURRVVNVXGVXLVXPUUHVXLVYDVVO
        XQOZVURVXLVWAVXJQVYDVYERABVVOUYHEFGHIJKLMVXSVXOXRUUAVVGVURVWTVXAXSUUIXO
        VXHVXJVXIVYDNZVYBUXSSOZUXSXQOZNZVYFDUYHIUXSUYHPVYGVXIVYHVYDUXSUYHSYBUXS
        UYHXQYBUUJUXSINZVYHHNZVYIVYJUXSVWQFGUULZNVYKVYIQZIVYLUXSLUUKVWQVWNVYGVW
        MNZQVYMFGUXSVWOVYGPVWPVYNVWNVWOVYGVWMWAXAVWMVYHPZVWNVYKVYNVYIVWMVYHHWAZ
        VWMVYHVYGXBXCUUMUUNZUUOUUPXOUUQXTUURVVNVUCUSZVVPVUCULZRVVSVXFYAITVUCUFZ
        VYRVUKITRVYTVULIUUSTTISUVCUUTZITVUCVDVCZVVNVVPIVYSVVNCVVPIVYAYCVYTVYSIP
        WUAITVUCUVAVCZVPCVVPVURVUCYDVKYEVVBVVSCVVPUYBUYHVVPPVVAVVRVURUYHVVPVUCY
        FYGYHXGYIYLYJYMUVBUXQVUTVVCVUSUXQVUTQZVVBVUSCUYBWUDUYHUYBNZUXSUYAOZUYHP
        ZDIUMZVVBVUSUOWUDUXRWUEWUHYAZUXQUXRVUTVUBYKUXRVWCVVTWUIVWJVWKDIUYHUYAUV
        DWSXOWUDVVBWUHVUSVVBWUHVUCWUFVMZVURRZDIUMWUDVUSVVBWUGWUKDIWUGWUKVVBWUGW
        UJVVAVURWUFUYHVUCYFYGUVEUVFWUDWUKVUSDIUXQVYJVUTWUKVUSUOUXQVYJQZVUTWUKVU
        SWULVUTWUKQVUTVXIVURNZCWUFXHZQZVUSWULWUKWUNVUTUXQUXRVYJWUKWUNYAVUBUXRVY
        JQZWUKVXECWUFXHZWUNWUPVYRWUFVYSRWUKWUQYAWUBWUPWUFIVYSWUPCWUFIWUPUYHWUFN
        ZVXRVXJWUPWURUXSUYHEWTZVXRUXRVYJVXNWURWUSYAVXOUXSUYHTEIVWIXIXKUXSUYHEDW
        RZVXOXMXPVXTUVGYCZWUCVPCWUFVURVUCYDVKWUPVXEWUMCWUFWUPWURQZVXDVXIVURWVBV
        XJVXKWUPWURVXJWUPWUFIUYHWVAUVHWEVYCXOYNUVIUVJYOXAWULWUOVUSWULWUOQUXQVYK
        VUTVYHVURRZVUSUXQVYJWUOUVKVYJVYKUXQWUOVYJVYKVYIVYQUVLZUVMWULVUTWUNUVNWU
        LWUNWVCVUTWULWUNQUBVYHVURWULVVGVYHNZWUNVVHWULWVEQZVVGVYHWJZWUFNZWUNVVHU
        OWVFWVHUXSWVGEWTZVYJWVEWVIUXQVYJWVEQZVYJWVGINZWVIVYJWVEUVOWVJVYKWVEQZWV
        KVYJVYKWVEWVDUVPWVLVVGVYHIWTWVKVWQVWSWVLFGVVGVYHIVWTUXSXQUVQZVXBVYOVWNV
        YKVWRWVEVYPVWMVYHVVGXBXCLXDVVGVYHIXJXEWHWVIVYJWVKQWVGXQOZVYHRABUXSWVGEF
        GHIJKLMWUTVVGVYHXLZXRWVNVYHVVGVYHVWTWVMXSUVRUVSUWCUVTWULWVHWVIYAZWVEUXQ
        UXRVYJWVPVUBUXRVYJWVGTNWVPWVOUXSWVGTEIVWIXIXKYOYKYEWUMVVHCWVGWUFUYHWVGP
        ZVXIVVGVURWVQVXIWVGSOVVGUYHWVGSYBVVGVYHVWTUWAUWBYNUWDXOYPYCUWEUXQVYKVUT
        WVCWIVUSVYHVURHJKUWFWEUWGYMYQUWHYPYRUWIUWJYQYRUWKUWMUXQVUOUYBUWLNZUYBVF
        ZJVUCUFZVVEVVDYAVUQUXQUXRIVRVSZWVRVUBUXQVUSUAUJZWWAUXQHVRVSWWBHUWNUAHWG
        WHUXQVUSWWAUAUXQVUSWWAVVFVVIWWAVVMVVFVVHWWAUBUXQVUSVVHWWAVVNVWAWWAVXCIV
        VOUWOXOYIYLYJYMYLYJEIVWIUWPXGUXQWVTVUEVUEUXQVUEVUCIWKZVXDJNZCIXHCIJVUCU
        WQVYTWWCWUAITVUCWOVCWWDCIVXJVXDVXIJVYCVXJUYHUYRNVXIJNIUYRUYHVUNXNUYHJHU
        WRXOXTUWSUWTZVQUXQWVSIJVUCUXQUXRWVSIPVUBEIVWIUXDXOYSYECVURUYBTVUCJWVSWV
        SUXAUXBUXCUXEUXFWWEUXGUYFVUHDVUCTUXSVUCPZUXTVUEUYEVUGIJUXSVUCUXHWWFUYDV
        UFHUXSVUCUYCYBYTXCUXIUXJUYQUYGCEUDUYHEPZUYPUYFDWWGUYJUXTUYOUYEWWGUYIIJU
        XSWWGUYIVWFIUYHEUXKVWIUXLYSWWGUYNUYDHWWGUXSUYMUYCWWGUYLUYBJUIWWGUYKUYAU
        YHEUGYBUXMUXNUXOYTXCUXPYHXG $.
        $( [14-Dec-2013] $) $( [15-Dec-2009] $)
    $}

    $( A filter has the same convergence and clustering properties as some
       net.  (Contributed by Jeff Hankins, 12-Dec-2009.) $)
    filnet $p |- ( F e. Fil -> E. d e. DirRel E. f ( f : dom d --> X
       /\ F = ( ( X FilMap ran ( tail ` d ) ) ` f ) ) ) $=
      ( vx vy vn vm cv wcel wa copab c2nd cfv wss eqid filnetlem4 ) FGDAFJZHJZB
      KIJTKLIHMZKGJZUAKLUBNOSNOPLFGMZIHBUACEUAQUCQR $.
      $( [24-Nov-2013] $) $( [12-Dec-2009] $)
  $}

$( (End of Jeffrey Hankins' mathbox.) $)


