$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Directed sets, nets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d d x D $.  $d x X $.  $d x A $.
    tailfval.1 $e |- X = dom D $.
    $( The tail function for a directed set.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    tailfval $p |- ( D e. DirRel -> ( tail ` D ) =
                     ( x e. X |-> ( D " { x } ) ) ) $=
      ( vd cdir wcel ctail cfv cuni cv csn cima cmpt cvv wceq uniexg syl mptexg
      mpteq12dv unieq unieqd imaeq1 df-tail mpdan cdm dirdm syl5req eqidd eqtrd
      fvmptg ) BFGZBHIZABJZJZBAKLZMZNZACUQNULUROGZUMURPULUOOGZUSULUNOGUTBFQUNOQ
      RAUOUQOSREBAEKZJZJZVAUPMZNURFOHVABPZAVCVDUOUQVEVBUNVABUAUBVABUPUCTAEUDUKU
      EULAUOUQCUQULCBUFUODBUGUHULUQUITUJ $.
      $( [24-Nov-2013] $) $( [25-Nov-2009] $)

    $( The tail of an element in a directed set.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    tailval $p |- ( ( D e. DirRel /\ A e. X ) ->
                   ( ( tail ` D ) ` A ) = ( D " { A } ) ) $=
      ( vx cdir wcel wa ctail cfv csn cima cmpt wceq tailfval fveq1d adantr cvv
      cv id imaexg sneq imaeq2d eqid fvmptg syl2anr eqtrd ) BFGZACGZHABIJZJZAEC
      BESZKZLZMZJZBAKZLZUHUKUPNUIUHAUJUOEBCDOPQUIUIURRGUPURNUHUITBUQFUAEAUNURCR
      UOULANUMUQBULAUBUCUOUDUEUFUG $.
      $( [24-Nov-2013] $) $( [25-Nov-2009] $)

    $( An element of a tail.  (Contributed by Jeff Hankins, 25-Nov-2009.) $)
    eltail $p |- ( ( D e. DirRel /\ A e. X /\ B e. C ) ->
                   ( B e. ( ( tail ` D ) ` A ) <-> A D B ) ) $=
      ( cdir wcel w3a ctail cfv csn cima wbr wb wa tailval eleq2d 3adant3 cop
      elimasng df-br syl6bbr 3adant1 bitrd ) DGHZAEHZBCHZIBADJKKZHZBDALMZHZABDN
      ZUFUGUJULOUHUFUGPUIUKBADEFQRSUGUHULUMOUFUGUHPULABTDHUMDABECUAABDUBUCUDUE
      $.
      $( [24-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  ${
    $d s x y z D $.  $d s x y z X $.
    tailf.1 $e |- X = dom D $.
    $( The tail function of a directed set sends its elements to its subsets.
       (Contributed by Jeff Hankins, 25-Nov-2009.) $)
    tailf $p |- ( D e. DirRel -> ( tail ` D ) : X --> ~P X ) $=
      ( vx cdir wcel cpw ctail cfv wf cv csn cima cmpt wral wss cuni cvv mpbird
      sstri crn imassrn cdm cun ssun2 dmrnssfld dirdm syl5req syl5sseq wb dmexg
      syl5eqel elpw2g syl a1d ralrimiv eqid fmpt sylib tailfval feq1d ) AEFZBBG
      ZAHIZJBVCDBADKZLZMZNZJZVBVGVCFZDBOVIVBVJDBVBVJVEBFVBVJVGBPZVBAQQZVGBVGAUA
      ZVLAVFUBVMAUCZVMUDVLVMVNUEAUFTTVBBVNVLCAUGUHUIVBBRFVJVKUJVBBVNRCAEUKULVGB
      RUMUNSUOUPDBVCVGVHVHUQURUSVBBVCVDVHDABCUTVAS $.
      $( [24-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  ${
    tailini.1 $e |- X = dom D $.
    $( A tail contains its initial element.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    tailini $p |- ( ( D e. DirRel /\ A e. X ) -> A e. ( ( tail ` D ) ` A ) ) $=
      ( cdir wcel wa ctail cfv wbr dirref wb eltail 3anidm23 mpbird ) BEFZACFZG
      AABHIIFZAABJZABCDKPQRSLAACBCDMNO $.
      $( [25-Nov-2009] $)
  $}

  ${
    $d t x D $.  $d t x X $.
    tailuni.1 $e |- X = dom D $.
    $( The union of all the tails of a directed set.  (Contributed by Jeff
       Hankins, 13-Dec-2009.) $)
    tailuni $p |- ( D e. DirRel -> U. ran ( tail ` D ) = X ) $=
      ( vx vt cdir wcel ctail cfv crn cuni cv wss wral cpw wf wi syl sylibr wa
      tailf frn sseld vex elpw syl6ib ralrimiv unissb wex tailini wfn ffn sylan
      fnfvelrn fvex wceq eleq2 eleq1 anbi12d cla4ev syl2anc eluni ssrdv eqssd
      ex ) AFGZAHIZJZKZBVFDLZBMZDVHNVIBMVFVKDVHVFBBOZVGPZVJVHGZVKQABCUAZVMVNVJV
      LGVKVMVHVLVJBVLVGUBUCVJBDUDUEUFRUGDVHBUHSVFDBVIVFVJBGZVJVIGZVFVPTZVJELZGZ
      VSVHGZTZEUIZVQVRVJVJVGIZGZWDVHGZWCVJABCUJVFVGBUKZVPWFVFVMWGVOBVLVGULRBVJV
      GUNUMWBWEWFTEWDVJVGUOVSWDUPVTWEWAWFVSWDVJUQVSWDVHURUSUTVAEVJVHVBSVEVCVD
      $.
      $( [13-Dec-2009] $)
  $}

  ${
    $d u v w x y z D $.  $d u v w x y z X $.
    tailfb.1 $e |- X = dom D $.
    $( The collection of tails of a directed set is a filter base.
       (Contributed by Jeff Hankins, 25-Nov-2009.) $)
    tailfb $p |- ( ( D e. DirRel /\ X =/= (/) )
          -> ran ( tail ` D ) e. fBas ) $=
      ( vz vx vy vu vv vw wcel c0 wa cfv cv wss wrex wi wb wbr cvv cdir wne crn
      ctail wnel cin wral w3a cfbas wex n0 cpw wfn tailf ffn fnfvelrn 3syl ne0i
      wf ex syl6 exlimdv syl5bi imp wn tailini n0i nrexdv adantr fvelrnb mtbird
      wceq syl df-nel sylibr anbi12d dirge 3expb sylan ad2ant2r vex dirtr exp32
      reeanv mpan2 ad2ant2rl anim12d expr eltail mp3an3 adantrr adantrl 3imtr4d
      com23 impr elin syl6ibr ssrdv sseq1 rcla4ev syl2anc rexlimdv ineq1 sseq2d
      mpd rexbidv ineq2 sylan9bb syl5ibcom rexlimdvv sylbid ralrimivv 3jca fvex
      syl5bir rnex isfbas2 ax-mp ) AUAJZBKUBZLZAUDMZUCZKUBZKYCUEZDNZENZFNZUFZOZ
      DYCPZFYCUGEYCUGZUHZYCUIJZYAYDYEYLXSXTYDXTYGBJZEUJXSYDEBUKXSYOYDEXSYOYGYBM
      ZYCJZYDXSBBULZYBUSZYBBUMZYOYQQABCUNZBYRYBUOZYTYOYQBYGYBUPUTUQYCYPURVAVBVC
      VDYAKYCJZVEYEYAUUCYPKVLZEBPZXSUUEVEXTXSUUDEBXSYOLYGYPJUUDVEYGABCVFYPYGVGV
      MVHVIXSUUCUUERZXTXSYSYTUUFUUAUUBEBKYBVJUQVIVKKYCVNVOYAYKEFYCYCXSYGYCJZYHY
      CJZLZYKQXTXSUUIGNZYBMZYGVLZGBPZHNZYBMZYHVLZHBPZLZYKXSYSYTUUIUURRUUAUUBYTU
      UGUUMUUHUUQGBYGYBVJHBYHYBVJVPUQUURUULUUPLZHBPGBPXSYKUULUUPGHBBWDXSUUSYKGH
      BBXSUUJBJZUUNBJZLZUUSYKQXSUVBLZYFUUKUUOUFZOZDYCPZUUSYKUVCUUJINZASZUUNUVGA
      SZLZIBPZUVFXSUUTUVAUVKIUUJUUNABCVQVRUVCUVJUVFIBUVCUVGBJZUVJUVFUVCUVLUVJLZ
      LZUVGYBMZYCJZUVOUVDOZUVFXSUVLUVPUVBUVJXSYTUVLUVPXSYSYTUUAUUBVMBUVGYBUPVSV
      TUVNEUVOUVDUVNYGUVOJZYGUUKJZYGUUOJZLZYGUVDJUVNUVGYGASZUUJYGASZUUNYGASZLZU
      VRUWAUVCUVLUVJUWBUWEQUVCUVLLUWBUVJUWEUVCUVLUWBUVJUWEQUVCUVLUWBLLUVHUWCUVI
      UWDXSUWBUVHUWCQZUVBUVLXSUWBUWFXSUVHUWBUWCXSYGTJZUVHUWBUWCQQEWAZXSUWGLZUVH
      UWBUWCUUJUVGYGATWBWCWEWNVDWFXSUWBUVIUWDQZUVBUVLXSUWBUWJXSUVIUWBUWDXSUWGUV
      IUWBUWDQQUWHUWIUVIUWBUWDUUNUVGYGATWBWCWEWNVDWFWGWHWNWOXSUVLUVRUWBRZUVBUVJ
      XSUVLUWGUWKUWHUVGYGTABCWIWJVTUVCUWAUWERUVMUVCUVSUWCUVTUWDXSUUTUVSUWCRZUVA
      XSUUTUWGUWLUWHUUJYGTABCWIWJWKXSUVAUVTUWDRZUUTXSUVAUWGUWMUWHUUNYGTABCWIWJW
      LVPVIWMYGUUKUUOWPWQWRUVEUVQDUVOYCYFUVOUVDWSWTXAWCXBXEUULUVFYFYGUUOUFZOZDY
      CPUUPYKUULUVEUWODYCUULUVDUWNYFUUKYGUUOXCXDXFUUPUWOYJDYCUUPUWNYIYFUUOYHYGX
      GXDXFXHXIUTXJXOXKVIXLXMYCTJYNYMRYBAUDXNXPEFDTYCXQXRVO $.
      $( [25-Nov-2009] $)
  $}

  ${
    $d d f m n t v x y w z F $.  $d d f m n t w z X $.
    filnet.1 $e |- X = U. F $.
    ${
      $d t v w x y z H $.  $d t v w z D $.
      filnet.2 $e |- H = { <. m , n >. | ( n e. F /\ m e. n ) } $.
      filnet.3 $e |- D = { <. x , y >. |
        ( ( x e. H /\ y e. H ) /\ ( 2nd ` y ) C_ ( 2nd ` x ) ) } $.
      ${
        $d A x y $.  $d B x y $.
        filnetlem1.4 $e |- A e. _V $.
        filnetlem1.5 $e |- B e. _V $.
        $( Lemma for ~ filnet .  Change variables. $)
        filnetlem1 $p |- ( A D B <-> ( ( A e. H /\ B e. H ) /\
          ( 2nd ` B ) C_ ( 2nd ` A ) ) ) $=
          ( wcel wa c2nd cfv wss wceq eleq1 anbi1d sseq2d anbi12d anbi2d sseq1d
          cv fveq2 brab ) AUHZIPZBUHZIPZQZUMRSZUKRSZTZQCIPZUNQZUPCRSZTZQUSDIPZQ
          ZDRSZVATZQABCDENOUKCUAZUOUTURVBVGULUSUNUKCIUBUCVGUQVAUPUKCRUIUDUEUMDU
          AZUTVDVBVFVHUNVCUSUMDIUBUFVHUPVEVAUMDRUIUGUEMUJ $.
          $( [23-Nov-2013] $) $( [13-Dec-2009] $)
      $}

      $( Lemma for ~ filnet .  The field of the direction. $)
      filnetlem2 $p |- ( H = U. U. D /\ H = dom D ) $=
        ( vz cuni cv wcel wa c2nd cfv wss sstri wceq cdm crn cun wbr filnetlem1
        ssid mpbiran2 breldm sylbir anidms ssriv ssun1 dmrnssfld copab opabssxp
        vex cxp eqsstri uniss mp2b unixpss unidm eqssi dmss ax-mp dmxpid pm3.2i
        sseqtri ) GCMZMZUAGCUBZUAGVKGVLCUCZUDZVKGVLVNLGVLLNZGOZVOVLOZVPVPPZVOVO
        CUEZVQVSVRVOQRZVTSABVOVOCDEFGHIJKLUQZWAUFVTUGUHVOVOCWAUIUJUKULZVLVMUMTC
        UNTVKGGUDZGVKGGURZMZMZWCCWDSZVJWESVKWFSCANZGOBNZGOPWIQRWHQRSZPABUOWDKWJ
        ABGGUPUSZCWDUTVJWEUTVAGGVBTGVCVIVDGVLWBVLWDUBZGWGVLWLSWKCWDVEVFGVGVIVDV
        H $.
        $( [23-Nov-2013] $) $( [13-Dec-2009] $)

      $( Lemma for ~ filnet . $)
      filnetlem3 $p |- ( H C_ ( X X. F ) /\ ( F e. Fil -> D e. DirRel ) ) $=
        ( vv vw vz wss wcel wa wbr c2nd cvv vt cxp cfil cdir wi cv copab elunii
        cuni syl6eleqr simpl jca ssopab2i df-xp 3sstr4i wrel cid cres ccom ccnv
        ancoms wex wral cfv cin c0 c1st wceq eleq1 anbi2d eleq2 anbi12d elopabi
        wne eleq2s simpld fveq2 eleq1d vtoclga anim12i fipfil imp n0 w3a simp2l
        syl5 cop filint 3expb sylan2 3adant3 simp3 df-br fvex inex1 brab bitr3i
        sylanbrc op2nd inss1 syl5eqss opex filnetlem1 simp2r inss2 breq2 cla4ev
        vex a1i syl2anc 3expia exlimdv syl5bi mpd ralrimivva sylibr wal simplll
        codir simprlr sstr ad2ant2l jca31 anbi12i 3imtr4i gen2 cotr mpbir jctil
        ax-gen relopab releqi ssid mpbiran2 biimpri anidms pm3.2i xpexg sylancr
        ssexg rgen ref4w wb opabssxp eqsstri uniexg syl5eqel mpancom filnetlem2
        mpbi cdm simpli isdir syl mpbird ) GHFUBZOZFUCPZCUDPZUEEUFZFPZDUFZUUTPZ
        QZDEUGZUVBHPZUVAQZDEUGGUUPUVDUVGDEUVDUVFUVAUVDUVBFUIZHUVCUVAUVBUVHPUVBU
        UTFUHVAIUJUVAUVCUKULUMJDEHFUNUOZUURUUSCUPZUQGURCOZQZCCUSCOZGGUBZCUTCUSO
        ZQZQZUURUVPUVLUURUVOUVMUURUAUFZLUFZCRZMUFZUVSCRZQZLVBZMGVCUAGVCUVOUURUW
        DUAMGGUURUVRGPZUWAGPZQZQZUVRSVDZUWASVDZVEZVFVNZUWDUURUWGUWLUWGUWIFPZUWJ
        FPZQZUURUWLUWEUWMUWFUWNUWEUWMUVRVGVDZUWIPZUWMUWQQZUVRUVEGUVDUVAUWPUUTPZ
        QUWRDEUVRUVBUWPVHUVCUWSUVAUVBUWPUUTVIVJUUTUWIVHUVAUWMUWSUWQUUTUWIFVIUUT
        UWIUWPVKVLVMJVOVPZUWMUWNUAUWAGUVRUWAVHUWIUWJFUVRUWASVQVRUWTVSVTZUWIUWJF
        WAWFWBUWLNUFZUWKPZNVBUWHUWDNUWKWCUWHUXCUWDNUURUWGUXCUWDUURUWGUXCWDZUVRU
        XBUWKWGZCRZUWAUXECRZUWDUXDUWEUXEGPZQUXESVDZUWIOUXFUXDUWEUXHUURUWEUWFUXC
        WEUXDUWKFPZUXCUXHUURUWGUXJUXCUWGUURUWOUXJUXAUURUWMUWNUXJUWIUWJFWHWIWJWK
        UURUWGUXCWLUXHUXBUWKGRUXJUXCQZUXBUWKGWMUVDUVAUXBUUTPZQUXKDEUXBUWKGNXHZU
        WIUWJUVRSWNWOZUVBUXBVHUVCUXLUVAUVBUXBUUTVIVJUUTUWKVHUVAUXJUXLUXCUUTUWKF
        VIUUTUWKUXBVKVLJWPWQWRZULUXDUXIUWKUWIUXBUWKUXMUXNWSZUWKUWIOUXDUWIUWJWTX
        IXAABUVRUXECDEFGHIJKUAXHUXBUWKXBZXCWRUXDUWFUXHQUXIUWJOUXGUXDUWFUXHUURUW
        EUWFUXCXDUXOULUXDUXIUWKUWJUXPUWKUWJOUXDUWIUWJXEXIXAABUWAUXECDEFGHIJKMXH
        ZUXQXCWRUWCUXFUXGQLUXEUXQUVSUXEVHUVTUXFUWBUXGUVSUXEUVRCXFUVSUXEUWACXFVL
        XGXJXKXLXMXNXOUAMLGGCXSXPUVMUVSUWACRZUWAUXBCRZQZUVSUXBCRZUEZNXQZMXQLXQU
        YDLMUYCNUVSGPZUWFQZUWJUVSSVDZOZQZUWFUXBGPZQZUXBSVDZUWJOZQZQZUYEUYJQUYLU
        YGOZQUYAUYBUYOUYEUYJUYPUYEUWFUYHUYNXRUYIUWFUYJUYMXTUYHUYMUYPUYFUYKUYMUY
        HUYPUYLUWJUYGYAVAYBYCUXSUYIUXTUYNABUVSUWACDEFGHIJKLXHZUXRXCABUWAUXBCDEF
        GHIJKUXRUXMXCYDABUVSUXBCDEFGHIJKUYQUXMXCYEYJYFLMNCYGYHYIUVJUVKUVJAUFZGP
        BUFZGPQUYSSVDUYRSVDOZQZABUGZUPVUAABYKCVUBKYLYHUXBUXBCRZNGVCUVKVUCNGUYJV
        UCVUCUYJUYJQZVUCVUDUYLUYLOABUXBUXBCDEFGHIJKUXMUXMXCUYLYMYNYOYPUUANGCUUB
        UUJYQYIUURCTPZUUSUVQUUCUURCUVNOUVNTPZVUECVUBUVNKUYTABGGUUDUUEUURGTPZVUG
        VUFUURUUQUUPTPZVUGUVIHTPUURVUHUURHUVHTIFUCUUFUUGHFTUCYRUUHGUUPTYTYSZVUI
        GGTTYRXJCUVNTYTYSGCTGCUIUIVHGCUUKVHABCDEFGHIJKUUIUULUUMUUNUUOYQ $.
        $( [24-Nov-2013] $) $( [13-Dec-2009] $)

      $( Lemma for ~ filnet . $)
      filnetlem4 $p |- ( F e. Fil -> E. z e. DirRel E. w ( w : dom z --> X
       /\ F = ( ( X FilMap ran ( tail ` z ) ) ` w ) ) ) $=
        ( wcel cfv wceq wa wss c1st cvv vt vv cfil cdir cv wf ctail crn cfm wex
        co cdm wrex cxp wi filnetlem3 simpri cres wfun wfo fo1st fof ax-mp ffun
        simpli cuni syl5eqel xpexg mpancom ssexg sylancr resfunexg cima elssuni
        uniexg syl6sseqr a1i c0 wne filesn eleq1 notbid syl5ibrcom necon2ad imp
        wn n0 sylib w3a cop wfn cpw filnetlem2 ffn 3syl 3ad2ant1 3simpc wbr vex
        tailf anbi2d eleq2 anbi12d brab df-br bitr3i fnfvelrn syl2anc wb eltail
        wral mp3an3 opex brelrn sseli syl syl6bi filnetlem1 op2nd fveq2 eqeltrd
        c2nd ssrdv funimass4 mpbird imaeq2 sseq1d rcla4ev 3expia exlimdv mpd ex
        adantr eleq1d sylan impancom sylbid rexlimdv feq2d eqeq2d cun dmrnssfld
        ssun2 sstri sseqtr4i fvres biimpa simprbi syl6sseq eleq12d copab eleq2i
        elopabi sylbi simprd vtoclga sseldd ralrimiva ssv fssres mp2an fdm jcad
        fvelrnb biimprcd reximdv syl6 sseld ralbidva bitrd simpll simpld simprl
        ad2antlr simpl anim1i fvex eqimssi mpbiran2 adantll op1st syl6eq rcla4v
        sylanbrc adantrl fillsb syl13anc exp3a syl9r com23 expimpd impbid cfbas
        emnfil ne0i tailfb ffnfv rgen mpbir2an tailuni eqid elfm syl3anc bitr4d
        xp1st eqrdv jctil feq1 cla4egv sylc syl6eqr rneqd opreq2d fveq1d exbidv
        dmeq ) HUCNZEUDNZIJDUEZUFZHUXSJEUGOZUHZUIUKZOZPZQZDUJZCUEZULZJUXSUFZHUX
        SJUYHUGOZUHZUIUKZOZPZQZDUJZCUDUMIJHUNZRZUXQUXRUOZABEFGHIJKLMUPZUQZUXQSI
        URZTNZIJVUCUFZHVUCUYCOZPZQZUYGUXQSUSZITNZVUDTTSUFZVUITTSUTVUKVATTSVBVCZ
        TTSVDVCUXQUYSUYRTNZVUJUYSUYTVUAVEZJTNZUXQVUMUXQJHVFZTKHUCVOVGZJHTUCVHVI
        IUYRTVJVKSITVLVKUXQVUGVUEUXQUAHVUFUXQUAUEZHNZVURJRZVUCUYHVMZVURRZCUYBUM
        ZQZVURVUFNZUXQVUSVVDUXQVUSVUTVVCVUSVUTUOUXQVUSVURVUPJVURHVNKVPVQUXQVUSV
        VCUXQVUSQZUBUEZVURNZUBUJZVVCVVFVURVRVSZVVIUXQVUSVVJUXQVUSVURVRUXQVUSWFV
        URVRPZVRHNZWFHVTVVKVUSVVLVURVRHWAWBWCWDWEUBVURWGWHZVVFVVHVVCUBUXQVUSVVH
        VVCUXQVUSVVHWIZVVGVURWJZUYAOZUYBNZVUCVVPVMZVURRZVVCVVNUYAIWKZVVOINZVVQU
        XQVUSVVTVVHUXQUXRIIWLZUYAUFZVVTVUBEIIEVFVFZPZIEULZPZABEFGHIJKLMWMZUQZWT
        ZIVWBUYAWNZWOWPVVNVUSVVHQZVWAUXQVUSVVHWQVWLVVGVURIWRVWAGUEZHNZFUEZVWMNZ
        QZVWNVVGVWMNZQZVWLFGVVGVURIUBWSZUAWSZVWOVVGPVWPVWRVWNVWOVVGVWMWAXAZVWMV
        URPVWNVUSVWRVVHVWMVURHWAVWMVURVVGXBXCLXDVVGVURIXEXFWHZIVVOUYAXGXHVVNVVS
        UYHVUCOZVURNZCVVPXKZVVNVXECVVPVVNUYHVVPNZQZVXDUYHSOZVURVXHUYHINZVXDVXIP
        ZVVNVXGVXJVVNVXGVVOUYHEWRZVXJVVNUXRVWAVXGVXLXIZUXQVUSUXRVVHVUBWPVXCUXRV
        WAUYHTNZVXMCWSZVVOUYHTEIVWIXJXLXHZVXLUYHEUHZNZVXJVVOUYHEVVGVURXMZVXOXNV
        XQIUYHVXQVWDIVXQVWFVXQUUAVWDVXQVWFUUCEUUBUUDVWEVWGVWHVEUUEXOZXPXQZWEZUY
        HISUUFZXPVXHUYHYBOZVURVXIVXHVXLVYDVURRVVNVXGVXLVXPUUGVXLVYDVVOYBOZVURVX
        LVWAVXJQVYDVYERABVVOUYHEFGHIJKLMVXSVXOXRUUHVVGVURVWTVXAXSUUIXPVXHVXJVXI
        VYDNZVYBUXSSOZUXSYBOZNZVYFDUYHIUXSUYHPVYGVXIVYHVYDUXSUYHSXTUXSUYHYBXTUU
        JUXSINZVYHHNZVYIVYJUXSVWQFGUUKZNVYKVYIQZIVYLUXSLUULVWQVWNVYGVWMNZQVYMFG
        UXSVWOVYGPVWPVYNVWNVWOVYGVWMWAXAVWMVYHPZVWNVYKVYNVYIVWMVYHHWAZVWMVYHVYG
        XBXCUUMUUNZUUOUUPXPUUQYAUURVVNVUCUSZVVPVUCULZRVVSVXFXIITVUCUFZVYRVUKITR
        VYTVULIUUSTTISUUTUVAZITVUCVDVCZVVNVVPIVYSVVNCVVPIVYAYCVYTVYSIPWUAITVUCU
        VBVCZVPCVVPVURVUCYDVKYEVVBVVSCVVPUYBUYHVVPPVVAVVRVURUYHVVPVUCYFYGYHXHYI
        YJYKYLUVCUXQVUTVVCVUSUXQVUTQZVVBVUSCUYBWUDUYHUYBNZUXSUYAOZUYHPZDIUMZVVB
        VUSUOWUDUXRWUEWUHXIZUXQUXRVUTVUBYMUXRVWCVVTWUIVWJVWKDIUYHUYAUVDWOXPWUDV
        VBWUHVUSVVBWUHVUCWUFVMZVURRZDIUMWUDVUSVVBWUGWUKDIWUGWUKVVBWUGWUJVVAVURW
        UFUYHVUCYFYGUVEUVFWUDWUKVUSDIUXQVYJVUTWUKVUSUOUXQVYJQZVUTWUKVUSWULVUTWU
        KQVUTVXIVURNZCWUFXKZQZVUSWULWUKWUNVUTUXQUXRVYJWUKWUNXIVUBUXRVYJQZWUKVXE
        CWUFXKZWUNWUPVYRWUFVYSRWUKWUQXIWUBWUPWUFIVYSWUPCWUFIWUPUYHWUFNZVXRVXJWU
        PWURUXSUYHEWRZVXRUXRVYJVXNWURWUSXIVXOUXSUYHTEIVWIXJXLUXSUYHEDWSZVXOXNXQ
        VXTUVGYCZWUCVPCWUFVURVUCYDVKWUPVXEWUMCWUFWUPWURQZVXDVXIVURWVBVXJVXKWUPW
        URVXJWUPWUFIUYHWVAUVHWEVYCXPYNUVIUVJYOXAWULWUOVUSWULWUOQUXQVYKVUTVYHVUR
        RZVUSUXQVYJWUOUVKVYJVYKUXQWUOVYJVYKVYIVYQUVLZUVNWULVUTWUNUVMWULWUNWVCVU
        TWULWUNQUBVYHVURWULVVGVYHNZWUNVVHWULWVEQZVVGVYHWJZWUFNZWUNVVHUOWVFWVHUX
        SWVGEWRZVYJWVEWVIUXQVYJWVEQZVYJWVGINZWVIVYJWVEUVOWVJVYKWVEQZWVKVYJVYKWV
        EWVDUVPWVLVVGVYHIWRWVKVWQVWSWVLFGVVGVYHIVWTUXSYBUVQZVXBVYOVWNVYKVWRWVEV
        YPVWMVYHVVGXBXCLXDVVGVYHIXEXFWHWVIVYJWVKQWVGYBOZVYHRABUXSWVGEFGHIJKLMWU
        TVVGVYHXMZXRWVNVYHVVGVYHVWTWVMXSUVRUVSUWDUVTWULWVHWVIXIZWVEUXQUXRVYJWVP
        VUBUXRVYJWVGTNWVPWVOUXSWVGTEIVWIXJXLYOYMYEWUMVVHCWVGWUFUYHWVGPZVXIVVGVU
        RWVQVXIWVGSOVVGUYHWVGSXTVVGVYHVWTUWAUWBYNUWCXPYPYCUWEUXQVYKVUTWVCWIVUSV
        YHVURHJKUWFWEUWGYLYQUWHYPYRUWIUWJYQYRUWKUWLUXQVUOUYBUWMNZUYBVFZJVUCUFZV
        VEVVDXIVUQUXQUXRIVRVSZWVRVUBUXQVUSUAUJZWWAUXQHVRVSWWBHUWNUAHWGWHUXQVUSW
        WAUAUXQVUSWWAVVFVVIWWAVVMVVFVVHWWAUBUXQVUSVVHWWAVVNVWAWWAVXCIVVOUWOXPYI
        YJYKYLYJYKEIVWIUWPXHUXQWVTVUEVUEUXQVUEVUCIWKZVXDJNZCIXKCIJVUCUWQVYTWWCW
        UAITVUCWNVCWWDCIVXJVXDVXIJVYCVXJUYHUYRNVXIJNIUYRUYHVUNXOUYHJHUXEXPYAUWR
        UWSZVQUXQWVSIJVUCUXQUXRWVSIPVUBEIVWIUWTXPYSYECVURUYBTVUCJWVSWVSUXAUXBUX
        CUXDUXFWWEUXGUYFVUHDVUCTUXSVUCPZUXTVUEUYEVUGIJUXSVUCUXHWWFUYDVUFHUXSVUC
        UYCXTYTXCUXIUXJUYQUYGCEUDUYHEPZUYPUYFDWWGUYJUXTUYOUYEWWGUYIIJUXSWWGUYIV
        WFIUYHEUXPVWIUXKYSWWGUYNUYDHWWGUXSUYMUYCWWGUYLUYBJUIWWGUYKUYAUYHEUGXTUX
        LUXMUXNYTXCUXOYHXH $.
        $( [14-Dec-2013] $) $( [15-Dec-2009] $)
    $}

    $( A filter has the same convergence and clustering properties as some
       net.  (Contributed by Jeff Hankins, 12-Dec-2009.) $)
    filnet $p |- ( F e. Fil -> E. d e. DirRel E. f ( f : dom d --> X
       /\ F = ( ( X FilMap ran ( tail ` d ) ) ` f ) ) ) $=
      ( vx vy vn vm cv wcel wa copab c2nd cfv wss eqid filnetlem4 ) FGDAFJZHJZB
      KIJTKLIHMZKGJZUAKLUBNOSNOPLFGMZIHBUACEUAQUCQR $.
      $( [24-Nov-2013] $) $( [12-Dec-2009] $)
  $}

$( (End of Jeffrey Hankins' mathbox.) $)


