$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Filter bases
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c fBas $.
  $( Extend class definition to include the class of filter bases. $)
  cfbas $a class fBas $.

  $c filGen $.
  $( Extend class definition to include the filter generating function. $)
  cfg $a class filGen $.

  ${
    $d x y z $.
    $( Define the class of all filter bases. $)
    df-fbas $a |- fBas = { x | ( x =/= (/) /\ (/) e/ x /\ A. y e. x A. z e. x (
    x i^i P~ ( y i^i z ) ) =/= (/) ) } $.
  $}

  ${
    $d x y z $.
    $( Define the filter generating function. $)
    df-fg $a |- filGen = { <. x , y >. | ( x e. fBas /\ y = { z e. P~ U. x | (
    x i^i P~ z ) =/= (/) } ) } $.
  $}

  ${
    $d x y z F $.
    $( The predicate " ` F ` is a filter base." Note that some authors require
    filter bases to be closed under pairwise intersections, but that is not
    necessary under our definition. One advantage of this definition is that
    tails in a directed set form a filter base under our meaning. $)
    isfbas $p |- ( F e. A -> ( F e. fBas <-> ( F =/= (/) /\ (/) e/ F /\ A. x e.
    F A. y e. F ( F i^i P~ ( x i^i y ) ) =/= (/) ) ) ) $=
      ( vz cv c0 wne wnel cin cpw wral w3a cfbas wceq neeq1 neleq2 ineq1
      neeq1d raleqd 3anbi123d df-fbas elab2g ) EFZGHZGUDIZUDAFBFJKZJZGHZBUDLZAU
      DLZMDGHZGDIZDUGJZGHZBDLZADLZMEDNCUDDOZUEULUFUMUKUQUDDGPUDDGQUJUPAUDDUIUOB
      UDDURUHUNGUDDUGRSTTUAEABUBUC $.
      $( [1-Sep-2009] $) $( [1-Sep-2009] $)
  $}

  ${
    $d x y z F $.
    $( The predicate " ` F ` is a filter base." $)
    isfbas2 $p |- ( F e. A -> ( F e. fBas <-> ( F =/= (/) /\ (/) e/ F /\ A. x
    e. F A. y e. F E. z e. F z (_ ( x i^i y ) ) ) ) $=
      ( wcel cfbas c0 wne wnel cv cin cpw wral w3a wss wrex isfbas wex wa elin
      visset elpw anbi2i bitri exbii n0 df-rex 3bitr4i 2ralbii 3anbi3i syl6bb
      ) EDFEGFEHIZHEJZEAKBKLZMZLZHIZBENAENZOUMUNCKZUOPZCEQZBENAENZOABDERUSVCUMU
      NURVBABEEUTUQFZCSUTEFZVATZCSURVBVDVFCVDVEUTUPFZTVFUTEUPUAVGVAVEUTUOCUBUCU
      DUEUFCUQUGVACEUHUIUJUKUL $.
      $( [1-Sep-2009] $) $( [1-Sep-2009] $)
  $}

  ${
    $d x y F $.
    $( There are no empty filter bases. $)
    fbasne0 $p |- ( F e. fBas -> F =/= (/) ) $=
      ( vx vy cfbas wcel c0 wne wnel cv cin cpw wral w3a isfbas ibi 3simp1d )
      ADEZAFGZFAHZABICIJKJFGCALBALZQRSTMBCDANOP $.
      $( [1-Sep-2009] $) $( [1-Sep-2009] $)
  $}

  ${
    $d x y F $.
    $( No filter base contains the empty set. $)
    0nelfb $p |- ( F e. fBas -> (/) e/ F ) $=
      ( vx vy cfbas wcel c0 wne wnel cv cin cpw wral w3a isfbas ibi 3simp2d )
      ADEZAFGZFAHZABICIJKJFGCALBALZQRSTMBCDANOP $.
      $( [1-Sep-2009] $) $( [1-Sep-2009] $)
  $}

  ${
    $d x y z A $. $d x y z B $. $d x y z F $.
    $( A filter base contains subsets of its pairwise intersections. $)
    fbasssin $p |- ( ( F e. fBas /\ A e. F /\ B e. F ) -> E. x e. F x (_ ( A
    i^i B ) ) $=
      ( vy vz cfbas wcel cv cin wss wrex wa wral wceq ineq1 sseq2d rexbidv
      ineq2 rcla42v c0 wne wnel w3a isfbas2 ibi 3simp3d syl5com 3impib ) DGHZBD
      HZCDHZAIZBCJZKZADLZUKULMUMEIZFIZJZKZADLZFDNEDNZUPUJVAUPUMBURJZKZADLEFBCDD
      UQBOZUTVDADVEUSVCUMUQBURPQRURCOZVDUOADVFVCUNUMURCBSQRTUJDUAUBZUADUCZVBUJV
      GVHVBUDEFAGDUEUFUGUHUI $.
      $( [1-Sep-2009] $) $( [1-Sep-2009] $)
  $}

  ${
    $d f g k n t x y z A $. $d f g k n t x y z F $.
    $( A filter base contains subsets of its finite intersections. $)
    fbssint $p |- ( ( F e. fBas /\ A (_ F /\ A e. Fin ) ->
    E. x e. F x (_ |^| A ) $=
      ( vn vf vy vk vt vz vg cfbas wcel wss cfn cv cint wrex wa cen wbr com wi
      isfi cvv relen brrelexi wf1o wex visset bren wal wceq f1oeq2 exbidv
      sseq1 anbi2d inteq sseq2d rexbidv imbi12d cla4gv c0 csuc f1oeq3 imbi1d
      albidv wne ccnv f1ocnv f1o00 pm3.27bi ssv int0 sseqtr4i mpbiri 3syl a1d
      ancld 19.22dv n0 df-rex 3imtr4g fbasne0 syl5 adantrd 19.23aiv ax-gen
      cima cres wf1 f1ores f1of1 sssucid a1i sylanc adantl sylan2 cnvex resexg
      ax-mp f1oeq1 cla4ev pm2.27 simprl sstr cdm cnvimass wfn wb f1ofn fndm
      sseq2 mpbii sylan ad2ant2l cbvrexv cfv cin fbasssin simprrl simpll
      simprrr wf f1of sucid ffvelrn mpan2 ad2antlr sseldd syl3anc sstr2 ssrin
      syl5com r19.22sdv mpd csn cun intun fnsnfv ad2antrl uneq2d inteqd fvex
      intsn ineq2i 3eqtr3rd wfo f1ofo foima df-suc imaeq2i imaundi eqtri
      syl5eqr eqtrd mpbid exp31 r19.23aiv sylbi syl6 com3r mp2and ex imaexg
      cla4v com23 imp 19.23adv 19.21adv cbvalv cbvexv imbi1i albii bitri
      syl5ib finds com12 3impia ) CKLZBCMZBNLZAOZBPZMZACQZUWAUVSUVTRZUWEUWABDOZ
      STZDUAQUWFUWEUBZDBUCUWHUWIDUAUWHUWGUALZUWIUWHBUDLZUWJUWIUBZBUWGSUEUFUWHBU
      WGEOZUGZEUHZUWKUWLUBBUWGEDUIUJUWKUWJUWOUWIUWKFOZUWGUWMUGZEUHZUVSUWPCMZRZU
      WBUWPPZMZACQZUBZUBZFUKZUWOUWIUBZUWJUXEUXGFBUDUWPBULZUWRUWOUXDUWIUXHUWQUWN
      EUWPBUWGUWMUMUNUXHUWTUWFUXCUWEUXHUWSUVTUVSUWPBCUOUPUXHUXBUWDACUXHUXAUWCUW
      BUWPBUQURUSUTUTVAUWPGOZUWMUGZEUHZUXDUBZFUKUWPVBUWMUGZEUHZUXDUBZFUKUWPHOZU
      WMUGZEUHZUXDUBZFUKZUWPUXPVCZUWMUGZEUHZUXDUBZFUKZUXFGHUWGUXIVBULZUXLUXOFUY
      FUXKUXNUXDUYFUXJUXMEUXIVBUWPUWMVDUNVEVFUXIUXPULZUXLUXSFUYGUXKUXRUXDUYGUXJ
      UXQEUXIUXPUWPUWMVDUNVEVFUXIUYAULZUXLUYDFUYHUXKUYCUXDUYHUXJUYBEUXIUYAUWPUW
      MVDUNVEVFUXIUWGULZUXLUXEFUYIUXKUWRUXDUYIUXJUWQEUXIUWGUWPUWMVDUNVEVFUXOFUX
      MUXDEUXMUVSUXCUWSUXMCVBVGZUXCUVSUXMUWBCLZAUHUYKUXBRZAUHUYJUXCUXMUYKUYLAUX
      MUYKUXBUXMUXBUYKUXMVBUWPUWMVHZUGZUWPVBULZUXBUWPVBUWMVIUYNUYMVBULUYOUWPUYM
      VJVKUYOUXBUWBVBPZMUWBUDUYPUWBVLVMVNUYOUXAUYPUWBUWPVBUQURVOVPVQVRVSACVTUXB
      ACWAWBCWCWDWEWFWGUXPUALZIOZUXPJOZUGZJUHZUVSUYRCMZRZUWBUYRPZMZACQZUBZUBZIU
      KZUYEUXTUYQVUIUYDFUYQVUIUYDUYQVUIRUYBUXDEUYQVUIUYBUXDUBUYQUYBVUIUXDUYQUYB
      VUIUXDUBUYQUYBRZUYMUXPWHZUXPUYSUGZJUHZUVSVUKCMZRZUWBVUKPZMZACQZUBZUBZUXDV
      UIVUJVUMVUTUXDUBVUJUXPVUKUYMUXPWIZUGZVUKUXPVVAVHZUGZVUMUYQUYAUWPUYMUGZVVB
      UYBVVEVVBUYQUYAUWPUYMWJUXPUYAMZVVBVVEUYAUWPUXPUYMWKUYAUWPUYMWLVVFVVEUXPWM
      WNWOWPUWPUYAUWMVIZWQUXPVUKVVAVIVULVVDJVVCVVAUYMUDLZVVAUDLUWMEUIWRZUYMUXPU
      DWSWTWRVUKUXPUYSVVCXAXBVPVUMVUTVUJUXDVUMVUTVUSVUJUXDUBVUMVUSXCVUJUWTVUSUX
      CVUJUWTVUSUXCUBZVUJUWTRZUVSVUNVVJVUJUVSUWSXDUYBUWSVUNUYQUVSVUKUWPMZUWSVUN
      UYBVUKUWPCXEUYBVUKUWMXFZMZVVLUWMUXPXGUYBUWMUWPXHVVMUWPULVVNVVLXIUWPUYAUWM
      XJUWPUWMXKVVMUWPVUKXLVPXMXNXOVUOVUSVVKUXCVUOVUSVURVVKUXCUBZVUOVURXCVURUYR
      VUPMZICQVVOVUQVVPAICUWBUYRVUPUOXPVVPVVOICUYRCLZVVPVVKUXCVVQVVPRZVVKRZUWBV
      UPUXPUYMXQZXRZMZACQZUXCVVSUWBUYRVVTXRZMZACQZVWCUVSVVQVVTCLVWFVVSAUYRVVTCX
      SVVRVUJUVSUWSXTVVQVVPVVKYAVVSUWPCVVTVVRVUJUVSUWSYBVVKVVTUWPLZVVRUYBVWGUYQ
      UWTUYBVVEUYAUWPUYMYCZVWGVVGUYAUWPUYMYDVWHUXPUYALZVWGUXPHUIYEZUYAUWPUXPUYM
      YFYGVPYHWPYIYJVVSVWEVWBACVVPVWEVWBUBVVQVVKVWEVWDVWAMVWBVVPUWBVWDVWAYKUYRV
      UPVVTYLYMYHYNYOVVSVWBUXBACVVSVWAUXAUWBVVSVWAVUKUYMUXPYPZWHZYQZPZUXAVVSVUK
      VVTYPZYQZPZVUPVWOPZXRZVWNVWAVWQVWSULVVSVUKVWOYRWNVVSVWPVWMVVSVWOVWLVUKVUJ
      VWOVWLULZVVRUWTUYBVWTUYQUYBVVEUYMUYAXHZVWTVVGUYAUWPUYMXJVXAVWIVWTVWJUYAUX
      PUYMYSYGVPWPYTUUAUUBVWSVWAULVVSVWRVVTVUPVVTUXPUYMUUCUUDUUEWNUUFVVSVWMUWPV
      VSUYMUYAWHZUWPVWMVVKVXBUWPULZVVRUYBVXCUYQUWTUYBVVEUYAUWPUYMUUGVXCVVGUYAUW
      PUYMUUHUYAUWPUYMUUIVPYHWPVXBUYMUXPVWKYQZWHVWMUYAVXDUYMUXPUUJUUKUYMUXPVWKU
      ULUUMUUNUUBUUOURUSUUPUUQUURUUSUUTUVAUVBUVCUVAUUTUVAYOVUHVUTIVUKVVHVUKUDLV
      VIUYMUXPUDUVDWTUYRVUKULZVUAVUMVUGVUSVXEUYTVULJUYRVUKUXPUYSUMUNVXEVUCVUOVU
      FVURVXEVUBVUNUVSUYRVUKCUOUPVXEVUEVUQACVXEVUDVUPUWBUYRVUKUQURUSUTUTUVEWDUV
      CUVFUVGUVHUVCUVIUXTUYRUXPUWMUGZEUHZVUGUBZIUKVUIUXSVXHFIUWPUYRULZUXRVXGUXD
      VUGVXIUXQVXFEUWPUYRUXPUWMUMUNVXIUWTVUCUXCVUFVXIUWSVUBUVSUWPUYRCUOUPVXIUXB
      VUEACVXIUXAVUDUWBUWPUYRUQURUSUTUTUVJVXHVUHIVXGVUAVUGVXFUYTEJUYRUXPUWMUYSX
      AUVKUVLUVMUVNUVOUVPWDUVAUUSYOUVQUURUUSUVQUVR $.
      $( [1-Sep-2009] $) $( [1-Sep-2009] $)
  $}

  ${
    $d x y z F $.
    $( A filter base has the finite intersection property. $)
    fbasfip $p |- ( F e. fBas -> (/) e/ ( fi ` F ) ) $=
      ( vy vz vx cfbas wcel c0 cfi cfv wn wnel cv wss cfn cint wceq w3a wex wa
      wi wne wrex fbssint 3expa adantrr 0nelfb df-nel eleq1 notbid biimprcd
      sylbi syl necon2ad ssn0 expcom syl6 ad2antrr r19.23adv mpd necom df-ne
      bitri sylib expr syl5ibr cvv vn0 mpbi int0 eqtr mpan2 mto inteq eqeq2d
      mtbiri pm2.61d2 expl imnan df-3an notbii bitr4i nexdv cab fiv eleq2d 0ex
      eqeq1 3anbi3d exbidv elab syl6bb mtbird sylibr ) AEFZGAHIZFZJGWOKWNWPBLZA
      MZWQNFZGWQOZPZQZBRZWNXBBWNWRWSSZXAJZTZXBJZWNWRWSXEWNWRSZWSSZWQGPZXEXIWQGU
      AZXEXJJXHWSXKXEXHWSXKSZSZWTGUAZXEXMCLZWTMZCAUBZXNXHWSXQXKWNWRWSXQCWQAUCUD
      UEXMXPXNCAWNXOAFZXPXNTZTWRXLWNXRXOGUAZXSWNXRXOGWNGAKZXOGPZXRJZTZAUFYAGAFZ
      JZYDGAUGYBYCYFYBXRYEXOGAUHUIUJUKULUMXPXTXNXOWTUNUOUPUQURUSXNGWTUAXEWTGUTG
      WTVAVBVCVDWQGVAVEXJXAGGOZPZYHGVFPZVFGUAZYIJZVGYJGVFUAYKVFGUTGVFVAVBVHYHYG
      VFPYIVIGYGVFVJVKVLXJWTYGGWQGVMVNVOVPVQXFXDXASZJXGXDXAVRXBYLWRWSXAVSVTWAVC
      WBWNWPGWRWSDLZWTPZQZBRZDWCZFXCWNWOYQGBDAEWDWEYPXCDGWFYMGPZYOXBBYRYNXAWRWS
      YMGWTWGWHWIWJWKWLGWOUGWM $.
      $( [2-Sep-2009] $) $( [2-Sep-2009] $)
  $}

  ${
    $d x y z F $.
    $( A filter is a filter base. $)
    filfbas $p |- ( F e. Fil -> F e. fBas ) $=
      ( vz vx vy cfil wcel cfbas c0 wne wnel cv cin wss wrex wral w3a cuni
      eqid filusb ne0i syl wn filesn df-nel sylibr sseq1 rcla4ev filint ssid
      a1i sylanc 3expib r19.21aivv 3jca isfbas2 mpbird ) AEFZAGFAHIZHAJZBKZCKZD
      KZLZMZBANZDAOCAOZPUQURUSVFUQAQZAFURAVGVGRSAVGTUAUQHAFUBUSAUCHAUDUEUQVECDA
      AUQVAAFZVBAFZVEVCAFVCVCMZVEUQVHVIPZVDVJBVCAUTVCVCUFUGVAVBAUHVJVKVCUIUJUKU
      LUMUNCDBEAUOUP $.
      $( [2-Sep-2009] $) $( [2-Sep-2009] $)
  $}

  ${
    $d r s t x J $. $d r s t x S $. $d r s t x X $.
    opnfbas.1 $e |- X = U. J $.
  $( The collection of open supersets of a nonempty set in a topology is a
  filter base. The filter generated by this base is the collection of
  neighborhoods of the set, one of the motivations for the filter concept. $)
  opnfbas $p |- ( ( J e. Top /\ S (_ X /\ S =/= (/) ) -> { x e. J | S (_ x } e.
  fBas ) $=
    ( vt vr vs ctop wcel wss c0 wne w3a cv crab cfbas wnel cin wrex wral wa
    topopn anim1i 3adant3 sseq2 elrab sylibr ne0i syl wn ss0 necon3ai 3ad2ant3
    intnand df-nel notbii bitr2i sylib sseq1 rcla4ev inopn pm3.26 simprll
    simprrl syl3anc ssin biimpi ad2ant2l adantl jca 3ad2antl1 ssid a1i sylanc
    ex anbi12i syl5ib r19.21aivv 3jca wb cvv rabexg isfbas2 3ad2ant1 mpbird )
    CIJZBDKZBLMZNZBAOZKZACPZQJZWMLMZLWMRZFOZGOZHOZSZKZFWMTZHWMUAGWMUAZNZWJWOWPX
    CWJDWMJZWOWJDCJZWHUBZXEWGWHXGWIWGXFWHCDEUCUDUEWLWHADCWKDBUFUGUHWMDUIUJWJLCJ
    ZBLKZUBZUKZWPWJXIXHWIWGXIUKWHXIBLBULUMUNUOWPLWMJZUKXKLWMUPXLXJWLXIALCWKLBUF
    UGUQURUSWJXBGHWMWMWJWRCJZBWRKZUBZWSCJZBWSKZUBZUBZXBWRWMJZWSWMJZUBWJXSXBWTWM
    JZWTWTKZXBWJXSUBZXAYCFWTWMWQWTWTUTVAYDWTCJZBWTKZUBZYBWGWHXSYGWIWGXSUBZYEYFW
    GXMXPYEYHWRWSCVBWGXSVCWGXMXNXRVDWGXOXPXQVEVFXSYFWGXNXQYFXMXPXNXQUBYFBWRWSVG
    VHVIVJVKVLWLYFAWTCWKWTBUFUGUHYCYDWTVMVNVOVPXTXOYAXRWLXNAWRCWKWRBUFUGWLXQAWS
    CWKWSBUFUGVQVRVSVTWGWHWNXDWAZWIWGWMWBJYIWLACIWCGHFWBWMWDUJWEWF $.
    $( [2-Sep-2009] $) $( [2-Sep-2009] $)
  $}

  ${
    $d t x y z A $. $d t x y z B $.
    $( A condition for a set to generate a filter base. $)
    fsubbas $p |- ( A e. B -> ( ( fi ` A ) e. fBas <-> ( A =/= (/) /\ (/) e/ (
    fi ` A ) ) ) ) $=
      ( vt vy vx vz wcel cfi cfv cfbas c0 wne wnel wa wceq wi fveq2 cv wn eq0
      wss cfn cint w3a wex ss0 cvv vprc int0 eleq1i mtbir isset mtbi nexnoti
      inteq eqeq2d mtbiri syl imnan mpbi 3simpb mto nex visset eqeq1 3anbi3d
      exbidv cab 0ex fiv ax-mp elab2 mpgbir syl6eq a1i necon3d imp fbasne0
      sylan2 0nelfb adantl jca ex cin wrex wral fine2 3adant3 3simp3 infi ssid
      sseq1 rcla4ev mpan2 syl6 r19.21aivv 3ad2ant1 3jca wb fvex isfbas2 sylibr
      3expib impbid ) ABGZAHIZJGZAKLZKXFMZNZXEXGXJXEXGNXHXIXEXFKLZXHXGXEXKXHXEA
      KXFKAKOZXFKOPXEXLXFKHIZKAKHQXMKOCRZXMGZSCCXMTXODRZKUAZXPUBGZXNXPUCZOZUDZD
      UEZYADYAXQXTNZXQXTSZPYCSXQXPKOZYDXPUFYEXTXNKUCZOZYGCYFUGGZYGCUEYHUGUGGUHY
      FUGUGUIUJUKCYFULUMUNYEXSYFXNXPKUOUPUQURXQXTUSUTXQXRXTVAVBVCXQXRERZXSOZUDZ
      DUEZYBEXNXMCVDYIXNOZYKYADYMYJXTXQXRYIXNXSVEVFVGKUGGXMYLEVHOVIDEKUGVJVKVLU
      KVMVNVOVPVQXFVRVSXGXIXEXFVTWAWBWCXEXHXIXGXEXHXIUDZXKXIFRZYIXPWDZUAZFXFWEZ
      DXFWFEXFWFZUDZXGYNXKXIYSXEXHXKXIXEXHXKABWGVQWHXEXHXIWIXEXHYSXIXEYREDXFXFX
      EYIXFGXPXFGNYPXFGZYRYIXPABWJUUAYPYPUAZYRYPWKYQUUBFYPXFYOYPYPWLWMWNWOWPWQW
      RXFUGGXGYTWSAHWTEDFUGXFXAVKXBXCXD $.
      $( [2-Sep-2009] $) $( [2-Sep-2009] $)
  $}

  ${
    $d r s u v x y F $. $d r s u v x y G $.
    $( A helpful lemma for showing that certain sets generate filters. $)
    fbunfip $p |- ( ( F e. fBas /\ G e. fBas ) -> ( (/) e/ ( fi ` ( F u. G ) )
    <-> A. x e. F A. y e. G ( x i^i y ) =/= (/) ) ) $=
      ( vv vu vr vs cfbas wcel wa c0 cun cfi cfv wnel cv wss cfn cint wceq w3a
      wn wal cin wne wral wex cab cvv unexg fiv syl eleq2d 0ex eqeq1 3anbi3d
      exbidv elab syl6bb notbid df-nel alnex 3bitr4g cpr wi ssun1 a1i simprl
      sseldd ssun2 simprr jca visset prss sylib prfi jctir pm2.27 necom df-ne
      intpr neeq1i 3bitr3i biimpi a1d syl6 com3r mpd df-3an notbii imnan
      bitr4i syl5ib zfpair sseq1 eleq1 inteq eqeq2d 3anbi123d cla4v syl5
      r19.21advva wrex inss2 fbssint mp3an2 ad2ant2r ad2ant2l ineq1 neeq1d
      ineq2 rcla42v ad2antrl ss2in ad2antll ssn0 ex syld exp32 com3l
      r19.23aivv impcom reeanv sylan2br syldan bitri syl6ib adantrl intun
      eqeq2i syl6ibr inteqd sylibd inss1 ssfi mpan2 sylan2i df-ss indi eqeq1i
      anbi1i com23 19.21adv impbid bitrd ) CIJZDIJZKZLCDMZNOZPZEQZUUJRZUUMSJZLU
      UMTZUAZUBZUCZEUDZAQZBQZUEZLUFZBDUGACUGZUUILUUKJZUCUUREUHZUCUULUUTUUIUVFUV
      GUUIUVFLUUNUUOFQZUUPUAZUBZEUHZFUIZJUVGUUIUUKUVLLUUIUUJUJJUUKUVLUACDIIUKEF
      UUJUJULUMUNUVKUVGFLUOUVHLUAZUVJUUREUVMUVIUUQUUNUUOUVHLUUPUPUQURUSUTVALUUK
      VBUUREVCVDUUIUUTUVEUUIUUTUVDABCDUUIUVACJZUVBDJZKKZUVAUVBVEZUUJRZUVQSJZLUV
      QTZUAZUBZUCZUVDUUTUVPUVRUVSKZUWAUCZVFZUVDUWCUVPUWDUWFUVDVFUVPUVRUVSUVPUVA
      UUJJZUVBUUJJZKUVRUVPUWGUWHUVPCUUJUVACUUJRUVPCDVGVHUUIUVNUVOVIVJUVPDUUJUVB
      DUUJRUVPDCVKVHUUIUVNUVOVLVJVMUVAUVBUUJAVNZBVNZVOVPUVAUVBVQVRUWDUWFUVPUVDU
      WDUWFUWEUVPUVDVFUWDUWEVSUWEUVDUVPUWEUVDLUVTUFUVTLUFUWEUVDLUVTVTLUVTWAUVTU
      VCLUVAUVBUWIUWJWBWCWDWEWFWGWHWIUWCUWDUWAKZUCUWFUWBUWKUVRUVSUWAWJWKUWDUWAW
      LWMWNUUSUWCEUVQABWOUUMUVQUAZUURUWBUWLUUNUVRUUOUVSUUQUWAUUMUVQUUJWPUUMUVQS
      WQUWLUUPUVTLUUMUVQWRWSWTVAXAXBXCUUIUVEUUSEUUIUVEUUNUUOKZUUQUCZVFZUUSUUIUW
      MUVEUWNUUIUUMCUEZUUMDUEZMZUUMUAZUUOKUVEUWNVFZUWMUUIUWSUWPSJZUWQSJZKZUWTUU
      OUUIUWSUXCKZUWTUUIUXDKZUVELUWRTZUAZUCZUWNUXEUVELUWPTZUWQTZUEZUAZUCZUXHUUI
      UXCUVEUXMVFUWSUUIUXCKZUVEUXKLUFZUXMUUIUXCGQZUXIRZGCXDZHQZUXJRZHDXDZKZUVEU
      XOVFZUXNUXRUYAUUGUXAUXRUUHUXBUUGUWPCRUXAUXRUUMCXEGUWPCXFXGXHUUHUXBUYAUUGU
      XAUUHUWQDRUXBUYAUUMDXEHUWQDXFXGXIVMUUIUXQUXTKZHDXDGCXDZUYCUYBUYEUUIUYCUYD
      UUIUYCVFGHCDUUIUXPCJUXSDJKZUYDUYCUUIUYFUYDUYCUUIUYFUYDKKZUVEUXPUXSUEZLUFZ
      UXOUYFUVEUYIVFUUIUYDUVDUYIUXPUVBUEZLUFABUXPUXSCDUVAUXPUAUVCUYJLUVAUXPUVBX
      JXKUVBUXSUAUYJUYHLUVBUXSUXPXLXKXMXNUYGUYHUXKRZUYIUXOVFUYDUYKUUIUYFUXPUXIU
      XSUXJXOXPUYKUYIUXOUYHUXKXQXRUMXSXTYAYBYCUXQUXTGHCDYDYEYFUXOLUXKUFUXMUXKLV
      TLUXKWAYGYHYIUXGUXLUXFUXKLUWPUWQYJYKWKYLUXEUXGUUQUXEUXFUUPLUXEUWRUUMUUIUW
      SUXCVIYMWSVAYNXRUUOUXAUXBUUOUWPUUMRUXAUUMCYOUUMUWPYPYQUUOUWQUUMRUXBUUMDYO
      UUMUWQYPYQVMYRUUNUWSUUOUUNUUMUUJUEZUUMUAUWSUUMUUJYSUYLUWRUUMUUMCDYTUUAYGU
      UBWNUUCUWOUWMUUQKZUCUUSUWMUUQWLUURUYMUUNUUOUUQWJWKWMYHUUDUUEUUF $.
      $( [3-Sep-2009] $) $( [3-Sep-2009] $)
  $}

  ${
    $d w x y z F $. $d w x y z X $. $d w x y z $.
    fgf.1 $e |- X = U. F $.
    $( The filter generating class gives a filter for every filter base. $)
    fgf $p |- ( F e. fBas -> ( filGen ` F ) = { x e. P~ X | ( F i^i P~ x ) =/=
    (/) } ) $=
      ( vy vz vw cfbas wcel cv cpw cin c0 wne cuni crab wceq wa copab cfv cfg
      fveq2 ineq1 neeq1d rabbisdv unieq pweq syl ax-mp syl6eqr rabeq eqtrd
      eqeq12d cvv uniexg pwexg rabexg 3syl eqid fvopab4g mpdan vtoclga df-fg
      fveq1i syl5eq ) BHIBEJZHIFJVFAJKZLZMNZAVFOZKZPZQREFSZTZBVGLZMNZACKZPZBUAT
      GJZVMTZVSVGLZMNZAVSOZKZPZQZVNVRQGBHVSBQZVTVNWEVRVSBVMUBWGWEVPAWDPZVRWGWBV
      PAWDWGWAVOMVSBVGUCUDUEWGWDVQQWHVRQWGWDBOZKZVQWGWCWIQWDWJQVSBUFWCWIUGUHCWI
      QVQWJQDCWIUGUIUJVPAWDVQUKUHULUMVSHIZWEUNIZWFWKWCUNIWDUNIWLVSHUOWCUNUPWBAW
      DUNUQUREFVSVLWEHUNVMVFVSQZVLWBAVKPZWEWMVIWBAVKWMVHWAMVFVSVGUCUDUEWMVJWCQV
      KWDQWNWEQVFVSUFVJWCUGWBAVKWDUKURULVMUSUTVAVBBUAVMEFAVCVDVE $.
      $( [3-Sep-2009] $) $( [3-Sep-2009] $)
  $}

  ${
    $d x y A $. $d y B $. $d x y F $. $d y X $. $d x y $.
    elfg.1 $e |- X = U. F $.
    $( A condition for elements of a generated filter. $)
    elfg $p |- ( F e. fBas -> ( A e. ( filGen ` F ) <-> ( A (_ X
    /\ E. x e. F x (_ A ) ) ) $=
      ( vy cfbas wcel cfg cfv cv cpw cin c0 wne crab wss wrex wa fgf eleq2d
      cvv wb cuni uniexg syl5eqel elpw2g syl wex elin visset elpw anbi2i bitri
      exbii n0 df-rex 3bitr4i a1i anbi12d wceq pweq ineq2d neeq1d elrab syl5bb
      bitrd ) CGHZBCIJZHBCFKZLZMZNOZFDLZPZHZBDQZAKZBQZACRZSZVHVIVOBFCDETUAVHBVN
      HZCBLZMZNOZSWAVPVHWBVQWEVTVHDUBHWBVQUCVHCUDUBDCGUEEUFBDUBUGUHWEVTUCVHVRWD
      HZAUIVRCHZVSSZAUIWEVTWFWHAWFWGVRWCHZSWHVRCWCUJWIVSWGVRBAUKULUMUNUOAWDUPVS
      ACUQURUSUTVMWEFBVNVJBVAZVLWDNWJVKWCCVJBVBVCVDVEVFVG $.
      $( [3-Sep-2009] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x F $. $d t x G $.
    $( A bigger base generates a bigger filter. $)
    fgss $p |- ( ( F e. fBas /\ G e. fBas /\ F (_ G ) -> ( filGen ` F ) (_ (
    filGen ` G ) ) $=
      ( vt vx cfbas wcel wss w3a cfg cfv cv cuni wrex wa wi sstr2 uniss
      syl5com ssrexv anim12d 3ad2ant3 wb eqid elfg 3ad2ant1 3ad2ant2 3imtr4d
      ssrdv ) AEFZBEFZABGZHZCAIJZBIJZULCKZALZGZDKUOGZDAMZNZUOBLZGZURDBMZNZUOUMF
      ZUOUNFZUKUIUTVDOUJUKUQVBUSVCUQUPVAGVBUKUOUPVAPABQRURDABSTUAUIUJVEUTUBUKDU
      OAUPUPUCUDUEUJUIVFVDUBUKDUOBVAVAUCUDUFUGUH $.
      $( [5-Sep-2009] $) $( [5-Sep-2009] $)
  $}

  ${
    $d t x F $.
    $( A filter base is a subset of its generated filter. $)
    fbssfg $p |- ( F e. fBas -> F (_ ( filGen ` F ) ) $=
      ( vt vx cfbas wcel cfg cfv cv cuni wss wrex wa eqid elfg elssuni ssid
      sseq1 rcla4ev mpan2 jca syl5bir ssrdv ) ADEZBAAFGZUCBHZUDEUEAIZJZCHZUEJZC
      AKZLUEAEZCUEAUFUFMNUKUGUJUEAOUKUEUEJZUJUEPUIULCUEAUHUEUEQRSTUAUB $.
      $( [3-Sep-2009] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x y F $. $d t x y X $.
    fgbas.1 $e |- X = U. F $.
    $( The base set of a generated filter is the base set of the parent base.
    $)
    fgbas $p |- ( F e. fBas -> X = U. ( filGen ` F ) ) $=
      ( vt vx vy cfbas wcel cfg cfv cuni wss fbssfg uniss syl syl5ss cv wa wex
      wrex elfg anbi2d ssel2 ancoms adantrr syl6bi 19.23adv eluni syl5ib ssrdv
      eqssd ) AGHZBAIJZKZULAKZUNBULAUMLUOUNLAMAUMNOCPULDUNBULDQZEQZHZUQUMHZRZES
      UPBHZUPUNHULUTVAEULUTURUQBLZFQUQLFATZRZRVAULUSVDURFUQABCUAUBURVBVAVCVBURV
      AUQBUPUCUDUEUFUGEUPUMUHUIUJUK $.
      $( [3-Sep-2009] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x F $.
    $( A filter generates itself. $)
    fgid $p |- ( F e. Fil -> ( filGen ` F ) = F ) $=
      ( vt vx cfil wcel cfg cfv cv cuni wss wrex wa cfbas wb filfbas eqid elfg
      syl wi fillsb 3expd com34 r19.23adv com23 imp3a sylbid ssrdv fbssfg
      eqssd ) ADEZAFGZAUJBUKAUJBHZUKEZULAIZJZCHZULJZCAKZLZULAEZUJAMEZUMUSNAOZCU
      LAUNUNPZQRUJUOURUTUJURUOUTUJUQUOUTSCAUJUPAEZUOUQUTUJVDUOUQUTUPULAUNVCTUAU
      BUCUDUEUFUGUJVAAUKJVBAUHRUI $.
      $( [5-Sep-2009] $) $( [5-Sep-2009] $)
  $}

  ${
    $d u v w x y z F $.
    $( A generated filter is a filter. $)
    fgfil $p |- ( F e. fBas -> ( filGen ` F ) e. Fil ) $=
      ( vx vy vz vw vu vv cfbas wcel c0 cfg cfv wn cuni wa cv wss w3a wi wal
      cin wral cfil wrex wceq eleq1 notbid wnel 0nelfb df-nel sylib syl5cbir
      con2d imp ss0 nsyl nrexdv intnand eqid elfg mtbird fgbas wne fbasne0 wex
      elssuni ancli 19.22i n0 df-rex 3imtr4i syl ssid jctil mpbird eqeltrrd
      jca 3simp2 a1i sseq1 rcla4ev ex sstr syl5 exp3a r19.23aiv adantll
      3adant2 jcad eqcomd sseq2d 3anbi12d 3imtr4d 19.21aivv inss2 mpan
      3ad2ant1 ad2antlr fbasssin 3com23 3expia 3adant3 adantr ss2in expcom
      ancoms 3ad2antl3 3ad2antr2 r19.22sdv mpd exp516 3exp r19.23adv com23
      imp3a com25 imp4c anbi12d r19.21aivv 3jca cvv wb fvex isfil ax-mp sylibr
      ) AHIZJAKLZIZMZYHNZYHIZOZBPZYHIZCPZYKQZYNYPQZRZYPYHIZSZCTBTZYNYPUAZYHIZCY
      HUBBYHUBZRZYHUCIZYGYMUUBUUEYGYJYLYGYIJANZQZYNJQZBAUDZOYGUUKUUIYGUUJBAYGYN
      AIZOYNJUEZUUJYGUULUUMMYGUUMUULUUMUULMJAIZMZYGUUMUULUUNYNJAUFUGYGJAUHUUOAU
      IJAUJUKULUMUNYNUOUPUQURBJAUUHUUHUSZUTVAYGUUHYKYHAUUHUUPVBZYGUUHYHIUUHUUHQ
      ZYNUUHQZBAUDZOYGUUTUURYGAJVCZUUTAVDUULBVEUULUUSOZBVEUVAUUTUULUVBBUULUUSYN
      AVFVGVHBAVIUUSBAVJVKVLUUHVMVNBUUHAUUHUUPUTVOVPVQYGUUABCYGUUSDPZYNQZDAUDZO
      ZYPUUHQZYRRZUVGEPZYPQZEAUDZOYSYTYGUVHUVGUVKUVHUVGSYGUVFUVGYRVRVSUVHUVKSYG
      UVFYRUVKUVGUVEYRUVKUUSUVEYRUVKUVDYRUVKSDAUVCAIZUVDYRUVKUVLUVCYPQZUVKUVDYR
      OUVLUVMUVKUVJUVMEUVCAUVIUVCYPVTWAWBUVCYNYPWCWDWEWFUNWGWHVSWIYGYOUVFYQUVGY
      RDYNAUUHUUPUTZYGYKUUHYPYGUUHYKUUQWJWKWLEYPAUUHUUPUTWMWNYGUUDBCYHYHYGUVFUV
      GFPZYPQZFAUDZOZOUUCUUHQZGPZUUCQZGAUDZOZYOYTOUUDYGUUSUVEUVRUWCYGUVEUUSUVRU
      WCSZYGUVDUUSUWDSDAYGUVRUVDUUSUVLUWCYGUVGUVQUVDUUSUVLUWCSSSZYGUVQUVGUWEYGU
      VPUVGUWESZFAYGUVOAIZUVPUWFYGUWGUVPRZUVGUVDUUSUVLUWCUWHUVGUVDUUSRZOZUVLOZU
      VSUWBUWIUVSUWHUVLUVGUVDUVSUUSUUCYPQUVGUVSYNYPWOUUCYPUUHWCWPWQWRUWKUVTUVCU
      VOUAZQZGAUDZUWBUWJUVLUWNUWHUVLUWNSZUWIYGUWGUWOUVPYGUWGUVLUWNYGUVLUWGUWNGU
      VCUVOAWSWTXAXBXCUNUWKUWMUWAGAUWJUWMUWASZUVLUWHUVGUVDUWPUUSUVPYGUVDUWPUWGU
      VDUVPUWPUVDUVPOUWLUUCQZUWPUVCYNUVOYPXDUWMUWQUWAUVTUWLUUCWCXEVLXFXGXHXCXIX
      JVQXKXLXMXNXOXPXMXNXQYGYOUVFYTUVRUVNFYPAUUHUUPUTXRGUUCAUUHUUPUTWMXSXTYHYA
      IUUGUUFYBAKYCBCYAYHYKYKUSYDYEYF $.
      $( [3-Sep-2009] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t x B $. $d t x F $. $d t x X $. $d t x Y $.
    fgmin.1 $e |- X = U. B $.
    fgmin.2 $e |- Y = U. F $.
    $( Minimality property of a generated filter: every filter that contains `
    B ` contains its generated filter. $)
    fgmin $p |- ( ( B e. fBas /\ F e. Fil /\ X = Y ) -> ( B (_ F <-> ( filGen `
    B ) (_ F ) ) $=
      ( vt vx cfbas wcel cfil wceq w3a wss cfg cfv wa cv wrex wb elfg 3ad2ant1
      adantr wi sseq2 3ad2ant3 ssrexv adantl fillsb 3expd com34 r19.23adv
      ad2antlr syld com23 3adantl3 sylbid imp3a ssrdv ex fbssfg sstr2 syl
      impbid ) AIJZBKJZCDLZMZABNZAOPZBNZVHVIVKVHVIQZGVJBVLGRZVJJZVMCNZHRZVMNZHA
      SZQZVMBJZVHVNVSTZVIVEVFWAVGHVMACEUAUBUCVLVOVRVTVLVOVMDNZVRVTUDZVHVOWBTZVI
      VGVEWDVFCDVMUEUFUCVEVFVIWBWCUDVGVEVFQZVIQZVRWBVTWFVRVQHBSZWBVTUDZVIVRWGUD
      WEVQHABUGUHVFWGWHUDVEVIVFVQWHHBVFVPBJZWBVQVTVFWIWBVQVTVPVMBDFUIUJUKULUMUN
      UOUPUQURUQUSUTVEVFVKVIUDZVGVEAVJNWJAVAAVJBVBVCUBVD $.
      $( [5-Sep-2009] $) $( [5-Sep-2009] $)
  $}

  ${
    $d t u v x y F $. $d t u v x y G $. $d t u v x y X $. $d t u v x y Y $. $d
    t u v x y $.
    fbfgss.1 $e |- X = U. F $.
    fbfgss.2 $e |- Y = U. G $.
    $( A condition for a filter to be finer than another involving their filter
    bases. $)
    fbfgss $p |- ( ( F e. fBas /\ G e. fBas /\ X = Y ) -> ( ( filGen ` F ) (_ (
    filGen ` G ) <-> A. x e. F E. y e. G y (_ x ) ) $=
      ( vt vu vv cfbas wcel wceq w3a cfg cfv wss cv wrex wral wa wi elfg
      pm3.27 syl6bi 3ad2ant2 ssel2 syl5 exp3a fbssfg 3ad2ant1 sseld syl5d
      r19.21adv sseq2 rexbidv rcla4v adantl 3simp3 ad2antrr sseq2d biimpd
      sseq1 rcla4ev a1d sstr sylanr2 jcad exp45 r19.23adv syld ex com23 imp
      imp3a wb adantr 3imtr4d ssrdv impbid ) CLMZDLMZEFNZOZCPQZDPQZRZBSZASZRZBD
      TZACUAZWEWHWLACWEWHWJWFMZWLWJCMWEWHWNWLWEWJWGMZWLWHWNUBWCWBWOWLUCWDWCWOWJ
      FRZWLUBWLBWJDFHUDWPWLUEUFUGWFWGWJUHUIUJWECWFWJWBWCCWFRWDCUKULUMUNUOWEWMWH
      WEWMUBZIWFWGWQISZERZJSZWRRZJCTZUBZWRFRZKSZWRRZKDTZUBZWRWFMZWRWGMZWQWSXBXH
      WQXBWSXHWQXAWSXHUCZJCWEWMWTCMZXAXKUCZUCWEXLWMXMWEXLWMXMUCWEXLUBZWMWIWTRZB
      DTZXMXLWMXPUCWEWLXPAWTCWJWTNWKXOBDWJWTWIUPUQURUSXNXOXMBDXNWIDMZXOXAXKXNXQ
      XOXAUBZUBZUBZWSXDXGXTWSXDXTEFWRWEWDXLXSWBWCWDUTVAVBVCXNXQWIWRRZWSXGUCXRXN
      XQYAUBZUBXGWSYBXGXNXFYAKWIDXEWIWRVDVEUSVFWIWTWRVGVHVIVJVKVLVMVNVOVKVNVPWE
      XIXCVQZWMWBWCYCWDJWRCEGUDULVRWEXJXHVQZWMWCWBYDWDKWRDFHUDUGVRVSVTVMWA $.
      $( [3-Sep-2009] $) $( [3-Sep-2009] $)
  $}

  ${
    $d t w x y z A $. $d t w x y z F $. $d t w x y z X $.
    extbas.1 $e |- X = U. F $.
    $( A way to extend the base set of a filter. $)
    extbas1 $p |- ( ( F e. fBas /\ X (_ A ) -> ( F u. { A } ) e. fBas ) $=
      ( vz vx vy vw vt cfbas wcel wss wa csn cun c0 wne wnel cv cin wrex wral
      w3a fbasne0 ssun1 ssn0 mpan syl adantr wn 0nelfb df-nel sylib wceq wex
      n0 eleq1 notbid syl5cbir necon2ad ancrd 19.22dv mpd anbi1i 19.41v bitr4i
      exbii cuni neeq1i excom eluni 3bitr4ri sylan2 ancoms necom df-ne bitri
      elsni nsyl jca wo elun notbii ioran 3bitrri wi fbasssin elun1 anim1i
      r19.22i2 3com23 3exp sseq1 rcla4ev ad2antll sstr elssuni sseq1i sylanb
      ad2ant2l wb sseq2 ad2antrl mpbird df-ss eqcomd eqimss sylanc exp32 syl5
      jaod com23 ad2ant2lr incom syl6ss cvv ssun2 a1i snidg 3ad2ant3 sseldd
      pm3.27 pm3.26 ineq12d inidm syl6req adantl 3expia snprc noel eleq2
      mtbiri sylbi pm2.21d pm2.61d1 imp3a anbi12i syl5ib r19.21aivv 3jca snex
      unexg mpan2 isfbas2 ) BJKZCALZMZBANZOZJKZUUIPQZPUUIRZESZFSZGSZTZLZEUUIUAZ
      GUUIUBFUUIUBZUCZUUGUUKUULUUSUUEUUKUUFUUEBPQZUUKBUDZBUUILUVAUUKBUUHUEBUUIU
      FUGUHUIUUGPBKZUJZPUUHKZUJZMZUULUUGUVDUVFUUEUVDUUFUUEPBRUVDBUKPBULUMZUIUUG
      PAUNZUVEUUGAPQZUVIUJZUUFUUEUVJUUFCPQZUVJUUECAUFUUEHSZISZKZUVNBKZMZHUOZIUO
      ZUVLUUEUVNPQZUVPMZIUOZUVSUUEUVPIUOZUWBUUEUVAUWCUVBIBUPUMUUEUVPUWAIUUEUVPU
      VTUUEUVPUVNPUVNPUNZUVPUJUVDUUEUWDUVPUVCUVNPBUQURUVHUSUTVAVBVCUWAUVRIUWAUV
      OHUOZUVPMUVRUVTUWEUVPHUVNUPVDUVOUVPHVEVFVGUMBVHZPQUVMUWFKZHUOZUVLUVSHUWFU
      PCUWFPDVIUVSUVQIUOZHUOUWHUVQIHVJUWGUWIHIUVMBVKVGVFVLUMVMVNUVJPAQUVKAPVOPA
      VPVQUMPAVRVSVTUULPUUIKZUJUVCUVEWAZUJUVGPUUIULUWJUWKPBUUHWBWCUVCUVEWDWEUMU
      UGUURFGUUIUUIUUGUUNBKZUUNUUHKZWAZUUOBKZUUOUUHKZWAZMUURUUNUUIKZUUOUUIKZMUU
      GUWNUWQUURUUGUWLUWQUURWFZUWMUUGUWQUWLUURUUGUWOUWLUURWFZUWPUUEUWOUXAWFUUFU
      UEUWOUWLUURUUEUWLUWOUURUUEUWLUWOUCUUQEBUAUUREUUNUUOBWGUUQUUQEBUUIUUMBKUUM
      UUIKUUQUUMBUUHWHWIWJUHWKWLUIUUGUUOAUNZUXAUWPUUGUXBUWLUURUWRUUNUUPLZUURUUG
      UXBUWLMMZUUQUXCEUUNUUIUUMUUNUUPWMWNUWLUWRUUGUXBUUNBUUHWHWOUXDUUNUUPUNUXCU
      XDUUPUUNUXDUUNUUOLZUUPUUNUNUXDUXEUUNALZUUFUWLUXFUUEUXBUWFALZUWLUXFUUFUXGU
      UNUWFLZUXFUWLUXHUXGUXFUUNUWFAWPVNUUNBWQVMCUWFADWRZWSWTUXBUXEUXFXAUUGUWLUU
      OAUUNXBXCXDUUNUUOXEUMXFUUNUUPXGUHXHXIUUOAVRZXJXKXLUUGUUNAUNZUWTUWMUUGUWQU
      XKUURUUGUWOUXKUURWFZUWPUUGUWOUXKUURUWSUUOUUPLZUURUUGUWOUXKMMZUUQUXMEUUOUU
      IUUMUUOUUPWMWNUWOUWSUUGUXKUUOBUUHWHXCUXNUUOUUOUUNTZUUPUXNUUOUXOUNUUOUXOLU
      XNUXOUUOUXNUUOUUNLZUXOUUOUNUXNUXPUUOALZUUFUWOUXQUUEUXKUXGUWOUXQUUFUXGUUOU
      WFLZUXQUWOUXRUXGUXQUUOUWFAWPVNUUOBWQVMUXIWSXMUXKUXPUXQXAUUGUWOUUNAUUOXBWO
      XDUUOUUNXEUMXFUUOUXOXGUHUUOUUNXNXOXHXIUUGAXPKZUWPUXLWFZUUEUUFUXSUXTUUEUUF
      UXSUCZUXBUXLUWPUYAUXBUXKUURAUUIKAUUPLZUURUYAUXBUXKMZMZUUQUYBEAUUIUUMAUUPW
      MWNUYDUUHUUIAUUHUUILUYDUUHBXQXRUYAAUUHKZUYCUXSUUEUYEUUFAXPXSXTUIYAUYDAUUP
      UNZUYBUYCUYFUYAUYCUUPAATAUYCUUNAUUOAUXBUXKYBUXBUXKYCYDAYEYFYGAUUPXGUHXHXI
      UXJXJYHUXSUJZUWPUXLUYGUUHPUNZUWPUJAYIUYHUWPUUOPKUUOYJUUHPUUOYKYLYMYNYOXKX
      LUUNAVRXJXKYPUWRUWNUWSUWQUUNBUUHWBUUOBUUHWBYQYRYSYTUUEUUJUUTXAZUUFUUEUUIX
      PKZUYIUUEUUHXPKUYJAUUABUUHJXPUUBUUCFGEXPUUIUUDUHUIXD $.
      $( [6-Sep-2009] $) $( [6-Sep-2009] $)

      $( The base set of an extended filter. $)
      extbas2 $p |- ( ( X (_ A /\ A e. B ) -> U. ( F u. { A } ) =
      A ) $=
        ( wss wcel wa csn cuni cun unisng uneq2d ssid jctr unss sylib ssun2
        a1i eqssd sylan9eqr uniun uneq1i eqtr4i syl5eq ) DAFZABGZHDAIZJZKZACUHK
        JZUGUFUJDAKZAUGUIADABLMUFULAUFUFAAFZHULAFUFUMANODAAPQAULFUFADRSTUAUKCJZ
        UIKUJCUHUBDUNUIEUCUDUE $.
        $( [7-Sep-2009] $) $( [7-Sep-2009] $)
  $}

  ${
    $d b c r s t u v x y B $. $d b c r s t u v C $. $d b c r s t u v x y F $.
    $d b c r s t u v x X $. $d b c r s t u v x y $.
    filrn.1 $e |- X = U. B $.
    filrn.2 $e |- C = { y | E. x e. B y = ( F " x ) } $.
    $( Given a filter on a domain, produce a filter on the range. $)
    filrn $p |- ( ( B e. fBas /\ F Fn X ) -> C e. fBas ) $=
      ( vt vr vs vb vc vu vv cfbas wcel wfn wa c0 wne wnel cv cin wss wrex
      wral w3a wex wi cima wceq cvv eqeq1 rexbidv cla4egv wfun funimaexg fnfun
      sylan ancoms eqid imaeq2 eqeq2d rcla4ev mpan2 adantr sylc ex cab eleq2i
      visset elab bitr2i exbii n0 bitr4i syl6ib 19.23aiv imp fbasne0 sylib wn
      cuni elssuni a1i eleq1 notbid 0nelfb df-nel syl5cbir con2d jcad cdm fndm
      adantl syl6eq sseq2d biimpar sseqin2 eqeq1d biimpd con3d expimpd eqcom
      imadisj bitri notbii syl6ibr syld r19.21aiv 0ex ralnex 3bitr4i fbasssin
      3expb ad2ant2r sseq1 ad2antrl wb funimaex elabg 3syl syl5bb ad2antrr
      mpbird imass2 ad2antll inss1 ax-mp inss2 ssini ineq12 ad2antlr sseqtr4d
      sstrd sylanc adantlrl exp32 r19.23adv mpd r19.23advv cbvrexv 3bitri
      anbi12i reeanv syl5ib r19.21aivv 3jca abrexexg syl5eqel isfbas2 syl ) CPQ
      ZEFRZSZDPQZDTUAZTDUBZIUCZJUCZKUCZUDZUEZIDUFZKDUGJDUGZUHZUUPUURUUSUVFLUCZC
      QZLUIZUUOUURUUNUVJUUOUURUVIUUOUURUJLUVIUUOMUCZEAUCZUKZULZACUFZMUIZUURUVIU
      UOUVPEUVHUKZUMQZUVQUVMULZACUFZUVPUVIUUOSUVOUVTMUVQUMUVKUVQULUVNUVSACUVKUV
      QUVMUNUOUPUUOUVIUVREUQZUVIUVRUUOEUVHCURFEUSZUTVAUVIUVTUUOUVIUVQUVQULZUVTU
      VQVBUVSUWCAUVHCUVLUVHULUVMUVQUVQUVLUVHEVCVDVEVFZVGVHVIUVPUVKDQZMUIUURUVOU
      WEMUWEUVKBUCZUVMULZACUFZBVJZQUVODUWIUVKHVKUWHUVOBUVKMVLUWFUVKULUWGUVNACUW
      FUVKUVMUNUOVMVNVOMDVPVQVRVSVTUUNCTUAUVJCWALCVPWBUTUUPTUVMULZWCZACUGZUUSUU
      PUWKACUUPUVLCQZUVLCWDZUEZUVLTULZWCZSZUWKUUPUWMUWOUWQUWMUWOUJUUPUVLCWEWFUU
      NUWMUWQUJUUOUUNUWPUWMUWPUWMWCTCQZWCZUUNUWPUWMUWSUVLTCWGWHUUNTCUBUWTCWITCW
      JWBWKWLVGWMUUPUWREWNZUVLUDZTULZWCZUWKUUPUWOUWQUXDUUPUWOSZUXCUWPUXEUXCUWPU
      XEUXBUVLTUXEUVLUXAUEZUXBUVLULUUPUXFUWOUUPUXAUWNUVLUUPUXAFUWNUUOUXAFULUUNF
      EWOWPGWQWRWSUVLUXAWTWBXAXBXCXDUWJUXCUWJUVMTULUXCTUVMXEEUVLXFXGXHXIXJXKUWJ
      ACUFZWCTDQZWCUWLUUSUXGUXHUXHTUWIQUXGDUWITHVKUWHUXGBTXLUWFTULUWGUWJACUWFTU
      VMUNUOVMVNXHUWJACXMTDWJXNWBUUPUVEJKDDUUPUVAENUCZUKZULZUVBEOUCZUKZULZSZOCU
      FNCUFZUVEUVADQZUVBDQZSZUUPUXOUVENOCCUUPUXICQZUXLCQZSZUXOUVEUUPUYBUXOSSZUV
      HUXIUXLUDZUEZLCUFZUVEUUNUYBUYFUUOUXOUUNUXTUYAUYFLUXIUXLCXOXPXQUYCUYEUVELC
      UYCUVIUYEUVEUUPUXOUVIUYESZUVEUYBUVQDQZUVQUVCUEZUVEUUPUXOSZUYGSZUVDUYIIUVQ
      DUUTUVQUVCXRVEUYKUYHUVTUVIUVTUYJUYEUWDXSUUPUYHUVTXTZUXOUYGUUOUYLUUNUUOUVQ
      UWIQZUVTUYHUUOUWAUVRUYMUVTXTUWBEUVHLVLYAUWHUVTBUVQUMUWFUVQULUWGUVSACUWFUV
      QUVMUNUOYBYCDUWIUVQHVKYDWPYEYFUYKUVQEUYDUKZUVCUYEUVQUYNUEUYJUVIUVHUYDEYGY
      HUYKUYNUXJUXMUDZUVCUYNUYOUEUYKUYNUXJUXMUYDUXIUEUYNUXJUEUXIUXLYIUYDUXIEYGY
      JUYDUXLUEUYNUXMUEUXIUXLYKUYDUXLEYGYJYLWFUXOUVCUYOULUUPUYGUVAUXJUVBUXMYMYN
      YOYPYQYRYSYTUUAYSUUBUXSUXKNCUFZUXNOCUFZSUXPUXQUYPUXRUYQUXQUVAUWIQUVAUVMUL
      ZACUFZUYPDUWIUVAHVKUWHUYSBUVAJVLUWFUVAULUWGUYRACUWFUVAUVMUNUOVMUYRUXKANCU
      VLUXIULUVMUXJUVAUVLUXIEVCVDUUCUUDUXRUVBUWIQUVBUVMULZACUFZUYQDUWIUVBHVKUWH
      VUABUVBKVLUWFUVBULUWGUYTACUWFUVBUVMUNUOVMUYTUXNAOCUVLUXLULUVMUXMUVBUVLUXL
      EVCVDUUCUUDUUEUXKUXNNOCCUUFVQUUGUUHUUIUUNUUQUVGXTZUUOUUNDUMQVUBUUNUWIUMDA
      BCUVMPUUJHUUKJKIUMDUULUUMVGYF $.
      $( [7-Sep-2009] $) $( [7-Sep-2009] $)
  $}

  ${
    $d t u x y z J $. $d t u x y z S $. $d t u x y z X $.
    neifg.1 $e |- X = U. J $.
    $( The neighborhood filter of a nonempty set is generated by its open
    supersets. See comments for ~ opnfbas . $)
    neifg $p |- ( ( J e. Top /\ S (_ X /\ S =/= (/) ) -> ( filGen ` { x e. J |
    S (_ x } ) = ( ( nei ` J ) ` S ) ) $=
      ( vt vu vz vy ctop wcel wss c0 wne w3a cv crab cfg cfv cpw cin cuni cnei
      cfbas wceq opnfbas eqid fgf syl wa wex wral wi eltopss ex adantrd
      3ad2ant1 sseq2 elrab syl5ib r19.21aiv unissb sylibr topopn 3simp2 jca
      elssuni eqssd sseq2d visset elpw syl5bb wb n0 elin anbi12i bitri exbii
      a1i anbi12d wrex isnei 3adant3 anass df-rex bitr4i anbi2i syl6rbbr bitrd
      pweq ineq2d neeq1d eqrdv eqtrd ) CJKZBDLZBMNZOZBAPZLZACQZRSZXAFPZTZUAZMNZ
      FXAUBZTZQZBCUCSSZWRXAUDKXBXIUEABCDEUFFXAXGXGUGUHUIWRGXIXJWRGPZXHKZXAXKTZU
      AZMNZUJZXKXJKZXKXIKWRXPXKDLZHPZCKZBXSLZUJZXSXKLZUJZHUKZUJZXQWRXLXRXOYEWRX
      KXGLXRXLWRXGDXKWRXGDWRIPZDLZIXAULXGDLWRYHIXAWRYGCKZBYGLZUJZYHYGXAKWOWPYKY
      HUMWQWOYIYHYJWOYIYHYGCDEUNUOUPUQWTYJAYGCWSYGBURUSUTVAIXADVBVCWRDXAKZDXGLW
      RDCKZWPUJYLWRYMWPWOWPYMWQCDEVDUQWOWPWQVEVFWTWPADCWSDBURUSVCDXAVGUIVHVIXKX
      GGVJVKVLXOYEVMWRXOXSXNKZHUKYEHXNVNYNYDHYNXSXAKZXSXMKZUJYDXSXAXMVOYOYBYPYC
      WTYAAXSCWSXSBURUSXSXKHVJVKVPVQVRVQVSVTWRXQXRYAYCUJZHCWAZUJZYFWOWPXQYSVMWQ
      BHCXKDEWBWCYEYRXRYEXTYQUJZHUKYRYDYTHXTYAYCWDVRYQHCWEWFWGWHWIXFXOFXKXHXCXK
      UEZXEXNMUUAXDXMXAXCXKWJWKWLUSVLWMWN $.
      $( [3-Sep-2009] $) $( [3-Sep-2009] $)
  $}

  ${
    $d x y z A $. $d x y z B $. $d x y z C $.
  $( The supersets of a nonempty set which are also subsets of a given base set
  form a filter. $)
  supfil $p |- ( ( A e. C /\ B (_ A /\ B =/= (/) ) -> ( { x | ( x (_ A /\ B (_
  x ) } e. Fil /\ A = U. { x | ( x (_ A /\ B (_ x ) } ) ) $=
    ( vy vz wcel wss c0 wne w3a cv wa cab cuni wceq cfil 3simp2 ssid jctil wb
    sseq2 elssabg 3ad2ant1 mpbird elssuni syl wral wi pm3.26 a1i visset sseq1
    anbi12d elab syl5ib r19.21aiv unissb sylibr eqssd wn wal cin df-ne biimpi
    3ad2ant3 ss0 adantl nsyl 0ex mtbird adantr pm3.27 eqeltrrd jca biimpar
    adantll 3ad2antr2 anasss sstr2 sylbi imp 3adant2 ad2antll expr 19.21aivv
    ssinss1 ad2antrr ssin ad2ant2l inex1 syl2anb rgen2a 3jca cvv abssexg eqid
    isfil ex jcai ancomd ) BDGZCBHZCIJZKZBALZBHZCXFHZMZANZOZPZXJQGZXEXLXMXEBXKX
    EBXJGZBXKHXEXNBBHZXCMZXEXCXOXBXCXDRBSTXBXCXNXPUAXDXHXCABBDXFBCUBUCUDUEZBXJU
    FUGXEELZBHZEXJUHXKBHXEXSEXJXEXSCXRHZMZXSXRXJGZYAXSUIXEXSXTUJUKXIYAAXREULZXF
    XRPXGXSXHXTXFXRBUMXFXRCUBUNUOZUPUQEXJBURUSUTXEXLXMXEXLMZXMIXJGZVAZXKXJGZMZY
    BFLZXKHZXRYJHZKZYJXJGZUIZFVBEVBZXRYJVCZXJGZFXJUHEXJUHZKZYEYIYPYSYEYGYHXEYGX
    LXEYFIBHZCIHZMZXECIPZUUCXDXBUUDVAZXCXDUUECIVDVEVFUUBUUDUUACVGVHVIYFUUCUAXEX
    IUUCAIVJXFIPXGUUAXHUUBXFIBUMXFICUBUNUOUKVKVLYEBXKXJXEXLVMXEXNXLXQVLVNVOYEYO
    EFXEXLYMYNXEXLYMMMZYJBHZCYJHZMZYNUUFUUGUUHXEXLYMUUGYEYBYKUUGYLXLYKUUGXEXLUU
    GYKBXKYJUBVPVQVRVSYMUUHXEXLYBYLUUHYKYBYLUUHYBYAYLUUHUIZYDXTUUJXSCXRYJVTVHWA
    WBWCWDVOXIUUIAYJFULXFYJPXGUUGXHUUHXFYJBUMXFYJCUBUNUOZUSWEWFYSYEYREFXJYAUUIY
    RYBYNYAUUIMZYQBHZCYQHZMZYRUULUUMUUNXSUUMXTUUIXRYJBWGWHXTUUHUUNXSUUGXTUUHMUU
    NCXRYJWIVEWJVOXIUUOAYQXRYJYCWKXFYQPXGUUMXHUUNXFYQBUMXFYQCUBUNUOUSYDUUKWLWMU
    KWNXEXMYTUAZXLXBXCUUPXDXBXJWOGUUPXHABDWPEFWOXJXKXKWQWRUGUDVLUEWSWTXA $.
    $( [12-Nov-2009] $) $( [12-Nov-2009] $)
  $}

  ${
    $d s t x y F $. $d s t x y S $.
    $( No filter containing a finite element is free. $)
    filfinnfr $p |- ( ( F e. Fil /\ S e. F /\ S e. Fin ) -> |^| F =/= (/) ) $=
      ( vs vt vx vy cfil wcel cfn cint c0 wne cv wss wa wceq wi wal wex wn
      eleq1 notbid filesn syl5cbir necon2ad imp n0 sylib w3a cin inss1 a1i
      filint 3expa 3adantl3 adantrr inss2 sseq1 mpbiri ad2antll 3simpl3 sseldd
      expr con3d impr visset inex1 anbi12d eqeq2 cla4ev sylan31c ex 19.23adv
      exanali elint notbii bitr4i 3imtr3g con4d ne0i syl6 3expia mpd expimpd
      wrex rcla4ev ancoms finminlem syl syl5 3impib ) BGHZABHZAIHZBJZKLZWLCMZBH
      ZDMZWQNZWSBHZOZWQWSPZQDRZOZCSZWPWMWNOZWLXEWPCWLWRXDWPWLWROZEMZWQHZESZXDWP
      QZXHWQKLZXKWLWRXMWLWRWQKWQKPZWRTKBHZTWLXNWRXOWQKBUAUBBUCUDUEUFEWQUGUHXHXJ
      XLEWLWRXJXLWLWRXJUIZXDXIWOHZWPXPXQXDXPFMZBHZXIXRHZTZOZFSZXBXCTZOZDSZXQTZX
      DTXPYBYFFXPYBYFWQXRUJZWQNZYHBHZWQYHPZTZYFXPYBOZYIYMWQXRUKULXPXSYJYAWLWRXS
      YJXJWLWRXSYJWQXRBUMUNUOUPXPXSYAYLXPXSOYKXTXPXSYKXTXPXSYKOZOWQXRXIYKWQXRNZ
      XPXSYKYOYHXRNWQXRUQWQYHXRURUSUTWLWRXJYNVAVBVCVDVEYEYIYJOZYLODYHWQXRCVFVGW
      SYHPZXBYPYDYLYQWTYIXAYJWSYHWQURWSYHBUAVHYQXCYKWSYHWQVIUBVHVJVKVLVMYCXSXTQ
      FRZTYGXSXTFVNXQYRFXIBEVFVOVPVQXBXCDVNVRVSWOXIVTWAWBVMWCWDVMXGWRCIWEZXFWNW
      MYSWRWMCAIWQABUAWFWGWRXACDWQWSBUAWHWIWJWK $.
      $( [5-Dec-2009] $) $( [5-Dec-2009] $)
  $}

