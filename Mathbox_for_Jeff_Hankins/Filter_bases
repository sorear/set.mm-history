$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Filter bases
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d r s t x J $.  $d r s t x S $.  $d r s t x X $.
    opnfbas.1 $e |- X = U. J $.
    $( The collection of open supersets of a nonempty set in a topology is a
       neighborhoods of the set, one of the motivations for the filter
       concept.  (Contributed by Jeff Hankins, 2-Sep-2009.) $)
    opnfbas $p |- ( ( J e. Top /\ S C_ X /\ S =/= (/) ) -> { x e. J | S C_ x }
        e.
  fBas ) $=
      ( vt vr vs ctop wcel wss c0 wne w3a cv wral wa sseq2 elrab wn crab topopn
      cfbas wnel cin wrex anim1i 3adant3 ne0i syl ss0 necon3ai 3ad2ant3 intnand
      sylibr df-nel notbii bitr2i sylib anbi12i simpl simprll simprrl syl111anc
      inopn ssin biimpi ad2ant2l adantl jca 3ad2antl1 ssid a1i rcla4ev syl11anc
      sseq1 ex syl5bi r19.21aivv 3jca wb cvv rabexg isfbas2 3ad2ant1 mpbird )
      CIJZBDKZBLMZNZBAOZKZACUAZUCJZWMLMZLWMUDZFOZGOZHOZUEZKZFWMUFZHWMPGWMPZNZWJ
      WOWPXCWJDWMJZWOWJDCJZWHQZXEWGWHXGWIWGXFWHCDEUBUGUHWLWHADCWKDBRSUOWMDUIUJW
      JLCJZBLKZQZTZWPWJXIXHWIWGXITWHXIBLBUKULUMUNWPLWMJZTXKLWMUPXLXJWLXIALCWKLB
      RSUQURUSWJXBGHWMWMWRWMJZWSWMJZQWRCJZBWRKZQZWSCJZBWSKZQZQZWJXBXMXQXNXTWLXP
      AWRCWKWRBRSWLXSAWSCWKWSBRSUTWJYAXBWJYAQZWTWMJZWTWTKZXBYBWTCJZBWTKZQZYCWGW
      HYAYGWIWGYAQZYEYFYHWGXOXRYEWGYAVAWGXOXPXTVBWGXQXRXSVCWRWSCVEVDYAYFWGXPXSY
      FXOXRXPXSQYFBWRWSVFVGVHVIVJVKWLYFAWTCWKWTBRSUOYDYBWTVLVMXAYDFWTWMWQWTWTVP
      VNVOVQVRVSVTWGWHWNXDWAZWIWGWMWBJYIWLACIWCGHFWBWMWDUJWEWF $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d t x B $.  $d t x F $.  $d t x X $.  $d t x Y $.
    fgmin.1 $e |- X = U. B $.
    fgmin.2 $e |- Y = U. F $.
    $( Minimality property of a generated filter: every filter that contains
       ` B ` contains its generated filter.  (Contributed by Jeff Hankins,
       5-Sep-2009.) $)
    fgmin $p |- ( ( B e. fBas /\ F e. Fil /\ X = Y ) -> ( B C_ F <-> ( filGen `
    B ) C_ F ) ) $=
      ( vt vx cfbas wcel cfil wss wa cv wrex wb 3ad2ant1 adantr wi sylbid sseq2
      wceq w3a cfg cfv elfg 3ad2ant3 ssrexv adantl fillsb 3expd com34 r19.23adv
      ad2antlr syld com23 3adantl3 imp3a ssrdv ex fbssfg sstr2 syl impbid ) AIJ
      ZBKJZCDUBZUCZABLZAUDUEZBLZVHVIVKVHVIMZGVJBVLGNZVJJZVMCLZHNZVMLZHAOZMZVMBJ
      ZVHVNVSPZVIVEVFWAVGHVMACEUFQRVLVOVRVTVLVOVMDLZVRVTSZVHVOWBPZVIVGVEWDVFCDV
      MUAUGRVEVFVIWBWCSVGVEVFMZVIMZVRWBVTWFVRVQHBOZWBVTSZVIVRWGSWEVQHABUHUIVFWG
      WHSVEVIVFVQWHHBVFVPBJZWBVQVTVFWIWBVQVTVPVMBDFUJUKULUMUNUOUPUQTURTUSUTVEVF
      VKVISZVGVEAVJLWJAVAAVJBVBVCQVD $.
      $( [5-Sep-2009] $)
  $}

  ${
    $d t u x y z J $.  $d t u x y z S $.  $d t u x y z X $.
    neifg.1 $e |- X = U. J $.
    $( The neighborhood filter of a nonempty set is generated by its open
       supersets.  See comments for ~ opnfbas .  (Contributed by Jeff Hankins,
       3-Sep-2009.) $)
    neifg $p |- ( ( J e. Top /\ S C_ X /\ S =/= (/) ) -> ( filGen ` { x e. J |
    S C_ x } ) = ( ( nei ` J ) ` S ) ) $=
      ( vt vu vz vy wcel wss c0 wne cv cfv cpw wa elrab wex sseq2 ctop w3a crab
      cfg cin cuni cnei cfbas wceq opnfbas eqid fgf syl pweq ineq2d neeq1d elpw
      visset wral wi eltopss ex adantrd 3ad2ant1 syl5bi r19.21aiv unissb sylibr
      topopn simp2 sylanbrc elssuni eqssd sseq2d wb n0 elin anbi12i bitri exbii
      syl5bb a1i anbi12d wrex isnei 3adant3 anass df-rex bitr4i anbi2i syl6rbbr
      bitrd eqrdv eqtrd ) CUAJZBDKZBLMZUBZBANZKZACUCZUDOZXAFNZPZUEZLMZFXAUFZPZU
      CZBCUGOOZWRXAUHJXBXIUIABCDEUJFXAXGXGUKULUMWRGXIXJGNZXIJXKXHJZXAXKPZUEZLMZ
      QZWRXKXJJZXFXOFXKXHXCXKUIZXEXNLXRXDXMXAXCXKUNUOUPRWRXPXKDKZHNZCJZBXTKZQZX
      TXKKZQZHSZQZXQWRXLXSXOYFXLXKXGKWRXSXKXGGURUQWRXGDXKWRXGDWRINZDKZIXAUSXGDK
      WRYIIXAYHXAJYHCJZBYHKZQZWRYIWTYKAYHCWSYHBTRWOWPYLYIUTWQWOYJYIYKWOYJYIYHCD
      EVAVBVCVDVEVFIXADVGVHWRDXAJZDXGKWRDCJZWPYMWOWPYNWQCDEVIVDWOWPWQVJWTWPADCW
      SDBTRVKDXAVLUMVMVNWAXOYFVOWRXOXTXNJZHSYFHXNVPYOYEHYOXTXAJZXTXMJZQYEXTXAXM
      VQYPYCYQYDWTYBAXTCWSXTBTRXTXKHURUQVRVSVTVSWBWCWRXQXSYBYDQZHCWDZQZYGWOWPXQ
      YTVOWQBHCXKDEWEWFYFYSXSYFYAYRQZHSYSYEUUAHYAYBYDWGVTYRHCWHWIWJWKWLWAWMWN
      $.
      $( [3-Sep-2009] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d w y z V $.
    $( The supersets of a nonempty set which are also subsets of a given base
       set form a filter.  (Contributed by Jeff Hankins, 12-Nov-2009.)  (The
       proof was shortened by Mario Carneiro, 14-Dec-2013.) $)
    supfil $p |- ( ( A e. V /\ B C_ A /\ B =/= (/) ) ->
        ( { x e. ~P A | B C_ x } e. Fil /\ A = U. { x e. ~P A | B C_ x } ) ) $=
      ( vy vz vw wcel wss c0 w3a cv wa wb sseq2 visset a1i cvv wsbc sbcie elrab
      wne cpw crab cfil cuni wceq elpw anbi1i bitri elisset 3ad2ant1 sbcieg syl
      simp2 mpbird wn ss0 necon3ai 3ad2ant3 0ex mtbird sstr expcom 3imtr4g ssin
      wi cin biimpi anbi12i inex1 3imtr4i isfild isfildu jca ) BDHZCBIZCJUBZKZC
      ALZIZABUCZUDZUEHBWCUFUGVSCELZIZEFGBWCWDWCHZWDBIZWEMZNVSWFWDWBHZWEMWHWAWEA
      WDWBVTWDCOUAWIWGWEWDBEPUHUIUJQZVPVQBRHZVRBDUKULZVSWEEBSZVQVPVQVRUOVSWKWMV
      QNWLWEVQEBRWDBCOUMUNUPZVSWEEJSZCJIZVRVPWPUQVQWPCJCURUSUTWOWPNVSWEWPEJVAWD
      JCOTQVBGLZFLZIZVSWEEWQSZWEEWRSZVGWRBIZWSCWQIZCWRIZWTXAXCWSXDCWQWRVCVDWEXC
      EWQGPWDWQCOTZWEXDEWRFPZWDWRCOTZVEUTXAWTMZWEEWRWQVHZSZVGVSXBWQBIKXDXCMZCXI
      IZXHXJXKXLCWRWQVFVIXAXDWTXCXGXEVJWEXLEXIWRWQXFVKWDXICOTVLQVMVSWEEBWCWJWLW
      NVNVO $.
      $( [14-Dec-2013] $) $( [12-Nov-2009] $)
  $}

  ${
    $d s t x y F $.  $d s t x y S $.
    $( No filter containing a finite element is free.  (Contributed by Jeff
       Hankins, 5-Dec-2009.) $)
    filfinnfr $p |- ( ( F e. Fil /\ S e. F /\ S e. Fin ) -> |^| F =/= (/) ) $=
      ( vs vt vx vy wcel cfn c0 wne wa cv wss wceq wi wal wex eleq1 wn 19.23adv
      cfil cint wrex rcla4ev ancoms finminlem filesn notbid syl5ibrcom necon2ad
      syl imp sylib w3a cin inss1 a1i filint 3expa 3adantl3 adantrr inss2 sseq1
      mpbiri ad2antll simpl3 sseldd expr con3d impr visset inex1 anbi12d cla4ev
      n0 eqeq2 syl21anc ex exanali elint notbii bitr4i 3imtr3g ne0i syl6 3expia
      con4d mpd expimpd syl5 3impib ) BUAGZABGZAHGZBUBZIJZWMWNKZCLZBGZDLZWRMZWT
      BGZKZWRWTNZODPZKZCQZWLWPWQWSCHUCZXGWNWMXHWSWMCAHWRABRUDUEWSXBCDWRWTBRUFUK
      WLXFWPCWLWSXEWPWLWSKZELZWRGZEQZXEWPOZXIWRIJZXLWLWSXNWLWSWRIWLWSSWRINZIBGZ
      SBUGXOWSXPWRIBRUHUIUJULEWRVOUMXIXKXMEWLWSXKXMWLWSXKUNZXEXJWOGZWPXQXRXEXQF
      LZBGZXJXSGZSZKZFQZXCXDSZKZDQZXRSZXESXQYCYGFXQYCYGXQYCKZWRXSUOZWRMZYJBGZWR
      YJNZSZYGYKYIWRXSUPUQXQXTYLYBWLWSXTYLXKWLWSXTYLWRXSBURUSUTVAXQXTYBYNXQXTKY
      MYAXQXTYMYAXQXTYMKZKWRXSXJYMWRXSMZXQXTYMYPYJXSMWRXSVBWRYJXSVCVDVEWLWSXKYO
      VFVGVHVIVJYFYKYLKZYNKDYJWRXSCVKVLWTYJNZXCYQYEYNYRXAYKXBYLWTYJWRVCWTYJBRVM
      YRXDYMWTYJWRVPUHVMVNVQVRTYDXTYAOFPZSYHXTYAFVSXRYSFXJBEVKVTWAWBXCXDDVSWCWG
      WOXJWDWEWFTWHWITWJWK $.
      $( [5-Dec-2009] $)
  $}

