$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       Separation axioms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Kol2 $.
  $( Extend class notation with the class of all T0 spaces.  See comments in
     ~ df-t0ALT . $)
  ct0ALT $a class Kol2 $.

  $c Reg $.
  $( Extend class notation with the class of all regular topologies. $)
  creg $a class Reg $.

  $c Nrm $.
  $( Extend class notation with the class of all normal topologies. $)
  cnrm $a class Nrm $.

  ${
    $d j o x y $.
    $( Define T0 or Kolmogorov spaces.  The use of a different symbol for this
       class is to improve compatibility with various Metamath parsers until
       the erroneous definition is removed. $)
    df-t0ALT $a |- Kol2 = { j e. Top | A. x e. U. j A. y e. U. j ( x =/= y ->
    E. o e. j -. ( x e. o <-> y e. o ) ) } $.
  $}

  ${
    $d c j o p x $.
    $( Define regular spaces. $)
    df-reg $a |- Reg = { j e. Top | A. c e. ( Clsd ` j ) A. x e. U. j ( x e/ c
    -> E. o e. j E. p e. j ( c C_ o /\ x e. p /\ ( o i^i p ) = (/) ) ) } $.
  $}

  ${
    $d c d j o p $.
    $( Define normal spaces. $)
    df-nrm $a |- Nrm = { j e. Top | A. c e. ( Clsd ` j ) A. d e. ( Clsd ` j ) (
    ( c i^i d ) = (/) -> E. o e. j E. p e. j ( c C_ o /\ d C_ p /\ ( o i^i p )
    = (/) ) ) } $.
  $}

  ${
    $d j o x y J $.  $d j o x y X $.
    ist0.1 $e |- X = U. J $.
    $( The predicate "is a T0 space."  Every pair of distinct points is
       topologically distinguishable.  For the way this definition is usually
       encountered, see ~ ist0-2 .  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    ist0 $p |- ( J e. Kol2 <-> ( J e. Top /\ A. x e. X A. y e. X ( x =/= y ->
    E. o e. J -. ( x e. o <-> y e. o ) ) ) ) $=
      ( vj cv wne wcel wb wn wrex wi cuni wral ctop ct0ALT wceq raleqbidv unieq
      syl6eqr rexeq imbi2d df-t0ALT elrab2 ) AHZBHZIZUGCHZJUHUJJKLZCGHZMZNZBULO
      ZPZAUOPUIUKCDMZNZBEPZAEPGDQRULDSZUPUSAUOEUTUODOEULDUAFUBZUTUNURBUOEVAUTUM
      UQUIUKCULDUCUDTTABGCUEUF $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d j o x y J $.  $d j o x y X $.
    ist0-2.1 $e |- X = U. J $.
    $( The predicate "is a T0 space," expressed in more familiar terms.
       (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    ist0-2 $p |- ( J e. Top -> ( J e. Kol2 <-> A. x e. X A. y e. X ( x =/= y ->
    E. o e. J ( ( x e. o /\ -. y e. o ) \/ ( -. x e. o /\ y e. o ) ) ) ) ) $=
      ( ctop wcel ct0ALT cv wne wb wn wrex wi wral wa wo ist0 baib ancom orbi2i
      xor bitri rexbii imbi2i 2ralbii syl6bb ) DGHZDIHZAJZBJZKZUKCJZHZULUNHZLMZ
      CDNZOZBEPAEPZUMUOUPMQZUOMZUPQZRZCDNZOZBEPAEPUJUIUTABCDEFSTUSVFABEEURVEUMU
      QVDCDUQVAUPVBQZRVDUOUPUCVGVCVAUPVBUAUBUDUEUFUGUH $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o x y J $.
    $( A T0 space is a topological space.  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    t0top $p |- ( J e. Kol2 -> J e. Top ) $=
      ( vx vy vo ct0ALT wcel ctop cv wne wb wn wrex cuni wral eqid ist0 simplbi
      wi ) AEFAGFBHZCHZISDHZFTUAFJKDALRCAMZNBUBNBCDAUBUBOPQ $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o x y A $.  $d o x y B $.  $d o x y J $.  $d o x y X $.
    t0dist.1 $e |- X = U. J $.
    $( Any two distinct points in a T0 space are topologically
       distinguishable.  (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    t0dist $p |- ( ( J e. Kol2 /\ ( A e. X /\ B e. X /\ A =/= B ) ) -> E. o e.
    J -. ( A e. o <-> B e. o ) ) $=
      ( vx vy wcel wne cv wb wn wrex wi wral wceq eleq1 notbid rexbidv w3a ctop
      ct0ALT ist0 simprbi neeq1 bibi1d imbi12d neeq2 bibi2d rcla42v com12 exp3a
      wa 3impd syl imp ) DUCIZAEIZBEIZABJZUAZACKZIZBVCIZLZMZCDNZURGKZHKZJZVIVCI
      ZVJVCIZLZMZCDNZOZHEPGEPZVBVHOURDUBIVRGHCDEFUDUEVRUSUTVAVHVRUSUTVAVHOZUSUT
      UNVRVSVQVSAVJJZVDVMLZMZCDNZOGHABEEVIAQZVKVTVPWCVIAVJUFWDVOWBCDWDVNWAWDVLV
      DVMVIAVCRUGSTUHVJBQZVTVAWCVHVJBAUIWEWBVGCDWEWAVFWEVMVEVDVJBVCRUJSTUHUKULU
      MUOUPUQ $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o w x y z J $.  $d o w x y z X $.
    ist1-2.1 $e |- X = U. J $.
    $( An alternate characterization of T1 spaces.  (Contributed by Jeff
       Hankins, 31-Jan-2010.) $)
    ist1-2 $p |- ( J e. Top -> ( J e. Fre <-> A. x e. X A. y e. X ( x =/= y ->
    E. o e. J ( x e. o /\ -. y e. o ) ) ) ) $=
      ( vz vw wcel cv csn wral wne wn wa wrex wi wceq 3ad2ant3 wss ctop ct1 cfv
      ccld ist1 baib w3a sneq eleq1d rcla4v cdif cldopn 3ad2antl1 adantrr elsni
      simpl2 necon3ai ad2antll eldif sylanbrc vex snid elndif ax-mp jctir eleq2
      notbid anbi12d rcla4ev exp32 syld 3expib com23 ralrimdvv cuni eldifn elsn
      syl2anc necon3bbii sylib eldifi simp2 neeq1 elequ1 anbi1d rexbidv imbi12d
      neeq2 anbi2d rcla42v mpid cin c0 disjsn eltopss reldisj biimpd syl anim2d
      syl5bir reximdva 3expia ralrimdv sseqtri a1i jctild eltop2 adantr sylibrd
      difss wb snssi adantl iscld ralrimdva impbid bitrd ) DUAIZDUBIZGJZKZDUDUC
      ZIZGELZAJZBJZMZYECJZIZYFYHIZNZOZCDPZQZBELAELZXSXRYDDEGFUEUFXRYDYOXRYDYNAB
      EEXRYEEIZYFEIZOYDYNXRYPYQYDYNQXRYPYQUGZYDYFKZYBIZYNYQXRYDYTQYPYCYTGYFEXTY
      FRYAYSYBXTYFUHUIUJSYRYTYGYMYRYTYGOZOZEYSUKZDIZYEUUCIZYFUUCIZNZOZYMYRYTUUD
      YGXRYPYTUUDYQYSDEFULUMUNUUBUUEUUGUUBYPYEYSIZNZUUEXRYPYQUUAUPYGUUJYRYTUUIY
      EYFYEYFUOUQURYEEYSUSUTYFYSIUUGYFBVAVBYFYSEVCVDVEYLUUHCUUCDYHUUCRZYIUUEYKU
      UGYHUUCYEVFUUKYJUUFYHUUCYFVFVGVHVIVRVJVKVLVMVNXRYOYCGEXRXTEIZOZYOYAETZEYA
      UKZDIZOZYCUUMYOUUPUUNUUMYOUUODVOZTZHJZYHIZYHUUOTZOZCDPZHUUOLZOZUUPUUMYOUV
      EUUSUUMYOUVDHUUOUUMUUTUUOIZYOUVDXRUULUVGYOUVDQXRUULUVGUGZYOUVAXTYHIZNZOZC
      DPZUVDUVHYOUUTXTMZUVLUVGXRUVMUULUVGUUTYAIZNUVMUUTEYAVPUVNUUTXTHXTVQVSVTSU
      VHUUTEIZUULYOUVMUVLQZQUVGXRUVOUULUUTEYAWASXRUULUVGWBYNUVPUUTYFMZUVAYKOZCD
      PZQABUUTXTEEYEUUTRZYGUVQYMUVSYEUUTYFWCUVTYLUVRCDUVTYIUVAYKAHCWDWEWFWGYFXT
      RZUVQUVMUVSUVLYFXTUUTWHUWAUVRUVKCDUWAYKUVJUVAUWAYJUVIBGCWDVGWIWFWGWJVRWKU
      VHUVKUVCCDUVHYHDIZOZUVJUVBUVAUVJYHYAWLWMRZUWCUVBYHXTWNXRUULUWBUWDUVBQZUVG
      XRUWBOYHETZUWEYHDEFWOUWFUWDUVBYHYAEWPWQWRUMWTWSXAVKXBVMXCUUSUUMUUOEUUREYA
      XJFXDXEXFXRUUPUVFXKUULHCUUODXGXHXIUULUUNXRXTEXLXMXFXRYCUUQXKUULYADEFXNXHX
      IXOXPXQ $.
      $( [31-Jan-2010] $)
  $}

  ${
    $d o w x y z J $.  $d o w x y z X $.
    ist1-3.1 $e |- X = U. J $.
    $( A space is T1 iff every point is the only point in the intersection of
       all open sets containing that point.  (Contributed by Jeff Hankins,
       31-Jan-2010.) $)
    ist1-3 $p |- ( J e. Top -> ( J e. Fre <-> A. x e. X |^| { o e. J | x e. o }
    = { x } ) ) $=
      ( vy vz vw wcel cv wne wn wa wrex wi wral cint wceq eleq1 notbid ctop ct1
      csn ist1-2 necom neeq1 anbi1d rexbidv imbi12d neeq2 anbi2d rcla42v syl7bi
      crab ex adantl com23 imp w3a topopn 3ad2ant1 3simpc eleq2 anbi12d rcla4ev
      syl2anc 3expia adantr a1dd pm2.61d rexanali elintrab notbii bitr4i syl6ib
      vex necon4ad elsn syl6ibr wss cab wal df-rab inteqi sseq2i ssintab bitr2i
      ssrdv snssi mpgbi eqssd exp31 ralrimdv rabbidv inteqd sneq eqeq12d rcla4v
      3ad2ant2 elsni eqcomd necon3ai syl5ibr syl5bi syl9r syld 3expib ralrimdvv
      a1i idd impbid bitrd ) CUAIZCUBIFJZGJZKZXNBJZIZXOXQIZLZMZBCNZOZGDPFDPZAJZ
      XQIZBCUNZQZYEUCZRZADPZFGBCDEUDXMYDYKXMYDYJADXMYEDIZYDYJXMYLYDYJXMYLMZYDMZ
      YHYIYNHYHYIYNHJZYHIZYOYERYOYIIYNYPYOYEYNYOYEKZYFYOXQIZLZMZBCNZYPLZYNYODIZ
      YQUUAOZYMYDUUCUUDOYMUUCYDUUDYLUUCYDUUDOZOXMYLUUCUUEYQYEYOKZYLUUCMYDUUAYOY
      EUEYCUUFUUAOYEXOKZYFXTMZBCNZOFGYEYODDXNYERZXPUUGYBUUIXNYEXOUFUUJYAUUHBCUU
      JXRYFXTXNYEXQSUGUHUIXOYORZUUGUUFUUIUUAXOYOYEUJUUKUUHYTBCUUKXTYSYFUUKXSYRX
      OYOXQSTUKUHUIULUMUOUPUQURYNUUCLZUUAYQYMUULUUAOYDXMYLUULUUAXMYLUULUSDCIZYL
      UULMZUUAXMYLUUMUULCDEUTVAXMYLUULVBYTUUNBDCXQDRZYFYLYSUULXQDYEVCUUOYRUUCXQ
      DYOVCTVDVEVFVGVHVIVJUUAYFYROBCPZLUUBYFYRBCVKYPUUPYFBYOCHVPVLVMVNVOVQHYEVR
      VSWHYIYHVTZYNXQCIZYFMZYIXQVTZOZUUQBUUQYIUUSBWAZQZVTUVABWBYHUVCYIYGUVBYFBC
      WCWDWEUUSBYIWFWGYFUUTUURYEXQWIUPWJXIWKWLUQWMXMYKYCFGDDXMXNDIZXODIZMYKYCXM
      UVDUVEYKYCOXMUVDUVEUSZYKXRBCUNZQZXNUCZRZYCUVDXMYKUVJOUVEYJUVJAXNDYEXNRZYH
      UVHYIUVIUVKYGUVGUVKYFXRBCYEXNXQSWNWOYEXNWPWQWRWSUVJXPXOUVHIZLZUVFYBXPUVMU
      VJXOUVIIZLUVNXNXOUVNXOXNXOXNWTXAXBUVJUVLUVNUVHUVIXOVCTXCUVMYBUVFYBUVMXRXS
      OBCPZLYBUVLUVOXRBXOCGVPVLVMXRXSBCVKVNUVFYBXJXDXEXFXGUQXHXKXL $.
      $( [31-Jan-2010] $)
  $}

  ${
    $d x J $.
    $( A T1 space is a topological space.  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    t1top $p |- ( J e. Fre -> J e. Top ) $=
      ( vx ct1 wcel ctop cv csn ccld cfv cuni wral eqid ist1 simplbi ) ACDAEDBF
      GAHIDBAJZKAOBOLMN $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d x A $.  $d x J $.  $d x X $.
    t1sncld.1 $e |- X = U. J $.
    $( In a T1 space, one-point sets are closed.  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    t1sncld $p |- ( ( J e. Fre /\ A e. X ) -> { A } e. ( Clsd ` J ) ) $=
      ( vx ct1 wcel csn ccld cfv cv wral wi ctop ist1 simprbi wceq sneq rcla4cv
      eleq1d syl imp ) BFGZACGZAHZBIJZGZUCEKZHZUFGZECLZUDUGMUCBNGUKBCEDOPUJUGEA
      CUHAQUIUEUFUHARTSUAUB $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o x y A $.  $d o x y B $.  $d o x y J $.  $d o x y X $.
    t1sep.1 $e |- X = U. J $.
    $( Any two distinct points in a T1 space are separated by an open set.
       (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    t1sep $p |- ( ( J e. Fre /\ ( A e. X /\ B e. X /\ A =/= B ) ) -> E. o e. J
    ( A e. o /\ -. B e. o ) ) $=
      ( vx vy wcel wne cv wn wa wrex wi wral wceq eleq1 rexbidv imbi12d ct1 w3a
      ctop t1top ist1-2 biimpd mpcom neeq1 anbi1d neeq2 notbid anbi2d com12 syl
      rcla42v exp3a 3impd imp ) DUAIZAEIZBEIZABJZUBZACKZIZBVDIZLZMZCDNZUSGKZHKZ
      JZVJVDIZVKVDIZLZMZCDNZOZHEPGEPZVCVIODUCIZUSVSDUDVTUSVSGHCDEFUEUFUGVSUTVAV
      BVIVSUTVAVBVIOZUTVAMVSWAVRWAAVKJZVEVOMZCDNZOGHABEEVJAQZVLWBVQWDVJAVKUHWEV
      PWCCDWEVMVEVOVJAVDRUISTVKBQZWBVBWDVIVKBAUJWFWCVHCDWFVOVGVEWFVNVFVKBVDRUKU
      LSTUOUMUPUQUNUR $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o x y J $.
    $( A T1 space is a T0 space.  (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    t1t0 $p |- ( J e. Fre -> J e. Kol2 ) $=
      ( vx vy vo ct1 wcel ct0ALT cv wne wn wa wo wrex wi cuni wral w3a eqid syl
      t1sep orc reximi 3exp2 imp3a ralrimivv ctop wb t1top ist0-2 mpbird ) AEFZ
      AGFZBHZCHZIZUMDHZFZUNUPFZJKZUQJURKZLZDAMZNZCAOZPBVDPZUKVCBCVDVDUKUMVDFZUN
      VDFZVCUKVFVGUOVBUKVFVGUOQKUSDAMVBUMUNDAVDVDRZTUSVADAUSUTUAUBSUCUDUEUKAUFF
      ULVEUGAUHBCDAVDVHUISUJ $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c j o p x J $.  $d c j o p x X $.
    isreg.1 $e |- X = U. J $.
    $( The predicate "is a regular space."  In a regular space, any closed set
       is separated from any point not in it by neighborhoods.  Note that some
       authors require the space to be Hausdorff (which would make it the same
       as T3), but we reserve the phrase "regular Hausdorff" for that as many
       topologists do.  (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    isreg $p |- ( J e. Reg <-> ( J e. Top /\ A. c e. ( Clsd ` J ) A. x e. X ( x
    e/ c -> E. o e. J E. p e. J ( c C_ o /\ x e. p /\ ( o i^i p ) = (/) ) ) ) )
        $=
      ( vj cv wnel wss wcel wceq wrex wi cuni wral ccld cfv raleqbidv ctop creg
      cin c0 w3a fveq2 unieq syl6eqr rexeq rexeqbi1dv imbi2d df-reg elrab2 ) AI
      ZFIZJZUOBIZKUNEIZLUQURUCUDMUEZEHIZNZBUTNZOZAUTPZQZFUTRSZQUPUSECNZBCNZOZAD
      QZFCRSZQHCUAUBUTCMZVEVJFVFVKUTCRUFVLVCVIAVDDVLVDCPDUTCUGGUHVLVBVHUPVAVGBU
      TCUSEUTCUIUJUKTTAHBEFULUM $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c o p x J $.
    $( A regular space is a topological space.  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    regtop $p |- ( J e. Reg -> J e. Top ) $=
      ( vx vc vo vp creg wcel ctop cv wnel wss cin wceq w3a wrex cuni wral ccld
      c0 wi cfv eqid isreg simplbi ) AFGAHGBIZCIZJUFDIZKUEEIZGUGUHLSMNEAODAOTBA
      PZQCARUAQBDAUIECUIUBUCUD $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c o p x A $.  $d c o p x C $.  $d c o p x J $.  $d c o p x X $.
    regsep.1 $e |- X = U. J $.
    $( In a regular space, a closed set is separated by open sets from a point
       not in it.  (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    regsep $p |- ( ( J e. Reg /\ ( C e. ( Clsd ` J ) /\ A e. X /\ A e/ C ) ) ->
    E. o e. J E. p e. J ( C C_ o /\ A e. p /\ ( o i^i p ) = (/) ) ) $=
      ( vx vc wcel wnel w3a cv wss wceq wrex wi wral 2rexbidv imbi12d creg ccld
      cfv cin c0 ctop isreg simprbi neleq2 sseq1 3anbi1d neleq1 3anbi2d rcla42v
      wa eleq1 com12 exp3a 3impd syl imp ) DUAJZBDUBUCZJZAEJZABKZLZBCMZNZAFMZJZ
      VHVJUDUEOZLZFDPCDPZVBHMZIMZKZVPVHNZVOVJJZVLLZFDPCDPZQZHERIVCRZVGVNQVBDUFJ
      WCHCDEFIGUGUHWCVDVEVFVNWCVDVEVFVNQZVDVEUOWCWDWBWDVOBKZVIVSVLLZFDPCDPZQIHB
      AVCEVPBOZVQWEWAWGVPBVOUIWHVTWFCFDDWHVRVIVSVLVPBVHUJUKSTVOAOZWEVFWGVNVOABU
      LWIWFVMCFDDWIVSVKVIVLVOAVJUPUMSTUNUQURUSUTVA $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d j o p J $.
    $( The predicate "is a normal space."  Much like the case for regular
       spaces, normal does not imply Hausdorff or even regular.  (Contributed
       by Jeff Hankins, 1-Feb-2010.) $)
    isnrm $p |- ( J e. Nrm <-> ( J e. Top /\ A. c e. ( Clsd ` J ) A. d e. (
    Clsd ` J ) ( ( c i^i d ) = (/) -> E. o e. J E. p e. J ( c C_ o /\ d C_ p /\
    ( o i^i p ) = (/) ) ) ) ) $=
      ( vj cv cin c0 wceq wss w3a wrex wi ccld cfv wral ctop cnrm raleqbidv
      fveq2 rexeq rexeqbi1dv imbi2d df-nrm elrab2 ) DGZEGZHIJZUGAGZKUHCGZKUJUKH
      IJLZCFGZMZAUMMZNZEUMOPZQZDUQQUIULCBMZABMZNZEBOPZQZDVBQFBRSUMBJZURVCDUQVBU
      MBOUAZVDUPVAEUQVBVEVDUOUTUIUNUSAUMBULCUMBUBUCUDTTFACDEUEUF $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d o p J $.
    $( An alternate characterization of normality.  This is the important
       property in the proof of Urysohn's lemma.  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    isnrm2 $p |- ( J e. Top -> ( J e. Nrm <-> A. c e. ( Clsd ` J ) A. d e. (
    Clsd ` J ) ( ( c i^i d ) = (/) -> E. o e. J ( c C_ o /\ ( ( ( cls ` J ) ` o
    ) i^i d ) = (/) ) ) ) ) $=
      ( vp wcel cv cin c0 wceq wss wrex wi cfv wral wa wb ad2ant2r syl expr w3a
      ctop cnrm ccld ccl isnrm baib simprr1 incom cuni cdif cnt simprr2 simplll
      difss a1i simprlr eqeq1i 3ad2ant3 ad2antll eqid eltopss ad2ant2rl reldisj
      biimpi mpbid ssntr syl22anc sstrd ntrcmp syl2anc sseqtrd cldss adantr jca
      mpbird syl5eq rexlimdv adantlr cmclsopn simprrl syl5bb biimpd imp3a sscls
      impr ssrin disjdif syl6sseq sseq2 eqeq1d 3anbi23d rcla4ev syl13anc impbid
      ss0 ineq2 rexbidva pm5.74d ralbidva bitrd ) BUBFZBUCFZCGZDGZHIJZXDAGZKZXE
      EGZKZXGXIHZIJZUAZEBLZABLZMZDBUDNZOZCXQOZXFXHXGBUENNZXEHZIJZPZABLZMZDXQOZC
      XQOXCXBXSABECDUFUGXBXRYFCXQXBXDXQFZPZXPYEDXQYHXEXQFZPXFXOYDYHYIXFXOYDQYHY
      IXFPZPZXNYCABYKXGBFZPZXNYCYMXMYCEBYKYLXIBFZXMYCMYKYLYNPZXMYCYKYOXMPZPZXHY
      BXHXJXLYOYKUHYQYAXEXTHZIXTXEUIZYQYRIJZXEBUJZXTUKZKZYQXEUUAXGUKZBULNNZUUBY
      QXEXIUUEXHXJXLYOYKUMYQXBUUDUUAKZYNXIUUDKZXIUUEKXBYGYJYPUNZUUFYQUUAXGUOUPY
      KYLYNXMUQYQXIXGHZIJZUUGXMUUJYKYOXLXHUUJXJXLUUJXKUUIIXGXIUIURVEUSUTYQXIUUA
      KZUUJUUGQYHYOUUKYJXMXBYNUUKYGYLXIBUUAUUAVAZVBVCRXIXGUUAVDSVFUUDBXIUUAUULV
      GVHVIYQXBXGUUAKZUUEUUBJUUHYHYOUUMYJXMXBYLUUMYGYNXGBUUAUULVBZRRXGBUUAUULVJ
      VKVLYQXEUUAKZYTUUCQZYKUUOYPXBYIUUOYGXFXEBUUAUULVMZRVNXEXTUUAVDZSVPVQVOTTV
      RYKYLYCXNYKYLYCPZPZUUBBFZXHUUCXGUUBHZIJZXNUUTXBUUMUVAXBYGYJUUSUNZYHYLUUMY
      JYCXBYLUUMYGUUNVSRZXGBUUAUULVTVKYKYLXHYBWAYKYLYCUUCYMXHYBUUCYKYLXHYBUUCMY
      KYLXHPZPZYBUUCYBYTUVGUUCYAYRIYSURYKUUPUVFXBYIUUPYGXFXBYIPUUOUUPUUQUURSRVN
      WBWCTWDWFUUTUVBIKUVCUUTUVBXTUUBHZIUUTXGXTKZUVBUVHKUUTXBUUMUVIUVDUVEXGBUUA
      UULWEVKXGXTUUBWGSXTUUAWHWIUVBWPSXMXHUUCUVCUAEUUBBXIUUBJZXJUUCXLUVCXHXIUUB
      XEWJUVJXKUVBIXIUUBXGWQWKWLWMWNTWOWRTWSWTWTXA $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d o p J $.
    $( A normal space is a topological space.  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    nrmtop $p |- ( J e. Nrm -> J e. Top ) $=
      ( vc vd vo vp cnrm wcel ctop cv cin c0 wceq wss w3a wrex wi ccld cfv wral
      isnrm simplbi ) AFGAHGBIZCIZJKLUBDIZMUCEIZMUDUEJKLNEAODAOPCAQRZSBUFSDAEBC
      TUA $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d o p C $.  $d c d o p D $.  $d c d o p J $.
    $( In a normal space, disjoint closed sets are separated by open sets.
       (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    nrmsep $p |- ( ( J e. Nrm /\ ( C e. ( Clsd ` J ) /\ D e. ( Clsd ` J ) /\ (
    C i^i D ) = (/) ) ) -> E. o e. J E. p e. J ( C C_ o /\ D C_ p /\ ( o i^i p
    ) = (/) ) ) $=
      ( vc vd wcel cin c0 wceq w3a cv wss wrex wi wral eqeq1d sseq1 2rexbidv wa
      cnrm ccld isnrm simprbi ineq1 3anbi1d imbi12d ineq2 3anbi2d rcla42v com12
      cfv ctop exp3a 3impd syl imp ) DUBHZADUCUMZHZBUTHZABIZJKZLZACMZNZBEMZNZVF
      VHIJKZLZEDOCDOZUSFMZGMZIZJKZVMVFNZVNVHNZVJLZEDOCDOZPZGUTQFUTQZVEVLPUSDUNH
      WBCDEFGUDUEWBVAVBVDVLWBVAVBVDVLPZVAVBUAWBWCWAWCAVNIZJKZVGVRVJLZEDOCDOZPFG
      ABUTUTVMAKZVPWEVTWGWHVOWDJVMAVNUFRWHVSWFCEDDWHVQVGVRVJVMAVFSUGTUHVNBKZWEV
      DWGVLWIWDVCJVNBAUIRWIWFVKCEDDWIVRVIVGVJVNBVHSUJTUHUKULUOUPUQUR $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d o C $.  $d c d o D $.  $d c d o J $.
    $( In a normal space, any two disjoint closed sets have the property that
       each one is a subset of an open set whose closure is disjoint from the
       other.  (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    nrmsep2 $p |- ( ( J e. Nrm /\ ( C e. ( Clsd ` J ) /\ D e. ( Clsd ` J ) /\ (
    C i^i D ) = (/) ) ) -> E. o e. J ( C C_ o /\ ( ( ( cls ` J ) ` o ) i^i D )
    = (/) ) ) $=
      ( vc vd wcel cfv cin c0 wceq cv wss wa wrex wral eqeq1d rexbidv imbi12d
      wi cnrm ccld w3a ctop nrmtop isnrm2 biimpd mpcom ineq1 sseq1 anbi1d ineq2
      ccl anbi2d rcla42v com12 exp3a 3impd syl imp ) DUAGZADUBHZGZBVBGZABIZJKZU
      CZACLZMZVHDUMHHZBIZJKZNZCDOZVAELZFLZIZJKZVOVHMZVJVPIZJKZNZCDOZTZFVBPEVBPZ
      VGVNTDUDGZVAWEDUEWFVAWECDEFUFUGUHWEVCVDVFVNWEVCVDVFVNTZVCVDNWEWGWDWGAVPIZ
      JKZVIWANZCDOZTEFABVBVBVOAKZVRWIWCWKWLVQWHJVOAVPUIQWLWBWJCDWLVSVIWAVOAVHUJ
      UKRSVPBKZWIVFWKVNWMWHVEJVPBAULQWMWJVMCDWMWAVLVIWMVTVKJVPBVJULQUNRSUOUPUQU
      RUSUT $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d r s u v w x y z A $.  $d r s u v w x y z B $.  $d w z F $.  $d w z G $.
    $d a b r s u v w x y z D $.  $d a b r s u v w x y z J $.
    $d r s u v x y z ph $.  $d s y R $.  $d s x S $.
    metnrm.1 $e |- J = ( MetOpen ` D ) $.
    ${
      metnrmlem.2 $e |- X = dom dom D $.
      metnrmlem.3 $e |- R =
                 sup ( { r | E. u e. B r = ( x D u ) } , RR , `' < ) $.
      metnrmlem.4 $e |- ( ph -> A e. ( Clsd ` J ) ) $.
      metnrmlem.5 $e |- ( ph -> B e. ( Clsd ` J ) ) $.
      metnrmlem.6 $e |- ( ph -> D e. Met ) $.
      metnrmlem.7 $e |- ( ph -> ( A i^i B ) = (/) ) $.
      $( Lemma for ~ metnrm . $)
      metnrmlem1 $p |- ( ( ph /\ x e. A /\ y e. B ) ->
                         ( R e. RR+ /\ R <_ ( x D y ) ) ) $=
        ( wcel cr vs vv cv w3a crp co cle wbr cc0 clt wceq wrex cab ccnv wss c0
        csup wne wral wa wi cme ad2antrr cuni ctop ccld cfv opntop eqid syl2anc
        cldss uniopn2 sseqtr4d ssel2 sylan adantr metcl eleq1a rexlimdva abssdv
        syl syl3anc 3adant3 wex oprex isseti 2th rcla4ev mpan2 3ad2ant3 rexcom4
        wb a1i abn0 bitr4i sylib cbl cdif 3ad2ant1 cldopn wn inelcm ex 3ad2ant2
        cin necon2bd mpd eldif sylanbrc opni2 simplr rpre ad2antrl ltnle simprl
        wal jca elbl2 syl21anc ssel ad2antll syl6 sylbird mt4d an32s syl5ibrcom
        eldifn breq2 3adantl3 alrimiv eqeq1 rexbidv sylibr expr reximdva rpssre
        ralab ssrexv ax-mp infmrcl syl5eqel 0re infmrgelb syl31anc ltletrd elrp
        rpgt0 syl6breqr opreq2 eqeq2d elab infmrlb syl5eqbr ) ABUCZESZCUCZFSZUD
        ZHUESZHUUNUUPGUFZUGUHUURHTSZUIHUJUHZUUSUURHKUCZUUNDUCZGUFZUKZDFULZKUMZT
        UJUNUQZTNUURUVHTUOZUVHUPURZUAUCZUBUCZUGUHZUBUVHUSZUATULZUVITSAUUOUVJUUQ
        AUUOUTZUVGKTUVQUVFUVCTSZDFUVQUVDFSZUTZUVETSZUVFUVRVAUVTGVBSZUUNJSZUVDJS
        ZUWAAUWBUUOUVSQVCZUVQUWCUVSAEJUOUUOUWCAEIVDZJAIVESZEIVFVGZSEUWFUOZAUWBU
        WGQGILVHWAZOEIUWFUWFVIZVKVJZAUWBJUWFUKQGIJMLVLWAZVMEJUUNVNVOVPZUVQFJUOZ
        UVSUWDAUWOUUOAFUWFJAUWGFUWHSZFUWFUOUWJPFIUWFUWKVKVJUWMVMVPFJUVDVNVOZUUN
        UVDGJMVQWBZUVETUVCVRWAVSVTWCZUURUVFKWDZDFULZUVKUUQAUXAUUOUUQUWFUWFUKZUX
        AUWKUWTUXBDUUPFUWTUXBWLUVDUUPUKZUWTUXBKUVEUUNUVDGWEWFUWKWGWMWHWIWJUXAUV
        GKWDUVKUVFDKFWKUVGKWNWOWPZUURUUNUVLGWQVGUFZUWFFWRZUOZUAUEULZUVPUURUWBUX
        FISZUUNUXFSZUXHAUUOUWBUUQQWSAUUOUXIUUQAUWGUWPUXIUWJPFIUWFUWKWTVJWSUURUU
        NUWFSZUUNFSZXAZUXJAUUOUXKUUQAUWIUUOUXKUWLEUWFUUNVNVOWCUUREFXEZUPUKZUXMA
        UUOUXOUUQRWSUURUXLUXNUPUUOAUXLUXNUPURZVAUUQUUOUXLUXPUUNEFXBXCXDXFXGUUNU
        WFFXHXIUAUXFGUUNILXJWBZUURUXHUVOUAUEULZUVPUURUXGUVOUAUEUURUVLUESZUXGUVO
        UURUXSUXGUTZUTZUVMUVEUKZDFULZUVNVAZUBXPUVOUYAUYDUBAUUOUXTUYDUUQUVQUXTUT
        ZUYBUVNDFUYEUVSUTUVNUYBUVLUVEUGUHZUVQUVSUXTUYFUVTUXTUTZUVSUYFUVQUVSUXTX
        KUYGUYFXAZUVEUVLUJUHZUVSXAZUYGUWAUVLTSZUYIUYHWLUVTUWAUXTUWRVPUXSUYKUVTU
        XGUVLXLZXMUVEUVLXNVJUYGUYIUVDUXESZUYJUYGUWBUXSUWCUWDUTZUYMUYIWLUVTUWBUX
        TUWEVPUVTUXSUXGXOUVTUYNUXTUVTUWCUWDUWNUWQXQVPUVDGUUNUVLJMXRXSUYGUYMUVDU
        XFSZUYJUXGUYMUYOVAUVTUXSUXEUXFUVDXTYAUVDUWFFYGYBYCYCYDYEUVMUVEUVLUGYHYF
        VSYIYJUVGUYCUVNUBKUVCUVMUKUVFUYBDFUVCUVMUVEYKYLYQYMZYNYOUETUOUXRUVPVAYP
        UVOUAUETYRYSYBXGZUAUBUVHYTWBUUAZUURUXHUVBUXQUURUXGUVBUAUEUURUXSUXGUVBUY
        AUIUVLHUITSUYAUUBWMUXSUYKUURUXGUYLXMZUURUVAUXTUYRVPUXSUIUVLUJUHUURUXGUV
        LUUGXMUYAUVLUVIHUGUYAUVJUVKUYKUVOUVLUVIUGUHUURUVJUXTUWSVPUURUVKUXTUXDVP
        UYSUYPUBUVHUVLUUCUUDNUUHUUEYNVSXGHUUFXIUURHUVIUUTUGNUURUVJUVPUUTUVHSZUV
        IUUTUGUHUWSUYQUURUUTUVEUKZDFULZUYTUUQAVUBUUOUUQUUTUUTUKZVUBUUTVIVUAVUCD
        UUPFUXCUVEUUTUUTUVDUUPUUNGUUIUUJWHWIWJUVGVUBKUUTUUNUUPGWEUVCUUTUKUVFVUA
        DFUVCUUTUVEYKYLUUKYMUAUBUUTUVHUULWBUUMXQ $.
        $( [14-Jan-2014] $)

      metnrmlem.8 $e |- F =
                 U. { s | E. x e. A s = ( x ( ball ` D ) ( R / 2 ) ) } $.
      metnrmlem.9 $e |- ( ph -> B =/= (/) ) $.
      $( Lemma for ~ metnrm . $)
      metnrmlem2 $p |- ( ph -> ( F e. J /\ A C_ F ) ) $=
        ( vy wcel wss cv c2 cdiv cbl cfv wceq wrex cab cuni ctop cme opntop syl
        co wa wi crp adantr ccld cldss syl2anc uniopn2 sseqtr4d ssel2 sylan wex
        eqid c0 wne sylib w3a cle wbr metnrmlem1 simpld 3expia exlimdv mpd 2pos
        2re elrpii rpdivcl sylancl blopn syl3anc eleq1a rexlimdva abssdv uniopn
        n0 syl5eqel wral blcntr oprex elabrex adantl ralrimiva ax-17 hbre1 hbab
        elunii hbuni dfss3f sylibr syl6sseqr jca ) AHIUCDHUDAHKUEZBUEZGUFUGURZF
        UHUIZURZUJZBDUKZKULZUMZITAIUNUCZXRIUDXSIUCAFUOUCZXTRFIMUPUQZAXQKIAXPXKI
        UCZBDAXLDUCZUSZXOIUCZXPYCUTYEYAXLJUCZXMVAUCZYFAYAYDRVBZADJUDYDYGADIUMZJ
        AXTDIVCUIUCDYJUDYBPDIYJYJVKVDVEAYAJYJUJRFIJNMVFUQVGDJXLVHVIZYEGVAUCZUFV
        AUCYHYEUBUEZEUCZUBVJZYLAYOYDAEVLVMYOUAUBEWNVNVBYEYNYLUBAYDYNYLAYDYNVOYL
        GXLYMFURVPVQABUBCDEFGIJLMNOPQRSVRVSVTWAWBUFWDWCWEGUFWFWGZFXLXMIJNMWHWIX
        OIXKWJUQWKWLXRIWMVEWOADXSHAXLXSUCZBDWPDXSUDAYQBDYEXLXOUCZXOXRUCZYQYEYAY
        GYHYRYIYKYPFXLXMJNWQWIYDYSABKDXOXLXMXNWRWSWTXLXOXRXEVEXABUBDXSYMDUCBXBB
        UBXRXQBKUBXPBDXCXDXFXGXHTXIXJ $.
        $( [14-Jan-2014] $)

      metnrmlem.10 $e |- G =
                 U. { s | E. y e. B s = ( y ( ball ` D ) ( S / 2 ) ) } $.
      metnrmlem.11 $e |- S =
                 sup ( { r | E. v e. A r = ( y D v ) } , RR , `' < ) $.
      metnrmlem.12 $e |- ( ph -> A =/= (/) ) $.
      $( Lemma for ~ metnrm . $)
      metnrmlem3 $p |- ( ph -> E. z e. J E. w e. J
                         ( A C_ z /\ B C_ w /\ ( z i^i w ) = (/) ) ) $=
        ( wcel wss cin c0 wceq cv wrex metnrmlem2 simpld incom syl5eq simprd c2
        w3a cdiv co cbl cfv wa wex cab eqeq1 rexbidv cbvabv unieqi eqtri eleq2i
        eluniab bitri anbi12i reeanv oprex eleq2 ceqsexv exancom bitr3i rexcom4
        cuni elin rexbii r19.42v exbii 3bitri 3bitr4i wi cme crp caddc 3ad2ant1
        cle wbr ctop ccld opntop syl cldss syl2anc uniopn2 sseqtr4d ssel2 sylan
        eqid 3adant3 3adant2 metnrmlem1 2re elrpii rpdivcl sylancl 3com23 cr wb
        2pos rpre metcl syl3anc cc0 clt elrp lediv1 mp3an3 mpbid metsym breqtrd
        mpbi cn 2nn nndivre sseq2 eqeq1d le2add syl22anc mp2and cc recnd bldisj
        2halves syl33anc eleq2d biimpd syl5bir 3expib rexlimdvv ssrdv ss0 ineq1
        syl5bi 3anbi13d ineq2 3anbi23d rcla42ev syl113anc ) AMOUKZNOUKZHMULZINU
        LZMNUMZUNUOZHDUPZULZIEUPZULZUVIUVKUMZUNUOZVDZEOUQDOUQAUVCUVEABGHIJKMOPQ
        RSTUAUBUCUDUEUFUGURZUSAUVDUVFACFIHJLNOPQRSTUIUCUBUDAIHUMHIUMUNIHUTUEVAZ
        UHUJURZUSAUVCUVEUVPVBAUVDUVFUVRVBAUVGUNULUVHADUVGUNUVIUVGUKZUVIBUPZKVCV
        EVFZJVGVHZVFZUKZUVICUPZLVCVEVFZUWBVFZUKZVIZCIUQBHUQZAUVIUNUKZUVIMUKZUVI
        NUKZVIUVIUWEUKZUWEUWCUOZBHUQZVIZCVJZUVIQUPZUKZUWSUWGUOZCIUQZVIZQVJZVIZU
        VSUWJUWLUWRUWMUXDUWLUVIUWPCVKZWHZUKUWRMUXGUVIMUWSUWCUOZBHUQZQVKZWHUXGUF
        UXJUXFUXIUWPQCUWSUWEUOUXHUWOBHUWSUWEUWCVLVMVNVOVPVQUWPCUVIVRVSUWMUVIUXB
        QVKWHZUKUXDNUXKUVIUHVQUXBQUVIVRVSVTUVIMNWIUWJUWDBHUQZUWHCIUQZVIUXEUWDUW
        HBCHIWAUXLUWRUXMUXDUXLUWNUWOVIZCVJZBHUQUXNBHUQZCVJUWRUWDUXOBHUWDUWOUWNV
        ICVJUXOUWNUWDCUWCUVTUWAUWBWBUWEUWCUVIWCWDUWOUWNCWEWFWJUXNBCHWGUXPUWQCUW
        NUWOBHWKWLWMUXMUWTUXAVIZQVJZCIUQUXQCIUQZQVJUXDUWHUXRCIUWHUXAUWTVIQVJUXR
        UWTUWHQUWGUWEUWFUWBWBUWSUWGUVIWCWDUXAUWTQWEWFWJUXQCQIWGUXSUXCQUWTUXACIW
        KWLWMVTVSWNAUWIUWKBCHIAUVTHUKZUWEIUKZUWIUWKWOUWIUVIUWCUWGUMZUKZAUXTUYAV
        DZUWKUVIUWCUWGWIUYDUYCUWKUYDUYBUNUVIUYDJWPUKZUVTPUKZUWEPUKZUWAWQUKZUWFW
        QUKZUWAUWFWRVFZUVTUWEJVFZWTXAUYBUNUOAUXTUYEUYAUDWSZAUXTUYFUYAAHPULUXTUY
        FAHOWHZPAOXBUKZHOXCVHZUKHUYMULAUYEUYNUDJOSXDXEZUBHOUYMUYMXLZXFXGAUYEPUY
        MUOUDJOPTSXHXEZXIHPUVTXJXKXMZAUYAUYGUXTAIPULUYAUYGAIUYMPAUYNIUYOUKIUYMU
        LUYPUCIOUYMUYQXFXGUYRXIIPUWEXJXKXNZUYDKWQUKZVCWQUKZUYHUYDVUAKUYKWTXAZAB
        CGHIJKOPRSTUAUBUCUDUEXOZUSZVCXPYCXQZKVCXRXSZUYDLWQUKZVUBUYIUYDVUHLUWEUV
        TJVFZWTXAZAUYAUXTVUHVUJVIACBFIHJLOPRSTUIUCUBUDUVQXOXTZUSZVUFLVCXRXSZUYD
        UYJUYKVCVEVFZVUNWRVFZUYKWTUYDUWAVUNWTXAZUWFVUNWTXAZUYJVUOWTXAZUYDVUCVUP
        UYDVUAVUCVUDVBUYDKYAUKZUYKYAUKZVUCVUPYBZUYDVUAVUSVUEKYDXEUYDUYEUYFUYGVU
        TUYLUYSUYTUVTUWEJPTYEYFZVUSVUTVCYAUKYGVCYHXAVIZVVAVUBVVCVUFVCYIYOZKUYKV
        CYJYKXGYLUYDLUYKWTXAZVUQUYDLVUIUYKWTUYDVUHVUJVUKVBUYDUYEUYGUYFVUIUYKUOU
        YLUYTUYSUWEUVTJPTYMYFYNUYDLYAUKZVUTVVEVUQYBZUYDVUHVVFVULLYDXEVVBVVFVUTV
        VCVVGVVDLUYKVCYJYKXGYLUYDUWAYAUKZUWFYAUKZVUNYAUKZVVJVUPVUQVIVURWOUYDUYH
        VVHVUGUWAYDXEUYDUYIVVIVUMUWFYDXEUYDVUTVCYPUKVVJVVBYQUYKVCYRXSZVVKUWAUWF
        VUNVUNUUAUUBUUCUYDUYKUUDUKVUOUYKUOUYDUYKVVBUUEUYKUUGXEYNJUVTUWEUWAUWFPT
        UUFUUHUUIUUJUUKUULUUMUUQUUNUVGUUOXEUVOUVEUVFUVHVDUVEUVLMUVKUMZUNUOZVDDE
        MNOOUVIMUOZUVJUVEUVNVVMUVLUVIMHYSVVNUVMVVLUNUVIMUVKUUPYTUURUVKNUOZUVLUV
        FVVMUVHUVEUVKNIYSVVOVVLUVGUNUVKNMUUSYTUUTUVAUVB $.
        $( [14-Jan-2014] $)
    $}

    $( A metric space is normal.  (Contributed by Jeff Hankins, 31-Aug-2013.)
       (The proof was shortened by Mario Carneiro, 14-Jan-2014.) $)
    metnrm $p |- ( D e. Met -> J e. Nrm ) $=
      ( va vb vz vw vr vs wcel cv cin c0 wceq wss w3a wrex eqid sseq2 co vx cme
      vy vv vu ctop wi ccld cfv wral cnrm opntop wne nne cuni 3ad2ant1 0opn syl
      wa topopn ssid a1i simp2r cldss syl2anc eqidd 3jca ineq1 incom in0 syl6eq
      wn eqtri 3anbi13d 3anbi2d rcla42ev syl3anc sseq1 3anbi1d 2rexbidv syl5ibr
      eqeq1d sylbi simp2l ineq2 3anbi23d cab clt ccnv csup cdiv cbl cdm simpr2l
      cr c2 simpr2r simpr1 simpr3 simplr simpll metnrmlem3 ecase 3exp ralrimivv
      ex isnrm sylanbrc ) AUBJZBUFJZDKZEKZLMNZXKFKZOZXLGKZOZXNXPLZMNZPZGBQFBQZU
      GZEBUHUIZUJDYCUJBUKJABCULZXIYBDEYCYCXIXKYCJZXLYCJZUSZXMYAXKMUMZXLMUMZXIYG
      XMPZYAUGZYHVLXKMNZYKXKMUNYJYAYLMXNOZXQXSPZGBQFBQZYJMBJZBUOZBJZMMOZXLYQOZM
      MNZPZYOYJXJYPXIYGXJXMYDUPZBUQURZYJXJYRUUCBYQYQRZUTURZYJYSYTUUAYSYJMVAVBZY
      JXJYFYTUUCXIYEYFXMVCXLBYQUUEVDVEYJMVFZVGYNUUBYSXQUUAPFGMYQBBXNMNZYMYSXSUU
      AXQXNMMSUUIXRMMUUIXRMXPLZMXNMXPVHUUJXPMLMMXPVIXPVJVMVKWBVNXPYQNXQYTYSUUAX
      PYQXLSVOVPVQYLXTYNFGBBYLXOYMXQXSXKMXNVRVSVTWAWCYIVLXLMNZYKXLMUNYJYAUUKXOM
      XPOZXSPZGBQFBQZYJYRYPXKYQOZYSUUAPZUUNUUFUUDYJUUOYSUUAYJXJYEUUOUUCXIYEYFXM
      WDXKBYQUUEVDVEUUGUUHVGUUMUUPUUOUULYQXPLZMNZPFGYQMBBXNYQNZXOUUOXSUURUULXNY
      QXKSUUSXRUUQMXNYQXPVHWBVNXPMNZUULYSUURUUAUUOXPMMSUUTUUQMMUUTUUQYQMLMXPMYQ
      WEYQVJVKWBWFVPVQUUKXTUUMFGBBUUKXQUULXOXSXLMXPVRVOVTWAWCYHYIUSZYJYAUVAYJUS
      UAUCFGUDUEXKXLAHKZUAKZUEKATNUEXLQHWGWOWHWIZWJZUVBUCKZUDKATNUDXKQHWGWOUVDW
      JZIKZUVCUVEWPWKTAWLUIZTNUAXKQIWGUOZUVHUVFUVGWPWKTUVITNUCXLQIWGUOZBAWMWMZI
      HCUVLRUVERYEYFXIXMUVAWNYEYFXIXMUVAWQUVAXIYGXMWRUVAXIYGXMWSUVJRYHYIYJWTUVK
      RUVGRYHYIYJXAXBXFXCXDXEFBGDEXGXH $.
      $( [14-Jan-2014] $) $( [31-Aug-2013] $)
  $}

