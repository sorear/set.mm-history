$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       Separation axioms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Kol2 $.
  $( Extend class notation with the class of all T0 spaces.  See comments in
     ~ df-t0ALT . $)
  ct0ALT $a class Kol2 $.

  $c Reg $.
  $( Extend class notation with the class of all regular topologies. $)
  creg $a class Reg $.

  $c Nrm $.
  $( Extend class notation with the class of all normal topologies. $)
  cnrm $a class Nrm $.

  ${
    $d j o x y $.
    $( Define T0 or Kolmogorov spaces.  The use of a different symbol for this
       class is to improve compatibility with various Metamath parsers until
       the erroneous definition is removed. $)
    df-t0ALT $a |- Kol2 = { j e. Top | A. x e. U. j A. y e. U. j ( x =/= y ->
    E. o e. j -. ( x e. o <-> y e. o ) ) } $.
  $}

  ${
    $d c j o p x $.
    $( Define regular spaces. $)
    df-reg $a |- Reg = { j e. Top | A. c e. ( Clsd ` j ) A. x e. U. j ( x e/ c
    -> E. o e. j E. p e. j ( c C_ o /\ x e. p /\ ( o i^i p ) = (/) ) ) } $.
  $}

  ${
    $d c d j o p $.
    $( Define normal spaces. $)
    df-nrm $a |- Nrm = { j e. Top | A. c e. ( Clsd ` j ) A. d e. ( Clsd ` j ) (
    ( c i^i d ) = (/) -> E. o e. j E. p e. j ( c C_ o /\ d C_ p /\ ( o i^i p )
    = (/) ) ) } $.
  $}

  ${
    $d j o x y J $.  $d j o x y X $.
    ist0.1 $e |- X = U. J $.
    $( The predicate "is a T0 space."  Every pair of distinct points is
       topologically distinguishable.  For the way this definition is usually
       encountered, see ~ ist0-2 .  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    ist0 $p |- ( J e. Kol2 <-> ( J e. Top /\ A. x e. X A. y e. X ( x =/= y ->
    E. o e. J -. ( x e. o <-> y e. o ) ) ) ) $=
      ( vj cv wne wcel wb wn wrex wi cuni wral ctop ct0ALT wceq raleqbidv unieq
      syl6eqr rexeq imbi2d df-t0ALT elrab2 ) AHZBHZIZUGCHZJUHUJJKLZCGHZMZNZBULO
      ZPZAUOPUIUKCDMZNZBEPZAEPGDQRULDSZUPUSAUOEUTUODOEULDUAFUBZUTUNURBUOEVAUTUM
      UQUIUKCULDUCUDTTABGCUEUF $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d j o x y J $.  $d j o x y X $.
    ist0-2.1 $e |- X = U. J $.
    $( The predicate "is a T0 space," expressed in more familiar terms.
       (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    ist0-2 $p |- ( J e. Top -> ( J e. Kol2 <-> A. x e. X A. y e. X ( x =/= y ->
    E. o e. J ( ( x e. o /\ -. y e. o ) \/ ( -. x e. o /\ y e. o ) ) ) ) ) $=
      ( ctop wcel ct0ALT cv wne wb wn wrex wi wral wa wo ist0 baib ancom orbi2i
      xor bitri rexbii imbi2i 2ralbii syl6bb ) DGHZDIHZAJZBJZKZUKCJZHZULUNHZLMZ
      CDNZOZBEPAEPZUMUOUPMQZUOMZUPQZRZCDNZOZBEPAEPUJUIUTABCDEFSTUSVFABEEURVEUMU
      QVDCDUQVAUPVBQZRVDUOUPUCVGVCVAUPVBUAUBUDUEUFUGUH $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o x y J $.
    $( A T0 space is a topological space.  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    t0top $p |- ( J e. Kol2 -> J e. Top ) $=
      ( vx vy vo ct0ALT wcel ctop cv wne wb wn wrex cuni wral eqid ist0 simplbi
      wi ) AEFAGFBHZCHZISDHZFTUAFJKDALRCAMZNBUBNBCDAUBUBOPQ $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o x y A $.  $d o x y B $.  $d o x y J $.  $d o x y X $.
    t0dist.1 $e |- X = U. J $.
    $( Any two distinct points in a T0 space are topologically
       distinguishable.  (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    t0dist $p |- ( ( J e. Kol2 /\ ( A e. X /\ B e. X /\ A =/= B ) ) -> E. o e.
    J -. ( A e. o <-> B e. o ) ) $=
      ( vx vy wcel wne cv wb wn wrex wi wral wceq eleq1 notbid rexbidv w3a ctop
      ct0ALT ist0 simprbi neeq1 bibi1d imbi12d neeq2 bibi2d rcla42v com12 exp3a
      wa 3impd syl imp ) DUCIZAEIZBEIZABJZUAZACKZIZBVCIZLZMZCDNZURGKZHKZJZVIVCI
      ZVJVCIZLZMZCDNZOZHEPGEPZVBVHOURDUBIVRGHCDEFUDUEVRUSUTVAVHVRUSUTVAVHOZUSUT
      UNVRVSVQVSAVJJZVDVMLZMZCDNZOGHABEEVIAQZVKVTVPWCVIAVJUFWDVOWBCDWDVNWAWDVLV
      DVMVIAVCRUGSTUHVJBQZVTVAWCVHVJBAUIWEWBVGCDWEWAVFWEVMVEVDVJBVCRUJSTUHUKULU
      MUOUPUQ $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o w x y z J $.  $d o w x y z X $.
    ist1-2.1 $e |- X = U. J $.
    $( An alternate characterization of T1 spaces.  (Contributed by Jeff
       Hankins, 31-Jan-2010.) $)
    ist1-2 $p |- ( J e. Top -> ( J e. Fre <-> A. x e. X A. y e. X ( x =/= y ->
    E. o e. J ( x e. o /\ -. y e. o ) ) ) ) $=
      ( vz vw wcel cv csn wral wne wn wa wrex wi wceq 3ad2ant3 wss ctop ct1 cfv
      ccld ist1 baib w3a sneq eleq1d rcla4v cdif cldopn 3ad2antl1 adantrr elsni
      simpl2 necon3ai ad2antll eldif sylanbrc visset elndif ax-mp jctir anbi12d
      snid eleq2 notbid rcla4ev syl11anc exp32 syld 3expib com23 ralrimdvv cuni
      eldifn necon3bbii sylib eldifi simp2 elequ1 anbi1d rexbidv imbi12d anbi2d
      elsn neeq1 neeq2 rcla42v cin c0 disjsn eltopss reldisj biimpd syl syl5bir
      anim2d reximdva 3expia ralrimdv difss sseqtri a1i jctild wb eltop2 adantr
      mpid sylibrd snssi adantl iscld ralrimdva impbid bitrd ) DUAIZDUBIZGJZKZD
      UDUCZIZGELZAJZBJZMZYECJZIZYFYHIZNZOZCDPZQZBELAELZXSXRYDDEGFUEUFXRYDYOXRYD
      YNABEEXRYEEIZYFEIZOYDYNXRYPYQYDYNQXRYPYQUGZYDYFKZYBIZYNYQXRYDYTQYPYCYTGYF
      EXTYFRYAYSYBXTYFUHUIUJSYRYTYGYMYRYTYGOZOZEYSUKZDIZYEUUCIZYFUUCIZNZOZYMYRY
      TUUDYGXRYPYTUUDYQYSDEFULUMUNUUBUUEUUGUUBYPYEYSIZNZUUEXRYPYQUUAUPYGUUJYRYT
      UUIYEYFYEYFUOUQURYEEYSUSUTYFYSIUUGYFBVAVFYFYSEVBVCVDYLUUHCUUCDYHUUCRZYIUU
      EYKUUGYHUUCYEVGUUKYJUUFYHUUCYFVGVHVEVIVJVKVLVMVNVOXRYOYCGEXRXTEIZOZYOYAET
      ZEYAUKZDIZOZYCUUMYOUUPUUNUUMYOUUODVPZTZHJZYHIZYHUUOTZOZCDPZHUUOLZOZUUPUUM
      YOUVEUUSUUMYOUVDHUUOUUMUUTUUOIZYOUVDXRUULUVGYOUVDQXRUULUVGUGZYOUVAXTYHIZN
      ZOZCDPZUVDUVHYOUUTXTMZUVLUVGXRUVMUULUVGUUTYAIZNUVMUUTEYAVQUVNUUTXTHXTWGVR
      VSSUVHUUTEIZUULYOUVMUVLQZQUVGXRUVOUULUUTEYAVTSXRUULUVGWAYNUVPUUTYFMZUVAYK
      OZCDPZQABUUTXTEEYEUUTRZYGUVQYMUVSYEUUTYFWHUVTYLUVRCDUVTYIUVAYKAHCWBWCWDWE
      YFXTRZUVQUVMUVSUVLYFXTUUTWIUWAUVRUVKCDUWAYKUVJUVAUWAYJUVIBGCWBVHWFWDWEWJV
      JXJUVHUVKUVCCDUVHYHDIZOZUVJUVBUVAUVJYHYAWKWLRZUWCUVBYHXTWMXRUULUWBUWDUVBQ
      ZUVGXRUWBOYHETZUWEYHDEFWNUWFUWDUVBYHYAEWOWPWQUMWRWSWTVLXAVNXBUUSUUMUUOEUU
      REYAXCFXDXEXFXRUUPUVFXGUULHCUUODXHXIXKUULUUNXRXTEXLXMXFXRYCUUQXGUULYADEFX
      NXIXKXOXPXQ $.
      $( [31-Jan-2010] $)
  $}

  ${
    $d o w x y z J $.  $d o w x y z X $.
    ist1-3.1 $e |- X = U. J $.
    $( A space is T1 iff every point is the only point in the intersection of
       all open sets containing that point.  (Contributed by Jeff Hankins,
       31-Jan-2010.) $)
    ist1-3 $p |- ( J e. Top -> ( J e. Fre <-> A. x e. X |^| { o e. J | x e. o }
    = { x } ) ) $=
      ( vy vz vw wcel cv wne wn wa wrex wi wral cint wceq eleq1 notbid ctop ct1
      csn ist1-2 necom neeq1 anbi1d rexbidv imbi12d neeq2 anbi2d rcla42v syl7bi
      crab ex adantl com23 imp w3a topopn 3ad2ant1 3simpc eleq2 anbi12d rcla4ev
      syl11anc 3expia adantr a1dd rexanali visset elintrab notbii bitr4i syl6ib
      pm2.61d necon4ad elsn syl6ibr wss cab df-rab inteqi sseq2i ssintab bitr2i
      ssrdv wal snssi mpgbi a1i eqssd exp31 ralrimdv rabbidv inteqd sneq rcla4v
      eqeq12d 3ad2ant2 elsni eqcomd necon3ai syl5ibr idd syl5bi syl9r ralrimdvv
      syld 3expib impbid bitrd ) CUAIZCUBIFJZGJZKZXNBJZIZXOXQIZLZMZBCNZOZGDPFDP
      ZAJZXQIZBCUNZQZYEUCZRZADPZFGBCDEUDXMYDYKXMYDYJADXMYEDIZYDYJXMYLYDYJXMYLMZ
      YDMZYHYIYNHYHYIYNHJZYHIZYOYERYOYIIYNYPYOYEYNYOYEKZYFYOXQIZLZMZBCNZYPLZYNY
      ODIZYQUUAOZYMYDUUCUUDOYMUUCYDUUDYLUUCYDUUDOZOXMYLUUCUUEYQYEYOKZYLUUCMYDUU
      AYOYEUEYCUUFUUAOYEXOKZYFXTMZBCNZOFGYEYODDXNYERZXPUUGYBUUIXNYEXOUFUUJYAUUH
      BCUUJXRYFXTXNYEXQSUGUHUIXOYORZUUGUUFUUIUUAXOYOYEUJUUKUUHYTBCUUKXTYSYFUUKX
      SYRXOYOXQSTUKUHUIULUMUOUPUQURYNUUCLZUUAYQYMUULUUAOYDXMYLUULUUAXMYLUULUSDC
      IZYLUULMZUUAXMYLUUMUULCDEUTVAXMYLUULVBYTUUNBDCXQDRZYFYLYSUULXQDYEVCUUOYRU
      UCXQDYOVCTVDVEVFVGVHVIVPUUAYFYROBCPZLUUBYFYRBCVJYPUUPYFBYOCHVKVLVMVNVOVQH
      YEVRVSWGYIYHVTZYNXQCIZYFMZYIXQVTZOZUUQBUUQYIUUSBWAZQZVTUVABWHYHUVCYIYGUVB
      YFBCWBWCWDUUSBYIWEWFYFUUTUURYEXQWIUPWJWKWLWMUQWNXMYKYCFGDDXMXNDIZXODIZMYK
      YCXMUVDUVEYKYCOXMUVDUVEUSZYKXRBCUNZQZXNUCZRZYCUVDXMYKUVJOUVEYJUVJAXNDYEXN
      RZYHUVHYIUVIUVKYGUVGUVKYFXRBCYEXNXQSWOWPYEXNWQWSWRWTUVJXPXOUVHIZLZUVFYBXP
      UVMUVJXOUVIIZLUVNXNXOUVNXOXNXOXNXAXBXCUVJUVLUVNUVHUVIXOVCTXDUVMYBUVFYBUVM
      XRXSOBCPZLYBUVLUVOXRBXOCGVKVLVMXRXSBCVJVNUVFYBXEXFXGXIXJUQXHXKXL $.
      $( [31-Jan-2010] $)
  $}

  ${
    $d x J $.
    $( A T1 space is a topological space.  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    t1top $p |- ( J e. Fre -> J e. Top ) $=
      ( vx ct1 wcel ctop cv csn ccld cfv cuni wral eqid ist1 simplbi ) ACDAEDBF
      GAHIDBAJZKAOBOLMN $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d x A $.  $d x J $.  $d x X $.
    t1sncld.1 $e |- X = U. J $.
    $( In a T1 space, one-point sets are closed.  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    t1sncld $p |- ( ( J e. Fre /\ A e. X ) -> { A } e. ( Clsd ` J ) ) $=
      ( vx ct1 wcel csn ccld cfv cv wral wi ctop ist1 simprbi wceq sneq rcla4cv
      eleq1d syl imp ) BFGZACGZAHZBIJZGZUCEKZHZUFGZECLZUDUGMUCBNGUKBCEDOPUJUGEA
      CUHAQUIUEUFUHARTSUAUB $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o x y A $.  $d o x y B $.  $d o x y J $.  $d o x y X $.
    t1sep.1 $e |- X = U. J $.
    $( Any two distinct points in a T1 space are separated by an open set.
       (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    t1sep $p |- ( ( J e. Fre /\ ( A e. X /\ B e. X /\ A =/= B ) ) -> E. o e. J
    ( A e. o /\ -. B e. o ) ) $=
      ( vx vy wcel wne cv wn wa wrex wi wral wceq eleq1 rexbidv imbi12d ct1 w3a
      ctop t1top ist1-2 biimpd mpcom neeq1 anbi1d neeq2 notbid anbi2d com12 syl
      rcla42v exp3a 3impd imp ) DUAIZAEIZBEIZABJZUBZACKZIZBVDIZLZMZCDNZUSGKZHKZ
      JZVJVDIZVKVDIZLZMZCDNZOZHEPGEPZVCVIODUCIZUSVSDUDVTUSVSGHCDEFUEUFUGVSUTVAV
      BVIVSUTVAVBVIOZUTVAMVSWAVRWAAVKJZVEVOMZCDNZOGHABEEVJAQZVLWBVQWDVJAVKUHWEV
      PWCCDWEVMVEVOVJAVDRUISTVKBQZWBVBWDVIVKBAUJWFWCVHCDWFVOVGVEWFVNVFVKBVDRUKU
      LSTUOUMUPUQUNUR $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o x y J $.
    $( A T1 space is a T0 space.  (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    t1t0 $p |- ( J e. Fre -> J e. Kol2 ) $=
      ( vx vy vo ct1 wcel ct0ALT cv wne wn wa wo wrex wi cuni wral w3a eqid syl
      t1sep orc reximi 3exp2 imp3a ralrimivv ctop wb t1top ist0-2 mpbird ) AEFZ
      AGFZBHZCHZIZUMDHZFZUNUPFZJKZUQJURKZLZDAMZNZCAOZPBVDPZUKVCBCVDVDUKUMVDFZUN
      VDFZVCUKVFVGUOVBUKVFVGUOQKUSDAMVBUMUNDAVDVDRZTUSVADAUSUTUAUBSUCUDUEUKAUFF
      ULVEUGAUHBCDAVDVHUISUJ $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c j o p x J $.  $d c j o p x X $.
    isreg.1 $e |- X = U. J $.
    $( The predicate "is a regular space."  In a regular space, any closed set
       is separated from any point not in it by neighborhoods.  Note that some
       authors require the space to be Hausdorff (which would make it the same
       as T3), but we reserve the phrase "regular Hausdorff" for that as many
       topologists do.  (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    isreg $p |- ( J e. Reg <-> ( J e. Top /\ A. c e. ( Clsd ` J ) A. x e. X ( x
    e/ c -> E. o e. J E. p e. J ( c C_ o /\ x e. p /\ ( o i^i p ) = (/) ) ) ) )
        $=
      ( vj cv wnel wss wcel wceq wrex wi cuni wral ccld cfv raleqbidv ctop creg
      cin c0 w3a fveq2 unieq syl6eqr rexeq rexeqbi1dv imbi2d df-reg elrab2 ) AI
      ZFIZJZUOBIZKUNEIZLUQURUCUDMUEZEHIZNZBUTNZOZAUTPZQZFUTRSZQUPUSECNZBCNZOZAD
      QZFCRSZQHCUAUBUTCMZVEVJFVFVKUTCRUFVLVCVIAVDDVLVDCPDUTCUGGUHVLVBVHUPVAVGBU
      TCUSEUTCUIUJUKTTAHBEFULUM $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c o p x J $.
    $( A regular space is a topological space.  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    regtop $p |- ( J e. Reg -> J e. Top ) $=
      ( vx vc vo vp creg wcel ctop cv wnel wss cin wceq w3a wrex cuni wral ccld
      c0 wi cfv eqid isreg simplbi ) AFGAHGBIZCIZJUFDIZKUEEIZGUGUHLSMNEAODAOTBA
      PZQCARUAQBDAUIECUIUBUCUD $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c o p x A $.  $d c o p x C $.  $d c o p x J $.  $d c o p x X $.
    regsep.1 $e |- X = U. J $.
    $( In a regular space, a closed set is separated by open sets from a point
       not in it.  (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    regsep $p |- ( ( J e. Reg /\ ( C e. ( Clsd ` J ) /\ A e. X /\ A e/ C ) ) ->
    E. o e. J E. p e. J ( C C_ o /\ A e. p /\ ( o i^i p ) = (/) ) ) $=
      ( vx vc wcel wnel w3a cv wss wceq wrex wi wral 2rexbidv imbi12d creg ccld
      cfv cin c0 ctop isreg simprbi neleq2 sseq1 3anbi1d neleq1 3anbi2d rcla42v
      wa eleq1 com12 exp3a 3impd syl imp ) DUAJZBDUBUCZJZAEJZABKZLZBCMZNZAFMZJZ
      VHVJUDUEOZLZFDPCDPZVBHMZIMZKZVPVHNZVOVJJZVLLZFDPCDPZQZHERIVCRZVGVNQVBDUFJ
      WCHCDEFIGUGUHWCVDVEVFVNWCVDVEVFVNQZVDVEUOWCWDWBWDVOBKZVIVSVLLZFDPCDPZQIHB
      AVCEVPBOZVQWEWAWGVPBVOUIWHVTWFCFDDWHVRVIVSVLVPBVHUJUKSTVOAOZWEVFWGVNVOABU
      LWIWFVMCFDDWIVSVKVIVLVOAVJUPUMSTUNUQURUSUTVA $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d j o p J $.
    $( The predicate "is a normal space."  Much like the case for regular
       spaces, normal does not imply Hausdorff or even regular.  (Contributed
       by Jeff Hankins, 1-Feb-2010.) $)
    isnrm $p |- ( J e. Nrm <-> ( J e. Top /\ A. c e. ( Clsd ` J ) A. d e. (
    Clsd ` J ) ( ( c i^i d ) = (/) -> E. o e. J E. p e. J ( c C_ o /\ d C_ p /\
    ( o i^i p ) = (/) ) ) ) ) $=
      ( vj cv cin c0 wceq wss w3a wrex wi ccld cfv wral ctop cnrm raleqbidv
      fveq2 rexeq rexeqbi1dv imbi2d df-nrm elrab2 ) DGZEGZHIJZUGAGZKUHCGZKUJUKH
      IJLZCFGZMZAUMMZNZEUMOPZQZDUQQUIULCBMZABMZNZEBOPZQZDVBQFBRSUMBJZURVCDUQVBU
      MBOUAZVDUPVAEUQVBVEVDUOUTUIUNUSAUMBULCUMBUBUCUDTTFACDEUEUF $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d o p J $.
    $( An alternate characterization of normality.  This is the important
       property in the proof of Urysohn's lemma.  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    isnrm2 $p |- ( J e. Top -> ( J e. Nrm <-> A. c e. ( Clsd ` J ) A. d e. (
    Clsd ` J ) ( ( c i^i d ) = (/) -> E. o e. J ( c C_ o /\ ( ( ( cls ` J ) ` o
    ) i^i d ) = (/) ) ) ) ) $=
      ( vp wcel cv cin c0 wceq wss wrex wi cfv wral wa wb ad2ant2r syl expr w3a
      ctop cnrm ccld ccl isnrm baib simprr1 incom cuni cdif cnt simprr2 simplll
      difss a1i simprlr eqeq1i 3ad2ant3 ad2antll eqid eltopss ad2ant2rl reldisj
      biimpi mpbid ssntr syl22anc sstrd ntrcmp syl11anc cldss adantr mpbird jca
      sseqtrd syl5eq rexlimdv adantlr cmclsopn simprrl syl5bb biimpd imp3a impr
      sscls ssrin disjdif syl6sseq sseq2 ineq2 eqeq1d 3anbi23d rcla4ev syl13anc
      ss0 impbid rexbidva pm5.74d ralbidva bitrd ) BUBFZBUCFZCGZDGZHIJZXDAGZKZX
      EEGZKZXGXIHZIJZUAZEBLZABLZMZDBUDNZOZCXQOZXFXHXGBUENNZXEHZIJZPZABLZMZDXQOZ
      CXQOXCXBXSABECDUFUGXBXRYFCXQXBXDXQFZPZXPYEDXQYHXEXQFZPXFXOYDYHYIXFXOYDQYH
      YIXFPZPZXNYCABYKXGBFZPZXNYCYMXMYCEBYKYLXIBFZXMYCMYKYLYNPZXMYCYKYOXMPZPZXH
      YBXHXJXLYOYKUHYQYAXEXTHZIXTXEUIZYQYRIJZXEBUJZXTUKZKZYQXEUUAXGUKZBULNNZUUB
      YQXEXIUUEXHXJXLYOYKUMYQXBUUDUUAKZYNXIUUDKZXIUUEKXBYGYJYPUNZUUFYQUUAXGUOUP
      YKYLYNXMUQYQXIXGHZIJZUUGXMUUJYKYOXLXHUUJXJXLUUJXKUUIIXGXIUIURVEUSUTYQXIUU
      AKZUUJUUGQYHYOUUKYJXMXBYNUUKYGYLXIBUUAUUAVAZVBVCRXIXGUUAVDSVFUUDBXIUUAUUL
      VGVHVIYQXBXGUUAKZUUEUUBJUUHYHYOUUMYJXMXBYLUUMYGYNXGBUUAUULVBZRRXGBUUAUULV
      JVKVPYQXEUUAKZYTUUCQZYKUUOYPXBYIUUOYGXFXEBUUAUULVLZRVMXEXTUUAVDZSVNVQVOTT
      VRYKYLYCXNYKYLYCPZPZUUBBFZXHUUCXGUUBHZIJZXNUUTXBUUMUVAXBYGYJUUSUNZYHYLUUM
      YJYCXBYLUUMYGUUNVSRZXGBUUAUULVTVKYKYLXHYBWAYKYLYCUUCYMXHYBUUCYKYLXHYBUUCM
      YKYLXHPZPZYBUUCYBYTUVGUUCYAYRIYSURYKUUPUVFXBYIUUPYGXFXBYIPUUOUUPUUQUURSRV
      MWBWCTWDWEUUTUVBIKUVCUUTUVBXTUUBHZIUUTXGXTKZUVBUVHKUUTXBUUMUVIUVDUVEXGBUU
      AUULWFVKXGXTUUBWGSXTUUAWHWIUVBWPSXMXHUUCUVCUAEUUBBXIUUBJZXJUUCXLUVCXHXIUU
      BXEWJUVJXKUVBIXIUUBXGWKWLWMWNWOTWQWRTWSWTWTXA $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d o p J $.
    $( A normal space is a topological space.  (Contributed by Jeff Hankins,
       1-Feb-2010.) $)
    nrmtop $p |- ( J e. Nrm -> J e. Top ) $=
      ( vc vd vo vp cnrm wcel ctop cv cin c0 wceq wss w3a wrex wi ccld cfv wral
      isnrm simplbi ) AFGAHGBIZCIZJKLUBDIZMUCEIZMUDUEJKLNEAODAOPCAQRZSBUFSDAEBC
      TUA $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d o p C $.  $d c d o p D $.  $d c d o p J $.
    $( In a normal space, disjoint closed sets are separated by open sets.
       (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    nrmsep $p |- ( ( J e. Nrm /\ ( C e. ( Clsd ` J ) /\ D e. ( Clsd ` J ) /\ (
    C i^i D ) = (/) ) ) -> E. o e. J E. p e. J ( C C_ o /\ D C_ p /\ ( o i^i p
    ) = (/) ) ) $=
      ( vc vd wcel cin c0 wceq w3a cv wss wrex wi wral eqeq1d sseq1 2rexbidv wa
      cnrm ccld isnrm simprbi ineq1 3anbi1d imbi12d ineq2 3anbi2d rcla42v com12
      cfv ctop exp3a 3impd syl imp ) DUBHZADUCUMZHZBUTHZABIZJKZLZACMZNZBEMZNZVF
      VHIJKZLZEDOCDOZUSFMZGMZIZJKZVMVFNZVNVHNZVJLZEDOCDOZPZGUTQFUTQZVEVLPUSDUNH
      WBCDEFGUDUEWBVAVBVDVLWBVAVBVDVLPZVAVBUAWBWCWAWCAVNIZJKZVGVRVJLZEDOCDOZPFG
      ABUTUTVMAKZVPWEVTWGWHVOWDJVMAVNUFRWHVSWFCEDDWHVQVGVRVJVMAVFSUGTUHVNBKZWEV
      DWGVLWIWDVCJVNBAUIRWIWFVKCEDDWIVRVIVGVJVNBVHSUJTUHUKULUOUPUQUR $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d o C $.  $d c d o D $.  $d c d o J $.
    $( In a normal space, any two disjoint closed sets have the property that
       each one is a subset of an open set whose closure is disjoint from the
       other.  (Contributed by Jeff Hankins, 1-Feb-2010.) $)
    nrmsep2 $p |- ( ( J e. Nrm /\ ( C e. ( Clsd ` J ) /\ D e. ( Clsd ` J ) /\ (
    C i^i D ) = (/) ) ) -> E. o e. J ( C C_ o /\ ( ( ( cls ` J ) ` o ) i^i D )
    = (/) ) ) $=
      ( vc vd wcel cfv cin c0 wceq cv wss wa wrex wral eqeq1d rexbidv imbi12d
      wi cnrm ccld w3a ctop nrmtop isnrm2 biimpd mpcom ineq1 sseq1 anbi1d ineq2
      ccl anbi2d rcla42v com12 exp3a 3impd syl imp ) DUAGZADUBHZGZBVBGZABIZJKZU
      CZACLZMZVHDUMHHZBIZJKZNZCDOZVAELZFLZIZJKZVOVHMZVJVPIZJKZNZCDOZTZFVBPEVBPZ
      VGVNTDUDGZVAWEDUEWFVAWECDEFUFUGUHWEVCVDVFVNWEVCVDVFVNTZVCVDNWEWGWDWGAVPIZ
      JKZVIWANZCDOZTEFABVBVBVOAKZVRWIWCWKWLVQWHJVOAVPUIQWLWBWJCDWLVSVIWAVOAVHUJ
      UKRSVPBKZWIVFWKVNWMWHVEJVPBAULQWMWJVMCDWMWAVLVIWMVTVKJVPBVJULQUNRSUOUPUQU
      RUSUT $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d a b c d k p q r s t u v x y B $.  $d a b c d k p q r s t u v x y C $.
    $d a b c d k p q r s t u v x y D $.  $d a b c d k p q r s t u v x y J $.
    $d a r s t x y P $.  $d k t x y Q $.
    metnrm.0 $e |- J = ( MetOpen ` D ) $.
    $( Lemma for ~ metnrm . $)
    metnrmlem1 $p |- { r e. RR | ( 0 < r /\ ( r = 1 \/ ( ( P ( ball ` D ) ( 2
    x. r ) ) i^i C ) =/= (/) ) ) } =/= (/) $=
      ( cc0 clt wbr c1 wceq c2 cmul co cin c0 wne wo wa cr cv cbl cfv crab wrex
      wcel 1re lt01 eqid orci pm3.2i breq2 opreq2 opreq2d ineq1d neeq1d orbi12d
      eqeq1 anbi12d rcla4ev mp2an rabn0 mpbir ) GEUAZHIZVDJKZCLVDMNZBUBUCZNZAOZ
      PQZRZSZETUDPQVMETUEZJTUFGJHIZJJKZCLJMNZVHNZAOZPQZRZSZVNUGVOWAUHVPVTJUIUJU
      KVMWBEJTVFVEVOVLWAVDJGHULVFVFVPVKVTVDJJURVFVJVSPVFVIVRAVFVGVQCVHVDJLMUMUN
      UOUPUQUSUTVAVMETVBVC $.
      $( [1-Sep-2013] $)

    $( Lemma for ~ metnrm . $)
    metnrmlem2 $p |- sup ( { r e. RR | ( 0 < r /\ ( r = 1 \/ ( ( P ( ball ` D )
    ( 2 x. r ) ) i^i C ) =/= (/) ) ) } , RR , `' < ) e. RR $=
      ( vs vt cc0 cv clt wbr c1 wceq c2 cmul co c0 cr wcel cbl cfv cin wne crab
      wo wss cle wral wrex ccnv csup ssrab2 metnrmlem1 0re breq2 opreq2 opreq2d
      wa eqeq1 ineq1d neeq1d orbi12d anbi12d elrab ltle mpan adantrr sylbi rgen
      wi imp breq1 ralbidv rcla4ev mp2an infmrcl mp3an ) IEJZKLZVSMNZCOVSPQZBUA
      UBZQZAUCZRUDZUFZUSZESUEZSUGWIRUDGJZHJZUHLZHWIUIZGSUJZWISKUKULSTWHESUMABCD
      EFUNISTZIWKUHLZHWIUIZWNUOWPHWIWKWITWKSTZIWKKLZWKMNZCOWKPQZWCQZAUCZRUDZUFZ
      USZUSWPWHXFEWKSVSWKNZVTWSWGXEVSWKIKUPXGWAWTWFXDVSWKMUTXGWEXCRXGWDXBAXGWBX
      ACWCVSWKOPUQURVAVBVCVDVEWRWSWPXEWRWSWPWOWRWSWPVKUOIWKVFVGVLVHVIVJWMWQGISW
      JINWLWPHWIWJIWKUHVMVNVOVPGHWIVQVR $.
      $( [2-Sep-2013] $)

    $( Lemma for ~ metnrm . $)
    metnrmlem3 $p |- ( ( ( ( D e. Met /\ J e. Top ) /\ ( B e. ( Clsd ` J )
    /\ C e. ( Clsd ` J ) /\ ( B i^i C ) = (/) ) ) /\ P e. B ) -> 0 < sup ( { r
    e. RR | ( 0 < r /\ ( r = 1 \/ ( ( P ( ball ` D ) ( 2 x. r ) ) i^i C ) =/=
    (/) ) ) } , RR , `' < ) ) $=
      ( va wcel wa c0 wceq wss cc0 clt wbr c1 c2 co cr vs cme ctop ccld cfv cin
      vt w3a cuni cdif cbl crn wrex cmul wne crab ccnv csup simplll eqid cldopn
      cv wo adantll 3ad2antr2 adantr wn cldss 3ad2antr1 sseld imp n0i sylbir ex
      elin con2d impcom 3ad2antl3 eldif sylanbrc opni syl111anc cdm wb ad2antrr
      uniopn2 sseqtrd blssex syl11anc simpll simprl simprrl 2re pm3.2i syl22anc
      ssblex mp3an2 0re a1i rehalfcl ad2antrl metnrmlem2 halfpos2 biimpa adantl
      2pos cdiv wral ssrab2 metnrmlem1 breq2 eqeq1 opreq2 opreq2d ineq1d neeq1d
      cle orbi12d anbi12d elrab wi ltdiv1 mp3an23 2cn ltle sylancl sylbid lenlt
      1re ad2antlr jca adantlr ad2ant2lr syl31anc ssrin ad2antll exp32 rexlimdv
      ancoms mpd 2ne0 dividi breq2i syl6bb mpbid notbid syl5ibrcom remulcl mpan
      breq1 mulgt0 mpanl12 ltmuldiv2 mp3an3 sylan sylbird syl 3ad2ant3 ssdifin0
      ssbl sstrd ss0 syl6 necon3ad jaod expr imp3a impr mpbird ralrimiv lbleinf
      id syl5bi ltletrd ) CUBIZEUCIZJZAEUDUEZIZBUVRIZABUFZKLZUHZJZDAIZJZDHVBZIU
      WGEUIZBUJZMJHCUKUEZULUMZNNFVBZOPZUWLQLZDRUWLUNSZUWJSZBUFZKUOZVCZJZFTUPZTO
      UQURZOPZUWFUVOUWIEIZDUWIIZUWKUVOUVPUWCUWEUSZUWDUXDUWEUVQUVSUVTUXDUWBUVPUV
      TUXDUVOBEUWHUWHUTZVAVDVEVFUWFDUWHIZDBIZVGZUXEUWDUWEUXHUWDAUWHDUVQUVTUVSAU
      WHMZUWBUVPUVSUXKUVOAEUWHUXGVHVDVIZVJVKUWCUWEUXJUVQUWBUVSUWEUXJUVTUWEUWBUX
      JUWEUXIUWBUWEUXIUWBVGZUWEUXIJDUWAIUXMDABVOUWADVLVMVNVPVQVRVDDUWHBVSVTHUWI
      CDEGWAWBUWFUWKNUAVBZOPZDUXNUWJSZUWIMZJZUATUMZUXCUWFUVODCWCWCZIZUWKUXSWDUX
      FUWDUWEUYAUWDAUXTDUWDAUWHUXTUXLUVOUWHUXTLUVPUWCCEUXTUXTUTZGWFWEWGVJVKZHUA
      UWICDUXTUYBWHWIUWFUXRUXCUATUWFUXNTIZUXRUXCUWFUYDUXRJZJZNUGVBZOPZUYGROPZDU
      YGUWJSZUXPMZUHZUGTUMZUXCUYFUVOUYAUYDUXOUYMUWDUVOUWEUYEUVOUVPUWCWJWEUWFUYA
      UYEUYCVFUWFUYDUXRWKUWFUYDUXOUXQWLUVOUYAJZRTIZNROPZJZUYDUXOJUYMUYOUYPWMXFW
      NZUGCDRUXNUXTUYBWPWQWOUYFUYLUXCUGTUYFUYGTIZUYLUXCUYFUYSUYLJZJZNUYGRXGSZUX
      BNTIVUAWRWSUYSVUBTIZUYFUYLUYGWTZXAZUXBTIVUABCDEFGXBWSUYTNVUBOPZUYFUYSUYIU
      YHVUFUYKUYSUYHVUFUYGXCXDVIXEVUAUXATMZUXAKUOZVUCVUBUWGXQPZHUXAXHVUBUXBXQPV
      UGVUAUWTFTXIWSVUHVUABCDEFGXJWSVUEVUAVUIHUXAUWGUXAIUWGTIZNUWGOPZUWGQLZDRUW
      GUNSZUWJSZBUFZKUOZVCZJZJZVUAVUIUWTVURFUWGTUWLUWGLZUWMVUKUWSVUQUWLUWGNOXKV
      UTUWNVULUWRVUPUWLUWGQXLVUTUWQVUOKVUTUWPVUNBVUTUWOVUMDUWJUWLUWGRUNXMXNXOXP
      XRXSXTVUAVUSVUIVUAVUSJZVUIUWGVUBOPZVGZVUAVUJVURVVCVUAVUJJVUKVUQVVCVUAVUJV
      UKVUQVVCYAVUAVUJVUKJZJZVULVVCVUPUYTVULVVCYAZUYFVVDUYSUYHUYIVVFUYKUYSUYIJZ
      VVCVULQVUBOPZVGZVVGVUBQXQPZVVIUYSUYIVVJUYSUYIVUBQOPZVVJUYSUYIVUBRRXGSZOPZ
      VVKUYSUYOUYQUYIVVMWDWMUYRUYGRRYBYCVVLQVUBORYDUUAUUBUUCUUDUYSVUCQTIZVVKVVJ
      YAVUDYIVUBQYEYFYGVKUYSVVJVVIWDZUYIUYSVUCVVNVVOVUDYIVUBQYHYFVFUUEVULVVBVVH
      UWGQVUBOUUJUUFUUGVEYJVVEVVBVUOKVVEVVBVUOKMZVUOKLVVEVVBVVPVVEVVBJZVUOUYJBU
      FZKVVQVUNUYJMZVUOVVRMVVQUYNVUMTIZNVUMOPZJZUYSUYHJZVUMUYGXQPZVVSVUAUYNVVDV
      VBUWFUYNUYEUYTUWFUVOUYAUXFUYCYKWEWEVVDVWBVUAVVBVVDVVTVWAVUJVVTVUKUYOVUJVV
      TWMRUWGUUHUUIZVFUYOUYPVVDVWAWMXFRUWGUUKUULYKYJVUAVWCVVDVVBUYTVWCUYFUYSUYI
      UYHVWCUYKVWCUVLVIXEWEVVEVVBVWDVVEVVBVUMUYGOPZVWDUYTVUJVWFVVBWDZUYFVUKUYSV
      UJVWGUYLVUJUYSVWGVUJUYSUYQVWGUYRUWGUYGRUUMUUNYSYLYMUYTVUJVWFVWDYAZUYFVUKU
      YSVUJVWHUYLVUJUYSVWHVUJVVTUYSVWHVWEVUMUYGYEUUOYSYLYMUUPVKCDVUMUYGUXTUYBUU
      TYNVUNUYJBYOUUQVVQVVRUXPBUFZKVUAVVRVWIMZVVDVVBUYLVWJUYFUYSUYKUYHVWJUYIUYJ
      UXPBYOUURYPWEVUAVWIKLZVVDVVBUYEVWKUWFUYTUXQVWKUYDUXOUXPUWHBUUSYPYJWEWGUVA
      VNVUOUVBUVCUVDUVEUVFUVGUVHVVAVUCVUJVUIVVCWDUYTVUCUYFVUSUYSVUCUYLVUDVFYJVU
      AVUJVURWKVUBUWGYHWIUVIVNUVMUVJHUXAVUBUVKYNUVNYQYRYTYQYRYGYT $.
      $( [1-Sep-2013] $)

    $( Lemma for ~ metnrm . $)
    metnrmlem4 $p |- ( ( ( D e. Met /\ J e. Top ) /\ ( B e. ( Clsd ` J ) /\ C
    e. ( Clsd ` J ) /\ ( B i^i C ) = (/) ) ) -> U. { a | E. p e. B a = ( p (
    ball ` D ) sup ( { r e. RR | ( 0 < r /\ ( r = 1 \/ ( ( p ( ball ` D ) ( 2
    x. r ) ) i^i C ) =/= (/) ) ) } , RR , `' < ) ) } e. J ) $=
      ( wcel wa cfv cin c0 wceq cv cc0 clt wbr co cr cme ctop ccld w3a cmul cbl
      c1 c2 wne wo crab ccnv csup wrex cab wss cuni simplr simplll eqid adantll
      cdm cldss 3ad2antr1 uniopn2 sseqtrd sseld imp metnrmlem2 metnrmlem3 blopn
      ad2antrr a1i syl22anc eleq1 syl5ibrcom rexlimdva abssdv uniopn syl11anc )
      CUAIZDUBIZJZADUCKZIZBWDIZABLMNZUDZJZWBGOZFOZPEOZQRWLUGNWKUHWLUESCUFKZSBLM
      UIUJJETUKTQULUMZWMSZNZFAUNZGUOZDUPWRUQDIWAWBWHURWIWQGDWIWPWJDIZFAWIWKAIZJ
      ZWSWPWODIZXAWAWKCVBVBZIZWNTIZPWNQRXBWAWBWHWTUSWIWTXDWIAXCWKWIADUQZXCWCWFW
      EAXFUPZWGWBWEXGWAADXFXFUTVCVAVDWAXFXCNWBWHCDXCXCUTZHVEVLVFVGVHXEXABCWKDEH
      VIVMABCWKDEHVJCWKWNDXCXHHVKVNWJWODVOVPVQVRWRDVSVT $.
      $( [1-Sep-2013] $)

    $( Lemma for ~ metnrm . $)
    metnrmlem5 $p |- ( ( ( D e. Met /\ J e. Top ) /\ ( B e. ( Clsd ` J ) /\ C
    e. ( Clsd ` J ) /\ ( B i^i C ) = (/) ) ) -> B C_ U. { a | E. p e. B a = ( p
    ( ball ` D ) sup ( { r e. RR | ( 0 < r /\ ( r = 1 \/ ( ( p ( ball ` D ) ( 2
    x. r ) ) i^i C ) =/= (/) ) ) } , RR , `' < ) ) } ) $=
      ( vq wcel wa cfv cin c0 wceq cv clt co cr eqid cme ctop w3a cc0 wbr c1 c2
      ccld cmul cbl wne wo crab ccnv csup wrex cab cuni wex cdm simplll adantll
      wss cldss uniopn2 ad2antrr sseqtrd 3ad2antr1 metnrmlem3 metnrmlem2 blcntr
      imp mpanr1 syl21anc id opreq1 ineq1d neeq1d orbi2d anbi2d rabbidv supeq1d
      sseld opreq12d eqeq2d rcla4ev mpan2 adantl oprex rexbidv anbi12d syl11anc
      eleq2 eqeq1 cla4ev eluniab sylibr ex ssrdv ) CUAJZDUBJZKZADUHLZJZBXCJZABM
      NOZUCZKZIAGPZFPZUDEPZQUEZXKUFOZXJUGXKUIRZCUJLZRZBMZNUKZULZKZESUMZSQUNZUOZ
      XORZOZFAUPZGUQURZXHIPZAJZYHYGJZXHYIKZYHXIJZYFKZGUSZYJYKYHYHXLXMYHXNXORZBM
      ZNUKZULZKZESUMZSYBUOZXORZJZUUBYDOZFAUPZYNYKWTYHCUTUTZJZUDUUAQUEZUUCWTXAXG
      YIVAXHYIUUGXHAUUFYHXBXEXDAUUFVCXFXBXDKADURZUUFXAXDAUUIVCWTADUUIUUITVDVBWT
      UUIUUFOXAXDCDUUFUUFTZHVEVFVGVHWCVLABCYHDEHVIWTUUGKUUASJUUHUUCBCYHDEHVJCYH
      UUAUUFUUJVKVMVNYIUUEXHYIUUBUUBOZUUEUUBTUUDUUKFYHAXJYHOZYDUUBUUBUULXJYHYCU
      UAXOUULVOUULSYAYTYBUULXTYSESUULXSYRXLUULXRYQXMUULXQYPNUULXPYOBXJYHXNXOVPV
      QVRVSVTWAWBWDWEWFWGWHYMUUCUUEKGUUBYHUUAXOWIXIUUBOZYLUUCYFUUEXIUUBYHWMUUMY
      EUUDFAXIUUBYDWNWJWKWOWLYFGYHWPWQWRWS $.
      $( [2-Sep-2013] $)

    $( Lemma for ~ metnrm . $)
    metnrmlem6 $p |- ( ( ( ( ( ( D e. Met /\ J e. Top ) /\ ( B e. ( Clsd ` J )
    /\ C e. ( Clsd ` J ) /\ ( B i^i C ) = (/) ) ) /\ P e. B ) /\ ( X e. ( P (
    ball ` D ) sup ( { r e. RR | ( 0 < r /\ ( r = 1 \/ ( ( P ( ball ` D ) ( 2
    x. r ) ) i^i C ) =/= (/) ) ) } , RR , `' < ) ) /\ X e. ( Q ( ball ` D ) sup
    ( { k e. RR | ( 0 < k /\ ( k = 1 \/ ( ( Q ( ball ` D ) ( 2 x. k ) ) i^i B )
    =/= (/) ) ) } , RR , `' < ) ) ) ) /\ Q e. C ) -> ( sup ( { r e. RR | ( 0 <
    r /\ ( r = 1 \/ ( ( P ( ball ` D ) ( 2 x. r ) ) i^i C ) =/= (/) ) ) } , RR
    , `' < ) <_ sup ( { k e. RR | ( 0 < k /\ ( k = 1 \/ ( ( Q ( ball ` D ) ( 2
    x. k ) ) i^i B ) =/= (/) ) ) } , RR , `' < ) -> sup ( { k e. RR | ( 0 < k
    /\ ( k = 1 \/ ( ( Q ( ball ` D ) ( 2 x. k ) ) i^i B ) =/= (/) ) ) } , RR ,
    `' < ) < sup ( { k e. RR | ( 0 < k /\ ( k = 1 \/ ( ( Q ( ball ` D ) ( 2 x.
    k ) ) i^i B ) =/= (/) ) ) } , RR , `' < ) ) ) $=
      ( wcel wa wceq cc0 clt wbr c2 cmul co cr vt vx vy cme ctop cfv cin c0 w3a
      ccld cv c1 cbl wne wo crab ccnv csup cle caddc metnrmlem2 a1i cdm simplll
      cdiv ad2antrr cuni wss eqid cldss adantll 3ad2antr2 uniopn2 sseqtrd sseld
      imp adantlr ad2ant2r 3ad2antr1 metcl syl111anc rehalfcl readdcl mpan 3syl
      syl simp2 simp1 incom eqeq1i biimpi 3ad2ant3 metnrmlem3 sylanl2 metge0 wb
      3jca halfnneg2 addgtge0 mpanl1 syl12anc halfpos2 mpanr1 syl21anc simprbda
      mpbid elbl adantrr adantr syl11anc readdcli jca elbl2 syl31anc wi lelttrd
      syl6breqr 2re recn 2cn mp3an12 2ne0 divcan2 mp3an23 opreq2d 2pos sylanbrc
      cc mpbird breq2 eqeq1 opreq2 ineq1d neeq1d orbi12d anbi12d elrab wral 0re
      ltdivmul mettri3 simplrr simplrl simprr ltletrd lt2add mpanr12 sylc recni
      syl13anc 2timesi remulcli ltadd1 mp3an2 anidms 2times adddi eqtrd 3brtr4d
      pm3.2i ltmul2 mp3an3 breqtrrd eqeltrd mulgt0 mpanl12 simpllr elin cbvrabv
      syl32anc ne0i olcd eleq2i bitri wrex ssrab2 ltle sylbi rgen breq1 ralbidv
      rcla4ev mp2an infle ltadd2 expr ) CUDKZGUEKZLZAGUJUFZKZBUWJKZABUGZUHMZUIZ
      LZDAKZLZHDNIUKZOPUWSULMDQUWSRSCUMUFZSBUGUHUNUOLITUPTOUQZURZUWTSKZHENFUKZO
      PZUXDULMZEQUXDRSZUWTSZAUGZUHUNZUOZLZFTUPZTUXAURZUWTSKZLZLZEBKZUXBUXNUSPZU
      XNUXNOPUXQUXRUXSLZLZUXNUXNEDCSZQVESZUTSZQVESZUXNUXNTKZUYAACEGFJVAZVBZUYAU
      YCTKZUYDTKZUYETKZUYAUYBTKZUYIUYAUWGECVCVCZKZDUYMKZUYLUWRUWGUXPUXTUWGUWHUW
      OUWQVDZVFZUWRUXRUYNUXPUXSUWPUXRUYNUWQUWPUXRUYNUWPBUYMEUWPBGVGZUYMUWIUWKUW
      LBUYRVHZUWNUWHUWLUYSUWGBGUYRUYRVIZVJVKVLUWGUYRUYMMUWHUWOCGUYMUYMVIZJVMVFZ
      VNVOVPVQVRZUWRUYOUXPUXTUWPUWQUYOUWPAUYMDUWPAUYRUYMUWIUWLUWKAUYRVHZUWNUWHU
      WKVUDUWGAGUYRUYTVJVKVSVUBVNVOVPZVFZEDCUYMVUAVTWAZUYBWBZWFZUYFUYIUYJUYGUXN
      UYCWCWDZUYDWBWEZUYHUYAUYEUXMKZUXNUYEUSPZUYAUYKNUYEOPZUYEULMZEQUYERSZUWTSZ
      AUGZUHUNZUOZLZVULVUKUYAVUNVUTUYANUYDOPZVUNUYAUYINUXNOPZNUYCUSPZVVBVUIUWRU
      XRVVCUXPUXSUWPUXRVVCUWQUWOUWIUWLUWKBAUGZUHMZUIUXRVVCUWOUWLUWKVVFUWKUWLUWN
      WGUWKUWLUWNWHUWNUWKVVFUWLUWNVVFUWMVVEUHABWIWJWKWLWQBACEGFJWMWNVQVRZUYANUY
      BUSPZVVDUYAUWGUYNUYOVVHUYQVUCVUFEDCUYMVUAWOWAUYAUYLVVHVVDWPVUGUYBWRWFXFUY
      FUYIVVCVVDLVVBUYGUXNUYCWSWTXAUYAUYIUYJVVBVUNWPVUIVUJUYDXBWEXFZUYAVUSVUOUY
      ADVURKZVUSUYADVUQKZUWQVVJUYAVVKUYBVUPOPZUYAUYBUYDVUPOUYAUYBUYDOPZQUYBRSZQ
      UYDRSZOPZUYAUYBUYBUTSZQUXNRSZUYBUTSZVVNVVOOUYAUYBVVROPZVVQVVSOPZUYAUYBUXN
      UXNUTSZVVROUYAUYBEHCSZDHCSZUTSZVWBVUGUYAVWCTKZVWDTKZVWETKUYAUWGUYNHUYMKZV
      WFUYQVUCUXQVWHUXTUWRUXCVWHUXOUWRUXCVWHVWDUXBOPZUWRUWGUYONUXBOPZUXCVWHVWIL
      WPZUYPVUEABCDGIJWMZUWGUYOLUXBTKZVWJVWKBCDGIJVAZHCDUXBUYMVUAXGXCXDXEXHXIZE
      HCUYMVUAVTWAZUYAUWGUYOVWHVWGUYQVUFVWODHCUYMVUAVTWAZVWCVWDWCXJVWBTKUYAUXNU
      XNUYGUYGXKVBUYAUWGUYNUYOVWHUYBVWEUSPUYQVUCVUFVWOEDHCUYMVUAUUAUUJUYAVWFVWG
      LZVWCUXNOPZVWDUXNOPZLZVWEVWBOPZUYAVWFVWGVWPVWQXLUYAVWSVWTUYAUXOVWSUWRUXCU
      XOUXTUUBUYAUWGUYNVWHVVCUXOVWSWPZUYQVUCVWOVVGUWGUYNVWHUIUYFVVCVXCUYGHCEUXN
      UYMVUAXMXCXNXFUYAVWDUXBUXNVWQVWMUYAVWNVBUYHUYAUXCVWIUWRUXCUXOUXTUUCUYAUWG
      UYOVWHVWJUXCVWIWPZUYQVUFVWOUWRVWJUXPUXTVWLVFUWGUYOVWHUIVWMVWJVXDVWNHCDUXB
      UYMVUAXMXCXNXFUXQUXRUXSUUDUUEXLVWRUYFUYFVXAVXBXOUYGUYGVWCVWDUXNUXNUUFUUGU
      UHXPUXNUXNUYGUUIZUUKZXQZUYAUYLVVTVWAWPZVUGUYLVXHUYLVVRTKUYLVXHQUXNXRUYGUU
      LUYBVVRUYBUUMUUNUUOWFXFUYAUYLUYBYHKZVVNVVQMVUGUYBXSZUYBUUPWEUYAVVOVVRQUYC
      RSZUTSZVVSUYAUYIUYCYHKZVVOVXLMZVUIUYCXSQYHKZUXNYHKVXMVXNXTVXEQUXNUYCUUQYA
      WEUYAVXKUYBVVRUTUYAUYLVXIVXKUYBMZVUGVXJVXIVXOQNUNZVXPXTYBUYBQYCYDWEYEUURU
      USUYAUYLUYJVVMVVPWPZVUGUYAUYLUYIUYJVUGVUHVUJWEZUYLUYJQTKZNQOPZLZVXRVXTVYA
      XRYFUUTZUYBUYDQUVAUVBXJYIUYAUYJUYDYHKZVUPUYDMZVXSUYDXSVYDVXOVXQVYEXTYBUYD
      QYCYDWEZUVCUYAUWGUYNUYOVUPTKNVUPOPZVVKVVLWPUYQVUCVUFUYAVUPUYDTVYFVXSUVDUY
      AUYKVUNVYGVUKVVIVXTVYAUYKVUNLVYGXRYFQUYEUVEUVFXJDCEVUPUYMVUAXMUVJYIUWPUWQ
      UXPUXTUVGDVUQAUVHYGVURDUVKWFUVLXLVULUYENUAUKZOPZVYHULMZEQVYHRSZUWTSZAUGZU
      HUNZUOZLZUATUPZKUYKVVALUXMVYQUYEUXLVYPFUATUXDVYHMZUXEVYIUXKVYOUXDVYHNOYJV
      YRUXFVYJUXJVYNUXDVYHULYKVYRUXIVYMUHVYRUXHVYLAVYRUXGVYKEUWTUXDVYHQRYLYEYMY
      NYOYPUVIUVMVYPVVAUAUYETVYHUYEMZVYIVUNVYOVUTVYHUYENOYJVYSVYJVUOVYNVUSVYHUY
      EULYKVYSVYMVURUHVYSVYLVUQAVYSVYKVUPEUWTVYHUYEQRYLYEYMYNYOYPYQUVNYGUXMTVHU
      BUKZUCUKZUSPZUCUXMYRZUBTUVOZVULVUMUXLFTUVPNTKZNWUAUSPZUCUXMYRZWUDYSWUFUCU
      XMWUAUXMKWUATKZNWUAOPZWUAULMZEQWUARSZUWTSZAUGZUHUNZUOZLZLWUFUXLWUPFWUATUX
      DWUAMZUXEWUIUXKWUOUXDWUANOYJWUQUXFWUJUXJWUNUXDWUAULYKWUQUXIWUMUHWUQUXHWUL
      AWUQUXGWUKEUWTUXDWUAQRYLYEYMYNYOYPYQWUHWUIWUFWUOWUHWUIWUFWUEWUHWUIWUFXOYS
      NWUAUVQWDVPXHUVRUVSWUCWUGUBNTVYTNMWUBWUFUCUXMVYTNWUAUSUVTUWAUWBUWCUBUCUYE
      UXMUWDYAWFUYAUYEUXNOPZUYDVVROPZUYAUYDVWBVVROUYAUYCUXNOPZUYDVWBOPZUYAWUTVV
      TVXGUYAUYLWUTVVTWPZVUGUYLUYFVYBWVBUYGVYCUYBUXNQYTYDWFYIUYAUYLUYIWUTWVAWPZ
      VUGVUHUYIUYFUYFWVCUYGUYGUYCUXNUXNUWEYDWEXFVXFXQUYAUYJWURWUSWPZVXSUYJUYFVY
      BWVDUYGVYCUYDUXNQYTYDWFYIXPUWF $.
      $( [15-Sep-2013] $)

    $( A metric space is normal.  (Contributed by Jeff Hankins,
       31-Aug-2013.) $)
    metnrm $p |- ( D e. Met -> J e. Nrm ) $=
      ( va vp vr vb vq vk vx wcel cv cin c0 wceq wi wa wbr co wn vc vd cme ctop
      vu vv wss w3a wrex ccld cfv wral cnrm opntop cc0 clt c1 c2 cmul cbl wo cr
      wne crab ccnv csup cab cuni metnrmlem4 simp2 simp1 eqeq1i biimpi 3ad2ant3
      incom 3jca sylan2 metnrmlem5 wex wal metnrmlem2 ltnri eleq2 anbi2d anbi1d
      a1i cle letrii metnrmlem6 anim2i ad2antrr anim1i pm3.22 ad2antlr syl21anc
      simpllr orim12d syl6bi com12 mtord ralrimiva exp43 imbi1d imbi2d rexlimdv
      mpi syl5ibr com3l com23 imp3a alrimdv exlimdv alrimiv ianor eluniab bitri
      anbi12i notbii notnot ralnex imbi2i albii alinexa imbi12i bitr4i 3bitr4ri
      elin df-or eq0 sylib sseq2 ineq1 eqeq1d 3anbi13d ineq2 3anbi23d syl113anc
      rcla42ev 3exp2 ralrimivv ex jcai isnrm sylibr ) AUCKZBUDKZUALZUBLZMZNOZUU
      GUELZUGZUUHUFLZUGZUUKUUMMZNOZUHZUFBUIUEBUIZPZUBBUJUKZULUAUUTULZQBUMKUUEUU
      FUVAABCUNUUEUUFUVAUUEUUFQZUUSUAUBUUTUUTUVBUUGUUTKZUUHUUTKZUUSUVBUVCUVDUUJ
      UURUVBUVCUVDUUJUHZQZDLZELZUOFLZUPRUVIUQOUVHURUVIUSSAUTUKZSUUHMNVCVAQFVBVD
      VBUPVEZVFZUVJSZOZEUUGUIZDVGVHZBKGLZHLZUOILZUPRUVSUQOUVRURUVSUSSUVJSUUGMNV
      CVAQIVBVDVBUVKVFZUVJSZOZHUUHUIZGVGVHZBKZUUGUVPUGZUUHUWDUGZUVPUWDMZNOZUURU
      UGUUHABFEDCVIUVEUVBUVDUVCUUHUUGMZNOZUHZUWEUVEUVDUVCUWKUVCUVDUUJVJUVCUVDUU
      JVKUUJUVCUWKUVDUUJUWKUUIUWJNUUGUUHVOVLVMVNVPZUUHUUGABIHGCVIVQUUGUUHABFEDC
      VRUVEUVBUWLUWGUWMUUHUUGABIHGCVRVQUVFJLZUVGKZUVOQZDVSZUWNUVQKZUWBTZHUUHULZ
      PZGVTZPZJVTZUWIUVFUXCJUVFUWPUXBDUVFUWPUXAGUVFUWOUVOUXAUVFUVOUWOUXAUVFUVNU
      WOUXAPZEUUGUVNUVFUVHUUGKZUXEUVFUXFUXEPUVNUXFUWNUVMKZUXAPZPUVFUXFUXGUWRUWT
      UVFUXFQZUXGUWRQZQZUWSHUUHUXKUVRUUHKZQZUWBUVTUVTUPRZUVLUVLUPRZUXNTUXMUVTUU
      GAUVRBICWAZWBWFUXOTUXMUVLUUHAUVHBFCWAZWBWFUWBUXMUXNUXOVAZUWBUXMUXIUXGUWNU
      WAKZQZQZUXLQZUXRUWBUXKUYAUXLUWBUXJUXTUXIUWBUWRUXSUXGUVQUWAUWNWCWDWDWEUYBU
      VLUVTWGRZUVTUVLWGRZVAUXRUVLUVTUXQUXPWHUYBUYCUXNUYDUXOUUGUUHAUVHUVRIBUWNFC
      WIUYBUVBUWLQZUXLQUXSUXGQZUXFUYDUXOPUYAUYEUXLUVFUYEUXFUXTUVEUWLUVBUWMWJWKW
      LUXTUYFUXIUXLUXGUXSWMWNUVFUXFUXTUXLWPUUHUUGAUVRUVHFBUWNICWIWOWQXFWRWSWTXA
      XBUVNUXEUXHUXFUVNUWOUXGUXAUVGUVMUWNWCXCXDXGXHXEXIXJXKXLXMUXDUWNUWHKZTZJVT
      UWIUXCUYHJUWQUWRUWCQGVSZQZTUWQTZUYITZVAZUYHUXCUWQUYIXNUYGUYJUYGUWNUVPKZUW
      NUWDKZQUYJUWNUVPUWDYGUYNUWQUYOUYIUVODUWNXOUWCGUWNXOXQXPXRUXCUYKTZUYLPUYMU
      WQUYPUXBUYLUWQXSUXBUWRUWCTZPZGVTUYLUXAUYRGUWTUYQUWRUWBHUUHXTYAYBUWRUWCGYC
      XPYDUYKUYLYHYEYFYBJUWHYIYEYJUUQUWFUWGUWIUHUWFUUNUVPUUMMZNOZUHUEUFUVPUWDBB
      UUKUVPOZUULUWFUUPUYTUUNUUKUVPUUGYKVUAUUOUYSNUUKUVPUUMYLYMYNUUMUWDOZUUNUWG
      UYTUWIUWFUUMUWDUUHYKVUBUYSUWHNUUMUWDUVPYOYMYPYRYQYSXJYTUUAUUBUEBUFUAUBUUC
      UUD $.
      $( [31-Aug-2013] $)
  $}

