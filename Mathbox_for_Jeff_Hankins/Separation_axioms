$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       Separation axioms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Kol2 $.
  $( Extend class notation with the class of all T0 spaces.  See comments in
     ~ df-t0ALT . $)
  ct0ALT $a class Kol2 $.

  $c Reg $.
  $( Extend class notation with the class of all regular topologies. $)
  creg $a class Reg $.

  $c Nrm $.
  $( Extend class notation with the class of all normal topologies. $)
  cnrm $a class Nrm $.

  ${
    $d j o x y $.
    $( Define T0 or Kolmogorov spaces.  The use of a different symbol for this
       class is to improve compatibility with various Metamath parsers until
       the erroneous definition is removed. $)
    df-t0ALT $a |- Kol2 = { j e. Top | A. x e. U. j A. y e. U. j ( x =/= y ->
    E. o e. j -. ( x e. o <-> y e. o ) ) } $.
  $}

  ${
    $d c j o p x $.
    $( Define regular spaces. $)
    df-reg $a |- Reg = { j e. Top | A. c e. ( Clsd ` j ) A. x e. U. j ( x e/ c
    -> E. o e. j E. p e. j ( c C_ o /\ x e. p /\ ( o i^i p ) = (/) ) ) } $.
  $}

  ${
    $d c d j o p $.
    $( Define normal spaces. $)
    df-nrm $a |- Nrm = { j e. Top | A. c e. ( Clsd ` j ) A. d e. ( Clsd ` j ) (
    ( c i^i d ) = (/) -> E. o e. j E. p e. j ( c C_ o /\ d C_ p /\ ( o i^i p )
    = (/) ) ) } $.
  $}

  ${
    $d j o x y J $.  $d j o x y X $.
    ist0.1 $e |- X = U. J $.
    $( The predicate "is a T0 space."  Every pair of distinct points is
       topologically distinguishable.  For the way this definition is usually
       encountered, see ~ ist0-2 . $)
    ist0 $p |- ( J e. Kol2 <-> ( J e. Top /\ A. x e. X A. y e. X ( x =/= y ->
    E. o e. J -. ( x e. o <-> y e. o ) ) ) ) $=
      ( vj cv wne wcel wb wn wrex wi cuni wral ctop ct0ALT wceq unieq syl6eqr
      rexeq imbi2d raleqbidv df-t0ALT elrab2 ) AHZBHZIZUGCHZJUHUJJKLZCGHZMZNZBU
      LOZPZAUOPUIUKCDMZNZBEPZAEPGDQRULDSZUPUSAUOEUTUODOEULDTFUAZUTUNURBUOEVAUTU
      MUQUIUKCULDUBUCUDUDABGCUEUF $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d j o x y J $.  $d j o x y X $.
    ist0-2.1 $e |- X = U. J $.
    $( The predicate "is a T0 space," expressed in more familiar terms. $)
    ist0-2 $p |- ( J e. Top -> ( J e. Kol2 <-> A. x e. X A. y e. X ( x =/= y ->
    E. o e. J ( ( x e. o /\ -. y e. o ) \/ ( -. x e. o /\ y e. o ) ) ) ) ) $=
      ( ctop wcel ct0ALT cv wne wb wn wrex wi wral wa wo ist0 baib xor ancom
      orbi2i bitri rexbii imbi2i 2ralbii syl6bb ) DGHZDIHZAJZBJZKZUKCJZHZULUNHZ
      LMZCDNZOZBEPAEPZUMUOUPMQZUOMZUPQZRZCDNZOZBEPAEPUJUIUTABCDEFSTUSVFABEEURVE
      UMUQVDCDUQVAUPVBQZRVDUOUPUAVGVCVAUPVBUBUCUDUEUFUGUH $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o x y J $.
    $( A T0 space is a topological space. $)
    t0top $p |- ( J e. Kol2 -> J e. Top ) $=
      ( vx vy vo ct0ALT wcel ctop cv wne wb wn wrex wi cuni wral eqid ist0
      simplbi ) AEFAGFBHZCHZISDHZFTUAFJKDALMCANZOBUBOBCDAUBUBPQR $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o x y A $.  $d o x y B $.  $d o x y J $.  $d o x y X $.
    t0dist.1 $e |- X = U. J $.
    $( Any two distinct points in a T0 space are topologically
       distinguishable. $)
    t0dist $p |- ( ( J e. Kol2 /\ ( A e. X /\ B e. X /\ A =/= B ) ) -> E. o e.
    J -. ( A e. o <-> B e. o ) ) $=
      ( vx vy ct0ALT wcel wne w3a cv wb wn wrex wi wral ctop ist0 simprbi wa
      wceq neeq1 eleq1 bibi1d notbid rexbidv imbi12d neeq2 bibi2d rcla42v
      com12 exp3a 3impd syl imp ) DIJZAEJZBEJZABKZLZACMZJZBVCJZNZOZCDPZURGMZHMZ
      KZVIVCJZVJVCJZNZOZCDPZQZHERGERZVBVHQURDSJVRGHCDEFTUAVRUSUTVAVHVRUSUTVAVHQ
      ZUSUTUBVRVSVQVSAVJKZVDVMNZOZCDPZQGHABEEVIAUCZVKVTVPWCVIAVJUDWDVOWBCDWDVNW
      AWDVLVDVMVIAVCUEUFUGUHUIVJBUCZVTVAWCVHVJBAUJWEWBVGCDWEWAVFWEVMVEVDVJBVCUE
      UKUGUHUIULUMUNUOUPUQ $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o w x y z J $.  $d o w x y z X $.
    ist1-2.1 $e |- X = U. J $.
    $( An alternate characterization of T1 spaces. $)
    ist1-2 $p |- ( J e. Top -> ( J e. Fre <-> A. x e. X A. y e. X ( x =/= y ->
    E. o e. J ( x e. o /\ -. y e. o ) ) ) ) $=
      ( vz vw ctop wcel ct1 cv csn ccld cfv wral wne wn wa wrex wi ist1 baib
      w3a wceq sneq eleq1d rcla4v 3ad2ant3 cdif cldopn 3ad2antl1 adantrr
      simpl2 elsni necon3ai ad2antll eldif sylanbrc visset snid elndif ax-mp
      jctir eleq2 notbid anbi12d rcla4ev syl11anc exp32 syld 3expib com23
      r19.21advv wss cuni eldifn elsn necon3bbii sylib eldifi simp2 neeq1
      elequ1 anbi1d rexbidv imbi12d neeq2 anbi2d rcla42v mpid cin c0 disjsn
      eltopss reldisj biimpd syl syl5bir anim2d reximdva 3expia r19.21adv
      difss sseqtri a1i jctild wb eltop2 adantr sylibrd snssi adantl iscld
      r19.21adva impbid bitrd ) DIJZDKJZGLZMZDNOZJZGEPZALZBLZQZYECLZJZYFYHJZRZS
      ZCDTZUAZBEPAEPZXSXRYDDEGFUBUCXRYDYOXRYDYNABEEXRYEEJZYFEJZSYDYNXRYPYQYDYNU
      AXRYPYQUDZYDYFMZYBJZYNYQXRYDYTUAYPYCYTGYFEXTYFUEYAYSYBXTYFUFUGUHUIYRYTYGY
      MYRYTYGSZSZEYSUJZDJZYEUUCJZYFUUCJZRZSZYMYRYTUUDYGXRYPYTUUDYQYSDEFUKULUMUU
      BUUEUUGUUBYPYEYSJZRZUUEXRYPYQUUAUNYGUUJYRYTUUIYEYFYEYFUOUPUQYEEYSURUSYFYS
      JUUGYFBUTVAYFYSEVBVCVDYLUUHCUUCDYHUUCUEZYIUUEYKUUGYHUUCYEVEUUKYJUUFYHUUCY
      FVEVFVGVHVIVJVKVLVMVNXRYOYCGEXRXTEJZSZYOYAEVOZEYAUJZDJZSZYCUUMYOUUPUUNUUM
      YOUUODVPZVOZHLZYHJZYHUUOVOZSZCDTZHUUOPZSZUUPUUMYOUVEUUSUUMYOUVDHUUOUUMUUT
      UUOJZYOUVDXRUULUVGYOUVDUAXRUULUVGUDZYOUVAXTYHJZRZSZCDTZUVDUVHYOUUTXTQZUVL
      UVGXRUVMUULUVGUUTYAJZRUVMUUTEYAVQUVNUUTXTHXTVRVSVTUIUVHUUTEJZUULYOUVMUVLU
      AZUAUVGXRUVOUULUUTEYAWAUIXRUULUVGWBYNUVPUUTYFQZUVAYKSZCDTZUAABUUTXTEEYEUU
      TUEZYGUVQYMUVSYEUUTYFWCUVTYLUVRCDUVTYIUVAYKAHCWDWEWFWGYFXTUEZUVQUVMUVSUVL
      YFXTUUTWHUWAUVRUVKCDUWAYKUVJUVAUWAYJUVIBGCWDVFWIWFWGWJVIWKUVHUVKUVCCDUVHY
      HDJZSZUVJUVBUVAUVJYHYAWLWMUEZUWCUVBYHXTWNXRUULUWBUWDUVBUAZUVGXRUWBSYHEVOZ
      UWEYHDEFWOUWFUWDUVBYHYAEWPWQWRULWSWTXAVKXBVMXCUUSUUMUUOEUUREYAXDFXEXFXGXR
      UUPUVFXHUULHCUUODXIXJXKUULUUNXRXTEXLXMXGXRYCUUQXHUULYADEFXNXJXKXOXPXQ $.
      $( [31-Jan-2010] $)
  $}

  ${
    $d o w x y z J $.  $d o w x y z X $.
    ist1-3.1 $e |- X = U. J $.
    $( A space is T1 iff every point is the only point in the intersection of
       all open sets containing that point. $)
    ist1-3 $p |- ( J e. Top -> ( J e. Fre <-> A. x e. X |^| { o e. J | x e. o }
    = { x } ) ) $=
      ( vy vz vw ctop wcel ct1 cv wne wn wa wrex wi wral crab cint csn wceq
      ist1-2 necom neeq1 eleq1 anbi1d rexbidv imbi12d neeq2 notbid anbi2d
      rcla42v syl7bi ex adantl com23 imp w3a topopn 3ad2ant1 3simpc eleq2
      anbi12d rcla4ev syl11anc 3expia adantr a1dd pm2.61d rexanali visset
      elintrab notbii bitr4i syl6ib necon4ad elsn syl6ibr ssrdv wss cab wal
      df-rab inteqi sseq2i ssintab bitr2i snssi mpgbi a1i eqssd exp31
      r19.21adv rabbidv inteqd sneq eqeq12d rcla4v 3ad2ant2 elsni eqcomd
      necon3ai syl5ibr idd syl5bi syl9r syld 3expib r19.21advv impbid bitrd )
      CIJZCKJFLZGLZMZXNBLZJZXOXQJZNZOZBCPZQZGDRFDRZALZXQJZBCSZTZYEUAZUBZADRZFGB
      CDEUCXMYDYKXMYDYJADXMYEDJZYDYJXMYLYDYJXMYLOZYDOZYHYIYNHYHYIYNHLZYHJZYOYEU
      BYOYIJYNYPYOYEYNYOYEMZYFYOXQJZNZOZBCPZYPNZYNYODJZYQUUAQZYMYDUUCUUDQYMUUCY
      DUUDYLUUCYDUUDQZQXMYLUUCUUEYQYEYOMZYLUUCOYDUUAYOYEUDYCUUFUUAQYEXOMZYFXTOZ
      BCPZQFGYEYODDXNYEUBZXPUUGYBUUIXNYEXOUEUUJYAUUHBCUUJXRYFXTXNYEXQUFUGUHUIXO
      YOUBZUUGUUFUUIUUAXOYOYEUJUUKUUHYTBCUUKXTYSYFUUKXSYRXOYOXQUFUKULUHUIUMUNUO
      UPUQURYNUUCNZUUAYQYMUULUUAQYDXMYLUULUUAXMYLUULUSDCJZYLUULOZUUAXMYLUUMUULC
      DEUTVAXMYLUULVBYTUUNBDCXQDUBZYFYLYSUULXQDYEVCUUOYRUUCXQDYOVCUKVDVEVFVGVHV
      IVJUUAYFYRQBCRZNUUBYFYRBCVKYPUUPYFBYOCHVLVMVNVOVPVQHYEVRVSVTYIYHWAZYNXQCJ
      ZYFOZYIXQWAZQZUUQBUUQYIUUSBWBZTZWAUVABWCYHUVCYIYGUVBYFBCWDWEWFUUSBYIWGWHY
      FUUTUURYEXQWIUPWJWKWLWMUQWNXMYKYCFGDDXMXNDJZXODJZOYKYCXMUVDUVEYKYCQXMUVDU
      VEUSZYKXRBCSZTZXNUAZUBZYCUVDXMYKUVJQUVEYJUVJAXNDYEXNUBZYHUVHYIUVIUVKYGUVG
      UVKYFXRBCYEXNXQUFWOWPYEXNWQWRWSWTUVJXPXOUVHJZNZUVFYBXPUVMUVJXOUVIJZNUVNXN
      XOUVNXOXNXOXNXAXBXCUVJUVLUVNUVHUVIXOVCUKXDUVMYBUVFYBUVMXRXSQBCRZNYBUVLUVO
      XRBXOCGVLVMVNXRXSBCVKVOUVFYBXEXFXGXHXIUQXJXKXL $.
      $( [31-Jan-2010] $)
  $}

  ${
    $d x J $.
    $( A T1 space is a topological space. $)
    t1top $p |- ( J e. Fre -> J e. Top ) $=
      ( vx ct1 wcel ctop cv csn ccld cfv cuni wral eqid ist1 simplbi ) ACDAEDBF
      GAHIDBAJZKAOBOLMN $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d x A $.  $d x J $.  $d x X $.
    t1sncld.1 $e |- X = U. J $.
    $( In a T1 space, one-point sets are closed. $)
    t1sncld $p |- ( ( J e. Fre /\ A e. X ) -> { A } e. ( Clsd ` J ) ) $=
      ( vx ct1 wcel csn ccld cfv cv wral wi ctop ist1 simprbi wceq sneq eleq1d
      rcla4cv syl imp ) BFGZACGZAHZBIJZGZUCEKZHZUFGZECLZUDUGMUCBNGUKBCEDOPUJUGE
      ACUHAQUIUEUFUHARSTUAUB $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o x y A $.  $d o x y B $.  $d o x y J $.  $d o x y X $.
    t1sep.1 $e |- X = U. J $.
    $( Any two distinct points in a T1 space are separated by an open set. $)
    t1sep $p |- ( ( J e. Fre /\ ( A e. X /\ B e. X /\ A =/= B ) ) -> E. o e. J
    ( A e. o /\ -. B e. o ) ) $=
      ( vx vy ct1 wcel wne w3a cv wn wa wrex wi wral ctop t1top ist1-2 biimpd
      mpcom wceq neeq1 eleq1 anbi1d rexbidv imbi12d neeq2 notbid anbi2d
      rcla42v com12 exp3a 3impd syl imp ) DIJZAEJZBEJZABKZLZACMZJZBVDJZNZOZCDPZ
      USGMZHMZKZVJVDJZVKVDJZNZOZCDPZQZHERGERZVCVIQDSJZUSVSDTVTUSVSGHCDEFUAUBUCV
      SUTVAVBVIVSUTVAVBVIQZUTVAOVSWAVRWAAVKKZVEVOOZCDPZQGHABEEVJAUDZVLWBVQWDVJA
      VKUEWEVPWCCDWEVMVEVOVJAVDUFUGUHUIVKBUDZWBVBWDVIVKBAUJWFWCVHCDWFVOVGVEWFVN
      VFVKBVDUFUKULUHUIUMUNUOUPUQUR $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d o x y J $.
    $( A T1 space is a T0 space. $)
    t1t0 $p |- ( J e. Fre -> J e. Kol2 ) $=
      ( vx vy vo ct1 wcel ct0ALT cv wne wn wa wo wrex wi cuni wral w3a eqid
      t1sep orc reximi syl 3exp2 imp3a r19.21aivv ctop wb t1top ist0-2 mpbird
      ) AEFZAGFZBHZCHZIZUMDHZFZUNUPFZJKZUQJURKZLZDAMZNZCAOZPBVDPZUKVCBCVDVDUKUM
      VDFZUNVDFZVCUKVFVGUOVBUKVFVGUOQKUSDAMVBUMUNDAVDVDRZSUSVADAUSUTTUAUBUCUDUE
      UKAUFFULVEUGAUHBCDAVDVHUIUBUJ $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c j o p x J $.  $d c j o p x X $.
    isreg.1 $e |- X = U. J $.
    $( The predicate "is a regular space."  In a regular space, any closed set
       is separated from any point not in it by neighborhoods.  Note that some
       authors require the space to be Hausdorff (which would make it the same
       as T3), but we reserve the phrase "regular Hausdorff" for that as many
       topologists do. $)
    isreg $p |- ( J e. Reg <-> ( J e. Top /\ A. c e. ( Clsd ` J ) A. x e. X ( x
    e/ c -> E. o e. J E. p e. J ( c C_ o /\ x e. p /\ ( o i^i p ) = (/) ) ) ) )
        $=
      ( vj cv wnel wss wcel cin c0 wceq w3a wrex wi cuni wral ccld cfv ctop
      creg fveq2 unieq syl6eqr rexeq rexeqbi1dv imbi2d raleqbidv df-reg elrab2
      ) AIZFIZJZUOBIZKUNEIZLUQURMNOPZEHIZQZBUTQZRZAUTSZTZFUTUAUBZTUPUSECQZBCQZR
      ZADTZFCUAUBZTHCUCUDUTCOZVEVJFVFVKUTCUAUEVLVCVIAVDDVLVDCSDUTCUFGUGVLVBVHUP
      VAVGBUTCUSEUTCUHUIUJUKUKAHBEFULUM $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c o p x J $.
    $( A regular space is a topological space. $)
    regtop $p |- ( J e. Reg -> J e. Top ) $=
      ( vx vc vo vp creg wcel ctop cv wnel wss cin c0 wceq w3a wrex wi cuni
      wral ccld cfv eqid isreg simplbi ) AFGAHGBIZCIZJUFDIZKUEEIZGUGUHLMNOEAPDA
      PQBARZSCATUASBDAUIECUIUBUCUD $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c o p x A $.  $d c o p x C $.  $d c o p x J $.  $d c o p x X $.
    regsep.1 $e |- X = U. J $.
    $( In a regular space, a closed set is separated by open sets from a point
       not in it. $)
    regsep $p |- ( ( J e. Reg /\ ( C e. ( Clsd ` J ) /\ A e. X /\ A e/ C ) ) ->
    E. o e. J E. p e. J ( C C_ o /\ A e. p /\ ( o i^i p ) = (/) ) ) $=
      ( vx vc creg wcel ccld cfv wnel w3a cv wss cin c0 wceq wrex wi wral ctop
      isreg simprbi wa neleq2 sseq1 3anbi1d 2rexbidv imbi12d neleq1 eleq1
      3anbi2d rcla42v com12 exp3a 3impd syl imp ) DJKZBDLMZKZAEKZABNZOZBCPZQZAF
      PZKZVHVJRSTZOZFDUACDUAZVBHPZIPZNZVPVHQZVOVJKZVLOZFDUACDUAZUBZHEUCIVCUCZVG
      VNUBVBDUDKWCHCDEFIGUEUFWCVDVEVFVNWCVDVEVFVNUBZVDVEUGWCWDWBWDVOBNZVIVSVLOZ
      FDUACDUAZUBIHBAVCEVPBTZVQWEWAWGVPBVOUHWHVTWFCFDDWHVRVIVSVLVPBVHUIUJUKULVO
      ATZWEVFWGVNVOABUMWIWFVMCFDDWIVSVKVIVLVOAVJUNUOUKULUPUQURUSUTVA $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d j o p J $.
    $( The predicate "is a normal space."  Much like the case for regular
       spaces, normal does not imply Hausdorff or even regular. $)
    isnrm $p |- ( J e. Nrm <-> ( J e. Top /\ A. c e. ( Clsd ` J ) A. d e. (
    Clsd ` J ) ( ( c i^i d ) = (/) -> E. o e. J E. p e. J ( c C_ o /\ d C_ p /\
    ( o i^i p ) = (/) ) ) ) ) $=
      ( vj cv cin c0 wceq wss w3a wrex wi ccld cfv wral ctop cnrm fveq2 rexeq
      rexeqbi1dv imbi2d raleqbidv df-nrm elrab2 ) DGZEGZHIJZUGAGZKUHCGZKUJUKHIJ
      LZCFGZMZAUMMZNZEUMOPZQZDUQQUIULCBMZABMZNZEBOPZQZDVBQFBRSUMBJZURVCDUQVBUMB
      OTZVDUPVAEUQVBVEVDUOUTUIUNUSAUMBULCUMBUAUBUCUDUDFACDEUEUF $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d o p J $.
    $( An alternate characterization of normality.  This is the important
       property in the proof of Urysohn's lemma. $)
    isnrm2 $p |- ( J e. Top -> ( J e. Nrm <-> A. c e. ( Clsd ` J ) A. d e. (
    Clsd ` J ) ( ( c i^i d ) = (/) -> E. o e. J ( c C_ o /\ ( ( ( cls ` J ) ` o
    ) i^i d ) = (/) ) ) ) ) $=
      ( vp ctop wcel cnrm cv cin c0 wceq wss w3a wrex wi ccld cfv wral ccl wa
      isnrm baib wb simprr1 incom cuni cdif cnt simprr2 simplll difss a1i
      simprlr eqeq1i biimpi 3ad2ant3 ad2antll eqid eltopss ad2ant2rl ad2ant2r
      reldisj syl mpbid ssntr syl22anc sstrd ntrcmp syl11anc sseqtrd cldss
      adantr mpbird syl5eq jca expr r19.23adv adantlr cmclsopn simprrl syl5bb
      biimpd imp3a impr sscls ssrin difdisj syl6sseq ss0 sseq2 ineq2 eqeq1d
      3anbi23d rcla4ev syl13anc impbid rexbidva pm5.74d ralbidva bitrd ) BFGZBH
      GZCIZDIZJKLZXDAIZMZXEEIZMZXGXIJZKLZNZEBOZABOZPZDBQRZSZCXQSZXFXHXGBTRRZXEJ
      ZKLZUAZABOZPZDXQSZCXQSXCXBXSABECDUBUCXBXRYFCXQXBXDXQGZUAZXPYEDXQYHXEXQGZU
      AXFXOYDYHYIXFXOYDUDYHYIXFUAZUAZXNYCABYKXGBGZUAZXNYCYMXMYCEBYKYLXIBGZXMYCP
      YKYLYNUAZXMYCYKYOXMUAZUAZXHYBXHXJXLYOYKUEYQYAXEXTJZKXTXEUFZYQYRKLZXEBUGZX
      TUHZMZYQXEUUAXGUHZBUIRRZUUBYQXEXIUUEXHXJXLYOYKUJYQXBUUDUUAMZYNXIUUDMZXIUU
      EMXBYGYJYPUKZUUFYQUUAXGULUMYKYLYNXMUNYQXIXGJZKLZUUGXMUUJYKYOXLXHUUJXJXLUU
      JXKUUIKXGXIUFUOUPUQURYQXIUUAMZUUJUUGUDYHYOUUKYJXMXBYNUUKYGYLXIBUUAUUAUSZU
      TVAVBXIXGUUAVCVDVEUUDBXIUUAUULVFVGVHYQXBXGUUAMZUUEUUBLUUHYHYOUUMYJXMXBYLU
      UMYGYNXGBUUAUULUTZVBVBXGBUUAUULVIVJVKYQXEUUAMZYTUUCUDZYKUUOYPXBYIUUOYGXFX
      EBUUAUULVLZVBVMXEXTUUAVCZVDVNVOVPVQVQVRYKYLYCXNYKYLYCUAZUAZUUBBGZXHUUCXGU
      UBJZKLZXNUUTXBUUMUVAXBYGYJUUSUKZYHYLUUMYJYCXBYLUUMYGUUNVSVBZXGBUUAUULVTVJ
      YKYLXHYBWAYKYLYCUUCYMXHYBUUCYKYLXHYBUUCPYKYLXHUAZUAZYBUUCYBYTUVGUUCYAYRKY
      SUOYKUUPUVFXBYIUUPYGXFXBYIUAUUOUUPUUQUURVDVBVMWBWCVQWDWEUUTUVBKMUVCUUTUVB
      XTUUBJZKUUTXGXTMZUVBUVHMUUTXBUUMUVIUVDUVEXGBUUAUULWFVJXGXTUUBWGVDXTUUAWHW
      IUVBWJVDXMXHUUCUVCNEUUBBXIUUBLZXJUUCXLUVCXHXIUUBXEWKUVJXKUVBKXIUUBXGWLWMW
      NWOWPVQWQWRVQWSWTWTXA $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d o p J $.
    $( A normal space is a topological space. $)
    nrmtop $p |- ( J e. Nrm -> J e. Top ) $=
      ( vc vd vo vp cnrm wcel ctop cv cin c0 wceq wss w3a wrex wi ccld cfv
      wral isnrm simplbi ) AFGAHGBIZCIZJKLUBDIZMUCEIZMUDUEJKLNEAODAOPCAQRZSBUFS
      DAEBCTUA $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d o p C $.  $d c d o p D $.  $d c d o p J $.
    $( In a normal space, disjoint closed sets are separated by open sets. $)
    nrmsep $p |- ( ( J e. Nrm /\ ( C e. ( Clsd ` J ) /\ D e. ( Clsd ` J ) /\ (
    C i^i D ) = (/) ) ) -> E. o e. J E. p e. J ( C C_ o /\ D C_ p /\ ( o i^i p
    ) = (/) ) ) $=
      ( vc vd cnrm wcel ccld cfv cin c0 wceq w3a cv wss wrex wi wral ctop
      isnrm simprbi wa ineq1 eqeq1d sseq1 3anbi1d 2rexbidv imbi12d ineq2
      3anbi2d rcla42v com12 exp3a 3impd syl imp ) DHIZADJKZIZBUTIZABLZMNZOZACPZ
      QZBEPZQZVFVHLMNZOZEDRCDRZUSFPZGPZLZMNZVMVFQZVNVHQZVJOZEDRCDRZSZGUTTFUTTZV
      EVLSUSDUAIWBCDEFGUBUCWBVAVBVDVLWBVAVBVDVLSZVAVBUDWBWCWAWCAVNLZMNZVGVRVJOZ
      EDRCDRZSFGABUTUTVMANZVPWEVTWGWHVOWDMVMAVNUEUFWHVSWFCEDDWHVQVGVRVJVMAVFUGU
      HUIUJVNBNZWEVDWGVLWIWDVCMVNBAUKUFWIWFVKCEDDWIVRVIVGVJVNBVHUGULUIUJUMUNUOU
      PUQUR $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d c d o C $.  $d c d o D $.  $d c d o J $.
    $( In a normal space, any two disjoint closed sets have the property that
       each one is a subset of an open set whose closure is disjoint from the
       other. $)
    nrmsep2 $p |- ( ( J e. Nrm /\ ( C e. ( Clsd ` J ) /\ D e. ( Clsd ` J ) /\ (
    C i^i D ) = (/) ) ) -> E. o e. J ( C C_ o /\ ( ( ( cls ` J ) ` o ) i^i D )
    = (/) ) ) $=
      ( vc vd cnrm wcel ccld cfv cin c0 wceq w3a cv wss ccl wa wrex wi wral
      ctop nrmtop isnrm2 biimpd mpcom ineq1 eqeq1d sseq1 anbi1d rexbidv
      imbi12d ineq2 anbi2d rcla42v com12 exp3a 3impd syl imp ) DGHZADIJZHZBVBHZ
      ABKZLMZNZACOZPZVHDQJJZBKZLMZRZCDSZVAEOZFOZKZLMZVOVHPZVJVPKZLMZRZCDSZTZFVB
      UAEVBUAZVGVNTDUBHZVAWEDUCWFVAWECDEFUDUEUFWEVCVDVFVNWEVCVDVFVNTZVCVDRWEWGW
      DWGAVPKZLMZVIWARZCDSZTEFABVBVBVOAMZVRWIWCWKWLVQWHLVOAVPUGUHWLWBWJCDWLVSVI
      WAVOAVHUIUJUKULVPBMZWIVFWKVNWMWHVELVPBAUMUHWMWJVMCDWMWAVLVIWMVTVKLVPBVJUM
      UHUNUKULUOUPUQURUSUT $.
      $( [1-Feb-2010] $)
  $}

  ${
    $d a b c d k p q r s t u v x y B $.  $d a b c d k p q r s t u v x y C $.
    $d a b c d k p q r s t u v x y D $.  $d a b c d k p q r s t u v x y J $.
    $d a r s t x y P $.  $d k t x y Q $.
    metnrm.0 $e |- J = ( MetOpen ` D ) $.
    $( Lemma for ~ metnrm . $)
    metnrmlem1 $p |- { r e. RR | ( 0 < r /\ ( r = 1 \/ ( ( P ( ball ` D ) ( 2
    x. r ) ) i^i C ) =/= (/) ) ) } =/= (/) $=
      ( cc0 cv clt wbr c1 wceq c2 cmul co cbl cfv cin c0 wne wo wa cr crab
      wrex wcel 1re lt01 eqid orci pm3.2i breq2 eqeq1 opreq2 opreq2d ineq1d
      neeq1d orbi12d anbi12d rcla4ev mp2an rabn0 mpbir ) GEHZIJZVDKLZCMVDNOZBPQ
      ZOZARZSTZUAZUBZEUCUDSTVMEUCUEZKUCUFGKIJZKKLZCMKNOZVHOZARZSTZUAZUBZVNUGVOW
      AUHVPVTKUIUJUKVMWBEKUCVFVEVOVLWAVDKGIULVFVFVPVKVTVDKKUMVFVJVSSVFVIVRAVFVG
      VQCVHVDKMNUNUOUPUQURUSUTVAVMEUCVBVC $.
      $( [1-Sep-2013] $)

    $( Lemma for ~ metnrm . $)
    metnrmlem2 $p |- sup ( { r e. RR | ( 0 < r /\ ( r = 1 \/ ( ( P ( ball ` D )
    ( 2 x. r ) ) i^i C ) =/= (/) ) ) } , RR , `' < ) e. RR $=
      ( vs vt cc0 cv clt wbr c1 wceq c2 cmul co cbl cfv cin c0 wne wo wa cr
      crab wss cle wral wrex ccnv csup wcel ssrab2 metnrmlem1 0re breq2 eqeq1
      opreq2 opreq2d ineq1d neeq1d orbi12d anbi12d elrab wi ltle mpan imp
      adantrr sylbi rgen breq1 ralbidv rcla4ev mp2an infmrcl mp3an ) IEJZKLZVSM
      NZCOVSPQZBRSZQZATZUAUBZUCZUDZEUEUFZUEUGWIUAUBGJZHJZUHLZHWIUIZGUEUJZWIUEKU
      KULUEUMWHEUEUNABCDEFUOIUEUMZIWKUHLZHWIUIZWNUPWPHWIWKWIUMWKUEUMZIWKKLZWKMN
      ZCOWKPQZWCQZATZUAUBZUCZUDZUDWPWHXFEWKUEVSWKNZVTWSWGXEVSWKIKUQXGWAWTWFXDVS
      WKMURXGWEXCUAXGWDXBAXGWBXACWCVSWKOPUSUTVAVBVCVDVEWRWSWPXEWRWSWPWOWRWSWPVF
      UPIWKVGVHVIVJVKVLWMWQGIUEWJINWLWPHWIWJIWKUHVMVNVOVPGHWIVQVR $.
      $( [2-Sep-2013] $)

    $( Lemma for ~ metnrm . $)
    metnrmlem3 $p |- ( ( ( ( D e. Met /\ J e. Top ) /\ ( B e. ( Clsd ` J )
    /\ C e. ( Clsd ` J ) /\ ( B i^i C ) = (/) ) ) /\ P e. B ) -> 0 < sup ( { r
    e. RR | ( 0 < r /\ ( r = 1 \/ ( ( P ( ball ` D ) ( 2 x. r ) ) i^i C ) =/=
    (/) ) ) } , RR , `' < ) ) $=
      ( va vs vt cme wcel ctop wa ccld cfv cin c0 wceq w3a cv cuni cdif wss
      cbl crn wrex cc0 clt wbr c1 c2 cmul co wne wo cr crab ccnv csup simplll
      eqid cldopn adantll 3ad2antr2 adantr wn cldss 3ad2antr1 sseld imp elin
      n0i bnj1359 con2d impcom 3ad2antl3 eldif sylanbrc opni syl111anc cdm wb
      uniopn2 ad2antrr sseqtrd blssex syl11anc simpll simprl simprrl 2re 2pos
      pm3.2i ssblex mp3an2 syl22anc cdiv 0re a1i rehalfcl ad2antrl metnrmlem2
      halfpos2 biimpa adantl cle wral ssrab2 metnrmlem1 breq2 eqeq1 opreq2
      opreq2d ineq1d neeq1d orbi12d anbi12d elrab wi ltdiv1 mp3an23 2cn 2ne0
      dividi breq2i syl6bb 1re ltle sylancl sylbid lenlt mpbid breq1 notbid
      syl5ibrcom ad2antlr jca remulcl mpan mulgt0 mpanl12 id ltmuldiv2 mp3an3
      ancoms adantlr ad2ant2lr sylan sylbird ssbl syl31anc ssrin syl 3ad2ant3
      ad2antll ssdifin0 sstrd ex ss0 syl6 necon3ad jaod expr imp3a impr mpbird
      syl5bi r19.21aiv lbleinf ltletrd exp32 r19.23adv mpd ) CKLZEMLZNZAEOPZLZB
      UVRLZABQZRSZTZNZDALZNZDHUAZLUWGEUBZBUCZUDNHCUEPZUFUGZUHUHFUAZUIUJZUWLUKSZ
      DULUWLUMUNZUWJUNZBQZRUOZUPZNZFUQURZUQUIUSUTZUIUJZUWFUVOUWIELZDUWILZUWKUVO
      UVPUWCUWEVAZUWDUXDUWEUVQUVSUVTUXDUWBUVPUVTUXDUVOBEUWHUWHVBZVCVDVEVFUWFDUW
      HLZDBLZVGZUXEUWDUWEUXHUWDAUWHDUVQUVTUVSAUWHUDZUWBUVPUVSUXKUVOAEUWHUXGVHVD
      VIZVJVKUWCUWEUXJUVQUWBUVSUWEUXJUVTUWEUWBUXJUWEUXIUWBDUWALUWEUXIUWBVGDABVL
      UWADVMVNVOVPVQVDDUWHBVRVSHUWICDEGVTWAUWFUWKUHIUAZUIUJZDUXMUWJUNZUWIUDZNZI
      UQUGZUXCUWFUVODCWBWBZLZUWKUXRWCUXFUWDUWEUXTUWDAUXSDUWDAUWHUXSUXLUVOUWHUXS
      SUVPUWCCEUXSUXSVBZGWDWEWFVJVKZHIUWICDUXSUYAWGWHUWFUXQUXCIUQUWFUXMUQLZUXQU
      XCUWFUYCUXQNZNZUHJUAZUIUJZUYFULUIUJZDUYFUWJUNZUXOUDZTZJUQUGZUXCUYEUVOUXTU
      YCUXNUYLUWDUVOUWEUYDUVOUVPUWCWIWEUWFUXTUYDUYBVFUWFUYCUXQWJUWFUYCUXNUXPWKU
      VOUXTNZULUQLZUHULUIUJZNZUYCUXNNUYLUYNUYOWLWMWNZJCDULUXMUXSUYAWOWPWQUYEUYK
      UXCJUQUYEUYFUQLZUYKUXCUYEUYRUYKNZNZUHUYFULWRUNZUXBUHUQLUYTWSWTUYRVUAUQLZU
      YEUYKUYFXAZXBZUXBUQLUYTBCDEFGXCWTUYSUHVUAUIUJZUYEUYRUYHUYGVUEUYJUYRUYGVUE
      UYFXDXEVIXFUYTUXAUQUDZUXARUOZVUBVUAUWGXGUJZHUXAXHVUAUXBXGUJVUFUYTUWTFUQXI
      WTVUGUYTBCDEFGXJWTVUDUYTVUHHUXAUWGUXALUWGUQLZUHUWGUIUJZUWGUKSZDULUWGUMUNZ
      UWJUNZBQZRUOZUPZNZNZUYTVUHUWTVUQFUWGUQUWLUWGSZUWMVUJUWSVUPUWLUWGUHUIXKVUS
      UWNVUKUWRVUOUWLUWGUKXLVUSUWQVUNRVUSUWPVUMBVUSUWOVULDUWJUWLUWGULUMXMXNXOXP
      XQXRXSUYTVURVUHUYTVURNZVUHUWGVUAUIUJZVGZUYTVUIVUQVVBUYTVUINVUJVUPVVBUYTVU
      IVUJVUPVVBXTUYTVUIVUJNZNZVUKVVBVUOUYSVUKVVBXTZUYEVVCUYRUYGUYHVVEUYJUYRUYH
      NZVVBVUKUKVUAUIUJZVGZVVFVUAUKXGUJZVVHUYRUYHVVIUYRUYHVUAUKUIUJZVVIUYRUYHVU
      AULULWRUNZUIUJZVVJUYRUYNUYPUYHVVLWCWLUYQUYFULULYAYBVVKUKVUAUIULYCYDYEYFYG
      UYRVUBUKUQLZVVJVVIXTVUCYHVUAUKYIYJYKVKUYRVVIVVHWCZUYHUYRVUBVVMVVNVUCYHVUA
      UKYLYJVFYMVUKVVAVVGUWGUKVUAUIYNYOYPVEYQVVDVVAVUNRVVDVVAVUNRUDZVUNRSVVDVVA
      VVOVVDVVANZVUNUYIBQZRVVPVUMUYIUDZVUNVVQUDVVPUYMVULUQLZUHVULUIUJZNZUYRUYGN
      ZVULUYFXGUJZVVRUYTUYMVVCVVAUWFUYMUYDUYSUWFUVOUXTUXFUYBYRWEWEVVCVWAUYTVVAV
      VCVVSVVTVUIVVSVUJUYNVUIVVSWLULUWGYSYTZVFUYNUYOVVCVVTWLWMULUWGUUAUUBYRYQUY
      TVWBVVCVVAUYSVWBUYEUYRUYHUYGVWBUYJVWBUUCVIXFWEVVDVVAVWCVVDVVAVULUYFUIUJZV
      WCUYSVUIVWEVVAWCZUYEVUJUYRVUIVWFUYKVUIUYRVWFVUIUYRUYPVWFUYQUWGUYFULUUDUUE
      UUFUUGUUHUYSVUIVWEVWCXTZUYEVUJUYRVUIVWGUYKVUIUYRVWGVUIVVSUYRVWGVWDVULUYFY
      IUUIUUFUUGUUHUUJVKCDVULUYFUXSUYAUUKUULVUMUYIBUUMUUNVVPVVQUXOBQZRUYTVVQVWH
      UDZVVCVVAUYKVWIUYEUYRUYJUYGVWIUYHUYIUXOBUUMUUOUUPWEUYTVWHRSZVVCVVAUYDVWJU
      WFUYSUXPVWJUYCUXNUXOUWHBUUQUUPYQWEWFUURUUSVUNUUTUVAUVBUVCUVDUVEUVFVUTVUBV
      UIVUHVVBWCUYSVUBUYEVURUYRVUBUYKVUCVFYQUYTVUIVUQWJVUAUWGYLWHUVGUUSUVHUVIHU
      XAVUAUVJUULUVKUVLUVMUVNUVLUVMYKUVN $.
      $( [1-Sep-2013] $)

    $( Lemma for ~ metnrm . $)
    metnrmlem4 $p |- ( ( ( D e. Met /\ J e. Top ) /\ ( B e. ( Clsd ` J ) /\ C
    e. ( Clsd ` J ) /\ ( B i^i C ) = (/) ) ) -> U. { a | E. p e. B a = ( p (
    ball ` D ) sup ( { r e. RR | ( 0 < r /\ ( r = 1 \/ ( ( p ( ball ` D ) ( 2
    x. r ) ) i^i C ) =/= (/) ) ) } , RR , `' < ) ) } e. J ) $=
      ( cme wcel ctop wa ccld cfv cin c0 wceq w3a cv cc0 clt wbr c1 c2 cmul co
      cbl wne wo cr crab ccnv csup wrex cab wss cuni simplr cdm simplll eqid
      cldss adantll 3ad2antr1 uniopn2 ad2antrr sseqtrd sseld imp metnrmlem2
      a1i metnrmlem3 blopn syl22anc eleq1 syl5ibrcom r19.23adva abssdv uniopn
      syl11anc ) CIJZDKJZLZADMNZJZBWDJZABOPQZRZLZWBGSZFSZTESZUAUBWLUCQWKUDWLUEU
      FCUGNZUFBOPUHUILEUJUKUJUAULUMZWMUFZQZFAUNZGUOZDUPWRUQDJWAWBWHURWIWQGDWIWP
      WJDJZFAWIWKAJZLZWSWPWODJZXAWAWKCUSUSZJZWNUJJZTWNUAUBXBWAWBWHWTUTWIWTXDWIA
      XCWKWIADUQZXCWCWFWEAXFUPZWGWBWEXGWAADXFXFVAVBVCVDWAXFXCQWBWHCDXCXCVAZHVEV
      FVGVHVIXEXABCWKDEHVJVKABCWKDEHVLCWKWNDXCXHHVMVNWJWODVOVPVQVRWRDVSVT $.
      $( [1-Sep-2013] $)

    $( Lemma for ~ metnrm . $)
    metnrmlem5 $p |- ( ( ( D e. Met /\ J e. Top ) /\ ( B e. ( Clsd ` J ) /\ C
    e. ( Clsd ` J ) /\ ( B i^i C ) = (/) ) ) -> B C_ U. { a | E. p e. B a = ( p
    ( ball ` D ) sup ( { r e. RR | ( 0 < r /\ ( r = 1 \/ ( ( p ( ball ` D ) ( 2
    x. r ) ) i^i C ) =/= (/) ) ) } , RR , `' < ) ) } ) $=
      ( vq cme wcel ctop wa ccld cfv cin c0 wceq w3a cv cc0 clt wbr c1 c2 cmul
      co cbl wne wo cr crab ccnv csup wrex cab cuni wex cdm simplll wss eqid
      cldss adantll uniopn2 ad2antrr sseqtrd 3ad2antr1 sseld imp metnrmlem3
      metnrmlem2 blcntr mpanr1 syl21anc id opreq1 ineq1d neeq1d orbi2d anbi2d
      rabbidv supeq1d opreq12d eqeq2d rcla4ev mpan2 adantl oprex eleq2 eqeq1
      rexbidv anbi12d cla4ev syl11anc eluniab sylibr ex ssrdv ) CJKZDLKZMZADNOZ
      KZBXCKZABPQRZSZMZIAGTZFTZUAETZUBUCZXKUDRZXJUEXKUFUGZCUHOZUGZBPZQUIZUJZMZE
      UKULZUKUBUMZUNZXOUGZRZFAUOZGUPUQZXHITZAKZYHYGKZXHYIMZYHXIKZYFMZGURZYJYKYH
      YHXLXMYHXNXOUGZBPZQUIZUJZMZEUKULZUKYBUNZXOUGZKZUUBYDRZFAUOZYNYKWTYHCUSUSZ
      KZUAUUAUBUCZUUCWTXAXGYIUTXHYIUUGXHAUUFYHXBXEXDAUUFVAXFXBXDMADUQZUUFXAXDAU
      UIVAWTADUUIUUIVBVCVDWTUUIUUFRXAXDCDUUFUUFVBZHVEVFVGVHVIVJABCYHDEHVKWTUUGM
      UUAUKKUUHUUCBCYHDEHVLCYHUUAUUFUUJVMVNVOYIUUEXHYIUUBUUBRZUUEUUBVBUUDUUKFYH
      AXJYHRZYDUUBUUBUULXJYHYCUUAXOUULVPUULUKYAYTYBUULXTYSEUKUULXSYRXLUULXRYQXM
      UULXQYPQUULXPYOBXJYHXNXOVQVRVSVTWAWBWCWDWEWFWGWHYMUUCUUEMGUUBYHUUAXOWIXIU
      UBRZYLUUCYFUUEXIUUBYHWJUUMYEUUDFAXIUUBYDWKWLWMWNWOYFGYHWPWQWRWS $.
      $( [2-Sep-2013] $)

    $( Lemma for ~ metnrm . $)
    metnrmlem6 $p |- ( ( ( ( ( ( D e. Met /\ J e. Top ) /\ ( B e. ( Clsd ` J )
    /\ C e. ( Clsd ` J ) /\ ( B i^i C ) = (/) ) ) /\ P e. B ) /\ ( X e. ( P (
    ball ` D ) sup ( { r e. RR | ( 0 < r /\ ( r = 1 \/ ( ( P ( ball ` D ) ( 2
    x. r ) ) i^i C ) =/= (/) ) ) } , RR , `' < ) ) /\ X e. ( Q ( ball ` D ) sup
    ( { k e. RR | ( 0 < k /\ ( k = 1 \/ ( ( Q ( ball ` D ) ( 2 x. k ) ) i^i B )
    =/= (/) ) ) } , RR , `' < ) ) ) ) /\ Q e. C ) -> ( sup ( { r e. RR | ( 0 <
    r /\ ( r = 1 \/ ( ( P ( ball ` D ) ( 2 x. r ) ) i^i C ) =/= (/) ) ) } , RR
    , `' < ) <_ sup ( { k e. RR | ( 0 < k /\ ( k = 1 \/ ( ( Q ( ball ` D ) ( 2
    x. k ) ) i^i B ) =/= (/) ) ) } , RR , `' < ) -> sup ( { k e. RR | ( 0 < k
    /\ ( k = 1 \/ ( ( Q ( ball ` D ) ( 2 x. k ) ) i^i B ) =/= (/) ) ) } , RR ,
    `' < ) < sup ( { k e. RR | ( 0 < k /\ ( k = 1 \/ ( ( Q ( ball ` D ) ( 2 x.
    k ) ) i^i B ) =/= (/) ) ) } , RR , `' < ) ) ) $=
      ( vt vx vy cme wcel ctop wa ccld cfv cin c0 wceq w3a cc0 cv clt wbr c1
      c2 cmul co cbl wne wo cr crab ccnv csup cle cdiv caddc metnrmlem2 a1i
      cdm simplll ad2antrr cuni wss eqid cldss adantll 3ad2antr2 uniopn2
      sseqtrd sseld imp adantlr ad2ant2r 3ad2antr1 metcl syl111anc rehalfcl
      syl readdcl mpan 3syl simp2 simp1 incom eqeq1i biimpi 3ad2ant3 3jca
      metnrmlem3 sylanl2 metge0 wb halfnneg2 mpbid addgtge0 mpanl1 syl12anc
      halfpos2 elbl mpanr1 syl21anc simprbda adantrr adantr syl11anc readdcli
      mettri3 syl13anc jca simplrr elbl2 syl31anc simplrl simprr ltletrd wi
      lt2add mpanr12 sylc lelttrd recni 2timesi syl6breqr 2re remulcli ltadd1
      mp3an2 anidms cc recn 2times 2cn adddi mp3an12 2ne0 divcan2 mp3an23
      opreq2d eqtrd 3brtr4d 2pos pm3.2i ltmul2 mp3an3 mpbird breqtrrd eqeltrd
      mulgt0 mpanl12 syl32anc simpllr elin sylanbrc ne0i olcd breq2 eqeq1
      opreq2 ineq1d neeq1d orbi12d anbi12d cbvrabv eleq2i elrab bitri wral
      wrex ssrab2 0re ltle sylbi rgen breq1 ralbidv rcla4ev mp2an infle
      ltdivmul ltadd2 expr ) CNOZGPOZQZAGRSZOZBUWJOZABTZUAUBZUCZQZDAOZQZHDUDIUE
      ZUFUGUWSUHUBDUIUWSUJUKCULSZUKBTUAUMUNQIUOUPUOUFUQZURZUWTUKOZHEUDFUEZUFUGZ
      UXDUHUBZEUIUXDUJUKZUWTUKZATZUAUMZUNZQZFUOUPZUOUXAURZUWTUKOZQZQZEBOZUXBUXN
      USUGZUXNUXNUFUGUXQUXRUXSQZQZUXNUXNEDCUKZUIUTUKZVAUKZUIUTUKZUXNUXNUOOZUYAA
      CEGFJVBZVCZUYAUYCUOOZUYDUOOZUYEUOOZUYAUYBUOOZUYIUYAUWGECVDVDZOZDUYMOZUYLU
      WRUWGUXPUXTUWGUWHUWOUWQVEZVFZUWRUXRUYNUXPUXSUWPUXRUYNUWQUWPUXRUYNUWPBUYME
      UWPBGVGZUYMUWIUWKUWLBUYRVHZUWNUWHUWLUYSUWGBGUYRUYRVIZVJVKVLUWGUYRUYMUBUWH
      UWOCGUYMUYMVIZJVMVFZVNVOVPVQVRZUWRUYOUXPUXTUWPUWQUYOUWPAUYMDUWPAUYRUYMUWI
      UWLUWKAUYRVHZUWNUWHUWKVUDUWGAGUYRUYTVJVKVSVUBVNVOVPZVFZEDCUYMVUAVTWAZUYBW
      BZWCZUYFUYIUYJUYGUXNUYCWDWEZUYDWBWFZUYHUYAUYEUXMOZUXNUYEUSUGZUYAUYKUDUYEU
      FUGZUYEUHUBZEUIUYEUJUKZUWTUKZATZUAUMZUNZQZVULVUKUYAVUNVUTUYAUDUYDUFUGZVUN
      UYAUYIUDUXNUFUGZUDUYCUSUGZVVBVUIUWRUXRVVCUXPUXSUWPUXRVVCUWQUWOUWIUWLUWKBA
      TZUAUBZUCUXRVVCUWOUWLUWKVVFUWKUWLUWNWGUWKUWLUWNWHUWNUWKVVFUWLUWNVVFUWMVVE
      UAABWIWJWKWLWMBACEGFJWNWOVQVRZUYAUDUYBUSUGZVVDUYAUWGUYNUYOVVHUYQVUCVUFEDC
      UYMVUAWPWAUYAUYLVVHVVDWQVUGUYBWRWCWSUYFUYIVVCVVDQVVBUYGUXNUYCWTXAXBUYAUYI
      UYJVVBVUNWQVUIVUJUYDXCWFWSZUYAVUSVUOUYADVUROZVUSUYADVUQOZUWQVVJUYAVVKUYBV
      UPUFUGZUYAUYBUYDVUPUFUYAUYBUYDUFUGZUIUYBUJUKZUIUYDUJUKZUFUGZUYAUYBUYBVAUK
      ZUIUXNUJUKZUYBVAUKZVVNVVOUFUYAUYBVVRUFUGZVVQVVSUFUGZUYAUYBUXNUXNVAUKZVVRU
      FUYAUYBEHCUKZDHCUKZVAUKZVWBVUGUYAVWCUOOZVWDUOOZVWEUOOUYAUWGUYNHUYMOZVWFUY
      QVUCUXQVWHUXTUWRUXCVWHUXOUWRUXCVWHVWDUXBUFUGZUWRUWGUYOUDUXBUFUGZUXCVWHVWI
      QWQZUYPVUEABCDGIJWNZUWGUYOQUXBUOOZVWJVWKBCDGIJVBZHCDUXBUYMVUAXDXEXFXGXHXI
      ZEHCUYMVUAVTWAZUYAUWGUYOVWHVWGUYQVUFVWODHCUYMVUAVTWAZVWCVWDWDXJVWBUOOUYAU
      XNUXNUYGUYGXKVCUYAUWGUYNUYOVWHUYBVWEUSUGUYQVUCVUFVWOEDHCUYMVUAXLXMUYAVWFV
      WGQZVWCUXNUFUGZVWDUXNUFUGZQZVWEVWBUFUGZUYAVWFVWGVWPVWQXNUYAVWSVWTUYAUXOVW
      SUWRUXCUXOUXTXOUYAUWGUYNVWHVVCUXOVWSWQZUYQVUCVWOVVGUWGUYNVWHUCUYFVVCVXCUY
      GHCEUXNUYMVUAXPXEXQWSUYAVWDUXBUXNVWQVWMUYAVWNVCUYHUYAUXCVWIUWRUXCUXOUXTXR
      UYAUWGUYOVWHVWJUXCVWIWQZUYQVUFVWOUWRVWJUXPUXTVWLVFUWGUYOVWHUCVWMVWJVXDVWN
      HCDUXBUYMVUAXPXEXQWSUXQUXRUXSXSXTXNVWRUYFUYFVXAVXBYAUYGUYGVWCVWDUXNUXNYBY
      CYDYEUXNUXNUYGYFZYGZYHZUYAUYLVVTVWAWQZVUGUYLVXHUYLVVRUOOUYLVXHUIUXNYIUYGY
      JUYBVVRUYBYKYLYMWCWSUYAUYLUYBYNOZVVNVVQUBVUGUYBYOZUYBYPWFUYAVVOVVRUIUYCUJ
      UKZVAUKZVVSUYAUYIUYCYNOZVVOVXLUBZVUIUYCYOUIYNOZUXNYNOVXMVXNYQVXEUIUXNUYCY
      RYSWFUYAVXKUYBVVRVAUYAUYLVXIVXKUYBUBZVUGVXJVXIVXOUIUDUMZVXPYQYTUYBUIUUAUU
      BWFUUCUUDUUEUYAUYLUYJVVMVVPWQZVUGUYAUYLUYIUYJVUGVUHVUJWFZUYLUYJUIUOOZUDUI
      UFUGZQZVXRVXTVYAYIUUFUUGZUYBUYDUIUUHUUIXJUUJUYAUYJUYDYNOZVUPUYDUBZVXSUYDY
      OVYDVXOVXQVYEYQYTUYDUIUUAUUBWFZUUKUYAUWGUYNUYOVUPUOOUDVUPUFUGZVVKVVLWQUYQ
      VUCVUFUYAVUPUYDUOVYFVXSUULUYAUYKVUNVYGVUKVVIVXTVYAUYKVUNQVYGYIUUFUIUYEUUM
      UUNXJDCEVUPUYMVUAXPUUOUUJUWPUWQUXPUXTUUPDVUQAUUQUURVURDUUSWCUUTXNVULUYEUD
      KUEZUFUGZVYHUHUBZEUIVYHUJUKZUWTUKZATZUAUMZUNZQZKUOUPZOUYKVVAQUXMVYQUYEUXL
      VYPFKUOUXDVYHUBZUXEVYIUXKVYOUXDVYHUDUFUVAVYRUXFVYJUXJVYNUXDVYHUHUVBVYRUXI
      VYMUAVYRUXHVYLAVYRUXGVYKEUWTUXDVYHUIUJUVCUUCUVDUVEUVFUVGUVHUVIVYPVVAKUYEU
      OVYHUYEUBZVYIVUNVYOVUTVYHUYEUDUFUVAVYSVYJVUOVYNVUSVYHUYEUHUVBVYSVYMVURUAV
      YSVYLVUQAVYSVYKVUPEUWTVYHUYEUIUJUVCUUCUVDUVEUVFUVGUVJUVKUURUXMUOVHLUEZMUE
      ZUSUGZMUXMUVLZLUOUVMZVULVUMUXLFUOUVNUDUOOZUDWUAUSUGZMUXMUVLZWUDUVOWUFMUXM
      WUAUXMOWUAUOOZUDWUAUFUGZWUAUHUBZEUIWUAUJUKZUWTUKZATZUAUMZUNZQZQWUFUXLWUPF
      WUAUOUXDWUAUBZUXEWUIUXKWUOUXDWUAUDUFUVAWUQUXFWUJUXJWUNUXDWUAUHUVBWUQUXIWU
      MUAWUQUXHWULAWUQUXGWUKEUWTUXDWUAUIUJUVCUUCUVDUVEUVFUVGUVJWUHWUIWUFWUOWUHW
      UIWUFWUEWUHWUIWUFYAUVOUDWUAUVPWEVPXHUVQUVRWUCWUGLUDUOVYTUDUBWUBWUFMUXMVYT
      UDWUAUSUVSUVTUWAUWBLMUYEUXMUWCYSWCUYAUYEUXNUFUGZUYDVVRUFUGZUYAUYDVWBVVRUF
      UYAUYCUXNUFUGZUYDVWBUFUGZUYAWUTVVTVXGUYAUYLWUTVVTWQZVUGUYLUYFVYBWVBUYGVYC
      UYBUXNUIUWDUUBWCUUJUYAUYLUYIWUTWVAWQZVUGVUHUYIUYFUYFWVCUYGUYGUYCUXNUXNUWE
      UUBWFWSVXFYHUYAUYJWURWUSWQZVXSUYJUYFVYBWVDUYGVYCUYDUXNUIUWDUUBWCUUJYEUWF
      $.
      $( [15-Sep-2013] $)

    $( A metric space is normal. $)
    metnrm $p |- ( D e. Met -> J e. Nrm ) $=
      ( vc vd vu vv va vp vr vb vq vk vx cme wcel ctop cv cin c0 wceq wss w3a
      wrex wi ccld cfv wral wa cnrm opntop cc0 clt wbr c1 c2 cmul co cbl wne
      wo cr crab ccnv csup cab cuni metnrmlem4 simp2 simp1 incom eqeq1i biimpi
      3ad2ant3 3jca sylan2 metnrmlem5 wex wn wal metnrmlem2 ltnri a1i eleq2
      anbi2d anbi1d cle letrii metnrmlem6 anim2i ad2antrr anim1i pm3.22
      ad2antlr simpllr syl21anc orim12d mpi syl6bi com12 mtord r19.21aiva
      exp43 imbi1d imbi2d syl5ibr com3l r19.23adv com23 imp3a 19.21adv
      19.23adv 19.21aiv ianor elin eluniab anbi12i bitri notbii notnot ralnex
      imbi2i albii alinexa imbi12i df-or bitr4i 3bitr4ri eq0 sylib sseq2 ineq1
      eqeq1d 3anbi13d ineq2 3anbi23d rcla42ev syl113anc 3exp2 r19.21aivv ex
      jcai isnrm sylibr ) AOPZBQPZDRZERZSZTUAZUUGFRZUBZUUHGRZUBZUUKUUMSZTUAZUCZ
      GBUDFBUDZUEZEBUFUGZUHDUUTUHZUIBUJPUUEUUFUVAABCUKUUEUUFUVAUUEUUFUIZUUSDEUU
      TUUTUVBUUGUUTPZUUHUUTPZUUSUVBUVCUVDUUJUURUVBUVCUVDUUJUCZUIZHRZIRZULJRZUMU
      NUVIUOUAUVHUPUVIUQURAUSUGZURUUHSTUTVAUIJVBVCVBUMVDZVEZUVJURZUAZIUUGUDZHVF
      VGZBPKRZLRZULMRZUMUNUVSUOUAUVRUPUVSUQURUVJURUUGSTUTVAUIMVBVCVBUVKVEZUVJUR
      ZUAZLUUHUDZKVFVGZBPZUUGUVPUBZUUHUWDUBZUVPUWDSZTUAZUURUUGUUHABJIHCVHUVEUVB
      UVDUVCUUHUUGSZTUAZUCZUWEUVEUVDUVCUWKUVCUVDUUJVIUVCUVDUUJVJUUJUVCUWKUVDUUJ
      UWKUUIUWJTUUGUUHVKVLVMVNVOZUUHUUGABMLKCVHVPUUGUUHABJIHCVQUVEUVBUWLUWGUWMU
      UHUUGABMLKCVQVPUVFNRZUVGPZUVOUIZHVRZUWNUVQPZUWBVSZLUUHUHZUEZKVTZUEZNVTZUW
      IUVFUXCNUVFUWPUXBHUVFUWPUXAKUVFUWOUVOUXAUVFUVOUWOUXAUVFUVNUWOUXAUEZIUUGUV
      NUVFUVHUUGPZUXEUVFUXFUXEUEUVNUXFUWNUVMPZUXAUEZUEUVFUXFUXGUWRUWTUVFUXFUIZU
      XGUWRUIZUIZUWSLUUHUXKUVRUUHPZUIZUWBUVTUVTUMUNZUVLUVLUMUNZUXNVSUXMUVTUUGAU
      VRBMCWAZWBWCUXOVSUXMUVLUUHAUVHBJCWAZWBWCUWBUXMUXNUXOVAZUWBUXMUXIUXGUWNUWA
      PZUIZUIZUXLUIZUXRUWBUXKUYAUXLUWBUXJUXTUXIUWBUWRUXSUXGUVQUWAUWNWDWEWEWFUYB
      UVLUVTWGUNZUVTUVLWGUNZVAUXRUVLUVTUXQUXPWHUYBUYCUXNUYDUXOUUGUUHAUVHUVRMBUW
      NJCWIUYBUVBUWLUIZUXLUIUXSUXGUIZUXFUYDUXOUEUYAUYEUXLUVFUYEUXFUXTUVEUWLUVBU
      WMWJWKWLUXTUYFUXIUXLUXGUXSWMWNUVFUXFUXTUXLWOUUHUUGAUVRUVHJBUWNMCWIWPWQWRW
      SWTXAXBXCUVNUXEUXHUXFUVNUWOUXGUXAUVGUVMUWNWDXDXEXFXGXHXIXJXKXLXMUXDUWNUWH
      PZVSZNVTUWIUXCUYHNUWQUWRUWCUIKVRZUIZVSUWQVSZUYIVSZVAZUYHUXCUWQUYIXNUYGUYJ
      UYGUWNUVPPZUWNUWDPZUIUYJUWNUVPUWDXOUYNUWQUYOUYIUVOHUWNXPUWCKUWNXPXQXRXSUX
      CUYKVSZUYLUEUYMUWQUYPUXBUYLUWQXTUXBUWRUWCVSZUEZKVTUYLUXAUYRKUWTUYQUWRUWBL
      UUHYAYBYCUWRUWCKYDXRYEUYKUYLYFYGYHYCNUWHYIYGYJUUQUWFUWGUWIUCUWFUUNUVPUUMS
      ZTUAZUCFGUVPUWDBBUUKUVPUAZUULUWFUUPUYTUUNUUKUVPUUGYKVUAUUOUYSTUUKUVPUUMYL
      YMYNUUMUWDUAZUUNUWGUYTUWIUWFUUMUWDUUHYKVUBUYSUWHTUUMUWDUVPYOYMYPYQYRYSXJY
      TUUAUUBFBGDEUUCUUD $.
      $( [31-Aug-2013] $)
  $}

