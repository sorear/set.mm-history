$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Abelian groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Abel $.

  $( Extend class notation with class of all Abelian groups. $)
  cabel $a class Abel $.

  ${
    $d a b g $.
    $( Define class of all Abelian groups. $)
    df-abl $a |- Abel = { g e. Grp | A. a e. ( Base ` g )
      A. b e. ( Base ` g ) ( a ( +g ` g ) b ) = ( b ( +g ` g ) a ) } $.
  $}

  ${
    $d g x y B $.  $d g x y G $.  $d g P $.
    isabl.b $e |- B = ( Base ` G ) $.
    isabl.p $e |- P = ( +g ` G ) $.
    $( The predicate "is an Abelian (commutative) group." $)
    isabl $p |- ( G e. Abel <-> ( G e. Grp /\
             A. x e. B A. y e. B ( x P y ) = ( y P x ) ) ) $=
      ( vg cv cplusg cfv co wceq cbs wral cgrp cabel fveq2 syl6eqr opreqd raleq
      wb raleqbi1dv syl eqeq12d 2ralbidv bitrd df-abl elrab2 ) AIZBIZHIZJKZLZUK
      UJUMLZMZBULNKZOZAUQOZUJUKDLZUKUJDLZMZBCOACOZHEPQULEMZUSUPBCOZACOZVCVDUQCM
      USVFUBVDUQENKCULENRFSURVEAUQCUPBUQCUAUCUDVDUPVBABCCVDUNUTUOVAVDUMDUJUKVDU
      MEJKDULEJRGSZTVDUMDUKUJVGTUEUFUGHABUHUI $.
      $( [17-Oct-2011] $)
  $}

  ${
    $d a b K $.  $d a b L $.
    ablprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    ablprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    $( If two structures have the same group components (properties), one is an
       Abelian group iff the other one is. $)
    ablprop $p |- ( K e. Abel <-> L e. Abel ) $=
      ( va vb cgrp wcel cv cplusg cfv co wceq cbs wral cabel grpprop eqid isabl
      wa anbi1i 3bitr4i ) AGHZEIZFIZAJKZLUEUDUFLMFANKZOEUGOZTBGHZUHTAPHBPHUCUIU
      HABCDQUAEFUGUFAUGRUFRSEFUGUFBCDSUB $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d x y B $.  $d x y G $.  $d x y ph $.
    isabld.g $e |- ( ph -> G e. Grp ) $.
    isabld.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isabld.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isabld.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) = ( y P x ) ) $.
    $( Properties that determine an Abelian group. $)
    isabld $p |- ( ph -> G e. Abel ) $=
      ( cgrp wcel cv cfv co wceq wral opreqd raleqbidv eqid cplusg cbs wa cabel
      eqeq12d anbi2d 3expib r19.21aivv mpbi2and isabl sylibr ) AFKLZBMZCMZFUANZ
      OZUNUMUOOZPZCFUBNZQZBUSQZUCZFUDLAULUMUNEOZUNUMEOZPZCDQZBDQZVBAVGVAULAVFUT
      BDUSHAVEURCDUSHAVCUPVDUQAEUOUMUNIRAEUOUNUMIRUESSUFGAVEBCDDAUMDLUNDLVEJUGU
      HUIBCUSUOFUSTUOTUJUK $.
      $( [6-Aug-2013] $)
  $}

  ${
    $d x y B $.  $d x y G $.
    isabli.g $e |- G e. Grp $.
    isabli.b $e |- B = ( Base ` G ) $.
    isabli.p $e |- P = ( +g ` G ) $.
    isabli.c $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) = ( y P x ) ) $.
    $( Properties that determine an Abelian group. $)
    isabli $p |- G e. Abel $=
      ( cabel wcel cgrp cv co wceq wral isabl rgen2a mpbir2an ) EJKELKAMZBMZDNU
      ATDNOZBCPACPABCDEGHQFUBABCIRS $.
      $( [4-Sep-2011] $)
  $}

  ${
    $d x y G $.
    $( An Abelian group is a group. $)
    ablgrp $p |- ( G e. Abel -> G e. Grp ) $=
      ( vx vy cabel wcel cgrp cv cplusg cfv co wceq cbs wral eqid isabl simplbi
      ) ADEAFEBGZCGZAHIZJRQSJKCALIZMBTMBCTSATNSNOP $.
      $( [26-Aug-2011] $)
  $}

  ${
    $d x y B $.  $d x y G $.  $d x y P $.  $d x y X $.  $d y Y $.
    ablcom.b $e |- B = ( Base ` G ) $.
    ablcom.p $e |- P = ( +g ` G ) $.
    $( An Abelian group operation is commutative. $)
    ablcom $p |- ( ( G e. Abel /\ X e. B /\ Y e. B ) ->
                 ( X P Y ) = ( Y P X ) ) $=
      ( vx vy cabel wcel co wceq cv wral wa cgrp opreq1 opreq2 eqeq12d simprbi
      isabl rcla42v syl5com 3impib ) CJKZDAKZEAKZDEBLZEDBLZMZUFHNZINZBLZUMULBLZ
      MZIAOHAOZUGUHPUKUFCQKUQHIABCFGUBUAUPUKDUMBLZUMDBLZMHIDEAAULDMUNURUOUSULDU
      MBRULDUMBSTUMEMURUIUSUJUMEDBSUMEDBRTUCUDUE $.
      $( [26-Aug-2011] $)
  $}

  ${
    $d x y z G $.
    cnaddablx.g $e |- G = { <. 1 , CC >. , <. 2 , + >. } $.
    $( The complex numbers are an Abelian group under addition.  This version
       of ~ cnaddabl shows the explicit structure "scaffold" we chose for the
       definition for Abelian groups.  Note:  This theorem has hard-coded
       structure indices for demonstration purposes.  It is not intended for
       general use; use ~ cnaddabl instead. $)
    cnaddablx $p |- G e. Abel $=
      ( vx vy vz cc caddc cv cneg ax-cnex addex ax-addcl ax-addass addid2 negcl
      cc0 0cn wcel co addcom wceq mpdan negid eqtr3d isgrpix grpbasex grpplusgx
      isabli ) CDFGACDEFGACHZIZPJKBUIDHZLUIUKEHMQUINUIOZUIFRZUIUJGSZUJUIGSZPUMU
      JFRUNUOUAULUIUJTUBUIUCUDUEFGAJKBUFFGAJKBUGUIUKTUH $.
      $( [18-Oct-2012] $)
  $}

  ${
    $d x y z G $.
    cnaddabl.g $e |- G
               = { <. ( Base ` ndx ) , CC >. , <. ( +g ` ndx ) , + >. } $.
    $( The complex numbers are an Abelian group under addition.  This version
       of ~ cnaddablx hides the explicit structure indices i.e. is
       "scaffold-independent".  Note that the proof also does not reference
       explicit structure indices.  The actual structure is dependent on how
       ` Base ` and ` +g ` is defined. $)
    cnaddabl $p |- G e. Abel $=
      ( vx vy vz cc caddc cc0 cnx cbs cfv cop cplusg cvv wcel wceq ax-mp addcom
      cv co cneg prex eqeltri ax-cnex grpbase addex grpplusg ax-addcl ax-addass
      cpr 0cn addid2 negcl mpdan negid eqtr3d isgrpi isabli ) CDFGACDEFGACSZUAZ
      HAIJKFLZIMKGLZUJNBVAVBUBUCFNOFAJKPUDFGANBUEQZGNOGAMKPUFFGANBUGQZUSDSZUHUS
      VEESUIUKUSULUSUMZUSFOZUSUTGTZUTUSGTZHVGUTFOVHVIPVFUSUTRUNUSUOUPUQVCVDUSVE
      RUR $.
      $( [20-Oct-2012] $)
  $}

  ${
    $d x y z G $.
    zaddablx.g $e |- G = { <. 1 , ZZ >. , <. 2 , + >. } $.
    $( The integers are an Abelian group under addition.  Note:  This theorem
       has hard-coded structure indices for demonstration purposes.  It is not
       intended for general use. $)
    zaddablx $p |- G e. Abel $=
      ( vx vy vz cz caddc cv cneg cc0 zex addex wcel wceq zcn syl addcom syl2an
      cc co zaddcl addass syl3an 0z addid2 znegcl mpdan eqtr3d isgrpix grpbasex
      negid grpplusgx isabli ) CDFGACDEFGACHZIZJKLBUNDHZUAUNFMZUNSMZUPFMZUPSMZE
      HZFMVASMUNUPGTZVAGTUNUPVAGTGTNUNOZUPOZVAOUNUPVAUBUCUDUQURJUNGTUNNVCUNUEPU
      NUFZUQUNUOGTZUOUNGTZJUQUOFMZVFVGNZVEUQURUOSMVIVHVCUOOUNUOQRUGUQURVFJNVCUN
      UKPUHUIFGAKLBUJFGAKLBULUQURUTVBUPUNGTNUSVCVDUNUPQRUM $.
      $( [4-Sep-2011] $)
  $}

