$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Division rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c DivRing $.

  $( Extend class notation with class of all division rings. $)
  cdr $a class DivRing $.

  $( Define class of all division rings.  A division ring is a ring in which
     multiplication forms a group on the base set minus the ring zero element.
     Note that we construct an explicit 2-element group structure member to
     specify this condition. $)
  df-drng $a |- DivRing = { r e. Ring |
    { <. ( Base ` ndx ) , ( ( Base ` r ) \ { ( 0g ` r ) } ) >. ,
           <. ( +g ` ndx ) , ( .r ` r ) >. } e. Grp } $.

  ${
    $d r G $.  $d r R $.
    isdivrng.b $e |- B = ( Base ` R ) $.
    isdivrng.t $e |- T = ( .r ` R ) $.
    isdivrng.z $e |- Z = ( 0g ` R ) $.
    isdivrng.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
          <. ( +g ` ndx ) , T >. } $.
    $( The predicate "is a division ring". $)
    isdrng $p |- ( R e. DivRing <-> ( R e. Ring /\ G e. Grp ) ) $=
      ( vr cbs cfv c0g csn cdif cop cmulr cgrp wcel fveq2 cnx cv cplusg cpr crg
      cdr wceq syl6eqr difeq1d sneqd sneqi difeq2d opeq2d preq12d eleq1d eleq1i
      eqtrd syl6bbr df-drng elrab2 ) UAKLZJUBZKLZVBMLZNZOZPZUAUCLZVBQLZPZUDZRSZ
      DRSZJBUEUFVBBUGZVLVAAENZOZPZVHCPZUDZRSVMVNVKVSRVNVGVQVJVRVNVFVPVAVNVFAVEO
      VPVNVCAVEVNVCBKLAVBBKTFUHUIVNVEVOAVNVEBMLZNVOVNVDVTVBBMTUJEVTHUKUHULUQUMV
      NVICVHVNVIBQLCVBBQTGUHUMUNUODVSRIUPURJUSUT $.
      $( [18-Oct-2012] $)
  $}

  $( A division ring is a ring. $)
  drngrng $p |- ( R e. DivRing -> R e. Ring ) $=
    ( cdr wcel crg cnx cbs cfv c0g csn cdif cop cplusg cmulr cpr isdrng simplbi
    cgrp eqid ) ABCADCEFGAFGZAHGZIJKELGAMGZKNZQCSAUAUBTSRUARTRUBROP $.
    $( [8-Sep-2011] $)

  $( A division ring is a group. $)
  drnggrp $p |- ( R e. DivRing -> R e. Grp ) $=
    ( cdr wcel crg cgrp drngrng rnggrp syl ) ABCADCAECAFAGH $.
    $( [8-Sep-2011] $)

  ${
    $d u x K $.  $d u x L $.
    dvrngprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    dvrngprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    dvrngprop.m $e |- ( .r ` K ) = ( .r ` L ) $.
    $( If two structures have the same ring components (properties), one is a
       division ring iff the other one is. $)
    drngprop $p |- ( K e. DivRing <-> L e. DivRing ) $=
      ( vu vx cvv wcel cdr wn cbs cfv c0 wceq cgrp syl cop wa eqid wb con3i wne
      elisset fvprc drnggrp necon2bi 2falsed eqcomi crg cnx c0g csn cdif cplusg
      grpn0 cmulr cpr isdrng cv wral crio grpidval adantr adantl eqtr4d difeq2d
      co sneqd opeq2d preq1d eleq1d anbi2d rngprop anbi1i 3bitr4g syl5bb ecase
      ) AHIZBHIZAJIZBJIZUAVSKZWAWBWAVSAJUDUBWCALMZNOWBKALUEWBWDNWBBPIWDNUCBUFWD
      BCUPQUGQUHVTKZWAWBWEBLMZNOWAKBLUEWAWFNWAAPIWFNUCAUFWFAWDWFCUIUPQUGQWBVTBJ
      UDUBUHWAAUJIZUKLMZWDAULMZUMZUNZRZUKUOMAUQMZRZURZPIZSZVSVTSZWBWDAWMWOWIWDT
      ZWMTWITZWOTUSWRBUJIZWPSXAWHWDBULMZUMZUNZRZWNURZPIZSWQWBWRWPXGXAWRWOXFPWRW
      LXEWNWRWKXDWHWRWJXCWDWRWIXBWRWIFUTGUTZAUOMZVHXHOGWDVAFWDVBZXBVSWIXJOVTGWD
      XIFAWIHWSXITWTVCVDVTXBXJOVSGWDXIFBXBHCDXBTZVCVEVFVIVGVJVKVLVMWGXAWPABCDEV
      NVOWDBWMXFXBCEXKXFTUSVPVQVR $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d x y z B $.  $d x y z T $.  $d x U $.  $d x y z Z $.  $d x y z ph $.
    isdivrngd.b $e |- ( ph -> B = ( Base ` R ) ) $.
    isdivrngd.t $e |- ( ph -> T = ( .r ` R ) ) $.
    isdivrngd.z $e |- ( ph -> Z = ( 0g ` R ) ) $.
    isdivrngd.u $e |- ( ph -> U = ( 1r ` R ) ) $.
    isdivrngd.r $e |- ( ph -> R e. Ring ) $.
    isdivrngd.n $e |- ( ( ph /\ ( x e. B /\ x =/= Z )
            /\ ( y e. B /\ y =/= Z ) ) -> ( x T y ) =/= Z ) $.
    isdivrngd.o $e |- ( ph -> U =/= Z ) $.
    isdivrngd.i $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I e. B ) $.
    isdivrngd.j $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I =/= Z ) $.
    ${
      isdivrngd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( I T x ) = U ) $.
      $( Properties that determine a division ring. ` I ` (reciprocal) is
         normally dependent on ` x ` i.e. read it as ` I ( x ) ` ." $)
      isdrngd $p |- ( ph -> R e. DivRing ) $=
        ( wcel vz crg cnx cbs cfv c0g csn cdif cop cplusg cmulr cpr cgrp wa cdr
        difeq1d sneqd difeq2d eqtrd opeq2d eleq1d anbi2d cvv wceq fvex syl6eqel
        preq12d difexg syl eqid grpbase grpplusg prex a1i cv wne co eldifsn w3a
        syl3an1 3expib eleq2d anbi12d opreqd eleq12d 3adant2r 3adant3r sylanbrc
        rngcl 3imtr4d 3impib syl3an3b syl3an2b eldifi 3anim123i wi ex 3anbi123d
        rngass eqidd opreq123d eqeq12d imp sylan2 rngidcl mpbird rnglidm eqeq1d
        cur adantrr sylan2b isgrpd mpbi2and isdrng sylibr ) AEUBTZUCUDUEZEUDUEZ
        EUFUEZUGZUHZUIZUCUJUEZEUKUEZUIZULZUMTZUNZEUOTAXPXQDIUGZUHZUIZYCFUIZULZU
        MTZYHAYNYGXPAYMYFUMAYKYBYLYEAYJYAXQAYJXRYIUHYAADXRYIJUPAYIXTXRAIXSLUQUR
        USUTAFYDYCKUTVGVAVBNABCUAYJFYMHGAYJVCTZYJYMUDUEVDADVCTYOADXRVCJEUDVEVFD
        YIVCVHVIYJFYMVCYMVJZVKVIAFVCTFYMUJUEVDAFYDVCKEUKVEVFYJFYMVCYPVLVIYMVCTA
        YKYLVMVNBVOZYJTZAYQDTZYQIVPZUNZCVOZYJTZYQUUBFVQZYJTZYQDIVRZUUCAUUAUUBDT
        ZUUBIVPZUNZUUEUUBDIVRAUUAUUIVSUUDDTZUUDIVPUUEAUUAUUGUUJUUHAYSUUGUUJYTAY
        SUUGUUJAYQXRTZUUBXRTZUNYQUUBYDVQZXRTZYSUUGUNUUJAUUKUULUUNAXPUUKUULUUNNX
        REYDYQUUBXRVJZYDVJZWIVTWAAYSUUKUUGUULADXRYQJWBZADXRUUBJWBZWCAUUDUUMDXRA
        FYDYQUUBKWDZJWEWJWKWFWGOUUDDIVRWHWLWMYRUUCUAVOZYJTZVSAYSUUGUUTDTZVSZUUD
        UUTFVQZYQUUBUUTFVQZFVQZVDZYRYSUUCUUGUVAUVBYQDYIWNUUBDYIWNUUTDYIWNWOAUVC
        UVGAUUKUULUUTXRTZVSZUUMUUTYDVQZYQUUBUUTYDVQZYDVQZVDZUVCUVGAXPUVIUVMWPNX
        PUVIUVMXREYDYQUUBUUTUUOUUPWSWQVIAYSUUKUUGUULUVBUVHUUQUURADXRUUTJWBWRAUV
        DUVJUVFUVLAUUDUUMUUTUUTFYDKUUSAUUTWTXAAYQYQUVEUVKFYDKAYQWTZAFYDUUBUUTKW
        DXAXBWJXCXDAGDTZGIVPGYJTAUVOEXIUEZXRTZAXPUVQNXREUVPUUOUVPVJZXEVIAGUVPDX
        RMJWEXFPGDIVRWHYRAUUAGYQFVQZYQVDZUUFAYSUVTYTAYSUVTAUUKUVPYQYDVQZYQVDZYS
        UVTAXPUUKUWBWPNXPUUKUWBXREYDUVPYQUUOUUPUVRXGWQVIUUQAUVSUWAYQAGUVPYQYQFY
        DKMUVNXAXHWJXCXJXKYRAUUAHYJTZUUFAUUAUNHDTHIVPUWCQRHDIVRWHXKYRAUUAHYQFVQ
        GVDUUFSXKXLXMXREYDYFXSUUOUUPXSVJYFVJXNXO $.
        $( [2-Aug-2013] $)
    $}

    ${
      $d y z I $.
      isdivrngrd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( x T I ) = U )
          $.
      $( Properties that determine a division ring. ` I ` (reciprocal) is
         normally dependent on ` x ` i.e. read it as ` I ( x ) ` ."  In this
         version of ~ isdrngd we are use right reciprocal of ~ isgrprd rather
         than the left reciprocal of ~ isgrpd . $)
      isdrngrd $p |- ( ph -> R e. DivRing ) $=
        ( wcel vz cv wne wa csn cdif co wceq eldifsn w3a cbs cfv cmulr crg eqid
        syl3an1 3expib eleq2d anbi12d opreqd eleq12d 3adant2r 3adant3r sylanbrc
        rngcl 3imtr4d 3impib syl3an3b syl3an2b cur rngidcl syl eleqtrrd eqeltrd
        eldifi eqidd opreq123d adantr rngridm sylan sylbid imp sylan2 3anim123i
        ex eqtrd 3anbi123d wi rngass 3eqtr4d sylan2b grplinvd sylan2br isdrngd
        ) ABCDEFGHIJKLMNOPQRBUBZDTZWOIUCZUDZAWODIUEZUFZTZHWOFUGGUHWODIUIZABCUAW
        TFHGXAAWRCUBZWTTZWOXCFUGZWTTZXBXDAWRXCDTZXCIUCZUDZXFXCDIUIAWRXIUJXEDTZX
        EIUCXFAWRXGXJXHAWPXGXJWQAWPXGXJAWOEUKULZTZXCXKTZUDWOXCEUMULZUGZXKTZWPXG
        UDXJAXLXMXPAEUNTZXLXMXPNXKEXNWOXCXKUOZXNUOZVEUPUQAWPXLXGXMADXKWOJURZADX
        KXCJURZUSAXEXODXKAFXNWOXCKUTZJVAVFVGVBVCOXEDIUIVDVHVIAGDTGIUCGWTTAGEVJU
        LZDMAYCXKDAXQYCXKTNXKEYCXRYCUOZVKVLJVMVNPGDIUIVDXAAWPWOGFUGZWOUHWODWSVO
        ZAWPUDYEWOYCXNUGZWOAYEYGUHWPAWOWOGYCFXNKAWOVPZMVQVRAWPYGWOUHZAWPXLYIXTA
        XLYIAXQXLYINXKEXNYCWOXRXSYDVSVTWEWAWBWFWCXAXDUAUBZWTTZUJAWPXGYJDTZUJZXE
        YJFUGZWOXCYJFUGZFUGZUHXAWPXDXGYKYLYFXCDWSVOYJDWSVOWDAYMUDXOYJXNUGZWOXCY
        JXNUGZXNUGZYNYPAYMYQYSUHZAYMXLXMYJXKTZUJZYTAWPXLXGXMYLUUAXTYAADXKYJJURW
        GAXQUUBYTWHNXQUUBYTXKEXNWOXCYJXRXSWIWEVLWAWBAYNYQUHYMAXEXOYJYJFXNKYBAYJ
        VPVQVRAYPYSUHYMAWOWOYOYRFXNKYHAFXNXCYJKUTVQVRWJWCXAAWRHWTTZXBAWRUDHDTHI
        UCUUCQRHDIUIVDWKXAAWRWOHFUGGUHXBSWKWLWMWN $.
        $( [10-Aug-2013] $)
    $}
  $}

  ${
    divrngmgrp.b $e |- B = ( Base ` R ) $.
    divrngmgrp.t $e |- T = ( .r ` R ) $.
    divrngmgrp.z $e |- Z = ( 0g ` R ) $.
    divrngmgrp.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
                          <. ( +g ` ndx ) , T >. } $.
    $( A division ring contains a multiplicative group. $)
    drngmgrp $p |- ( R e. DivRing -> G e. Grp ) $=
      ( cdr wcel crg cgrp isdrng simprbi ) BJKBLKDMKABCDEFGHINO $.
      $( [8-Sep-2011] $)
  $}

  ${
    divrngmcl.b $e |- B = ( Base ` R ) $.
    divrngmcl.t $e |- T = ( .r ` R ) $.
    divrngmcl.z $e |- Z = ( 0g ` R ) $.
    $( The product of two nonzero elements of a division ring is nonzero. $)
    drngmcl $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } )
                    /\ Y e. ( B \ { Z } ) ) -> ( X T Y ) e. ( B \ { Z } ) ) $=
      ( wcel cnx cbs cfv cop cplusg cvv wceq fvex eqeltri ax-mp cdr csn cdif co
      cpr cgrp eqid drngmgrp difexg grpbase cmulr grpplusg grpcl syl3an1 ) BUAJ
      KLMAFUBZUCZNKOMCNUEZUFJDUPJEUPJDECUDUPJABCUQFGHIUQUGZUHUPCUQDEUPPJZUPUQLM
      QAPJUSABLMPGBLRSAUOPUITUPCUQPURUJTCPJCUQOMQCBUKMPHBUKRSUPCUQPURULTUMUN $.
      $( [7-Sep-2011] $)
  $}

  ${
    divrngid.b $e |- B = ( Base ` R ) $.
    divrngid.t $e |- T = ( .r ` R ) $.
    divrngid.z $e |- Z = ( 0g ` R ) $.
    divrngid.u $e |- U = ( 1r ` R ) $.
    divrngid.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
                          <. ( +g ` ndx ) , T >. } $.
    $( Lemma for ~ drngid and ~ drngunz . $)
    drngidlem $p |- ( R e. DivRing -> U =/= Z ) $=
      ( wcel cfv wne wa simpr sylbi cvv cbs wceq cdr c0g csn cdif cgrp crg fvex
      isdrng eqeltri difexg ax-mp grpbase eqid grpidcl eldifsn drngrng drngmgrp
      syl co eldifi 3syl rnglidm syl11anc opreq1 sylan9req rnglz adantr necon3d
      eqtrd ex mpd ) BUALZEUBMZFNZDFNVLVMAFUCZUDZLZVNVLEUELZVQVLBUFLZVROVRABCEF
      GHIKUHVSVRPQVPEVMVPRLZVPESMTARLVTABSMRGBSUGUIAVORUJUKVPCERKULUKVMUMUNZURV
      QVMALZVNOVNVMAFUOWBVNPQURVLDFVMFVLDFTZVMFTVLWCOVMFVMCUSZFVLWCVMDVMCUSZWDV
      LVSWBWEVMTBUPZVLVRVQWBABCEFGHIKUQWAVMAVOUTVAZABCDVMGHJVBVCDFVMCVDVEVLWDFT
      ZWCVLVSWBWHWFWGABCVMFGHIVFVCVGVIVJVHVK $.
      $( [8-Sep-2011] $)

    $( A division ring's unit is the identity element of its multiplicative
       group. $)
    drngid $p |- ( R e. DivRing -> U = ( 0g ` G ) ) $=
      ( wcel cfv co wceq cvv cbs fvex ax-mp syl cdr c0g crg drngrng csn eqeltri
      cdif drngmgrp difexg grpbase grpidcl eldifi rnglidm syl11anc cplusg cmulr
      cgrp grpplusg grplid eqtr4d wb wn rngidcl wne drngidlem df-ne sylib elsni
      eqid nsyl eldif sylanbrc grprcan syl13anc mpbid ) BUALZDEUBMZCNZVQVQCNZOZ
      DVQOZVPVRVQVSVPBUCLZVQALZVRVQOBUDZVPVQAFUEZUGZLZWCVPEUQLZWGABCEFGHIKUHZWF
      EVQWFPLZWFEQMOAPLWJABQMPGBQRUFAWEPUISWFCEPKUJSZVQVIZUKTZVQAWEULTABCDVQGHJ
      UMUNVPWHWGVSVQOWIWMWFCEVQVQWKCPLCEUOMOCBUPMPHBUPRUFWFCEPKURSZWLUSUNUTVPWH
      DWFLZWGWGVTWAVAWIVPDALZDWELZVBWOVPWBWPWDABDGJVCTVPDFOZWQVPDFVDWRVBABCDEFG
      HIJKVEDFVFVGDFVHVJDAWEVKVLWMWMWFCEDVQVQWKWNVMVNVO $.
      $( [7-Sep-2011] $)
  $}

  ${
    divrngunz.z $e |- Z = ( 0g ` R ) $.
    divrngunz.u $e |- U = ( 1r ` R ) $.
    $( A division ring's unit is different from its zero. $)
    drngunz $p |- ( R e. DivRing -> U =/= Z ) $=
      ( cbs cfv cmulr cnx csn cdif cop cplusg cpr eqid drngidlem ) AFGZAAHGZBIF
      GQCJKLIMGRLNZCQORODESOP $.
      $( [8-Sep-2011] $)
  $}

  ${
    dvrngid2.b $e |- B = ( Base ` R ) $.
    dvrngid2.t $e |- T = ( .r ` R ) $.
    dvrngid2.o $e |- Z = ( 0g ` R ) $.
    dvrngid2.u $e |- U = ( 1r ` R ) $.
    $( Properties showing that an element ` I ` is the identity element of a
       division ring. $)
    drngid2 $p |- ( R e. DivRing
        -> ( ( I e. B /\ I =/= Z /\ ( I T I ) = I ) <-> U = I ) ) $=
      ( wcel wceq cnx cbs cfv cop cplusg wa eqid cvv cdr wne co w3a csn cpr c0g
      cdif df-3an eldifsn anbi1i bitr4i cgrp wb drngmgrp eqeltri difexg grpbase
      fvex mp2b cmulr grpplusg ax-mp isgrpid2 syl syl5bb drngid eqeq1d bitr4d )
      BUAKZEAKZEFUBZEECUCELZUDZMNOAFUEZUHZPMQOCPUFZUGOZELZDELVNEVPKZVMRZVJVSVNV
      KVLRZVMRWAVKVLVMUIVTWBVMEAFUJUKULVJVQUMKWAVSUNABCVQFGHIVQSZUOVPCVQVREATKV
      PTKVPVQNOLABNOTGBNUSUPAVOTUQVPCVQTWCURUTCTKCVQQOLCBVAOTHBVAUSUPVPCVQTWCVB
      VCVRSVDVEVFVJDVREABCDVQFGHIJWCVGVHVI $.
      $( [11-Oct-2013] $)
  $}

  $c invr $.

  $( Extend class notation with multiplicative inverse. $)
  cinvr $a class invr $.

  $( Define multiplicative inverse. $)
  df-invr $a |- invr = ( r e. _V |-> ( invg `
      { <. ( Base ` ndx ) , ( ( Base ` r ) \ { ( 0g ` r ) } ) >. ,
        <. ( +g ` ndx ) , ( .r ` r ) >. } ) ) $.

  ${
    $d r N $.  $d r R $.
    invrfval.b $e |- B = ( Base ` R ) $.
    invrfval.z $e |- Z = ( 0g ` R ) $.
    invrfval.t $e |- T = ( .r ` R ) $.
    invrfval.4 $e |- G =
           { <. ( Base ` ndx ) , ( B \ { Z } ) >. , <. ( +g ` ndx ) , T >. } $.
    invrfval.n $e |- N = ( invg ` G ) $.
    invrfval.i $e |- I = ( invr ` R ) $.
    $( Multiplicative inverse function for a division ring. $)
    invrfval $p |- ( R e. A -> I = N ) $=
      ( vr cfv cbs cop cminusg syl6eqr wcel cinvr cvv wceq elisset cnx c0g cdif
      cv csn cplusg cmulr cpr fveq2 difeq1d sneqd difeq2d opeq2d preq12d fveq2d
      eqtrd df-invr fvex eqeltri fvmpt syl syl5eq ) CAUAZFCUBPZGNVHCUCUAVIGUDCA
      UEOCUFQPZOUIZQPZVKUGPZUJZUHZRZUFUKPZVKULPZRZUMZSPZGUCUBVKCUDZWAESPZGWBVTE
      SWBVTVJBHUJZUHZRZVQDRZUMEWBVPWFVSWGWBVOWEVJWBVOBVNUHWEWBVLBVNWBVLCQPBVKCQ
      UNITUOWBVNWDBWBVMHWBVMCUGPHVKCUGUNJTUPUQVAURWBVRDVQWBVRCULPDVKCULUNKTURUS
      LTUTMTOVBGWCUCMESVCVDVEVFVG $.
      $( [21-Sep-2011] $)
  $}

  ${
    invrcl.b $e |- B = ( Base ` R ) $.
    invrcl.z $e |- Z = ( 0g ` R ) $.
    invrcl.i $e |- I = ( invr ` R ) $.
    $( Closure of the multiplicative inverse in a division ring. $)
    invrcl $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } ) )
             -> ( I ` X ) e. ( B \ { Z } ) ) $=
      ( cdr wcel csn cdif wa cfv cnx cbs cop wceq eqid cvv cplusg cmulr cminusg
      cpr invrfval fveq1d adantr cgrp drngmgrp fvex eqeltri difexg grpbase mp2b
      grpinvcl sylan eqeltrd ) BIJZDAEKZLZJZMDCNZDOPNUTQOUANBUBNZQUDZUCNZNZUTUR
      VBVFRVAURDCVEIABVCVDCVEEFGVCSZVDSZVESZHUEUFUGURVDUHJVAVFUTJABVCVDEFVGGVHU
      IUTVDVEDATJUTTJUTVDPNRABPNTFBPUJUKAUSTULUTVCVDTVHUMUNVIUOUPUQ $.
      $( [21-Sep-2011] $)
  $}

  ${
    invrl.b $e |- B = ( Base ` R ) $.
    invrl.z $e |- Z = ( 0g ` R ) $.
    invrl.t $e |- T = ( .r ` R ) $.
    invrl.u $e |- U = ( 1r ` R ) $.
    invrl.i $e |- I = ( invr ` R ) $.
    $( Property of the multiplicative inverse in a division ring. $)
    invrl $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } ) )
             -> ( ( I ` X ) T X ) = U ) $=
      ( cdr wcel cnx cbs cfv wceq eqid cvv csn wa cop cplusg cpr cminusg co c0g
      cdif cgrp drngmgrp fvex eqeltri grpbase mp2b cmulr grpplusg ax-mp grplinv
      difexg sylan invrfval fveq1d opreq1d adantr drngid 3eqtr4d ) BMNZFAGUAZUI
      ZNZUBFOPQVJUCOUDQCUCUEZUFQZQZFCUGZVLUHQZFEQZFCUGZDVHVLUJNVKVOVPRABCVLGHJI
      VLSZUKVJCVLVMVPFATNVJTNVJVLPQRABPQTHBPULUMAVITUTVJCVLTVSUNUOCTNCVLUDQRCBU
      PQTJBUPULUMVJCVLTVSUQURVPSVMSZUSVAVHVRVORVKVHVQVNFCVHFEVMMABCVLEVMGHIJVSV
      TLVBVCVDVEVHDVPRVKABCDVLGHJIKVSVFVEVG $.
      $( [10-Apr-2014] $)

    $( Property of the multiplicative inverse in a division ring. $)
    invrr $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } ) )
             -> ( X T ( I ` X ) ) = U ) $=
      ( cdr wcel cnx cbs cfv wceq eqid cvv csn wa cop cplusg cpr cminusg co c0g
      cdif cgrp drngmgrp fvex eqeltri grpbase mp2b cmulr grpplusg ax-mp grprinv
      difexg sylan invrfval fveq1d opreq2d adantr drngid 3eqtr4d ) BMNZFAGUAZUI
      ZNZUBFFOPQVJUCOUDQCUCUEZUFQZQZCUGZVLUHQZFFEQZCUGZDVHVLUJNVKVOVPRABCVLGHJI
      VLSZUKVJCVLVMVPFATNVJTNVJVLPQRABPQTHBPULUMAVITUTVJCVLTVSUNUOCTNCVLUDQRCBU
      PQTJBUPULUMVJCVLTVSUQURVPSVMSZUSVAVHVRVORVKVHVQVNFCVHFEVMMABCVLEVMGHIJVSV
      TLVBVCVDVEVHDVPRVKABCDVLGHJIKVSVFVEVG $.
      $( [10-Apr-2014] $)
  $}

