$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Division rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c DivRing $.

  $( Extend class notation with class of all division rings. $)
  cdivring $a class DivRing $.

  $( Define class of all division rings.  A division ring is a ring in which
     multiplication forms a group on the base set minus the ring zero element.
     Note that we construct an explicit 2-element group structure member to
     specify this condition. $)
  df-drng $a |- DivRing = { r e. Ring |
    { <. ( Base ` ndx ) , ( ( Base ` r ) \ { ( 0g ` r ) } ) >. ,
           <. ( +g ` ndx ) , ( .r ` r ) >. } e. Grp } $.

  ${
    $d r G $.  $d r R $.
    isdivrng.b $e |- B = ( Base ` R ) $.
    isdivrng.t $e |- T = ( .r ` R ) $.
    isdivrng.z $e |- Z = ( 0g ` R ) $.
    isdivrng.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
          <. ( +g ` ndx ) , T >. } $.
    $( The predicate "is a division ring". $)
    isdivrng $p |- ( R e. DivRing <-> ( R e. Ring /\ G e. Grp ) ) $=
      ( vr cbs cfv c0g csn cdif cop cmulr cgrp wcel fveq2 cnx cv cplusg cpr crg
      cdivring syl6eqr difeq1d sneqd sneqi difeq2d opeq2d preq12d eleq1d eleq1i
      wceq eqtrd syl6bbr df-drng elrab2 ) UAKLZJUBZKLZVBMLZNZOZPZUAUCLZVBQLZPZU
      DZRSZDRSZJBUEUFVBBUPZVLVAAENZOZPZVHCPZUDZRSVMVNVKVSRVNVGVQVJVRVNVFVPVAVNV
      FAVEOVPVNVCAVEVNVCBKLAVBBKTFUGUHVNVEVOAVNVEBMLZNVOVNVDVTVBBMTUIEVTHUJUGUK
      UQULVNVICVHVNVIBQLCVBBQTGUGULUMUNDVSRIUOURJUSUT $.
      $( [18-Oct-2012] $)
  $}

  $( A division ring is a ring. $)
  divrngring $p |- ( R e. DivRing -> R e. Ring ) $=
    ( cdivring wcel crg cnx cbs cfv c0g csn cdif cop cplusg cmulr cpr cgrp eqid
    isdivrng simplbi ) ABCADCEFGAFGZAHGZIJKELGAMGZKNZOCSAUAUBTSPUAPTPUBPQR $.
    $( [8-Sep-2011] $)

  $( A division ring is a group. $)
  divrnggrp $p |- ( R e. DivRing -> R e. Grp ) $=
    ( cdivring wcel crg cgrp divrngring ringgrp syl ) ABCADCAECAFAGH $.
    $( [8-Sep-2011] $)

  ${
    $d u x K $.  $d u x L $.
    dvrngprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    dvrngprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    dvrngprop.m $e |- ( .r ` K ) = ( .r ` L ) $.
    $( If two structures have the same ring components (properties), one is a
       division ring iff the other one is. $)
    dvrngprop $p |- ( K e. DivRing <-> L e. DivRing ) $=
      ( vu vx cvv wcel cdivring wn cbs cfv c0 wceq cgrp syl cop wa eqid elisset
      wb con3i fvprc wne divrnggrp necon2bi 2falsed eqcomi crg cnx c0g csn cdif
      grpn0 cplusg cmulr isdivrng cv co wral crio grpidval adantr adantl eqtr4d
      cpr sneqd difeq2d opeq2d preq1d eleq1d anbi2d rngprop anbi1i syl5bb ecase
      3bitr4g ) AHIZBHIZAJIZBJIZUBVSKZWAWBWAVSAJUAUCWCALMZNOWBKALUDWBWDNWBBPIWD
      NUEBUFWDBCUOQUGQUHVTKZWAWBWEBLMZNOWAKBLUDWAWFNWAAPIWFNUEAUFWFAWDWFCUIUOQU
      GQWBVTBJUAUCUHWAAUJIZUKLMZWDAULMZUMZUNZRZUKUPMAUQMZRZVGZPIZSZVSVTSZWBWDAW
      MWOWIWDTZWMTWITZWOTURWRBUJIZWPSXAWHWDBULMZUMZUNZRZWNVGZPIZSWQWBWRWPXGXAWR
      WOXFPWRWLXEWNWRWKXDWHWRWJXCWDWRWIXBWRWIFUSGUSZAUPMZUTXHOGWDVAFWDVBZXBVSWI
      XJOVTGWDXIFAWIHWSXITWTVCVDVTXBXJOVSGWDXIFBXBHCDXBTZVCVEVFVHVIVJVKVLVMWGXA
      WPABCDEVNVOWDBWMXFXBCEXKXFTURVRVPVQ $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d x y z B $.  $d x y z T $.  $d x U $.  $d x y z Z $.  $d x y z ph $.
    isdivrngd.b $e |- ( ph -> B = ( Base ` R ) ) $.
    isdivrngd.t $e |- ( ph -> T = ( .r ` R ) ) $.
    isdivrngd.z $e |- ( ph -> Z = ( 0g ` R ) ) $.
    isdivrngd.u $e |- ( ph -> U = ( 1r ` R ) ) $.
    isdivrngd.r $e |- ( ph -> R e. Ring ) $.
    isdivrngd.n $e |- ( ( ph /\ ( x e. B /\ x =/= Z )
            /\ ( y e. B /\ y =/= Z ) ) -> ( x T y ) =/= Z ) $.
    isdivrngd.o $e |- ( ph -> U =/= Z ) $.
    isdivrngd.i $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I e. B ) $.
    isdivrngd.j $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I =/= Z ) $.
    ${
      isdivrngd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( I T x ) = U ) $.
      $( Properties that determine a division ring. ` I ` (reciprocal) is
         normally dependent on ` x ` i.e. read it as ` I ( x ) ` ." $)
      isdivrngd $p |- ( ph -> R e. DivRing ) $=
        ( wcel vz crg cnx cbs cfv c0g csn cdif cop cplusg cmulr cpr wa cdivring
        cgrp difeq1d sneqd difeq2d eqtrd opeq2d preq12d eleq1d anbi2d wceq fvex
        cvv syl6eqel difexg syl eqid grpbase grpplusg a1i cv wne co eldifsn w3a
        prex ringcl syl3an1 3expib eleq2d anbi12d opreqd eleq12d 3imtr4d 3impib
        3adant2r 3adant3r sylanbrc syl3an3b syl3an2b eldifi 3anim123i 3anbi123d
        wi ringass ex opreq123d eqeq12d imp sylan2 cur ringidcl mpbird ringlidm
        eqidd eqeq1d adantrr sylan2b isgrpd mpbi2and isdivrng sylibr ) AEUBTZUC
        UDUEZEUDUEZEUFUEZUGZUHZUIZUCUJUEZEUKUEZUIZULZUOTZUMZEUNTAXPXQDIUGZUHZUI
        ZYCFUIZULZUOTZYHAYNYGXPAYMYFUOAYKYBYLYEAYJYAXQAYJXRYIUHYAADXRYIJUPAYIXT
        XRAIXSLUQURUSUTAFYDYCKUTVAVBVCNABCUAYJFYMHGAYJVFTZYJYMUDUEVDADVFTYOADXR
        VFJEUDVEVGDYIVFVHVIYJFYMVFYMVJZVKVIAFVFTFYMUJUEVDAFYDVFKEUKVEVGYJFYMVFY
        PVLVIYMVFTAYKYLVSVMBVNZYJTZAYQDTZYQIVOZUMZCVNZYJTZYQUUBFVPZYJTZYQDIVQZU
        UCAUUAUUBDTZUUBIVOZUMZUUEUUBDIVQAUUAUUIVRUUDDTZUUDIVOUUEAUUAUUGUUJUUHAY
        SUUGUUJYTAYSUUGUUJAYQXRTZUUBXRTZUMYQUUBYDVPZXRTZYSUUGUMUUJAUUKUULUUNAXP
        UUKUULUUNNXREYDYQUUBXRVJZYDVJZVTWAWBAYSUUKUUGUULADXRYQJWCZADXRUUBJWCZWD
        AUUDUUMDXRAFYDYQUUBKWEZJWFWGWHWIWJOUUDDIVQWKWLWMYRUUCUAVNZYJTZVRAYSUUGU
        UTDTZVRZUUDUUTFVPZYQUUBUUTFVPZFVPZVDZYRYSUUCUUGUVAUVBYQDYIWNUUBDYIWNUUT
        DYIWNWOAUVCUVGAUUKUULUUTXRTZVRZUUMUUTYDVPZYQUUBUUTYDVPZYDVPZVDZUVCUVGAX
        PUVIUVMWQNXPUVIUVMXREYDYQUUBUUTUUOUUPWRWSVIAYSUUKUUGUULUVBUVHUUQUURADXR
        UUTJWCWPAUVDUVJUVFUVLAUUDUUMUUTUUTFYDKUUSAUUTXHWTAYQYQUVEUVKFYDKAYQXHZA
        FYDUUBUUTKWEWTXAWGXBXCAGDTZGIVOGYJTAUVOEXDUEZXRTZAXPUVQNXREUVPUUOUVPVJZ
        XEVIAGUVPDXRMJWFXFPGDIVQWKYRAUUAGYQFVPZYQVDZUUFAYSUVTYTAYSUVTAUUKUVPYQY
        DVPZYQVDZYSUVTAXPUUKUWBWQNXPUUKUWBXREYDUVPYQUUOUUPUVRXGWSVIUUQAUVSUWAYQ
        AGUVPYQYQFYDKMUVNWTXIWGXBXJXKYRAUUAHYJTZUUFAUUAUMHDTHIVOUWCQRHDIVQWKXKY
        RAUUAHYQFVPGVDUUFSXKXLXMXREYDYFXSUUOUUPXSVJYFVJXNXO $.
        $( [2-Aug-2013] $)
    $}

    ${
      $d y z I $.
      isdivrngrd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( x T I ) = U )
          $.
      $( Properties that determine a division ring. ` I ` (reciprocal) is
         normally dependent on ` x ` i.e. read it as ` I ( x ) ` ."  In this
         version of ~ isdivrngd we are use right reciprocal of ~ isgrprd rather
         than the left reciprocal of ~ isgrpd . $)
      isdivrngrd $p |- ( ph -> R e. DivRing ) $=
        ( wcel vz cv wne wa csn cdif co wceq eldifsn w3a cbs cfv cmulr crg eqid
        ringcl syl3an1 3expib eleq2d anbi12d eleq12d 3adant2r 3adant3r sylanbrc
        opreqd 3imtr4d 3impib syl3an3b syl3an2b cur ringidcl syl eqeltrd eldifi
        eleqtrrd eqidd opreq123d adantr ringridm ex sylbid imp sylan2 3anim123i
        sylan 3anbi123d wi ringass 3eqtr4d sylan2b grplinvd sylan2br isdivrngd
        eqtrd ) ABCDEFGHIJKLMNOPQRBUBZDTZWOIUCZUDZAWODIUEZUFZTZHWOFUGGUHWODIUIZ
        ABCUAWTFHGXAAWRCUBZWTTZWOXCFUGZWTTZXBXDAWRXCDTZXCIUCZUDZXFXCDIUIAWRXIUJ
        XEDTZXEIUCXFAWRXGXJXHAWPXGXJWQAWPXGXJAWOEUKULZTZXCXKTZUDWOXCEUMULZUGZXK
        TZWPXGUDXJAXLXMXPAEUNTZXLXMXPNXKEXNWOXCXKUOZXNUOZUPUQURAWPXLXGXMADXKWOJ
        USZADXKXCJUSZUTAXEXODXKAFXNWOXCKVEZJVAVFVGVBVCOXEDIUIVDVHVIAGDTGIUCGWTT
        AGEVJULZDMAYCXKDAXQYCXKTNXKEYCXRYCUOZVKVLJVOVMPGDIUIVDXAAWPWOGFUGZWOUHW
        ODWSVNZAWPUDYEWOYCXNUGZWOAYEYGUHWPAWOWOGYCFXNKAWOVPZMVQVRAWPYGWOUHZAWPX
        LYIXTAXLYIAXQXLYINXKEXNYCWOXRXSYDVSWEVTWAWBWNWCXAXDUAUBZWTTZUJAWPXGYJDT
        ZUJZXEYJFUGZWOXCYJFUGZFUGZUHXAWPXDXGYKYLYFXCDWSVNYJDWSVNWDAYMUDXOYJXNUG
        ZWOXCYJXNUGZXNUGZYNYPAYMYQYSUHZAYMXLXMYJXKTZUJZYTAWPXLXGXMYLUUAXTYAADXK
        YJJUSWFAXQUUBYTWGNXQUUBYTXKEXNWOXCYJXRXSWHVTVLWAWBAYNYQUHYMAXEXOYJYJFXN
        KYBAYJVPVQVRAYPYSUHYMAWOWOYOYRFXNKYHAFXNXCYJKVEVQVRWIWCXAAWRHWTTZXBAWRU
        DHDTHIUCUUCQRHDIUIVDWJXAAWRWOHFUGGUHXBSWJWKWLWM $.
        $( [10-Aug-2013] $)
    $}
  $}

  ${
    divrngmgrp.b $e |- B = ( Base ` R ) $.
    divrngmgrp.t $e |- T = ( .r ` R ) $.
    divrngmgrp.z $e |- Z = ( 0g ` R ) $.
    divrngmgrp.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
                          <. ( +g ` ndx ) , T >. } $.
    $( A division ring contains a multiplicative group. $)
    divrngmgrp $p |- ( R e. DivRing -> G e. Grp ) $=
      ( cdivring wcel crg cgrp isdivrng simprbi ) BJKBLKDMKABCDEFGHINO $.
      $( [8-Sep-2011] $)
  $}

  ${
    divrngmcl.b $e |- B = ( Base ` R ) $.
    divrngmcl.t $e |- T = ( .r ` R ) $.
    divrngmcl.z $e |- Z = ( 0g ` R ) $.
    $( The product of two nonzero elements of a division ring is nonzero. $)
    divrngmcl $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } )
                    /\ Y e. ( B \ { Z } ) ) -> ( X T Y ) e. ( B \ { Z } ) ) $=
      ( wcel cnx cbs cfv cop cplusg cvv wceq fvex eqeltri ax-mp cdivring csn co
      cdif cpr cgrp eqid divrngmgrp difexg grpbase cmulr grpplusg grpcl syl3an1
      ) BUAJKLMAFUBZUDZNKOMCNUEZUFJDUPJEUPJDECUCUPJABCUQFGHIUQUGZUHUPCUQDEUPPJZ
      UPUQLMQAPJUSABLMPGBLRSAUOPUITUPCUQPURUJTCPJCUQOMQCBUKMPHBUKRSUPCUQPURULTU
      MUN $.
      $( [7-Sep-2011] $)
  $}

  ${
    divrngid.b $e |- B = ( Base ` R ) $.
    divrngid.t $e |- T = ( .r ` R ) $.
    divrngid.z $e |- Z = ( 0g ` R ) $.
    divrngid.u $e |- U = ( 1r ` R ) $.
    divrngid.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
                          <. ( +g ` ndx ) , T >. } $.
    $( Lemma for ~ divrngid and ~ divrngunz . $)
    divrngidlem $p |- ( R e. DivRing -> U =/= Z ) $=
      ( wcel cfv wne wa simpr sylbi cvv cbs wceq cdivring c0g csn cdif cgrp crg
      isdivrng fvex eqeltri difexg ax-mp grpbase grpidcl syl eldifsn divrngring
      eqid co divrngmgrp eldifi ringlidm syl11anc opreq1 sylan9req ringlz eqtrd
      3syl adantr ex necon3d mpd ) BUALZEUBMZFNZDFNVLVMAFUCZUDZLZVNVLEUELZVQVLB
      UFLZVROVRABCEFGHIKUGVSVRPQVPEVMVPRLZVPESMTARLVTABSMRGBSUHUIAVORUJUKVPCERK
      ULUKVMUQUMZUNVQVMALZVNOVNVMAFUOWBVNPQUNVLDFVMFVLDFTZVMFTVLWCOVMFVMCURZFVL
      WCVMDVMCURZWDVLVSWBWEVMTBUPZVLVRVQWBABCEFGHIKUSWAVMAVOUTVGZABCDVMGHJVAVBD
      FVMCVCVDVLWDFTZWCVLVSWBWHWFWGABCVMFGHIVEVBVHVFVIVJVK $.
      $( [8-Sep-2011] $)

    $( A division ring's unit is the identity element of its multiplicative
       group. $)
    divrngid $p |- ( R e. DivRing -> U = ( 0g ` G ) ) $=
      ( wcel cfv co wceq cvv cbs fvex ax-mp syl cdivring c0g crg divrngring csn
      cdif cgrp divrngmgrp eqeltri difexg grpbase eqid eldifi ringlidm syl11anc
      grpidcl cplusg cmulr grpplusg grplid eqtr4d wb ringidcl divrngidlem df-ne
      wn wne sylib elsni nsyl eldif sylanbrc grprcan syl13anc mpbid ) BUALZDEUB
      MZCNZVQVQCNZOZDVQOZVPVRVQVSVPBUCLZVQALZVRVQOBUDZVPVQAFUEZUFZLZWCVPEUGLZWG
      ABCEFGHIKUHZWFEVQWFPLZWFEQMOAPLWJABQMPGBQRUIAWEPUJSWFCEPKUKSZVQULZUPTZVQA
      WEUMTABCDVQGHJUNUOVPWHWGVSVQOWIWMWFCEVQVQWKCPLCEUQMOCBURMPHBURRUIWFCEPKUS
      SZWLUTUOVAVPWHDWFLZWGWGVTWAVBWIVPDALZDWELZVFWOVPWBWPWDABDGJVCTVPDFOZWQVPD
      FVGWRVFABCDEFGHIJKVDDFVEVHDFVIVJDAWEVKVLWMWMWFCEDVQVQWKWNVMVNVO $.
      $( [7-Sep-2011] $)
  $}

  ${
    divrngunz.z $e |- Z = ( 0g ` R ) $.
    divrngunz.u $e |- U = ( 1r ` R ) $.
    $( A division ring's unit is different from its zero. $)
    divrngunz $p |- ( R e. DivRing -> U =/= Z ) $=
      ( cbs cfv cmulr cnx csn cdif cop cplusg cpr eqid divrngidlem ) AFGZAAHGZB
      IFGQCJKLIMGRLNZCQORODESOP $.
      $( [8-Sep-2011] $)
  $}

  ${
    dvrngid2.b $e |- B = ( Base ` R ) $.
    dvrngid2.t $e |- T = ( .r ` R ) $.
    dvrngid2.o $e |- Z = ( 0g ` R ) $.
    dvrngid2.u $e |- U = ( 1r ` R ) $.
    $( Properties showing that an element ` I ` is the identity element of a
       division ring. $)
    dvrngid2 $p |- ( R e. DivRing
        -> ( ( I e. B /\ I =/= Z /\ ( I T I ) = I ) <-> U = I ) ) $=
      ( wcel wceq cnx cbs cfv cop cplusg wa eqid cvv cdivring wne co w3a df-3an
      csn cdif cpr eldifsn anbi1i bitr4i cgrp wb divrngmgrp fvex eqeltri difexg
      c0g grpbase mp2b cmulr grpplusg ax-mp isgrpid2 syl syl5bb divrngid eqeq1d
      bitr4d ) BUAKZEAKZEFUBZEECUCELZUDZMNOAFUFZUGZPMQOCPUHZUROZELZDELVNEVPKZVM
      RZVJVSVNVKVLRZVMRWAVKVLVMUEVTWBVMEAFUIUJUKVJVQULKWAVSUMABCVQFGHIVQSZUNVPC
      VQVREATKVPTKVPVQNOLABNOTGBNUOUPAVOTUQVPCVQTWCUSUTCTKCVQQOLCBVAOTHBVAUOUPV
      PCVQTWCVBVCVRSVDVEVFVJDVREABCDVQFGHIJWCVGVHVI $.
      $( [11-Oct-2013] $)
  $}

  $c invr $.

  $( Extend class notation with multiplicative inverse. $)
  cinvr $a class invr $.

  $( Define multiplicative inverse. $)
  df-invr $a |- invr = ( r e. _V |-> ( -g `
      { <. ( Base ` ndx ) , ( ( Base ` r ) \ { ( 0g ` r ) } ) >. ,
        <. ( +g ` ndx ) , ( .r ` r ) >. } ) ) $.

  ${
    $d r N $.  $d r R $.
    invrfval.b $e |- B = ( Base ` R ) $.
    invrfval.z $e |- Z = ( 0g ` R ) $.
    invrfval.t $e |- T = ( .r ` R ) $.
    invrfval.4 $e |- G =
           { <. ( Base ` ndx ) , ( B \ { Z } ) >. , <. ( +g ` ndx ) , T >. } $.
    invrfval.n $e |- N = ( -g ` G ) $.
    invrfval.i $e |- I = ( invr ` R ) $.
    $( Multiplicative inverse function for a division ring. $)
    invrfval $p |- ( R e. A -> I = N ) $=
      ( vr cfv cbs cop cminusg syl6eqr wcel cinvr cvv wceq elisset cnx c0g cdif
      cv csn cplusg cmulr cpr fveq2 difeq1d sneqd difeq2d opeq2d preq12d fveq2d
      eqtrd df-invr fvex eqeltri fvmpt syl syl5eq ) CAUAZFCUBPZGNVHCUCUAVIGUDCA
      UEOCUFQPZOUIZQPZVKUGPZUJZUHZRZUFUKPZVKULPZRZUMZSPZGUCUBVKCUDZWAESPZGWBVTE
      SWBVTVJBHUJZUHZRZVQDRZUMEWBVPWFVSWGWBVOWEVJWBVOBVNUHWEWBVLBVNWBVLCQPBVKCQ
      UNITUOWBVNWDBWBVMHWBVMCUGPHVKCUGUNJTUPUQVAURWBVRDVQWBVRCULPDVKCULUNKTURUS
      LTUTMTOVBGWCUCMESVCVDVEVFVG $.
      $( [21-Sep-2011] $)
  $}

  ${
    invrcl.b $e |- B = ( Base ` R ) $.
    invrcl.z $e |- Z = ( 0g ` R ) $.
    invrcl.i $e |- I = ( invr ` R ) $.
    $( Closure of the multiplicative inverse in a division ring. $)
    invrcl $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } ) )
             -> ( I ` X ) e. ( B \ { Z } ) ) $=
      ( cdivring wcel csn cfv cnx cbs cop cplusg wceq eqid cvv ax-mp cdif cmulr
      wa cpr cminusg invrfval fveq1d adantr cgrp divrngmgrp fvex eqeltri difexg
      grpbase grpinvcl sylan eqeltrd ) BIJZDAEKZUAZJZUCDCLZDMNLUTOMPLBUBLZOUDZU
      ELZLZUTURVBVFQVAURDCVEIABVCVDCVEEFGVCRZVDRZVERZHUFUGUHURVDUIJVAVFUTJABVCV
      DEFVGGVHUJUTVDPLZVDVEDUTSJZUTVDNLQASJVKABNLSFBNUKULAUSSUMTUTVCVDSVHUNTVJR
      VIUOUPUQ $.
      $( [21-Sep-2011] $)
  $}

