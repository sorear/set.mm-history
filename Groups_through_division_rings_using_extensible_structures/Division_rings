$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Division rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c DivRing $.

  $( Extend class notation with class of all division rings. $)
  cdivring $a class DivRing $.

  $( Define class of all division rings.  A division ring is a ring in which
     multiplication forms a group on the base set minus the ring zero element.
     Note that we construct an explicit 2-element group structure member to
     specify this condition. $)
  df-drng $a |- DivRing = { r e. Ring |
    { <. ( Base ` ndx ) , ( ( Base ` r ) \ { ( 0g ` r ) } ) >. ,
           <. ( +g ` ndx ) , ( .r ` r ) >. } e. Grp } $.

  ${
    $d r G $.  $d r R $.
    isdivrng.b $e |- B = ( Base ` R ) $.
    isdivrng.t $e |- T = ( .r ` R ) $.
    isdivrng.z $e |- Z = ( 0g ` R ) $.
    isdivrng.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
          <. ( +g ` ndx ) , T >. } $.
    $( The predicate "is a division ring". $)
    isdivrng $p |- ( R e. DivRing <-> ( R e. Ring /\ G e. Grp ) ) $=
      ( vr cnx cbs cfv cv c0g csn cdif cop cplusg cmulr cpr cgrp wcel crg
      cdivring wceq fveq2 syl6eqr difeq1d sneqd sneqi difeq2d eqtrd opeq2d
      preq12d eleq1d eleq1i syl6bbr df-drng elrab2 ) KLMZJNZLMZVBOMZPZQZRZKSMZV
      BTMZRZUAZUBUCZDUBUCZJBUDUEVBBUFZVLVAAEPZQZRZVHCRZUAZUBUCVMVNVKVSUBVNVGVQV
      JVRVNVFVPVAVNVFAVEQVPVNVCAVEVNVCBLMAVBBLUGFUHUIVNVEVOAVNVEBOMZPVOVNVDVTVB
      BOUGUJEVTHUKUHULUMUNVNVICVHVNVIBTMCVBBTUGGUHUNUOUPDVSUBIUQURJUSUT $.
      $( [18-Oct-2012] $)
  $}

  $( A division ring is a ring. $)
  divrngring $p |- ( R e. DivRing -> R e. Ring ) $=
    ( cdivring wcel crg cnx cbs cfv c0g csn cdif cop cplusg cmulr cpr cgrp
    eqid isdivrng simplbi ) ABCADCEFGAFGZAHGZIJKELGAMGZKNZOCSAUAUBTSPUAPTPUBPQR
    $.
    $( [8-Sep-2011] $)

  $( A division ring is a group. $)
  divrnggrp $p |- ( R e. DivRing -> R e. Grp ) $=
    ( cdivring wcel crg cgrp divrngring ringgrp syl ) ABCADCAECAFAGH $.
    $( [8-Sep-2011] $)

  ${
    $d u x K $.  $d u x L $.
    dvrngprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    dvrngprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    dvrngprop.m $e |- ( .r ` K ) = ( .r ` L ) $.
    $( If two structures have the same ring components (properties), one is a
       division ring iff the other one is. $)
    dvrngprop $p |- ( K e. DivRing <-> L e. DivRing ) $=
      ( vu vx cvv wcel cdivring wb wn elisset con3i cbs cfv c0 wceq fvprc cgrp
      wne divrnggrp grpn0 syl necon2bi 2falsed eqcomi crg cnx c0g csn cdif cop
      cplusg cmulr cpr wa eqid isdivrng cv co wral crio grpidval adantr adantl
      eqtr4d sneqd difeq2d opeq2d preq1d eleq1d anbi2d rngprop anbi1i 3bitr4g
      syl5bb ecase ) AHIZBHIZAJIZBJIZKVSLZWAWBWAVSAJMNWCAOPZQRWBLAOSWBWDQWBBTIW
      DQUABUBWDBCUCUDUEUDUFVTLZWAWBWEBOPZQRWALBOSWAWFQWAATIWFQUAAUBWFAWDWFCUGUC
      UDUEUDWBVTBJMNUFWAAUHIZUIOPZWDAUJPZUKZULZUMZUIUNPAUOPZUMZUPZTIZUQZVSVTUQZ
      WBWDAWMWOWIWDURZWMURWIURZWOURUSWRBUHIZWPUQXAWHWDBUJPZUKZULZUMZWNUPZTIZUQW
      QWBWRWPXGXAWRWOXFTWRWLXEWNWRWKXDWHWRWJXCWDWRWIXBWRWIFUTGUTZAUNPZVAXHRGWDV
      BFWDVCZXBVSWIXJRVTGWDXIFAWIHWSXIURWTVDVEVTXBXJRVSGWDXIFBXBHCDXBURZVDVFVGV
      HVIVJVKVLVMWGXAWPABCDEVNVOWDBWMXFXBCEXKXFURUSVPVQVR $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d x y z B $.  $d x y z T $.  $d x U $.  $d x y z Z $.  $d x y z ph $.
    isdivrngd.b $e |- ( ph -> B = ( Base ` R ) ) $.
    isdivrngd.t $e |- ( ph -> T = ( .r ` R ) ) $.
    isdivrngd.z $e |- ( ph -> Z = ( 0g ` R ) ) $.
    isdivrngd.u $e |- ( ph -> U = ( 1r ` R ) ) $.
    isdivrngd.r $e |- ( ph -> R e. Ring ) $.
    isdivrngd.n $e |- ( ( ph /\ ( x e. B /\ x =/= Z )
            /\ ( y e. B /\ y =/= Z ) ) -> ( x T y ) =/= Z ) $.
    isdivrngd.o $e |- ( ph -> U =/= Z ) $.
    isdivrngd.i $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I e. B ) $.
    isdivrngd.j $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> I =/= Z ) $.
    ${
      isdivrngd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( I T x ) = U ) $.
      $( Properties that determine a division ring. ` I ` (reciprocal) is
         normally dependent on ` x ` i.e. read it as ` I ( x ) ` ." $)
      isdivrngd $p |- ( ph -> R e. DivRing ) $=
        ( vz crg wcel cnx cbs cfv c0g csn cdif cop cplusg cmulr cpr cgrp wa
        cdivring difeq1d sneqd difeq2d eqtrd opeq2d preq12d eleq1d anbi2d cvv
        wceq fvex syl6eqel difexg syl eqid grpbase grpplusg prex a1i cv wne co
        eldifsn w3a ringcl syl3an1 3expib eleq2d anbi12d opreqd eleq12d
        3imtr4d 3impib 3adant2r 3adant3r sylanbrc syl3an3b syl3an2b eldifi
        3anim123i wi ringass ex 3anbi123d eqidd opreq123d eqeq12d imp sylan2
        cur ringidcl mpbird ringlidm eqeq1d adantrr sylan2b isgrpd mpbi2and
        isdivrng sylibr ) AEUAUBZUCUDUEZEUDUEZEUFUEZUGZUHZUIZUCUJUEZEUKUEZUIZUL
        ZUMUBZUNZEUOUBAXPXQDIUGZUHZUIZYCFUIZULZUMUBZYHAYNYGXPAYMYFUMAYKYBYLYEAY
        JYAXQAYJXRYIUHYAADXRYIJUPAYIXTXRAIXSLUQURUSUTAFYDYCKUTVAVBVCNABCTYJFYMH
        GAYJVDUBZYJYMUDUEVEADVDUBYOADXRVDJEUDVFVGDYIVDVHVIYJFYMVDYMVJZVKVIAFVDU
        BFYMUJUEVEAFYDVDKEUKVFVGYJFYMVDYPVLVIYMVDUBAYKYLVMVNBVOZYJUBZAYQDUBZYQI
        VPZUNZCVOZYJUBZYQUUBFVQZYJUBZYQDIVRZUUCAUUAUUBDUBZUUBIVPZUNZUUEUUBDIVRA
        UUAUUIVSUUDDUBZUUDIVPUUEAUUAUUGUUJUUHAYSUUGUUJYTAYSUUGUUJAYQXRUBZUUBXRU
        BZUNYQUUBYDVQZXRUBZYSUUGUNUUJAUUKUULUUNAXPUUKUULUUNNXREYDYQUUBXRVJZYDVJ
        ZVTWAWBAYSUUKUUGUULADXRYQJWCZADXRUUBJWCZWDAUUDUUMDXRAFYDYQUUBKWEZJWFWGW
        HWIWJOUUDDIVRWKWLWMYRUUCTVOZYJUBZVSAYSUUGUUTDUBZVSZUUDUUTFVQZYQUUBUUTFV
        QZFVQZVEZYRYSUUCUUGUVAUVBYQDYIWNUUBDYIWNUUTDYIWNWOAUVCUVGAUUKUULUUTXRUB
        ZVSZUUMUUTYDVQZYQUUBUUTYDVQZYDVQZVEZUVCUVGAXPUVIUVMWPNXPUVIUVMXREYDYQUU
        BUUTUUOUUPWQWRVIAYSUUKUUGUULUVBUVHUUQUURADXRUUTJWCWSAUVDUVJUVFUVLAUUDUU
        MUUTUUTFYDKUUSAUUTWTXAAYQYQUVEUVKFYDKAYQWTZAFYDUUBUUTKWEXAXBWGXCXDAGDUB
        ZGIVPGYJUBAUVOEXEUEZXRUBZAXPUVQNXREUVPUUOUVPVJZXFVIAGUVPDXRMJWFXGPGDIVR
        WKYRAUUAGYQFVQZYQVEZUUFAYSUVTYTAYSUVTAUUKUVPYQYDVQZYQVEZYSUVTAXPUUKUWBW
        PNXPUUKUWBXREYDUVPYQUUOUUPUVRXHWRVIUUQAUVSUWAYQAGUVPYQYQFYDKMUVNXAXIWGX
        CXJXKYRAUUAHYJUBZUUFAUUAUNHDUBHIVPUWCQRHDIVRWKXKYRAUUAHYQFVQGVEUUFSXKXL
        XMXREYDYFXSUUOUUPXSVJYFVJXNXO $.
        $( [2-Aug-2013] $)
    $}

    ${
      $d y z I $.
      isdivrngrd.k $e |- ( ( ph /\ ( x e. B /\ x =/= Z ) ) -> ( x T I ) = U )
          $.
      $( Properties that determine a division ring. ` I ` (reciprocal) is
         normally dependent on ` x ` i.e. read it as ` I ( x ) ` ."  In this
         version of ~ isdivrngd we are use right reciprocal of ~ isgrprd rather
         than the left reciprocal of ~ isgrpd . $)
      isdivrngrd $p |- ( ph -> R e. DivRing ) $=
        ( vz cv wcel wne wa csn cdif co wceq eldifsn w3a cbs cfv cmulr crg
        eqid ringcl syl3an1 3expib eleq2d anbi12d opreqd eleq12d 3imtr4d
        3impib 3adant2r 3adant3r sylanbrc syl3an3b syl3an2b cur ringidcl syl
        eleqtrrd eqeltrd eldifi eqidd opreq123d adantr ringridm sylan ex
        sylbid imp eqtrd sylan2 3anim123i 3anbi123d wi ringass 3eqtr4d sylan2b
        grplinvd sylan2br isdivrngd ) ABCDEFGHIJKLMNOPQRBUAZDUBZWOIUCZUDZAWODIU
        EZUFZUBZHWOFUGGUHWODIUIZABCTWTFHGXAAWRCUAZWTUBZWOXCFUGZWTUBZXBXDAWRXCDU
        BZXCIUCZUDZXFXCDIUIAWRXIUJXEDUBZXEIUCXFAWRXGXJXHAWPXGXJWQAWPXGXJAWOEUKU
        LZUBZXCXKUBZUDWOXCEUMULZUGZXKUBZWPXGUDXJAXLXMXPAEUNUBZXLXMXPNXKEXNWOXCX
        KUOZXNUOZUPUQURAWPXLXGXMADXKWOJUSZADXKXCJUSZUTAXEXODXKAFXNWOXCKVAZJVBVC
        VDVEVFOXEDIUIVGVHVIAGDUBGIUCGWTUBAGEVJULZDMAYCXKDAXQYCXKUBNXKEYCXRYCUOZ
        VKVLJVMVNPGDIUIVGXAAWPWOGFUGZWOUHWODWSVOZAWPUDYEWOYCXNUGZWOAYEYGUHWPAWO
        WOGYCFXNKAWOVPZMVQVRAWPYGWOUHZAWPXLYIXTAXLYIAXQXLYINXKEXNYCWOXRXSYDVSVT
        WAWBWCWDWEXAXDTUAZWTUBZUJAWPXGYJDUBZUJZXEYJFUGZWOXCYJFUGZFUGZUHXAWPXDXG
        YKYLYFXCDWSVOYJDWSVOWFAYMUDXOYJXNUGZWOXCYJXNUGZXNUGZYNYPAYMYQYSUHZAYMXL
        XMYJXKUBZUJZYTAWPXLXGXMYLUUAXTYAADXKYJJUSWGAXQUUBYTWHNXQUUBYTXKEXNWOXCY
        JXRXSWIWAVLWBWCAYNYQUHYMAXEXOYJYJFXNKYBAYJVPVQVRAYPYSUHYMAWOWOYOYRFXNKY
        HAFXNXCYJKVAVQVRWJWEXAAWRHWTUBZXBAWRUDHDUBHIUCUUCQRHDIUIVGWKXAAWRWOHFUG
        GUHXBSWKWLWMWN $.
        $( [10-Aug-2013] $)
    $}
  $}

  ${
    divrngmgrp.b $e |- B = ( Base ` R ) $.
    divrngmgrp.t $e |- T = ( .r ` R ) $.
    divrngmgrp.z $e |- Z = ( 0g ` R ) $.
    divrngmgrp.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
                          <. ( +g ` ndx ) , T >. } $.
    $( A division ring contains a multiplicative group. $)
    divrngmgrp $p |- ( R e. DivRing -> G e. Grp ) $=
      ( cdivring wcel crg cgrp isdivrng simprbi ) BJKBLKDMKABCDEFGHINO $.
      $( [8-Sep-2011] $)
  $}

  ${
    divrngmcl.b $e |- B = ( Base ` R ) $.
    divrngmcl.t $e |- T = ( .r ` R ) $.
    divrngmcl.z $e |- Z = ( 0g ` R ) $.
    $( The product of two nonzero elements of a division ring is nonzero. $)
    divrngmcl $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } )
                    /\ Y e. ( B \ { Z } ) ) -> ( X T Y ) e. ( B \ { Z } ) ) $=
      ( cdivring wcel cnx cbs cfv csn cdif cop cplusg cpr cgrp co eqid
      divrngmgrp cvv wceq fvex eqeltri difexg ax-mp grpbase cmulr grpplusg
      grpcl syl3an1 ) BJKLMNAFOZPZQLRNCQSZTKDUPKEUPKDECUAUPKABCUQFGHIUQUBZUCUPC
      UQDEUPUDKZUPUQMNUEAUDKUSABMNUDGBMUFUGAUOUDUHUIUPCUQUDURUJUICUDKCUQRNUECBU
      KNUDHBUKUFUGUPCUQUDURULUIUMUN $.
      $( [7-Sep-2011] $)
  $}

  ${
    divrngid.b $e |- B = ( Base ` R ) $.
    divrngid.t $e |- T = ( .r ` R ) $.
    divrngid.z $e |- Z = ( 0g ` R ) $.
    divrngid.u $e |- U = ( 1r ` R ) $.
    divrngid.g $e |- G = { <. ( Base ` ndx ) , ( B \ { Z } ) >. ,
                          <. ( +g ` ndx ) , T >. } $.
    $( Lemma for ~ divrngid and ~ divrngunz . $)
    divrngidlem $p |- ( R e. DivRing -> U =/= Z ) $=
      ( cdivring wcel c0g cfv wne csn cdif cgrp crg wa isdivrng simpr sylbi
      cvv cbs wceq fvex eqeltri difexg ax-mp grpbase eqid grpidcl syl eldifsn
      co divrngring divrngmgrp eldifi 3syl ringlidm syl11anc opreq1 sylan9req
      ringlz adantr eqtrd ex necon3d mpd ) BLMZENOZFPZDFPVLVMAFQZRZMZVNVLESMZVQ
      VLBTMZVRUAVRABCEFGHIKUBVSVRUCUDVPEVMVPUEMZVPEUFOUGAUEMVTABUFOUEGBUFUHUIAV
      OUEUJUKVPCEUEKULUKVMUMUNZUOVQVMAMZVNUAVNVMAFUPWBVNUCUDUOVLDFVMFVLDFUGZVMF
      UGVLWCUAVMFVMCUQZFVLWCVMDVMCUQZWDVLVSWBWEVMUGBURZVLVRVQWBABCEFGHIKUSWAVMA
      VOUTVAZABCDVMGHJVBVCDFVMCVDVEVLWDFUGZWCVLVSWBWHWFWGABCVMFGHIVFVCVGVHVIVJV
      K $.
      $( [8-Sep-2011] $)

    $( A division ring's unit is the identity element of its multiplicative
       group. $)
    divrngid $p |- ( R e. DivRing -> U = ( 0g ` G ) ) $=
      ( cdivring wcel c0g cfv co wceq crg divrngring csn cdif cgrp divrngmgrp
      cvv cbs fvex eqeltri difexg ax-mp grpbase eqid grpidcl syl eldifi
      ringlidm syl11anc cplusg cmulr grpplusg grplid eqtr4d wb wn ringidcl wne
      divrngidlem df-ne sylib elsni nsyl eldif sylanbrc grprcan syl13anc mpbid
      ) BLMZDENOZCPZVQVQCPZQZDVQQZVPVRVQVSVPBRMZVQAMZVRVQQBSZVPVQAFTZUAZMZWCVPE
      UBMZWGABCEFGHIKUCZWFEVQWFUDMZWFEUEOQAUDMWJABUEOUDGBUEUFUGAWEUDUHUIWFCEUDK
      UJUIZVQUKZULUMZVQAWEUNUMABCDVQGHJUOUPVPWHWGVSVQQWIWMWFCEVQVQWKCUDMCEUQOQC
      BUROUDHBURUFUGWFCEUDKUSUIZWLUTUPVAVPWHDWFMZWGWGVTWAVBWIVPDAMZDWEMZVCWOVPW
      BWPWDABDGJVDUMVPDFQZWQVPDFVEWRVCABCDEFGHIJKVFDFVGVHDFVIVJDAWEVKVLWMWMWFCE
      DVQVQWKWNVMVNVO $.
      $( [7-Sep-2011] $)
  $}

  ${
    divrngunz.z $e |- Z = ( 0g ` R ) $.
    divrngunz.u $e |- U = ( 1r ` R ) $.
    $( A division ring's unit is different from its zero. $)
    divrngunz $p |- ( R e. DivRing -> U =/= Z ) $=
      ( cbs cfv cmulr cnx csn cdif cop cplusg cpr eqid divrngidlem ) AFGZAAHGZB
      IFGQCJKLIMGRLNZCQORODESOP $.
      $( [8-Sep-2011] $)
  $}

  ${
    dvrngid2.b $e |- B = ( Base ` R ) $.
    dvrngid2.t $e |- T = ( .r ` R ) $.
    dvrngid2.o $e |- Z = ( 0g ` R ) $.
    dvrngid2.u $e |- U = ( 1r ` R ) $.
    $( Properties showing that an element ` I ` is the identity element of a
       division ring. $)
    dvrngid2 $p |- ( R e. DivRing
        -> ( ( I e. B /\ I =/= Z /\ ( I T I ) = I ) <-> U = I ) ) $=
      ( cdivring wcel wne co wceq w3a cnx cbs cfv csn cdif cop cplusg cpr c0g
      wa df-3an eldifsn anbi1i bitr4i cgrp wb eqid divrngmgrp cvv fvex eqeltri
      difexg grpbase mp2b cmulr grpplusg ax-mp isgrpid2 syl syl5bb divrngid
      eqeq1d bitr4d ) BKLZEALZEFMZEECNEOZPZQRSAFTZUAZUBQUCSCUBUDZUESZEOZDEOVNEV
      PLZVMUFZVJVSVNVKVLUFZVMUFWAVKVLVMUGVTWBVMEAFUHUIUJVJVQUKLWAVSULABCVQFGHIV
      QUMZUNVPCVQVREAUOLVPUOLVPVQRSOABRSUOGBRUPUQAVOUOURVPCVQUOWCUSUTCUOLCVQUCS
      OCBVASUOHBVAUPUQVPCVQUOWCVBVCVRUMVDVEVFVJDVREABCDVQFGHIJWCVGVHVI $.
      $( [11-Oct-2013] $)
  $}

  $c invr $.

  $( Extend class notation with multiplicative inverse. $)
  cinvr $a class invr $.

  $( Define multiplicative inverse. $)
  df-invr $a |- invr = ( r e. _V |-> ( -g `
      { <. ( Base ` ndx ) , ( ( Base ` r ) \ { ( 0g ` r ) } ) >. ,
        <. ( +g ` ndx ) , ( .r ` r ) >. } ) ) $.

  ${
    $d r N $.  $d r R $.
    invrfval.b $e |- B = ( Base ` R ) $.
    invrfval.z $e |- Z = ( 0g ` R ) $.
    invrfval.t $e |- T = ( .r ` R ) $.
    invrfval.4 $e |- G =
           { <. ( Base ` ndx ) , ( B \ { Z } ) >. , <. ( +g ` ndx ) , T >. } $.
    invrfval.n $e |- N = ( -g ` G ) $.
    invrfval.i $e |- I = ( invr ` R ) $.
    $( Multiplicative inverse function for a division ring. $)
    invrfval $p |- ( R e. A -> I = N ) $=
      ( vr wcel cinvr cfv cvv wceq elisset cnx cbs cv c0g csn cdif cop cplusg
      cmulr cpr cminusg fveq2 syl6eqr difeq1d sneqd difeq2d eqtrd opeq2d
      preq12d fveq2d df-invr fvex eqeltri fvmpt syl syl5eq ) CAPZFCQRZGNVHCSPVI
      GTCAUAOCUBUCRZOUDZUCRZVKUERZUFZUGZUHZUBUIRZVKUJRZUHZUKZULRZGSQVKCTZWAEULR
      ZGWBVTEULWBVTVJBHUFZUGZUHZVQDUHZUKEWBVPWFVSWGWBVOWEVJWBVOBVNUGWEWBVLBVNWB
      VLCUCRBVKCUCUMIUNUOWBVNWDBWBVMHWBVMCUERHVKCUEUMJUNUPUQURUSWBVRDVQWBVRCUJR
      DVKCUJUMKUNUSUTLUNVAMUNOVBGWCSMEULVCVDVEVFVG $.
      $( [21-Sep-2011] $)
  $}

  ${
    invrcl.b $e |- B = ( Base ` R ) $.
    invrcl.z $e |- Z = ( 0g ` R ) $.
    invrcl.i $e |- I = ( invr ` R ) $.
    $( Closure of the multiplicative inverse in a division ring. $)
    invrcl $p |- ( ( R e. DivRing /\ X e. ( B \ { Z } ) )
             -> ( I ` X ) e. ( B \ { Z } ) ) $=
      ( cdivring wcel csn cdif wa cfv cnx cbs cop cplusg cmulr cpr cminusg
      wceq eqid invrfval fveq1d adantr cgrp divrngmgrp cvv fvex eqeltri difexg
      ax-mp grpbase grpinvcl sylan eqeltrd ) BIJZDAEKZLZJZMDCNZDOPNUTQORNBSNZQT
      ZUANZNZUTURVBVFUBVAURDCVEIABVCVDCVEEFGVCUCZVDUCZVEUCZHUDUEUFURVDUGJVAVFUT
      JABVCVDEFVGGVHUHUTVDRNZVDVEDUTUIJZUTVDPNUBAUIJVKABPNUIFBPUJUKAUSUIULUMUTV
      CVDUIVHUNUMVJUCVIUOUPUQ $.
      $( [21-Sep-2011] $)
  $}


