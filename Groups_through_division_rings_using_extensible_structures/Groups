$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c e f g m p w x $.
    $( Define class of all groups. $)
    df-grp $a |- Grp = { f | E. g E. p ( g = ( Base ` f )
       /\ p = ( +g ` f ) /\ ( A. a e. g A. b e. g A. c e. g ( ( a p b ) e. g
            /\ ( ( a p b ) p c ) = ( a p ( b p c ) ) ) /\
          E. e e. g A. a e. g ( ( e p a ) = a /\
            E. m e. g ( m p a ) = e ) ) ) } $.

    $( Define group identity element. $)
    df-0g $a |- 0g = ( g e. _V |-> ( iota_ e e. ( Base ` g )
      A. x e. ( Base ` g ) ( e ( +g ` g ) x ) = x ) ) $.

    $( Define inverse of group element. $)
    df-minusg $a |- -g = ( g e. _V |-> ( x e. ( Base ` g )
  |-> ( iota_ w e. ( Base ` g ) ( w ( +g ` g ) x ) = ( 0g ` g ) ) ) ) $.
  $}


  ${
    $d a b c e f g m p $.
    $( Alternate definition of the class of all groups using class
       substitution. $)
    dfgrp2 $p |- Grp = { f | [ ( Base ` f ) / g ] [ ( +g ` f ) / p ]
      ( A. a e. g A. b e. g A. c e. g ( ( a p b ) e. g
            /\ ( ( a p b ) p c ) = ( a p ( b p c ) ) ) /\
          E. e e. g A. a e. g ( ( e p a ) = a /\
            E. m e. g ( m p a ) = e ) ) } $=
      ( cgrp cv cbs cfv wceq cplusg co wcel wa wral wrex w3a wex cab wsbc
      df-grp fvex sbc5 19.42v 3anass exbii anbi2i 3bitr4ri bitri abbii eqtr4i
      ) ICJZBJZKLZMZEJZUPNLZMZFJZGJZUSOZUOPVDHJZUSOVBVCVEUSOUSOMQHUORGUORFUORAJ
      ZVBUSOVBMDJVBUSOVFMDUOSQFUORAUOSQZTZEUAZCUAZBUBVGEUTUCZCUQUCZBUBABCDEFGHU
      DVLVJBVLURVKQZCUAVJVKCUQUPKUEUFVMVICURVAVGQZQZEUAURVNEUAZQVIVMURVNEUGVHVO
      EURVAVGUHUIVKVPURVGEUTUPNUEUFUJUKUIULUMUN $.
      $( [2-Dec-2013] $)
  $}

  $( Utility lemma for constructing a (2-element) group structure without
     direct reference to component indices.  This lemma lets us add a second
     element to a singleton using ~ fnunsn . $)
  grplem $p |- -. ( +g ` ndx ) e. { ( Base ` ndx ) } $=
    ( vs cnx cplusg cfv cbs csn wcel wceq wne wn c2 df-plusg 2nn ndxarg c1 1re
    2re 1lt2 ltneii basendx neeqtrri eqnetri df-ne mpbi elsni mto ) BCDZBEDZFGU
    GUHHZUGUHIUIJUGKUHCKAALMNKOUHOKPQRSTUAUBUGUHUCUDUGUHUEUF $.
    $( [1-Oct-2013] $)

  ${
    grpfn.g $e |- G = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    ${
      grpfn.d $e |- D = { ( Base ` ndx ) , ( +g ` ndx ) } $.
      $( A constructed group is a function with domain ` D ` .  (We include the
         domain for use with later structures that extend the domain.) $)
      grpfn $p |- G Fn D $=
        ( cnx cbs cfv csn cop cplusg fvex fnsn cpr cun df-pr eqtri grplem
        fnunsn ) GHIZJZBUAAKZJZDGLIZCGLMUAAGHMNDUCUECKZOUDUFJPEUCUFQRBUAUEOUBUE
        JPFUAUEQRST $.
        $( [28-Sep-2013] $)
    $}

    $( The base set of a constructed group. $)
    grpbase $p |- ( B e. V -> B = ( Base ` G ) ) $=
      ( vs cbs cnx cfv cop cplusg cpr cvv prex eqeltri wfn wfun eqid grpfn
      fnfun ax-mp baseid opex prid1 eleqtrri strfv ) ACGDFCHGIZAJZHKIZBJZLZMEUH
      UJNOCUGUILZPCQAULBCEULRSULCTUAFUBUHUKCUHUJUGAUCUDEUEUF $.
      $( [2-Aug-2013] $)

    $( The operation of a constructed group. $)
    grpplusg $p |- ( P e. V -> P = ( +g ` G ) ) $=
      ( vs cplusg cnx cbs cfv cop cpr cvv prex eqeltri wfn wfun eqid grpfn
      fnfun ax-mp plusgid opex prid2 eleqtrri strfv ) BCGDFCHIJZAKZHGJZBKZLZMEU
      HUJNOCUGUILZPCQAULBCEULRSULCTUAFUBUJUKCUHUJUIBUCUDEUEUF $.
      $( [2-Aug-2013] $)
  $}

  ${
    grpstrx.b $e |- B e. _V $.
    grpstrx.p $e |- P e. _V $.
    grpstrx.g $e |- G = { <. 1 , B >. , <. 2 , P >. } $.
    $( The base of an explicitly given group.  Note:  This theorem has
       hard-coded structure indices for demonstration purposes.  It is not
       intended for general use; use ~ grpbase instead. $)
    grpbasex $p |- B = ( Base ` G ) $=
      ( vs cvv wcel cbs cfv wceq c1 cop c2 cpr cnx cplusg basendx opeq1i
      df-plusg 2nn ndxarg preq12i eqtr4i grpbase ax-mp ) AHIACJKLDABCHCMANZOBNZ
      PQJKZANZQRKZBNZPFUKUHUMUIUJMASTULOBROGGUAUBUCTUDUEUFUG $.
      $( [17-Oct-2012] $)

    $( The operation of an explicitly given group.  Note:  This theorem has
       hard-coded structure indices for demonstration purposes.  It is not
       intended for general use; use ~ grpplusgx instead. $)
    grpplusgx $p |- P = ( +g ` G ) $=
      ( vs cvv wcel cplusg cfv wceq c1 cop c2 cpr cnx cbs basendx opeq1i
      df-plusg 2nn ndxarg preq12i eqtr4i grpplusg ax-mp ) BHIBCJKLEABCHCMANZOBN
      ZPQRKZANZQJKZBNZPFUKUHUMUIUJMASTULOBJOGGUAUBUCTUDUEUFUG $.
      $( [17-Oct-2012] $)
  $}

  ${
    $d a b c e f g m p B $.  $d f g p G $.  $d a b c e f g m p P $.
    isgrp.b $e |- B = ( Base ` G ) $.
    isgrp.p $e |- P = ( +g ` G ) $.
    $( The predicate "is a group." $)
    isgrp $p |- ( G e. Grp <-> ( G e. _V
            /\ A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( vg vf vp cgrp wcel cv cbs cfv wceq cplusg co wa wral wrex w3a wex cab
      cvv df-grp eleq2i fveq2 syl6eqr eqeq2d 3anbi12d 2exbidv eqid elab4g fvex
      eqeltri eleq2 anbi1d raleqbi1dv rexeq anbi2d rexeqbi1dv anbi12d opreq
      eleq1d opreq1d eqtrd opreq2d eqeq12d ralbidv 2ralbidv eqeq1d rexbidv
      rexralbidv ceqsex2v anbi2i 3anass bitr4i 3bitri ) ENOEKPZLPZQRZSZMPZWDTRZ
      SZFPZGPZWGUAZWCOZWLHPZWGUAZWJWKWNWGUAZWGUAZSZUBZHWCUCZGWCUCZFWCUCZCPZWJWG
      UAZWJSZDPZWJWGUAZXCSZDWCUDZUBZFWCUCZCWCUDZUBZUEZMUFKUFZLUGZOEUHOZWCASZWGB
      SZXMUEZMUFKUFZUBZXQWJWKBUAZAOZYCWNBUAZWJWKWNBUAZBUAZSZUBZHAUCZGAUCFAUCZXC
      WJBUAZWJSZXFWJBUAZXCSZDAUDZUBZFAUCCAUDZUEZNXPECLKDMFGHUIUJXOYALEXPWDESZXN
      XTKMYTWFXRWIXSXMYTWEAWCYTWEEQRZAWDEQUKIULUMYTWHBWGYTWHETRZBWDETUKJULUMUNU
      OXPUPUQYBXQYKYRUBZUBYSYAUUCXQXMWLAOZWRUBZHAUCZGAUCZFAUCZXEXHDAUDZUBZFAUCZ
      CAUDZUBUUCKMABAUUAUHIEQURUSBUUBUHJETURUSXRXBUUHXLUULXAUUGFWCAWTUUFGWCAWSU
      UEHWCAXRWMUUDWRWCAWLUTVAVBVBVBXKUUKCWCAXJUUJFWCAXRXIUUIXEXHDWCAVCVDVBVEVF
      XSUUHYKUULYRXSUUFYJFGAAXSUUEYIHAXSUUDYDWRYHXSWLYCAWJWKWGBVGZVHXSWOYEWQYGX
      SWOWLWNBUAYEWLWNWGBVGXSWLYCWNBUUMVIVJXSWQWJWPBUAYGWJWPWGBVGXSWPYFWJBWKWNW
      GBVGVKVJVLVFVMVNXSUUJYQCFAAXSXEYMUUIYPXSXDYLWJXCWJWGBVGVOXSXHYODAXSXGYNXC
      XFWJWGBVGVOVPVFVQVFVRVSXQYKYRVTWAWB $.
      $( [17-Oct-2012] $)

    $( The predicate "is a group." $)
    isgrp2 $p |- ( G e. Grp <-> ( A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( cgrp wcel cvv cv co wceq wa wral wrex elisset c0 wne rexn0 wn cbs cfv
      fvprc syl5eq necon1ai syl adantl wb wi w3a isgrp pm4.71ri 3anass 3bitr3i
      pm5.32 mpbir pm5.21nii ) EKLZEMLZFNZGNZBOZALVFHNZBOVDVEVGBOBOPQHARGARFARZ
      CNZVDBOVDPDNVDBOVIPDASQFARZCASZQZEKTZVKVCVHVKAUAUBVCVJCAUCVCAUAVCUDAEUEUF
      UAIEUEUGUHUIUJUKVCVBVLULUMVCVBQZVCVLQZULVBVCVHVKUNVNVOABCDEFGHIJUOVBVCVMU
      PVCVHVKUQURVCVBVLUSUTVA $.
      $( [24-Oct-2012] $)
  $}

  ${
    $d a b c e m K $.
    grpprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    grpprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    $( If two structures have the same group components (properties), one is a
       group iff the other one is. $)
    grpprop $p |- ( K e. Grp <-> L e. Grp ) $=
      ( va vb vc ve vm cgrp wcel cv cplusg cfv co cbs wceq wa wral wrex eqid
      isgrp2 bitr4i ) AJKELZFLZAMNZOZAPNZKUGGLZUFOUDUEUIUFOUFOQRGUHSFUHSEUHSHLZ
      UDUFOUDQILUDUFOUJQIUHTREUHSHUHTRBJKUHUFHIAEFGUHUAUFUAUBUHUFHIBEFGCDUBUC
      $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d a b c e m B $.  $d a b c e m P $.
    elgrp.b $e |- B e. _V $.
    elgrp.p $e |- P e. _V $.
    elgrp.k $e |- K
          = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    $( The predicate "is a group structure" for a specific structure member. $)
    elgrp $p |- ( K e. Grp <-> ( A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( cvv wcel cbs cfv wceq grpbase ax-mp cplusg grpplusg isgrp2 ) ABCDEFGHAL
      MAENOPIABELKQRBLMBESOPJABELKTRUA $.
      $( [24-Oct-2012] $)
  $}

  ${
    $d a b c e m B $.  $d a b c e m P $.
    grppropOLD.b $e |- ( Base ` K ) = B $.
    grppropOLD.p $e |- ( +g ` K ) = P $.
    grppropOLD.c $e |- ( Base ` L ) = B $.
    grppropOLD.q $e |- ( +g ` L ) = P $.
    $( Any set with the same (relevant) properties as a given group is also a
       group (and vice-versa). $)
    grppropOLD $p |- ( K e. Grp <-> L e. Grp ) $=
      ( va vb vc ve vm cgrp wcel cv co wceq wa wral wrex cbs cfv eqcomi cplusg
      isgrp2 bitr4i ) CNOIPZJPZBQZAOUJKPZBQUHUIUKBQBQRSKATJATIATLPZUHBQUHRMPUHB
      QULRMAUASIATLAUASDNOABLMCIJKCUBUCAEUDCUEUCBFUDUFABLMDIJKDUBUCAGUDDUEUCBHU
      DUFUG $.
      $( [28-Oct-2012] $)
  $}

  ${
    $d a b c e m B $.  $d a b c e m P $.
    grpstr.b $e |- ( Base ` K ) = B $.
    grpstr.p $e |- ( +g ` K ) = P $.
    grpstr.l $e |- L
          = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    $( Generalize a specific 2-element group ` L ` to show that any set ` K `
       with the same (relevant) properties is also a group. $)
    grpstr $p |- ( K e. Grp <-> L e. Grp ) $=
      ( va vb vc ve vm cgrp wcel cv co wceq wa wral wrex cbs cfv eqcomi cplusg
      isgrp2 cvv fvex eqeltrri elgrp bitr4i ) CMNHOZIOZBPZANUMJOZBPUKULUNBPBPQR
      JASIASHASKOZUKBPUKQLOUKBPUOQLATRHASKATRDMNABKLCHIJCUAUBZAEUCCUDUBZBFUCUEA
      BKLDHIJUPAUFECUAUGUHUQBUFFCUDUGUHGUIUJ $.
      $( [28-Oct-2012] $)
  $}

  ${
    grpss.g $e |- G = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    grpss.r $e |- R e. _V $.
    grpss.s $e |- G C_ R $.
    grpss.f $e |- Fun R $.
    $( Show that a structure extending a constructed group (e.g. a ring) is
       also a group.  This allows us to prove that a constructed potential ring
       ` R ` is a group before we know that it is also a ring.  (Theorem
       ~ ringgrp , on the other hand, requires that we know in advance that
       ` R ` is a ring.) $)
    grpss $p |- ( G e. Grp <-> R e. Grp ) $=
      ( vs cbs cfv cplusg eqid baseid cnx cop cpr opex prid1 eleqtrri strss
      plusgid prid2 grppropOLD ) DJKZDLKZDCUEMUFMADCJIFHGINOJKZAPZUHOLKZBPZQZDU
      HUJUGARSETUABDCLIFHGIUBUJUKDUHUJUIBRUCETUAUD $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d a b c e f m B $.  $d f G $.  $d a b c e f m P $.  $d a b c X $.
    $d b c Y $.  $d c Z $.
    grplem1.b $e |- B = ( Base ` G ) $.
    grplem1.p $e |- P = ( +g ` G ) $.
    $( Lemma for group properties. $)
    grplem1 $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
       ( ( X P Y ) e. B /\ ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) ) $=
      ( va vb vc ve vm cgrp wcel cv co wceq wa wral w3a cvv wrex isgrp simp2bi
      opreq1 eleq1d opreq1d eqeq12d anbi12d opreq2 opreq2d anbi2d rcla43v
      mpan9 ) CNOZIPZJPZBQZAOZUSKPZBQZUQURVABQZBQZRZSZKATJATIATZDAOEAOFAOUADEBQ
      ZAOZVHFBQZDEFBQZBQZRZSZUPCUBOVGLPZUQBQUQRMPUQBQVORMAUCSIATLAUCABLMCIJKGHU
      DUEVFVNDURBQZAOZVPVABQZDVCBQZRZSVIVHVABQZDEVABQZBQZRZSIJKDEFAAAUQDRZUTVQV
      EVTWEUSVPAUQDURBUFZUGWEVBVRVDVSWEUSVPVABWFUHUQDVCBUFUIUJURERZVQVIVTWDWGVP
      VHAUREDBUKZUGWGVRWAVSWCWGVPVHVABWHUHWGVCWBDBUREVABUFULUIUJVAFRZWDVMVIWIWA
      VJWCVLVAFVHBUKWIWBVKDBVAFEBUKULUIUMUNUO $.
      $( [14-Aug-2011] $)

    $( Closure of the operation of a group. $)
    grpcl $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) -> ( X P Y ) e. B ) $=
      ( cgrp wcel co wa w3a 3anass anidm anbi2i bitri wceq grplem1 simpld
      sylan2br 3impb ) CHIZDAIZEAIZDEBJZAIZUCUDKZUBUCUDUDLZUFUHUCUDUDKZKUGUCUDU
      DMUIUDUCUDNOPUBUHKUFUEEBJDEEBJBJQABCDEEFGRSTUA $.
      $( [14-Aug-2011] $)

    $( A group operation is associative. $)
    grpass $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
              ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) $=
      ( cgrp wcel w3a wa co wceq grplem1 simprd ) CIJDAJEAJFAJKLDEBMZAJQFBMDEFB
      MBMNABCDEFGHOP $.
      $( [14-Aug-2011] $)

    $d u x y B $.  $d u x y P $.  $d a b c f u x y $.
    $( A group has a left identity element, and every member has a left
       inverse. $)
    grplidinv $p |- ( G e. Grp ->
         E. u e. B A. x e. B ( ( u P x ) = x /\ E. y e. B ( y P x ) = u ) ) $=
      ( vb vc cgrp wcel cvv cv co wceq wa wral wrex isgrp simp3bi ) FKLFMLANZIN
      ZEOZDLUDJNZEOUBUCUEEOEOPQJDRIDRADRCNZUBEOUBPBNUBEOUFPBDSQADRCDSDECBFAIJGH
      TUA $.
      $( [16-Aug-2011] $)

    $( Lemma for ~ grpidinv . $)
    grpidinvlem1 $p |- ( ( ( G e. Grp /\ ( Y e. B /\ A e. B ) ) /\
                  ( ( Y P A ) = U /\ ( A P A ) = A ) ) -> ( U P A ) = U ) $=
      ( cgrp wcel wa co wceq opreq1 ad2antrl w3a id 3anidm23 grpass sylan2
      adantr eqtr3d opreq2 ad2antll simprl 3eqtrd ) EIJZFBJZABJZKZKZFACLZDMZAAC
      LZAMZKZKZDACLZFUNCLZULDUQULACLZURUSUMUTURMUKUOULDACNOUKUTUSMZUPUJUGUHUIUI
      PZVAUHUIVBVBQRBCEFAAGHSTUAUBUOUSULMUKUMUNAFCUCUDUKUMUOUEUF $.
      $( [16-Aug-2011] $)

    $( Lemma for ~ grpidinv . $)
    grpidinvlem2 $p |- ( ( ( G e. Grp /\ ( Y e. B /\ A e. B ) ) /\ ( (
 U P Y ) = Y /\ ( Y P A ) = U ) ) -> ( ( A P Y ) P ( A P Y ) ) = ( A P Y ) ) $=
      ( cgrp wcel wa co wceq w3a simprr simprl grpcl 3com23 3expb 3jca grpass
      syldan adantr opreq1 adantl simpl eqtr2d id 3anidm13 sylan2 sylan9eqr
      eqcomd opreq2d eqtrd ) EIJZFBJZABJZKZKZDFCLZFMZFACLZDMZKZKZAFCLZVFCLZAFVF
      CLZCLZVFUSVGVIMZVDUOURUQUPVFBJZNVJUSUQUPVKUOUPUQOUOUPUQPUOUPUQVKUOUQUPVKB
      CEAFGHQRSTBCEAFVFGHUAUBUCVEVHFACVEFVHVDUSFVBFCLZVHVDVLUTFVCVLUTMVAVBDFCUD
      UEVAVCUFUGURUOUPUQUPNZVLVHMUPUQVMVMUHUIBCEFAFGHUAUJUKULUMUN $.
      $( [16-Aug-2011] $)

    ${
      $d w x y A $.  $d w z B $.  $d w y G $.  $d w z P $.  $d w x y z U $.
      $d w y ph $.  $d y ps $.
      grpidinvlem3.ph $e |- ( ph <-> A. x e. B ( U P x ) = x ) $.
      grpidinvlem3.ps $e |- ( ps <-> A. x e. B E. z e. B ( z P x ) = U ) $.
      $( Lemma for ~ grpidinv . $)
      grpidinvlem3 $p |- ( ( ( ( G e. Grp /\ U e. B ) /\ ( ph /\ ps ) )
               /\ A e. B ) -> E. y e. B ( ( y P A ) = U /\ ( A P y ) = U ) ) $=
        ( vw cgrp wcel wa cv co wceq wrex wral opreq1 eqeq1d cbvrexv ralbii
        bitri opreq2 rexbidv rcla4cva sylanb adantll grpcl 3expa adantllr
        biimpi ad2antrl ad2antrr id eqeq12d rcla4va syl11anc adantr pm3.22
        an31s adantlr adantlll anim1i grpidinvlem2 wi 3expb ad2ant2rl sylan2b
        anass an32s ex syldan imp grpidinvlem1 sylan exp43 r19.23adv syl5
        mpand exp32 com34 imp32 exp3a imp31 mpd eqtr3d ancld reximdva ) JPQZIGQ
        ZRZABRZRZFGQZRZDSZFHTZIUAZDGUBZXDFXBHTZIUAZRZDGUBWRWTXEWQBWTXEABXBCSZHT
        ZIUAZDGUBZCGUCZWTXEBESZXIHTZIUAZEGUBZCGUCZXMNXQXLCGXPXKEDGXNXBUAXOXJIXN
        XBXIHUDUEUFUGUHXLXECFGXIFUAZXKXDDGXSXJXCIXIFXBHUIUEUJUKULUMUMXAXDXHDGXA
        XBGQZRZXDXGYAXDXGYAXDRZIXFHTZXFIYAYCXFUAZXDYAXFGQZIXIHTZXIUAZCGUCZYDWQW
        TXTYEWRWOWTXTYEWPWOWTXTYEGHJFXBKLUNZUOUPUPWSYHWTXTAYHWQBAYHMUQURUSYGYDC
        XFGXIXFUAZYFYCXIXFXIXFIHUIYJUTVAVBVCVDYBXFXFHTXFUAZYCIUAZYBWOXTWTRZRZIX
        BHTZXBUAZXDRYKYAYNXDWQWTXTYNWRWOWTXTYNWPXTWTWOYNYMWOVEVFUPUPVDYAYPXDWRW
        TXTYPWQWRXTYPWTAXTYPBAYHXTYPMYGYPCXBGXIXBUAZYFYOXIXBXIXBIHUIYQUTVAUKULV
        GVGVHVIFGHIJXBKLVJVCYAYKYLVKZXDWSWTXTYRWSWTXTYRWQABWTXTRZYRVKWQAYSBYRWQ
        AYSBYRVKWQAYSRRZYEBYRWOYSYEWPAWOWTXTYEYIVLZVMYEBROSZXFHTZIUAZOGUBZYTYRB
        YEUUBXIHTZIUAZOGUBZCGUCZUUEBXRUUINXQUUHCGXPUUGEOGXNUUBUAXOUUFIXNUUBXIHU
        DUEUFUGUHUUHUUECXFGYJUUGUUDOGYJUUFUUCIXIXFUUBHUIUEUJVBVNYTUUDYROGYTUUBG
        QZUUDYKYLYTUUJRWOUUJYERRZUUDYKRYLYTUUJUUKWOYSUUJUUKVKZWPAWOYSYEUULUUAWO
        YERUUJUUKWOUUJYEUUKWOUUJRYERUUKWOUUJYEVOUQVPVQVRVMVSXFGHIJUUBKLVTWAWBWC
        WDWEWFWGWHWIWJVDWKWLVQWMWNWK $.
        $( [16-Aug-2011] $)
    $}

    $d y G $.  $d y U $.  $d y X $.
    $( Lemma for ~ grpidinv . $)
    grpidinvlem4 $p |- ( ( ( G e. Grp /\ X e. B ) /\ E. y e. B
             ( ( y P X ) = U /\ ( X P y ) = U ) ) -> ( X P U ) = ( U P X ) ) $=
      ( cgrp wcel wa cv co wceq wrex simpll simplr simpr grpass syl13anc
      opreq2 sylan9eq opreq1 sylan9req anasss exp31 r19.23adv imp ) EIJZFBJZKZA
      LZFCMZDNZFULCMZDNZKZABOFDCMZDFCMZNZUKUQUTABUKULBJZUQUTUKVAKZUNUPUTVBUNKUP
      URUOFCMZUSVBUNVCFUMCMZURVBUIUJVAUJVCVDNUIUJVAPUIUJVAQZUKVARVEBCEFULFGHSTU
      MDFCUAUBUODFCUCUDUEUFUGUH $.
      $( [16-Aug-2011] $)

    $d w z B $.  $d u x G $.  $d w z P $.  $d u w x y z $.
    $( A group has a left and right identity element, and every member has a
       left and right inverse. $)
    grpidinv $p |- ( G e. Grp -> E. u e. B A. x e. B ( ( ( u P x ) = x /\
         ( x P u ) = x ) /\ E. y e. B ( ( y P x ) = u /\ ( x P y ) = u ) ) ) $=
      ( vz vw cgrp wcel cv co wceq wrex wa wral grplidinv simpl ralimi opreq2
      id eqeq12d rcla4cva sylan adantll anim1i adantrr adantr adantl ad2antlr
      simpr jca32 biid grpidinvlem3 sylancom grpidinvlem4 syl11anc eqtrd jca31
      r19.21aiva exp32 reximdvai mpd ) FKLZCMZIMZENZVHOZJMVHENVGOJDPZQZIDRZCDPV
      GAMZENZVNOZVNVGENZVNOZQBMZVNENVGOVNVSENVGOQBDPZQZADRZCDPIJCDEFGHSVFVMWBCD
      VFVGDLZVMWBVFWCVMQZQZWAADWEVNDLZQZVPVRVTWDWFVPVFVMWFVPWCVMVJIDRZWFVPVLVJI
      DVJVKTUAZVJVPIVNDVHVNOZVIVOVHVNVHVNVGEUBWJUCUDUEUFUGUGZWGVQVOVNWGVFWFQVTV
      QVOOWEVFWFVFWDTUHWEWFVFWCQZWHVKIDRZQQVTWGWLWHWMWEWLWFVFWCWLVMWLUCUIUJWDWH
      VFWFVMWHWCWIUKULWDWMVFWFVMWMWCVLVKIDVJVKUMUAUKULUNWHWMIBJVNDEVGFGHWHUOWMU
      OUPUQZBDEVGFVNGHURUSWKUTWNVAVBVCVDVE $.
      $( [16-Aug-2011] $)

    $d w G $.  $d z G $.
    $( The left identity element of a group is unique.  Lemma 2.2.1(a) of
       [Herstein] p. 55. $)
    grpideu $p |- ( G e. Grp -> E! u e. B A. x e. B ( u P x ) = x ) $=
      ( vw vz vy cgrp wcel cv co wceq wral wi wa wrex wreu grpidinv simpll
      ralimi opreq2 id eqeq12d cbvralv sylib adantl ad2antlr simpr eqeq1d
      opreq1 anbi12d rexbidv rcla4va adantll sylan2 grpidinvlem4 syldan an32s
      adantllr adantr ad2ant2rl ad2ant2lr 3eqtr3d ex mpand r19.21aiva jca
      reximdva mpd ralbidv reu8 sylibr ) EKLZBMZAMZDNZVROZACPZHMZVRDNZVROZACPZV
      QWBOZQZHCPZRZBCSZWABCTVPVQIMZDNZWKOZWKVQDNWKOZRZJMZWKDNZVQOZWKWPDNZVQOZRZ
      JCSZRZICPZBCSWJIJBCDEFGUAVPXDWIBCVPVQCLZRZXDWIXFXDRZWAWHXDWAXFXDWMICPWAXC
      WMICWMWNXBUBUCWMVTIACWKVROZWLVSWKVRWKVRVQDUDXHUEUFUGUHZUIXGWGHCXGWBCLZRZW
      AWEWFXDWAXFXJXIUJXKWAWERZWFXKXLRWBVQDNZVQWBDNZVQWBXKXMXNOZXLVPXDXJXOXEVPX
      JXDXOVPXJRZXDWPWBDNZVQOZWBWPDNZVQOZRZJCSZXOXDXPXBICPZYBXCXBICWOXBUKUCXJYC
      YBVPXBYBIWBCWKWBOZXAYAJCYDWRXRWTXTYDWQXQVQWKWBWPDUDULYDWSXSVQWKWBWPDUMULU
      NUOUPUQURJCDVQEWBFGUSUTVAVBVCXFXJXLXMVQOZXDXFWEYEXJWAXEWEYEVPWDYEAVQCVRVQ
      OZWCXMVRVQVRVQWBDUDYFUEUFUPUQVDVBXJWAXNWBOZXGWEVTYGAWBCVRWBOZVSXNVRWBVRWB
      VQDUDYHUEUFUPVEVFVGVHVIVJVGVKVLWAWEBHCWFVTWDACWFVSWCVRVQWBVRDUMULVMVNVO
      $.
      $( [16-Aug-2011] $)
  $}

  ${
    $d u x y B $.  $d u x y G $.
    grpn0.b $e |- B = ( Base ` G ) $.
    $( The base set of a group is not empty.  (Contributed by Szymon
       Jaroszewicz, 3-Apr-2007.) $)
    grpn0 $p |- ( G e. Grp -> B =/= (/) ) $=
      ( vu vx vy cgrp wcel cv cplusg cfv co wceq wrex wa wral c0 wne eqid
      grplidinv rexn0 syl ) BGHDIZEIZBJKZLUDMFIUDUELUCMFANOEAPZDANAQREFDAUEBCUE
      STUFDAUAUB $.
      $( [18-Oct-2012] $)
  $}

  ${
    $d e m x y z B $.  $d e m x y z G $.  $d m N $.  $d e m P $.  $d e m x Z $.
    $d e m x y z ph $.
    isgrpd.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isgrpd.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isgrpd.g $e |- ( ph -> G e. _V ) $.
    isgrpd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpd.z $e |- ( ph -> Z e. B ) $.
    isgrpd.i $e |- ( ( ph /\ x e. B ) -> ( Z P x ) = x ) $.
    isgrpd.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrpd.j $e |- ( ( ph /\ x e. B ) -> ( N P x ) = Z ) $.
    $( Deduce a group from its properties. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpd $p |- ( ph -> G e. Grp ) $=
      ( ve vm cvv wcel cv cplusg cfv co cbs wceq wa wral wrex w3a cgrp wi
      3adant3r3 jca 3exp2 imp3a r19.21adv r19.21aivv opreq1 eqeq1d rcla4ev
      syl11anc r19.21aiva eqeq2 rexbidv anbi12d ralbidv 3jca opreqd eleq12d
      eqidd opreq123d eqeq12d raleqbidv rexeqbidv 3anbi23d mpbid eqid isgrp
      sylibr ) AGUAUBZBUCZCUCZGUDUEZUFZGUGUEZUBZWGDUCZWFUFZWDWEWJWFUFZWFUFZUHZU
      IZDWHUJZCWHUJZBWHUJZSUCZWDWFUFZWDUHZTUCZWDWFUFZWSUHZTWHUKZUIZBWHUJZSWHUKZ
      ULZGUMUBAWCWDWEFUFZEUBZXJWJFUFZWDWEWJFUFZFUFZUHZUIZDEUJZCEUJZBEUJZWSWDFUF
      ZWDUHZXBWDFUFZWSUHZTEUKZUIZBEUJZSEUKZULXIAWCXSYGLAXQBCEEAWDEUBZWEEUBZUIXP
      DEAYHYIWJEUBZXPUNAYHYIYJXPAYHYIYJULUIXKXOAYHYIXKYJMUONUPUQURUSUTAIEUBIWDF
      UFZWDUHZYBIUHZTEUKZUIZBEUJZYGOAYOBEAYHUIZYLYNPYQHEUBHWDFUFZIUHZYNQRYMYSTH
      EXBHUHYBYRIXBHWDFVAVBVCVDUPVEYFYPSIEWSIUHZYEYOBEYTYAYLYDYNYTXTYKWDWSIWDFV
      AVBYTYCYMTEWSIYBVFVGVHVIVCVDVJAXSWRYGXHWCAXRWQBEWHJAXQWPCEWHJAXPWODEWHJAX
      KWIXOWNAXJWGEWHAFWFWDWEKVKZJVLAXLWKXNWMAXJWGWJWJFWFKUUAAWJVMVNAWDWDXMWLFW
      FKAWDVMAFWFWEWJKVKVNVOVHVPVPVPAYFXGSEWHJAYEXFBEWHJAYAXAYDXEAXTWTWDAFWFWSW
      DKVKVBAYCXDTEWHJAYBXCWSAFWFXBWDKVKVBVQVHVPVQVRVSWHWFSTGBCDWHVTWFVTWAWB $.
      $( [6-Jun-2013] $)
  $}

  ${
    $d x y z B $.  $d x y z G $.  $d y z N $.  $d x O $.  $d x y z P $.
    $d x y z ph $.
    isgrprd.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isgrprd.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isgrprd.g $e |- ( ph -> G e. _V ) $.
    isgrprd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrprd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrprd.z $e |- ( ph -> O e. B ) $.
    isgrprd.i $e |- ( ( ph /\ x e. B ) -> ( x P O ) = x ) $.
    isgrprd.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrprd.j $e |- ( ( ph /\ x e. B ) -> ( x P N ) = O ) $.
    $( Deduce a group from its properties.  Same as ~ isgrpd except with right
       identity and inverse. ` N ` (negative) is normally dependent on ` x `
       i.e. read it as ` N ( x ) ` . $)
    isgrprd $p |- ( ph -> G e. Grp ) $=
      ( grplidd grplinvd isgrpd ) ABCDEFGHIJKLMNOABCDEFHIMOPNQRSQABCDEFHIMOPNQR
      TUA $.
      $( [10-Aug-2013] $)
  $}

  ${
    $d e m x y z B $.  $d m N $.  $d e m x y z P $.  $d e m x Z $.
    $d x y z ph $.
    isgrpdOLD.b $e |- B = ( Base ` G ) $.
    isgrpdOLD.p $e |- P = ( +g ` G ) $.
    isgrpdOLD.g $e |- ( ph -> G e. _V ) $.
    isgrpdOLD.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpdOLD.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpdOLD.z $e |- ( ph -> Z e. B ) $.
    isgrpdOLD.i $e |- ( ( ph /\ x e. B ) -> ( Z P x ) = x ) $.
    isgrpdOLD.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrpdOLD.j $e |- ( ( ph /\ x e. B ) -> ( N P x ) = Z ) $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpdOLD $p |- ( ph -> G e. Grp ) $=
      ( ve vm cvv wcel cv co wceq wa wral wrex w3a cgrp wi 3adant3r3 jca 3exp2
      imp3a r19.21adv r19.21aivv opreq1 eqeq1d rcla4ev syl11anc r19.21aiva
      eqeq2 rexbidv anbi12d ralbidv 3jca isgrp sylibr ) AGUAUBZBUCZCUCZFUDZEUBZ
      VMDUCZFUDVKVLVOFUDFUDUEZUFZDEUGZCEUGBEUGZSUCZVKFUDZVKUEZTUCZVKFUDZVTUEZTE
      UHZUFZBEUGZSEUHZUIGUJUBAVJVSWILAVRBCEEAVKEUBZVLEUBZUFVQDEAWJWKVOEUBZVQUKA
      WJWKWLVQAWJWKWLUIUFVNVPAWJWKVNWLMULNUMUNUOUPUQAIEUBIVKFUDZVKUEZWDIUEZTEUH
      ZUFZBEUGZWIOAWQBEAWJUFZWNWPPWSHEUBHVKFUDZIUEZWPQRWOXATHEWCHUEWDWTIWCHVKFU
      RUSUTVAUMVBWHWRSIEVTIUEZWGWQBEXBWBWNWFWPXBWAWMVKVTIVKFURUSXBWEWOTEVTIWDVC
      VDVEVFUTVAVGEFSTGBCDJKVHVI $.
      $( [5-Jun-2013] $)
  $}

  ${
    $d e m x y z B $.  $d m N $.  $d e m x y z P $.  $d e m x Z $.
    isgrpi.g $e |- G e. _V $.
    isgrpi.b $e |- B = ( Base ` G ) $.
    isgrpi.p $e |- P = ( +g ` G ) $.
    isgrpi.c $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpi.a $e |- ( ( x e. B /\ y e. B /\ z e. B ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpi.z $e |- Z e. B $.
    isgrpi.i $e |- ( x e. B -> ( Z P x ) = x ) $.
    isgrpi.n $e |- ( x e. B -> N e. B ) $.
    isgrpi.j $e |- ( x e. B -> ( N P x ) = Z ) $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` .  OBSOLETE PROOF. $)
    isgrpiOLD $p |- G e. Grp $=
      ( ve vm cgrp wcel cvv cv co wceq wa wral wrex isgrp w3a 3adant3 jca
      rgen3 opreq1 eqeq1d rcla4ev syl11anc rgen eqeq2 rexbidv anbi12d ralbidv
      mp2an mpbir3an ) FTUAFUBUAAUCZBUCZEUDZDUAZVGCUCZEUDVEVFVIEUDEUDUEZUFZCDUG
      BDUGADUGRUCZVEEUDZVEUEZSUCZVEEUDZVLUEZSDUHZUFZADUGZRDUHZDERSFABCJKUIIVKAB
      CDDDVEDUAZVFDUAZVIDUAZUJVHVJWBWCVHWDLUKMULUMHDUAHVEEUDZVEUEZVPHUEZSDUHZUF
      ZADUGZWANWIADWBWFWHOWBGDUAGVEEUDZHUEZWHPQWGWLSGDVOGUEVPWKHVOGVEEUNUOUPUQU
      LURVTWJRHDVLHUEZVSWIADWMVNWFVRWHWMVMWEVEVLHVEEUNUOWMVQWGSDVLHVPUSUTVAVBUP
      VCVD $.
      $( [3-Sep-2011] $)

    $d x y z G $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpi $p |- G e. Grp $=
      ( wtru cgrp wcel tru cbs cfv wceq a1i cplusg cvv cv co 3adant1 w3a
      adantl isgrpd ax-mp ) RFSTUARABCDEFGHDFUBUCUDRJUEEFUFUCUDRKUEFUGTRIUEAUHZ
      DTZBUHZDTZUOUQEUIZDTRLUJUPURCUHZDTUKUSUTEUIUOUQUTEUIEUIUDRMULHDTRNUEUPHUO
      EUIUOUDROULUPGDTRPULUPGUOEUIHUDRQULUMUN $.
      $( [3-Sep-2011] $)
  $}

  ${
    $d x y z B $.  $d x y z g G $.  $d x y z P $.  $d x Z $.
    isgrpix.a $e |- B e. _V $.
    isgrpix.b $e |- P e. _V $.
    isgrpix.g $e |- G = { <. 1 , B >. , <. 2 , P >. } $.
    isgrpix.2 $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpix.3 $e |- ( ( x e. B /\ y e. B /\ z e. B ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpix.z $e |- Z e. B $.
    isgrpix.5 $e |- ( x e. B -> ( Z P x ) = x ) $.
    isgrpix.6 $e |- ( x e. B -> N e. B ) $.
    isgrpix.7 $e |- ( x e. B -> ( N P x ) = Z ) $.
    $( Properties that determine a group.  Read ` N ` as ` N ( x ) ` .  Note:
       This theorem has hard-coded structure indices for demonstration
       purposes.  It is not intended for general use. $)
    isgrpix $p |- G e. Grp $=
      ( c1 cop c2 cpr cvv prex eqeltri grpbasex grpplusgx isgrpi ) ABCDEFGHFRDS
      ZTESZUAUBKUHUIUCUDDEFIJKUEDEFIJKUFLMNOPQUG $.
      $( [4-Sep-2011] $)
  $}

  ${
    $d e g x B $.  $d e g x G $.  $d g P $.
    grpidval.b $e |- B = ( Base ` G ) $.
    grpidval.p $e |- P = ( +g ` G ) $.
    grpidval.o $e |- O = ( 0g ` G ) $.
    $( The value of the identity element of a group. $)
    grpidval $p |- ( G e. V
  -> O = ( iota_ e e. B A. x e. B ( e P x ) = x ) ) $=
      ( vg wcel cvv cv co wceq wral crio elisset c0g cfv cplusg cbs fveq2
      syl6eqr opreqd eqeq1d raleqbidv riotaeqbidv df-0g riotaex fvmpt syl5eq
      syl ) EGLEMLZFDNZANZCOZUQPZABQZDBRZPEGSUOFETUAVAJKEUPUQKNZUBUAZOZUQPZAVBU
      CUAZQZDVFRVAMTVBEPZVGUTDVFBVHVFEUCUABVBEUCUDHUEZVHVEUSAVFBVIVHVDURUQVHVCC
      UPUQVHVCEUBUACVBEUBUDIUEUFUGUHUIADKUJUTDBUKULUMUN $.
      $( [7-Aug-2013] $) $( [20-Aug-2011] $)
  $}

  ${
    $d o x B $.  $d o x G $.
    grpidcl.b $e |- B = ( Base ` G ) $.
    grpidcl.o $e |- O = ( 0g ` G ) $.
    $( The identity element of a group belongs to the group. $)
    grpidcl $p |- ( G e. Grp -> O e. B ) $=
      ( vo vx cgrp wcel cv cplusg cfv co wceq wral wreu eqid grpideu wa crio
      grpidval adantr riotacl adantl eqeltrd mpdan ) BHIZFJGJZBKLZMUHNGAOZFAPZC
      AIGFAUIBDUIQZRUGUKSCUJFATZAUGCUMNUKGAUIFBCHDULEUAUBUKUMAIUGUJFAUCUDUEUF
      $.
      $( [27-Aug-2011] $)
  $}

  ${
    $d x y X $.  $d u w x y B $.  $d u x y G $.  $d u w x y P $.
    $d u w x y O $.
    grpidinv2.b $e |- B = ( Base ` G ) $.
    grpidinv2.p $e |- P = ( +g ` G ) $.
    grpidinv2.o $e |- O = ( 0g ` G ) $.
    $( X group's properties using the explicit identity element. $)
    grpidinv2 $p |- ( ( G e. Grp /\ X e. B ) -> ( ( ( O P X ) = X /\
         ( X P O ) = X ) /\ E. y e. B ( ( y P X ) = O /\ ( X P y ) = O ) ) ) $=
      ( vx vu vw cgrp wcel cv co wceq wa wrex wral crio wreu grpidinv grpideu
      wss wi ssid simpll ralimi a1i rgen riotass2 mpanl12 syl11anc grpidval
      eqtr4d wb grpidcl reuss2 ax-17 a17d opreq1 eqeq1d opreq2 anbi12d eqeq2
      rexbidv ralbidv riota2f mpbird id eqeq12d rcla4cva sylan ) DMNZEJOZCPZVPQ
      ZVPECPZVPQZRZAOZVPCPZEQZVPWBCPZEQZRZABSZRZJBTZFBNEFCPZFQZFECPZFQZRZWBFCPZ
      EQZFWBCPZEQZRZABSZRZVOWJKOZVPCPZVPQZVPXCCPZVPQZRZWCXCQZWEXCQZRZABSZRZJBTZ
      KBUAZEQZVOXOXEJBTZKBUAZEVOXNKBSZXQKBUBZXOXRQZJAKBCDGHUCZJKBCDGHUDZBBUEZXN
      XQUFZKBTZXSXTRZYABUGZYEKBYEXCBNXMXEJBXEXGXLUHUIUJUKZXNXQKBBULUMUNJBCKDEMG
      HIUOUPVOEBNZXNKBUBZWJXPUQBDEGIURVOXSXTYKYBYCYDYFYGYKYHYIXNXQKBBUSUMUNXNWJ
      KLBELOENKUTYJWJKVAXCEQZXMWIJBYLXHWAXLWHYLXEVRXGVTYLXDVQVPXCEVPCVBVCYLXFVS
      VPXCEVPCVDVCVEYLXKWGABYLXIWDXJWFXCEWCVFXCEWEVFVEVGVEVHVIUNVJWIXBJFBVPFQZW
      AWOWHXAYMVRWLVTWNYMVQWKVPFVPFECVDYMVKZVLYMVSWMVPFVPFECVBYNVLVEYMWGWTABYMW
      DWQWFWSYMWCWPEVPFWBCVDVCYMWEWREVPFWBCVBVCVEVGVEVMVN $.
      $( [18-Aug-2011] $)

    $( The identity element of a group is a left identity. $)
    grplid $p |- ( ( G e. Grp /\ X e. B ) -> ( O P X ) = X ) $=
      ( vy cgrp wcel wa co wceq cv wrex grpidinv2 simpll syl ) CJKEAKLDEBMENZED
      BMENZLIOZEBMDNEUBBMDNLIAPZLTIABCDEFGHQTUAUCRS $.
      $( [18-Aug-2011] $)

    $( The identity element of a group is a right identity. $)
    grprid $p |- ( ( G e. Grp /\ X e. B ) -> ( X P O ) = X ) $=
      ( vy cgrp wcel wa co wceq cv wrex grpidinv2 simplr syl ) CJKEAKLDEBMENZED
      BMENZLIOZEBMDNEUBBMDNLIAPZLUAIABCDEFGHQTUAUCRS $.
      $( [18-Aug-2011] $)

    $d e B $.  $d e G $.  $d e P $.  $d e x w Z $.
    $( Properties showing that an element ` Z ` is the identity element of a
       group.  See also ~ isgrpid2 . $)
    isgrpid $p |- ( G e. Grp
  -> ( ( Z e. B /\ A. x e. B ( Z P x ) = x ) <-> O = Z ) ) $=
      ( ve vw cgrp wcel cv co wceq wral wa crio grpidval adantr wreu wb
      grpideu anim2i ancoms ax-17 a17d opreq1 eqeq1d ralbidv riota2f syl
      biimpd impr eqtrd ex grpidcl eleq1 syl5ibcom grplid r19.21aiva jcad
      impbid ) DLMZFBMZFANZCOZVGPZABQZRZEFPZVEVKVLVEVKREJNZVGCOZVGPZABQZJBSZFVE
      EVQPVKABCJDELGHITUAVEVFVJVQFPZVEVFRZVJVRVSVFVPJBUBZRZVJVRUCVFVEWAVEVTVFAJ
      BCDGHUDUEUFVPVJJKBFKNFMJUGVFVJJUHVMFPZVOVIABWBVNVHVGVMFVGCUIUJUKULUMUNUOU
      PUQVEVLVFVJVEEBMVLVFBDEGIUREFBUSUTVEEVGCOZVGPZABQVLVJVEWDABBCDEVGGHIVAVBV
      LWDVIABVLWCVHVGEFVGCUIUJUKUTVCVD $.
      $( [7-Aug-2013] $)
  $}

  ${
    $d y B $.  $d y G $.  $d y P $.  $d y X $.  $d y Y $.  $d y Z $.
    grprcan.b $e |- B = ( Base ` G ) $.
    grprcan.p $e |- P = ( +g ` G ) $.
    $( Right cancellation law for groups. $)
    grprcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( X P Z ) = ( Y P Z ) <-> X = Y ) ) $=
      ( vy cgrp wcel co wceq wb wa cv c0g cfv wrex wi eqid grpidinv2 simpr
      reximi adantl syl ad2ant2rl opreq1 ad2antll grpass 3exp2 imp41 adantlrl
      adantrr imp42 adantllr 3eqtr3d adantrrl opreq2 ad2antrl grprid ad2antrr
      ad2ant2r adantr exp45 r19.23adv mpd impbid1 exp43 3imp2 ) CJKZDAKZEAKZFAK
      ZDFBLZEFBLZMZDEMZNZVKVLVMVNVSVKVLOZVMVNOZOZVQVRWBFIPZBLZCQRZMZIASZVQVRTZV
      KVNWGVLVMVKVNOWEFBLFMFWEBLFMOZWCFBLWEMZWFOZIASZOWGIABCWEFGHWEUAZUBWLWGWIW
      KWFIAWJWFUCUDUEUFUGWBWFWHIAWBWCAKZWFVQVRWBWNWFVQOOZOZDWEBLZEWEBLZDEWPDWDB
      LZEWDBLZWQWRWBWNVQWSWTMWFWBWNVQOOVOWCBLZVPWCBLZWSWTVQXAXBMWBWNVOVPWCBUHUI
      WBWNXAWSMZVQVTVNWNXCVMVKVLVNWNXCVKVLVNWNXCABCDFWCGHUJUKULUMUNWBWNXBWTMZVQ
      VKWAWNXDVLVKVMVNWNXDVKVMVNWNXDABCEFWCGHUJUKUOUPUNUQURWOWSWQMZWBWFXEWNVQWD
      WEDBUSUTUEWOWTWRMZWBWFXFWNVQWDWEEBUSUTUEUQVTWQDMWAWOABCWEDGHWMVAVBWBWREMZ
      WOVKVMXGVLVNABCWEEGHWMVAVCVDUQVEVFVGDEFBUHVHVIVJ $.
      $( [24-Aug-2011] $)
  $}

  ${
    $d y z B $.  $d y z G $.  $d y z P $.  $d y z O $.  $d y z X $.
    grpinveu.b $e |- B = ( Base ` G ) $.
    grpinveu.p $e |- P = ( +g ` G ) $.
    grpinveu.o $e |- O = ( 0g ` G ) $.
    $( The left inverse element of a group is unique.  Lemma 2.2.1(b) of
       [Herstein] p. 55. $)
    grpinveu $p |- ( ( G e. Grp /\ X e. B )
           -> E! y e. B ( y P X ) = O ) $=
      ( vz cgrp wcel wa cv co wceq wi wral wrex wreu grpidinv2 simpl reximi
      adantl syl w3a eqtr3 grprcan syl5ib 3exp2 com24 imp41 an32s exp3a
      r19.21adva ancld reximdva mpd opreq1 eqeq1d reu8 sylibr ) DKLZFBLZMZANZFC
      OZEPZJNZFCOZEPZVFVIPZQZJBRZMZABSZVHABTVEVHABSZVPVEEFCOFPFECOFPMZVHFVFCOEP
      ZMZABSZMVQABCDEFGHIUAWAVQVRVTVHABVHVSUBUCUDUEVEVHVOABVEVFBLZMZVHVNWCVHVMJ
      BWCVIBLZMVHVKVLVEWDWBVHVKMZVLQZVCVDWDWBWFVCWBWDVDWFVCWBWDVDWFWEVGVJPVCWBW
      DVDUFMVLVGVJEUGBCDVFVIFGHUHUIUJUKULUMUNUOUPUQURVHVKAJBVLVGVJEVFVIFCUSUTVA
      VB $.
      $( [24-Aug-2011] $)

    $( Two ways of saying that an element of a group is the identity
       element. $)
    grpid $p |- ( ( G e. Grp /\ X e. B ) ->
                  ( X = O <-> ( X P X ) = X ) ) $=
      ( cgrp wcel wa co wceq wb wi grpidcl grprcan 3exp2 mpid pm2.43d imp
      grplid eqeq2d bitr3d ) CIJZEAJZKZEEBLZDEBLZMZEDMZUHEMUEUFUJUKNZUEUFULUEUF
      DAJZUFULOACDFHPUEUFUMUFULABCEDEFGQRSTUAUGUIEUHABCDEFGHUBUCUD $.
      $( [24-Aug-2011] $)

    $( Properties showing that an element ` Z ` is the identity element of a
       group. $)
    isgrpid2 $p |- ( G e. Grp
  -> ( ( Z e. B /\ ( Z P Z ) = Z ) <-> O = Z ) ) $=
      ( cgrp wcel co wceq wa simprr wb grpid adantrr mpbird eqcomd ex grpidcl
      grplid mpdan jca eleq1 id opreq12d eqeq12d anbi12d syl5ibcom impbid ) CIJ
      ZEAJZEEBKZELZMZDELZULUPUQULUPMZEDUREDLZUOULUMUONULUMUSUOOUOABCDEFGHPQRSTU
      LDAJZDDBKZDLZMUQUPULUTVBACDFHUAZULUTVBVCABCDDFGHUBUCUDUQUTUMVBUODEAUEUQVA
      UNDEUQDEDEBUQUFZVDUGVDUHUIUJUK $.
      $( [7-Aug-2013] $)
  $}

  ${
    $d g x y B $.  $d g x y G $.  $d g O $.  $d g P $.
    grpinvval.b $e |- B = ( Base ` G ) $.
    grpinvval.p $e |- P = ( +g ` G ) $.
    grpinvval.o $e |- O = ( 0g ` G ) $.
    grpinvval.n $e |- N = ( -g ` G ) $.
    $( The inverse function of a group. $)
    grpinvfval $p |- ( G e. V
              -> N = ( x e. B |-> ( iota_ y e. B ( y P x ) = O ) ) ) $=
      ( vg wcel cvv cv co wceq crio cmpt elisset cminusg cfv cbs cplusg c0g
      fveq2 syl6eqr opreqd eqeq12d riotaeqbidv mpteq12dv df-minusg fvex
      eqeltri mptexg ax-mp fvmpt syl5eq syl ) EHNEONZFACBPZAPZDQZGRZBCSZTZREHUA
      VAFEUBUCVGLMEAMPZUDUCZVBVCVHUEUCZQZVHUFUCZRZBVISZTVGOUBVHERZAVIVNCVFVOVIE
      UDUCZCVHEUDUGIUHZVOVMVEBVICVQVOVKVDVLGVOVJDVBVCVOVJEUEUCDVHEUEUGJUHUIVOVL
      EUFUCGVHEUFUGKUHUJUKULABMUMCONVGONCVPOIEUDUNUOACVFOUPUQURUSUT $.
      $( [7-Aug-2013] $) $( [24-Aug-2011] $)

    $d x O $.  $d x P $.  $d x y X $.
    $( The inverse of a group element. $)
    grpinvval $p |- ( ( G e. V /\ X e. B ) ->
                    ( N ` X ) = ( iota_ y e. B ( y P X ) = O ) ) $=
      ( vx wcel cfv cv co wceq crio cmpt grpinvfval fveq1d opreq2 eqeq1d
      riotabidv eqid riotaex fvmpt sylan9eq ) DGNZHBNHEOHMBAPZMPZCQZFRZABSZTZOU
      KHCQZFRZABSZUJHEUPMABCDEFGIJKLUAUBMHUOUSBUPULHRZUNURABUTUMUQFULHUKCUCUDUE
      UPUFURABUGUHUI $.
      $( [7-Aug-2013] $) $( [24-Aug-2011] $)
  $}

  ${
    $d y B $.  $d y G $.  $d y P $.  $d y X $.
    grpinvcl.b $e |- B = ( Base ` G ) $.
    grpinvcl.p $e |- P = ( +g ` G ) $.
    grpinvcl.n $e |- N = ( -g ` G ) $.
    $( X group element's inverse is a group element. $)
    grpinvcl $p |- ( ( G e. Grp /\ X e. B ) -> ( N ` X ) e. B ) $=
      ( vy cgrp wcel wa cfv cv co c0g wceq crio eqid grpinvval wreu grpinveu
      riotacl syl eqeltrd ) CJKEAKLZEDMINEBOCPMZQZIARZAIABCDUGJEFGUGSZHTUFUHIAU
      AUIAKIABCUGEFGUJUBUHIAUCUDUE $.
      $( [24-Aug-2011] $)
  $}

  ${
    $d w y B $.  $d y G $.  $d w y N $.  $d w y P $.  $d w y O $.  $d w y X $.
    grpinv.b $e |- B = ( Base ` G ) $.
    grpinv.p $e |- P = ( +g ` G ) $.
    grpinv.u $e |- O = ( 0g ` G ) $.
    grpinv.n $e |- N = ( -g ` G ) $.
    $( The properties of a group element's inverse. $)
    grpinv $p |- ( ( G e. Grp /\ X e. B ) ->
                 ( ( ( N ` X ) P X ) = O /\ ( X P ( N ` X ) ) = O ) ) $=
      ( vy vw cgrp wcel wa cfv co wceq cv crio wrex wreu grpidinv2 simprd
      grpinveu wss wi wral ssid simpl a1i rgen riotass2 mpanl12 syl11anc
      grpinvval eqtr4d wb grpinvcl reuss2 ax-17 a17d opreq1 eqeq1d opreq2
      anbi12d riota2f mpbird ) CMNFANOZFDPZFBQZERZFVJBQZERZOZKSZFBQZERZFVPBQZER
      ZOZKATZVJRZVIWBVRKATZVJVIWAKAUAZVRKAUBZWBWDRZVIEFBQFRFEBQFROWEKABCEFGHIUC
      UDZKABCEFGHIUEZAAUFZWAVRUGZKAUHZWEWFOZWGAUIZWKKAWKVPANVRVTUJUKULZWAVRKAAU
      MUNUOKABCDEMFGHIJUPUQVIVJANZWAKAUBZVOWCURABCDFGHJUSVIWEWFWQWHWIWJWLWMWQWN
      WOWAVRKAAUTUNUOWAVOKLAVJLSVJNKVAWPVOKVBVPVJRZVRVLVTVNWRVQVKEVPVJFBVCVDWRV
      SVMEVPVJFBVEVDVFVGUOVH $.
      $( [24-Aug-2011] $)

    $( The left inverse of a group element. $)
    grplinv $p |- ( ( G e. Grp /\ X e. B ) -> ( ( N ` X ) P X ) = O ) $=
      ( cgrp wcel wa cfv co wceq grpinv simpld ) CKLFALMFDNZFBOEPFSBOEPABCDEFGH
      IJQR $.
      $( [24-Aug-2011] $)

    $( The right inverse of a group element. $)
    grprinv $p |- ( ( G e. Grp /\ X e. B ) -> ( X P ( N ` X ) ) = O ) $=
      ( cgrp wcel wa cfv co wceq grpinv simprd ) CKLFALMFDNZFBOEPFSBOEPABCDEFGH
      IJQR $.
      $( [24-Aug-2011] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid1 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                    ( ( N ` X ) = Y <-> ( X P Y ) = O ) ) $=
      ( cgrp wcel w3a cfv wceq co wa opreq2 adantl grprinv 3adant3 adantr
      eqtr3d grplinv opreq1d grpinvcl adantrr simprl simprr 3jca grpass syldan
      3impb grplid 3adant2 3eqtr3d grprid 3eqtr3rd impbida ) CLMZFAMZGAMZNZFDOZ
      GPZFGBQZEPZVDVFRFVEBQZVGEVFVIVGPVDVEGFBSTVDVIEPZVFVAVBVJVCABCDEFHIJKUAUBU
      CUDVDVHRVEVGBQZVEEBQZGVEVHVKVLPVDVGEVEBSTVDVKGPVHVDVEFBQZGBQZEGBQZVKGVAVB
      VNVOPVCVAVBRVMEGBABCDEFHIJKUEUFUBVAVBVCVNVKPZVAVBVCRZVEAMZVBVCNVPVAVQRVRV
      BVCVAVBVRVCABCDFHIKUGZUHVAVBVCUIVAVBVCUJUKABCVEFGHIULUMUNVAVCVOGPVBABCEGH
      IJUOUPUQUCVDVLVEPZVHVAVBVTVCVAVBVRVTVSABCEVEHIJURUMUBUCUSUT $.
      $( [24-Aug-2011] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid2 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                    ( ( N ` X ) = Y <-> ( Y P X ) = O ) ) $=
      ( cgrp wcel w3a cfv wceq co wa opreq1 adantl grplinv 3adant3 adantr
      eqtr3d grpinvcl grplid syldan eqcomd simprr simprl adantrr 3jca grpass
      3impb grprinv opreq2d grprid 3adant2 3eqtrd 3eqtr2d impbida ) CLMZFAMZGAM
      ZNZFDOZGPZGFBQZEPZVEVGRVFFBQZVHEVGVJVHPVEVFGFBSTVEVJEPZVGVBVCVKVDABCDEFHI
      JKUAUBUCUDVEVIRVFEVFBQZVHVFBQZGVEVFVLPVIVEVLVFVBVCVLVFPZVDVBVCVFAMZVNABCD
      FHIKUEZABCEVFHIJUFUGUBUHUCVIVMVLPVEVHEVFBSTVEVMGPVIVEVMGFVFBQZBQZGEBQZGVB
      VCVDVMVRPZVBVCVDRZVDVCVONVTVBWARVDVCVOVBVCVDUIVBVCVDUJVBVCVOVDVPUKULABCGF
      VFHIUMUGUNVBVCVRVSPVDVBVCRVQEGBABCDEFHIJKUOUPUBVBVDVSGPVCABCEGHIJUQURUSUC
      UTVA $.
      $( [24-Aug-2011] $)

    $d e w x B $.  $d e x G $.  $d e w x M $.  $d x N $.  $d e w x O $.
    $d e w x P $.
    $( Properties showing that a function ` M ` is the inverse function of a
       group. $)
    isgrpinv $p |- ( G e. Grp
  -> ( ( M : B --> B /\ A. x e. B ( ( M ` x ) P x ) = O ) <-> N = M ) ) $=
      ( ve vw cgrp wcel wf cv cfv co wceq wral wa crio simplll simplr
      grpinvval syl11anc simpr wreu wb simpllr ffvelrn grpinveu ax-17 a17d
      opreq1 eqeq1d riota2f mpbid eqtrd ex ralimdva impr wfn cmpt cvv eqid
      fnmpt riotaex a1i mprg grpinvfval fneq1d adantr mpbiri ffn ad2antrl
      eqfnfv2 mpbird grpinvcl eqeltrrd fmptd feq1d grplinv r19.21aiva jca feq1
      fveq1 opreq1d ralbidv anbi12d syl5ibcom impbid ) DNOZBBEPZAQZERZWPCSZGTZA
      BUAZUBZFETZWNXAXBWNXAUBZXBWPFRZWQTZABUAZWNWOWTXFWNWOUBZWSXEABXGWPBOZUBZWS
      XEXIWSUBZXDLQZWPCSZGTZLBUCZWQXJWNXHXDXNTWNWOXHWSUDZXGXHWSUEZLBCDFGNWPHIJK
      UFZUGXJWSXNWQTZXIWSUHXJWQBOZXMLBUIZWSXRUJXJWOXHXSWNWOXHWSUKXPBBWPEULUGXJW
      NXHXTXOXPLBCDGWPHIJUMUGXMWSLMBWQMQWQOLUNXSWSLUOXKWQTXLWRGXKWQWPCUPUQURUGU
      SUTVAVBVCXCFBVDZEBVDZXBXFUJXCYAABXNVEZBVDZXNVFOZYDABABXNYCVFYCVGZVHYEXHXM
      LBVIVJVKWNYAYDUJXAWNBFYCALBCDFGNHIJKVLZVMVNVOWOYBWNWTBBEVPVQABFEVRUGVSVAW
      NBBFPZXDWPCSZGTZABUAZUBXBXAWNYHYKWNYHBBYCPWNABXNBYCWNXHXNBOWNXHUBXDXNBXQB
      CDFWPHIKVTWAVAYFWBWNBBFYCYGWCVSWNYJABBCDFGWPHIJKWDWEWFXBYHWOYKWTBBFEWGXBY
      JWSABXBYIWRGXBXDWQWPCWPFEWHWIUQWJWKWLWM $.
      $( [7-Aug-2013] $)
  $}

  ${
    grpinvid.u $e |- O = ( 0g ` G ) $.
    grpinvid.n $e |- N = ( -g ` G ) $.
    $( The inverse of the identity element of a group. $)
    grpinvid $p |- ( G e. Grp -> ( N ` O ) = O ) $=
      ( cgrp wcel cfv wceq cplusg co cbs eqid grpidcl grplid mpdan wb
      grpinvid1 mpd3an23 mpbird ) AFGZCBHCIZCCAJHZKCIZUACALHZGZUDUEACUEMZDNZUEU
      CACCUGUCMZDOPUAUFUFUBUDQUHUHUEUCABCCCUGUIDERST $.
      $( [24-Aug-2011] $)
  $}

  ${
    grplcan.b $e |- B = ( Base ` G ) $.
    grplcan.p $e |- P = ( +g ` G ) $.
    $( Left cancellation law for groups. $)
    grplcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( Z P X ) = ( Z P Y ) <-> X = Y ) ) $=
      ( cgrp wcel w3a wa co wceq wi cminusg cfv opreq2 adantl c0g eqid grplinv
      adantlr opreq1d grpinvcl adantrl simprr simprl 3jca grpass syldan
      anassrs grplid adantr 3eqtr3d adantrr exp53 3imp2 impbid1 ) CIJZDAJZEAJZF
      AJZKLFDBMZFEBMZNZDENZUTVAVBVCVFVGOUTVAVBVCVFVGUTVALZVBVCLZLZVFLFCPQZQZVDB
      MZVLVEBMZDEVFVMVNNVJVDVEVLBRSVJVMDNZVFVHVCVOVBVHVCLZVLFBMZDBMZCTQZDBMZVMD
      VPVQVSDBUTVCVQVSNZVAABCVKVSFGHVSUAZVKUAZUBZUCUDUTVAVCVRVMNZUTVAVCLZVLAJZV
      CVAKWEUTWFLWGVCVAUTVCWGVAABCVKFGHWCUEZUFUTVAVCUGUTVAVCUHUIABCVLFDGHUJUKUL
      VHVTDNVCABCVSDGHWBUMUNUOUFUNVJVNENZVFUTVIWIVAUTVILZVQEBMZVSEBMZVNEWJVQVSE
      BUTVCWAVBWDUFUDUTVIWGVCVBKWKVNNWJWGVCVBUTVCWGVBWHUFUTVBVCUGUTVBVCUHUIABCV
      LFEGHUJUKUTVBWLENVCABCVSEGHWBUMUPUOUCUNUOUQURDEFBRUS $.
      $( [25-Aug-2011] $)
  $}


