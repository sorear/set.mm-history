$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c e f g m p w x $.
    $( Define class of all groups. $)
    df-grp $a |- Grp = { f | E. g E. p ( g = ( Base ` f )
       /\ p = ( +g ` f ) /\ ( A. a e. g A. b e. g A. c e. g ( ( a p b ) e. g
            /\ ( ( a p b ) p c ) = ( a p ( b p c ) ) ) /\
          E. e e. g A. a e. g ( ( e p a ) = a /\
            E. m e. g ( m p a ) = e ) ) ) } $.

    $( Define group identity element. $)
    df-0g $a |- 0g = ( g e. _V |-> ( iota_ e e. ( Base ` g )
      A. x e. ( Base ` g ) ( e ( +g ` g ) x ) = x ) ) $.

    $( Define inverse of group element. $)
    df-minusg $a |- invg = ( g e. _V |-> ( x e. ( Base ` g )
  |-> ( iota_ w e. ( Base ` g ) ( w ( +g ` g ) x ) = ( 0g ` g ) ) ) ) $.

    $( Define group subtraction (also called division for multiplicative
       groups). $)
    df-sbg $a |- -g = ( g e. _V |-> ( x e. ( Base ` g ) , y e. ( Base ` g )
  |-> ( x ( +g ` g ) ( ( invg ` g ) ` y ) ) ) ) $.
  $}


  ${
    $d a b c e f g m p $.
    $( Alternate definition of the class of all groups using class
       substitution. $)
    dfgrp2 $p |- Grp = { f | [ ( Base ` f ) / g ] [ ( +g ` f ) / p ]
      ( A. a e. g A. b e. g A. c e. g ( ( a p b ) e. g
            /\ ( ( a p b ) p c ) = ( a p ( b p c ) ) ) /\
          E. e e. g A. a e. g ( ( e p a ) = a /\
            E. m e. g ( m p a ) = e ) ) } $=
      ( cv cbs cfv wceq cplusg co wa wral wrex wex cab wsbc cgrp wcel fvex sbc5
      w3a df-grp 19.42v 3anass exbii anbi2i 3bitr4ri bitri abbii eqtr4i ) UACIZ
      BIZJKZLZEIZUPMKZLZFIZGIZUSNZUOUBVDHIZUSNVBVCVEUSNUSNLOHUOPGUOPFUOPAIZVBUS
      NVBLDIVBUSNVFLDUOQOFUOPAUOQOZUEZERZCRZBSVGEUTTZCUQTZBSABCDEFGHUFVLVJBVLUR
      VKOZCRVJVKCUQUPJUCUDVMVICURVAVGOZOZERURVNERZOVIVMURVNEUGVHVOEURVAVGUHUIVK
      VPURVGEUTUPMUCUDUJUKUIULUMUN $.
      $( [2-Dec-2013] $)
  $}

  $( Utility lemma for constructing a (2-element) group structure without
     direct reference to component indices.  This lemma lets us add a second
     element to a singleton using ~ fnunsn . $)
  grplem $p |- -. ( +g ` ndx ) e. { ( Base ` ndx ) } $=
    ( vs cnx cplusg cfv cbs csn wcel wceq wne wn c2 df-plusg 2nn ndxarg 1re 2re
    c1 1lt2 ltneii basendx neeqtrri eqnetri df-ne mpbi elsni mto ) BCDZBEDZFGUG
    UHHZUGUHIUIJUGKUHCKAALMNKQUHQKOPRSTUAUBUGUHUCUDUGUHUEUF $.
    $( [1-Oct-2013] $)

  ${
    grpfn.g $e |- G = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    ${
      grpfn.d $e |- D = { ( Base ` ndx ) , ( +g ` ndx ) } $.
      $( A constructed group is a function with domain ` D ` .  (We include the
         domain for use with later structures that extend the domain.) $)
      grpfn $p |- G Fn D $=
        ( cnx cbs cfv csn cop cplusg fvex fnsn cpr df-pr eqtri grplem fnunsn
        cun ) GHIZJZBUAAKZJZDGLIZCGLMUAAGHMNDUCUECKZOUDUFJTEUCUFPQBUAUEOUBUEJTF
        UAUEPQRS $.
        $( [28-Sep-2013] $)
    $}

    $( The base set of a constructed group. $)
    grpbase $p |- ( B e. V -> B = ( Base ` G ) ) $=
      ( cbs cnx cfv cop cplusg cpr cvv prex eqeltri wfn wfun eqid grpfn fnfun
      vs ax-mp baseid opex prid1 eleqtrri strfv ) ACFDTCGFHZAIZGJHZBIZKZLEUHUJM
      NCUGUIKZOCPAULBCEULQRULCSUATUBUHUKCUHUJUGAUCUDEUEUF $.
      $( [2-Aug-2013] $)

    $( The operation of a constructed group. $)
    grpplusg $p |- ( P e. V -> P = ( +g ` G ) ) $=
      ( cplusg cnx cbs cfv cop cpr cvv prex eqeltri wfn wfun eqid grpfn fnfun
      vs ax-mp plusgid opex prid2 eleqtrri strfv ) BCFDTCGHIZAJZGFIZBJZKZLEUHUJ
      MNCUGUIKZOCPAULBCEULQRULCSUATUBUJUKCUHUJUIBUCUDEUEUF $.
      $( [2-Aug-2013] $)
  $}

  ${
    grpstrx.b $e |- B e. _V $.
    grpstrx.p $e |- P e. _V $.
    grpstrx.g $e |- G = { <. 1 , B >. , <. 2 , P >. } $.
    $( The base of an explicitly given group.  Note:  This theorem has
       hard-coded structure indices for demonstration purposes.  It is not
       intended for general use; use ~ grpbase instead. $)
    grpbasex $p |- B = ( Base ` G ) $=
      ( vs cvv wcel cbs cfv wceq c1 cop c2 cpr cnx cplusg basendx opeq1i ndxarg
      df-plusg 2nn preq12i eqtr4i grpbase ax-mp ) AHIACJKLDABCHCMANZOBNZPQJKZAN
      ZQRKZBNZPFUKUHUMUIUJMASTULOBROGGUBUCUATUDUEUFUG $.
      $( [17-Oct-2012] $)

    $( The operation of an explicitly given group.  Note:  This theorem has
       hard-coded structure indices for demonstration purposes.  It is not
       intended for general use; use ~ grpplusgx instead. $)
    grpplusgx $p |- P = ( +g ` G ) $=
      ( vs cvv wcel cplusg cfv wceq c1 cop c2 cpr cnx cbs basendx opeq1i ndxarg
      df-plusg 2nn preq12i eqtr4i grpplusg ax-mp ) BHIBCJKLEABCHCMANZOBNZPQRKZA
      NZQJKZBNZPFUKUHUMUIUJMASTULOBJOGGUBUCUATUDUEUFUG $.
      $( [17-Oct-2012] $)
  $}

  ${
    $d a b c e f g m p B $.  $d f g p G $.  $d a b c e f g m p P $.
    isgrp.b $e |- B = ( Base ` G ) $.
    isgrp.p $e |- P = ( +g ` G ) $.
    $( The predicate "is a group." $)
    isgrp $p |- ( G e. Grp <-> ( G e. _V
            /\ A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( vg vp wcel cv wceq co wa wral wrex opreq vf cgrp cbs cfv cplusg w3a wex
      cab cvv df-grp eleq2i syl6eqr eqeq2d 3anbi12d 2exbidv eqid elab4g eqeltri
      fveq2 fvex eleq2 anbi1d raleqbi1dv rexeq anbi2d rexeqbi1dv anbi12d eleq1d
      opreq1d eqtrd opreq2d eqeq12d ralbidv 2ralbidv eqeq1d rexralbidv ceqsex2v
      rexbidv anbi2i 3anass bitr4i 3bitri ) EUBMEKNZUANZUCUDZOZLNZWDUEUDZOZFNZG
      NZWGPZWCMZWLHNZWGPZWJWKWNWGPZWGPZOZQZHWCRZGWCRZFWCRZCNZWJWGPZWJOZDNZWJWGP
      ZXCOZDWCSZQZFWCRZCWCSZQZUFZLUGKUGZUAUHZMEUIMZWCAOZWGBOZXMUFZLUGKUGZQZXQWJ
      WKBPZAMZYCWNBPZWJWKWNBPZBPZOZQZHARZGARFARZXCWJBPZWJOZXFWJBPZXCOZDASZQZFAR
      CASZUFZUBXPECUAKDLFGHUJUKXOYAUAEXPWDEOZXNXTKLYTWFXRWIXSXMYTWEAWCYTWEEUCUD
      ZAWDEUCUSIULUMYTWHBWGYTWHEUEUDZBWDEUEUSJULUMUNUOXPUPUQYBXQYKYRQZQYSYAUUCX
      QXMWLAMZWRQZHARZGARZFARZXEXHDASZQZFARZCASZQUUCKLABAUUAUIIEUCUTURBUUBUIJEU
      EUTURXRXBUUHXLUULXAUUGFWCAWTUUFGWCAWSUUEHWCAXRWMUUDWRWCAWLVAVBVCVCVCXKUUK
      CWCAXJUUJFWCAXRXIUUIXEXHDWCAVDVEVCVFVGXSUUHYKUULYRXSUUFYJFGAAXSUUEYIHAXSU
      UDYDWRYHXSWLYCAWJWKWGBTZVHXSWOYEWQYGXSWOWLWNBPYEWLWNWGBTXSWLYCWNBUUMVIVJX
      SWQWJWPBPYGWJWPWGBTXSWPYFWJBWKWNWGBTVKVJVLVGVMVNXSUUJYQCFAAXSXEYMUUIYPXSX
      DYLWJXCWJWGBTVOXSXHYODAXSXGYNXCXFWJWGBTVOVRVGVPVGVQVSXQYKYRVTWAWB $.
      $( [17-Oct-2012] $)

    $( The predicate "is a group." $)
    isgrp2 $p |- ( G e. Grp <-> ( A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( cgrp wcel cv co wceq wa wral wrex c0 cbs cvv elisset wne rexn0 wn fvprc
      cfv syl5eq necon1ai syl adantl wb wi isgrp pm4.71ri 3anass 3bitr3i pm5.32
      w3a mpbir pm5.21nii ) EKLZEUALZFMZGMZBNZALVFHMZBNVDVEVGBNBNOPHAQGAQFAQZCM
      ZVDBNVDODMVDBNVIODARPFAQZCARZPZEKUBZVKVCVHVKASUCVCVJCAUDVCASVCUEAETUGSIET
      UFUHUIUJUKVCVBVLULUMVCVBPZVCVLPZULVBVCVHVKUSVNVOABCDEFGHIJUNVBVCVMUOVCVHV
      KUPUQVCVBVLURUTVA $.
      $( [24-Oct-2012] $)
  $}

  ${
    $d a b c e m K $.
    grpprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    grpprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    $( If two structures have the same group components (properties), one is a
       group iff the other one is. $)
    grpprop $p |- ( K e. Grp <-> L e. Grp ) $=
      ( va vb vc ve vm cgrp wcel cv cfv co wceq wa wral wrex eqid isgrp2 cplusg
      cbs bitr4i ) AJKELZFLZAUAMZNZAUBMZKUGGLZUFNUDUEUIUFNUFNOPGUHQFUHQEUHQHLZU
      DUFNUDOILUDUFNUJOIUHRPEUHQHUHRPBJKUHUFHIAEFGUHSUFSTUHUFHIBEFGCDTUC $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d a b c e m B $.  $d a b c e m P $.
    elgrp.b $e |- B e. _V $.
    elgrp.p $e |- P e. _V $.
    elgrp.k $e |- K
          = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    $( The predicate "is a group structure" for a specific structure member. $)
    elgrp $p |- ( K e. Grp <-> ( A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
   /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( cvv wcel cbs cfv wceq grpbase ax-mp cplusg grpplusg isgrp2 ) ABCDEFGHAL
      MAENOPIABELKQRBLMBESOPJABELKTRUA $.
      $( [24-Oct-2012] $)
  $}

  ${
    $d a b c e m B $.  $d a b c e m P $.
    grppropOLD.b $e |- ( Base ` K ) = B $.
    grppropOLD.p $e |- ( +g ` K ) = P $.
    grppropOLD.c $e |- ( Base ` L ) = B $.
    grppropOLD.q $e |- ( +g ` L ) = P $.
    $( Any set with the same (relevant) properties as a given group is also a
       group (and vice-versa). $)
    grppropOLD $p |- ( K e. Grp <-> L e. Grp ) $=
      ( va vb vc ve vm wcel cv co wceq wral cfv eqcomi cbs cplusg isgrp2 bitr4i
      cgrp wa wrex ) CUENIOZJOZBPZANUJKOZBPUHUIUKBPBPQUFKARJARIARLOZUHBPUHQMOUH
      BPULQMAUGUFIARLAUGUFDUENABLMCIJKCUASAETCUBSBFTUCABLMDIJKDUASAGTDUBSBHTUCU
      D $.
      $( [28-Oct-2012] $)
  $}

  ${
    $d a b c e m B $.  $d a b c e m P $.
    grpstr.b $e |- ( Base ` K ) = B $.
    grpstr.p $e |- ( +g ` K ) = P $.
    grpstr.l $e |- L
          = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    $( Generalize a specific 2-element group ` L ` to show that any set ` K `
       with the same (relevant) properties is also a group. $)
    grpstr $p |- ( K e. Grp <-> L e. Grp ) $=
      ( va vb vc ve vm cgrp wcel cv co wceq wa wral wrex cbs eqcomi cplusg fvex
      cfv isgrp2 cvv eqeltrri elgrp bitr4i ) CMNHOZIOZBPZANUMJOZBPUKULUNBPBPQRJ
      ASIASHASKOZUKBPUKQLOUKBPUOQLATRHASKATRDMNABKLCHIJCUAUEZAEUBCUCUEZBFUBUFAB
      KLDHIJUPAUGECUAUDUHUQBUGFCUCUDUHGUIUJ $.
      $( [28-Oct-2012] $)
  $}

  ${
    grpss.g $e |- G = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. } $.
    grpss.r $e |- R e. _V $.
    grpss.s $e |- G C_ R $.
    grpss.f $e |- Fun R $.
    $( Show that a structure extending a constructed group (e.g. a ring) is
       also a group.  This allows us to prove that a constructed potential ring
       ` R ` is a group before we know that it is also a ring.  (Theorem
       ~ rnggrp , on the other hand, requires that we know in advance that
       ` R ` is a ring.) $)
    grpss $p |- ( G e. Grp <-> R e. Grp ) $=
      ( vs cbs cfv cplusg eqid baseid cnx cop cpr opex eleqtrri strss plusgid
      prid1 prid2 grppropOLD ) DJKZDLKZDCUEMUFMADCJIFHGINOJKZAPZUHOLKZBPZQZDUHU
      JUGARUBESTBDCLIFHGIUAUJUKDUHUJUIBRUCESTUD $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d a b c e f m B $.  $d f G $.  $d a b c e f m P $.  $d a b c X $.
    $d b c Y $.  $d c Z $.
    grplem1.b $e |- B = ( Base ` G ) $.
    grplem1.p $e |- P = ( +g ` G ) $.
    $( Lemma for group properties. $)
    grplem1 $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
       ( ( X P Y ) e. B /\ ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) ) $=
      ( va vb vc ve vm wcel cv co wceq wa wral opreq1 cgrp w3a cvv wrex simp2bi
      isgrp eleq1d opreq1d eqeq12d anbi12d opreq2 opreq2d anbi2d rcla43v mpan9
      ) CUANZIOZJOZBPZANZUSKOZBPZUQURVABPZBPZQZRZKASJASIASZDANEANFANUBDEBPZANZV
      HFBPZDEFBPZBPZQZRZUPCUCNVGLOZUQBPUQQMOUQBPVOQMAUDRIASLAUDABLMCIJKGHUFUEVF
      VNDURBPZANZVPVABPZDVCBPZQZRVIVHVABPZDEVABPZBPZQZRIJKDEFAAAUQDQZUTVQVEVTWE
      USVPAUQDURBTZUGWEVBVRVDVSWEUSVPVABWFUHUQDVCBTUIUJUREQZVQVIVTWDWGVPVHAURED
      BUKZUGWGVRWAVSWCWGVPVHVABWHUHWGVCWBDBUREVABTULUIUJVAFQZWDVMVIWIWAVJWCVLVA
      FVHBUKWIWBVKDBVAFEBUKULUIUMUNUO $.
      $( [14-Aug-2011] $)

    $( Closure of the operation of a group. $)
    grpcl $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) -> ( X P Y ) e. B ) $=
      ( cgrp wcel co w3a 3anass anidm anbi2i bitri wceq grplem1 simpld sylan2br
      wa 3impb ) CHIZDAIZEAIZDEBJZAIZUCUDTZUBUCUDUDKZUFUHUCUDUDTZTUGUCUDUDLUIUD
      UCUDMNOUBUHTUFUEEBJDEEBJBJPABCDEEFGQRSUA $.
      $( [14-Aug-2011] $)

    $( A group operation is associative. $)
    grpass $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
              ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) $=
      ( cgrp wcel w3a wa co wceq grplem1 simprd ) CIJDAJEAJFAJKLDEBMZAJQFBMDEFB
      MBMNABCDEFGHOP $.
      $( [14-Aug-2011] $)

    $d u x y B $.  $d u x y P $.  $d a b c f u x y $.
    $( A group has a left identity element, and every member has a left
       inverse. $)
    grplidinv $p |- ( G e. Grp ->
         E. u e. B A. x e. B ( ( u P x ) = x /\ E. y e. B ( y P x ) = u ) ) $=
      ( vb vc cgrp wcel cvv cv co wceq wa wral wrex isgrp simp3bi ) FKLFMLANZIN
      ZEOZDLUDJNZEOUBUCUEEOEOPQJDRIDRADRCNZUBEOUBPBNUBEOUFPBDSQADRCDSDECBFAIJGH
      TUA $.
      $( [16-Aug-2011] $)

    $( Lemma for ~ grpidinv . $)
    grpidinvlem1 $p |- ( ( ( G e. Grp /\ ( Y e. B /\ A e. B ) ) /\
                  ( ( Y P A ) = U /\ ( A P A ) = A ) ) -> ( U P A ) = U ) $=
      ( cgrp wcel wa co wceq opreq1 ad2antrl w3a id 3anidm23 grpass sylan2
      adantr eqtr3d opreq2 ad2antll simprl 3eqtrd ) EIJZFBJZABJZKZKZFACLZDMZAAC
      LZAMZKZKZDACLZFUNCLZULDUQULACLZURUSUMUTURMUKUOULDACNOUKUTUSMZUPUJUGUHUIUI
      PZVAUHUIVBVBQRBCEFAAGHSTUAUBUOUSULMUKUMUNAFCUCUDUKUMUOUEUF $.
      $( [16-Aug-2011] $)

    $( Lemma for ~ grpidinv . $)
    grpidinvlem2 $p |- ( ( ( G e. Grp /\ ( Y e. B /\ A e. B ) ) /\ ( (
 U P Y ) = Y /\ ( Y P A ) = U ) ) -> ( ( A P Y ) P ( A P Y ) ) = ( A P Y ) ) $=
      ( cgrp wcel wa co wceq w3a simprr simprl grpcl 3com23 3expb grpass syldan
      3jca adantr opreq1 adantl simpl eqtr2d id sylan2 sylan9eqr eqcomd opreq2d
      3anidm13 eqtrd ) EIJZFBJZABJZKZKZDFCLZFMZFACLZDMZKZKZAFCLZVFCLZAFVFCLZCLZ
      VFUSVGVIMZVDUOURUQUPVFBJZNVJUSUQUPVKUOUPUQOUOUPUQPUOUPUQVKUOUQUPVKBCEAFGH
      QRSUBBCEAFVFGHTUAUCVEVHFACVEFVHVDUSFVBFCLZVHVDVLUTFVCVLUTMVAVBDFCUDUEVAVC
      UFUGURUOUPUQUPNZVLVHMUPUQVMVMUHUMBCEFAFGHTUIUJUKULUN $.
      $( [16-Aug-2011] $)

    ${
      $d w x y A $.  $d w z B $.  $d w y G $.  $d w z P $.  $d w x y z U $.
      $d w y ph $.  $d y ps $.
      grpidinvlem3.ph $e |- ( ph <-> A. x e. B ( U P x ) = x ) $.
      grpidinvlem3.ps $e |- ( ps <-> A. x e. B E. z e. B ( z P x ) = U ) $.
      $( Lemma for ~ grpidinv . $)
      grpidinvlem3 $p |- ( ( ( ( G e. Grp /\ U e. B ) /\ ( ph /\ ps ) )
               /\ A e. B ) -> E. y e. B ( ( y P A ) = U /\ ( A P y ) = U ) ) $=
        ( vw wcel wa co wceq wrex cgrp wral opreq1 eqeq1d cbvrexv ralbii opreq2
        cv bitri rexbidv rcla4cva sylanb adantll grpcl adantllr biimpi ad2antrl
        3expa ad2antrr id eqeq12d rcla4va syl11anc adantr pm3.22 an31s adantlll
        adantlr anim1i grpidinvlem2 wi 3expb ad2ant2rl sylan2b anass syldan imp
        an32s ex grpidinvlem1 sylan exp43 rexlimdv syl5 mpand exp32 com34 imp32
        exp3a imp31 mpd eqtr3d ancld reximdva ) JUAPZIGPZQZABQZQZFGPZQZDUHZFHRZ
        ISZDGTZXDFXBHRZISZQZDGTWRWTXEWQBWTXEABXBCUHZHRZISZDGTZCGUBZWTXEBEUHZXIH
        RZISZEGTZCGUBZXMNXQXLCGXPXKEDGXNXBSXOXJIXNXBXIHUCUDUEUFUIXLXECFGXIFSZXK
        XDDGXSXJXCIXIFXBHUGUDUJUKULUMUMXAXDXHDGXAXBGPZQZXDXGYAXDXGYAXDQZIXFHRZX
        FIYAYCXFSZXDYAXFGPZIXIHRZXISZCGUBZYDWQWTXTYEWRWOWTXTYEWPWOWTXTYEGHJFXBK
        LUNZURUOUOWSYHWTXTAYHWQBAYHMUPUQUSYGYDCXFGXIXFSZYFYCXIXFXIXFIHUGYJUTVAV
        BVCVDYBXFXFHRXFSZYCISZYBWOXTWTQZQZIXBHRZXBSZXDQYKYAYNXDWQWTXTYNWRWOWTXT
        YNWPXTWTWOYNYMWOVEVFUOUOVDYAYPXDWRWTXTYPWQWRXTYPWTAXTYPBAYHXTYPMYGYPCXB
        GXIXBSZYFYOXIXBXIXBIHUGYQUTVAUKULVHVHVGVIFGHIJXBKLVJVCYAYKYLVKZXDWSWTXT
        YRWSWTXTYRWQABWTXTQZYRVKWQAYSBYRWQAYSBYRVKWQAYSQQZYEBYRWOYSYEWPAWOWTXTY
        EYIVLZVMYEBQOUHZXFHRZISZOGTZYTYRBYEUUBXIHRZISZOGTZCGUBZUUEBXRUUINXQUUHC
        GXPUUGEOGXNUUBSXOUUFIXNUUBXIHUCUDUEUFUIUUHUUECXFGYJUUGUUDOGYJUUFUUCIXIX
        FUUBHUGUDUJVBVNYTUUDYROGYTUUBGPZUUDYKYLYTUUJQWOUUJYEQQZUUDYKQYLYTUUJUUK
        WOYSUUJUUKVKZWPAWOYSYEUULUUAWOYEQUUJUUKWOUUJYEUUKWOUUJQYEQUUKWOUUJYEVOU
        PVRVSVPVMVQXFGHIJUUBKLVTWAWBWCWDWEWFWGWHWIWJVDWKWLVSWMWNWK $.
        $( [16-Aug-2011] $)
    $}

    $d y G $.  $d y U $.  $d y X $.
    $( Lemma for ~ grpidinv . $)
    grpidinvlem4 $p |- ( ( ( G e. Grp /\ X e. B ) /\ E. y e. B
             ( ( y P X ) = U /\ ( X P y ) = U ) ) -> ( X P U ) = ( U P X ) ) $=
      ( cgrp wcel wa cv co wceq wrex simpll simplr simpr grpass syl13anc opreq2
      sylan9eq opreq1 sylan9req anasss exp31 rexlimdv imp ) EIJZFBJZKZALZFCMZDN
      ZFULCMZDNZKZABOFDCMZDFCMZNZUKUQUTABUKULBJZUQUTUKVAKZUNUPUTVBUNKUPURUOFCMZ
      USVBUNVCFUMCMZURVBUIUJVAUJVCVDNUIUJVAPUIUJVAQZUKVARVEBCEFULFGHSTUMDFCUAUB
      UODFCUCUDUEUFUGUH $.
      $( [16-Aug-2011] $)

    $d w z B $.  $d u x G $.  $d w z P $.  $d u w x y z $.
    $( A group has a left and right identity element, and every member has a
       left and right inverse. $)
    grpidinv $p |- ( G e. Grp -> E. u e. B A. x e. B ( ( ( u P x ) = x /\
         ( x P u ) = x ) /\ E. y e. B ( ( y P x ) = u /\ ( x P y ) = u ) ) ) $=
      ( vz vw wcel cv co wceq wrex wa wral simpl ralimi id cgrp opreq2 rcla4cva
      grplidinv eqeq12d sylan adantll anim1i adantr adantl ad2antlr simpr jca32
      adantrr grpidinvlem3 sylancom grpidinvlem4 syl11anc eqtrd jca31 ralrimiva
      biid exp32 reximdvai mpd ) FUAKZCLZILZEMZVHNZJLVHEMVGNJDOZPZIDQZCDOVGALZE
      MZVNNZVNVGEMZVNNZPBLZVNEMVGNVNVSEMVGNPBDOZPZADQZCDOIJCDEFGHUDVFVMWBCDVFVG
      DKZVMWBVFWCVMPZPZWAADWEVNDKZPZVPVRVTWDWFVPVFVMWFVPWCVMVJIDQZWFVPVLVJIDVJV
      KRSZVJVPIVNDVHVNNZVIVOVHVNVHVNVGEUBWJTUEUCUFUGUGZWGVQVOVNWGVFWFPVTVQVONWE
      VFWFVFWDRUHWEWFVFWCPZWHVKIDQZPPVTWGWLWHWMWEWLWFVFWCWLVMWLTUNUIWDWHVFWFVMW
      HWCWIUJUKWDWMVFWFVMWMWCVLVKIDVJVKULSUJUKUMWHWMIBJVNDEVGFGHWHVBWMVBUOUPZBD
      EVGFVNGHUQURWKUSWNUTVAVCVDVE $.
      $( [16-Aug-2011] $)

    $d w G $.  $d z G $.
    $( The left identity element of a group is unique.  Lemma 2.2.1(a) of
       [Herstein] p. 55. $)
    grpideu $p |- ( G e. Grp -> E! u e. B A. x e. B ( u P x ) = x ) $=
      ( vw vz vy wcel cv co wceq wral wa wrex opreq2 id eqeq12d grpidinv simpll
      cgrp wi ralimi cbvralv sylib adantl ad2antlr simpr eqeq1d anbi12d rexbidv
      wreu opreq1 rcla4va adantll sylan2 grpidinvlem4 syldan adantllr ad2ant2rl
      an32s adantr ad2ant2lr 3eqtr3d ex ralrimiva jca reximdva mpd ralbidv reu8
      mpand sylibr ) EUCKZBLZALZDMZVRNZACOZHLZVRDMZVRNZACOZVQWBNZUDZHCOZPZBCQZW
      ABCUNVPVQILZDMZWKNZWKVQDMWKNZPZJLZWKDMZVQNZWKWPDMZVQNZPZJCQZPZICOZBCQWJIJ
      BCDEFGUAVPXDWIBCVPVQCKZPZXDWIXFXDPZWAWHXDWAXFXDWMICOWAXCWMICWMWNXBUBUEWMV
      TIACWKVRNZWLVSWKVRWKVRVQDRXHSTUFUGZUHXGWGHCXGWBCKZPZWAWEWFXDWAXFXJXIUIXKW
      AWEPZWFXKXLPWBVQDMZVQWBDMZVQWBXKXMXNNZXLVPXDXJXOXEVPXJXDXOVPXJPZXDWPWBDMZ
      VQNZWBWPDMZVQNZPZJCQZXOXDXPXBICOZYBXCXBICWOXBUJUEXJYCYBVPXBYBIWBCWKWBNZXA
      YAJCYDWRXRWTXTYDWQXQVQWKWBWPDRUKYDWSXSVQWKWBWPDUOUKULUMUPUQURJCDVQEWBFGUS
      UTVCVAVDXFXJXLXMVQNZXDXFWEYEXJWAXEWEYEVPWDYEAVQCVRVQNZWCXMVRVQVRVQWBDRYFS
      TUPUQVBVAXJWAXNWBNZXGWEVTYGAWBCVRWBNZVSXNVRWBVRWBVQDRYHSTUPVEVFVGVNVHVIVG
      VJVKWAWEBHCWFVTWDACWFVSWCVRVQWBVRDUOUKVLVMVO $.
      $( [16-Aug-2011] $)
  $}

  ${
    $d u x y B $.  $d u x y G $.
    grpn0.b $e |- B = ( Base ` G ) $.
    $( The base set of a group is not empty.  (Contributed by Szymon
       Jaroszewicz, 3-Apr-2007.) $)
    grpn0 $p |- ( G e. Grp -> B =/= (/) ) $=
      ( vu vx vy cgrp wcel cv cplusg cfv co wceq wrex wa wral c0 eqid grplidinv
      wne rexn0 syl ) BGHDIZEIZBJKZLUDMFIUDUELUCMFANOEAPZDANAQTEFDAUEBCUERSUFDA
      UAUB $.
      $( [18-Oct-2012] $)
  $}

  ${
    $d e m x y z B $.  $d e m x y z G $.  $d m N $.  $d e m P $.  $d e m x Z $.
    $d e m x y z ph $.
    isgrpd.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isgrpd.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isgrpd.g $e |- ( ph -> G e. _V ) $.
    isgrpd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpd.z $e |- ( ph -> Z e. B ) $.
    isgrpd.i $e |- ( ( ph /\ x e. B ) -> ( Z P x ) = x ) $.
    isgrpd.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrpd.j $e |- ( ( ph /\ x e. B ) -> ( N P x ) = Z ) $.
    $( Deduce a group from its properties. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpd $p |- ( ph -> G e. Grp ) $=
      ( co wceq ve vm cvv wcel cv cplusg cfv cbs wa wral wrex cgrp wi 3adant3r3
      w3a jca 3exp2 ralrimdv ralrimivv opreq1 eqeq1d rcla4ev syl11anc ralrimiva
      imp3a eqeq2 rexbidv anbi12d ralbidv 3jca opreqd eleq12d opreq123d eqeq12d
      eqidd raleqbidv rexeqbidv 3anbi23d mpbid eqid isgrp sylibr ) AGUCUDZBUEZC
      UEZGUFUGZSZGUHUGZUDZWGDUEZWFSZWDWEWJWFSZWFSZTZUIZDWHUJZCWHUJZBWHUJZUAUEZW
      DWFSZWDTZUBUEZWDWFSZWSTZUBWHUKZUIZBWHUJZUAWHUKZUOZGULUDAWCWDWEFSZEUDZXJWJ
      FSZWDWEWJFSZFSZTZUIZDEUJZCEUJZBEUJZWSWDFSZWDTZXBWDFSZWSTZUBEUKZUIZBEUJZUA
      EUKZUOXIAWCXSYGLAXQBCEEAWDEUDZWEEUDZUIXPDEAYHYIWJEUDZXPUMAYHYIYJXPAYHYIYJ
      UOUIXKXOAYHYIXKYJMUNNUPUQVEURUSAIEUDIWDFSZWDTZYBITZUBEUKZUIZBEUJZYGOAYOBE
      AYHUIZYLYNPYQHEUDHWDFSZITZYNQRYMYSUBHEXBHTYBYRIXBHWDFUTVAVBVCUPVDYFYPUAIE
      WSITZYEYOBEYTYAYLYDYNYTXTYKWDWSIWDFUTVAYTYCYMUBEWSIYBVFVGVHVIVBVCVJAXSWRY
      GXHWCAXRWQBEWHJAXQWPCEWHJAXPWODEWHJAXKWIXOWNAXJWGEWHAFWFWDWEKVKZJVLAXLWKX
      NWMAXJWGWJWJFWFKUUAAWJVOVMAWDWDXMWLFWFKAWDVOAFWFWEWJKVKVMVNVHVPVPVPAYFXGU
      AEWHJAYEXFBEWHJAYAXAYDXEAXTWTWDAFWFWSWDKVKVAAYCXDUBEWHJAYBXCWSAFWFXBWDKVK
      VAVQVHVPVQVRVSWHWFUAUBGBCDWHVTWFVTWAWB $.
      $( [6-Jun-2013] $)
  $}

  ${
    $d x y z B $.  $d x y z G $.  $d y z N $.  $d x O $.  $d x y z P $.
    $d x y z ph $.
    isgrprd.b $e |- ( ph -> B = ( Base ` G ) ) $.
    isgrprd.p $e |- ( ph -> P = ( +g ` G ) ) $.
    isgrprd.g $e |- ( ph -> G e. _V ) $.
    isgrprd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrprd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrprd.z $e |- ( ph -> O e. B ) $.
    isgrprd.i $e |- ( ( ph /\ x e. B ) -> ( x P O ) = x ) $.
    isgrprd.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrprd.j $e |- ( ( ph /\ x e. B ) -> ( x P N ) = O ) $.
    $( Deduce a group from its properties.  Same as ~ isgrpd except with right
       identity and inverse. ` N ` (negative) is normally dependent on ` x `
       i.e. read it as ` N ( x ) ` . $)
    isgrprd $p |- ( ph -> G e. Grp ) $=
      ( grplidd grplinvd isgrpd ) ABCDEFGHIJKLMNOABCDEFHIMOPNQRSQABCDEFHIMOPNQR
      TUA $.
      $( [10-Aug-2013] $)
  $}

  ${
    $d e m x y z B $.  $d m N $.  $d e m x y z P $.  $d e m x Z $.
    $d x y z ph $.
    isgrpdOLD.b $e |- B = ( Base ` G ) $.
    isgrpdOLD.p $e |- P = ( +g ` G ) $.
    isgrpdOLD.g $e |- ( ph -> G e. _V ) $.
    isgrpdOLD.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpdOLD.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpdOLD.z $e |- ( ph -> Z e. B ) $.
    isgrpdOLD.i $e |- ( ( ph /\ x e. B ) -> ( Z P x ) = x ) $.
    isgrpdOLD.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    isgrpdOLD.j $e |- ( ( ph /\ x e. B ) -> ( N P x ) = Z ) $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpdOLD $p |- ( ph -> G e. Grp ) $=
      ( wcel co ve vm cvv cv wceq wa wral wrex w3a wi 3adant3r3 jca 3exp2 imp3a
      ralrimdv ralrimivv opreq1 eqeq1d rcla4ev syl11anc ralrimiva eqeq2 rexbidv
      cgrp anbi12d ralbidv 3jca isgrp sylibr ) AGUCSZBUDZCUDZFTZESZVMDUDZFTVKVL
      VOFTFTUEZUFZDEUGZCEUGBEUGZUAUDZVKFTZVKUEZUBUDZVKFTZVTUEZUBEUHZUFZBEUGZUAE
      UHZUIGVDSAVJVSWILAVRBCEEAVKESZVLESZUFVQDEAWJWKVOESZVQUJAWJWKWLVQAWJWKWLUI
      UFVNVPAWJWKVNWLMUKNULUMUNUOUPAIESIVKFTZVKUEZWDIUEZUBEUHZUFZBEUGZWIOAWQBEA
      WJUFZWNWPPWSHESHVKFTZIUEZWPQRWOXAUBHEWCHUEWDWTIWCHVKFUQURUSUTULVAWHWRUAIE
      VTIUEZWGWQBEXBWBWNWFWPXBWAWMVKVTIVKFUQURXBWEWOUBEVTIWDVBVCVEVFUSUTVGEFUAU
      BGBCDJKVHVI $.
      $( [5-Jun-2013] $)
  $}

  ${
    $d e m x y z B $.  $d m N $.  $d e m x y z P $.  $d e m x Z $.
    isgrpi.g $e |- G e. _V $.
    isgrpi.b $e |- B = ( Base ` G ) $.
    isgrpi.p $e |- P = ( +g ` G ) $.
    isgrpi.c $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpi.a $e |- ( ( x e. B /\ y e. B /\ z e. B ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpi.z $e |- Z e. B $.
    isgrpi.i $e |- ( x e. B -> ( Z P x ) = x ) $.
    isgrpi.n $e |- ( x e. B -> N e. B ) $.
    isgrpi.j $e |- ( x e. B -> ( N P x ) = Z ) $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` .  OBSOLETE PROOF. $)
    isgrpiOLD $p |- G e. Grp $=
      ( wcel co wceq ve vm cgrp cvv cv wa wral wrex isgrp w3a 3adant3 jca rgen3
      opreq1 rcla4ev syl11anc rgen eqeq2 rexbidv anbi12d ralbidv mp2an mpbir3an
      eqeq1d ) FUCRFUDRAUEZBUEZESZDRZVGCUEZESVEVFVIESESTZUFZCDUGBDUGADUGUAUEZVE
      ESZVETZUBUEZVEESZVLTZUBDUHZUFZADUGZUADUHZDEUAUBFABCJKUIIVKABCDDDVEDRZVFDR
      ZVIDRZUJVHVJWBWCVHWDLUKMULUMHDRHVEESZVETZVPHTZUBDUHZUFZADUGZWANWIADWBWFWH
      OWBGDRGVEESZHTZWHPQWGWLUBGDVOGTVPWKHVOGVEEUNVDUOUPULUQVTWJUAHDVLHTZVSWIAD
      WMVNWFVRWHWMVMWEVEVLHVEEUNVDWMVQWGUBDVLHVPURUSUTVAUOVBVC $.
      $( [3-Sep-2011] $)

    $d x y z G $.
    $( Properties that determine a group. ` N ` (negative) is normally
       dependent on ` x ` i.e. read it as ` N ( x ) ` . $)
    isgrpi $p |- G e. Grp $=
      ( wcel wtru co cgrp cbs cfv wceq a1i cplusg cvv 3adant1 w3a adantl isgrpd
      cv trud ) FUARSABCDEFGHDFUBUCUDSJUEEFUFUCUDSKUEFUGRSIUEAULZDRZBULZDRZUNUP
      ETZDRSLUHUOUQCULZDRUIURUSETUNUPUSETETUDSMUJHDRSNUEUOHUNETUNUDSOUJUOGDRSPU
      JUOGUNETHUDSQUJUKUM $.
      $( [3-Sep-2011] $)
  $}

  ${
    $d x y z B $.  $d x y z g G $.  $d x y z P $.  $d x Z $.
    isgrpix.a $e |- B e. _V $.
    isgrpix.b $e |- P e. _V $.
    isgrpix.g $e |- G = { <. 1 , B >. , <. 2 , P >. } $.
    isgrpix.2 $e |- ( ( x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    isgrpix.3 $e |- ( ( x e. B /\ y e. B /\ z e. B ) ->
                   ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    isgrpix.z $e |- Z e. B $.
    isgrpix.5 $e |- ( x e. B -> ( Z P x ) = x ) $.
    isgrpix.6 $e |- ( x e. B -> N e. B ) $.
    isgrpix.7 $e |- ( x e. B -> ( N P x ) = Z ) $.
    $( Properties that determine a group.  Read ` N ` as ` N ( x ) ` .  Note:
       This theorem has hard-coded structure indices for demonstration
       purposes.  It is not intended for general use. $)
    isgrpix $p |- G e. Grp $=
      ( c1 cop c2 cpr cvv prex eqeltri grpbasex grpplusgx isgrpi ) ABCDEFGHFRDS
      ZTESZUAUBKUHUIUCUDDEFIJKUEDEFIJKUFLMNOPQUG $.
      $( [4-Sep-2011] $)
  $}

  ${
    $d e g x B $.  $d e g x G $.  $d g P $.
    grpidval.b $e |- B = ( Base ` G ) $.
    grpidval.p $e |- P = ( +g ` G ) $.
    grpidval.o $e |- O = ( 0g ` G ) $.
    $( The value of the identity element of a group. $)
    grpidval $p |- ( G e. V
  -> O = ( iota_ e e. B A. x e. B ( e P x ) = x ) ) $=
      ( vg wcel cvv cv co wceq wral cfv cplusg cbs elisset fveq2 syl6eqr opreqd
      crio c0g eqeq1d raleqbidv riotaeqbidv df-0g riotaex fvmpt syl5eq syl ) EG
      LEMLZFDNZANZCOZUQPZABQZDBUEZPEGUAUOFEUFRVAJKEUPUQKNZSRZOZUQPZAVBTRZQZDVFU
      EVAMUFVBEPZVGUTDVFBVHVFETRBVBETUBHUCZVHVEUSAVFBVIVHVDURUQVHVCCUPUQVHVCESR
      CVBESUBIUCUDUGUHUIADKUJUTDBUKULUMUN $.
      $( [7-Aug-2013] $) $( [20-Aug-2011] $)
  $}

  ${
    $d o x B $.  $d o x G $.
    grpidcl.b $e |- B = ( Base ` G ) $.
    grpidcl.o $e |- O = ( 0g ` G ) $.
    $( The identity element of a group belongs to the group. $)
    grpidcl $p |- ( G e. Grp -> O e. B ) $=
      ( vo vx cgrp wcel cv cplusg cfv co wceq wral wreu eqid grpideu wa crio
      grpidval adantr riotacl adantl eqeltrd mpdan ) BHIZFJGJZBKLZMUHNGAOZFAPZC
      AIGFAUIBDUIQZRUGUKSCUJFATZAUGCUMNUKGAUIFBCHDULEUAUBUKUMAIUGUJFAUCUDUEUF
      $.
      $( [27-Aug-2011] $)
  $}

  ${
    $d x y X $.  $d u w x y B $.  $d u x y G $.  $d u w x y P $.
    $d u w x y O $.
    grpidinv2.b $e |- B = ( Base ` G ) $.
    grpidinv2.p $e |- P = ( +g ` G ) $.
    grpidinv2.o $e |- O = ( 0g ` G ) $.
    $( X group's properties using the explicit identity element. $)
    grpidinv2 $p |- ( ( G e. Grp /\ X e. B ) -> ( ( ( O P X ) = X /\
         ( X P O ) = X ) /\ E. y e. B ( ( y P X ) = O /\ ( X P y ) = O ) ) ) $=
      ( vx vu wcel cv co wceq wa wrex wral eqeq1d anbi12d vw cgrp crio grpidinv
      wreu grpideu wss wi ssid simpll ralimi a1i rgen riotass2 mpanl12 syl11anc
      grpidval eqtr4d wb grpidcl reuss2 ax-17 a17d opreq1 eqeq2 rexbidv ralbidv
      opreq2 riota2f mpbird id eqeq12d rcla4cva sylan ) DUBLZEJMZCNZVPOZVPECNZV
      POZPZAMZVPCNZEOZVPWBCNZEOZPZABQZPZJBRZFBLEFCNZFOZFECNZFOZPZWBFCNZEOZFWBCN
      ZEOZPZABQZPZVOWJKMZVPCNZVPOZVPXCCNZVPOZPZWCXCOZWEXCOZPZABQZPZJBRZKBUCZEOZ
      VOXOXEJBRZKBUCZEVOXNKBQZXQKBUEZXOXROZJAKBCDGHUDZJKBCDGHUFZBBUGZXNXQUHZKBR
      ZXSXTPZYABUIZYEKBYEXCBLXMXEJBXEXGXLUJUKULUMZXNXQKBBUNUOUPJBCKDEUBGHIUQURV
      OEBLZXNKBUEZWJXPUSBDEGIUTVOXSXTYKYBYCYDYFYGYKYHYIXNXQKBBVAUOUPXNWJKUABEUA
      MELKVBYJWJKVCXCEOZXMWIJBYLXHWAXLWHYLXEVRXGVTYLXDVQVPXCEVPCVDSYLXFVSVPXCEV
      PCVHSTYLXKWGABYLXIWDXJWFXCEWCVEXCEWEVETVFTVGVIUPVJWIXBJFBVPFOZWAWOWHXAYMV
      RWLVTWNYMVQWKVPFVPFECVHYMVKZVLYMVSWMVPFVPFECVDYNVLTYMWGWTABYMWDWQWFWSYMWC
      WPEVPFWBCVHSYMWEWREVPFWBCVDSTVFTVMVN $.
      $( [18-Aug-2011] $)

    $( The identity element of a group is a left identity. $)
    grplid $p |- ( ( G e. Grp /\ X e. B ) -> ( O P X ) = X ) $=
      ( vy cgrp wcel wa co wceq cv wrex grpidinv2 simpll syl ) CJKEAKLDEBMENZED
      BMENZLIOZEBMDNEUBBMDNLIAPZLTIABCDEFGHQTUAUCRS $.
      $( [18-Aug-2011] $)

    $( The identity element of a group is a right identity. $)
    grprid $p |- ( ( G e. Grp /\ X e. B ) -> ( X P O ) = X ) $=
      ( vy cgrp wcel wa co wceq cv wrex grpidinv2 simplr syl ) CJKEAKLDEBMENZED
      BMENZLIOZEBMDNEUBBMDNLIAPZLUAIABCDEFGHQTUAUCRS $.
      $( [18-Aug-2011] $)

    $d e B $.  $d e G $.  $d e P $.  $d e x w Z $.
    $( Properties showing that an element ` Z ` is the identity element of a
       group.  See also ~ isgrpid2 . $)
    isgrpid $p |- ( G e. Grp
  -> ( ( Z e. B /\ A. x e. B ( Z P x ) = x ) <-> O = Z ) ) $=
      ( ve vw cgrp wcel cv co wceq wral wa opreq1 eqeq1d crio adantr wb grpideu
      grpidval wreu anim2i ancoms ax-17 a17d ralbidv riota2f syl biimpd impr ex
      eqtrd grpidcl eleq1 syl5ibcom grplid ralrimiva jcad impbid ) DLMZFBMZFANZ
      COZVGPZABQZRZEFPZVEVKVLVEVKREJNZVGCOZVGPZABQZJBUAZFVEEVQPVKABCJDELGHIUEUB
      VEVFVJVQFPZVEVFRZVJVRVSVFVPJBUFZRZVJVRUCVFVEWAVEVTVFAJBCDGHUDUGUHVPVJJKBF
      KNFMJUIVFVJJUJVMFPZVOVIABWBVNVHVGVMFVGCSTUKULUMUNUOUQUPVEVLVFVJVEEBMVLVFB
      DEGIUREFBUSUTVEEVGCOZVGPZABQVLVJVEWDABBCDEVGGHIVAVBVLWDVIABVLWCVHVGEFVGCS
      TUKUTVCVD $.
      $( [7-Aug-2013] $)
  $}

  ${
    $d y B $.  $d y G $.  $d y P $.  $d y X $.  $d y Y $.  $d y Z $.
    grprcan.b $e |- B = ( Base ` G ) $.
    grprcan.p $e |- P = ( +g ` G ) $.
    $( Right cancellation law for groups. $)
    grprcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( X P Z ) = ( Y P Z ) <-> X = Y ) ) $=
      ( vy wcel co wceq wa wrex adantl opreq1 grpass 3exp2 adantrr 3eqtr3d cgrp
      wb cv c0g cfv wi eqid grpidinv2 simpr reximi syl ad2ant2rl ad2antll imp41
      adantlrl imp42 adantllr adantrrl opreq2 ad2antrl grprid ad2antrr ad2ant2r
      adantr exp45 rexlimdv mpd impbid1 exp43 3imp2 ) CUAJZDAJZEAJZFAJZDFBKZEFB
      KZLZDELZUBZVKVLVMVNVSVKVLMZVMVNMZMZVQVRWBFIUCZBKZCUDUEZLZIANZVQVRUFZVKVNW
      GVLVMVKVNMWEFBKFLFWEBKFLMZWCFBKWELZWFMZIANZMWGIABCWEFGHWEUGZUHWLWGWIWKWFI
      AWJWFUIUJOUKULWBWFWHIAWBWCAJZWFVQVRWBWNWFVQMMZMZDWEBKZEWEBKZDEWPDWDBKZEWD
      BKZWQWRWBWNVQWSWTLWFWBWNVQMMVOWCBKZVPWCBKZWSWTVQXAXBLWBWNVOVPWCBPUMWBWNXA
      WSLZVQVTVNWNXCVMVKVLVNWNXCVKVLVNWNXCABCDFWCGHQRUNUOSWBWNXBWTLZVQVKWAWNXDV
      LVKVMVNWNXDVKVMVNWNXDABCEFWCGHQRUPUQSTURWOWSWQLZWBWFXEWNVQWDWEDBUSUTOWOWT
      WRLZWBWFXFWNVQWDWEEBUSUTOTVTWQDLWAWOABCWEDGHWMVAVBWBWRELZWOVKVMXGVLVNABCW
      EEGHWMVAVCVDTVEVFVGDEFBPVHVIVJ $.
      $( [24-Aug-2011] $)
  $}

  ${
    $d y z B $.  $d y z G $.  $d y z P $.  $d y z O $.  $d y z X $.
    grpinveu.b $e |- B = ( Base ` G ) $.
    grpinveu.p $e |- P = ( +g ` G ) $.
    grpinveu.o $e |- O = ( 0g ` G ) $.
    $( The left inverse element of a group is unique.  Lemma 2.2.1(b) of
       [Herstein] p. 55. $)
    grpinveu $p |- ( ( G e. Grp /\ X e. B )
           -> E! y e. B ( y P X ) = O ) $=
      ( vz cgrp wcel wa cv co wceq wi wral wrex wreu grpidinv2 simpl reximi syl
      adantl eqtr3 grprcan syl5ib 3exp2 com24 imp41 an32s exp3a ralrimdva ancld
      w3a reximdva mpd opreq1 eqeq1d reu8 sylibr ) DKLZFBLZMZANZFCOZEPZJNZFCOZE
      PZVFVIPZQZJBRZMZABSZVHABTVEVHABSZVPVEEFCOFPFECOFPMZVHFVFCOEPZMZABSZMVQABC
      DEFGHIUAWAVQVRVTVHABVHVSUBUCUEUDVEVHVOABVEVFBLZMZVHVNWCVHVMJBWCVIBLZMVHVK
      VLVEWDWBVHVKMZVLQZVCVDWDWBWFVCWBWDVDWFVCWBWDVDWFWEVGVJPVCWBWDVDUPMVLVGVJE
      UFBCDVFVIFGHUGUHUIUJUKULUMUNUOUQURVHVKAJBVLVGVJEVFVIFCUSUTVAVB $.
      $( [24-Aug-2011] $)

    $( Two ways of saying that an element of a group is the identity element.
       Provides a convenient way to compute the value of the identity
       element. $)
    grpid $p |- ( ( G e. Grp /\ X e. B ) ->
                  ( ( X P X ) = X <-> O = X ) ) $=
      ( wceq cgrp wcel wa co eqcom wb wi grpidcl grprcan 3exp2 mpid pm2.43d imp
      grplid eqeq2d bitr3d syl5rbb ) DEIEDIZCJKZEAKZLZEEBMZEIZDENUJUKDEBMZIZUGU
      LUHUIUNUGOZUHUIUOUHUIDAKZUIUOPACDFHQUHUIUPUIUOABCEDEFGRSTUAUBUJUMEUKABCDE
      FGHUCUDUEUF $.
      $( [24-Aug-2011] $)

    $( Obsolete proof of ~ grpid as of 9-Mar-2014. $)
    grpidOLD $p |- ( ( G e. Grp /\ X e. B ) ->
                  ( X = O <-> ( X P X ) = X ) ) $=
      ( cgrp wcel wa co wceq wb wi grpidcl grprcan 3exp2 mpid pm2.43d grplid
      imp eqeq2d bitr3d ) CIJZEAJZKZEEBLZDEBLZMZEDMZUHEMUEUFUJUKNZUEUFULUEUFDAJ
      ZUFULOACDFHPUEUFUMUFULABCEDEFGQRSTUBUGUIEUHABCDEFGHUAUCUD $.
      $( [24-Aug-2011] $)

    $( Properties showing that an element ` Z ` is the identity element of a
       group. $)
    isgrpid2 $p |- ( G e. Grp
  -> ( ( Z e. B /\ ( Z P Z ) = Z ) <-> O = Z ) ) $=
      ( cgrp wcel co wceq wa grpid biimpd expimpd grpidcl grplid mpdan jca id
      eleq1 opreq12d eqeq12d anbi12d syl5ibcom impbid ) CIJZEAJZEEBKZELZMZDELZU
      HUIUKUMUHUIMUKUMABCDEFGHNOPUHDAJZDDBKZDLZMUMULUHUNUPACDFHQZUHUNUPUQABCDDF
      GHRSTUMUNUIUPUKDEAUBUMUOUJDEUMDEDEBUMUAZURUCURUDUEUFUG $.
      $( [7-Aug-2013] $)
  $}

  ${
    $d g x y B $.  $d g x y G $.  $d g O $.  $d g P $.
    grpinvval.b $e |- B = ( Base ` G ) $.
    grpinvval.p $e |- P = ( +g ` G ) $.
    grpinvval.o $e |- O = ( 0g ` G ) $.
    grpinvval.n $e |- N = ( invg ` G ) $.
    $( The inverse function of a group. $)
    grpinvfval $p |- ( G e. V
              -> N = ( x e. B |-> ( iota_ y e. B ( y P x ) = O ) ) ) $=
      ( vg cvv cv wceq cfv cbs cplusg c0g wcel co elisset cminusg fveq2 syl6eqr
      crio cmpt opreqd eqeq12d riotaeqbidv mpteq12dv df-minusg fvex mptex fvmpt
      eqeltri syl5eq syl ) EHUAENUAZFACBOZAOZDUBZGPZBCUGZUHZPEHUCUTFEUDQVFLMEAM
      OZRQZVAVBVGSQZUBZVGTQZPZBVHUGZUHVFNUDVGEPZAVHVMCVEVNVHERQZCVGERUEIUFZVNVL
      VDBVHCVPVNVJVCVKGVNVIDVAVBVNVIESQDVGESUEJUFUIVNVKETQGVGETUEKUFUJUKULABMUM
      ACVECVONIERUNUQUOUPURUS $.
      $( [7-Aug-2013] $) $( [24-Aug-2011] $)

    $d x O $.  $d x P $.  $d x y X $.
    $( The inverse of a group element. $)
    grpinvval $p |- ( ( G e. V /\ X e. B ) ->
                    ( N ` X ) = ( iota_ y e. B ( y P X ) = O ) ) $=
      ( vx wcel cfv cv co wceq crio cmpt fveq1d opreq2 eqeq1d riotabidv riotaex
      grpinvfval eqid fvmpt sylan9eq ) DGNZHBNHEOHMBAPZMPZCQZFRZABSZTZOUKHCQZFR
      ZABSZUJHEUPMABCDEFGIJKLUFUAMHUOUSBUPULHRZUNURABUTUMUQFULHUKCUBUCUDUPUGURA
      BUEUHUI $.
      $( [7-Aug-2013] $) $( [24-Aug-2011] $)
  $}

  ${
    $d g x y B $.  $d g x y G $.  $d g I $.  $d g P $.
    grpsubval.b $e |- B = ( Base ` G ) $.
    grpsubval.p $e |- P = ( +g ` G ) $.
    grpsubval.i $e |- I = ( invg ` G ) $.
    grpsubval.m $e |- M = ( -g ` G ) $.
    $( Group subtraction (division) operation. $)
    grpsubfval $p |- ( G e. V
              -> M = ( x e. B , y e. B |-> ( x P ( I ` y ) ) ) ) $=
      ( vg cvv cv cfv cbs cminusg cplusg fveq2 wcel co wceq elisset csg syl6eqr
      cmpt2 eqidd fveq1d opreq123d mpt2eq123dv df-sbg fvex eqeltri mpt2ex fvmpt
      syl5eq syl ) EHUAENUAZGABCCAOZBOZFPZDUBZUGZUCEHUDUSGEUEPVDLMEABMOZQPZVFUT
      VAVERPZPZVESPZUBZUGVDNUEVEEUCZABVFVFVJCCVCVKVFEQPZCVEEQTIUFZVMVKUTUTVHVBV
      IDVKVIESPDVEESTJUFVKUTUHVKVAVGFVKVGERPFVEERTKUFUIUJUKABMULABCCVCCVLNIEQUM
      UNZVNUOUPUQUR $.
      $( [31-Mar-2014] $)

    $d x y I $.  $d x y P $.  $d x y X $.  $d y Y $.
    $( Group subtraction (division) operation. $)
    grpsubval $p |- ( ( G e. V /\ X e. B /\ Y e. B ) ->
                    ( X M Y ) = ( X P ( I ` Y ) ) ) $=
      ( vx vy wcel co cfv wceq wa cv cmpt2 grpsubfval opreqd fveq2 opreq2d eqid
      opreq1 oprex ovmpt2 sylan9eq 3impb ) CFOZGAOZHAOZGHEPZGHDQZBPZRULUMUNSUOG
      HMNAAMTZNTZDQZBPZUAZPUQULEVBGHMNABCDEFIJKLUBUCMNGHAAVAUQVBGUTBPURGUTBUGUS
      HRUTUPGBUSHDUDUEVBUFGUPBUHUIUJUK $.
      $( [31-Mar-2014] $)
  $}

  ${
    $d y B $.  $d y G $.  $d y P $.  $d y X $.
    grpinvcl.b $e |- B = ( Base ` G ) $.
    grpinvcl.n $e |- N = ( invg ` G ) $.
    $( A group element's inverse is a group element. $)
    grpinvcl $p |- ( ( G e. Grp /\ X e. B ) -> ( N ` X ) e. B ) $=
      ( vy cgrp wcel wa cfv cv cplusg co c0g wceq crio eqid grpinvval wreu syl
      grpinveu riotacl eqeltrd ) BHIDAIJZDCKGLDBMKZNBOKZPZGAQZAGAUFBCUGHDEUFRZU
      GRZFSUEUHGATUIAIGAUFBUGDEUJUKUBUHGAUCUAUD $.
      $( [24-Aug-2011] $)
  $}

  ${
    $d w y B $.  $d y G $.  $d w y N $.  $d w y P $.  $d w y O $.  $d w y X $.
    grpinv.b $e |- B = ( Base ` G ) $.
    grpinv.p $e |- P = ( +g ` G ) $.
    grpinv.u $e |- O = ( 0g ` G ) $.
    grpinv.n $e |- N = ( invg ` G ) $.
    $( The properties of a group element's inverse. $)
    grpinv $p |- ( ( G e. Grp /\ X e. B ) ->
                 ( ( ( N ` X ) P X ) = O /\ ( X P ( N ` X ) ) = O ) ) $=
      ( vy vw cgrp wcel wa co wceq cv crio syl11anc cfv wrex grpidinv2 grpinveu
      wreu simprd wss wi wral ssid simpl rgen riotass2 mpanl12 grpinvval eqtr4d
      wb grpinvcl reuss2 ax-17 a17d opreq1 eqeq1d opreq2 anbi12d riota2f mpbird
      a1i ) CMNFANOZFDUAZFBPZEQZFVJBPZEQZOZKRZFBPZEQZFVPBPZEQZOZKASZVJQZVIWBVRK
      ASZVJVIWAKAUBZVRKAUEZWBWDQZVIEFBPFQFEBPFQOWEKABCEFGHIUCUFZKABCEFGHIUDZAAU
      GZWAVRUHZKAUIZWEWFOZWGAUJZWKKAWKVPANVRVTUKVHULZWAVRKAAUMUNTKABCDEMFGHIJUO
      UPVIVJANZWAKAUEZVOWCUQACDFGJURVIWEWFWQWHWIWJWLWMWQWNWOWAVRKAAUSUNTWAVOKLA
      VJLRVJNKUTWPVOKVAVPVJQZVRVLVTVNWRVQVKEVPVJFBVBVCWRVSVMEVPVJFBVDVCVEVFTVG
      $.
      $( [24-Aug-2011] $)

    $( The left inverse of a group element. $)
    grplinv $p |- ( ( G e. Grp /\ X e. B ) -> ( ( N ` X ) P X ) = O ) $=
      ( cgrp wcel wa cfv co wceq grpinv simpld ) CKLFALMFDNZFBOEPFSBOEPABCDEFGH
      IJQR $.
      $( [24-Aug-2011] $)

    $( The right inverse of a group element. $)
    grprinv $p |- ( ( G e. Grp /\ X e. B ) -> ( X P ( N ` X ) ) = O ) $=
      ( cgrp wcel wa cfv co wceq grpinv simprd ) CKLFALMFDNZFBOEPFSBOEPABCDEFGH
      IJQR $.
      $( [24-Aug-2011] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid1 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                    ( ( N ` X ) = Y <-> ( X P Y ) = O ) ) $=
      ( wcel w3a wceq co wa opreq2 adantl 3adant3 adantr grprinv eqtr3d grplinv
      cgrp cfv opreq1d grpinvcl adantrr simprl simprr 3jca grpass syldan grplid
      3impb 3adant2 3eqtr3d grprid 3eqtr3rd impbida ) CUDLZFALZGALZMZFDUEZGNZFG
      BOZENZVDVFPFVEBOZVGEVFVIVGNVDVEGFBQRVDVIENZVFVAVBVJVCABCDEFHIJKUASTUBVDVH
      PVEVGBOZVEEBOZGVEVHVKVLNVDVGEVEBQRVDVKGNVHVDVEFBOZGBOZEGBOZVKGVAVBVNVONVC
      VAVBPVMEGBABCDEFHIJKUCUFSVAVBVCVNVKNZVAVBVCPZVEALZVBVCMVPVAVQPVRVBVCVAVBV
      RVCACDFHKUGZUHVAVBVCUIVAVBVCUJUKABCVEFGHIULUMUOVAVCVOGNVBABCEGHIJUNUPUQTV
      DVLVENZVHVAVBVTVCVAVBVRVTVSABCEVEHIJURUMSTUSUT $.
      $( [24-Aug-2011] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid2 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                    ( ( N ` X ) = Y <-> ( Y P X ) = O ) ) $=
      ( wcel w3a wceq co wa opreq1 adantl 3adant3 adantr cgrp cfv eqtr3d grplid
      grplinv grpinvcl syldan eqcomd simprr simprl adantrr grpass 3impb grprinv
      3jca opreq2d grprid 3adant2 3eqtrd 3eqtr2d impbida ) CUALZFALZGALZMZFDUBZ
      GNZGFBOZENZVEVGPVFFBOZVHEVGVJVHNVEVFGFBQRVEVJENZVGVBVCVKVDABCDEFHIJKUESTU
      CVEVIPVFEVFBOZVHVFBOZGVEVFVLNVIVEVLVFVBVCVLVFNZVDVBVCVFALZVNACDFHKUFZABCE
      VFHIJUDUGSUHTVIVMVLNVEVHEVFBQRVEVMGNVIVEVMGFVFBOZBOZGEBOZGVBVCVDVMVRNZVBV
      CVDPZVDVCVOMVTVBWAPVDVCVOVBVCVDUIVBVCVDUJVBVCVOVDVPUKUOABCGFVFHIULUGUMVBV
      CVRVSNVDVBVCPVQEGBABCDEFHIJKUNUPSVBVDVSGNVCABCEGHIJUQURUSTUTVA $.
      $( [24-Aug-2011] $)

    $d e w x B $.  $d e x G $.  $d e w x M $.  $d x N $.  $d e w x O $.
    $d e w x P $.
    $( Properties showing that a function ` M ` is the inverse function of a
       group. $)
    isgrpinv $p |- ( G e. Grp
  -> ( ( M : B --> B /\ A. x e. B ( ( M ` x ) P x ) = O ) <-> N = M ) ) $=
      ( ve cgrp wcel wf cv co wceq wa syl11anc vw wral simplll simplr grpinvval
      cfv crio simpr wreu wb simpllr ffvelrn grpinveu ax-17 a17d opreq1 riota2f
      eqeq1d mpbid eqtrd ralimdva impr wfn cmpt cvv eqid fnmpt riotaex a1i mprg
      ex grpinvfval fneq1d adantr mpbiri ffn ad2antrl eqfnfv2 grpinvcl eqeltrrd
      mpbird fmptd feq1d grplinv ralrimiva feq1 fveq1 opreq1d ralbidv syl5ibcom
      jca anbi12d impbid ) DMNZBBEOZAPZEUFZWPCQZGRZABUBZSZFERZWNXAXBWNXASZXBWPF
      UFZWQRZABUBZWNWOWTXFWNWOSZWSXEABXGWPBNZSZWSXEXIWSSZXDLPZWPCQZGRZLBUGZWQXJ
      WNXHXDXNRWNWOXHWSUCZXGXHWSUDZLBCDFGMWPHIJKUEZTXJWSXNWQRZXIWSUHXJWQBNZXMLB
      UIZWSXRUJXJWOXHXSWNWOXHWSUKXPBBWPEULTXJWNXHXTXOXPLBCDGWPHIJUMTXMWSLUABWQU
      APWQNLUNXSWSLUOXKWQRXLWRGXKWQWPCUPURUQTUSUTVKVAVBXCFBVCZEBVCZXBXFUJXCYAAB
      XNVDZBVCZXNVENZYDABABXNYCVEYCVFZVGYEXHXMLBVHVIVJWNYAYDUJXAWNBFYCALBCDFGMH
      IJKVLZVMVNVOWOYBWNWTBBEVPVQABFEVRTWAVKWNBBFOZXDWPCQZGRZABUBZSXBXAWNYHYKWN
      YHBBYCOWNABXNBYCWNXHXNBNWNXHSXDXNBXQBDFWPHKVSVTVKYFWBWNBBFYCYGWCWAWNYJABB
      CDFGWPHIJKWDWEWKXBYHWOYKWTBBFEWFXBYJWSABXBYIWRGXBXDWQWPCWPFEWGWHURWIWLWJW
      M $.
      $( [7-Aug-2013] $)
  $}

  ${
    grpinvid.u $e |- O = ( 0g ` G ) $.
    grpinvid.n $e |- N = ( invg ` G ) $.
    $( The inverse of the identity element of a group. $)
    grpinvid $p |- ( G e. Grp -> ( N ` O ) = O ) $=
      ( cgrp wcel cfv wceq cplusg co cbs eqid grpidcl grplid mpdan wb grpinvid1
      mpd3an23 mpbird ) AFGZCBHCIZCCAJHZKCIZUACALHZGZUDUEACUEMZDNZUEUCACCUGUCMZ
      DOPUAUFUFUBUDQUHUHUEUCABCCCUGUIDERST $.
      $( [24-Aug-2011] $)
  $}

  ${
    grplcan.b $e |- B = ( Base ` G ) $.
    grplcan.p $e |- P = ( +g ` G ) $.
    $( Left cancellation law for groups. $)
    grplcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( Z P X ) = ( Z P Y ) <-> X = Y ) ) $=
      ( wcel w3a wa co wceq cfv opreq2 eqid adantlr adantrl adantr 3eqtr3d cgrp
      wi cminusg adantl c0g grplinv opreq1d grpinvcl simprr simprl 3jca anassrs
      grpass syldan grplid adantrr exp53 3imp2 impbid1 ) CUAIZDAIZEAIZFAIZJKFDB
      LZFEBLZMZDEMZUTVAVBVCVFVGUBUTVAVBVCVFVGUTVAKZVBVCKZKZVFKFCUCNZNZVDBLZVLVE
      BLZDEVFVMVNMVJVDVEVLBOUDVJVMDMZVFVHVCVOVBVHVCKZVLFBLZDBLZCUENZDBLZVMDVPVQ
      VSDBUTVCVQVSMZVAABCVKVSFGHVSPZVKPZUFZQUGUTVAVCVRVMMZUTVAVCKZVLAIZVCVAJWEU
      TWFKWGVCVAUTVCWGVAACVKFGWCUHZRUTVAVCUIUTVAVCUJUKABCVLFDGHUMUNULVHVTDMVCAB
      CVSDGHWBUOSTRSVJVNEMZVFUTVIWIVAUTVIKZVQEBLZVSEBLZVNEWJVQVSEBUTVCWAVBWDRUG
      UTVIWGVCVBJWKVNMWJWGVCVBUTVCWGVBWHRUTVBVCUIUTVBVCUJUKABCVLFEGHUMUNUTVBWLE
      MVCABCVSEGHWBUOUPTQSTUQURDEFBOUS $.
      $( [25-Aug-2011] $)
  $}


  ${
    grpinvinv.b $e |- B = ( Base ` G ) $.
    grpinvinv.n $e |- N = ( invg ` G ) $.
    $( Double inverse law for groups.  Lemma 2.2.1(c) of [Herstein] p. 55. $)
    grpinvinv $p |- ( ( G e. Grp /\ X e. B ) -> ( N ` ( N ` X ) ) = X ) $=
      ( cgrp wcel wa cfv cplusg co wceq c0g grpinvcl eqid grprinv syldan eqtr4d
      grplinv wb simpl simpr grplcan syl13anc mpbid ) BGHZDAHZIZDCJZUJCJZBKJZLZ
      UJDULLZMZUKDMZUIUMBNJZUNUGUHUJAHZUMUQMABCDEFOZAULBCUQUJEULPZUQPZFQRAULBCU
      QDEUTVAFTSUIUGUKAHZUHURUOUPUAUGUHUBUGUHURVBUSABCUJEFORUGUHUCUSAULBUKDUJEU
      TUDUEUF $.
      $( [31-Mar-2014] $)
  $}

  ${
    grpinvadd.b $e |- B = ( Base ` G ) $.
    grpinvadd.p $e |- P = ( +g ` G ) $.
    grpinvadd.n $e |- N = ( invg ` G ) $.
    $( The inverse of the group operation reverses the arguments.  Lemma
       2.2.1(d) of [Herstein] p. 55. $)
    grpinvadd $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) ->
                   ( N ` ( X P Y ) ) = ( ( N ` Y ) P ( N ` X ) ) ) $=
      ( wcel cfv wceq grpinvcl 3adant2 3adant3 grpcl syl111anc grpass syl13anc
      co cgrp w3a c0g simp1 simp2 simp3 grprinv opreq1d grplid syl11anc 3eqtr3d
      eqid opreq2d 3eqtrd wb grpinvid1 mpbird ) CUAJZEAJZFAJZUBZEFBTZDKFDKZEDKZ
      BTZLZVBVEBTZCUCKZLZVAVGEFVEBTZBTZEVDBTZVHVAURUSUTVEAJZVGVKLURUSUTUDZURUSU
      TUEURUSUTUFZVAURVCAJZVDAJZVMVNURUTVPUSACDFGIMNZURUSVQUTACDEGIMOZABCVCVDGH
      PQZABCEFVEGHRSVAVJVDEBVAFVCBTZVDBTZVHVDBTZVJVDVAWAVHVDBURUTWAVHLUSABCDVHF
      GHVHULZIUGNUHVAURUTVPVQWBVJLVNVOVRVSABCFVCVDGHRSVAURVQWCVDLVNVSABCVHVDGHW
      DUIUJUKUMURUSVLVHLUTABCDVHEGHWDIUGOUNVAURVBAJVMVFVIUOVNABCEFGHPVTABCDVHVB
      VEGHWDIUPQUQ $.
      $( [27-Oct-2006] $)
  $}

  ${
    grpsubcl.b $e |- B = ( Base ` G ) $.
    grpsubcl.m $e |- M = ( -g ` G ) $.
    $( Closure of group subtraction. $)
    grpsubcl $p |- ( ( G e. Grp /\ X e. B /\ Y e. B ) -> ( X M Y ) e. B ) $=
      ( cgrp wcel w3a co cminusg cfv cplusg grpsubval grpinvcl 3adant2 syld3an3
      eqid grpcl eqeltrd ) BHIZDAIZEAIZJDECKDEBLMZMZBNMZKZAAUGBUECHDEFUGSZUESZG
      OUBUCUDUFAIZUHAIUBUDUKUCABUEEFUJPQAUGBDUFFUITRUA $.
      $( [31-Mar-2014] $)

    $( Right cancellation law for group subtraction. $)
    grpsubrcan $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( X M Z ) = ( Y M Z ) <-> X = Y ) ) $=
      ( cgrp wcel w3a wa co wceq cminusg cfv cplusg eqid grpsubval 3adant3r2 wb
      3adant3r1 eqeq12d simpl simpr1 simpr2 grpinvcl 3ad2antr3 grprcan syl13anc
      bitrd ) BIJZDAJZEAJZFAJZKZLZDFCMZEFCMZNDFBOPZPZBQPZMZEVAVBMZNZDENZUQURVCU
      SVDULUMUOURVCNUNAVBBUTCIDFGVBRZUTRZHSTULUNUOUSVDNUMAVBBUTCIEFGVGVHHSUBUCU
      QULUMUNVAAJZVEVFUAULUPUDULUMUNUOUEULUMUNUOUFULUMUOVIUNABUTFGVHUGUHAVBBDEV
      AGVGUIUJUK $.
      $( [31-Mar-2014] $)
  $}

  ${
    grpsubid.b $e |- B = ( Base ` G ) $.
    grpsubid.o $e |- O = ( 0g ` G ) $.
    grpsubid.m $e |- M = ( -g ` G ) $.
    $( Subtraction of a group element from itself. $)
    grpsubid $p |- ( ( G e. Grp /\ X e. B ) -> ( X M X ) = O ) $=
      ( cgrp wcel wa co cminusg cfv cplusg wceq eqid grpsubval 3anidm23 grprinv
      eqtrd ) BIJZEAJZKEECLZEEBMNZNBONZLZDUBUCUDUGPAUFBUECIEEFUFQZUEQZHRSAUFBUE
      DEFUHGUITUA $.
      $( [31-Mar-2014] $)

    $( If the difference between two group elements is zero, they are equal. $)
    grpsubeq0 $p |- ( ( G e. Grp /\ X e. B /\ Y e. B )
                  -> ( ( X M Y ) = O <-> X = Y ) ) $=
      ( cgrp wcel w3a co wceq cminusg cfv cplusg eqid eqeq1d 3adant2 wb 3bitr2d
      grpsubval simp1 grpinvcl simp2 grpinvid2 syl111anc grpinvinv eqcom syl6bb
      ) BJKZEAKZFAKZLZEFCMZDNEFBOPZPZBQPZMZDNZURUQPZENZEFNZUOUPUTDAUSBUQCJEFGUS
      RZUQRZIUCSUOULURAKZUMVCVAUAULUMUNUDULUNVGUMABUQFGVFUETULUMUNUFAUSBUQDUREG
      VEHVFUGUHUOVCFENVDUOVBFEULUNVBFNUMABUQFGVFUITSFEUJUKUB $.
      $( [31-Mar-2014] $)
  $}

  ${
    grpsubadd.b $e |- B = ( Base ` G ) $.
    grpsubadd.p $e |- P = ( +g ` G ) $.
    grpsubadd.m $e |- M = ( -g ` G ) $.
    $( Relationship between group subtraction and addition. $)
    grpsubadd $p |- ( ( G e. Grp /\ ( X e. B /\ Y e. B /\ Z e. B ) )
          -> ( ( X M Y ) = Z <-> ( Z P Y ) = X ) ) $=
      ( cgrp wcel w3a co wceq cfv eqid eqeq1d 3ad2antr2 syl13anc cminusg simpr1
      wa grpsubval 3adant3r3 simpl grpinvcl syl111anc simpr3 simpr2 grprcan c0g
      grpcl grpass grplinv opreq2d grprid 3ad2antr1 3eqtrd 3bitr2d eqcom syl6bb
      wb ) CKLZEALZFALZGALZMZUCZEFDNZGOZEGFBNZOZVLEOVIVKEFCUAPZPZBNZGOZVPFBNZVL
      OZVMVIVJVPGVDVEVFVJVPOVGABCVNDKEFHIVNQZJUDUERVIVDVPALZVGVFVSVQVCVDVHUFZVI
      VDVEVOALZWAWBVDVEVFVGUBZVDVEVFWCVGACVNFHVTUGSZABCEVOHIUMUHVDVEVFVGUIVDVEV
      FVGUJZABCVPGFHIUKTVIVREVLVIVREVOFBNZBNZECULPZBNZEVIVDVEWCVFVRWHOWBWDWEWFA
      BCEVOFHIUNTVIWGWIEBVDVEVFWGWIOVGABCVNWIFHIWIQZVTUOSUPVDVFVEWJEOVGABCWIEHI
      WKUQURUSRUTEVLVAVB $.
      $( [31-Mar-2014] $)
  $}

