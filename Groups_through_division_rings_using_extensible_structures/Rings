$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c .r $.
  $c Ring $.
  $c 1r $.

  $( Extend class notation with ring multiplication. $)
  cmulr $a class .r $.

  $( Extend class notation with class of all rings. $)
  crg $a class Ring $.

  $( Extend class notation with ring unit. $)
  cur $a class 1r $.

  $( Define ring multiplication. $)
  df-mulr $a |- .r = ( r e. _V |-> ( r ` 3 ) ) $.

  $( Utility theorem: index-independent form of ~ df-mulr . $)
  mulrid $p |- .r = ( f e. _V |-> ( f ` ( .r ` ndx ) ) ) $=
    ( cmulr c3 df-mulr 3nn ndxid ) BCAADEF $.
    $( [8-Jun-2013] $)

  $( Utility theorem for constructing a ring (or generally, 3-element)
     structure without direct reference to component indices.  This theorem
     extends ~ grplem to a third element. $)
  rnglem $p |- -. ( .r ` ndx ) e. { ( Base ` ndx ) , ( +g ` ndx ) } $=
    ( vs cnx cmulr cfv cbs cplusg cpr wcel wceq wo wne wa c3 ndxarg c1 neeqtrri
    3re ltneii eqnetri c2 wn df-mulr 3nn 1re 1lt3 basendx 2re 2lt3 df-plusg 2nn
    pm3.2i neanior mpbi elpri mto ) BCDZBEDZBFDZGHUPUQIUPURIJZUPUQKZUPURKZLUSUA
    UTVAUPMUQCMAAUBUCNZMOUQOMUDQUERUFPSUPMURVBMTURTMUGQUHRFTAAUIUJNPSUKUPUQUPUR
    ULUMUPUQURUNUO $.
    $( [30-Sep-2013] $)

  ${
    rngfn.r $e |- R
               = { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. ,
                   <. ( .r ` ndx ) , T >. } $.
    ${
      rngfn.d $e |- D = { ( Base ` ndx ) , ( +g ` ndx ) , ( .r ` ndx ) } $.
      $( A constructed ring is a function. $)
      rngfn $p |- R Fn D $=
        ( cnx cbs cfv cplusg cpr cop cmulr eqid ctp csn cun df-tp eqtri rnglem
        fvex grpfn fnunsn ) HIJZHKJZLZBUEAMZUFCMZLZDHNJZEHNUBAUGCUJUJOUGOUCDUHU
        IUKEMZPUJULQRFUHUIULSTBUEUFUKPUGUKQRGUEUFUKSTUAUD $.
        $( [28-Sep-2013] $)
    $}

    $( The base set of a constructed ring. $)
    rngbase $p |- ( B e. V -> B = ( Base ` R ) ) $=
      ( vs cbs cnx cfv cop cplusg cmulr ctp cvv tpex eqeltri wfn wfun eqid opex
      rngfn fnfun ax-mp baseid tpid1 eleqtrri strfv ) ACHEGCIHJZAKZILJZBKZIMJZD
      KZNZOFUJULUNPQCUIUKUMNZRCSAUPBCDFUPTUBUPCUCUDGUEUJUOCUJULUNUIAUAUFFUGUH
      $.
      $( [2-Oct-2013] $)

    $( The additive operation of a constructed ring. $)
    rngplusg $p |- ( P e. V -> P = ( +g ` R ) ) $=
      ( vs cplusg cnx cbs cfv cop cmulr ctp cvv tpex eqeltri wfn wfun eqid opex
      rngfn fnfun ax-mp plusgid tpid2 eleqtrri strfv ) BCHEGCIJKZALZIHKZBLZIMKZ
      DLZNZOFUJULUNPQCUIUKUMNZRCSAUPBCDFUPTUBUPCUCUDGUEULUOCUJULUNUKBUAUFFUGUH
      $.
      $( [2-Oct-2013] $)

    $( The muliplicative operation of a constructed ring. $)
    rngmulr $p |- ( T e. V -> T = ( .r ` R ) ) $=
      ( vs cmulr cnx cbs cfv cop cplusg ctp cvv tpex eqeltri wfn wfun eqid opex
      rngfn fnfun ax-mp mulrid tpid3 eleqtrri strfv ) DCHEGCIJKZALZIMKZBLZIHKZD
      LZNZOFUJULUNPQCUIUKUMNZRCSAUPBCDFUPTUBUPCUCUDGUEUNUOCUJULUNUMDUAUFFUGUH
      $.
      $( [2-Oct-2013] $)
  $}

  ${
    $d f p r t u x y z $.
    $( Define class of all rings (with unity). $)
    df-ring $a |- Ring = { f e. Abel |
        E. r E. p E. t ( ( r = ( Base ` f ) /\ p = ( +g ` f )
           /\ t = ( .r ` f ) )
       /\ A. x e. r A. y e. r A. z e. r
           ( ( x t y ) e. r /\ ( ( ( x t y ) t z ) = ( x t ( y t z ) )
            /\ ( x t ( y p z ) ) = ( ( x t y ) p ( x t z ) )
            /\ ( ( x p y ) t z ) = ( ( x t z ) p ( y t z ) ) ) )
        /\ E. u e. r A. x e. r ( ( u t x ) = x /\ ( x t u ) = x ) ) } $.

    $( Define ring unity. $)
    df-ur $a |- 1r = ( r e. _V |-> ( iota_ u e. ( Base ` r )
      A. x e. ( Base ` r ) ( ( u ( .r ` r ) x ) = x
           /\ ( x ( .r ` r ) u ) = x ) ) ) $.
  $}

  ${
    $d u r x B $.  $d u r x R $.  $d r T $.
    rngidval.b $e |- B = ( Base ` R ) $.
    rngidval.t $e |- T = ( .r ` R ) $.
    rngidval.u $e |- U = ( 1r ` R ) $.
    $( The value of the unity element of a ring. $)
    rngidval $p |- ( R e. V -> U = ( iota_ u e. B A. x e. B
               ( ( u T x ) = x /\ ( x T u ) = x ) ) ) $=
      ( vr wcel cvv cv co wceq wa cfv cmulr cbs wral crio elex cur fveq2 opreqd
      syl6eqr eqeq1d anbi12d raleqbidv riotaeqbidv df-ur riotaex fvmpt syl5eq
      syl ) DGLDMLZFBNZANZEOZUSPZUSUREOZUSPZQZACUAZBCUBZPDGUCUQFDUDRVFJKDURUSKN
      ZSRZOZUSPZUSURVHOZUSPZQZAVGTRZUAZBVNUBVFMUDVGDPZVOVEBVNCVPVNDTRCVGDTUEHUG
      ZVPVMVDAVNCVQVPVJVAVLVCVPVIUTUSVPVHEURUSVPVHDSREVGDSUEIUGZUFUHVPVKVBUSVPV
      HEUSURVRUFUHUIUJUKABKULVEBCUMUNUOUP $.
      $( [7-Aug-2013] $) $( [27-Aug-2011] $)
  $}

  ${
    $d b r p t u x y z B $.  $d b r p t u x y z P $.  $d b r p t R $.
    $d b p r t u x y z T $.
    isrng.b $e |- B = ( Base ` R ) $.
    isrng.p $e |- P = ( +g ` R ) $.
    isrng.t $e |- T = ( .r ` R ) $.
    $( The predicate "is a (unital) ring."  Definition of ring with unit in
       [Schechter] p. 187. $)
    isrng $p |- ( R e. Ring <-> ( R e. Abel
             /\ A. x e. B A. y e. B A. z e. B ( ( x T y ) e. B
               /\ ( ( ( x T y ) T z ) = ( x T ( y T z ) )
            /\ ( x T ( y P z ) ) = ( ( x T y ) P ( x T z ) )
            /\ ( ( x P y ) T z ) = ( ( x T z ) P ( y T z ) ) ) )
           /\ E. u e. B A. x e. B ( ( u T x ) = x /\ ( x T u ) = x ) ) ) $=
      ( vb vp cv wceq w3a co wa wral opreq vr vt crg wcel cbs cplusg cmulr wrex
      cfv wex cabel crab df-ring eleq2i syl6eqr eqeq2d 3anbi123d 3anbi1d 3anass
      fveq2 syl6bb 3exbidv elrab cvv fvex eqeltri eleq2 anbi1d raleqbi1dv raleq
      rexeqbi1dv anbi12d opreq2d eqeq12d opreq1d 3anbi23d anbi2d ralbidv eleq1d
      2ralbidv eqtrd opreq12d eqeq1d rexralbidv ceqsex3v anbi2i bitr4i 3bitri )
      GUCUDGLNZUANZUEUIZOZMNZWJUFUIZOZUBNZWJUGUIZOZPZANZBNZWPQZWIUDZXBCNZWPQZWT
      XAXDWPQZWPQZOZWTXAXDWMQZWPQZXBWTXDWPQZWMQZOZWTXAWMQZXDWPQZXKXFWMQZOZPZRZC
      WISZBWISZAWISZDNZWTWPQZWTOZWTYCWPQZWTOZRZAWISZDWIUHZPZUBUJMUJLUJZUAUKULZU
      DGUKUDZWIEOZWMFOZWPHOZPZYBYJRZRZUBUJMUJLUJZRZYNWTXAHQZEUDZUUCXDHQZWTXAXDH
      QZHQZOZWTXAXDFQZHQZUUCWTXDHQZFQZOZWTXAFQZXDHQZUUKUUFFQZOZPZRZCESZBESAESZY
      CWTHQZWTOZWTYCHQZWTOZRZAESDEUHZPZUCYMGABCDUBUALMUMUNYLUUAUAGUKWJGOZYKYTLM
      UBUVIYKYRYBYJPYTUVIWSYRYBYJUVIWLYOWOYPWRYQUVIWKEWIUVIWKGUEUIZEWJGUEUTIUOU
      PUVIWNFWMUVIWNGUFUIZFWJGUFUTJUOUPUVIWQHWPUVIWQGUGUIZHWJGUGUTKUOUPUQURYRYB
      YJUSVAVBVCUUBYNUVAUVGRZRUVHUUAUVMYNYSXBEUDZXRRZCESZBESZAESZYHAESZDEUHZRUV
      NXHWTUUIWPQZXBXKFQZOZUUNXDWPQZXKXFFQZOZPZRZCESZBESAESZUVTRUVMLMUBEFHEUVJV
      DIGUEVEVFFUVKVDJGUFVEVFHUVLVDKGUGVEVFYOYBUVRYJUVTYAUVQAWIEXTUVPBWIEXSUVOC
      WIEYOXCUVNXRWIEXBVGVHVIVIVIYIUVSDWIEYHAWIEVJVKVLYPUVRUWJUVTYPUVPUWIABEEYP
      UVOUWHCEYPXRUWGUVNYPXMUWCXQUWFXHYPXJUWAXLUWBYPXIUUIWTWPXAXDWMFTVMXBXKWMFT
      VNYPXOUWDXPUWEYPXNUUNXDWPWTXAWMFTVOXKXFWMFTVNVPVQVRVTVHYQUWJUVAUVTUVGYQUW
      IUUTABEEYQUWHUUSCEYQUVNUUDUWGUURYQXBUUCEWTXAWPHTZVSYQXHUUHUWCUUMUWFUUQYQX
      EUUEXGUUGYQXEXBXDHQUUEXBXDWPHTYQXBUUCXDHUWKVOWAYQXGWTXFHQUUGWTXFWPHTYQXFU
      UFWTHXAXDWPHTZVMWAVNYQUWAUUJUWBUULWTUUIWPHTYQXBUUCXKUUKFUWKWTXDWPHTZWBVNY
      QUWDUUOUWEUUPUUNXDWPHTYQXKUUKXFUUFFUWMUWLWBVNUQVLVRVTYQYHUVFDAEEYQYEUVCYG
      UVEYQYDUVBWTYCWTWPHTWCYQYFUVDWTWTYCWPHTWCVLWDVLWEWFYNUVAUVGUSWGWH $.
      $( [18-Oct-2012] $)
  $}

  ${
    $d u x y z K $.
    rngprop.b $e |- ( Base ` K ) = ( Base ` L ) $.
    rngprop.p $e |- ( +g ` K ) = ( +g ` L ) $.
    rngprop.m $e |- ( .r ` K ) = ( .r ` L ) $.
    $( If two structures have the same ring components (properties), one is a
       ring iff the other one is. $)
    rngprop $p |- ( K e. Ring <-> L e. Ring ) $=
      ( vx vy vz vu cabel wcel cv cfv co wceq w3a wa wral crg eqid cmulr cplusg
      cbs wrex ablprop 3anbi1i isrng 3bitr4i ) AJKZFLZGLZAUAMZNZAUCMZKUMHLZULNU
      JUKUOULNZULNOUJUKUOAUBMZNULNUMUJUOULNZUQNOUJUKUQNUOULNURUPUQNOPQHUNRGUNRF
      UNRZILZUJULNUJOUJUTULNUJOQFUNRIUNUDZPBJKZUSVAPASKBSKUIVBUSVAABCDUEUFFGHIU
      NUQAULUNTUQTULTUGFGHIUNUQBULCDEUGUH $.
      $( [11-Oct-2013] $)
  $}

  ${
    $d b p r t u x y z B $.  $d b p r t u x y z P $.  $d b n p r t R $.
    $d b p r t u x y z T $.  $d n u x y z $.
    rngi.b $e |- B = ( Base ` R ) $.
    rngi.p $e |- P = ( +g ` R ) $.
    rngi.t $e |- T = ( .r ` R ) $.
    $d x y z X $.  $d y z Y $.  $d z Z $.
    $( Properties of a unital ring. $)
    rngi $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. B ) )
           -> ( ( X T Y ) e. B
                /\ ( ( ( X T Y ) T Z ) = ( X T ( Y T Z ) )
              /\ ( X T ( Y P Z ) ) = ( ( X T Y ) P ( X T Z ) )
                /\ ( ( X P Y ) T Z ) = ( ( X T Z ) P ( Y T Z ) ) ) ) ) $=
      ( vx wcel co wceq w3a opreq1 opreq1d eqeq12d opreq2 opreq2d vy vz vu wral
      crg cv cabel wrex isrng simp2bi eleq1d opreq12d 3anbi123d anbi12d rcla43v
      wa anbi2d mpan9 ) CUELZKUFZUAUFZDMZALZVBUBUFZDMZUTVAVDDMZDMZNZUTVAVDBMZDM
      ZVBUTVDDMZBMZNZUTVABMZVDDMZVKVFBMZNZOZUPZUBAUDUAAUDKAUDZEALFALGALOEFDMZAL
      ZWAGDMZEFGDMZDMZNZEFGBMZDMZWAEGDMZBMZNZEFBMZGDMZWIWDBMZNZOZUPZUSCUGLVTUCU
      FZUTDMUTNUTWRDMUTNUPKAUDUCAUHKUAUBUCABCDHIJUIUJVSWQEVADMZALZWSVDDMZEVFDMZ
      NZEVIDMZWSEVDDMZBMZNZEVABMZVDDMZXEVFBMZNZOZUPWBWAVDDMZEFVDDMZDMZNZEFVDBMZ
      DMZWAXEBMZNZWLVDDMZXEXNBMZNZOZUPKUAUBEFGAAAUTENZVCWTVRXLYEVBWSAUTEVADPZUK
      YEVHXCVMXGVQXKYEVEXAVGXBYEVBWSVDDYFQUTEVFDPRYEVJXDVLXFUTEVIDPYEVBWSVKXEBY
      FUTEVDDPZULRYEVOXIVPXJYEVNXHVDDUTEVABPQYEVKXEVFBYGQRUMUNVAFNZWTWBXLYDYHWS
      WAAVAFEDSZUKYHXCXPXGXTXKYCYHXAXMXBXOYHWSWAVDDYIQYHVFXNEDVAFVDDPZTRYHXDXRX
      FXSYHVIXQEDVAFVDBPTYHWSWAXEBYIQRYHXIYAXJYBYHXHWLVDDVAFEBSQYHVFXNXEBYJTRUM
      UNVDGNZYDWPWBYKXPWFXTWKYCWOYKXMWCXOWEVDGWADSYKXNWDEDVDGFDSZTRYKXRWHXSWJYK
      XQWGEDVDGFBSTYKXEWIWABVDGEDSZTRYKYAWMYBWNVDGWLDSYKXEWIXNWDBYMYLULRUMUQUOU
      R $.
      $( [26-Aug-2011] $)
  $}

  ${
    rngcl.b $e |- B = ( Base ` R ) $.
    rngcl.t $e |- T = ( .r ` R ) $.
    $( Closure of the multiplication operation of a ring. $)
    rngcl $p |- ( ( R e. Ring /\ X e. B /\ Y e. B ) -> ( X T Y ) e. B ) $=
      ( crg wcel co wa w3a 3anass anidm anbi2i bitri wceq cplusg cfv eqid 3impb
      rngi simpld sylan2br ) BHIZDAIZEAIZDECJZAIZUFUGKZUEUFUGUGLZUIUKUFUGUGKZKU
      JUFUGUGMULUGUFUGNOPUEUKKUIUHECJDEECJZCJQDEEBRSZJCJUHUHUNJQDEUNJECJUHUMUNJ
      QLAUNBCDEEFUNTGUBUCUDUA $.
      $( [26-Aug-2011] $)

    $( Associative law for the multiplication operation of a ring. $)
    rngass $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                      ( ( X T Y ) T Z ) = ( X T ( Y T Z ) ) ) $=
      ( crg wcel w3a wa co wceq cplusg cfv eqid rngi simpr1 syl ) BIJDAJEAJFAJK
      LDECMZAJZUAFCMDEFCMZCMNZDEFBOPZMCMUADFCMZUEMNZDEUEMFCMUFUCUEMNZKLUDAUEBCD
      EFGUEQHRUBUDUGUHST $.
      $( [27-Aug-2011] $)

    $d r u w x y z B $.  $d r u x y z R $.  $d r u w x y z T $.
    $( The unit element of a ring is unique. $)
    rngideu $p |- ( R e. Ring ->
                 E! u e. B A. x e. B ( ( u T x ) = x /\ ( x T u ) = x ) ) $=
      ( vw vy vz wcel cv co wceq wa wral ralimi opreq2 id eqeq12d crg wrex wreu
      wi cabel cplusg cfv w3a eqid isrng simp3bi simpl rcla4v syl5 simpr opreq1
      im2anan9 eqtr2 ancoms syl6 rgen2a jctir eqeq1d anbi12d ralbidv sylibr
      reu4 ) DUAKZBLZALZEMZVJNZVJVIEMZVJNZOZACPZBCUBZVPHLZVJEMZVJNZVJVREMZVJNZO
      ZACPZOZVIVRNZUDZHCPBCPZOVPBCUCVHVQWHVHDUEKVJILZEMZCKWJJLZEMVJWIWKEMZEMNVJ
      WIWKDUFUGZMEMWJVJWKEMZWMMNVJWIWMMWKEMWNWLWMMNUHOJCPICPACPVQAIJBCWMDEFWMUI
      GUJUKWGBHCVRCKZVICKZWGWOWPOWEVIVREMZVRNZWQVINZOWFWOVPWRWPWDWSVPVLACPWOWRV
      OVLACVLVNULQVLWRAVRCVJVRNZVKWQVJVRVJVRVIERWTSTUMUNWDWBACPWPWSWCWBACVTWBUO
      QWBWSAVICVJVINZWAWQVJVIVJVIVREUPXASTUMUNUQWSWRWFWQVIVRURUSUTUSVAVBVPWDBHC
      WFVOWCACWFVLVTVNWBWFVKVSVJVIVRVJEUPVCWFVMWAVJVIVRVJERVCVDVEVGVF $.
      $( [27-Aug-2011] $)
  $}

  ${
    rngdi.b $e |- B = ( Base ` R ) $.
    rngdi.p $e |- P = ( +g ` R ) $.
    rngdi.t $e |- T = ( .r ` R ) $.
    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    rngdi $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                     ( X T ( Y P Z ) ) = ( ( X T Y ) P ( X T Z ) ) ) $=
      ( crg wcel w3a wa co wceq rngi simpr2 syl ) CKLEALFALGALMNEFDOZALZTGDOEFG
      DOZDOPZEFGBODOTEGDOZBOPZEFBOGDOUDUBBOPZMNUEABCDEFGHIJQUAUCUEUFRS $.
      $( [27-Aug-2011] $)

    $( Distributive law for the multiplication operation of a ring.
       (Contributed by Steve Rodriguez, 9-Sep-2007.) $)
    rngdir $p |- ( ( R e. Ring /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                      ( ( X P Y ) T Z ) = ( ( X T Z ) P ( Y T Z ) ) ) $=
      ( crg wcel w3a wa co wceq rngi simpr3 syl ) CKLEALFALGALMNEFDOZALZTGDOEFG
      DOZDOPZEFGBODOTEGDOZBOPZEFBOGDOUDUBBOPZMNUFABCDEFGHIJQUAUCUEUFRS $.
      $( [27-Aug-2011] $)
  $}

  ${
    $d u x B $.  $d u x R $.
    rngidcl.b $e |- B = ( Base ` R ) $.
    rngidcl.u $e |- U = ( 1r ` R ) $.
    $( The unit element of a ring belongs to the base set of the ring. $)
    rngidcl $p |- ( R e. Ring -> U e. B ) $=
      ( vu vx crg wcel cv cmulr cfv co wceq wa wral wreu eqid rngideu crio
      rngidval adantr riotacl adantl eqeltrd mpdan ) BHIZFJZGJZBKLZMUINUIUHUJMU
      INOGAPZFAQZCAIGFABUJDUJRZSUGULOCUKFATZAUGCUNNULGFABUJCHDUMEUAUBULUNAIUGUK
      FAUCUDUEUF $.
      $( [27-Aug-2011] $)
  $}

  ${
    $d r u x y z R $.
    $( A ring is an Abelian group. $)
    rngabl $p |- ( R e. Ring -> R e. Abel ) $=
      ( vx vy vz vu crg wcel cabel cv cmulr cfv co cbs wceq cplusg wa wral wrex
      w3a eqid isrng biimpi simp1d ) AFGZAHGZBIZCIZAJKZLZAMKZGUIDIZUHLUFUGUKUHL
      ZUHLNUFUGUKAOKZLUHLUIUFUKUHLZUMLNUFUGUMLUKUHLUNULUMLNSPDUJQCUJQBUJQZEIZUF
      UHLUFNUFUPUHLUFNPBUJQEUJRZUDUEUOUQSBCDEUJUMAUHUJTUMTUHTUAUBUC $.
      $( [26-Aug-2011] $)
  $}

  $( A ring is a group. $)
  rnggrp $p |- ( R e. Ring -> R e. Grp ) $=
    ( crg wcel cabel cgrp rngabl ablgrp syl ) ABCADCAECAFAGH $.
    $( [15-Sep-2011] $)

  ${
    rngacl.b $e |- B = ( Base ` R ) $.
    rngacl.p $e |- P = ( +g ` R ) $.
    $( Closure of the addition operation of a ring. $)
    rngacl $p |- ( ( R e. Ring /\ X e. B /\ Y e. B ) -> ( X P Y ) e. B ) $=
      ( crg wcel cgrp co rnggrp grpcl syl3an1 ) CHICJIDAIEAIDEBKAICLABCDEFGMN
      $.
      $( [14-Jan-2014] $)
  $}

  ${
    rng0cl.b $e |- B = ( Base ` R ) $.
    rng0cl.z $e |- Z = ( 0g ` R ) $.
    $( The zero element of a ring belongs to its base set. $)
    rng0cl $p |- ( R e. Ring -> Z e. B ) $=
      ( crg wcel cgrp rnggrp grpidcl syl ) BFGBHGCAGBIABCDEJK $.
      $( [12-Jan-2014] $)
  $}

  ${
    $d u w x B $.  $d u w x I $.  $d u x R $.  $d u w x T $.  $d u w x U $.
    $d x X $.
    rngidm.b $e |- B = ( Base ` R ) $.
    rngidm.t $e |- T = ( .r ` R ) $.
    rngidm.u $e |- U = ( 1r ` R ) $.
    $( Lemma for ~ rnglidm and ~ rngridm . $)
    rngidmlem $p |- ( ( R e. Ring /\ X e. B )
           -> ( ( U T X ) = X /\ ( X T U ) = X ) ) $=
      ( vx vu vw crg wcel cv co wceq wa wral opreq1 eqeq1d crio rngidval eqcomd
      wreu wb rngidcl rngideu ax-17 a17d opreq2 anbi12d ralbidv riota2f syl2anc
      mpbird id eqeq12d rcla4v mpan9 ) BLMZDINZCOZVAPZVADCOZVAPZQZIARZEAMDECOZE
      PZEDCOZEPZQZUTVGJNZVACOZVAPZVAVMCOZVAPZQZIARZJAUAZDPZUTDVTIJABCDLFGHUBUCU
      TDAMZVSJAUDVGWAUEABDFHUFIJABCFGUGVSVGJKADKNDMJUHWBVGJUIVMDPZVRVFIAWCVOVCV
      QVEWCVNVBVAVMDVACSTWCVPVDVAVMDVACUJTUKULUMUNUOVFVLIEAVAEPZVCVIVEVKWDVBVHV
      AEVAEDCUJWDUPZUQWDVDVJVAEVAEDCSWEUQUKURUS $.
      $( [15-Sep-2011] $)

    $( The unit element of a ring is a left multiplicative identity. $)
    rnglidm $p |- ( ( R e. Ring /\ X e. B ) -> ( U T X ) = X ) $=
      ( crg wcel wa co wceq rngidmlem simpld ) BIJEAJKDECLEMEDCLEMABCDEFGHNO $.
      $( [15-Sep-2011] $)

    $( The unit element of a ring is a right multiplicative identity. $)
    rngridm $p |- ( ( R e. Ring /\ X e. B ) -> ( X T U ) = X ) $=
      ( crg wcel wa co wceq rngidmlem simprd ) BIJEAJKDECLEMEDCLEMABCDEFGHNO $.
      $( [15-Sep-2011] $)

    $( Properties showing that an element ` I ` is the unity element of a
       ring. $)
    isrngid $p |- ( R e. Ring
      -> ( ( I e. B /\ A. x e. B ( ( I T x ) = x /\ ( x T I ) = x ) )
        <-> U = I ) ) $=
      ( vu vw crg wcel cv co wceq wa wral eqeq1d anbi12d crio adantr wb rngideu
      rngidval wreu anim2i ancoms a17d opreq1 opreq2 ralbidv riota2f syl biimpd
      ax-17 impr eqtrd rngidcl rngidmlem ralrimiva jca eleq1 syl5ibcom impbid
      ex ) CLMZFBMZFANZDOZVIPZVIFDOZVIPZQZABRZQZEFPZVGVPVQVGVPQEJNZVIDOZVIPZVIV
      RDOZVIPZQZABRZJBUAZFVGEWEPVPAJBCDELGHIUEUBVGVHVOWEFPZVGVHQZVOWFWGVHWDJBUF
      ZQZVOWFUCVHVGWIVGWHVHAJBCDGHUDUGUHWDVOJKBFKNFMJUPVHVOJUIVRFPZWCVNABWJVTVK
      WBVMWJVSVJVIVRFVIDUJSWJWAVLVIVRFVIDUKSTULUMUNUOUQURVFVGEBMZEVIDOZVIPZVIED
      OZVIPZQZABRZQVQVPVGWKWQBCEGIUSVGWPABBCDEVIGHIUTVAVBVQWKVHWQVOEFBVCVQWPVNA
      BVQWMVKWOVMVQWLVJVIEFVIDUJSVQWNVLVIEFVIDUKSTULTVDVE $.
      $( [7-Aug-2013] $)
  $}

  ${
    $d u x y z B $.  $d u x y z R $.  $d u T $.  $d u x U $.  $d u x y z ph $.
    isrngd.b $e |- ( ph -> B = ( Base ` R ) ) $.
    isrngd.p $e |- ( ph -> P = ( +g ` R ) ) $.
    isrngd.t $e |- ( ph -> T = ( .r ` R ) ) $.
    isrngd.g $e |- ( ph -> R e. Abel ) $.
    isrngd.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x T y ) e. B ) $.
    isrngd.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                  ( ( x T y ) T z ) = ( x T ( y T z ) ) ) $.
    isrngd.d $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                  ( x T ( y P z ) ) = ( ( x T y ) P ( x T z ) ) ) $.
    isrngd.e $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) ) ->
                  ( ( x P y ) T z ) = ( ( x T z ) P ( y T z ) ) ) $.
    isrngd.u $e |- ( ph -> U e. B ) $.
    isrngd.i $e |- ( ( ph /\ x e. B ) -> ( U T x ) = x ) $.
    isrngd.h $e |- ( ( ph /\ x e. B ) -> ( x T U ) = x ) $.
    $( Properties that determine a ring. $)
    isrngd $p |- ( ph -> R e. Ring ) $=
      ( vu cabel wcel cv cmulr cfv co cbs wceq cplusg w3a wa wral crg 3adant3r3
      wrex wi jca 3exp2 imp3a ralrimdv ralrimivv ralrimiva opreq1 eqeq1d opreq2
      anbi12d ralbidv rcla4ev syl2anc eleq12d eqidd opreq123d eqeq12d 3anbi123d
      3jca opreqd raleqbidv rexeqbidv 3anbi23d mpbid eqid isrng sylibr ) AGUBUC
      ZBUDZCUDZGUEUFZUGZGUHUFZUCZWIDUDZWHUGZWFWGWLWHUGZWHUGZUIZWFWGWLGUJUFZUGZW
      HUGZWIWFWLWHUGZWQUGZUIZWFWGWQUGZWLWHUGZWTWNWQUGZUIZUKZULZDWJUMZCWJUMZBWJU
      MZUAUDZWFWHUGZWFUIZWFXLWHUGZWFUIZULZBWJUMZUAWJUPZUKZGUNUCAWEWFWGHUGZEUCZY
      AWLHUGZWFWGWLHUGZHUGZUIZWFWGWLFUGZHUGZYAWFWLHUGZFUGZUIZWFWGFUGZWLHUGZYIYD
      FUGZUIZUKZULZDEUMZCEUMZBEUMZXLWFHUGZWFUIZWFXLHUGZWFUIZULZBEUMZUAEUPZUKXTA
      WEYTUUGMAYRBCEEAWFEUCZWGEUCZULYQDEAUUHUUIWLEUCZYQUQAUUHUUIUUJYQAUUHUUIUUJ
      UKULZYBYPAUUHUUIYBUUJNUOUUKYFYKYOOPQVPURUSUTVAVBAIEUCIWFHUGZWFUIZWFIHUGZW
      FUIZULZBEUMZUUGRAUUPBEAUUHULUUMUUOSTURVCUUFUUQUAIEXLIUIZUUEUUPBEUURUUBUUM
      UUDUUOUURUUAUULWFXLIWFHVDVEUURUUCUUNWFXLIWFHVFVEVGVHVIVJVPAYTXKUUGXSWEAYS
      XJBEWJJAYRXICEWJJAYQXHDEWJJAYBWKYPXGAYAWIEWJAHWHWFWGLVQZJVKAYFWPYKXBYOXFA
      YCWMYEWOAYAWIWLWLHWHLUUSAWLVLZVMAWFWFYDWNHWHLAWFVLZAHWHWGWLLVQZVMVNAYHWSY
      JXAAWFWFYGWRHWHLUVAAFWQWGWLKVQVMAYAWIYIWTFWQKUUSAHWHWFWLLVQZVMVNAYMXDYNXE
      AYLXCWLWLHWHLAFWQWFWGKVQUUTVMAYIWTYDWNFWQKUVCUVBVMVNVOVGVRVRVRAUUFXRUAEWJ
      JAUUEXQBEWJJAUUBXNUUDXPAUUAXMWFAHWHXLWFLVQVEAUUCXOWFAHWHWFXLLVQVEVGVRVSVT
      WABCDUAWJWQGWHWJWBWQWBWHWBWCWD $.
      $( [2-Aug-2013] $)
  $}

  ${
    rngz.b $e |- B = ( Base ` R ) $.
    rngz.t $e |- T = ( .r ` R ) $.
    rngz.z $e |- Z = ( 0g ` R ) $.
    $( The zero of a unital ring is a left absorbing element.  (Contributed by
       FL, 31-Aug-2009.) $)
    rnglz $p |- ( ( R e. Ring /\ X e. B ) -> ( Z T X ) = Z ) $=
      ( crg wcel wa co cplusg cfv wceq cgrp rnggrp grpidcl syl adantr mpdan w3a
      eqid grplid opreq1d simpr rngdir syldan simpl rngcl syl3anc grprid eqcomd
      3jca syl2anc 3eqtr3d wb grplcan syl13anc mpbid ) BIJZDAJZKZEDCLZVDBMNZLZV
      DEVELZOZVDEOZVCEEVELZDCLZVDVFVGVCVJEDCVAVJEOZVBVABPJZVLBQZVMEAJZVLABEFHRZ
      AVEBEEFVEUCZHUDUASTUEVAVBVOVOVBUBVKVFOVCVOVOVBVAVOVBVAVMVOVNVPSTZVRVAVBUF
      ZUNAVEBCEEDFVQGUGUHVCVMVDAJZVDVGOVAVMVBVNTZVCVAVOVBVTVAVBUIVRVSABCEDFGUJU
      KZVMVTKVGVDAVEBEVDFVQHULUMUOUPVCVMVTVOVTVHVIUQWAWBVRWBAVEBVDEVDFVQURUSUT
      $.
      $( [15-Sep-2011] $)

    $( The zero of a unital ring is a right absorbing element.  (Contributed by
       FL, 31-Aug-2009.) $)
    rngrz $p |- ( ( R e. Ring /\ X e. B ) -> ( X T Z ) = Z ) $=
      ( crg wcel wa co cplusg cfv wceq cgrp rnggrp grplid syl adantr eqid mpdan
      grpidcl opreq2d w3a simpr 3jca rngdi syldan rngcl mpd3an3 syl2anc 3eqtr3d
      eqcomd wb grprcan syl13anc mpbid ) BIJZDAJZKZDECLZVBBMNZLZEVBVCLZOZVBEOZV
      ADEEVCLZCLZVBVDVEVAVHEDCUSVHEOZUTUSBPJZVJBQZVKEAJZVJABEFHUCZAVCBEEFVCUAZH
      RUBSTUDUSUTUTVMVMUEVIVDOVAUTVMVMUSUTUFUSVMUTUSVKVMVLVNSTZVPUGAVCBCDEEFVOG
      UHUIVAVKVBAJZVBVEOUSVKUTVLTZUSUTVMVQVPABCDEFGUJUKZVKVQKVEVBAVCBEVBFVOHRUN
      ULUMVAVKVQVMVQVFVGUOVRVSVPVSAVCBVBEVBFVOUPUQUR $.
      $( [15-Sep-2011] $)
  $}

  ${
    rngneg1.b $e |- B = ( Base ` R ) $.
    rngneg1.t $e |- T = ( .r ` R ) $.
    rngneg1.u $e |- U = ( 1r ` R ) $.
    rngneg1.m $e |- N = ( invg ` R ) $.
    $( Minus 1 times a ring element is its negative.  Equation 2 of [Kreyszig]
       p. 51. $)
    rngneg1 $p |- ( ( R e. Ring /\ X e. B )
         -> ( ( N ` U ) T X ) = ( N ` X ) ) $=
      ( wcel cfv co wceq adantr grpinvcl syl2anc sylan eqid syl13anc crg wa c0g
      cplusg rnggrp simpl rngidcl simpr syl3anc rnglidm opreq2d grplinv opreq1d
      rngcl grpass rngdir rnglz 3eqtr3d eqtr3d grprinv grplid grprid 3eqtr3rd
      cgrp ) BUAKZFAKZUBZBUCLZFELZBUDLZMZDELZFCMZVHVJMZVIVMVGVMFVJMZVIVJMZVMFVI
      VJMZVJMZVKVNVGBVDKZVMAKZVFVIAKZVPVRNVEVSVFBUEZOZVGVEVLAKZVFVTVEVFUFZVEWDV
      FVEVSDAKZWDWBABDGIUGZABEDGJPQOZVEVFUHZABCVLFGHUNUIZWIVEVSVFWAWBABEFGJPRZA
      VJBVMFVIGVJSZUOTVGVOVHVIVJVGVMDFCMZVJMZVOVHVGWMFVMVJABCDFGHIUJUKVGVLDVJMZ
      FCMZVHFCMWNVHVGWOVHFCVGVSWFWOVHNWCVEWFVFWGOZAVJBEVHDGWLVHSZJULQUMVGVEWDWF
      VFWPWNNWEWHWQWIAVJBCVLDFGWLHUPTABCFVHGHWRUQURUSUMVGVQVHVMVJVEVSVFVQVHNWBA
      VJBEVHFGWLWRJUTRUKURVGVSWAVKVINWCWKAVJBVHVIGWLWRVAQVGVSVTVNVMNWCWJAVJBVHV
      MGWLWRVBQVC $.
      $( [18-Apr-2014] $)
  $}

