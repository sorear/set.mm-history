$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Basic metric space properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new constant symbols and define their syntactical properties. $)
  $c Met $.
  $c MetSp $.
  $c ball $.
  $c dist $.
  $c MetOpen $.

  $( Extend class notation with the class of all metrics. $)
  cme $a class Met $.

  $( Extend class notation with the class of all metric spaces. $)
  cmt $a class MetSp $.

  $( Extend class notation with the metric space ball function. $)
  cbl $a class ball $.

  $( Extend class notation with the metric space distance function. $)
  cds $a class dist $.

  $( Extend class notation with a function mapping each metric space to the
     family of its open sets. $)
  copn $a class MetOpen $.

  ${
    $d d x y z w $.
    $( Define the (proper) class of all metrics.  (A metric space is the
       metric's base set paired with the metric; see ~ df-ms .  However, we
       will often also call the metric itself a "metric space".)  Equivalent to
       Definition 14-1.1 of [Gleason] p. 223.  The 4 properties in Gleason's
       definition are shown by ~ met0 , ~ metgt0 , ~ metsym , and ~ mettri . $)
    df-met $a |- Met = { d | E. x ( d : ( x X. x ) --> RR /\
                 A. y e. x A. z e. x ( ( ( y d z ) = 0 <-> y = z ) /\
                 A. w e. x ( y d z ) <_ ( ( w d y ) + ( w d z ) ) ) ) } $.

    $( Define the metric space ball function.  See ~ blval for its value. $)
    df-bl $a |- ball = ( d e. Met |-> ( x e. dom dom d , z e. RR+ |->
                { y e. dom dom d | ( x d y ) < z } ) ) $.

    $( Define a function whose value is the family of open sets of a metric
       space.  See ~ isopn for its main property. $)
    df-opn $a |- MetOpen = ( d e. Met |-> ( topGen ` ran ( ball ` d ) ) ) $.

    $( Define the distance function component of a metric space (structure). $)
    df-ds $a |- dist = ( f e. _V |-> ( f ` ( 10 + 2 ) ) ) $.

    $( Define the (proper) class of all metric spaces. $)
    df-ms $a |- MetSp = { f e. TopSp |
                          ( TopOpen ` f ) = ( MetOpen ` ( dist ` f ) ) } $.

    $( Utility theorem: index-independent form of ~ df-ds . $)
    dsid $p |- dist = ( f e. _V |-> ( f ` ( dist ` ndx ) ) ) $=
      ( cds c10 c2 caddc co df-ds cn wcel 10nn 2nn nnaddcl mp2an ndxid ) BCDEFZ
      AAGCHIDHIOHIJKCDLMN $.
      $( [23-Dec-2013] $)
  $}

  ${
    $d d t x y z D $.  $d t x y z X $.
    ismet.1 $e |- X = dom dom D $.
    $( Express the predicate " ` D ` is a metric." $)
    ismet $p |- ( D e. A -> ( D e. Met <-> ( D : ( X X. X ) --> RR /\
                 A. x e. X A. y e. X ( ( ( x D y ) = 0 <-> x = y ) /\
                 A. z e. X ( x D y ) <_ ( ( z D x ) + ( z D y ) ) ) ) ) ) $=
      ( vt vd wcel cv cr wf co cc0 wceq wb wral wa cvv cme cxp cle wbr wex feq1
      oveq eqeq1d bibi1d oveq12d breq12d ralbidv anbi12d 2ralbidv exbidv df-met
      caddc elab2g cdm dmxpid dmeqd syl5req syl5eqr adantr xpeq12 anidms anbi2d
      fdm feq2d raleq raleqbi1dv syl ibi exlimiv dmexg syl5eqel cla4egv impbid2
      wi bitrd ) EDJZEUAJHKZWBUBZLEMZAKZBKZENZOPZWEWFPZQZWGCKZWEENZWKWFENZUQNZU
      CUDZCWBRZSZBWBRZAWBRZSZHUEZFFUBZLEMZWJWOCFRZSZBFRZAFRZSZWCLIKZMZWEWFXINZO
      PZWIQZXKWKWEXINZWKWFXINZUQNZUCUDZCWBRZSZBWBRAWBRZSZHUEXAIEUADXIEPZYAWTHYB
      XJWDXTWSWCLXIEUFYBXSWQABWBWBYBXMWJXRWPYBXLWHWIYBXKWGOWEWFXIEUGZUHUIYBXQWO
      CWBYBXKWGXPWNUCYCYBXNWLXOWMUQWKWEXIEUGWKWFXIEUGUJUKULUMUNUMUOHABCIUPURWAX
      AXHWTXHHWTXHWTWBFPZWTXHQWDYDWSWDWBWCUSZFWBUTWDFEUSZUSZYEGWDYFWCWCLEVHVAVB
      VCVDYDWDXCWSXGYDWCXBLEYDWCXBPWBFWBFVEVFVIWRXFAWBFWQXEBWBFYDWPXDWJWOCWBFVJ
      VGVKVKUMZVLVMVNWAFTJXHXAVSWAFYGTGWAYFTJYGTJEDVOYFTVOVLVPWTXHHFTYHVQVLVRVT
      $.
      $( [11-Nov-2013] $) $( [25-Aug-2006] $)
  $}

  ${
    $d x y z D $.  $d x y z X $.
    ismeti.0 $e |- X e. _V $.
    ismeti.1 $e |- D : ( X X. X ) --> RR $.
    ismeti.2 $e |- ( ( x e. X /\ y e. X ) -> ( ( x D y ) = 0 <-> x = y ) ) $.
    ismeti.3 $e |- ( ( x e. X /\ y e. X /\ z e. X ) ->
                  ( x D y ) <_ ( ( z D x ) + ( z D y ) ) ) $.
    $( Properties that determine a metric. $)
    ismeti $p |- D e. Met $=
      ( cme wcel cr cv co wceq wb wral wa cvv cdm cxp wf cc0 caddc cle wbr xpex
      fex mp2an fdmi dmeqi dmxpid eqtr2i ismet ax-mp 3expa ralrimiva jca rgen2a
      mpbir2an ) DJKZEEUAZLDUBZAMZBMZDNZUCOVDVEOPZVFCMZVDDNVHVEDNUDNUEUFZCEQZRZ
      BEQAEQZDSKZVAVCVLRPVCVBSKVMGEEFFUGVBLSDUHUIABCSDEDTZTVBTEVNVBVBLDGUJUKEUL
      UMUNUOGVKABEVDEKZVEEKZRZVGVJHVQVICEVOVPVHEKVIIUPUQURUSUT $.
      $( [5-Sep-2007] $) $( [17-Nov-2006] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d z C $.  $d x y z D $.  $d x y z X $.
    metf.1 $e |- X = dom dom D $.
    $( Lemma for ~ metf and others. $)
    metflem $p |- ( D e. Met -> ( D : ( X X. X ) --> RR /\
                  A. x e. X A. y e. X ( ( ( x D y ) = 0 <-> x = y ) /\
                  A. z e. X ( x D y ) <_ ( ( z D x ) + ( z D y ) ) ) ) ) $=
      ( cme wcel cxp cr wf cv co cc0 wceq wb caddc cle wral wa wbr ismet ibi )
      DGHEEIJDKALZBLZDMZNOUDUEOPUFCLZUDDMUGUEDMQMRUACESTBESAESTABCGDEFUBUC $.
      $( [4-Sep-2007] $) $( [30-Aug-2006] $)

    $( Mapping of the distance function of a metric space. $)
    metf $p |- ( D e. Met -> D : ( X X. X ) --> RR ) $=
      ( vx vy vz cme wcel cxp cr wf cv co cc0 wceq wb caddc cle wbr wral simpld
      wa metflem ) AGHBBIJAKDLZELZAMZNOUDUEOPUFFLZUDAMUGUEAMQMRSFBTUBEBTDBTDEFA
      BCUCUA $.
      $( [4-Sep-2007] $) $( [30-Aug-2006] $)

    $( The base set of a metric subspace. $)
    metssba $p |- ( D e. Met -> ( X i^i Y ) = dom dom ( D |` ( Y X. Y ) ) ) $=
      ( cme wcel cxp cres cdm cin dmres cr wf wceq metf fdm ineq2d syl5eq dmeqd
      syl dmxpin incom eqtri syl6req ) AEFZACCGZHIZIUFBBGZJZIZBCJZUEUGUIUEUGUFA
      IZJUIAUFKUEULUHUFUEUHLAMULUHNABDOUHLAPTQRSUJCBJUKCBUACBUBUCUD $.
      $( [30-Jan-2008] $)

    $( The base set of a metric subspace. $)
    metssba2 $p |- ( ( D e. Met /\ Y C_ X ) ->
                 Y = dom dom ( D |` ( Y X. Y ) ) ) $=
      ( wss cme wcel cin cxp cres cdm wceq dfss biimpi syl6eq metssba sylan9eqr
      incom ) CBEZAFGCBCHZACCIJKKSCCBHZTSCUALCBMNCBROABCDPQ $.
      $( [31-Jan-2008] $)

    $( Closure of the distance function of a metric space.  Part of Property M1
       of [Kreyszig] p. 3. $)
    metcl $p |- ( ( D e. Met /\ A e. X /\ B e. X ) -> ( A D B ) e. RR ) $=
      ( cme wcel cxp cr wf co metf fovrn syl3an1 ) CFGDDHICJADGBDGABCKIGCDELABI
      DDCMN $.
      $( [5-Sep-2007] $) $( [30-Aug-2006] $)

    $( The value of a metric is zero iff its arguments are equal.  Property M2
       of [Kreyszig] p. 4. $)
    meteq0 $p |- ( ( D e. Met /\ A e. X /\ B e. X )
                 -> ( ( A D B ) = 0 <-> A = B ) ) $=
      ( vx vy vz cme wcel co cc0 wceq wb cv wral wa ralimi eqeq1d bibi12d caddc
      cle wbr wi cxp cr wf metflem simprd simpl oveq1 eqeq1 oveq2 eqeq2 rcla42v
      com12 3syl 3impib ) CIJZADJZBDJZABCKZLMZABMZNZUSFOZGOZCKZLMZVFVGMZNZVHHOZ
      VFCKVLVGCKUAKUBUCHDPZQZGDPZFDPZVKGDPZFDPZUTVAQZVEUDUSDDUEUFCUGVPFGHCDEUHU
      IVOVQFDVNVKGDVKVMUJRRVSVRVEVKVEAVGCKZLMZAVGMZNFGABDDVFAMZVIWAVJWBWCVHVTLV
      FAVGCUKSVFAVGULTVGBMZWAVCWBVDWDVTVBLVGBACUMSVGBAUNTUOUPUQUR $.
      $( [5-Sep-2007] $) $( [30-Aug-2006] $)

    $( Triangle inequality for the distance function of a metric space. $)
    mettri2 $p |- ( ( D e. Met /\ ( C e. X /\ A e. X /\ B e. X ) )
                 -> ( A D B ) <_ ( ( C D A ) + ( C D B ) ) ) $=
      ( vx vy vz wcel w3a co caddc cle wbr cv wral wceq oveq1 oveq2 cme cc0 cxp
      wi wb wa cr metflem simprd simpr ralimi syl oveq1d breq12d oveq2d oveq12d
      wf breq2d rcla43v syl5 3comr impcom ) CEJZAEJZBEJZKDUAJZABDLZCADLZCBDLZML
      ZNOZVDVEVCVFVKUDVFGPZHPZDLZIPZVLDLZVOVMDLZMLZNOZIEQZHEQZGEQZVDVEVCKVKVFVN
      UBRVLVMRUEZVTUFZHEQZGEQZWBVFEEUCUGDUQWFGHIDEFUHUIWEWAGEWDVTHEWCVTUJUKUKUL
      VSVKAVMDLZVOADLZVQMLZNOVGWHVOBDLZMLZNOGHIABCEEEVLARZVNWGVRWINVLAVMDSWLVPW
      HVQMVLAVODTUMUNVMBRZWGVGWIWKNVMBADTWMVQWJWHMVMBVODTUOUNVOCRZWKVJVGNWNWHVH
      WJVIMVOCADSVOCBDSUPURUSUTVAVB $.
      $( [16-Sep-2007] $) $( [30-Aug-2006] $)

    $( MAY BE REVISED - replace with mettri2 everywhere; make obsolete $)
    $( Triangle inequality for the distance function of a metric space. $)
    mettri4 $p |- ( ( ( D e. Met /\ A e. X ) /\ ( B e. X /\ C e. X ) )
                 -> ( A D B ) <_ ( ( C D A ) + ( C D B ) ) ) $=
      ( vx vy vz wcel wa co caddc cle wbr cv wral wceq oveq1 oveq2 cme wi wb cr
      w3a cc0 cxp metflem simprd simpr ralimi syl oveq1d breq12d oveq2d oveq12d
      wf breq2d rcla43v syl5 3expib com3r imp31 ) DUAJZAEJZBEJZCEJZKZABDLZCADLZ
      CBDLZMLZNOZVEVHVDVMVEVFVGVDVMUBVDGPZHPZDLZIPZVNDLZVQVODLZMLZNOZIEQZHEQZGE
      QZVEVFVGUEVMVDVPUFRVNVORUCZWBKZHEQZGEQZWDVDEEUGUDDUQWHGHIDEFUHUIWGWCGEWFW
      BHEWEWBUJUKUKULWAVMAVODLZVQADLZVSMLZNOVIWJVQBDLZMLZNOGHIABCEEEVNARZVPWIVT
      WKNVNAVODSWNVRWJVSMVNAVQDTUMUNVOBRZWIVIWKWMNVOBADTWOVSWLWJMVOBVQDTUOUNVQC
      RZWMVLVINWPWJVJWLVKMVQCADSVQCBDSUPURUSUTVAVBVC $.
      $( [15-Sep-2007] $) $( [30-Aug-2006] $)

    $( The distance function of a metric space is zero if its arguments are
       equal.  Definition 14-1.1(a) of [Gleason] p. 223. $)
    met0 $p |- ( ( D e. Met /\ A e. X ) -> ( A D A ) = 0 ) $=
      ( cme wcel wa co cc0 wceq eqid wb meteq0 3anidm23 mpbiri ) BEFZACFZGAABHI
      JZAAJZAKPQRSLAABCDMNO $.
      $( [7-Sep-2007] $) $( [30-Aug-2006] $)

    $( The distance function of a metric space is symmetric.  Definition
       14-1.1(c) of [Gleason] p. 223. $)
    metsym $p |- ( ( D e. Met /\ A e. X /\ B e. X ) ->
                  ( A D B ) = ( B D A ) ) $=
      ( wcel w3a co cle wbr cr wa metcl 3com23 caddc mettri4 anabsan2 3impa cc0
      wceq cme wb letri3 syl2anc met0 3adant2 oveq2d recnd addid1 eqtrd breqtrd
      cc syl 3adant3 mpbir2and ) CUAFZADFZBDFZGZABCHZBACHZTZUTVAIJZVAUTIJZUSUTK
      FVAKFZVBVCVDLUBABCDEMZUPURUQVEBACDEMZNUTVAUCUDUSUTVABBCHZOHZVAIUPUQURUTVI
      IJZUPUQLURVJABBCDEPQRUSVIVASOHZVAUSVHSVAOUPURVHSTUQBCDEUEUFUGUPURUQVKVATZ
      UPURUQGZVAULFVLVMVAVGUHVAUIUMNUJUKUSVAUTAACHZOHZUTIUPURUQVAVOIJZUPURUQVPU
      PURLUQVPBAACDEPQRNUSVOUTSOHZUTUSVNSUTOUPUQVNSTURACDEUEUNUGUSUTULFVQUTTUSU
      TVFUHUTUIUMUJUKUO $.
      $( [18-Sep-2007] $) $( [27-Aug-2006] $)

    $( Triangle inequality for the distance function of a metric space.
       Definition 14-1.1(d) of [Gleason] p. 223. $)
    mettri $p |- ( ( D e. Met /\ ( A e. X /\ B e. X /\ C e. X ) )
              -> ( A D B ) <_ ( ( A D C ) + ( C D B ) ) ) $=
      ( cme wcel w3a wa co caddc cle wbr wi mettri2 expcom 3coml impcom wceq
      metsym 3adant3r2 oveq1d breqtrrd ) DGHZAEHZBEHZCEHZIZJZABDKZCADKZCBDKZLKZ
      ACDKZUMLKMUIUEUKUNMNZUHUFUGUEUPOUEUHUFUGIUPABCDEFPQRSUJUOULUMLUEUFUHUOULT
      UGACDEFUAUBUCUD $.
      $( [12-Nov-2007] $) $( [27-Aug-2006] $)

    $( Triangle inequality for the distance function of a metric space. $)
    mettri3 $p |- ( ( D e. Met /\ ( A e. X /\ B e. X /\ C e. X ) )
              -> ( A D B ) <_ ( ( A D C ) + ( B D C ) ) ) $=
      ( cme wcel w3a wa caddc cle mettri wceq metsym 3adant3r1 oveq2d breqtrrd
      co ) DGHZAEHZBEHZCEHZIJZABDSACDSZCBDSZKSUEBCDSZKSLABCDEFMUDUGUFUEKTUBUCUG
      UFNUABCDEFOPQR $.
      $( [29-Jan-2008] $) $( [13-Mar-2007] $)
    $( Reverse triangle inequality for the distance function of a metric
       space.  (Contributed by Mario Carneiro, 5-May-2014.) $)
    metrtri $p |- ( ( D e. Met /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                    ( abs ` ( ( A D C ) - ( B D C ) ) ) <_ ( A D B ) ) $=
      ( cme wcel wa co cmin cle wbr caddc cr wb metcl syl3anc cc recnd w3a cabs
      cfv simpl simpr1 simpr3 simpr2 absdifle mettri2 lesubadd2 mpbird syl13anc
      mettri wceq addcom syl2anc breqtrd mpbir2and ) DGHZAEHZBEHZCEHZUAZIZACDJZ
      BCDJZKJUBUCABDJZLMZVFVGKJVELMZVEVFVGNJZLMZVDVEOHZVFOHZVGOHZVHVIVKIPVDUSUT
      VBVLUSVCUDZUSUTVAVBUEZUSUTVAVBUFZACDEFQRZVDUSVAVBVMVOUSUTVAVBUGZVQBCDEFQR
      ZVDUSUTVAVNVOVPVSABDEFQRZVEVFVGUHRVDVIVFVGVENJLMZBCADEFUIVDVMVNVLVIWBPVTW
      AVRVFVGVEUJRUKVDVEVGVFNJZVJLVDUSUTVBVAVEWCLMVOVPVQVSACBDEFUMULVDVGSHVFSHW
      CVJUNVDVGWATVDVFVTTVGVFUOUPUQUR $.
      $( [5-May-2014] $)

    $( The distance function of a metric space is nonnegative. $)
    metge0 $p |- ( ( D e. Met /\ A e. X /\ B e. X ) ->
                  0 <_ ( A D B ) ) $=
      ( cme wcel w3a co cr cc0 c2 cmul cle wbr metcl caddc wi mettri4 wceq 3imp
      exp43 pm2.43d com23 3adant2 cc recnd 2times syl eqcomd 3brtr3d 2re wa clt
      met0 2pos prodge0 mpanr1 mpanl1 syl2anc ) CFGZADGZBDGZHZABCIZJGZKLVEMIZNO
      ZKVENOZABCDEPZVDBBCIZVEVEQIZKVGNVAVBVCVKVLNOZVAVCVBVMVAVCVBVMRVAVCVCVBVMB
      BACDESUBUCUDUAVAVCVKKTVBBCDEUOUEVDVGVLVDVEUFGVGVLTVDVEVJUGVEUHUIUJUKLJGZV
      FVHVIULVNVFUMKLUNOVHVIUPLVEUQURUSUT $.
      $( [26-Sep-2007] $) $( [27-Aug-2006] $)

    $( The distance function of a metric space is positive for unequal points.
       Definition 14-1.1(b) of [Gleason] p. 223 and its converse. $)
    metgt0 $p |- ( ( D e. Met /\ A e. X /\ B e. X ) ->
                ( A =/= B <-> 0 < ( A D B ) ) ) $=
      ( cme wcel w3a cc0 co clt wbr cle wne wa cr wb 0re metcl ltlen biantrurd
      sylancr metge0 meteq0 necon3bid 3bitr2rd ) CFGADGBDGHZIABCJZKLZIUHMLZUHIN
      ZOZUKABNUGIPGUHPGUIULQRABCDESIUHTUBUGUJUKABCDEUCUAUGUHIABABCDEUDUEUF $.
      $( [5-Oct-2007] $) $( [27-Aug-2006] $)

    $( A metric space is nonempty iff its base set is nonempty. $)
    metn0 $p |- ( D e. Met -> ( D =/= (/) <-> X =/= (/) ) ) $=
      ( cme wcel c0 wceq cdm cxp cr wf wrel wb metf frel reldm0 fdm relxp releq
      3syl mpbiri syl bitrd eqeq1i syl6bbr necon3bid ) ADEZAFBFUGAFGZAHZHZFGZBF
      GUGUHUIFGZUKUGBBIZJAKZALUHULMABCNZUMJAOAPTUGUNUILZULUKMUOUNUIUMGZUPUMJAQU
      QUPUMLBBRUIUMSUAUBUIPTUCBUJFCUDUEUF $.
      $( [22-Jun-2008] $) $( [4-Oct-2007] $)

    ${
      $d x y z R $.
      metreslem.2 $e |- A = dom dom ( D |` ( R X. R ) ) $.
      $( Lemma for ~ metres .  (Contributed by FL, 10-Nov-2006.) $)
      metreslem $p |- ( ( D e. Met /\ R C_ X ) ->
                     ( D |` ( R X. R ) ) e. Met ) $=
        ( vx vy vz cme wcel wss wa cxp cr co wceq wb wral cdm cres wf cc0 caddc
        cle wbr metflem xpss12 anidms fssres sylan2 ssel ssralv anim2d ralimdv2
        cv imim12d ovres eqeq1d bibi1d adantl oveqan12d anandis ancoms ralbidva
        breq12d anbi12d ralbiia syl6ibr impcom anandirs sylan cvv resexg adantr
        anim12i ismet syl fdm sseqtr4d ssdmres sylib dmeqd dmxpid syl6eq syl5eq
        metf xpeq1 xpeq2 eqtrd feq2d raleq anbi2d raleqbi1dv bitrd mpbird ) BJK
        ZCDLZMZBCCNZUAZJKZWTOXAUBZGUPZHUPZXAPZUCQZXDXEQZRZXFIUPZXDXAPZXJXEXAPZU
        DPZUEUFZICSZMZHCSZGCSZMZWQDDNZOBUBZXDXEBPZUCQZXHRZYBXJXDBPZXJXEBPZUDPZU
        EUFZIDSZMZHDSZGDSZMWRXSGHIBDEUGYAYLWRXSYAWRMZXCYLWRMXRWRYAWTXTLZXCWRYNC
        DCDUHUIZXTOWTBUJUKWRYLXRWRYLYDYHICSZMZHCSZGCSXRWRYKYRGDCWRXDCKZXDDKYKYR
        CDXDULWRYJYQHDCWRXECKZXEDKYJYQCDXEULWRYIYPYDYHICDUMUNUQUOUQUOXQYRGCYSXP
        YQHCYSYTMZXIYDXOYPUUAXGYCXHUUAXFYBUCXDXECCBURZUSUTUUAXNYHICXJCKZUUAXNYH
        RUUCUUAMXFYBXMYGUEUUAXFYBQUUCUUBVAUUCYSYTXMYGQUUCYSMUUCYTMXKYEXLYFUDXJX
        DCCBURXJXECCBURVBVCVFVDVEVGVEVHVIVJVPVKVLWSXBAANZOXAUBZXIXNIASZMZHASZGA
        SZMZXSWSXAVMKZXBUUJRWQUUKWRBWTJVNVOGHIVMXAAFVQVRWSACQZUUJXSRWSAXATZTZCF
        WQYAWRUUNCQBDEWGYMUUNWTTCYMUUMWTYMWTBTZLUUMWTQYMWTXTUUOWRYNYAYOVAYAUUOX
        TQWRXTOBVSVOVTWTBWAWBWCCWDWEVLWFUULUUEXCUUIXRUULUUDWTOXAUULUUDCANWTACAW
        HACCWIWJWKUUHXQGACUUGXPHACUULUUFXOXIXNIACWLWMWNWNVGVRWOWP $.
        $( [20-Sep-2007] $) $( [12-Oct-2006] $)
    $}
  $}

  $( A restriction of a metric is a metric. $)
  metres $p |- ( D e. Met -> ( D |` ( R X. R ) ) e. Met ) $=
    ( cme wcel cxp cres cdm cin cr wf wceq eqid metf fdm resdmres dmres reseq2i
    eqtr3i ineq2 inxp syl6eq reseq2d syl5eq 3syl inss2 metreslem mpan2 eqeltrd
    wss ) ACDZABBEZFZABAGZGZHZUOEZFZCUJUNUNEZIAJUMURKZULUQKAUNUNLZMURIANUSULAUK
    UMHZFZUQAULGZFULVBAUKOVCVAAAUKPQRUSVAUPAUSVAUKURHUPUMURUKSBBUNUNTUAUBUCUDUJ
    UOUNUIUQCDBUNUEUQGGZAUOUNUTVDLUFUGUH $.
    $( [26-Aug-2007] $)

  ${
    metss.1 $e |- X = dom dom C $.
    metss.2 $e |- Y = dom dom D $.
    $( If two metrics are in a subset relationship, so are their base sets. $)
    metss $p |- ( C C_ D -> X C_ Y ) $=
      ( wss cdm dmss syl 3sstr4g ) ABGZAHZHZBHZHZCDLMOGNPGABIMOIJEFK $.
      $( [29-May-2007] $)
  $}

  ${
    $d x y z $.
    $( The empty metric. $)
    0met $p |- (/) e. Met $=
      ( vx vy vz c0 cme wcel cxp cr wf cv co cc0 wceq wb caddc wral cvv cdm dm0
      wa cle wbr 0ex dmeqi eqtr2i ismet ax-mp f0 xp0r feq2i mpbir ral0 mpbir2an
      ) DEFZDDGZHDIZAJZBJZDKZLMUQURMNUSCJZUQDKUTURDKOKUAUBCDPTBDPZADPZDQFUNUPVB
      TNUCABCQDDDRZRVCDVCDSUDSUEUFUGUPDHDIHUHUODHDDUIUJUKVAAULUM $.
      $( [24-Oct-2007] $) $( [30-Aug-2006] $)
  $}

  ${
    metxplem1.1 $e |- D e. Met $.
    metxplem1.2 $e |- X = dom dom D $.
    ${
      metxplem1.4 $e |- F = ( 1st ` R ) $.
      metxplem1.5 $e |- H = ( 1st ` S ) $.
      $( Lemma for ~ metxp . $)
      metxplem1 $p |- ( ( R e. ( X X. Y ) /\ S e. ( X X. Y ) ) ->
           ( F D H ) e. RR ) $=
        ( cxp wcel c1st cfv c2nd cvv wa elxp7 simprbi co cr simpld syl5eqel cme
        metcl mp3an1 syl2an ) BFGLZMZDFMZEFMZDEAUAUBMZCUIMZUJDBNOZFJUJUOFMZBPOG
        MZUJBQQLZMUPUQRBFGSTUCUDUNECNOZFKUNUSFMZCPOGMZUNCURMUTVARCFGSTUCUDAUEMU
        KULUMHDEAFIUFUGUH $.
        $( [20-Jul-2007] $)
    $}

    ${
      metxplem2.4 $e |- G = ( 2nd ` R ) $.
      metxplem2.5 $e |- J = ( 2nd ` S ) $.
      $( Lemma for ~ metxp . $)
      metxplem2 $p |- ( ( R e. ( Y X. X ) /\ S e. ( Y X. X ) ) ->
                     ( G D J ) e. RR ) $=
        ( cxp wcel c2nd cfv c1st cvv wa elxp7 simprbi co cr simprd syl5eqel cme
        metcl mp3an1 syl2an ) BGFLZMZDFMZEFMZDEAUAUBMZCUIMZUJDBNOZFJUJBPOGMZUOF
        MZUJBQQLZMUPUQRBGFSTUCUDUNECNOZFKUNCPOGMZUSFMZUNCURMUTVARCGFSTUCUDAUEMU
        KULUMHDEAFIUFUGUH $.
        $( [20-Jul-2007] $)
    $}

    ${
      metxplem3.4 $e |- ( ph -> F e. RR ) $.
      metxplem3.5 $e |- ( ph -> G e. RR ) $.
      metxplem3.6 $e |- ( ph -> ( C e. X /\ A e. X /\ B e. X ) ) $.
      $( Lemma for ~ metxp . $)
      metxplem3 $p |- ( ph -> ( ( C D A ) <_ F
          -> ( ( C D B ) <_ G -> ( A D B ) <_ ( F + G ) ) ) ) $=
        ( co cle wbr cr wcel metcl adantr wa w3a cme mp3an1 3adant1 syl 3adant3
        caddc 3adant2 readdcl syl2anc mettri2 sylancr le2add syl22anc imp letrd
        wi exp32 ) ADBENZFOPZDCENZGOPZBCENZFGUHNZOPAVAVCUAZUAVDUTVBUHNZVEAVDQRZ
        VFADHRZBHRZCHRZUBZVHMVJVKVHVIEUCRZVJVKVHIBCEHJSUDUEUFTAVGQRZVFAUTQRZVBQ
        RZVNAVLVOMVIVJVOVKVMVIVJVOIDBEHJSUDUGUFZAVLVPMVIVKVPVJVMVIVKVPIDCEHJSUD
        UIUFZUTVBUJUKTAVEQRZVFAFQRZGQRZVSKLFGUJUKTAVDVGOPZVFAVMVLWBIMBCDEHJULUM
        TAVFVGVEOPZAVOVPVTWAVFWCURVQVRKLUTVBFGUNUOUPUQUS $.
        $( [11-Jul-2007] $)
    $}
  $}

  ${
    $d x y z B $.  $d x y z C $.  $d u v w D $.  $d x y z R $.  $d x y z S $.
    $d u v w x y z X $.  $d u v w x y z Y $.
    metxp.1 $e |- X = dom dom B $.
    metxp.3 $e |- Y = dom dom C $.
    metxp.5 $e |- B e. Met $.
    metxp.6 $e |- C e. Met $.
    metxp.7 $e |- D = ( x e. ( X X. Y ) , y e. ( X X. Y ) |->
                        sup ( { ( ( 1st ` x ) B ( 1st ` y ) ) ,
                              ( ( 2nd ` x ) C ( 2nd ` y ) ) } , RR , < ) ) $.
    ${
      $d x y z F $.  $d x y z G $.  $d y z H $.  $d y z J $.
      metxpval.8 $e |- F = ( 1st ` R ) $.
      metxpval.9 $e |- G = ( 2nd ` R ) $.
      metxpval.10 $e |- H = ( 1st ` S ) $.
      metxpval.11 $e |- J = ( 2nd ` S ) $.
      $( Value of the distance function of the direct product of two metric
         spaces.  Based on Definition 14-1.5 of [Gleason] p. 225. $)
      metxpdval $p |- ( ( R e. ( X X. Y ) /\ S e. ( X X. Y ) ) -> ( R D S ) =
          if ( ( G C J ) < ( F B H ) , ( F B H ) , ( G C J ) ) ) $=
        ( cxp wcel wa co cpr cr clt csup wbr cif cv c1st cfv c2nd fveq2 syl6eqr
        wceq oveq1d preq12d supeq1d oveq2d ltso supex metxplem1 metxplem2 suppr
        ovmpt2 syl2anc eqtrd ) FLMUCZUDGVLUDUEZFGEUFHJCUFZIKDUFZUGZUHUIUJZVOVNU
        IUKVNVOULZABFGVLVLAUMZUNUOZBUMZUNUOZCUFZVSUPUOZWAUPUOZDUFZUGZUHUIUJVQEH
        WBCUFZIWEDUFZUGZUHUIUJVSFUSZUHWGWJUIWKWCWHWFWIWKVTHWBCWKVTFUNUOHVSFUNUQ
        SURUTWKWDIWEDWKWDFUPUOIVSFUPUQTURUTVAVBWAGUSZUHWJVPUIWLWHVNWIVOWLWBJHCW
        LWBGUNUOJWAGUNUQUAURVCWLWEKIDWLWEGUPUOKWAGUPUQUBURVCVAVBRUHVPUIVDVEVIVM
        VNUHUDVOUHUDVQVRUSCFGHJLMPNSUAVFDFGIKMLQOTUBVGUHVNVOUIVDVHVJVK $.
        $( [5-May-2014] $) $( [20-Jul-2007] $)

      $( Value of the distance function of the direct product of two metric
         spaces.  Based on Definition 14-1.5 of [Gleason] p. 225.  (Contributed
         by Mario Carneiro, 5-May-2014.) $)
      metxpdval2 $p |- ( ( R e. ( X X. Y ) /\ S e. ( X X. Y ) ) -> ( R D S ) =
          if ( ( G C J ) <_ ( F B H ) , ( F B H ) , ( G C J ) ) ) $=
        ( cxp wcel wa co clt wbr cif cle metxpdval wceq wi eqtr4i ifeq1 3eqtr4a
        ifid a1i wne wb metxplem2 metxplem1 ltlen syl2anc adantr simpr biantrud
        cr bitr4d ifbid ex pm2.61dne eqtrd ) FLMUCZUDGVNUDUEZFGEUFIKDUFZHJCUFZU
        GUHZVQVPUIZVPVQUJUHZVQVPUIZABCDEFGHIJKLMNOPQRSTUAUBUKVOVSWAULZVQVPVQVPU
        LZWBUMVOWCVRVPVPUIZVTVPVPUIZVSWAWDVPWEVRVPUQVTVPUQUNVRVQVPVPUOVTVQVPVPU
        OUPURVOVQVPUSZWBVOWFUEZVRVTVQVPWGVRVTWFUEZVTVOVRWHUTZWFVOVPVHUDVQVHUDWI
        DFGIKMLQOTUBVACFGHJLMPNSUAVBVPVQVCVDVEWGWFVTVOWFVFVGVIVJVKVLVM $.
        $( [5-May-2014] $)

      $( One case of the value of the distance function of the direct product
         of two metric spaces.  Based on Definition 14-1.5 of [Gleason]
         p. 225.  (Revised by Mario Carneiro, 5-May-2014.) $)
      metxptval $p |- ( ( ( R e. ( X X. Y ) /\ S e. ( X X. Y ) ) /\
              ( G C J ) <_ ( F B H ) ) ->
                 ( R D S ) = ( F B H ) ) $=
        ( cxp wcel wa co cle wbr cif metxpdval2 iftrue sylan9eq ) FLMUCZUDGUMUD
        UEIKDUFZHJCUFZUGUHZFGEUFUPUOUNUIUOABCDEFGHIJKLMNOPQRSTUAUBUJUPUOUNUKUL
        $.
        $( [5-May-2014] $) $( [20-Jul-2007] $)

      $( One case of the value of the distance function of the direct product
         of two metric spaces.  Based on Definition 14-1.5 of [Gleason]
         p. 225. $)
      metxpfval $p |- ( ( ( R e. ( X X. Y ) /\ S e. ( X X. Y ) ) /\
           ( F B H ) <_ ( G C J ) ) ->
                 ( R D S ) = ( G C J ) ) $=
        ( cxp wcel wa cle wbr clt cif wceq metxpdval adantr metxplem1 metxplem2
        co wn cr wb lenlt syl2anc biimpa iffalse syl eqtrd ) FLMUCZUDGVEUDUEZHJ
        CUOZIKDUOZUFUGZUEZFGEUOZVHVGUHUGZVGVHUIZVHVFVKVMUJVIABCDEFGHIJKLMNOPQRS
        TUAUBUKULVJVLUPZVMVHUJVFVIVNVFVGUQUDVHUQUDVIVNURCFGHJLMPNSUAUMDFGIKMLQO
        TUBUNVGVHUSUTVAVLVGVHVBVCVD $.
        $( [20-Jul-2007] $)
    $}

    $( Closure of the distance function of the direct product of two metric
       spaces.  Based on Definition 14-1.5 of [Gleason] p. 225. $)
    metxpcl $p |- ( ( R e. ( X X. Y ) /\ S e. ( X X. Y ) ) ->
               ( R D S ) e. RR ) $=
      ( wcel wa co cr cfv eqid cxp c2nd c1st metxplem2 metxplem1 cle wbr adantr
      metxptval eqeltrd metxpfval lecasei ) FHIUAZOGUMOPZFGEQZROFUBSZGUBSZDQZFU
      CSZGUCSZCQZDFGUPUQIHMKUPTZUQTZUDZCFGUSUTHILJUSTZUTTZUEZUNURVAUFUGZPUOVARA
      BCDEFGUSUPUTUQHIJKLMNVEVBVFVCUIUNVAROVHVGUHUJUNVAURUFUGZPUOURRABCDEFGUSUP
      UTUQHIJKLMNVEVBVFVCUKUNURROVIVDUHUJUL $.
      $( [9-Jul-2007] $)

    $( Lemma for ~ metxp .  Triangle inequality. $)
    metxplem4 $p |- ( ( u e. ( X X. Y ) /\ w e. ( X X. Y ) /\
              v e. ( X X. Y ) ) -> ( w D v ) <_ ( ( u D w ) + ( u D v ) ) ) $=
      ( wcel co cle wbr wa cv cxp w3a caddc c2nd c1st cr eqid metxplem2 3adant1
      cfv metxplem1 wceq metxptval 3adantl1 3adant3 3adant2 leid syl cvv simpld
      elxp7 simprbi 3anim123i metxplem3 mp2d adantr 3adantl2 oveq2d breqtrrd wi
      mpd metxpfval lecasei 3adantl3 oveq1d mpid adantlr imp31 eqbrtrd simprd
      imp ) EUAZIJUBZPZCUAZWDPZDUAZWDPZUCZWFWHHQZWCWFHQZWCWHHQZUDQZRSWFUEUKZWHU
      EUKZGQZWFUFUKZWHUFUKZFQZWGWIWQUGPWEGWFWHWOWPJINLWOUHZWPUHZUIUJWGWIWTUGPWE
      FWFWHWRWSIJMKWRUHZWSUHZULUJWJWQWTRSZTWKWTWNRWGWIXEWKWTUMWEABFGHWFWHWRWOWS
      WPIJKLMNOXCXAXDXBUNUOWJWTWNRSZXEWJXFWCUEUKZWOGQZWCUFUKZWRFQZWEWGXHUGPZWIG
      WCWFXGWOJINLXGUHZXAUIZUPZWEWGXJUGPZWIFWCWFXIWRIJMKXIUHZXCULZUPZWJXHXJRSZT
      ZWTXJWMUDQZWNRWJWTYARSZXSWJYBXGWPGQZXIWSFQZWEWIYCUGPZWGGWCWHXGWPJINLXLXBU
      IZUQZWEWIYDUGPZWGFWCWHXIWSIJMKXPXDULZUQZWJYCYDRSZTZWTXJYDUDQZYARWJWTYMRSZ
      YKWJXJXJRSZYDYDRSZYNWEWGYOWIWEWGTZXOYOXQXJURUSUPZWEWIYPWGWEWITZYHYPYIYDUR
      USUQZWJWRWSXIFXJYDIMKXRYJWEXIIPZWGWRIPZWIWSIPZWEUUAXGJPZWEWCUTUTUBZPUUAUU
      DTWCIJVBVCZVAWGUUBWOJPZWGWFUUEPUUBUUGTWFIJVBVCZVAWIUUCWPJPZWIWHUUEPUUCUUI
      TWHIJVBVCZVAVDZVEVFVGYLWMYDXJUDWEWIYKWMYDUMWGABFGHWCWHXIXGWSWPIJKLMNOXPXL
      XDXBUNVHZVIZVJWJYDYCRSZTZWTXJYCUDQZYARWJUUNWTUUPRSZWJYOUUNUUQVKYRWJWRWSXI
      FXJYCIMKXRYGUUKVEVLWBUUOWMYCXJUDWEWIUUNWMYCUMWGABFGHWCWHXIXGWSWPIJKLMNOXP
      XLXDXBVMVHZVIZVJVNVGXTWLXJWMUDWEWGXSWLXJUMWIABFGHWCWFXIXGWRWOIJKLMNOXPXLX
      CXAUNVOVPZVJWJXJXHRSZTZWTXHWMUDQZWNRUVBWTUVCRSYCYDWJYEUVAYGVGWJYHUVAYJVGU
      VBYKTWTXHYDUDQZUVCRUVBWTUVDRSZYKWJUVAUVEWJUVAYPUVEYTWJWRWSXIFXHYDIMKXNYJU
      UKVEVQWBVGWJYKUVCUVDUMUVAYLWMYDXHUDUULVIZVRVJUVBUUNTWTXHYCUDQZUVCRWJUVAUU
      NWTUVGRSWJWRWSXIFXHYCIMKXNYGUUKVEVSWJUUNUVCUVGUMUVAUUOWMYCXHUDUURVIZVRVJV
      NUVBWLXHWMUDWEWGUVAWLXHUMWIABFGHWCWFXIXGWRWOIJKLMNOXPXLXCXAVMVOVPZVJVNVGV
      TWJWTWQRSZTWKWQWNRWGWIUVJWKWQUMWEABFGHWFWHWRWOWSWPIJKLMNOXCXAXDXBVMUOWJWQ
      WNRSZUVJWJUVKXHXJXNXRXTWQYAWNRXTWQYARSYCYDWJYEXSYGVGWJYHXSYJVGXTYKTWQYMYA
      RWJXSYKWQYMRSWJWOWPXGGXJYDJNLXRYJWEUUDWGUUGWIUUIWEUUAUUDUUFWAWGUUBUUGUUHW
      AWIUUCUUIUUJWAVDZVEVSWJYKYAYMUMXSUUMVRVJXTUUNTWQUUPYARXTWQUUPRSZUUNWJXSUV
      MWJXSYCYCRSZUVMWEWIUVNWGYSYEUVNYFYCURUSUQZWJWOWPXGGXJYCJNLXRYGUVLVEVQWBVG
      WJUUNYAUUPUMXSUUSVRVJVNUUTVJUVBWQUVCWNRWJWQUVCRSZUVAWJUVPYCYDYGYJYLWQUVDU
      VCRWJYKWQUVDRSZWJXHXHRSZYKUVQVKWEWGUVRWIYQXKUVRXMXHURUSUPZWJWOWPXGGXHYDJN
      LXNYJUVLVEVLWBUVFVJUUOWQUVGUVCRWJWQUVGRSZUUNWJUVRUVNUVTUVSUVOWJWOWPXGGXHY
      CJNLXNYGUVLVEVFVGUVHVJVNVGUVIVJVNVGVTVN $.
      $( [11-Jul-2007] $)

    $( The direct product of two metric spaces.  Definition 14-1.5 of [Gleason]
       p. 225.  (Revised by Mario Carneiro, 5-May-2014.) $)
    metxp $p |- D e. Met $=
      ( wcel cr co cfv wa cc0 wceq wb vw vv vu cxp cdm cvv cme dmexg ax-mp dmex
      eqeltri xpex wf wfn cv wral ffnov c1st c2nd cpr csup ltso fnmpt2i metxpcl
      clt supex rgen2a mpbir2an wbr cif metxpdval eqeq1d wi iftrue adantl breq2
      biimpac adantll wn elxp7 simprbi simprd cle metge0 mp3an1 0re metcl lenlt
      eqid sylancr mpbid syl2an pm2.21d ad2antrr mpd sylbid metxplem1 metxplem2
      ex iffalse syl2anc simpld biantrud letri3 sylancl bitr4d sylan9bbr meteq0
      bi2anan9r an4s ancom xpopth syl5bb bitrd biimpd expdimp com23 sylbird imp
      pm2.61dan ifid met0 fveq2 oveq2d sylan9req ifeq12 anandirs sylan syl5reqr
      mpan adantr impbid caddc metxplem4 3coml ismeti ) UAUBUCEFGUDZFGFCUEZUEUF
      HYRCUGMZYRUFMJCUGUHUIUJUKGDUEZUEUFIYTDUGMZYTUFMKDUGUHUIUJUKULYQYQUDZNEUME
      UUBUNUAUOZUBUOZEOZNMZUBYQUPUAYQUPUAUBYQYQNEUQABYQYQAUOZURPBUOZURPCOUUGUSP
      UUHUSPDOUTZNVEVAELNUUIVEVBVFVCUUFUAUBYQABCDEUUCUUDFGHIJKLVDVGVHUUCYQMZUUD
      YQMZQZUUERSUUCUSPZUUDUSPZDOZUUCURPZUUDURPZCOZVEVIZUURUUOVJZRSZUUCUUDSZUUL
      UUEUUTRABCDEUUCUUDUUPUUMUUQUUNFGHIJKLUUPWIZUUMWIZUUQWIZUUNWIZVKVLUULUVAUV
      BUULUUSUVAUVBVMUULUUSQZUVAUURRSZUVBUUSUVAUVHTUULUUSUUTUURRUUSUURUUOVNVLVO
      UVGUVHUVBUVGUVHQUUORVEVIZUVBUUSUVHUVIUULUVHUUSUVIUURRUUOVEVPVQVRUULUVIUVB
      VMUUSUVHUULUVIUVBUUJUUMGMZUUNGMZUVIVSZUUKUUJUUPFMZUVJUUJUUCUFUFUDZMUVMUVJ
      QZUUCFGVTWAZWBUUKUUQFMZUVKUUKUUDUVNMUVQUVKQZUUDFGVTWAZWBUVJUVKQZRUUOWCVIZ
      UVLUUAUVJUVKUWAKUUMUUNDGIWDWEUVTRNMZUUONMZUWAUVLTWFUUAUVJUVKUWCKUUMUUNDGI
      WGWERUUOWHWJWKWLWMWNWOWSWPUULUUSVSZQUVAUUORSZUVBUWDUVAUWETUULUWDUUTUUORUU
      SUURUUOWTVLVOUULUWDUWEUVBVMZUULUWDUURUUOWCVIZUWFUULUURNMZUWCUWGUWDTCUUCUU
      DUUPUUQFGJHUVCUVEWQDUUCUUDUUMUUNGFKIUVDUVFWRUURUUOWHXAUULUWEUWGUVBUULUWEU
      WGUVBVMUULUWEQUWGUVHUVBUWEUWGUURRWCVIZUULUVHUUORUURWCVPUUJUVMUVQUWIUVHTUU
      KUUJUVMUVJUVPXBUUKUVQUVKUVSXBUVMUVQQZUWIUWIRUURWCVIZQZUVHUWJUWKUWIYSUVMUV
      QUWKJUUPUUQCFHWDWEXCUWJUWHUWBUVHUWLTYSUVMUVQUWHJUUPUUQCFHWGWEWFUURRXDXEXF
      WLXGUULUWEUVHUVBUULUWEUVHQZUVBUULUWMUUMUUNSZUUPUUQSZQZUVBUUJUVOUVRUWMUWPT
      ZUUKUVPUVSUVMUVQUVJUVKUWQUVTUWEUWNUWJUVHUWOUUAUVJUVKUWEUWNTKUUMUUNDGIXHWE
      YSUVMUVQUVHUWOTJUUPUUQCFHXHWEXIXJWLUWPUWOUWNQUULUVBUWNUWOXKUUCUUDFGFGXLXM
      XNXOXPWPWSXQXRXSWPXTUUJUVBUVAVMUUKUUJUVBUVAUUJUVBQRUUSRRVJZUUTUUSRYAUUJUV
      OUVBUWRUUTSZUVPUVMUVJUVBUWSUVMUVBQRUURSRUUOSUWSUVJUVBQUVMUVBRUUPUUPCOZUUR
      YSUVMUWTRSJUUPCFHYBYJUVBUUPUUQUUPCUUCUUDURYCYDYEUVJUVBRUUMUUMDOZUUOUUAUVJ
      UXARSKUUMDGIYBYJUVBUUMUUNUUMDUUCUUDUSYCYDYEUUSRUURRUUOYFWLYGYHYIWSYKYLXNU
      CUOZYQMUUJUUKUUEUXBUUCEOUXBUUDEOYMOWCVIABUAUBUCCDEFGHIJKLYNYOYP $.
      $( [5-May-2014] $) $( [20-Jun-2007] $)
  $}


