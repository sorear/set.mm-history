$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Basic metric space properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new constant symbols and define their syntactical properties. $)
  $c Met $.
  $c ball $.
  $c Open $.

  $( Extend class notation with the class of all metric spaces. $)
  cmt $a class Met $.

  $( Extend class notation with the metric space ball function. $)
  cbl $a class ball $.

  $( Extend class notation with a function mapping each metric space to the
     family of its open sets. $)
  copn $a class Open $.

  ${
    $d x y z w v u $.
    $( Define the (proper) class of all metric spaces.  Equivalent to
       Definition 14-1.1 of [Gleason] p. 223.  See ~ ismsg for the property
       "is a metric space."  The 4 properties in Gleason's definition are shown
       by ~ ms0 , ~ msgt0 , ~ mssym , and ~ mstri . $)
    df-ms $a |- Met = { <. x , y >. | ( y : ( x X. x ) --> RR /\
                 A. z e. x A. w e. x ( ( ( z y w ) = 0 <-> z = w ) /\
                 A. v e. x ( z y w ) <_ ( ( v y z ) + ( v y w ) ) ) ) } $.

    $( Define the metric space ball function.  See ~ blval for its value. $)
    df-bl $a |- ball = { <. x , y >. | ( x e. Met /\ y =
  { <. <. z , w >. , v >. | ( ( z e. ( 1st ` x ) /\ w e. RR ) /\ ( 0 < w /\ v =
                { u e. ( 1st ` x ) | ( z ( 2nd ` x ) u ) < w } ) ) } ) } $.

    $( Define a function whose value is the family of open sets of a metric
       space.  See ~ isopn for its main property. $)
    df-opn $a |- Open = { <. x , y >. | ( x e. Met /\ y = { z | ( z (_
            ( 1st ` x ) /\ A. w e. z E. v e. ran ( ball ` x ) (
            w e. v /\ v (_ z ) ) } ) } $.

    $( The class of all metric spaces is a relation. $)
    msrel $p |- Rel Met $=
      ( vx vy vz vw vv cmt wrel cv cxp cr wf co cc0 wceq wb caddc cle wbr wral
      wa copab relopab df-ms releqi mpbir ) FGAHZUFIJBHZKCHZDHZUGLZMNUHUINOUJEH
      ZUHUGLUKUIUGLPLQREUFSTDUFSCUFSTZABUAZGULABUBFUMABCDEUCUDUE $.
      $( [28-Aug-2006] $) $( [28-Aug-2006] $)
  $}

  ${
    $d v w x y z X $.  $d v w x y z D $.
    $( Express the predicate " ` <. X , D >. ` is a metric space" with
       underlying set ` X ` and distance function ` D ` . $)
    ismsg $p |- ( D e. A ->
                 ( <. X , D >. e. Met <-> ( D : ( X X. X ) --> RR /\
                 A. x e. X A. y e. X ( ( ( x D y ) = 0 <-> x = y ) /\
                 A. z e. X ( x D y ) <_ ( ( z D x ) + ( z D y ) ) ) ) ) ) $=
      ( vw vv wcel cop cmt cxp cr wf cv co cc0 wceq wb caddc cle wbr wral wa
      cvv df-br msrel brrelexi sylbir anim1i ancoms dmfex wo xpexr oridm sylib
      syl pm3.26 jca adantrr copab xpeq1 xpeq2 eqtrd feq2 raleq1 anbi2d raleqd
      anbi12d feq1 opreq eqeq1d bibi1d opreq12d breq12d ralbidv 2ralbidv
      opelopabg df-ms eleq2i syl5bb pm5.21nd ) EDIZFEJZKIZFFLZMENZAOZBOZEPZQRZW
      HWIRZSZWJCOZWHEPZWNWIEPZTPZUAUBZCFUCZUDZBFUCAFUCZUDZFUEIZWCUDZWEWCXDWEXCW
      CWEFEKUBXCFEKUFFEKUGUHUIUJUKWCWGXDXAWCWGUDZXCWCXEWFUEIZXCWFMDEULXFXCXCUMX
      CFFUEUNXCUOUPUQWCWGURUSUTXDWDGOZXGLZMHOZNZWHWIXIPZQRZWLSZXKWNWHXIPZWNWIXI
      PZTPZUAUBZCXGUCZUDZBXGUCZAXGUCZUDZGHVAZIXBWEYBWFMXINZXMXQCFUCZUDZBFUCZAFU
      CZUDXBGHFEUEDXGFRZXJYDYAYHYIXHWFRXJYDSYIXHFXGLWFXGFXGVBXGFFVCVDXHWFMXIVEU
      QXTYGAXGFXSYFBXGFYIXRYEXMXQCXGFVFVGVHVHVIXIERZYDWGYHXAWFMXIEVJYJYFWTABFFY
      JXMWMYEWSYJXLWKWLYJXKWJQWHWIXIEVKZVLVMYJXQWRCFYJXKWJXPWQUAYKYJXNWOXOWPTWN
      WHXIEVKWNWIXIEVKVNVOVPVIVQVIVRKYCWDGHABCVSVTWAWB $.
      $( [28-Aug-2006] $) $( [27-Aug-2006] $)
  $}

  ${
    $d x y z D $.  $d x y z X $.
    ismsi.0 $e |- D e. V $.
    ismsi.1 $e |- D : ( X X. X ) --> RR $.
    ismsi.2 $e |- ( ( x e. X /\ y e. X ) -> ( ( x D y ) = 0 <-> x = y ) ) $.
    ismsi.3 $e |- ( ( x e. X /\ y e. X /\ z e. X ) ->
                  ( x D y ) <_ ( ( z D x ) + ( z D y ) ) ) $.
    ismsi.4 $e |- M = <. X , D >. $.
    $( Properties that determine a metric space. $)
    ismsi $p |- M e. Met $=
      ( cop cmt wcel cxp cr wf cv co cc0 wceq wb caddc cle wbr wral wa cvv
      ismsg ax-mp 3expa r19.21aiva jca rgen2 mpbir2an eqeltr ) EFDLZMKUQMNZFFOP
      DQZARZBRZDSZTUAUTVAUAUBZVBCRZUTDSVDVADSUCSUDUEZCFUFZUGZBFUFAFUFZDUHNURUSV
      HUGUBGABCUHDFUIUJHVGABFUTFNZVAFNZUGZVCVFIVKVECFVIVJVDFNVEJUKULUMUNUOUP $.
      $( [17-Nov-2006] $) $( [17-Nov-2006] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d z C $.  $d v w x y z D $.  $d v w M $.
    $d v w x y z X $.  $d x y z R $.
    msf.1 $e |- X = ( 1st ` M ) $.
    msf.2 $e |- D = ( 2nd ` M ) $.
    $( Lemma for ~ msf and others. $)
    msflem $p |- ( M e. Met -> ( D :  ( X X. X ) --> RR /\
                  A. x e. X A. y e. X ( ( ( x D y ) = 0 <-> x = y ) /\
                  A. z e. X ( x D y ) <_ ( ( z D x ) + ( z D y ) ) ) ) ) $=
      ( vw vv cmt wcel cv cxp cr wf co cc0 wceq wb caddc cle wbr wral wa copab
      df-ms eleq2i c1st cfv eqeq2i xpeq1 xpeq2 eqtrd feq2 syl raleq1 anbi2d
      raleqd anbi12d sylbir c2nd feq1 opreq eqeq1d bibi1d opreq12d breq12d
      ralbidv 2ralbidv elopabi sylbi ) EKLEIMZVMNZOJMZPZAMZBMZVOQZRSZVQVRSZTZVS
      CMZVQVOQZWCVRVOQZUAQZUBUCZCVMUDZUEZBVMUDZAVMUDZUEZIJUFZLFFNZODPZVQVRDQZRS
      ZWATZWPWCVQDQZWCVRDQZUAQZUBUCZCFUDZUEZBFUDAFUDZUEZKWMEIJABCUGUHWLWNOVOPZW
      BWGCFUDZUEZBFUDZAFUDZUEZXFIJEVMEUIUJZSVMFSZWLXLTFXMVMGUKXNVPXGWKXKXNVNWNS
      VPXGTXNVNFVMNWNVMFVMULVMFFUMUNVNWNOVOUOUPWJXJAVMFWIXIBVMFXNWHXHWBWGCVMFUQ
      URUSUSUTVAVOEVBUJZSVODSZXLXFTDXOVOHUKXPXGWOXKXEWNOVODVCXPXIXDABFFXPWBWRXH
      XCXPVTWQWAXPVSWPRVQVRVODVDZVEVFXPWGXBCFXPVSWPWFXAUBXQXPWDWSWEWTUAWCVQVODV
      DWCVRVODVDVGVHVIUTVJUTVAVKVL $.
      $( [4-Sep-2006] $) $( [30-Aug-2006] $)

    $( Mapping of the distance function of a metric space. $)
    msf $p |- ( M e. Met -> D : ( X X. X ) --> RR ) $=
      ( vx vy vz cmt wcel cxp cr wf cv co cc0 wceq wb caddc cle wbr wral wa
      msflem pm3.26d ) BIJCCKLAMFNZGNZAOZPQUFUGQRUHHNZUFAOUIUGAOSOTUAHCUBUCGCUB
      FCUBFGHABCDEUDUE $.
      $( [4-Sep-2006] $) $( [30-Aug-2006] $)

    $( Closure of the distance function of a metric space. $)
    mscl $p |- ( ( M e. Met /\ A e. X /\ B e. X ) -> ( A D B ) e. RR ) $=
      ( cxp cr wf wcel co cmt foprrn msf syl3an1 ) EEHICJAEKBEKABCLIKDMKABIEECN
      CDEFGOP $.
      $( [4-Sep-2006] $) $( [30-Aug-2006] $)

    $( The distance function of a metric space is zero iff its arguments
       are equal. $)
    mseq0 $p |- ( ( M e. Met /\ A e. X /\ B e. X )
                 -> ( ( A D B ) = 0 <-> A = B ) ) $=
      ( vx vy vz cmt wcel co cc0 wceq wb cv caddc cle wbr wral wa wi cxp cr wf
      msflem pm3.27d pm3.26 r19.20si opreq1 eqeq1d eqeq1 bibi12d opreq2 eqeq2
      rcla42v com12 3syl 3impib ) DKLZAELZBELZABCMZNOZABOZPZVAHQZIQZCMZNOZVHVIO
      ZPZVJJQZVHCMVNVICMRMSTJEUAZUBZIEUAZHEUAZVMIEUAZHEUAZVBVCUBZVGUCVAEEUDUECU
      FVRHIJCDEFGUGUHVQVSHEVPVMIEVMVOUIUJUJWAVTVGVMVGAVICMZNOZAVIOZPHIABEEVHAOZ
      VKWCVLWDWEVJWBNVHAVICUKULVHAVIUMUNVIBOZWCVEWDVFWFWBVDNVIBACUOULVIBAUPUNUQ
      URUSUT $.
      $( [4-Sep-2006] $) $( [30-Aug-2006] $)

    $( Triangle inequality for the distance function of a metric space. $)
    mstri2 $p |- ( ( ( M e. Met /\ A e. X ) /\ ( B e. X /\ C e. X ) )
                 -> ( A D B ) <_ ( ( C D A ) + ( C D B ) ) ) $=
      ( vx vy vz cmt wcel wa co caddc cle wbr wi w3a cv wral wceq opreq1
      opreq2 opreq1d breq12d opreq2d opreq12d breq2d rcla43v cc0 wb cxp cr wf
      msflem pm3.27d pm3.27 r19.20si syl syl5 3expb ex com3r imp31 ) ELMZAFMZBF
      MZCFMZNZABDOZCADOZCBDOZPOZQRZVHVKVGVPVHVKVGVPSZVHVIVJVQVHVIVJTIUAZJUAZDOZ
      KUAZVRDOZWAVSDOZPOZQRZKFUBZJFUBZIFUBZVPVGWEVPAVSDOZWAADOZWCPOZQRVLWJWABDO
      ZPOZQRIJKABCFFFVRAUCZVTWIWDWKQVRAVSDUDWNWBWJWCPVRAWADUEUFUGVSBUCZWIVLWKWM
      QVSBADUEWOWCWLWJPVSBWADUEUHUGWACUCZWMVOVLQWPWJVMWLVNPWACADUDWACBDUDUIUJUK
      VGVTULUCVRVSUCUMZWFNZJFUBZIFUBZWHVGFFUNUODUPWTIJKDEFGHUQURWSWGIFWRWFJFWQW
      FUSUTUTVAVBVCVDVEVF $.
      $( [4-Sep-2006] $) $( [30-Aug-2006] $)

    $( The distance function of a metric space is zero if its arguments
       are equal.  Definition 14-1.1(a) of [Gleason] p. 223. $)
    ms0 $p |- ( ( M e. Met /\ A e. X ) -> ( A D A ) = 0 ) $=
      ( cmt wcel wa co cc0 wceq eqid wb mseq0 3anidm23 mpbiri ) CGHZADHZIAABJKL
      ZAALZAMRSTUANAABCDEFOPQ $.
      $( [4-Sep-2006] $) $( [30-Aug-2006] $)

    $( The distance function of a metric space is symmetric.  Definition
       14-1.1(c) of [Gleason] p. 223. $)
    mssym $p |- ( ( M e. Met /\ A e. X /\ B e. X ) ->
                  ( A D B ) = ( B D A ) ) $=
      ( cmt wcel w3a co wceq cle wbr wa caddc mstri2 anabsan2 3impa cc0 ms0
      3adant2 opreq2d cc mscl recnd ax0id syl 3com23 eqtrd breqtrd 3adant3 jca
      cr wb letri3t sylanc mpbird ) DHIZAEIZBEIZJZABCKZBACKZLZVCVDMNZVDVCMNZOZV
      BVFVGVBVCVDBBCKZPKZVDMUSUTVAVCVJMNZUSUTOVAVKABBCDEFGQRSVBVJVDTPKZVDVBVITV
      DPUSVAVITLUTBCDEFGUAUBUCUSVAUTVLVDLZUSVAUTJZVDUDIVMVNVDBACDEFGUEZUFVDUGUH
      UIUJUKVBVDVCAACKZPKZVCMUSVAUTVDVQMNZUSVAUTVRUSVAOUTVRBAACDEFGQRSUIVBVQVCT
      PKZVCVBVPTVCPUSUTVPTLVAACDEFGUAULUCVBVCUDIVSVCLVBVCABCDEFGUEZUFVCUGUHUJUK
      UMVCUNIVDUNIZVEVHUOVBVCVDUPVTUSVAUTWAVOUIUQUR $.
      $( [6-Sep-2006] $) $( [27-Aug-2006] $)

    $( Triangle inequality for the distance function of a metric space.
       Definition 14-1.1(d) of [Gleason] p. 223. $)
    mstri $p |- ( ( ( M e. Met /\ A e. X ) /\ ( B e. X /\ C e. X ) )
              -> ( A D B ) <_ ( ( A D C ) + ( C D B ) ) ) $=
      ( cmt wcel wa co caddc cle mstri2 wceq mssym 3expa adantrl opreq1d
      breqtrrd ) EIJZAFJZKZBFJZCFJZKKZABDLCADLZCBDLZMLACDLZUIMLNABCDEFGHOUGUJUH
      UIMUDUFUJUHPZUEUBUCUFUKACDEFGHQRSTUA $.
      $( [7-Sep-2006] $) $( [27-Aug-2006] $)

    $( Triangle inequality for the distance function of a metric space. $)
    mstri3 $p |- ( ( ( M e. Met /\ A e. X ) /\ ( B e. X /\ C e. X ) )
              -> ( A D B ) <_ ( ( A D C ) + ( B D C ) ) ) $=
      ( cmt wcel wa co caddc cle mstri wceq mssym 3expb adantlr opreq2d
      breqtrrd ) EIJZAFJZKBFJZCFJZKZKZABDLACDLZCBDLZMLUHBCDLZMLNABCDEFGHOUGUJUI
      UHMUBUFUJUIPZUCUBUDUEUKBCDEFGHQRSTUA $.
      $( [16-Mar-2007] $) $( [13-Mar-2007] $)

    $( The distance function of a metric space is nonnegative. $)
    msge0 $p |- ( ( M e. Met /\ A e. X /\ B e. X ) ->
                  0 <_ ( A D B ) ) $=
      ( co cr wcel cc0 c2 cmul cle wbr cmt w3a 2re wa clt 2pos prodge0t mpanr1
      mpanl1 mscl caddc wi mstri2 exp43 pm2.43d com23 3imp wceq ms0 3adant2 cc
      recnd 2timest syl eqcomd 3brtr3d sylanc ) ABCHZIJZKLVCMHZNOZKVCNOZDPJZAEJ
      ZBEJZQZLIJZVDVFVGRVLVDSKLTOVFVGUALVCUBUCUDABCDEFGUEZVKBBCHZVCVCUFHZKVENVH
      VIVJVNVONOZVHVJVIVPVHVJVIVPUGVHVJVJVIVPBBACDEFGUHUIUJUKULVHVJVNKUMVIBCDEF
      GUNUOVKVEVOVKVCUPJVEVOUMVKVCVMUQVCURUSUTVAVB $.
      $( [21-Sep-2006] $) $( [27-Aug-2006] $)

    $( The distance function of a metric space is positive for unequal
       points.  Definition 14-1.1(b) of [Gleason] p. 223 and its converse. $)
    msgt0 $p |- ( ( M e. Met /\ A e. X /\ B e. X ) ->
                ( A =/= B <-> 0 < ( A D B ) ) ) $=
      ( cmt wcel w3a cc0 co wne clt wbr wceq mseq0 eqcom syl5bb eqneqd wn cle
      wa msge0 biantrurd cr wb mscl 0re ltlent mpan syl bitr4d df-ne bitr3d )
      DHIAEIBEIJZKABCLZMZABMKUQNOZUPKUQABUPUQKPABPKUQPZABCDEFGQKUQRSTUPUTUAZUSU
      RUPVAKUQUBOZVAUCZUSUPVBVAABCDEFGUDUEUPUQUFIZUSVCUGZABCDEFGUHKUFIVDVEUIKUQ
      UJUKULUMKUQUNSUO $.
      $( [23-Sep-2006] $) $( [27-Aug-2006] $)

    $( A subset of a metric space is a metric space.  (Contributed by FL,
       10-Nov-2006.) $)
    ssms $p |- ( ( M e. Met /\ R (_ X ) -> <. R , ( D |` ( R X. R ) ) >.
      e. Met ) $=
      ( vx vy vz cmt wcel wss wa cxp cres cop cr wf cv co cc0 wceq weq wb
      caddc cle wbr wral fssres ancoms ssxp sylan anabsan wi ssralv adantr
      r19.20sdv oprvalres adantll ad2antrr pm3.27 eqtr3d ex sylibd eqtrt a1i
      mpand sylbird impbid eqcomd expcom a1d imp41 opreq12d 3brtr3d r19.20dva
      syld anim12d imp anim12i anabss5 anasss msflem cvv wfun resfunexg ffun
      xpexg c1st cfv fvex eqeltr ssex sylanc adantl ismsg syl mpbird ) CJKZBDLZ
      MZBABBNZOZPJKZXBQXCRZGSZHSZXCTZUAUBZGHUCZUDZXHISZXFXCTZXLXGXCTZUETZUFUGZI
      BUHZMZHBUHZGBUHZMZDDNZQARZXFXGATZUAUBZXJUDZYDXLXFATZXLXGATZUETZUFUGZIDUHZ
      MZHDUHZGDUHZMZWTYAWSWTYOYAWTYCYNYAWTYCMZYNYAYPXEYPYNMXTWTYCXEXBYBLZYCXEWT
      WTMYCYQXEYBQXBAUIUJBDBDUKULUMYPYNXTYPYNYMGBUHZXTWTYNYRUNYCYMGBDUOUPYPYRYL
      HBUHZGBUHZXTWTYRYTUNYCWTYMYSGBYLHBDUOUQUPYPYSXSGBYPXFBKZMZYLXRHBUUBXGBKZM
      ZYFXKYKXQUUDYFXKUUDYFMZXIXJUUEXIYEXJUUEXIYEUUEXIMXHYDUAUUDXHYDUBZYFXIUUAU
      UCUUFYPXFXGBBAURZUSZUTUUEXIVAVBVCUUDYFVAZVDUUEXJYEXIUUIUUEUUFYEXIUUDUUFYF
      UUHUPUUFYEMXIUNUUEXHYDUAVEVFVGVHVIVCUUDYKYJIBUHZXQYPYKUUJUNZUUAUUCWTUUKYC
      YJIBDUOUPUTUUDYJXPIBUUDXLBKZMZYJXPUUMYJMYDYIXHXOUFUUMYJVAUUDYDXHUBZUULYJU
      UAUUCUUNYPUUAUUCMXHYDUUGVJUSUTUUMYIXOUBYJUUMYGXMYHXNUEYPUUAUUCUULYGXMUBZU
      UAUUCUULUUOUNZUNUNYPUUAUUPUUCUULUUAUUOUULUUAMXMYGXLXFBBAURVJVKVLVFVMUUMXN
      YHUUCUULXNYHUBZUUBUULUUCUUQXLXGBBAURUJUSVJVNUPVOVCVPVQVRVPVPVQVQVSVTWAWBU
      JGHIACDEFWCZULXAXCWDKZXDYAUDYOWTUUSWSAWEZXBWDKZUUSYOWTMAXBWDWFYCUUTYNWTYB
      QAWGUTWTUVAYOBWDKZUVBUVAWTBBWDWDWHBDDCWIWJWDECWIWKWLWMZUVCWNWOWNUURULGHIW
      DXCBWPWQWR $.
      $( [10-Nov-2006] $) $( [12-Oct-2006] $)
  $}

