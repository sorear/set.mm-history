$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Basic metric space properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new constant symbols and define their syntactical properties. $)
  $c Met $.
  $c MetSp $.
  $c ball $.
  $c MetOpen $.

  $( Extend class notation with the class of all metrics. $)
  cme $a class Met $.

  $( Extend class notation with the class of all metric spaces. $)
  cmt $a class MetSp $.

  $( Extend class notation with the metric space ball function. $)
  cbl $a class ball $.

  $( Extend class notation with a function mapping each metric space to the
     family of its open sets. $)
  copn $a class MetOpen $.

  ${
    $d x y z w v u $.
    $( Define the (proper) class of all metrics.  (A metric space is the
       metric's base set paired with the metric; see ~ df-ms .  However, we
       will often also call the metric itself a "metric space.") Equivalent to
       Definition 14-1.1 of [Gleason] p. 223.  See ~ ismsg for the property "is
       a metric space."  The 4 properties in Gleason's definition are shown by
       ~ met0 , ~ metgt0 , ~ metsym , and ~ mettri . $)
    df-met $a |- Met = { x | E. y ( x : ( y X. y ) --> RR /\
                 A. z e. y A. w e. y ( ( ( z x w ) = 0 <-> z = w ) /\
                 A. v e. y ( z x w ) <_ ( ( v x z ) + ( v x w ) ) ) ) } $.

    $( Define the (proper) class of all metric spaces. $)
    df-ms $a |- MetSp = { <. x , y >. | ( y e. Met /\ x = dom dom y ) } $.

    $( Define the metric space ball function.  See ~ blval for its value. $)
    df-bl $a |- ball = { <. x , y >. | ( x e. Met /\ y =
  { <. <. z , w >. , v >. | ( ( z e. dom dom x /\ w e. RR ) /\ ( 0 < w /\ v =
                { u e. dom dom x | ( z x u ) < w } ) ) } ) } $.

    $( Define a function whose value is the family of open sets of a metric
       space.  See ~ isopn for its main property. $)
    df-opn $a |- MetOpen = { <. x , y >. | ( x e. Met /\ y = { z | ( z C_
            dom dom x /\ A. w e. z E. v e. ran ( ball ` x ) (
            w e. v /\ v C_ z ) ) } ) } $.

    $( The class of all metric spaces is a relation. $)
    msrel $p |- Rel MetSp $=
      ( vy vx cmt wrel cv cme wcel cdm wceq wa copab relopab df-ms releqi
      mpbir ) CDAEZFGBEPHHIJZBAKZDQBALCRBAMNO $.
      $( [28-Aug-2006] $)
  $}

  ${
    $d d t x y z D $.  $d t x y z X $.
    ismet.1 $e |- X = dom dom D $.
    $( Express the predicate " ` D ` is a metric." $)
    ismet $p |- ( D e. A -> ( D e. Met <-> ( D : ( X X. X ) --> RR /\
                 A. x e. X A. y e. X ( ( ( x D y ) = 0 <-> x = y ) /\
                 A. z e. X ( x D y ) <_ ( ( z D x ) + ( z D y ) ) ) ) ) ) $=
      ( vt vd wcel cme cv cdm wceq cxp cr wf co cc0 wb caddc cle wbr wral wa
      wex feq1 opreq eqeq1d bibi1d opreq12d breq12d ralbidv anbi12d 2ralbidv
      exbidv df-met elab2g fdm dmeq dmxpid syl6req syl adantr pm4.71ri exbii
      syl6bb cvv dmexg eqeq2i xpeq1 xpeq2 eqtrd feq2d raleq anbi2d raleqbi1dv
      sylbir ceqsexgv 3syl bitrd ) EDJZEKJZHLZEMZMZNZWDWDOZPEQZALZBLZERZSNZWJWK
      NZTZWLCLZWJERZWPWKERZUARZUBUCZCWDUDZUEZBWDUDZAWDUDZUEZUEZHUFZFFOZPEQZWOWT
      CFUDZUEZBFUDZAFUDZUEZWBWCXEHUFZXGWHPILZQZWJWKXPRZSNZWNTZXRWPWJXPRZWPWKXPR
      ZUARZUBUCZCWDUDZUEZBWDUDAWDUDZUEZHUFXOIEKDXPENZYHXEHYIXQWIYGXDWHPXPEUGYIY
      FXBABWDWDYIXTWOYEXAYIXSWMWNYIXRWLSWJWKXPEUHZUIUJYIYDWTCWDYIXRWLYCWSUBYJYI
      YAWQYBWRUAWPWJXPEUHWPWKXPEUHUKULUMUNUOUNUPIHABCUQURXEXFHXEWGWIWGXDWIWEWHN
      ZWGWHPEUSYKWFWHMWDWEWHUTWDVAVBVCVDVEVFVGWBWEVHJWFVHJXGXNTEDVIWEVHVIXEXNHW
      FVHWGWDFNZXEXNTFWFWDGVJYLWIXIXDXMYLWHXHPEYLWHFWDOXHWDFWDVKWDFFVLVMVNXCXLA
      WDFXBXKBWDFYLXAXJWOWTCWDFVOVPVQVQUNVRVSVTWA $.
      $( [4-Sep-2007] $) $( [25-Aug-2006] $)
  $}

  ${
    $d x y z w v u $.
    $( Alternate definition for the class of all metric spaces (replaces old
       version of ~ df-ms ). $)
    dfms2 $p |- MetSp = { <. x , y >. | ( y : ( x X. x ) --> RR /\
                 A. z e. x A. w e. x ( ( ( z y w ) = 0 <-> z = w ) /\
                 A. v e. x ( z y w ) <_ ( ( v y z ) + ( v y w ) ) ) ) } $=
      ( cmt cv cme wcel cdm wceq wa copab cxp cr wf co cc0 wb caddc cle wbr
      wral df-ms xpeq1 xpeq2 eqtrd feq2d raleq anbi2d raleqbi1dv anbi12d
      pm5.32ri fdm dmeq dmxpid eqeq2i biimpi eqcomd 3syl adantr pm4.71i cvv
      visset eqid ismet ax-mp anbi1i 3bitr4ri opabbii eqtri ) FBGZHIZAGZVLJZJZK
      ZLZABMVNVNNZOVLPZCGZDGZVLQZRKWAWBKSZWCEGZWAVLQWEWBVLQTQUAUBZEVNUCZLZDVNUC
      ZCVNUCZLZABMABUDVRWKABWKVQLVPVPNZOVLPZWDWFEVPUCZLZDVPUCZCVPUCZLZVQLWKVRVQ
      WKWRVQVTWMWJWQVQVSWLOVLVQVSVPVNNWLVNVPVNUEVNVPVPUFUGUHWIWPCVNVPWHWODVNVPV
      QWGWNWDWFEVNVPUIUJUKUKULUMWKVQVTVQWJVTVOVSKVPVSJZKZVQVSOVLUNVOVSUOWTVPVNW
      TVPVNKWSVNVPVNUPUQURUSUTVAVBVMWRVQVLVCIVMWRSBVDCDEVCVLVPVPVEVFVGVHVIVJVK
      $.
      $( [26-Aug-2007] $)
  $}

  ${
    $d v w x y z X $.  $d v w x y z D $.
    $( Express the predicate " ` <. X , D >. ` is a metric space" with
       underlying set ` X ` and distance function ` D ` . $)
    ismsg $p |- ( D e. A ->
                 ( <. X , D >. e. MetSp <-> ( D : ( X X. X ) --> RR /\
                 A. x e. X A. y e. X ( ( ( x D y ) = 0 <-> x = y ) /\
                 A. z e. X ( x D y ) <_ ( ( z D x ) + ( z D y ) ) ) ) ) ) $=
      ( vw vv wcel cop cmt cxp cr wf cv co cc0 wceq wb caddc cle wbr wral wa
      cvv df-br msrel brrelexi sylbir anim1i ancoms dmfex wo xpexr oridm sylib
      syl simpl jca adantrr copab xpeq1 xpeq2 eqtrd feq2d raleq anbi2d
      raleqbi1dv anbi12d feq1 opreq eqeq1d bibi1d opreq12d breq12d ralbidv
      2ralbidv opelopabg dfms2 eleq2i syl5bb pm5.21nd ) EDIZFEJZKIZFFLZMENZAOZB
      OZEPZQRZWHWIRZSZWJCOZWHEPZWNWIEPZTPZUAUBZCFUCZUDZBFUCAFUCZUDZFUEIZWCUDZWE
      WCXDWEXCWCWEFEKUBXCFEKUFFEKUGUHUIUJUKWCWGXDXAWCWGUDZXCWCXEWFUEIZXCWFMDEUL
      XFXCXCUMXCFFUEUNXCUOUPUQWCWGURUSUTXDWDGOZXGLZMHOZNZWHWIXIPZQRZWLSZXKWNWHX
      IPZWNWIXIPZTPZUAUBZCXGUCZUDZBXGUCZAXGUCZUDZGHVAZIXBWEYBWFMXINZXMXQCFUCZUD
      ZBFUCZAFUCZUDXBGHFEUEDXGFRZXJYDYAYHYIXHWFMXIYIXHFXGLWFXGFXGVBXGFFVCVDVEXT
      YGAXGFXSYFBXGFYIXRYEXMXQCXGFVFVGVHVHVIXIERZYDWGYHXAWFMXIEVJYJYFWTABFFYJXM
      WMYEWSYJXLWKWLYJXKWJQWHWIXIEVKZVLVMYJXQWRCFYJXKWJXPWQUAYKYJXNWOXOWPTWNWHX
      IEVKWNWIXIEVKVNVOVPVIVQVIVRKYCWDGHABCVSVTWAWB $.
      $( [27-Aug-2006] $)
  $}

  ${
    $d x y z D $.  $d x y z X $.
    ismsi.0 $e |- D e. _V $.
    ismsi.1 $e |- D : ( X X. X ) --> RR $.
    ismsi.2 $e |- ( ( x e. X /\ y e. X ) -> ( ( x D y ) = 0 <-> x = y ) ) $.
    ismsi.3 $e |- ( ( x e. X /\ y e. X /\ z e. X ) ->
                  ( x D y ) <_ ( ( z D x ) + ( z D y ) ) ) $.
    ismsi.4 $e |- M = <. X , D >. $.
    $( Properties that determine a metric space. $)
    ismsi $p |- M e. MetSp $=
      ( cop cmt wcel cxp cr wf cv co cc0 wceq wb caddc cle wbr wral wa cvv
      ismsg ax-mp 3expa r19.21aiva jca rgen2a mpbir2an eqeltri ) EFDLZMKUQMNZFF
      OPDQZARZBRZDSZTUAUTVAUAUBZVBCRZUTDSVDVADSUCSUDUEZCFUFZUGZBFUFAFUFZDUHNURU
      SVHUGUBGABCUHDFUIUJHVGABFUTFNZVAFNZUGZVCVFIVKVECFVIVJVDFNVEJUKULUMUNUOUP
      $.
      $( [17-Nov-2006] $)
  $}

  ${
    $d x y z D $.  $d x y z X $.
    ismeti.0 $e |- X e. _V $.
    ismeti.1 $e |- D : ( X X. X ) --> RR $.
    ismeti.2 $e |- ( ( x e. X /\ y e. X ) -> ( ( x D y ) = 0 <-> x = y ) ) $.
    ismeti.3 $e |- ( ( x e. X /\ y e. X /\ z e. X ) ->
                  ( x D y ) <_ ( ( z D x ) + ( z D y ) ) ) $.
    $( Properties that determine a metric. $)
    ismeti $p |- D e. Met $=
      ( cme wcel cxp cr wf cv co cc0 wceq wb caddc cle wbr wral wa cvv xpex
      fex mp2an cdm fdmi dmeqi dmxpid eqtr2i ismet ax-mp 3expa r19.21aiva jca
      rgen2a mpbir2an ) DJKZEELZMDNZAOZBOZDPZQRVDVERSZVFCOZVDDPVHVEDPTPUAUBZCEU
      CZUDZBEUCAEUCZDUEKZVAVCVLUDSVCVBUEKVMGEEFFUFVBMUEDUGUHABCUEDEDUIZUIVBUIEV
      NVBVBMDGUJUKEULUMUNUOGVKABEVDEKZVEEKZUDZVGVJHVQVICEVOVPVHEKVIIUPUQURUSUT
      $.
      $( [5-Sep-2007] $) $( [17-Nov-2006] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d z C $.  $d v w x y z D $.  $d v w M $.
    $d v w x y z X $.  $d x y z R $.
    msf.1 $e |- X = ( 1st ` M ) $.
    msf.2 $e |- D = ( 2nd ` M ) $.
    $( Lemma for ~ msf and others. $)
    msflem $p |- ( M e. MetSp -> ( D : ( X X. X ) --> RR /\
                  A. x e. X A. y e. X ( ( ( x D y ) = 0 <-> x = y ) /\
                  A. z e. X ( x D y ) <_ ( ( z D x ) + ( z D y ) ) ) ) ) $=
      ( vw vv cmt wcel cv cxp cr wf co cc0 wceq wb caddc cle wbr wral wa copab
      dfms2 eleq2i c1st cfv eqeq2i xpeq1 xpeq2 eqtrd feq2d raleq anbi2d
      raleqbi1dv anbi12d sylbir c2nd feq1 opreq eqeq1d bibi1d opreq12d breq12d
      ralbidv 2ralbidv elopabi sylbi ) EKLEIMZVLNZOJMZPZAMZBMZVNQZRSZVPVQSZTZVR
      CMZVPVNQZWBVQVNQZUAQZUBUCZCVLUDZUEZBVLUDZAVLUDZUEZIJUFZLFFNZODPZVPVQDQZRS
      ZVTTZWOWBVPDQZWBVQDQZUAQZUBUCZCFUDZUEZBFUDAFUDZUEZKWLEIJABCUGUHWKWMOVNPZW
      AWFCFUDZUEZBFUDZAFUDZUEZXEIJEVLEUIUJZSVLFSZWKXKTFXLVLGUKXMVOXFWJXJXMVMWMO
      VNXMVMFVLNWMVLFVLULVLFFUMUNUOWIXIAVLFWHXHBVLFXMWGXGWAWFCVLFUPUQURURUSUTVN
      EVAUJZSVNDSZXKXETDXNVNHUKXOXFWNXJXDWMOVNDVBXOXHXCABFFXOWAWQXGXBXOVSWPVTXO
      VRWORVPVQVNDVCZVDVEXOWFXACFXOVRWOWEWTUBXPXOWCWRWDWSUAWBVPVNDVCWBVQVNDVCVF
      VGVHUSVIUSUTVJVK $.
      $( [30-Aug-2006] $)

    $( Mapping of the distance function of a metric space. $)
    msf $p |- ( M e. MetSp -> D : ( X X. X ) --> RR ) $=
      ( vx vy vz cmt wcel cxp cr wf cv co cc0 wceq wb caddc cle wbr wral wa
      msflem simpld ) BIJCCKLAMFNZGNZAOZPQUFUGQRUHHNZUFAOUIUGAOSOTUAHCUBUCGCUBF
      CUBFGHABCDEUDUE $.
      $( [30-Aug-2006] $)

    $( Closure of the distance function of a metric space. $)
    mscl $p |- ( ( M e. MetSp /\ A e. X /\ B e. X ) -> ( A D B ) e. RR ) $=
      ( cxp cr wf wcel co cmt foprrn msf syl3an1 ) EEHICJAEKBEKABCLIKDMKABIEECN
      CDEFGOP $.
      $( [30-Aug-2006] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d z C $.  $d x y z D $.  $d x y z X $.
    metf.1 $e |- X = dom dom D $.
    $( Lemma for ~ metf and others. $)
    metflem $p |- ( D e. Met -> ( D : ( X X. X ) --> RR /\
                  A. x e. X A. y e. X ( ( ( x D y ) = 0 <-> x = y ) /\
                  A. z e. X ( x D y ) <_ ( ( z D x ) + ( z D y ) ) ) ) ) $=
      ( cme wcel cxp cr wf cv co cc0 wceq wb caddc cle wbr wral wa ismet ibi )
      DGHEEIJDKALZBLZDMZNOUDUEOPUFCLZUDDMUGUEDMQMRSCETUABETAETUAABCGDEFUBUC $.
      $( [4-Sep-2007] $) $( [30-Aug-2006] $)

    $( Mapping of the distance function of a metric space. $)
    metf $p |- ( D e. Met -> D : ( X X. X ) --> RR ) $=
      ( vx vy vz cme wcel cxp cr wf cv co cc0 wceq wb caddc cle wbr wral wa
      metflem simpld ) AGHBBIJAKDLZELZAMZNOUDUEOPUFFLZUDAMUGUEAMQMRSFBTUAEBTDBT
      DEFABCUBUC $.
      $( [4-Sep-2007] $) $( [30-Aug-2006] $)

    $( The base set of a metric space in terms of its distance function. $)
    metdmdm $p |- ( D e. Met -> X = dom dom D ) $=
      ( cdm wceq cme wcel a1i ) BADDEAFGCH $.
      $( [3-Apr-2008] $) $( [29-May-2007] $)

    $( The base set of a metric subspace. $)
    metssba $p |- ( D e. Met -> ( X i^i Y ) = dom dom ( D |` ( Y X. Y ) ) ) $=
      ( cme wcel cxp cres cdm cin cr wf wceq metf fdm syl ineq2d dmres syl5eq
      dmeqd dmxpin incom eqtri syl6req ) AEFZACCGZHIZIUFBBGZJZIZBCJZUEUGUIUEUFA
      IZJUIUGUEULUHUFUEUHKALULUHMABDNUHKAOPQAUFRSTUJCBJUKCBUACBUBUCUD $.
      $( [30-Jan-2008] $)

    $( The base set of a metric subspace. $)
    metssba2 $p |- ( ( D e. Met /\ Y C_ X ) ->
                 Y = dom dom ( D |` ( Y X. Y ) ) ) $=
      ( wss cme wcel cin cxp cres cdm wceq dfss biimpi incom syl6eq metssba
      sylan9eqr ) CBEZAFGCBCHZACCIJKKSCCBHZTSCUALCBMNCBOPABCDQR $.
      $( [31-Jan-2008] $)

    $( Closure of the distance function of a metric space.  Part of Property M1
       of [Kreyszig] p. 3. $)
    metcl $p |- ( ( D e. Met /\ A e. X /\ B e. X ) -> ( A D B ) e. RR ) $=
      ( cxp cr wf wcel co cme foprrn metf syl3an1 ) DDFGCHADIBDIABCJGICKIABGDDC
      LCDEMN $.
      $( [5-Sep-2007] $) $( [30-Aug-2006] $)

    $( The value of a metric is zero iff its arguments are equal.  Property M2
       of [Kreyszig] p. 4. $)
    meteq0 $p |- ( ( D e. Met /\ A e. X /\ B e. X )
                 -> ( ( A D B ) = 0 <-> A = B ) ) $=
      ( vx vy vz cme wcel co cc0 wceq wb cv caddc cle wbr wral wa wi cxp cr wf
      metflem simprd simpl ralimi opreq1 eqeq1d eqeq1 bibi12d opreq2 eqeq2
      rcla42v com12 3syl 3impib ) CIJZADJZBDJZABCKZLMZABMZNZUSFOZGOZCKZLMZVFVGM
      ZNZVHHOZVFCKVLVGCKPKQRHDSZTZGDSZFDSZVKGDSZFDSZUTVATZVEUAUSDDUBUCCUDVPFGHC
      DEUEUFVOVQFDVNVKGDVKVMUGUHUHVSVRVEVKVEAVGCKZLMZAVGMZNFGABDDVFAMZVIWAVJWBW
      CVHVTLVFAVGCUIUJVFAVGUKULVGBMZWAVCWBVDWDVTVBLVGBACUMUJVGBAUNULUOUPUQUR $.
      $( [5-Sep-2007] $) $( [30-Aug-2006] $)

    $( Triangle inequality for the distance function of a metric space. $)
    mettri2 $p |- ( ( D e. Met /\ ( C e. X /\ A e. X /\ B e. X ) )
                 -> ( A D B ) <_ ( ( C D A ) + ( C D B ) ) ) $=
      ( vx vy vz wcel w3a cme co caddc cle wbr wi cv wral wceq opreq1 opreq2
      opreq1d breq12d opreq2d opreq12d breq2d rcla43v cc0 wb wa cxp cr wf
      metflem simprd simpr ralimi syl syl5 3comr impcom ) CEJZAEJZBEJZKDLJZABDM
      ZCADMZCBDMZNMZOPZVDVEVCVFVKQVDVEVCKGRZHRZDMZIRZVLDMZVOVMDMZNMZOPZIESZHESZ
      GESZVKVFVSVKAVMDMZVOADMZVQNMZOPVGWDVOBDMZNMZOPGHIABCEEEVLATZVNWCVRWEOVLAV
      MDUAWHVPWDVQNVLAVODUBUCUDVMBTZWCVGWEWGOVMBADUBWIVQWFWDNVMBVODUBUEUDVOCTZW
      GVJVGOWJWDVHWFVINVOCADUAVOCBDUAUFUGUHVFVNUITVLVMTUJZVTUKZHESZGESZWBVFEEUL
      UMDUNWNGHIDEFUOUPWMWAGEWLVTHEWKVTUQURURUSUTVAVB $.
      $( [16-Sep-2007] $) $( [30-Aug-2006] $)

    $( MAY BE REVISED - replace with mettri2 everywhere; make obsolete $)
    $( Triangle inequality for the distance function of a metric space. $)
    mettri4 $p |- ( ( ( D e. Met /\ A e. X ) /\ ( B e. X /\ C e. X ) )
                 -> ( A D B ) <_ ( ( C D A ) + ( C D B ) ) ) $=
      ( vx vy vz cme wcel wa co caddc cle wbr wi w3a cv wral wceq opreq1
      opreq2 opreq1d breq12d opreq2d opreq12d breq2d rcla43v cc0 wb cxp cr wf
      metflem simprd simpr ralimi syl syl5 3expib com3r imp31 ) DJKZAEKZBEKZCEK
      ZLZABDMZCADMZCBDMZNMZOPZVEVHVDVMVEVFVGVDVMQVEVFVGRGSZHSZDMZISZVNDMZVQVODM
      ZNMZOPZIETZHETZGETZVMVDWAVMAVODMZVQADMZVSNMZOPVIWFVQBDMZNMZOPGHIABCEEEVNA
      UAZVPWEVTWGOVNAVODUBWJVRWFVSNVNAVQDUCUDUEVOBUAZWEVIWGWIOVOBADUCWKVSWHWFNV
      OBVQDUCUFUEVQCUAZWIVLVIOWLWFVJWHVKNVQCADUBVQCBDUBUGUHUIVDVPUJUAVNVOUAUKZW
      BLZHETZGETZWDVDEEULUMDUNWPGHIDEFUOUPWOWCGEWNWBHEWMWBUQURURUSUTVAVBVC $.
      $( [15-Sep-2007] $) $( [30-Aug-2006] $)

    $( The distance function of a metric space is zero if its arguments are
       equal.  Definition 14-1.1(a) of [Gleason] p. 223. $)
    met0 $p |- ( ( D e. Met /\ A e. X ) -> ( A D A ) = 0 ) $=
      ( cme wcel wa co cc0 wceq eqid wb meteq0 3anidm23 mpbiri ) BEFZACFZGAABHI
      JZAAJZAKPQRSLAABCDMNO $.
      $( [7-Sep-2007] $) $( [30-Aug-2006] $)

    $( The distance function of a metric space is symmetric.  Definition
       14-1.1(c) of [Gleason] p. 223. $)
    metsym $p |- ( ( D e. Met /\ A e. X /\ B e. X ) ->
                  ( A D B ) = ( B D A ) ) $=
      ( cme wcel w3a co wceq cle wbr cr wa wb metcl 3com23 letri3 syl11anc
      caddc mettri4 anabsan2 3impa cc0 met0 3adant2 opreq2d cc recnd addid1
      syl eqtrd breqtrd 3adant3 mpbir2and ) CFGZADGZBDGZHZABCIZBACIZJZUTVAKLZVA
      UTKLZUSUTMGVAMGZVBVCVDNOABCDEPZUPURUQVEBACDEPZQUTVARSUSUTVABBCIZTIZVAKUPU
      QURUTVIKLZUPUQNURVJABBCDEUAUBUCUSVIVAUDTIZVAUSVHUDVATUPURVHUDJUQBCDEUEUFU
      GUPURUQVKVAJZUPURUQHZVAUHGVLVMVAVGUIVAUJUKQULUMUSVAUTAACIZTIZUTKUPURUQVAV
      OKLZUPURUQVPUPURNUQVPBAACDEUAUBUCQUSVOUTUDTIZUTUSVNUDUTTUPUQVNUDJURACDEUE
      UNUGUSUTUHGVQUTJUSUTVFUIUTUJUKULUMUO $.
      $( [18-Sep-2007] $) $( [27-Aug-2006] $)

    $( Triangle inequality for the distance function of a metric space.
       Definition 14-1.1(d) of [Gleason] p. 223. $)
    mettri $p |- ( ( D e. Met /\ ( A e. X /\ B e. X /\ C e. X ) )
              -> ( A D B ) <_ ( ( A D C ) + ( C D B ) ) ) $=
      ( cme wcel w3a wa co caddc cle wbr wi mettri2 expcom 3coml impcom wceq
      metsym 3adant3r2 opreq1d breqtrrd ) DGHZAEHZBEHZCEHZIZJZABDKZCADKZCBDKZLK
      ZACDKZUMLKMUIUEUKUNMNZUHUFUGUEUPOUEUHUFUGIUPABCDEFPQRSUJUOULUMLUEUFUHUOUL
      TUGACDEFUAUBUCUD $.
      $( [12-Nov-2007] $) $( [27-Aug-2006] $)

    $( Triangle inequality for the distance function of a metric space. $)
    mettri3 $p |- ( ( D e. Met /\ ( A e. X /\ B e. X /\ C e. X ) )
              -> ( A D B ) <_ ( ( A D C ) + ( B D C ) ) ) $=
      ( cme wcel w3a wa co caddc cle mettri wceq metsym 3adant3r1 opreq2d
      breqtrrd ) DGHZAEHZBEHZCEHZIJZABDKACDKZCBDKZLKUEBCDKZLKMABCDEFNUDUGUFUELT
      UBUCUGUFOUABCDEFPQRS $.
      $( [29-Jan-2008] $) $( [13-Mar-2007] $)

    $( The distance function of a metric space is nonnegative. $)
    metge0 $p |- ( ( D e. Met /\ A e. X /\ B e. X ) ->
                  0 <_ ( A D B ) ) $=
      ( cme wcel w3a co cr cc0 c2 cmul cle wbr metcl caddc wi mettri4 exp43
      pm2.43d com23 3imp wceq met0 3adant2 cc recnd 2times syl eqcomd 3brtr3d
      2re wa clt 2pos prodge0 mpanr1 mpanl1 syl11anc ) CFGZADGZBDGZHZABCIZJGZKL
      VEMIZNOZKVENOZABCDEPZVDBBCIZVEVEQIZKVGNVAVBVCVKVLNOZVAVCVBVMVAVCVBVMRVAVC
      VCVBVMBBACDESTUAUBUCVAVCVKKUDVBBCDEUEUFVDVGVLVDVEUGGVGVLUDVDVEVJUHVEUIUJU
      KULLJGZVFVHVIUMVNVFUNKLUOOVHVIUPLVEUQURUSUT $.
      $( [26-Sep-2007] $) $( [27-Aug-2006] $)

    $( The distance function of a metric space is positive for unequal points.
       Definition 14-1.1(b) of [Gleason] p. 223 and its converse. $)
    metgt0 $p |- ( ( D e. Met /\ A e. X /\ B e. X ) ->
                ( A =/= B <-> 0 < ( A D B ) ) ) $=
      ( cme wcel w3a cc0 co clt wbr cle wne wa cr wb 0re metcl ltlen sylancr
      metge0 biantrurd meteq0 necon3bid 3bitr2rd ) CFGADGBDGHZIABCJZKLZIUHMLZUH
      INZOZUKABNUGIPGUHPGUIULQRABCDESIUHTUAUGUJUKABCDEUBUCUGUHIABABCDEUDUEUF $.
      $( [5-Oct-2007] $) $( [27-Aug-2006] $)

    $( A metric space is nonempty iff its base set is nonempty. $)
    metn0 $p |- ( D e. Met -> ( D =/= (/) <-> X =/= (/) ) ) $=
      ( cme wcel c0 wceq cdm cxp cr wf wrel wb metf frel reldm0 3syl fdm relxp
      releq mpbiri syl bitrd eqeq1i syl6bbr necon3bid ) ADEZAFBFUGAFGZAHZHZFGZB
      FGUGUHUIFGZUKUGBBIZJAKZALUHULMABCNZUMJAOAPQUGUNUILZULUKMUOUNUIUMGZUPUMJAR
      UQUPUMLBBSUIUMTUAUBUIPQUCBUJFCUDUEUF $.
      $( [22-Jun-2008] $) $( [4-Oct-2007] $)

    ${
      $d x y z R $.
      metreslem.2 $e |- A = dom dom ( D |` ( R X. R ) ) $.
      $( Lemma for ~ metres .  (Contributed by FL, 10-Nov-2006.) $)
      metreslem $p |- ( ( D e. Met /\ R C_ X ) ->
                     ( D |` ( R X. R ) ) e. Met ) $=
        ( vx vy vz cme wcel wss wa cxp cres cr wf cv co cc0 wceq wb caddc cle
        wbr wral metflem xpss12 anidms fssres sylan2 ssel ssralv anim2d
        imim12d ralimdv2 oprvres eqeq1d bibi1d adantl opreqan12d anandis
        breq12d ancoms ralbidva anbi12d ralbiia syl6ibr impcom anim12i
        anandirs sylan cvv resexg adantr ismet syl cdm metf fdm sseqtr4d
        ssdmres sylib dmeqd dmxpid syl6eq syl5eq xpeq1 xpeq2 eqtrd feq2d raleq
        anbi2d raleqbi1dv bitrd mpbird ) BJKZCDLZMZBCCNZOZJKZWTPXAQZGRZHRZXASZT
        UAZXDXEUAZUBZXFIRZXDXASZXJXEXASZUCSZUDUEZICUFZMZHCUFZGCUFZMZWQDDNZPBQZX
        DXEBSZTUAZXHUBZYBXJXDBSZXJXEBSZUCSZUDUEZIDUFZMZHDUFZGDUFZMWRXSGHIBDEUGY
        AYLWRXSYAWRMZXCYLWRMXRWRYAWTXTLZXCWRYNCDCDUHUIZXTPWTBUJUKWRYLXRWRYLYDYH
        ICUFZMZHCUFZGCUFXRWRYKYRGDCWRXDCKZXDDKYKYRCDXDULWRYJYQHDCWRXECKZXEDKYJY
        QCDXEULWRYIYPYDYHICDUMUNUOUPUOUPXQYRGCYSXPYQHCYSYTMZXIYDXOYPUUAXGYCXHUU
        AXFYBTXDXECCBUQZURUSUUAXNYHICXJCKZUUAXNYHUBUUCUUAMXFYBXMYGUDUUAXFYBUAUU
        CUUBUTUUCYSYTXMYGUAUUCYSMUUCYTMXKYEXLYFUCXJXDCCBUQXJXECCBUQVAVBVCVDVEVF
        VEVGVHVIVJVKVLWSXBAANZPXAQZXIXNIAUFZMZHAUFZGAUFZMZXSWSXAVMKZXBUUJUBWQUU
        KWRBWTJVNVOGHIVMXAAFVPVQWSACUAZUUJXSUBWSXAVRZVRZCAWQYAWRUUNCUABDEVSYMUU
        NWTVRCYMUUMWTYMWTBVRZLUUMWTUAYMWTXTUUOWRYNYAYOUTYAUUOXTUAWRXTPBVTVOWAWT
        BWBWCWDCWEWFVLFWGUULUUEXCUUIXRUULUUDWTPXAUULUUDCANWTACAWHACCWIWJWKUUHXQ
        GACUUGXPHACUULUUFXOXIXNIACWLWMWNWNVFVQWOWP $.
        $( [20-Sep-2007] $) $( [12-Oct-2006] $)
    $}
  $}

  $( A restriction of a metric is a metric. $)
  metres $p |- ( D e. Met -> ( D |` ( R X. R ) ) e. Met ) $=
    ( cme wcel cxp cres cdm cin cr wf wceq eqid metf fdm ineq2 inxp syl6eq
    reseq2d resdmres dmres reseq2i eqtr3i syl5eq 3syl wss inss2 metreslem
    mpan2 eqeltrd ) ACDZABBEZFZABAGZGZHZUOEZFZCUJUNUNEZIAJUMURKZULUQKAUNUNLZMUR
    IANUSAUKUMHZFZUQULUSVAUPAUSVAUKURHUPUMURUKOBBUNUNPQRAULGZFULVBAUKSVCVAAAUKT
    UAUBUCUDUJUOUNUEUQCDBUNUFUQGGZAUOUNUTVDLUGUHUI $.
    $( [26-Aug-2007] $)

  ${
    metss.1 $e |- X = dom dom C $.
    metss.2 $e |- Y = dom dom D $.
    $( If two metrics are in a subset relationship, so are their base sets. $)
    metss $p |- ( C C_ D -> X C_ Y ) $=
      ( wss cdm dmss syl 3sstr4g ) ABGZAHZHZBHZHZCDLMOGNPGABIMOIJEFK $.
      $( [29-May-2007] $)
  $}

  ${
    $d x y z $.
    $( The empty metric. $)
    0met $p |- (/) e. Met $=
      ( vx vy vz c0 cme wcel cxp cr wf cv co cc0 wceq wb caddc cle wbr wral wa
      cvv 0ex cdm dm0 dmeqi eqtr2i ismet ax-mp f0 xp0r feq2i mpbir ral0
      mpbir2an ) DEFZDDGZHDIZAJZBJZDKZLMUQURMNUSCJZUQDKUTURDKOKPQCDRSBDRZADRZDT
      FUNUPVBSNUAABCTDDDUBZUBVCDVCDUCUDUCUEUFUGUPDHDIHUHUODHDDUIUJUKVAAULUM $.
      $( [24-Oct-2007] $) $( [30-Aug-2006] $)
  $}

  ${
    metxplem1.1 $e |- D e. Met $.
    metxplem1.2 $e |- X = dom dom D $.
    ${
      metxplem1.4 $e |- F = ( 1st ` R ) $.
      metxplem1.5 $e |- H = ( 1st ` S ) $.
      $( Lemma for ~ metxp . $)
      metxplem1 $p |- ( ( R e. ( X X. Y ) /\ S e. ( X X. Y ) ) ->
           ( F D H ) e. RR ) $=
        ( cxp wcel co cr c1st cfv c2nd cvv wa elxp7 simprbi simpld syl5eqel
        cme metcl mp3an1 syl2an ) BFGLZMZDFMZEFMZDEANOMZCUIMZUJBPQZFDUJUOFMZBRQ
        GMZUJBSSLZMUPUQTBFGUAUBUCJUDUNCPQZFEUNUSFMZCRQGMZUNCURMUTVATCFGUAUBUCKU
        DAUEMUKULUMHDEAFIUFUGUH $.
        $( [20-Jul-2007] $)
    $}

    ${
      metxplem2.4 $e |- G = ( 2nd ` R ) $.
      metxplem2.5 $e |- J = ( 2nd ` S ) $.
      $( Lemma for ~ metxp . $)
      metxplem2 $p |- ( ( R e. ( Y X. X ) /\ S e. ( Y X. X ) ) ->
                     ( G D J ) e. RR ) $=
        ( cxp wcel co cr c2nd cfv c1st cvv wa elxp7 simprbi simprd syl5eqel
        cme metcl mp3an1 syl2an ) BGFLZMZDFMZEFMZDEANOMZCUIMZUJBPQZFDUJBRQGMZUO
        FMZUJBSSLZMUPUQTBGFUAUBUCJUDUNCPQZFEUNCRQGMZUSFMZUNCURMUTVATCGFUAUBUCKU
        DAUEMUKULUMHDEAFIUFUGUH $.
        $( [20-Jul-2007] $)
    $}

    ${
      metxplem3.4 $e |- ( ph -> F e. RR ) $.
      metxplem3.5 $e |- ( ph -> G e. RR ) $.
      metxplem3.6 $e |- ( ph -> ( C e. X /\ A e. X /\ B e. X ) ) $.
      $( Lemma for ~ metxp . $)
      metxplem3 $p |- ( ph -> ( ( C D A ) <_ F
          -> ( ( C D B ) <_ G -> ( A D B ) <_ ( F + G ) ) ) ) $=
        ( co cle wbr caddc wa cr wcel w3a cme metcl mp3an1 3adant1 syl adantr
        3adant3 3adant2 readdcl syl11anc mettri2 sylancr wi le2add syl22anc
        imp letrd exp32 ) ADBENZFOPZDCENZGOPZBCENZFGQNZOPAVAVCRZRVDUTVBQNZVEAVD
        STZVFADHTZBHTZCHTZUAZVHMVJVKVHVIEUBTZVJVKVHIBCEHJUCUDUEUFUGAVGSTZVFAUTS
        TZVBSTZVNAVLVOMVIVJVOVKVMVIVJVOIDBEHJUCUDUHUFZAVLVPMVIVKVPVJVMVIVKVPIDC
        EHJUCUDUIUFZUTVBUJUKUGAVESTZVFAFSTZGSTZVSKLFGUJUKUGAVDVGOPZVFAVMVLWBIMB
        CDEHJULUMUGAVFVGVEOPZAVOVPVTWAVFWCUNVQVRKLUTVBFGUOUPUQURUS $.
        $( [11-Jul-2007] $)
    $}
  $}

  ${
    $d x y z B $.  $d x y z C $.  $d u v w D $.  $d x y z R $.  $d x y z S $.
    $d u v w x y z X $.  $d u v w x y z Y $.
    metxp.1 $e |- X = dom dom B $.
    metxp.3 $e |- Y = dom dom C $.
    metxp.5 $e |- B e. Met $.
    metxp.6 $e |- C e. Met $.
    metxp.7 $e |- D = { <. <. x , y >. , z >. | ( ( x e. ( X X. Y )
         /\ y e. ( X X. Y ) ) /\ z = sup ( { ( ( 1st ` x ) B ( 1st ` y )
            ) , ( ( 2nd ` x ) C ( 2nd ` y ) ) } , RR , < ) ) } $.
    ${
      $d x y z F $.  $d x y z G $.  $d y z H $.  $d y z J $.
      metxpval.8 $e |- F = ( 1st ` R ) $.
      metxpval.9 $e |- G = ( 2nd ` R ) $.
      metxpval.10 $e |- H = ( 1st ` S ) $.
      metxpval.11 $e |- J = ( 2nd ` S ) $.
      $( Value of the distance function of the direct product of two metric
         spaces.  Based on Definition 14-1.5 of [Gleason] p. 225. $)
      metxpdval $p |- ( ( R e. ( X X. Y ) /\ S e. ( X X. Y ) ) -> ( R D S ) =
          if ( ( G C J ) < ( F B H
         ) , ( F B H ) , ( G C J ) ) ) $=
        ( cxp wcel wa co cpr cr clt csup wbr cif cv c1st cfv c2nd ltso supex
        wceq fveq2 syl6eqr opreq1d preq12d supeq1d opreq2d oprabval2 metxplem1
        metxplem2 suppr syl11anc eqtrd ) GMNUDZUEHVMUEUFZGHFUGIKDUGZJLEUGZUHZUI
        UJUKZVPVOUJULVOVPUMZABCGHVMVMAUNZUOUPZBUNZUOUPZDUGZVTUQUPZWBUQUPZEUGZUH
        ZUIUJUKVRFIWCDUGZJWFEUGZUHZUIUJUKUIVQUJURUSVTGUTZUIWHWKUJWLWDWIWGWJWLWA
        IWCDWLWAGUOUPIVTGUOVATVBVCWLWEJWFEWLWEGUQUPJVTGUQVAUAVBVCVDVEWBHUTZUIWK
        VQUJWMWIVOWJVPWMWCKIDWMWCHUOUPKWBHUOVAUBVBVFWMWFLJEWMWFHUQUPLWBHUQVAUCV
        BVFVDVESVGVNVOUIUEVPUIUEVRVSUTDGHIKMNQOTUBVHEGHJLNMRPUAUCVIUIVOVPUJURVJ
        VKVL $.
        $( [20-Jul-2007] $)

      $( One case of the value of the distance function of the direct product
         of two metric spaces.  Based on Definition 14-1.5 of [Gleason]
         p. 225. $)
      metxptval $p |- ( ( ( R e. ( X X. Y ) /\ S e. ( X X. Y ) ) /\
              ( G C J ) <_ ( F B H ) ) ->
                 ( R D S ) = ( F B H ) ) $=
        ( cxp wcel wa co cle wbr clt wceq wo cr wb metxplem2 metxplem1 leloe
        syl11anc biimpa cif metxpdval iftrue ifeq2 ifid syl6eq jaoi sylan9eq
        syldan ) GMNUDZUEHVIUEUFZJLEUGZIKDUGZUHUIZVKVLUJUIZVKVLUKZULZGHFUGZVLUK
        VJVMVPVJVKUMUEVLUMUEVMVPUNEGHJLNMRPUAUCUODGHIKMNQOTUBUPVKVLUQURUSVJVPVQ
        VNVLVKUTZVLABCDEFGHIJKLMNOPQRSTUAUBUCVAVNVRVLUKVOVNVLVKVBVOVRVNVLVLUTVL
        VNVKVLVLVCVNVLVDVEVFVGVH $.
        $( [20-Jul-2007] $)

      $( One case of the value of the distance function of the direct product
         of two metric spaces.  Based on Definition 14-1.5 of [Gleason]
         p. 225. $)
      metxpfval $p |- ( ( ( R e. ( X X. Y ) /\ S e. ( X X. Y ) ) /\
           ( F B H ) <_ ( G C J ) ) ->
                 ( R D S ) = ( G C J ) ) $=
        ( cxp wcel wa co cle wbr clt cif wceq metxpdval adantr wn cr wb
        metxplem1 metxplem2 lenlt syl11anc biimpa iffalse syl eqtrd ) GMNUDZUEH
        VFUEUFZIKDUGZJLEUGZUHUIZUFZGHFUGZVIVHUJUIZVHVIUKZVIVGVLVNULVJABCDEFGHIJ
        KLMNOPQRSTUAUBUCUMUNVKVMUOZVNVIULVGVJVOVGVHUPUEVIUPUEVJVOUQDGHIKMNQOTUB
        UREGHJLNMRPUAUCUSVHVIUTVAVBVMVHVIVCVDVE $.
        $( [20-Jul-2007] $)
    $}

    $( Closure of the distance function of the direct product of two metric
       spaces.  Based on Definition 14-1.5 of [Gleason] p. 225. $)
    metxpcl $p |- ( ( R e. ( X X. Y ) /\ S e. ( X X. Y ) ) ->
               ( R D S ) e. RR ) $=
      ( cxp wcel wa co cr c2nd cfv c1st eqid metxplem2 metxplem1 cle wbr
      metxptval adantr eqeltrd metxpfval lecasei ) GIJPZQHUNQRZGHFSZTQGUAUBZHUA
      UBZESZGUCUBZHUCUBZDSZEGHUQURJINLUQUDZURUDZUEZDGHUTVAIJMKUTUDZVAUDZUFZUOUS
      VBUGUHZRUPVBTABCDEFGHUTUQVAURIJKLMNOVFVCVGVDUIUOVBTQVIVHUJUKUOVBUSUGUHZRU
      PUSTABCDEFGHUTUQVAURIJKLMNOVFVCVGVDULUOUSTQVJVEUJUKUM $.
      $( [9-Jul-2007] $)

    $( Lemma for ~ metxp .  Triangle inequality. _Warning:  The HTML proof page
       is 0.6 megabyte in size_. $)
    metxplem4 $p |- ( ( u e. ( X X. Y ) /\ w e. ( X X. Y ) /\
              v e. ( X X. Y ) ) -> ( w D v ) <_ ( ( u D w ) + ( u D v ) ) ) $=
      ( cv cxp wcel w3a co caddc cle wbr c2nd cfv c1st cr eqid metxplem2
      3adant1 metxplem1 wa wceq metxptval 3adantl1 3adant3 3adant2 leid syl
      cvv elxp7 simprbi simpld 3anim123i metxplem3 mpid mpd adantr 3adantl2
      opreq2d breqtrrd wi imp metxpfval lecasei 3adantl3 opreq1d adantlr imp31
      eqbrtrd simprd ) FQZJKRZSZDQZWDSZEQZWDSZTZWFWHIUAZWCWFIUAZWCWHIUAZUBUAZUC
      UDWFUEUFZWHUEUFZHUAZWFUGUFZWHUGUFZGUAZWGWIWQUHSWEHWFWHWOWPKJOMWOUIZWPUIZU
      JUKWGWIWTUHSWEGWFWHWRWSJKNLWRUIZWSUIZULUKWJWQWTUCUDZUMWKWTWNUCWGWIXEWKWTU
      NWEABCGHIWFWHWRWOWSWPJKLMNOPXCXAXDXBUOUPWJWTWNUCUDZXEWJXFWCUEUFZWOHUAZWCU
      GUFZWRGUAZWEWGXHUHSZWIHWCWFXGWOKJOMXGUIZXAUJZUQZWEWGXJUHSZWIGWCWFXIWRJKNL
      XIUIZXCULZUQZWJXHXJUCUDZUMZWTXJWMUBUAZWNUCWJWTYAUCUDZXSWJYBXGWPHUAZXIWSGU
      AZWEWIYCUHSZWGHWCWHXGWPKJOMXLXBUJZURZWEWIYDUHSZWGGWCWHXIWSJKNLXPXDULZURZW
      JYCYDUCUDZUMZWTXJYDUBUAZYAUCWJWTYMUCUDZYKWJXJXJUCUDZYNWEWGYOWIWEWGUMZXOYO
      XQXJUSUTUQZWJYOYDYDUCUDZYNWEWIYRWGWEWIUMZYHYRYIYDUSUTURZWJWRWSXIGXJYDJNLX
      RYJWEXIJSZWGWRJSZWIWSJSZWEUUAXGKSZWEWCVAVARZSUUAUUDUMWCJKVBVCZVDWGUUBWOKS
      ZWGWFUUESUUBUUGUMWFJKVBVCZVDWIUUCWPKSZWIWHUUESUUCUUIUMWHJKVBVCZVDVEZVFVGV
      HVIYLWMYDXJUBWEWIYKWMYDUNWGABCGHIWCWHXIXGWSWPJKLMNOPXPXLXDXBUOVJZVKZVLWJY
      DYCUCUDZUMZWTXJYCUBUAZYAUCWJUUNWTUUPUCUDZWJYOUUNUUQVMYQWJWRWSXIGXJYCJNLXR
      YGUUKVFVHVNUUOWMYCXJUBWEWIUUNWMYCUNWGABCGHIWCWHXIXGWSWPJKLMNOPXPXLXDXBVOV
      JZVKZVLVPVIXTWLXJWMUBWEWGXSWLXJUNWIABCGHIWCWFXIXGWRWOJKLMNOPXPXLXCXAUOVQV
      RZVLWJXJXHUCUDZUMZWTXHWMUBUAZWNUCUVBWTUVCUCUDYCYDWJYEUVAYGVIWJYHUVAYJVIUV
      BYKUMWTXHYDUBUAZUVCUCUVBWTUVDUCUDZYKWJUVAUVEWJUVAYRUVEYTWJWRWSXIGXHYDJNLX
      NYJUUKVFVGVNVIWJYKUVCUVDUNUVAYLWMYDXHUBUULVKZVSVLUVBUUNUMWTXHYCUBUAZUVCUC
      WJUVAUUNWTUVGUCUDWJWRWSXIGXHYCJNLXNYGUUKVFVTWJUUNUVCUVGUNUVAUUOWMYCXHUBUU
      RVKZVSVLVPUVBWLXHWMUBWEWGUVAWLXHUNWIABCGHIWCWFXIXGWRWOJKLMNOPXPXLXCXAVOVQ
      VRZVLVPVIWAWJWTWQUCUDZUMWKWQWNUCWGWIUVJWKWQUNWEABCGHIWFWHWRWOWSWPJKLMNOPX
      CXAXDXBVOUPWJWQWNUCUDZUVJWJUVKXHXJXNXRXTWQYAWNUCXTWQYAUCUDYCYDWJYEXSYGVIW
      JYHXSYJVIXTYKUMWQYMYAUCWJXSYKWQYMUCUDWJWOWPXGHXJYDKOMXRYJWEUUDWGUUGWIUUIW
      EUUAUUDUUFWBWGUUBUUGUUHWBWIUUCUUIUUJWBVEZVFVTWJYKYAYMUNXSUUMVSVLXTUUNUMWQ
      UUPYAUCXTWQUUPUCUDZUUNWJXSUVMWJXSYCYCUCUDZUVMWEWIUVNWGYSYEUVNYFYCUSUTURZW
      JWOWPXGHXJYCKOMXRYGUVLVFVGVNVIWJUUNYAUUPUNXSUUSVSVLVPUUTVLUVBWQUVCWNUCWJW
      QUVCUCUDZUVAWJUVPYCYDYGYJYLWQUVDUVCUCWJYKWQUVDUCUDZWJXHXHUCUDZYKUVQVMWEWG
      UVRWIYPXKUVRXMXHUSUTUQZWJWOWPXGHXHYDKOMXNYJUVLVFVHVNUVFVLUUOWQUVGUVCUCWJW
      QUVGUCUDZUUNWJUVRUVTUVSWJUVRUVNUVTUVOWJWOWPXGHXHYCKOMXNYGUVLVFVGVHVIUVHVL
      VPVIUVIVLVPVIWAVP $.
      $( [11-Jul-2007] $)

    $( The direct product of two metric spaces.  Definition 14-1.5 of [Gleason]
       p. 225. $)
    metxp $p |- D e. Met $=
      ( vw vv vu cxp cdm cvv cme wcel dmexg ax-mp dmex eqeltri xpex cr wf wfn
      cv co wral ffnoprv c1st cfv c2nd cpr clt csup ltso supex fnoprab2
      metxpcl rgen2a mpbir2an wa cc0 wceq wbr cif eqid metxpdval eqeq1d wi wb
      iftrue adantl breq2 biimpac adantll wn elxp7 simprbi simprd cle metge0
      mp3an1 0re metcl lenlt sylancr mpbid syl2an pm2.21d ad2antrr mpd ex
      sylbid iffalse metxplem1 metxplem2 syl11anc simpld biantrud letri3
      sylancl bitr4d sylan9bbr meteq0 bi2anan9r an4s xpopth ancom syl5bb bitrd
      biimpd expdimp com23 sylbird imp pm2.61dan met0 mpan fveq2 opreq2d
      sylan9req ifeq12 anandirs sylan ifid syl5reqr adantr impbid caddc
      metxplem4 3coml ismeti ) NOPFGHQZGHGDRZRSIYSDTUAZYSSUAKDTUBUCUDUEHERZRSJU
      UAETUAZUUASUALETUBUCUDUEUFYRYRQZUGFUHFUUCUINUJZOUJZFUKZUGUAZOYRULNYRULNOY
      RYRUGFUMABCYRYRAUJZUNUOBUJZUNUODUKUUHUPUOUUIUPUOEUKUQZUGURUSFUGUUJURUTVAM
      VBUUGNOYRABCDEFUUDUUEGHIJKLMVCVDVEUUDYRUAZUUEYRUAZVFZUUFVGVHUUDUPUOZUUEUP
      UOZEUKZUUDUNUOZUUEUNUOZDUKZURVIZUUSUUPVJZVGVHZUUDUUEVHZUUMUUFUVAVGABCDEFU
      UDUUEUUQUUNUURUUOGHIJKLMUUQVKZUUNVKZUURVKZUUOVKZVLVMUUMUVBUVCUUMUUTUVBUVC
      VNUUMUUTVFZUVBUUSVGVHZUVCUUTUVBUVIVOUUMUUTUVAUUSVGUUTUUSUUPVPVMVQUVHUVIUV
      CUVHUVIVFUUPVGURVIZUVCUUTUVIUVJUUMUVIUUTUVJUUSVGUUPURVRVSVTUUMUVJUVCVNUUT
      UVIUUMUVJUVCUUKUUNHUAZUUOHUAZUVJWAZUULUUKUUQGUAZUVKUUKUUDSSQZUAUVNUVKVFZU
      UDGHWBWCZWDUULUURGUAZUVLUULUUEUVOUAUVRUVLVFZUUEGHWBWCZWDUVKUVLVFZVGUUPWEV
      IZUVMUUBUVKUVLUWBLUUNUUOEHJWFWGUWAVGUGUAZUUPUGUAZUWBUVMVOWHUUBUVKUVLUWDLU
      UNUUOEHJWIWGVGUUPWJWKWLWMWNWOWPWQWRUUMUUTWAZVFUVBUUPVGVHZUVCUWEUVBUWFVOUU
      MUWEUVAUUPVGUUTUUSUUPWSVMVQUUMUWEUWFUVCVNZUUMUWEUUSUUPWEVIZUWGUUMUUSUGUAZ
      UWDUWHUWEVODUUDUUEUUQUURGHKIUVDUVFWTEUUDUUEUUNUUOHGLJUVEUVGXAUUSUUPWJXBUU
      MUWFUWHUVCUUMUWFUWHUVCVNUUMUWFVFUWHUVIUVCUWFUWHUUSVGWEVIZUUMUVIUUPVGUUSWE
      VRUUKUVNUVRUWJUVIVOUULUUKUVNUVKUVQXCUULUVRUVLUVTXCUVNUVRVFZUWJUWJVGUUSWEV
      IZVFZUVIUWKUWLUWJYTUVNUVRUWLKUUQUURDGIWFWGXDUWKUWIUWCUVIUWMVOYTUVNUVRUWIK
      UUQUURDGIWIWGWHUUSVGXEXFXGWMXHUUMUWFUVIUVCUUMUWFUVIVFZUVCUUMUWNUUNUUOVHZU
      UQUURVHZVFZUVCUUKUVPUVSUWNUWQVOZUULUVQUVTUVNUVRUVKUVLUWRUWAUWFUWOUWKUVIUW
      PUUBUVKUVLUWFUWOVOLUUNUUOEHJXIWGYTUVNUVRUVIUWPVOKUUQUURDGIXIWGXJXKWMUUMUW
      PUWOVFUVCUWQUUDUUEGHGHXLUWOUWPXMXNXOXPXQWRWQXRXSXTWRYAUUKUVCUVBVNUULUUKUV
      CUVBUUKUVCVFUUTVGVGVJZUVAVGUUKUVPUVCUWSUVAVHZUVQUVNUVKUVCUWTUVNUVCVFVGUUS
      VHVGUUPVHUWTUVKUVCVFUVNUVCVGUUQUUQDUKZUUSYTUVNUXAVGVHKUUQDGIYBYCUVCUUQUUR
      UUQDUUDUUEUNYDYEYFUVKUVCVGUUNUUNEUKZUUPUUBUVKUXBVGVHLUUNEHJYBYCUVCUUNUUOU
      UNEUUDUUEUPYDYEYFUUTVGUUSVGUUPYGWMYHYIUUTVGYJYKWQYLYMXOPUJZYRUAUUKUULUUFU
      XCUUDFUKUXCUUEFUKYNUKWEVIABCNOPDEFGHIJKLMYOYPYQ $.
      $( [20-Jun-2007] $)
  $}


