$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Open sets of a metric space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.  $d v w x y z M $.  $d x y P $.  $d v w x y z X $.
    opnfval.1 $e |- X = ( 1st ` M ) $.
    $( An open set is a subset of a metric space which includes a ball around
       each of its points.  Definition 1.3-2 of [Kreyszig] p. 18.  The
       object ` ( Open `` M ) ` is the family of all open sets in metric space
       ` M ` . $)
    opnfval $p |- ( M e. Met -> ( Open ` M ) = { x | ( x (_ X /\
               A. y e. x E. z e. ran ( ball ` M ) ( y e. z /\ z (_ x ) ) } ) $=
      ( vw vv cv c1st cfv wss wcel wa cbl crn wrex wral cab cmt copn wceq
      fveq2 syl6eqr sseq2d rneqd rexeq1d ralbidv anbi12d abbidv df-opn cvv
      fvex eqeltr abssexg ax-mp fvopab4 ) GHDAIZGIZJKZLZBICIZMVBURLNZCUSOKZPZQZ
      BURRZNZASURELZVCCDOKZPZQZBURRZNZASZTUAUSDUBZVHVNAVPVAVIVGVMVPUTEURVPUTDJK
      ZEUSDJUCFUDUEVPVFVLBURVPVCCVEVKVPVDVJUSDOUCUFUGUHUIUJGHABCUKEULMVOULMEVQU
      LFDJUMUNVMAEULUOUPUQ $.
      $( [5-Sep-2006] $) $( [1-Sep-2006] $)

    $( The family of open sets of a metric space is a collection of subsets of
       the base set. $)
    opnfss $p |- ( M e. Met -> ( Open ` M ) (_ P~ X ) $=
      ( vx vy vz cmt wcel copn cfv cv wss wa cbl crn wrex wral cab cpw opnfval
      pm3.26 ss2abi df-pw sseqtr4 a1i eqsstrd ) AGHZAIJDKZBLZEKFKZHUJUHLMFANJOP
      EUHQZMZDRZBSZDEFABCTUMUNLUGUMUIDRUNULUIDUIUKUAUBDBUCUDUEUF $.
      $( [6-Sep-2006] $) $( [3-Sep-2006] $)

    $( The defining property of an open set of a metric space. $)
    isopn $p |- ( M e. Met -> ( A e. ( Open ` M ) <->
   ( A (_ X /\ A. x e. A E. y e. ran ( ball ` M ) ( x e. y /\ y (_ A ) ) ) ) $=
      ( vz cmt wcel copn cfv cv wss wa cbl crn wrex wral cab opnfval eleq2d
      c1st cvv fvex eqeltr wceq sseq2 anbi2d rexbidv raleqd elssab syl6bb ) DHI
      ZCDJKZICGLZEMALBLZIZUPUOMZNZBDOKPZQZAUORZNGSZICEMUQUPCMZNZBUTQZACRZNUMUNV
      CCGABDEFTUAVBVGGCEEDUBKUCFDUBUDUEVAVFAUOCUOCUFZUSVEBUTVHURVDUQUOCUPUGUHUI
      UJUKUL $.
      $( [11-Sep-2006] $) $( [1-Sep-2006] $)

    $( An open set of a metric space is a subspace of its base set. $)
    opnss $p |- ( ( M e. Met /\ A e. ( Open ` M ) ) -> A (_ X ) $=
      ( vx vy cmt wcel copn cfv wss cv wa cbl crn wrex wral isopn pm3.26
      syl6bi imp ) BGHZABIJHZACKZUBUCUDELFLZHUEAKMFBNJOPEAQZMUDEFABCDRUDUFSTUA
      $.
      $( [20-Nov-2006] $) $( [3-Sep-2006] $)

    $( The base set of a metric space is open.  Part of Theorem T1 of
       [Kreyszig] p. 19. $)
    opnm $p |- ( M e. Met -> X e. ( Open ` M ) ) $=
      ( vx vy cmt wcel copn cfv wss cv wa cbl crn wrex wral c1 cr cc0 clt wbr
      1re lt01 pm3.2i co wceq eleq2 sseq1 anbi12d rcla4ev blelrn c2nd eqid
      blcntr blssm jca sylanc mpan2 r19.21aiva ssid jctil isopn mpbird ) AFGZBA
      HIGBBJZDKZEKZGZVGBJZLZEAMIZNZOZDBPZLVDVNVEVDVMDBVDVFBGLZQRGZSQTUAZLZVMVPV
      QUBUCUDVFQVKUEZVLGVFVSGZVSBJZLZVMVOVRLZVJWBEVSVLVGVSUFVHVTVIWAVGVSVFUGVGV
      SBUHUIUJVFQABCUKWCVTWAAULIZVFQABCWDUMUNVFQABCUOUPUQURUSBUTVADEBABCVBVC $.
      $( [12-Oct-2006] $) $( [4-Sep-2006] $)

    $( The union of all open sets in a metric space is its underlying set. $)
    opnfuni $p |- ( M e. Met -> U. ( Open ` M ) = X ) $=
      ( cmt wcel copn cfv cuni cpw wss opnfss uniss unipw syl6ss syl opnm
      elssuni eqssd ) ADEZAFGZHZBSTBIZJZUABJABCKUCUAUBHBTUBLBMNOSBTEBUAJABCPBTQ
      OR $.
      $( [12-Oct-2006] $) $( [4-Sep-2006] $)
  $}

  ${
    $d f g u v x y z A $.  $d f g u v w x y B $.  $d f g u v w x y z M $.
    $d x y P $.
    $( An open set of a metric space includes a ball around each of its
       points. $)
    opni $p |- ( ( M e. Met /\ A e. ( Open ` M ) /\ P e. A ) ->
                 E. x e. ran ( ball ` M ) ( P e. x /\ x (_ A ) ) $=
      ( vy cmt wcel copn cfv cv wss wa cbl crn wrex c1st wral wi eqid isopn
      wceq eleq1 anbi1d rexbidv rcla4cv adantl syl6bi 3imp ) DFGZBDHIGZCBGZCAJZ
      GZULBKZLZADMINZOZUIUJBDPIZKZEJZULGZUNLZAUPOZEBQZLUKUQRZEABDURURSTVDVEUSVC
      UQECBUTCUAZVBUOAUPVFVAUMUNUTCULUBUCUDUEUFUGUH $.
      $( [13-Sep-2006] $) $( [3-Sep-2006] $)

    $( The balls of a metric space are open sets. $)
    blssopn $p |- ( M e. Met -> ran ( ball ` M ) (_ ( Open ` M ) ) $=
      ( vx vy vz cmt wcel cbl cfv crn copn cv c1st wss wa wrex wral eqid
      rnblssm ssid wceq elequ2 sseq1 anbi12d rcla4ev mpanr2 r19.21aiva adantl
      jca ex isopn sylibrd ssrdv ) AEFZBAGHIZAJHZUMBKZUNFZUPALHZMZCKZDKZFZVAUPM
      ZNZDUNOZCUPPZNZUPUOFUMUQVGUMUQNUSVFUPAURURQZRUQVFUMUQVECUPUQUTUPFZUPUPMZV
      EUPSVDVIVJNDUPUNVAUPTVBVIVCVJDBCUAVAUPUPUBUCUDUEUFUGUHUICDUPAURVHUJUKUL
      $.
      $( [19-Sep-2006] $) $( [12-Sep-2006] $)

    $( The union of a collection of open sets of a metric space is open.
       Theorem T2 of [Kreyszig] p. 19. $)
    opnuni $p |- ( ( M e. Met /\ A (_ ( Open ` M ) ) ->
                 U. A e. ( Open ` M ) ) $=
      ( vx vy vz cmt wcel copn cfv wss cuni c1st cv wa cbl crn wrex wral cpw
      sstr2 eqid opnfss syl5com uniss unipw syl6ss syl6 wi opni simpll ssel2
      adantrr adantll simprr syl3anc ssuni expcom anim2d r19.22sdv ad2antrl
      mpd exp32 r19.23adv eluni2 syl5ib ex r19.21adv jcad isopn sylibrd imp )
      BFGZABHIZJZAKZVMGZVLVNVOBLIZJZCMZDMZGZVTVOJZNZDBOIPZQZCVORZNVPVLVNVRWFVLV
      NAVQSZJZVRVNVMWGJWHVLAVMWGTBVQVQUAZUBUCWHVOWGKVQAWGUDVQUEUFUGVLVNWECVOVLV
      NVSVOGZWEUHVLVNNZVSEMZGZEAQWEWJWKWMWEEAWKWLAGZWMWEWKWNWMNZNZWAVTWLJZNZDWD
      QZWEVLWLVMGZWMWSWPDWLVSBUIVLVNWOUJVNWOWTVLVNWNWTWMAVMWLUKULUMWKWNWMUNUOWN
      WSWEUHWKWMWNWRWCDWDWNWQWBWAWQWNWBVTWLAUPUQURUSUTVAVBVCEVSAVDVEVFVGVHCDVOB
      VQWIVIVJVK $.
      $( [14-Sep-2006] $) $( [4-Sep-2006] $)

    $( The intersection of two open sets of a metric space is open. $)
    opnin $p |- ( ( M e. Met /\ A e. ( Open ` M ) /\ B e. ( Open ` M ) ) ->
               ( A i^i B ) e. ( Open ` M ) ) $=
      ( vz vv vw vu vx vy vf vg cmt wcel copn cfv cin c1st wss cv wa cbl crn
      wrex wral wi pm3.27 a1i reeanv cc0 clt wbr co cr blss 3expb ex anim12d
      adantr cle cif wceq eqid blin sseq1d eleq2 sseq1 anbi12d rcla4ev blelrn
      eleq1 breq2 ifboth sylan2 c2nd blcntr anim1i sylanc sylbid com3l an4s
      com4l ss2in sstr syl2an syl5 exp3a imp com4t r19.23advv syl5ibr syld
      anass syl2anbr r19.23aivv sylbir anbi1d rexbidv rcla4va elin sylanb
      com23 ssel com12 sylan2d com13 r19.21adv jcad com3r ssinss1 sylan isopn
      3imtr4d 3impib ) CLMZACNOZMZBXOMZABPZXOMZXNACQOZRZDSZESZMZYCARZTZECUAOZUB
      ZUCZDAUDZTZBXTRZFSZGSZMZYNBRZTZGYHUCZFBUDZTZTZXRXTRZHSZISZMZUUDXRRZTZIYHU
      CZHXRUDZTZXPXQTXSUUAXNUUJYAYLYJYSXNUUJUEZUUBYJYSTZUUKYAYLTUUBUULUUKUULXNU
      UBUUJUULXNUUBUUJUULXNUUBTZUUBUUIUUMUUBUEUULXNUUBUFUGUULUUMUUHHXRUUCXRMZUU
      MUULUUHUUNXNUUCXTMZUULUUHUEUUBUUNUULXNUUOTZUUHUUNUULUUPUUHUEZUUCAMZUUCBMZ
      TUULUUQUUNUURYJUUSYSUUQUUCYCMZYETZEYHUCZUUCYNMZYPTZGYHUCZUUQUURYJTUUSYSTU
      VBUVETUVAUVDTZGYHUCEYHUCUUQUVAUVDEGYHYHUHUVFUUQEGYHYHYCYHMZYNYHMZTUVFUUQU
      VGUVAUVHUVDUUQUVGUUTTZYETUVHUVCTZYPTUUQUVGUVATUVHUVDTUVIUVJYEYPUUQUVIUVJT
      ZYEYPTZUUQUUPUVKUVLUUHUUPUVKUIJSZUJUKZUUCUVMYGULZYCRZTZJUMUCZUIKSZUJUKZUU
      CUVSYGULZYNRZTZKUMUCZTZUVLUUHUEZXNUVKUWEUEUUOXNUVIUVRUVJUWDXNUVIUVRXNUVGU
      UTUVRJYCUUCCUNUOUPXNUVJUWDXNUVHUVCUWDKYNUUCCUNUOUPUQURUUPUVQUWCTZKUMUCJUM
      UCUWFUWEUUPUWGUWFJKUMUMUWGUVLUUPUVMUMMZUVSUMMZTZUUHUVNUVTUVPUWBUVLUUPUWJU
      UHUEUEZUEZUVNUVTTZUVPUWBTZUWLUWMUWNUVLUWKUWMUVOUWAPZXRRZUWKUWNUVLTUWJUWMU
      WPUUPUUHUWJUWMUWPUUQUEZUWHUVNUWIUVTUWQUUPUWHUVNTZUWIUVTTZTZUWPUUHUUPUWTUW
      PUUHUEUUPUWTTZUWPUUCUVMUVSUSUKZUVMUVSUTZYGULZXRRZUUHUXAUWOUXDXRUUPUWRUWSU
      WOUXDVAUUCUVMUVSCXTXTVBZVCUOVDUXAUXEUUHUXDYHMZUUCUXDMZUXETZUUHUXAUXETUUGU
      XIIUXDYHUUDUXDVAUUEUXHUUFUXEUUDUXDUUCVEUUDUXDXRVFVGVHUXAUXGUXEUUPUXCUMMZU
      IUXCUJUKZTZUXGUWTUUCUXCCXTUXFVIUXBUWRUWSUXLUVMUVSUVMUXCVAUWHUXJUVNUXKUVMU
      XCUMVJUVMUXCUIUJVKVGUVSUXCVAUWIUXJUVTUXKUVSUXCUMVJUVSUXCUIUJVKVGVLZVMURUX
      AUXHUXEUUPUXLUXHUWTCVNOZUUCUXCCXTUXFUXNVBVOUXMVMVPVQUPVRUPVSVTUPWAUVPYEUW
      BYPUWPUVOARUWABRUWPUVPYETUWBYPTUVOAUWABWBUVOYCAWCUWAYNBWCWDVTWEWFWGVTWHWI
      UVQUWCJKUMUMUHWJWKVSWGVTUVGUUTYEWLUVHUVCYPWLWMVTUPWNWOYIUVBDUUCAYBUUCVAZY
      FUVAEYHUXOYDUUTYEYBUUCYCVJWPWQWRYRUVEFUUCBYMUUCVAZYQUVDGYHUXPYOUVCYPYMUUC
      YNVJWPWQWRWDVTUUCABWSWTUPXAUUBUUNUUOXRXTUUCXBXCXDXEXFXGWFXHWGYAUUBYLABXTX
      IURXJVTXCXNXPYKXQYTDEACXTUXFXKFGBCXTUXFXKVGHIXRCXTUXFXKXLXM $.
      $( [15-Sep-2006] $) $( [4-Sep-2006] $)

    $( The set of open sets of a metric space is a topology.  Remark in
       [Kreyszig] p. 19.  This theorem connects the two concepts and makes
       available the theorems for topologies for use with metric spaces. $)
    opntop $p |- ( M e. Met -> ( Open ` M ) e. Top ) $=
      ( vx vy cmt wcel cv copn cfv wss cuni wi wal cin wral wa ctop opnuni ex
      19.21aiv opnin 3expb r19.21aivv jca cvv wb fvex istopg ax-mp sylibr ) ADE
      ZBFZAGHZIZUKJULEZKZBLZUKCFZMULEZCULNBULNZOZULPEZUJUPUSUJUOBUJUMUNUKAQRSUJ
      URBCULULUJUKULEZUQULEZOURUJVBVCURUKUQATUARUBUCULUDEVAUTUEAGUFBCUDULUGUHUI
      $.
      $( [15-Sep-2006] $) $( [28-Aug-2006] $)

    $( The topology generated by the balls of a metric space is its open
       sets. $)
    tgbl $p |- ( M e. Met -> ( topGen ` ran ( ball ` M ) ) = ( Open ` M ) ) $=
      ( vy vz vx copn cfv ctop wcel cbl crn wss cv wa wrex wral ctg wceq cmt
      w3a ctb basgen2t pm3.27d opntop blssopn opni 3expb ex r19.21aivv syl3anc
      ) AEFZGHZAIFJZUJKZBLZCLZHUODLZKMCULNZBUPODUJOZULPFUJQZARHZUKUMURSULTHUSDB
      CULUJUAUBAUCAUDUTUQDBUJUPUTUPUJHZUNUPHZMUQUTVAVBUQCUPUNAUEUFUGUHUI $.
      $( [20-Nov-2006] $) $( [13-Sep-2006] $)

    $( The balls of a metric space form a basis for a topology. $)
    blbas $p |- ( M e. Met -> ran ( ball ` M ) e. Bases ) $=
      ( vy vz vx copn cfv ctop wcel cbl crn wss cv wa wrex wral ctb cmt w3a
      ctg wceq basgen2t pm3.26d opntop blssopn opni 3expb ex r19.21aivv
      syl3anc ) AEFZGHZAIFJZUJKZBLZCLZHUODLZKMCULNZBUPODUJOZULPHZAQHZUKUMURRUSU
      LSFUJTDBCULUJUAUBAUCAUDUTUQDBUJUPUTUPUJHZUNUPHZMUQUTVAVBUQCUPUNAUEUFUGUHU
      I $.
      $( [21-Nov-2006] $) $( [12-Sep-2006] $)
  $}

  $( The empty set is an open set of a metric space.  Part of Theorem T1 of
     [Kreyszig] p. 19. $)
  opn0 $p |- ( M e. Met -> (/) e. ( Open ` M ) ) $=
    ( cmt wcel copn cfv ctop c0 opntop 0opnt syl ) ABCADEZFCGKCAHKIJ $.
    $( [22-Nov-2006] $) $( [4-Sep-2006] $)

  $( A ball of a metric space is an open set. $)
  rnblopn $p |- ( ( M e. Met /\ B e. ran ( ball ` M ) ) ->
              B e. ( Open ` M ) ) $=
    ( cmt wcel cbl cfv crn copn blssopn sseld imp ) BCDZABEFGZDABHFZDLMNABIJK
    $.
    $( [20-Nov-2006] $) $( [12-Sep-2006] $)

  ${
    $d x y M $.  $d x y X $.
    unirnbl.1 $e |- X = ( 1st ` M ) $.
    $( The union of the set of balls of a metric space is its base set. $)
    unirnbl $p |- ( M e. Met -> U. ran ( ball ` M ) = X ) $=
      ( vx vy cmt wcel cbl cfv crn cuni copn wss blssopn uniss syl opnfuni
      sseqtrd cv wrex blex ex eluni2 syl6ibr ssrdv eqssd ) AFGZAHIJZKZBUGUIALIZ
      KZBUGUHUJMUIUKMANUHUJOPABCQRUGDBUIUGDSZBGZULESGEUHTZULUIGUGUMUNEULABCUAUB
      EULUHUCUDUEUF $.
      $( [23-Nov-2006] $) $( [12-Sep-2006] $)

    $( A ball of a metric space is an open set. $)
    blopn $p |- ( ( ( M e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
                    ( P ( ball ` M ) R ) e. ( Open ` M ) ) $=
      ( cmt wcel wa cr cc0 clt wbr cbl cfv co crn copn blelrn wi rnblopn ex
      ad2antrr mpd ) CFGZADGZHBIGJBKLHZHABCMNZOZUGPGZUHCQNGZABCDERUDUIUJSUEUFUD
      UIUJUHCTUAUBUC $.
      $( [9-Mar-2007] $) $( [9-Mar-2007] $)
  $}

  ${
    $d m n x y M $.
    mshausi.1 $e |- M e. Met $.
    $( The topology generated by a metric space is Hausdorff.  Remark in
       [Munkres] p. 126. $)
    mshausi $p |- ( Open ` M ) e. Haus $=
      ( vx vy vm vn copn cfv c1st cuni cmt wcel wceq eqid opnfuni ax-mp eqcomi
      ctop opntop cv c2nd co c2 cdiv cbl cin c0 w3a wrex wne eleq2 ineq1
      eqeq1d 3anbi13d ineq2 3anbi23d rcla42ev cr cc0 clt wbr wa blopn mpanl1
      3simp1 mscl mp3an1 rehalfclt syl 3adant3 wb msgt0 biimp3a halfpos2t
      mpbid jca sylanc 3simp2 blcntr cle bl2in mp3anl1 3simpa leidt 3jca
      syl3anc ishausi ) CDEFAGHZAIHZWHJZWIAKLZWJWIMBAWIWINZOPQWKWHRLBASPCTZWMDT
      ZAUAHZUBZUCUDUBZAUEHZUBZWHLZWNWQWRUBZWHLZWMWSLZWNXALZWSXAUFZUGMZUHZWMFTZL
      ZWNETZLZXHXJUFZUGMZUHZEWHUIFWHUIWMWILZWNWILZWMWNUJZUHZXNXGXCXKWSXJUFZUGMZ
      UHFEWSXAWHWHXHWSMZXIXCXMXTXKXHWSWMUKYAXLXSUGXHWSXJULUMUNXJXAMZXKXDXTXFXCX
      JXAWNUKYBXSXEUGXJXAWSUOUMUPUQXOWQURLZUSWQUTVAZVBZWTXRWKXOYEWTBWMWQAWIWLVC
      VDXOXPXQVEZXRYCYDXOXPYCXQXOXPVBZWPURLZYCWKXOXPYHBWMWNWOAWIWLWONZVFVGZWPVH
      VIZVJZXRUSWPUTVAZYDXOXPXQYMWKXOXPXQYMVKBWMWNWOAWIWLYIVLVGVMXOXPYMYDVKZXQY
      GYHYNYJWPVNVIVJVOZVPZVQXPYEXBXRWKXPYEXBBWNWQAWIWLVCVDXOXPXQVRZYPVQXRXCXDX
      FXOYEXCXRWKXOYEXCBWOWMWQAWIWLYIVSVDYFYPVQXPYEXDXRWKXPYEXDBWOWNWQAWIWLYIVS
      VDYQYPVQYGYCYDWQWQVTVAZUHZXFXRWKXOXPYSXFBWOWMWNWQAWIWLYIWAWBXOXPXQWCXRYCY
      DYRYLYOXOXPYRXQYGYCYRYKWQWDVIVJWEVQWEWFWG $.
      $( [26-Apr-2007] $) $( [9-Mar-2007] $)
  $}

  $( The topology generated by a metric space is Hausdorff.  Remark in
     [Munkres] p. 126. $)
  mshaus $p |- ( M e. Met -> ( Open ` M ) e. Haus ) $=
    ( cmt wcel copn cfv cha c0 cop cif wceq fveq2 eleq1d 0ms elimel mshausi
    dedth ) ABCZADEZFCQAGGHZIZDEZFCASATJRUAFATDKLTASBMNOP $.
    $( [?] $) $( [15-Mar-2007] $)


