$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Open sets of a metric space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.  $d d x y z D $.  $d x y z X $.
    opnval.1 $e |- J = ( MetOpen ` D ) $.
    $( An open set is a subset of a metric space which includes a ball around
       each of its points.  Definition 1.3-2 of [Kreyszig] p. 18.  The object
       ` ( MetOpen `` D ) ` is the family of all open sets in the metric space
       determined by the metric ` D ` .  By ~ opntop , the open sets of a
       metric space form a topology ` J ` , whose base set is ` U. J ` by
       ~ uniopn2 . $)
    opnval $p |- ( D e. Met -> J = ( topGen ` ran ( ball ` D ) ) ) $=
      ( vd cme wcel copn cfv cbl crn ctg cv wceq fveq2 rneqd fveq2d df-opn fvex
      fvmpt syl5eq ) AEFBAGHAIHZJZKHZCDADLZIHZJZKHUCEGUDAMZUFUBKUGUEUAUDAINOPDQ
      UBKRST $.
      $( [12-Nov-2013] $) $( [1-Sep-2006] $)

    $( The set of open sets of a metric space is a topology.  Remark in
       [Kreyszig] p. 19.  This theorem connects the two concepts and makes
       available the theorems for topologies for use with metric spaces. $)
    opntop $p |- ( D e. Met -> J e. Top ) $=
      ( cme wcel cbl cfv crn ctg ctop opnval ctb blbas tgcl syl eqeltrd ) ADEZB
      AFGHZIGZJABCKQRLESJEAMRNOP $.
      $( [12-Nov-2013] $) $( [28-Aug-2006] $)
  $}

  ${
    $d x y z A $.  $d d x y z D $.  $d x y z X $.
    uniopn2.1 $e |- X = dom dom D $.
    uniopn2.2 $e |- J = ( MetOpen ` D ) $.
    $( The union of all open sets in a metric space is its underlying set. $)
    uniopn2 $p |- ( D e. Met -> X = U. J ) $=
      ( cme wcel cuni cbl cfv crn ctg opnval unieqd ctb blbas unitg syl unirnbl
      wceq 3eqtrrd ) AFGZBHAIJKZLJZHZUCHZCUBBUDABEMNUBUCOGUEUFTAPUCQRACDSUA $.
      $( [12-Nov-2013] $) $( [4-Sep-2006] $)

    $( The defining property of an open set of a metric space. $)
    isopn $p |- ( D e. Met -> ( A e. J <->
   ( A C_ X /\ A. x e. A E. y e. ran ( ball ` D ) ( x e. y /\ y C_ A ) ) ) ) $=
      ( cme wcel cbl cfv crn ctg cuni wss cv wa wrex wral opnval wb blbas eltg2
      eleq2d ctb syl unirnbl sseq2d anbi1d 3bitrd ) DIJZCEJCDKLMZNLZJZCUMOZPZAQ
      BQZJURCPRBUMSACTZRZCFPZUSRULEUNCDEHUAUEULUMUFJUOUTUBDUCABCUMUDUGULUQVAUSU
      LUPFCDFGUHUIUJUK $.
      $( [12-Nov-2013] $) $( [1-Sep-2006] $)

    $( The family of open sets of a metric space is a collection of subsets of
       the base set. $)
    opnfss $p |- ( D e. Met -> J C_ ~P X ) $=
      ( cme wcel cuni cpw pwuni uniopn2 pweqd syl5sseqr ) AFGZBHZIBCIBJNCOABCDE
      KLM $.
      $( [12-Nov-2013] $) $( [3-Sep-2006] $)

    $( The base set of a metric space is open.  Part of Theorem T1 of
       [Kreyszig] p. 19. $)
    opnm $p |- ( D e. Met -> X e. J ) $=
      ( cme wcel cuni uniopn2 ctop opntop eqid topopn syl eqeltrd ) AFGZCBHZBAB
      CDEIPBJGQBGABEKBQQLMNO $.
      $( [12-Nov-2013] $) $( [4-Sep-2006] $)

    $( A defining property of an open set of a metric space. $)
    isopn4 $p |- ( D e. Met -> ( A e. J <-> ( A C_ X /\
           A. x e. A E. y e. RR+ ( x ( ball ` D ) y ) C_ A ) ) ) $=
      ( vz cme wcel wss cv wa cbl cfv crn wrex wral co crp isopn blssex anassrs
      wb ssel2 sylan2 ralbidva pm5.32da bitrd ) DJKZCEKCFLZAMZIMZKUNCLNIDOPZQRZ
      ACSZNULUMBMUOTCLBUARZACSZNAICDEFGHUBUKULUQUSUKULNUPURACUKULUMCKZUPURUEZUL
      UTNUKUMFKVACFUMUFICDUMFBGUCUGUDUHUIUJ $.
      $( [12-Nov-2013] $) $( [5-May-2007] $)

    $( An open set of a metric space is a subspace of its base set. $)
    opnss $p |- ( ( D e. Met /\ A e. J ) -> A C_ X ) $=
      ( vx vy cme wcel wss cv wa cbl cfv crn wrex wral isopn simprbda ) BIJACJA
      DKGLHLZJUAAKMHBNOPQGARGHABCDEFST $.
      $( [4-Nov-2007] $) $( [3-Sep-2006] $)
  $}

  ${
    $d x D $.  $d x J $.  $d x K $.
    ismsg.j $e |- J = ( TopOpen ` K ) $.
    ismsg.d $e |- D = ( dist ` K ) $.
    $( Express the predicate " ` <. X , D >. ` is a metric space" with
       underlying set ` X ` and distance function ` D ` . $)
    ismsg $p |- ( K e. MetSp <->
                  ( K e. TopSp /\ D e. Met /\ J = ( MetOpen ` D ) ) ) $=
      ( vx cmt wcel ctps copn cfv wceq wa cme cts cds fveq2 syl6eqr ctop c0 w3a
      cv fveq2d eqeq12d df-ms elrab2 cdm wss cbl crn ctg cvv crab df-opn dmmpt2
      simpl ssrab2 eqsstri a1i cbs cuni eqid istps simplbi eleq1 syl5ibcom 0opn
      imp elfvdm 3syl sseldd simpr 3jca 3simpb impbii bitri ) CGHCIHZBAJKZLZMZV
      QANHZVSUAZFUBZOKZWCPKZJKZLVSFCIGWCCLZWDBWFVRWGWDCOKBWCCOQDRWGWEAJWGWECPKA
      WCCPQERUCUDFUEUFVTWBVTVQWAVSVQVSUPVTJUGZNAWHNUHVTWHWCUIKUJUKKZULHZFNUMNFN
      WIJFUNUOWJFNUQURUSVTVRSHZTVRHAWHHVQVSWKVQBSHZVSWKVQWLCUTKZBVALWMBCWMVBDVC
      VDBVRSVEVFVHVRVGTAJVIVJVKVQVSVLVMVQWAVSVNVOVP $.
      $( [12-Nov-2013] $) $( [27-Aug-2006] $)
  $}

  ${
    $d x D $.  $d x J $.  $d x K $.
    msf.1 $e |- X = ( Base ` M ) $.
    msf.2 $e |- D = ( dist ` M ) $.
    $( The relation between the base set and the distance function. $)
    msdmdm $p |- ( M e. MetSp -> X = dom dom D ) $=
      ( cmt wcel cts cfv cuni copn cdm ctps wceq eqid ismsg simp3bi unieqd ctop
      cme wa simp1bi istps sylib simprd simp2bi uniopn2 syl 3eqtr4d ) BFGZBHIZJ
      ZAKIZJZCALLZUJUKUMUJBMGZATGZUKUMNZAUKBUKOZEPZQRUJUKSGZCULNZUJUPVAVBUAUJUP
      UQURUTUBCUKBDUSUCUDUEUJUQUOUNNUJUPUQURUTUFAUMUOUOOUMOUGUHUI $.
      $( [12-Nov-2013] $)

    $( Mapping of the distance function of a metric space. $)
    msf $p |- ( M e. MetSp -> D : ( X X. X ) --> RR ) $=
      ( cmt wcel cxp cr wf cdm cme ctps cts cfv copn wceq eqid ismsg simp2bi
      metf syl msdmdm xpeq12 syl11anc feq2d mpbird ) BFGZCCHZIAJAKKZUJHZIAJZUHA
      LGZULUHBMGUMBNOZAPOQAUNBUNRESTAUJUJRUAUBUHUIUKIAUHCUJQZUOUIUKQABCDEUCZUPC
      UJCUJUDUEUFUG $.
      $( [12-Nov-2013] $) $( [30-Aug-2006] $)

    $( Closure of the distance function of a metric space. $)
    mscl $p |- ( ( M e. MetSp /\ A e. X /\ B e. X ) -> ( A D B ) e. RR ) $=
      ( cmt wcel cxp cr wf co msf foprrn syl3an1 ) DHIEEJKCLAEIBEIABCMKICDEFGNA
      BKEECOP $.
      $( [30-Aug-2006] $)
  $}

  ${
    $d f g u v w x y z A $.  $d f g u v w x y B $.  $d f g u v w x y z D $.
    $d x y P $.  $d x y R $.  $d u v w J $.
    opni.1 $e |- J = ( MetOpen ` D ) $.
    $( An open set of a metric space includes a ball around each of its
       points. $)
    opni $p |- ( ( D e. Met /\ A e. J /\ P e. A ) ->
                 E. x e. ran ( ball ` D ) ( P e. x /\ x C_ A ) ) $=
      ( vy cme wcel cv wss wa cbl cfv crn wrex cdm wral wi eqid rexbidv rcla4cv
      isopn wceq eleq1 anbi1d adantl syl6bi 3imp ) CHIZBEIZDBIZDAJZIZUMBKZLZACM
      NOZPZUJUKBCQQZKZGJZUMIZUOLZAUQPZGBRZLULURSZGABCEUSUSTFUCVEVFUTVDURGDBVADU
      DZVCUPAUQVGVBUNUOVADUMUEUFUAUBUGUHUI $.
      $( [12-Nov-2013] $) $( [3-Sep-2006] $)

    $( An open set of a metric space includes a ball around each of its
       points. $)
    opni2 $p |- ( ( D e. Met /\ A e. J /\ P e. A ) ->
                 E. x e. RR+ ( P ( ball ` D ) x ) C_ A ) $=
      ( vy cme wcel cv cbl cfv co wss crp wrex wa wral cdm eqid isopn4 rcla4cva
      biimpa simprd wceq opreq1 sseq1d rexbidv sylan 3impa ) CHIZBEIZDBIZDAJZCK
      LZMZBNZAOPZUKULQZGJZUNUOMZBNZAOPZGBRZUMURUSBCSSZNZVDUKULVFVDQGABCEVEVETFU
      AUCUDVCURGDBUTDUEZVBUQAOVGVAUPBUTDUNUOUFUGUHUBUIUJ $.
      $( [12-Nov-2013] $) $( [2-May-2007] $)

    $( An open set of a metric space includes an arbitrarily small ball around
       each of its points. $)
    opni3 $p |- ( ( ( D e. Met /\ A e. J /\ P e. A ) /\ R e. RR+ ) ->
        E. x e. RR+ ( x < R /\ ( P ( ball ` D ) x ) C_ A ) ) $=
      ( vy cme wcel w3a crp wa cv co wss wrex cdm wi sylan cbl cfv opni2 adantr
      clt wbr simp1 eqid opnss ssel2 3impa ssblex expcom anim2d reximdv syl5com
      jca sstr expr rexlimdv mpd ) CIJZBFJZDBJZKZELJZMDHNZCUAUBZOZBPZHLQZANZEUE
      UFZDVLVHOZBPZMZALQZVEVKVFHBCDFGUCUDVEVBDCRRZJZMZVFVKVQSVEVBVSVBVCVDUGVBVC
      VDVSVBVCMBVRPVDVSBCFVRVRUHZGUIBVRDUJTUKUQVTVFMVJVQHLVTVFVGLJZVJVQSVTVFWBM
      MVMVNVIPZMZALQVJVQACDEVGVRWAULVJWDVPALVJWCVOVMWCVJVOVNVIBURUMUNUOUPUSUTTV
      A $.
      $( [12-Nov-2013] $) $( [20-Sep-2007] $)

    $( The balls of a metric space are open sets. $)
    blssopn $p |- ( D e. Met -> ran ( ball ` D ) C_ J ) $=
      ( cme wcel cbl cfv crn ctg ctb wss blbas bastg syl opnval sseqtr4d ) ADEZ
      AFGHZRIGZBQRJERSKALRMNABCOP $.
      $( [23-Dec-2013] $) $( [12-Sep-2006] $)

    $( The union of a collection of open sets of a metric space is open.
       Theorem T2 of [Kreyszig] p. 19. $)
    opnuni $p |- ( ( D e. Met /\ A C_ J ) ->
                 U. A e. J ) $=
      ( cme wcel ctop wss cuni opntop uniopn sylan ) BEFCGFACHAICFBCDJACKL $.
      $( [23-Dec-2013] $) $( [4-Sep-2006] $)

    $( The intersection of two open sets of a metric space is open. $)
    opnin $p |- ( ( D e. Met /\ A e. J /\ B e. J ) ->
               ( A i^i B ) e. J ) $=
      ( cme wcel ctop cin opntop inopn syl3an1 ) CFGDHGADGBDGABIDGCDEJABDKL $.
      $( [23-Dec-2013] $) $( [4-Sep-2006] $)
  $}

  ${
    opn0.1 $e |- J = ( MetOpen ` D ) $.
    $( The empty set is an open set of a metric space.  Part of Theorem T1 of
       [Kreyszig] p. 19. $)
    opn0 $p |- ( D e. Met -> (/) e. J ) $=
      ( cme wcel ctop c0 opntop 0opn syl ) ADEBFEGBEABCHBIJ $.
      $( [21-Feb-2008] $) $( [4-Sep-2006] $)

    $( A ball of a metric space is an open set. $)
    rnblopn $p |- ( ( D e. Met /\ B e. ran ( ball ` D ) ) -> B e. J ) $=
      ( cme wcel cbl cfv crn blssopn sseld imp ) BEFZABGHIZFACFMNCABCDJKL $.
      $( [13-Nov-2007] $) $( [12-Sep-2006] $)
  $}

  ${
    $d r x y z D $.  $d r x y J $.  $d r x y z P $.  $d r y N $.  $d x y S $.
    $d r x y z R $.  $d r x y z X $.  $d z T $.
    blopn.1 $e |- X = dom dom D $.
    blopn.2 $e |- J = ( MetOpen ` D ) $.
    $( A ball of a metric space is an open set. $)
    blopn $p |- ( ( D e. Met /\ P e. X /\ R e. RR+ ) ->
                    ( P ( ball ` D ) R ) e. J ) $=
      ( cme wcel crp w3a cbl cfv co simp1 crn blelrn rnblopn sylan2 mpancom ) A
      HIZUABEIZCJIZKZBCALMZNZDIZUAUBUCOUDUAUFUEPIUGABCEFQUFADGRST $.
      $( [12-Nov-2013] $) $( [9-Mar-2007] $)

    $( A neighborhood of a point defined in terms of balls. $)
    neibl $p |- ( ( D e. Met /\ P e. X ) -> ( N e. ( ( nei ` J ) ` { P } )
    <-> ( N C_ X /\ E. r e. RR+ ( P ( ball ` D ) r ) C_ N ) ) ) $=
      ( vy wcel wa cfv wss cv wrex crp wb adantr syl11anc wi cme cnei cuni ctop
      csn co opntop uniopn2 eleq2d biimpa eqid isneip sseq2d anbi1d opni2 sstr2
      cbl com12 reximdv syl5com 3exp imp4a ad2antrr rexlimdv blopn blcntr eleq2
      w3a wceq sseq1 anbi12d rcla4ev expr 3expia impbid pm5.32da 3bitr2d ) AUAJ
      ZBEJZKZDBUECUBLLJZDCUCZMZBINZJZWDDMZKZICOZKZDEMZWHKWJBFNZAUQLUFZDMZFPOZKV
      TCUDJZBWBJZWAWIQVRWOVSACHUGRVRVSWPVREWBBACEGHUHZUIUJBICDWBWBUKULSVTWJWCWH
      VRWJWCQVSVREWBDWQUMRUNVTWJWHWNVTWJKZWHWNWRWGWNICVRWDCJZWGWNTTVSWJVRWSWEWF
      WNVRWSWEWFWNTVRWSWEVHWLWDMZFPOWFWNFWDABCHUOWFWTWMFPWTWFWMWLWDDUPURUSUTVAV
      BVCVDVTWNWHTWJVTWMWHFPVRVSWKPJZWMWHTZVRVSXAVHWLCJZBWLJZXBABWKCEGHVEABWKEG
      VFXCXDWMWHWGXDWMKIWLCWDWLVIWEXDWFWMWDWLBVGWDWLDVJVKVLVMSVNVDRVOVPVQ $.
      $( [23-Dec-2013] $) $( [8-Nov-2007] $)

    $( The neighborhoods around a point ` P ` of a metric space are those
       subsets containing a ball around ` P ` .  Definition of neighborhood in
       [Kreyszig] p. 19. $)
    metnei $p |- ( ( D e. Met /\ P e. X ) -> ( ( nei ` J ) ` { P } ) = { x |
         ( x C_ X /\ E. r e. RR+ ( P ( ball ` D ) r ) C_ x ) } ) $=
      ( cme wcel wa cv wss cbl cfv co crp wrex csn cnei neibl abbi2dv ) BIJCEJK
      ALZEMCFLBNOPUCMFQRKACSDTOOBCDUCEFGHUAUB $.
      $( [12-Nov-2013] $) $( [8-Nov-2007] $)

    $( A ball around a point is a neighborhood of the point. $)
    blnei $p |- ( ( D e. Met /\ P e. X /\ R e. RR+ ) ->
              ( P ( ball ` D ) R ) e. ( ( nei ` J ) ` { P } ) ) $=
      ( vr cme wcel crp w3a cbl cfv co csn cnei wss cv wrex wa wb neibl 3adant3
      blssm ssid wceq opreq2 sseq1d rcla4ev mpan2 3ad2ant3 mpbir2and ) AIJZBEJZ
      CKJZLBCAMNZOZBPDQNNJZURERZBHSZUQOZURRZHKTZUNUOUSUTVDUAUBUPABDUREHFGUCUDAB
      CEFUEUPUNVDUOUPURURRZVDURUFVCVEHCKVACUGVBURURVACBUQUHUIUJUKULUM $.
      $( [12-Nov-2013] $) $( [8-Nov-2007] $)

    $( Every ball around a limit point ` P ` of a subset ` S ` includes a
       member of ` S ` (even if ` P e/ S ` ). $)
    lpbl $p |- ( ( ( D e. Met /\ S C_ X /\ P e. ( ( limPt ` J ) ` S ) ) /\
           R e. RR+ ) -> E. x e. S x e. ( P ( ball ` D ) R ) ) $=
      ( wcel wss cfv wa cin c0 wne syl wceq syl11anc syl111anc cme clp w3a cdif
      crp cbl csn wrex cnei wral simpl1 cuni ctop opntop simpl2 uniopn2 sseqtrd
      co cv eqid simpl3 sseldd eleqtrrd simpr blnei wb islp2 mpbid ineq1 neeq1d
      lpss rcla4va wex elin eldifi anim2i ancomd sylbi eximi n0 df-rex 3imtr4i
      ) BUAJZEGKZCEFUBLLZJZUCZDUEJZMZCDBUFLURZECUGZUDZNZOPZAUSZWJJZAEUHZWIWJWKF
      UILLZJZWOWLNZOPZAWRUJZWNWIWCCGJWHWSWCWDWFWHUKZWICFULZGWIWEXDCWIFUMJZEXDKZ
      WEXDKWIWCXEXCBFIUNQZWIEGXDWCWDWFWHUOWIWCGXDRXCBFGHIUPQZUQZEFXDXDUTZVKSWCW
      DWFWHVAZVBZXHVCWGWHVDBCDFGHIVETWIWFXBXKWIXEXFCXDJWFXBVFXGXIXLCEAFXDXJVGTV
      HXAWNAWJWRWOWJRWTWMOWOWJWLVIVJVLSWOWMJZAVMWOEJZWPMZAVMWNWQXMXOAXMWPWOWLJZ
      MZXOWOWJWLVNXQWPXNXPXNWPWOEWKVOVPVQVRVSAWMVTWPAEWAWBQ $.
      $( [23-Dec-2013] $) $( [9-Nov-2007] $)

    blcld.3 $e |- S = { z e. X | ( P D z ) <_ R } $.
    $( A "closed ball" in a metric space is actually closed.  (Contributed by
       Mario Carneiro, 31-Dec-2013.) $)
    blcld $p |- ( ( D e. Met /\ P e. X /\ R e. RR ) -> S e. ( Clsd ` J ) ) $=
      ( vx wcel cr wceq wb co wbr syl111anc syl11anc cle cme w3a ccld cuni cdif
      vy cfv uniopn2 3ad2ant1 difeq1d wss cv wrex wral ctop opntop eltop2 difss
      wa syl syl5sseq cmin cbl crp simpl1 eldifi adantl cc0 simpl2 metcl simpl3
      clt resubcl wn eldif opreq2 breq1d elrab2 simplbi2 con3d imp sylbi mpbird
      ltnle posdif mpbid elrp sylanbrc blopn blcntr cin inss1 sseli blssm sseld
      c0 wal simp11 3adant3 3ad2ant2 simp3 elbl2 syl22anc ltsub13 caddc mettri3
      wi simp12 syl13anc lesubadd ltletr exp3acom23 mpd sylbid 3expia mpdd syl5
      com23 inss2 simplbi simprbi lenlt syl5ib mpdi pm2.65d alrimiv eq0 reldisj
      sylibr eleq2 anbi12d rcla4ev syl12anc ralrimiva mpbir2and eqeltrrd ssrab2
      sseq1 crab eqsstri eqid iscld2 ) BUALZCGLZDMLZUBZEFUCUGLZFUDZEUEZFLZUUFGE
      UEZUUIFUUFGUUHEUUCUUDGUUHNUUEBFGHIUHUIZUJUUFUUKFLZUUKUUHUKZUFULZKULZLZUUP
      UUKUKZUSZKFUMZUFUUKUNZUUFFUOLZUUMUUNUVAUSOUUCUUDUVBUUEBFIUPUIZUFKUUKFUQUT
      UUFGUUKUUHGEURUULVAUUFUUTUFUUKUUFUUOUUKLZUSZUUOCUUOBPZDVBPZBVCUGPZFLZUUOU
      VHLZUVHUUKUKZUUTUVEUUCUUOGLZUVGVDLZUVIUUCUUDUUEUVDVEZUVDUVLUUFUUOGEVFZVGZ
      UVEUVGMLZVHUVGVLQZUVMUVEUVFMLZUUEUVQUVEUUCUUDUVLUVSUVNUUCUUDUUEUVDVIUVPCU
      UOBGHVJRZUUCUUDUUEUVDVKZUVFDVMSUVEDUVFVLQZUVRUVEUWBUVFDTQZVNZUVDUWDUUFUVD
      UVLUUOELZVNZUSUWDUUOGEVOUVLUWFUWDUVLUWCUWEUWEUVLUWCCAULZBPZDTQZUWCAUUOGEU
      WGUUONUWHUVFDTUWGUUOCBVPVQJVRVSVTWAWBVGUVEUUEUVSUWBUWDOUWAUVTDUVFWDSWCUVE
      UUEUVSUWBUVROUWAUVTDUVFWESWFUVGWGWHZBUUOUVGFGHIWIRUVEUUCUVLUVMUVJUVNUVPUW
      JBUUOUVGGHWJRUVEUVHEWKZWPNZUVKUVEUUPUWKLZVNZKWQUWLUVEUWNKUVEUWMDCUUPBPZVL
      QZUWMUUPUVHLZUVEUWPUWKUVHUUPUVHEWLWMUVEUWQUUPGLZUWPUVEUVHGUUPUVEUUCUVLUVM
      UVHGUKZUVNUVPUWJBUUOUVGGHWNRZWOUVEUWRUWQUWPUUFUVDUWRUWQUWPXGUUFUVDUWRUBZU
      WQUUOUUPBPZUVGVLQZUWPUXAUUCUVMUVLUWRUWQUXCOUUCUUDUUEUVDUWRWRZUUFUVDUVMUWR
      UWJWSUVDUUFUVLUWRUVOWTZUUFUVDUWRXAZUUPBUUOUVGGHXBXCUXAUXCDUVFUXBVBPZVLQZU
      WPUXAUXBMLZUVSUUEUXCUXHOUXAUUCUVLUWRUXIUXDUXEUXFUUOUUPBGHVJRZUUFUVDUVSUWR
      UVTWSZUUFUVDUUEUWRUWAWSZUXBUVFDXDRUXAUXGUWOTQZUXHUWPXGZUXAUXMUVFUWOUXBXEP
      TQZUXAUUCUUDUVLUWRUXOUXDUUCUUDUUEUVDUWRXHZUXEUXFCUUOUUPBGHXFXIUXAUVSUXIUW
      OMLZUXMUXOOUXKUXJUXAUUCUUDUWRUXQUXDUXPUXFCUUPBGHVJRZUVFUXBUWOXJRWCUXAUUEU
      XGMLZUXQUXMUXNXGUXLUXAUVSUXIUXSUXKUXJUVFUXBVMSUXRUUEUXSUXQUBUXHUXMUWPDUXG
      UWOXKXLRXMXNXNXOXRXPXQUWMUUPELZUVEUWPVNZUWKEUUPUVHEXSWMUVEUXTUWRUYAUXTUWR
      UWODTQZUWIUYBAUUPGEUWGUUPNUWHUWODTUWGUUPCBVPVQJVRZXTUVEUWRUXTUYAUUFUVDUWR
      UXTUYAXGUXTUYBUXAUYAUXTUWRUYBUYCYAUXAUXQUUEUYBUYAOUXRUXLUWODYBSYCXOXRYDXQ
      YEYFKUWKYGYIUVEUWSUWLUVKOUWTUVHEGYHUTWFUUSUVJUVKUSKUVHFUUPUVHNUUQUVJUURUV
      KUUPUVHUUOYJUUPUVHUUKYRYKYLYMYNYOYPUUFUVBEUUHUKUUGUUJOUVCUUFGEUUHEUWIAGYS
      GJUWIAGYQYTUULVAEFUUHUUHUUAUUBSWC $.
      $( [29-Jan-2014] $) $( [31-Dec-2013] $)

    $( The closure of an open ball in a metric space is contained in the
       corresponding closed ball.  (The converse is not, in general, true; for
       example, with the discrete metric, the closed ball of radius 1 is the
       whole space, but the open ball of radius 1 is just a point, whose
       closure is also a point.)  (Contributed by Mario Carneiro,
       31-Dec-2013.) $)
    blcls $p |- ( ( D e. Met /\ P e. X /\ R e. RR+ ) ->
                  ( ( cls ` J ) ` ( P ( ball ` D ) R ) ) C_ S ) $=
      ( cme wcel crp w3a cfv co wss cr wbr wa ctop ccld cbl ccl opntop 3ad2ant1
      rpre blcld syl3an3 cv cle crab wral blssm clt metcl 3expa 3adantl3 simpl3
      elbl wi syl ltle syl11anc expimpd sylbid ralrimiv sylanbrc syl6sseqr cuni
      ssrab eqid clsss2 syl111anc ) BKLZCGLZDMLZNZFUALZEFUBOLZCDBUCOPZEQWAFUDOO
      EQVOVPVSVQBFIUEUFVQVOVPDRLZVTDUGZABCDEFGHIJUHUIVRWACAUJZBPZDUKSZAGULZEVRW
      AGQWFAWAUMWAWGQBCDGHUNVRWFAWAVRWDWALWDGLZWEDUOSZTWFWDBCDGHUTVRWHWIWFVRWHT
      ZWERLZWBWIWFVAVOVPWHWKVQVOVPWHWKCWDBGHUPUQURWJVQWBVOVPVQWHUSWCVBWEDVCVDVE
      VFVGWFAGWAVKVHJVIEWAFFVJZWLVLVMVN $.
      $( [31-Dec-2013] $)

    $( A smaller closed ball is contained in a larger open ball.  (Contributed
       by Mario Carneiro, 5-Jan-2014.) $)
    blsscls2 $p |- ( ( ( D e. Met /\ P e. X ) /\
                       ( R e. RR+ /\ T e. RR+ /\ R < T ) ) ->
                     S C_ ( P ( ball ` D ) T ) ) $=
      ( wcel wa crp clt wbr w3a co wi cr cme cv cle crab cbl cfv wral wss metcl
      simplr3 3expa adantlr simplr1 syl simplr2 lelttr exp3acom23 syl111anc mpd
      rpre wb simp2 elbl2 an4s sylanr1 anassrs sylibrd ralrimiva rabss syl5eqss
      sylibr ) BUALZCHLZMZDNLZFNLZDFOPZQZMZECAUBZBRZDUCPZAHUDZCFBUEUFRZKVSWBVTW
      DLZSZAHUGWCWDUHVSWFAHVSVTHLZMZWBWAFOPZWEWHVQWBWISZVOVPVQVNWGUJWHWATLZDTLZ
      FTLZVQWJSVNWGWKVRVLVMWGWKCVTBHIUIUKULWHVOWLVOVPVQVNWGUMDUTUNWHVPWMVOVPVQV
      NWGUOFUTUNWKWLWMQWBVQWIWADFUPUQURUSVNVRWGWEWIVAZVRVNVPWGWNVOVPVQVBVLVPVMW
      GWNVTBCFHIVCVDVEVFVGVHWBAHWDVIVKVJ $.
      $( [10-Jan-2014] $)
  $}

  ${
    $d x D $.  $d x P $.  $d x R $.  $d x S $.  $d x X $.
    blsscls.1 $e |- X = dom dom D $.
    blsscls.2 $e |- J = ( MetOpen ` D ) $.
    $( If two concentric balls have different radii, the closure of the smaller
       one is contained in the larger one.  (Contributed by Mario Carneiro,
       5-Jan-2014.) $)
    blsscls $p |- ( ( ( D e. Met /\ P e. X ) /\
                    ( R e. RR+ /\ S e. RR+ /\ R < S ) ) ->
            ( ( cls ` J ) ` ( P ( ball ` D ) R ) ) C_ ( P ( ball ` D ) S ) ) $=
      ( vx cme wcel wa crp clt wbr w3a cbl cfv co ccl cle crab eqid blcls 3expa
      cv wss 3ad2antr1 blsscls2 sstrd ) AJKZBFKZLZCMKZDMKZCDNOZPLBCAQRZSETRRZBI
      UFASCUAOIFUBZBDUQSUMUOUNURUSUGZUPUKULUNUTIABCUSEFGHUSUCZUDUEUHIABCUSDEFGH
      VAUIUJ $.
      $( [5-Jan-2014] $)
  $}

  ${
    $d g r s x C $.  $d g r s x D $.  $d g r s x J $.  $d g r s x K $.
    $d g r s x X $.  $d g r s x Y $.
    metequiv.1 $e |- X = dom dom C $.
    metequiv.2 $e |- Y = dom dom D $.
    metequiv.3 $e |- J = ( MetOpen ` C ) $.
    metequiv.4 $e |- K = ( MetOpen ` D ) $.
    $( One direction of ~ metequiv . $)
    metequivlem $p |- ( ( ( C e. Met /\ D e. Met ) /\ X = Y ) ->
          ( A. x e. X A. r e. RR+ E. s e. RR+
                ( x ( ball ` D ) s ) C_ ( x ( ball ` C ) r ) -> J C_ K ) ) $=
      ( wcel wa cv wss crp wral wi vg cme wceq cbl cfv wrex ssralv sstr2 reximi
      r19.37av syl ralimi r19.23v sylib ral2imi com12 ad2antll imdistand simprl
      co syl6 sseq2d anbi1d sylibd wb isopn4 ad2antrr ad2antlr 3imtr4d ssrdv ex
      expdimp ) BUBNZCUBNZOZFGUCZAPZHPCUDUEUTZVQIPBUDUEUTZQZHRUFZIRSZAFSZDEQZVO
      VPWCOZWDVOWEOZUADEWFUAPZFQZVSWGQZIRUFZAWGSZOZWGGQZVRWGQZHRUFZAWGSZOZWGDNZ
      WGENZWFWLWHWPOWQWFWHWKWPWCWHWKWPTZTVOVPWHWCWTWHWCWBAWGSWTWBAWGFUGWBWJWOAW
      GWBWIWOTZIRSWJWOTWAXAIRWAWIWNTZHRUFXAVTXBHRVRVSWGUHUIWIWNHRUJUKULWIWOIRUM
      UNUOVAUPUQURWFWHWMWPWFFGWGVOVPWCUSVBVCVDVMWRWLVEVNWEAIWGBDFJLVFVGVNWSWQVE
      VMWEAHWGCEGKMVFVHVIVJVKVL $.
      $( [12-Nov-2013] $)

    $( The other direction of ~ metequiv . $)
    metequivlem2 $p |- ( ( ( C e. Met /\ D e. Met /\ J = K ) /\ x e. X ) ->
      A. r e. RR+ E. s e. RR+ ( x ( ball ` D ) s ) C_ ( x ( ball ` C ) r ) ) $=
      ( cme wcel cv wa cbl crp syl111anc wceq w3a cfv co wss wrex simpl2 simpl1
      simprl simprr blopn simpl3 eleqtrd blcntr opni2 anassrs ralrimiva ) BNOZC
      NOZDEUAZUBZAPZFOZQVBHPCRUCUDVBIPZBRUCUDZUEHSUFZISVAVCVDSOZVFVAVCVGQZQZUSV
      EEOVBVEOZVFURUSUTVHUGVIVEDEVIURVCVGVEDOURUSUTVHUHZVAVCVGUIZVAVCVGUJZBVBVD
      DFJLUKTURUSUTVHULUMVIURVCVGVJVKVLVMBVBVDFJUNTHVECVBEMUOTUPUQ $.
      $( [12-Nov-2013] $)

    $( Two ways of saying that two metrics generate the same topology.  Two
       metrics satisfying the right-hand side are said to be (topologically)
       equivalent.  (Contributed by Jeffrey Hankins, 21-Jun-2009.) $)
    metequiv $p |- ( ( C e. Met /\ D e. Met ) -> ( J = K <->
      ( X = Y /\ A. x e. X
          ( A. r e. RR+ E. s e. RR+
                ( x ( ball ` D ) s ) C_ ( x ( ball ` C ) r ) /\
            A. s e. RR+ E. r e. RR+
                ( x ( ball ` C ) r ) C_ ( x ( ball ` D ) s ) ) ) ) ) $=
      ( wcel wa wceq cv wss crp wral cme cbl cfv co wrex w3a cuni unieq uniopn2
      eqeqan12d 3impia metequivlem2 simpl2 simpl1 simpl3 eqcomd eleq2d syl31anc
      syl5ibr biimpa ralrimiva 3expia metequivlem wb raleq adantl ancom syl2anb
      jca wi eqcom sylbid anim12d r19.26 eqss 3imtr4g expimpd impbid ) BUANZCUA
      NZOZDEPZFGPZAQZHQCUBUCUDZWDIQBUBUCUDZRHSUEISTZWFWERISUEHSTZOZAFTZOZVSVTWB
      WKVSVTWBUFZWCWJVSVTWBWCWBWCWADUGZEUGZPDEUHVSVTFWMGWNBDFJLUICEGKMUIUJUSUKZ
      WLWIAFWLWDFNZOZWGWHABCDEFGHIJKLMULWQVTVSEDPWDGNZWHVSVTWBWPUMVSVTWBWPUNWQD
      EVSVTWBWPUOUPWLWPWRWLFGWDWOUQUTACBEDGFIHKJMLULURVIVAVIVBWAWCWJWBWAWCOZWGA
      FTZWHAFTZODERZEDRZOWJWBWSWTXBXAXCABCDEFGHIJKLMVCWSXAWHAGTZXCWCXAXDVDWAWHA
      FGVEVFWAVTVSOGFPXDXCVJWCVSVTVGFGVKACBEDGFIHKJMLVCVHVLVMWGWHAFVNDEVOVPVQVR
      $.
      $( [12-Nov-2013] $) $( [20-Jun-2009] $)
  $}

  ${
    $d m n x y D $.  $d m n x y J $.
    methausi.1 $e |- D e. Met $.
    methausi.2 $e |- J = ( MetOpen ` D ) $.
    $( The topology generated by a metric space is Hausdorff.  Remark in
       [Munkres] p. 126. $)
    methausi $p |- J e. Haus $=
      ( vx vy vm vn cdm wcel wceq cv w3a co c2 cin c0 crp mp3an1 syl11anc ax-mp
      cme cuni eqid uniopn2 ctop opntop wne cdiv cbl cfv wrex simp1 cc0 clt wbr
      cr metcl 3adant3 wb metgt0 biimp3a elrp sylanbrc 2re 2pos rpdivcl sylancl
      elrpii blopn simp2 blcntr cle wa rehalfcl syl leid bl2in mp3anl1 syl22anc
      eleq2 ineq1 eqeq1d 3anbi13d ineq2 3anbi23d rcla42ev syl113anc ishausi ) E
      FGHBAIIZAUBJZWJBUCKCABWJWJUDZDUEUAWKBUFJCABDUGUAELZWJJZFLZWJJZWMWOUHZMZWM
      WMWOANZOUINZAUJUKZNZBJZWOWTXANZBJZWMXBJZWOXDJZXBXDPZQKZWMHLZJZWOGLZJZXJXL
      PZQKZMZGBULHBULWRWNWTRJZXCWNWPWQUMZWRWSRJZORJXQWRWSUQJZUNWSUOUPZXSWNWPXTW
      QWKWNWPXTCWMWOAWJWLURSZUSWNWPWQYAWKWNWPWQYAUTCWMWOAWJWLVASVBWSVCVDOVEVFVI
      WSOVGVHZWKWNXQXCCAWMWTBWJWLDVJSTWRWPXQXEWNWPWQVKZYCWKWPXQXECAWOWTBWJWLDVJ
      STWRWNXQXFXRYCWKWNXQXFCAWMWTWJWLVLSTWRWPXQXGYDYCWKWPXQXGCAWOWTWJWLVLSTWRW
      NWPXQWTWTVMUPZXIXRYDYCWNWPYEWQWNWPVNZWTUQJZYEYFXTYGYBWSVOVPWTVQVPUSWKWNWP
      XQYEVNXICAWMWOWTWJWLVRVSVTXPXFXGXIMXFXMXBXLPZQKZMHGXBXDBBXJXBKZXKXFXOYIXM
      XJXBWMWAYJXNYHQXJXBXLWBWCWDXLXDKZXMXGYIXIXFXLXDWOWAYKYHXHQXLXDXBWEWCWFWGW
      HWI $.
      $( [12-Nov-2013] $) $( [9-Mar-2007] $)
  $}

  ${
    methaus.1 $e |- J = ( MetOpen ` D ) $.
    $( The topology generated by a metric space is Hausdorff.  Remark in
       [Munkres] p. 126. $)
    methaus $p |- ( D e. Met -> J e. Haus ) $=
      ( cme wcel cha cif copn cfv wceq fveq2 syl5eq eleq1d 0met elimel methausi
      c0 eqid dedth ) ADEZBFETAQGZHIZFEAQAUAJZBUBFUCBAHIUBCAUAHKLMUAUBAQDNOUBRP
      S $.
      $( [3-Dec-2007] $) $( [15-Mar-2007] $)
  $}

  ${
    $d r u x y z C $.  $d r y D $.  $d r u x y z J $.  $d r u x y z X $.
    $d r u x y z Y $.  $d x K $.
    subtopmet.1 $e |- D = ( C |` ( Y X. Y ) ) $.
    subtopmet.2 $e |- X = dom dom C $.
    subtopmet.3 $e |- J = ( MetOpen ` C ) $.
    subtopmet.4 $e |- K = ( MetOpen ` D ) $.
    $( Two alternate formulations of a subspace topology of a metric space
       topology.  (Contributed by Jeff Hankins, 21-Aug-2009.)  (The proof was
       shortened by Mario Carneiro, 5-Jan-2014.) $)
    subtopmet $p |- ( ( C e. Met /\ Y C_ X ) -> ( subSp ` <. Y , J >. ) = K )
        $=
      ( vu vy vr vz wcel wss wa wrex crp wi cme cop csubsp cfv cin wceq cdm cbl
      vx cv co wral isopn4 biimpa simprd inss1 ssralv ax-mp ssrin reximi ralimi
      adantlr 3syl inss2 jctil sseq1 sseq2 rexbidv raleqbi1dv anbi12d rexlimdva
      syl5ibrcom cab cuni ctop opntop ad2antrr wal ssel2 w3a blopn eleq1a 3expa
      sylan2 anassrs adantrd adantrr alrimiv abss sylibr uniopn syl11anc opreq1
      syl ineq1d sseq1d rcla4cv ad2antll ssel blcntr a1d ancld reximdva sylan9r
      mpdd eleq2d rcla4ev sylcom simprl sseld jcad elin sylan2br expr rexlimivw
      ex imp impbid1 wex eluniab ancom anass r19.41v rexbii bitr2i 3bitri exbii
      oprex ineq1 eleq2 ceqsexv rexcom4 bitr3i anbi1i syl6bb eqrdv wb dmeqi cvv
      adantr eqeq2d impbid simpr sylanbrc blres rexbidva ralbidva pm5.32da cres
      cxp metssba2 syl6eqr sseq2d anbi1d 3bitr2d id opnm syl2anc elsubsp metres
      ssexg syl5eqel eqid 3bitr4d ) AUAOZFEPZQZUIFCUBUCUDZDUVGUIUJZKUJZFUEZUFZK
      CRZUVIBUGZUGZPZLUJZMUJZBUHUDUKZUVIPZMSRZLUVIULZQZUVIUVHOZUVIDOZUVGUVMUVIF
      PZUVQUVRAUHUDZUKZFUEZUVIPZMSRZLUVIULZQZUWFUWBQUWCUVGUVMUWMUVGUVLUWMKCUVGU
      VJCOZQZUWMUVLUVKFPZUWIUVKPZMSRZLUVKULZQUWOUWSUWPUWOUWHUVJPZMSRZLUVJULZUXA
      LUVKULZUWSUVEUWNUXBUVFUVEUWNQUVJEPZUXBUVEUWNUXDUXBQLMUVJACEHIUMUNUOVBUVKU
      VJPUXBUXCTUVJFUPUXALUVKUVJUQURUXAUWRLUVKUWTUWQMSUWHUVJFUSUTVAVCUVJFVDVEUV
      LUWFUWPUWLUWSUVIUVKFVFUWKUWRLUVIUVKUVLUWJUWQMSUVIUVKUWIVGVHVIVJVLVKUVGUWM
      UVMUVGUWMQZNUJZUWHUFZMSRZLUVIRZUXFFUEZUVIPZQZNVMZVNZCOZUVIUXNFUEZUFZUVMUX
      ECVOOZUXMCPZUXOUVEUXRUVFUWMACIVPZVQUXEUXLUXFCOZTZNVRUXSUXEUYBNUVGUWFUYBUW
      LUVGUWFQZUXIUYAUXKUYCUXHUYALUVIUVGUWFUVQUVIOZUXHUYATZUWFUYDQZUVGUVQFOZUYE
      UVIFUVQVSZUVEUVFUYGUYEUVFUYGQZUVEUVQEOZUYEFEUVQVSZUVEUYJQUXGUYAMSUVEUYJUV
      RSOZUXGUYATZUVEUYJUYLVTUWHCOUYMAUVQUVRCEHIWAUWHCUXFWBWNWCVKWDWEWDWEVKWFWG
      WHUXLNCWIWJUXMCWKWLUXEKUVIUXPUXEUVJUVIOZUWJUVJUWHOZQZMSRZLUVIRZUVJFOZQZUV
      JUXPOZUXEUYNUYTUXEUYNUYRUYSUXEUYNUVJUVRUWGUKZFUEZUVIPZUVJVUBOZQZMSRZUYRUX
      EUYNVUDMSRZVUGUWLUYNVUHTUVGUWFUWKVUHLUVJUVIUVQUVJUFZUWJVUDMSVUIUWIVUCUVIV
      UIUWHVUBFUVQUVJUVRUWGWMZWOWPZVHWQWRUVGUWFUYNVUHVUGTZTUWLUWFUYNUYSUVGVULUV
      IFUVJWSUVFUYSUVJEOZUVEVULFEUVJWSUVEVUMVULUVEVUMQVUDVUFMSUVEVUMUYLVUDVUFTU
      VEVUMUYLVTZVUDVUEVUNVUEVUDAUVJUVREHWTXAXBWCXCXPXDXDWGXEUYNVUGUYRUYQVUGLUV
      JUVIVUIUYPVUFMSVUIUWJVUDUYOVUEVUKVUIUWHVUBUVJVUJXFVJVHXGXPXHUXEUVIFUVJUVG
      UWFUWLXIXJXKUYRUYSUYNUYQUYSUYNTZLUVIUYPVUOMSUWJUYOUYSUYNUYOUYSQUWJUVJUWIO
      UYNUVJUWHFXLUWIUVIUVJVSXMXNXOXOXQXRVUAUVJUXNOZUYSQUYTUVJUXNFXLVUPUYRUYSVU
      PUVJUXFOZUXLQZNXSUXGUXKVUQQZQZMSRZLUVIRZNXSZUYRUXLNUVJXTVURVVBNVURUXLVUQQ
      UXIVUSQZVVBVUQUXLYAUXIUXKVUQYBVVBUXHVUSQZLUVIRVVDVVAVVELUVIUXGVUSMSYCYDUX
      HVUSLUVIYCYEYFYGUYRVVANXSZLUVIRVVCUYQVVFLUVIUYQVUTNXSZMSRVVFVVGUYPMSVUSUY
      PNUWHUVQUVRUWGYHUXGUXKUWJVUQUYOUXGUXJUWIUVIUXFUWHFYIWPUXFUWHUVJYJVJYKYDVU
      TMNSYLYMYDVVALNUVIYLYEYFYNYEYOYPUVLUXQKUXNCUVJUXNUFUVKUXPUVIUVJUXNFYIUUAX
      GWLXPUUBUVGUWFUWBUWLUYCUWAUWKLUVIUVGUWFUYDUWAUWKYQZUYFUVGUYGVVHUYHUVEUVFU
      YGVVHUYIUVEUVQEFUEOZVVHUYIUYJUYGVVIUYKUVFUYGUUCUVQEFXLUUDUVEVVIQUVTUWJMSU
      VEVVIUYLUVTUWJYQUVEVVIUYLVTUVSUWIUVIBAUVQUVREFHGUUEWPWCUUFWDWEWDWEUUGUUHU
      VGUWFUVPUWBUVGFUVOUVIUVGFAFFUUJUUIZUGZUGUVOAEFHUUKUVNVVKBVVJGYRYRUULUUMUU
      NUUOUVGUXRFYSOZUWDUVMYQUVEUXRUVFUXTYTUVFUVFECOVVLUVEUVFUUPACEHIUUQFECUVAU
      URKUVIFCYSUUSWLUVGBUAOZUWEUWCYQUVEVVMUVFUVEBVVJUAGAFUUTUVBYTLMUVIBDUVOUVO
      UVCJUMWNUVDYP $.
      $( [5-Jan-2014] $) $( [19-Aug-2009] $)
  $}


