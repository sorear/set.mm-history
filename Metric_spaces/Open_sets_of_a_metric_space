$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Open sets of a metric space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.  $d v w x y z D $.  $d x y P $.  $d v w x y z X $.
    opnfval.1 $e |- X = dom dom D $.
    opnfval.2 $e |- J = ( Open ` D ) $.
    $( An open set is a subset of a metric space which includes a ball around
       each of its points.  Definition 1.3-2 of [Kreyszig] p. 18.  The
       object ` ( Open `` D ) ` is the family of all open sets in the metric
       space determined by the metric ` D ` .  By ~ opntop , the open sets of a
       metric space form a topology ` J ` , whose base set is ` U. J ` by
       ~ uniopn2 . $)
    opnfval $p |- ( D e. Met -> J = { x | ( x (_ X /\
               A. y e. x E. z e. ran ( ball ` D ) ( y e. z /\ z (_ x ) ) } ) $=
      ( vw vv cme wcel copn cfv cv wss wa cbl crn wrex wral cab cvv wceq cdm
      dmexg syl syl5eqel abssexg dmeq dmeqd syl6eqr sseq2d fveq2 rneqd rexeq1d
      ralbidv anbi12d abbidv df-opn fvopab4g mpdan syl5eq ) DKLZDMNZAOZFPZBOCOZ
      LVHVFPQZCDRNZSZTZBVFUAZQZAUBZEVDVOUCLZVEVOUDVDFUCLVPVDDUEZUEZUCFVDVQUCLVR
      UCLDKUFVQUCUFUGGUHVMAFUCUIUGIJDVFIOZUEZUEZPZVICVSRNZSZTZBVFUAZQZAUBVOKUCM
      VSDUDZWGVNAWHWBVGWFVMWHWAFVFWHWAVRFWHVTVQVSDUJUKGULUMWHWEVLBVFWHVICWDVKWH
      WCVJVSDRUNUOUPUQURUSIJABCUTVAVBHVC $.
      $( [1-Nov-2007] $) $( [1-Sep-2006] $)

    $( The family of open sets of a metric space is a collection of subsets of
       the base set. $)
    opnfss $p |- ( D e. Met -> J (_ P~ X ) $=
      ( vx vy vz cme wcel cv wss wa cbl cfv crn wrex wral cab cpw opnfval
      pm3.26 ss2abi df-pw sseqtr4i a1i eqsstrd ) AIJZBFKZCLZGKHKZJUKUILMHANOPQG
      UIRZMZFSZCTZFGHABCDEUAUNUOLUHUNUJFSUOUMUJFUJULUBUCFCUDUEUFUG $.
      $( [1-Nov-2007] $) $( [3-Sep-2006] $)

    $( The defining property of an open set of a metric space. $)
    isopn $p |- ( D e. Met -> ( A e. J <->
   ( A (_ X /\ A. x e. A E. y e. ran ( ball ` D ) ( x e. y /\ y (_ A ) ) ) ) $=
      ( vz cme wcel cv wss wa cbl cfv crn wrex wral cab opnfval eleq2d cvv wb
      cdm dmexg syl syl5eqel wceq sseq2 anbi2d rexbidv raleqd elssabg bitrd )
      DJKZCEKCILZFMALBLZKZURUQMZNZBDOPQZRZAUQSZNITZKZCFMUSURCMZNZBVBRZACSZNZUPE
      VECIABDEFGHUAUBUPFUCKVFVKUDUPDUEZUEZUCFUPVLUCKVMUCKDJUFVLUCUFUGGUHVDVJICF
      UCVCVIAUQCUQCUIZVAVHBVBVNUTVGUSUQCURUJUKULUMUNUGUO $.
      $( [2-Nov-2007] $) $( [1-Sep-2006] $)

    $( The base set of a metric space is open.  Part of Theorem T1 of
       [Kreyszig] p. 19. $)
    opnm $p |- ( D e. Met -> X e. J ) $=
      ( vx vy cme wcel wss cv wa cbl cfv crn wrex wral c1 cr cc0 clt wbr 1re
      lt01 co wceq eleq2 sseq1 anbi12d rcla4ev blelrn blcntr blssm jca sylanc
      mpanr12 r19.21aiva ssid jctil isopn mpbird ) AHIZCBICCJZFKZGKZIZVECJZLZGA
      MNZOZPZFCQZLVBVLVCVBVKFCVBVDCILZRSIZTRUAUBZVKUCUDVDRVIUEZVJIVDVPIZVPCJZLZ
      VKVMVNVOLLZVHVSGVPVJVEVPUFVFVQVGVRVEVPVDUGVEVPCUHUIUJAVDRCDUKVTVQVRAVDRCD
      ULAVDRCDUMUNUOUPUQCURUSFGCABCDEUTVA $.
      $( [3-Nov-2007] $) $( [4-Sep-2006] $)

    $( The union of all open sets in a metric space is its underlying set. $)
    uniopn2 $p |- ( D e. Met -> U. J = X ) $=
      ( cme wcel cuni cpw wss opnfss uniss unipw syl6ss syl opnm elssuni eqssd
      ) AFGZBHZCSBCIZJZTCJABCDEKUBTUAHCBUALCMNOSCBGCTJABCDEPCBQOR $.
      $( [3-Nov-2007] $) $( [4-Sep-2006] $)

    $( A defining property of an open set of a metric space. $)
    isopn4 $p |- ( D e. Met -> ( A e. J <-> ( A (_ X /\
           A. x e. A E. y e. RR ( 0 < y /\ ( x ( ball ` D ) y ) (_ A ) ) ) ) $=
      ( vz cme wcel wss cv wa cbl cfv crn wrex wral cc0 clt wbr co cr isopn wb
      blssex ssel2 sylan2 anassrs ralbidva pm5.32da bitrd ) DJKZCEKCFLZAMZIMZKU
      QCLNIDOPZQRZACSZNUOTBMZUAUBUPVAURUCCLNBUDRZACSZNAICDEFGHUEUNUOUTVCUNUONUS
      VBACUNUOUPCKZUSVBUFZUNUPFKVEUOVDNIBCDUPFGUGCFUPUHUIUJUKULUM $.
      $( [4-Dec-2007] $) $( [5-May-2007] $)

    $( An open set of a metric space is a subspace of its base set. $)
    opnss $p |- ( ( D e. Met /\ A e. J ) -> A (_ X ) $=
      ( vx vy cme wcel wss cv wa cbl cfv crn wrex wral isopn pm3.26bda ) BIJACJ
      ADKGLHLZJUAAKMHBNOPQGARGHABCDEFST $.
      $( [4-Nov-2007] $) $( [3-Sep-2006] $)
  $}

  ${
    $d f g u v w x y z A $.  $d f g u v w x y B $.  $d f g u v w x y z D $.
    $d x y P $.  $d x y R $.  $d u v w J $.
    opni.1 $e |- J = ( Open ` D ) $.
    $( An open set of a metric space includes a ball around each of its
       points. $)
    opni $p |- ( ( D e. Met /\ A e. J /\ P e. A ) ->
                 E. x e. ran ( ball ` D ) ( P e. x /\ x (_ A ) ) $=
      ( vy cme wcel cv wss wa cbl cfv crn wrex cdm wral wi eqid isopn wceq
      eleq1 anbi1d rexbidv rcla4cv adantl syl6bi 3imp ) CHIZBEIZDBIZDAJZIZUMBKZ
      LZACMNOZPZUJUKBCQQZKZGJZUMIZUOLZAUQPZGBRZLULURSZGABCEUSUSTFUAVEVFUTVDURGD
      BVADUBZVCUPAUQVGVBUNUOVADUMUCUDUEUFUGUHUI $.
      $( [7-Nov-2007] $) $( [3-Sep-2006] $)

    $( An open set of a metric space includes a ball around each of its
       points. $)
    opni2 $p |- ( ( D e. Met /\ A e. J /\ P e. A ) ->
                 E. x e. RR ( 0 < x /\ ( P ( ball ` D ) x ) (_ A ) ) $=
      ( vy cme wcel w3a cv wss wa cbl cfv crn wrex cc0 clt wbr co cr opni wb
      cdm wi eqid opnss sseld ex imp32 blssex syldan 3impb mpbid ) CHIZBEIZDBIZ
      JDGKZIUSBLMGCNOZPQZRAKZSTDVBUTUABLMAUBQZGBCDEFUCUPUQURVAVCUDZUPUQURMDCUEU
      EZIZVDUPUQURVFUPUQURVFUFUPUQMBVEDBCEVEVEUGZFUHUIUJUKGABCDVEVGULUMUNUO $.
      $( [14-Nov-2007] $) $( [2-May-2007] $)

    $( An open set of a metric space includes an arbitrarily small ball around
       each of its points. $)
    opni3 $p |- ( ( ( D e. Met /\ A e. J /\ P e. A ) /\ ( R e. RR /\
   0 < R ) ) -> E. x e. RR ( 0 < x /\ x < R /\ ( P ( ball ` D ) x ) (_ A ) ) $=
      ( vy cme wcel w3a cr cc0 clt wbr wa cv cbl cfv co wss wrex opni2 adantr
      cdm wi eqid ssblex 3expa adantrrr idd sstr2 com12 3anim123d r19.22sdv
      adantl ad2antll mpd exp32 r19.23adv 3simp1 ssel2 opnss sylan 3impa jca )
      CIJZBFJZDBJZKZELJMENOPZPMHQZNOZDVLCRSZTZBUAZPZHLUBZMAQZNOZVSENOZDVSVNTZBU
      AZKZALUBZVJVRVKHBCDFGUCUDVGDCUEUEZJZPZVKVRWEUFVJWHVKPZVQWEHLWIVLLJZVQWEWI
      WJVQPPVTWAWBVOUAZKZALUBZWEWIWJVMWMVPWHVKWJVMPWMACDEVLWFWFUGZUHUIUJVQWMWEU
      FZWIWJVPWOVMVPWLWDALVPVTVTWAWAWKWCVPVTUKVPWAUKWKVPWCWBVOBULUMUNUOUPUQURUS
      UTVJVGWGVGVHVIVAVGVHVIWGBWFUAVIWGVGVHPBWFDVBBCFWFWNGVCVDVEVFVDUR $.
      $( [14-Nov-2007] $) $( [20-Sep-2007] $)

    $( The balls of a metric space are open sets. $)
    blssopn $p |- ( D e. Met -> ran ( ball ` D ) (_ J ) $=
      ( vw vv vu cme wcel cbl cfv crn cv cdm wss wa wrex wral eqid rnblssm
      ssid wceq elequ2 sseq1 anbi12d rcla4ev mpanr2 r19.21aiva adantl jca ex
      isopn sylibrd ssrdv ) AGHZDAIJKZBUNDLZUOHZUPAMMZNZELZFLZHZVAUPNZOZFUOPZEU
      PQZOZUPBHUNUQVGUNUQOUSVFUPAURURRZSUQVFUNUQVEEUPUQUTUPHZUPUPNZVEUPTVDVIVJO
      FUPUOVAUPUAVBVIVCVJFDEUBVAUPUPUCUDUEUFUGUHUIUJEFUPABURVHCUKULUM $.
      $( [13-Nov-2007] $) $( [12-Sep-2006] $)

    $( The union of a collection of open sets of a metric space is open.
       Theorem T2 of [Kreyszig] p. 19. $)
    opnuni $p |- ( ( D e. Met /\ A (_ J ) ->
                 U. A e. J ) $=
      ( vw vv vu cme wcel wss cuni cdm cv wa cbl cfv crn wrex wral cpw sstr2
      eqid opnfss syl5com uniss unipw syl6ss syl6 wi opni simpll ssel2
      ad2ant2lr simprr syl3anc ssuni expcom anim2d r19.22sdv ad2antrl mpd
      exp32 r19.23adv eluni2 syl5ib ex r19.21adv jcad isopn sylibrd imp ) BHIZA
      CJZAKZCIZVLVMVNBLLZJZEMZFMZIZVSVNJZNZFBOPQZRZEVNSZNVOVLVMVQWEVLVMAVPTZJZV
      QVMCWFJWGVLACWFUABCVPVPUBZDUCUDWGVNWFKVPAWFUEVPUFUGUHVLVMWDEVNVLVMVRVNIZW
      DUIVLVMNZVRGMZIZGARWDWIWJWLWDGAWJWKAIZWLWDWJWMWLNZNZVTVSWKJZNZFWCRZWDVLWK
      CIZWLWRWOFWKBVRCDUJVLVMWNUKVMWMWSVLWLACWKULUMWJWMWLUNUOWMWRWDUIWJWLWMWQWB
      FWCWMWPWAVTWPWMWAVSWKAUPUQURUSUTVAVBVCGVRAVDVEVFVGVHEFVNBCVPWHDVIVJVK $.
      $( [12-Nov-2007] $) $( [4-Sep-2006] $)

    $( The intersection of two open sets of a metric space is open. $)
    opnin $p |- ( ( D e. Met /\ A e. J /\ B e. J ) ->
               ( A i^i B ) e. J ) $=
      ( vz vv vw vu vx vy vf vg cme wcel cin cdm wss cv wa cbl cfv crn wrex
      wral wi pm3.27 a1i reeanv cc0 clt wbr co cr blss 3expib anim12d adantr
      cle cif wceq eqid blin 3expb sseq1d eleq2 sseq1 anbi12d rcla4ev blelrn
      eleq1 breq2 ifboth sylan2 blcntr anim1i sylanc ex sylbid com3l an4s
      com4l ss2in sstr syl2an syl5 expdimp com4t r19.23advv syl5ibr syld imp
      anass syl2anbr r19.23aivv sylbir anbi1d rexbidv rcla4va elin sylanb
      com23 ssel com12 sylan2d com13 r19.21adv jcad exp3a com3r ssinss1 sylan
      isopn 3imtr4d 3impib ) CNOZADOZBDOZABPZDOZXPACQQZRZFSZGSZOZYDARZTZGCUAUBZ
      UCZUDZFAUEZTZBYARZHSZISZOZYOBRZTZIYIUDZHBUEZTZTZXSYARZJSZKSZOZUUEXSRZTZKY
      IUDZJXSUEZTZXQXRTXTUUBXPUUKYBYMYKYTXPUUKUFZUUCYKYTTZUULYBYMTUUCUUMUULUUMX
      PUUCUUKUUMXPUUCUUKUUMXPUUCTZUUCUUJUUNUUCUFUUMXPUUCUGUHUUMUUNUUIJXSUUDXSOZ
      UUNUUMUUIUUOXPUUDYAOZUUMUUIUFUUCUUOUUMXPUUPTZUUIUUOUUMUUQUUIUFZUUDAOZUUDB
      OZTUUMUURUUOUUSYKUUTYTUURUUDYDOZYFTZGYIUDZUUDYOOZYQTZIYIUDZUURUUSYKTUUTYT
      TUVCUVFTUVBUVETZIYIUDGYIUDUURUVBUVEGIYIYIUIUVGUURGIYIYIYDYIOZYOYIOZTUVGUU
      RUVHUVBUVIUVEUURUVHUVATZYFTUVIUVDTZYQTUURUVHUVBTUVIUVETUVJUVKYFYQUURUVJUV
      KTZYFYQTZUURUUQUVLUVMUUIUUQUVLUJLSZUKULZUUDUVNYHUMZYDRZTZLUNUDZUJMSZUKULZ
      UUDUVTYHUMZYORZTZMUNUDZTZUVMUUIUFZXPUVLUWFUFUUPXPUVJUVSUVKUWEXPUVHUVAUVSL
      YDCUUDUOUPXPUVIUVDUWEMYOCUUDUOUPUQURUUQUVRUWDTZMUNUDLUNUDUWGUWFUUQUWHUWGL
      MUNUNUWHUVMUUQUVNUNOZUVTUNOZTZUUIUVOUWAUVQUWCUVMUUQUWKUUIUFUFZUFUVOUWATZU
      VQUWCTZUVMUWLUWMUVPUWBPZXSRZUWLUWNUVMTUWKUWMUWPUUQUUIUWKUWMUWPUURUFZUWIUV
      OUWJUWAUWQUUQUWIUVOTZUWJUWATZTZUWPUUIUUQUWTUWPUUIUFUUQUWTTZUWPUUDUVNUVTUS
      ULZUVNUVTUTZYHUMZXSRZUUIUXAUWOUXDXSUUQUWRUWSUWOUXDVACUUDUVNUVTYAYAVBZVCVD
      VEUXAUXEUUIUXDYIOZUUDUXDOZUXETZUUIUXAUXETUUHUXIKUXDYIUUEUXDVAUUFUXHUUGUXE
      UUEUXDUUDVFUUEUXDXSVGVHVIUXAUXGUXEUUQUXCUNOZUJUXCUKULZTZUXGUWTCUUDUXCYAUX
      FVJUXBUWRUWSUXLUVNUVTUVNUXCVAUWIUXJUVOUXKUVNUXCUNVKUVNUXCUJUKVLVHUVTUXCVA
      UWJUXJUWAUXKUVTUXCUNVKUVTUXCUJUKVLVHVMZVNURUXAUXHUXEUUQUXLUXHUWTCUUDUXCYA
      UXFVOUXMVNVPVQVRVSVRVTWAVRWBUVQYFUWCYQUWPUVPARUWBBRUWPUVQYFTUWCYQTUVPAUWB
      BWCUVPYDAWDUWBYOBWDWEWAWFWGWAWHWIUVRUWDLMUNUNUIWJWKVTWLWAUVHUVAYFWMUVIUVD
      YQWMWNWAVRWOWPYJUVCFUUDAYCUUDVAZYGUVBGYIUXNYEUVAYFYCUUDYDVKWQWRWSYSUVFHUU
      DBYNUUDVAZYRUVEIYIUXOYPUVDYQYNUUDYOVKWQWRWSWEWAUUDABWTXAVRXBUUCUUOUUPXSYA
      UUDXCXDXEXFXGXHXIXJWLYBUUCYMABYAXKURXLWAXDXPXQYLXRUUAFGACDYAUXFEXMHIBCDYA
      UXFEXMVHJKXSCDYAUXFEXMXNXO $.
      $( [13-Nov-2007] $) $( [4-Sep-2006] $)

    $( The set of open sets of a metric space is a topology.  Remark in
       [Kreyszig] p. 19.  This theorem connects the two concepts and makes
       available the theorems for topologies for use with metric spaces. $)
    opntop $p |- ( D e. Met -> J e. Top ) $=
      ( vw vv cme wcel cv wss cuni wi wal cin wral wa ctop opnuni ex 19.21aiv
      opnin 3expib r19.21aivv jca cvv wb copn cfv fvex eqeltri istopg ax-mp
      sylibr ) AFGZDHZBIZUNJBGZKZDLZUNEHZMBGZEBNDBNZOZBPGZUMURVAUMUQDUMUOUPUNAB
      CQRSUMUTDEBBUMUNBGUSBGUTUNUSABCTUAUBUCBUDGVCVBUEBAUFUGUDCAUFUHUIDEUDBUJUK
      UL $.
      $( [13-Nov-2007] $) $( [28-Aug-2006] $)

    $( The topology generated by the balls of a metric space is its open
       sets. $)
    tgbl $p |- ( D e. Met -> ( topGen ` ran ( ball ` D ) ) = J ) $=
      ( vv vu vw ctop wcel cbl cfv crn wss cv wa wrex wral ctg wceq cme w3a
      ctb basgen2 pm3.27d opntop blssopn opni 3expib r19.21aivv syl3anc ) BGHZA
      IJKZBLZDMZEMZHUNFMZLNEUKOZDUOPFBPZUKQJBRZASHZUJULUQTUKUAHURFDEUKBUBUCABCU
      DABCUEUSUPFDBUOUSUOBHUMUOHUPEUOAUMBCUFUGUHUI $.
      $( [3-Dec-2007] $) $( [13-Sep-2006] $)

    $( The balls of a metric space form a basis for a topology. $)
    blbas $p |- ( D e. Met -> ran ( ball ` D ) e. Bases ) $=
      ( vw vv vu ctop wcel cbl cfv crn wss cv wa wrex wral ctb cme w3a ctg
      wceq basgen2 pm3.26d opntop blssopn opni 3expib r19.21aivv syl3anc ) BGHZ
      AIJKZBLZDMZEMZHUNFMZLNEUKOZDUOPFBPZUKQHZARHZUJULUQSURUKTJBUAFDEUKBUBUCABC
      UDABCUEUSUPFDBUOUSUOBHUMUOHUPEUOAUMBCUFUGUHUI $.
      $( [1-Dec-2007] $) $( [12-Sep-2006] $)
  $}

  ${
    opn0.1 $e |- J = ( Open ` D ) $.
    $( The empty set is an open set of a metric space.  Part of Theorem T1 of
       [Kreyszig] p. 19. $)
    opn0 $p |- ( D e. Met -> (/) e. J ) $=
      ( cme wcel ctop c0 opntop 0opn syl ) ADEBFEGBEABCHBIJ $.
      $( [21-Feb-2008] $) $( [4-Sep-2006] $)

    $( A ball of a metric space is an open set. $)
    rnblopn $p |- ( ( D e. Met /\ B e. ran ( ball ` D ) ) ->
                B e. J ) $=
      ( cme wcel cbl cfv crn blssopn sseld imp ) BEFZABGHIZFACFMNCABCDJKL $.
      $( [13-Nov-2007] $) $( [12-Sep-2006] $)
  $}

  ${
    $d x y D $.  $d x y X $.
    unirnbl.1 $e |- X = dom dom D $.
    $( The union of the set of balls of a metric space is its base set. $)
    unirnbl $p |- ( D e. Met -> U. ran ( ball ` D ) = X ) $=
      ( vx vy cme wcel cbl cfv crn cuni copn wss eqid blssopn uniss syl
      uniopn2 sseqtrd cv wrex blex ex eluni2 syl6ibr ssrdv eqssd ) AFGZAHIJZKZB
      UHUJALIZKZBUHUIUKMUJULMAUKUKNZOUIUKPQAUKBCUMRSUHDBUJUHDTZBGZUNETGEUIUAZUN
      UJGUHUOUPEAUNBCUBUCEUNUIUDUEUFUG $.
      $( [5-Apr-2008] $) $( [12-Sep-2006] $)
  $}

  ${
    $d r x y D $.  $d r x y J $.  $d r x y P $.  $d r y N $.  $d r x y R $.
    $d x y S $.  $d r x y X $.
    blopn.1 $e |- X = dom dom D $.
    blopn.2 $e |- J = ( Open ` D ) $.
    $( A ball of a metric space is an open set. $)
    blopn $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
                    ( P ( ball ` D ) R ) e. J ) $=
      ( cme wcel wa cr cc0 clt wbr cbl cfv co crn blelrn wi rnblopn ex
      ad2antrr mpd ) AHIZBEIZJCKILCMNJZJBCAOPZQZUHRIZUIDIZABCEFSUEUJUKTUFUGUEUJ
      UKUIADGUAUBUCUD $.
      $( [14-Nov-2007] $) $( [9-Mar-2007] $)

    $( A neighborhood of a point defined in terms of balls. $)
    neibl $p |- ( ( D e. Met /\ P e. X ) -> ( N e. ( ( nei ` J ) ` { P } )
    <-> ( N (_ X /\ E. r e. RR ( 0 < r /\ ( P ( ball ` D ) r ) (_ N ) ) ) ) $=
      ( vy cme wcel wa csn cnei cfv cuni wss cv wrex cc0 clt wbr cbl co cr
      ctop wb eqid isneip opntop adantr uniopn2 eleq2d biimpar sylanc sseq2d
      anbi1d wi w3a sstr2 com12 anim2d r19.22sdv opni2 syl5com 3exp imp4a
      ad2antrr r19.23adv wceq eleq2 sseq1 anbi12d rcla4ev blopn blcntr anim1i
      exp42 impbid pm5.32da 3bitrd ) AJKZBEKZLZDBMCNOOKZDCPZQZBIRZKZWHDQZLZICSZ
      LZDEQZWLLWNTFRZUAUBZBWOAUCOUDZDQZLZFUESZLCUFKZBWFKZWEWMUGWDBICDWFWFUHUIWB
      XAWCACHUJUKWBXBWCWBWFEBACEGHULZUMUNUOWDWGWNWLWBWGWNUGWCWBWFEDXCUPUKUQWDWN
      WLWTWDWNLZWLWTXDWKWTICWBWHCKZWKWTURURWCWNWBXEWIWJWTWBXEWIWJWTURWJWPWQWHQZ
      LZFUESWTWBXEWIUSWJXGWSFUEWJXFWRWPXFWJWRWQWHDUTVAVBVCFWHABCHVDVEVFVGVHVIWD
      WTWLURWNWDWSWLFUEWDWOUEKZWPWRWLWDXHWPWRWLWQCKZBWQKZWRLZWLWDXHWPLLZWRLWKXK
      IWQCWHWQVJWIXJWJWRWHWQBVKWHWQDVLVMVNXLXIWRABWOCEGHVOUKXLXJWRABWOEGVPVQUOV
      RVGVIUKVSVTWA $.
      $( [7-Dec-2007] $) $( [8-Nov-2007] $)

    $( The neighborhoods around a point ` P ` of a metric space are those
       subsets containing a ball around ` P ` .  Definition of neighborhood in
       [Kreyszig] p. 19. $)
    metnei $p |- ( ( D e. Met /\ P e. X ) -> ( ( nei ` J ) ` { P } ) = { x |
         ( x (_ X /\ E. r e. RR ( 0 < r /\ ( P ( ball ` D ) r ) (_ x ) ) } ) $=
      ( cme wcel wa cv wss cc0 clt wbr cbl cfv co cr wrex csn cnei neibl
      abbi2dv ) BIJCEJKALZEMNFLZOPCUGBQRSUFMKFTUAKACUBDUCRRBCDUFEFGHUDUE $.
      $( [23-Jun-2008] $) $( [8-Nov-2007] $)

    $( A ball around a point is a neighborhood of the point. $)
    blnei $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
              ( P ( ball ` D ) R ) e. ( ( nei ` J ) ` { P } ) ) $=
      ( vr cme wcel wa cr cc0 clt wbr cbl cfv co csn cnei wss cv wrex blssm
      ssid wceq breq2 opreq2 sseq1d anbi12d rcla4ev mpanr2 adantl jca wb neibl
      adantr mpbird ) AIJBEJKZCLJZMCNOZKZKZBCAPQZRZBSDTQQJZVEEUAZMHUBZNOZBVHVDR
      ZVEUAZKZHLUCZKZVCVGVMABCEFUDVBVMUSUTVAVEVEUAZVMVEUEVLVAVOKHCLVHCUFZVIVAVK
      VOVHCMNUGVPVJVEVEVHCBVDUHUIUJUKULUMUNUSVFVNUOVBABDVEEHFGUPUQUR $.
      $( [19-Dec-2007] $) $( [8-Nov-2007] $)

    $( Every ball around a limit point ` P ` of a subset ` S ` includes a
       member of ` S ` (even if ` P e/ S ` ). $)
    lpbl $p |- ( ( ( D e. Met /\ S (_ X /\ P e. ( ( limPt ` J ) ` S ) ) /\
           ( R e. RR /\ 0 < R ) ) -> E. x e. S x e. ( P ( ball ` D ) R ) ) $=
      ( vy cme wcel cuni wss wa clp cfv cr cc0 clt wbr cv cbl co wrex w3a csn
      cdif cin c0 wne cnei wral wceq ineq1 neeq1d rcla4va blnei simpll ctop
      ssel2 eqid lpss sylan opntop sylanl1 uniopn2 ad2antrr eleqtrd jca pm3.27
      wb islp2 simplr syl3anc mpbid adantr sylanc wn incom eqeq1i disj bitr2i
      difss sslin ax-mp sseq0 mpan sylbi necon3ai dfrex2 sylibr syl pm3.26
      sseq2d biimpar anim1i 3impa ) BKLZEFMZNZOZCEFPQQZLZOZDRLSDTUAOZAUBCDBUCQU
      DZLZAEUEZWSEGNZXDUFXEXFOZXGECUGZUHZUIZUJUKZXIXGXLFULQQZLZJUBZXMUIZUJUKZJX
      PUMZXOXKXTXOJXGXPXRXGUNXSXNUJXRXGXMUOUPUQWSCGLZOXFXQXEBCDFGHIURXEWSYBWSXA
      XDUSXECWTGFUTLZXAXDCWTLZWSXCWTNXDYDYCXAOZXCWTCVAEFWTWTVBZVCVDZBFIVEZVFWSW
      TGUNXAXDBFGHIVGZVHVIVJVDXEYAXFYCXAXDYAWSYEXDOZXDYAYEXDVKYCXAYDXDYAVLYJCEJ
      FWTYFVMYCXAXDUSYCXAXDVNYGVOVPYHVFVQVRXOXHVSAEUMZVSXIYKXNUJYKXGEUIZUJUNZXN
      UJUNZYMEXGUIZUJUNYKYLYOUJXGEVTWAAEXGWBWCXNYLNZYMYNXMENYPEXLWDXMEXGWEWFXNY
      LWGWHWIWJXHAEWKWLWMWSXJXDXEWSXJOZXBXDYQWSXAWSXJWNWSXAXJWSWTGEYIWOWPVJWQWR
      VD $.
      $( [25-Dec-2007] $) $( [9-Nov-2007] $)
  $}

  ${
    $d o r s x C $.  $d o r s x D $.  $d o r s x J $.  $d o r s x K $.
    $d o r s x X $. $d o r s x Y $.
    metequiv.1 $e |- X = dom dom C $.
    metequiv.2 $e |- Y = dom dom D $.
    metequiv.3 $e |- J = ( Open ` C ) $.
    metequiv.4 $e |- K = ( Open ` D ) $.
    $( Two ways of saying that two metrics generate the same topology.  Two
       metrics satisfying the right-hand side are said to be (topologically)
       equivalent.  (Contributed by Jeffrey Hankins, 21-Jun-2009.) $)
    metequiv $p |- ( ( C e. Met /\ D e. Met ) -> ( J = K
   <-> ( X = Y /\ A. x e. X ( A. r e. RR ( 0 < r -> E. s e. RR ( 0 < s
  /\ ( x ( ball ` D ) s ) (_ ( x ( ball ` C ) r ) ) ) /\ A. s e. RR ( 0 < s
  -> E. r e. RR ( 0 < r /\ ( x ( ball ` C ) r )
              (_ ( x ( ball ` D ) s ) ) ) ) ) ) ) $=
      ( vo cme wcel wa wceq cc0 cv clt wbr cbl cfv co wss cr wrex wi wral cuni
      unieq adantl uniopn2 ad2antrr ad2antlr 3eqtr3d ex blopn adantllr pm3.27
      eleqtrd blcntr opni2 3com23 3exp adantlr mpd anassrs r19.21aiva eleq2d
      biimpa adantlll eleqtrrd syldan jca jcad simprl sseq2d biimpd adantrd
      ax-17 hbra1 hban pm2.27 ssel2 w3a sstr expcom anim2d a1d r19.22dv a1dd
      a1i com24 3imp com14 syl6 com23 imp com13 syld ra4 sylan2 imp31 exp3a
      r19.23ad exp31 adantlrr com4t impcom com4l r19.21ai raleq1 biimprd com12
      adantlrl com3l ancoms adantrl impbid wb isopn4 3bitr4d eqrdv ) BOPZCOPZQZ
      DERZFGRZSITZUAUBZSHTZUAUBZATZYMCUCUDUEZYOYKBUCUDUEZUFZQZHUGUHZUIZIUGUJZYN
      YLYQYPUFZQZIUGUHZUIZHUGUJZQZAFUJZQZYHYIYJUUIYHYIYJYHYIQZDUKZEUKZFGYIUULUU
      MRYHDEULUMYFUULFRYGYIBDFJLUNUOYGUUMGRYFYICEGKMUNUPUQZURYHYIUUIUUKUUHAFUUK
      YOFPZQZUUBUUGUUPUUAIUGUUPYKUGPZQYLYTUUPUUQYLYTUUPUUQYLQZQZYQEPZYTUUSYQDEY
      HUUOUURYQDPZYIYFUUOUURUVAYGBYOYKDFJLUSUTUTUUKYIUUOUURYHYIVAZUOVBYHUUOUURU
      UTYTUIZYIYHUUOQUURQYOYQPZUVCYFUUOUURUVDYGBYOYKFJVCUTYHUURUVDUVCUIZUUOYGUV
      EYFUURYGUVDUUTYTYGUUTUVDYTHYQCYOEMVDVEVFUPVGVHUTVHVIURVJUUKUUOYOGPZUUGUUK
      UUOUVFUUKFGYOUUNVKVLUUKUVFQZUUFHUGUVGYMUGPZQYNUUEUVGUVHYNUUEUVGUVHYNQZQZY
      PDPZUUEUVJYPEDYHUVFUVIYPEPZYIYGUVFUVIUVLYFCYOYMEGKMUSVMUTUUKYIUVFUVIUVBUO
      VNYHUVFUVIUVKUUEUIZYIYHUVFQUVIQYOYPPZUVMYGUVFUVIUVNYFCYOYMGKVCVMYHUVIUVNU
      VMUIZUVFYFUVOYGUVIYFUVNUVKUUEYFUVKUVNUUEIYPBYODLVDVEVFUOVGVHUTVHVIURVJVOV
      PVJURVQYHUUJYIYHUUJQZNDEUVPNTZFUFZYLYQUVQUFZQZIUGUHZAUVQUJZQZUVQGUFZYNYPU
      VQUFZQZHUGUHZAUVQUJZQZUVQDPZUVQEPZUVPUWCUWIUVPUWCUWDUWHUVPUVRUWDUWBUVPUVR
      UWDUVPFGUVQYHYJUUIVRVSVTWAUVPUWCUWHUVPUWCQUWGAUVQUVPUWCAYHUUJAYHAWBZYJUUI
      AYJAWBUUHAFWCWDWDUVRUWBAUVRAWBUWAAUVQWCWDWDUWCUVPYOUVQPZUWGUIZUVRUWMUWAUI
      ZUVPUWNUIZUWBUWOUVRUWPUWMUWOUVRUVPUWGUWMUWOUWAUVRUVPUWGUIUIUWMUWAWEUVPUWA
      UVRUWMUWGUUJYHUWAUVRUWNUIUIZYJUUOUUHUIZYHUWQUIUUIYJUWRQZYHUWQUVRUWMUWSYHQ
      ZUWAUWGUVRUWMUWTUWAUWGUIZUIZUVRUWMQZUUOUXBUVQFYOWFUWTUUOUXCUXAYJUWRYHUUOU
      XCUXAUIZUIUUOUWRYHYJUXDUUOUWRUUHYHYJUXDUIZUIUUOUUHWEUUOUUHYHUXEUUOUUBYHUX
      EUUGUUOUUBQZYHQZYJUXCUXAUXGYJQZUXCQZUVTUWGIUGUXHUXCIUXGYJIUXFYHIUUOUUBIUU
      OIWBUUAIUGWCWDYHIWBWDYJIWBWDUXCIWBWDUWGIWBUXIUUQUVTUWGUXGYJUXCUUQUVTQZUWG
      UIUXGUXJUXCYJUWGUXFYHUXJUXCYJUWGUIZUIZUIZUUOUUQUUAUIZYHUXMUIZUUBUUOUXNUXO
      UXJUXNYHUUOUXLUUQUVTUXNYHUUOUXLUIZUIZUIUUQUXNUVTUXQUUQUXNUUAUVTUXQUIUUQUU
      AWEUVTUUAUUQUXQYLUVSUUAUUQUXQUIZUIYLUUAUVSUXRYLUUAYTUVSUXRUIYLYTWEYHUVSUU
      QYTUXPYHUVSUUQYTUXPUIYHUVSUUQWGUXCUUOYTUXKYHUVSUUQUXCUUOYTUXKUIZUIZUIYHUX
      CUUQUVSUXTUXCUUQUVSUXTUIZUIZUIYHUYBUXCUYAUUQUVSUXSUUOUVSYTUWGYJUVSYSUWFHU
      GUVSYSUWFUIUVHUVSYRUWEYNYRUVSUWEYPYQUVQWHWIWJWKWLWMWKWNWNWNWOWPWOVFWQWRWS
      WTXAXBWSWTWQWTUUAIUGXCXDWTWOXEXFXGXHXIXHXBWQXEXAVHURXJURUUHAFXCXDXKWQXBXL
      XKUWAAUVQXCXDXKXMURVQUUJYHUWIUWCUIZYJUUIUUHAGUJZYHUYCUIYJUUIUYDUUHAFGXNVL
      YHYJUYDQZUYCYHUYEQZUWIUVRUWBUYFUWDUVRUWHUYFUVRUWDUYFFGUVQYHYJUYDVRVSXOWAY
      HUYDUWIUWBUIYJYHUYDQZUWIUWBUYGUWIQUWAAUVQUYGUWIAYHUYDAUWLUUHAGWCWDUWDUWHA
      UWDAWBUWGAUVQWCWDWDUWIUYGUWOUWDUWNUYGUWOUIZUWHUWNUWDUYHUWMUWNUWDUYGUWAUWM
      UWNUWGUWDUYGUWAUIUIUWMUWGWEUYGUWGUWDUWMUWAYHUVFUUHUIZUWGUWDUWOUIUIZUYDUYI
      YHUYJUWDUWMUYIYHQZUWGUWAUWDUWMUYKUWGUWAUIZUIZUWDUWMQZUVFUYMUVQGYOWFUYKUVF
      UYNUYLUYIYHUVFUYNUYLUIZUIUVFUYIYHUYOUVFUYIUUHYHUYOUIUVFUUHWEUVFUUHYHUYOUV
      FUUGYHUYOUUBUVFUUGQZYHQZUYNUYLUYQUYNQZUWFUWAHUGUYQUYNHUYPYHHUVFUUGHUVFHWB
      UUFHUGWCWDYHHWBWDUYNHWBWDUWAHWBUYRUVHUWFUWAUYQUYNUVHUWFQZUWAUIUYQUYSUYNUW
      AUYPYHUYSUYNUWAUIZUIZUVFUVHUUFUIZYHVUAUIZUUGUVFVUBVUCUYSVUBYHUVFUYTUVHUWF
      VUBYHUVFUYTUIZUIZUIUVHVUBUWFVUEUVHVUBUUFUWFVUEUIUVHUUFWEUWFUUFUVHVUEYNUWE
      UUFUVHVUEUIZUIYNUUFUWEVUFYNUUFUUEUWEVUFUIYNUUEWEYHUWEUVHUUEVUDUWEUVHUUEVU
      DUIZUIUIYHUWEVUGUVHUYNUUEUVFUWEUWAUUEUVFUWEUWAUIZUIUIUYNUUEVUHUVFUWEUUEUW
      AUWEUUDUVTIUGUWEUUDUVTUIUUQUWEUUCUVSYLUUCUWEUVSYQYPUVQWHWIWJWKWLXPWKWNWQW
      KWNWQWRWSWTXAXBWSWTWQWTUUFHUGXCXDWTWSWTXFXGURXQXHXBXRWTXAVHURXJXSUUHAGXCX
      DWQXBXLXKUWGAUVQXCXDXKXMURXTVQWIVOXKYAYFUWJUWCYBYGUUJAIUVQBDFJLYCUOYGUWKU
      WIYBYFUUJAHUVQCEGKMYCUPYDYEURYA $.
      $( [21-Jun-2009] $) $( [20-Jun-2009] $)
  $}

  ${
    $d m n x y D $.  $d m n x y J $.
    methausi.1 $e |- D e. Met $.
    methausi.2 $e |- J = ( Open ` D ) $.
    $( The topology generated by a metric space is Hausdorff.  Remark in
       [Munkres] p. 126. $)
    methausi $p |- J e. Haus $=
      ( vx vy vm vn cdm cuni cme wcel wceq eqid uniopn2 ax-mp eqcomi ctop
      opntop cv co c2 cdiv cbl cfv cin c0 w3a wrex wne eleq2 ineq1 eqeq1d
      3anbi13d ineq2 3anbi23d rcla42ev cr cc0 clt wbr wa blopn mpanl1 3simp1
      metcl mp3an1 rehalfcl syl 3adant3 wb metgt0 biimp3a halfpos2 mpbid jca
      sylanc 3simp2 blcntr cle bl2in mp3anl1 3simpa leid 3jca syl3anc ishausi
      ) EFGHBAIIZBJZWHAKLZWIWHMCABWHWHNZDOPQWJBRLCABDSPETZWLFTZAUAZUBUCUAZAUDUE
      ZUAZBLZWMWOWPUAZBLZWLWQLZWMWSLZWQWSUFZUGMZUHZWLHTZLZWMGTZLZXFXHUFZUGMZUHZ
      GBUIHBUIWLWHLZWMWHLZWLWMUJZUHZXLXEXAXIWQXHUFZUGMZUHHGWQWSBBXFWQMZXGXAXKXR
      XIXFWQWLUKXSXJXQUGXFWQXHULUMUNXHWSMZXIXBXRXDXAXHWSWMUKXTXQXCUGXHWSWQUOUMU
      PUQXMWOURLZUSWOUTVAZVBZWRXPWJXMYCWRCAWLWOBWHWKDVCVDXMXNXOVEZXPYAYBXMXNYAX
      OXMXNVBZWNURLZYAWJXMXNYFCWLWMAWHWKVFVGZWNVHVIZVJZXPUSWNUTVAZYBXMXNXOYJWJX
      MXNXOYJVKCWLWMAWHWKVLVGVMXMXNYJYBVKZXOYEYFYKYGWNVNVIVJVOZVPZVQXNYCWTXPWJX
      NYCWTCAWMWOBWHWKDVCVDXMXNXOVRZYMVQXPXAXBXDXMYCXAXPWJXMYCXACAWLWOWHWKVSVDY
      DYMVQXNYCXBXPWJXNYCXBCAWMWOWHWKVSVDYNYMVQYEYAYBWOWOVTVAZUHZXDXPWJXMXNYPXD
      CAWLWMWOWHWKWAWBXMXNXOWCXPYAYBYOYIYLXMXNYOXOYEYAYOYHWOWDVIVJWEVQWEWFWG $.
      $( [1-Dec-2007] $) $( [9-Mar-2007] $)
  $}

  ${
    methaus.1 $e |- J = ( Open ` D ) $.
    $( The topology generated by a metric space is Hausdorff.  Remark in
       [Munkres] p. 126. $)
    methaus $p |- ( D e. Met -> J e. Haus ) $=
      ( cme wcel cha c0 cif copn cfv wceq fveq2 syl5eq eleq1d 0met elimel eqid
      methausi dedth ) ADEZBFETAGHZIJZFEAGAUAKZBUBFUCAIJUBBAUAILCMNUAUBAGDOPUBQ
      RS $.
      $( [3-Dec-2007] $) $( [15-Mar-2007] $)
  $}


