$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Open sets of a metric space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.  $d d x y z D $.  $d d x y z X $.
    opnfval.1 $e |- X = dom dom D $.
    opnfval.2 $e |- J = ( MetOpen ` D ) $.
    $( An open set is a subset of a metric space which includes a ball around
       each of its points.  Definition 1.3-2 of [Kreyszig] p. 18.  The object
       ` ( MetOpen `` D ) ` is the family of all open sets in the metric space
       determined by the metric ` D ` .  By ~ opntop , the open sets of a
       metric space form a topology ` J ` , whose base set is ` U. J ` by
       ~ uniopn2 . $)
    opnfval $p |- ( D e. Met -> J = { x e. ~P X |
               A. y e. x E. z e. ran ( ball ` D ) ( y e. z /\ z C_ x ) } ) $=
      ( vd cme wcel copn cfv cv cbl crn wrex cvv wceq cdm wss wa wral cpw dmexg
      crab syl syl5eqel pwexg rabexg 3syl dmeq dmeqd syl6eqr pweq fveq2 rexeqdv
      rneqd ralbidv rabeqbidv df-opn fvmptg mpdan syl5eq ) DJKZEDLMZBNCNZKVGANZ
      UAUBZCDOMZPZQZBVHUCZAFUDZUFZHVEVORKZVFVOSVEFRKVNRKVPVEFDTZTZRGVEVQRKVRRKD
      JUEVQRUEUGUHFRUIVMAVNRUJUKIDVICINZOMZPZQZBVHUCZAVSTZTZUDZUFVOJRLVSDSZWCVM
      AWFVNWGWEFSWFVNSWGWEVRFWGWDVQVSDULUMGUNWEFUOUGWGWBVLBVHWGVICWAVKWGVTVJVSD
      OUPURUQUSUTABCIVAVBVCVD $.
      $( [16-Nov-2013] $) $( [1-Sep-2006] $)

    $( The family of open sets of a metric space is a collection of subsets of
       the base set. $)
    opnfss $p |- ( D e. Met -> J C_ ~P X ) $=
      ( vy vz vx cme wcel cv wss wa cbl cfv crn wrex wral cpw crab opnfval a1i
      ssrab2 eqsstrd ) AIJZBFKGKZJUFHKZLMGANOPQFUGRZHCSZTZUIHFGABCDEUAUJUILUEUH
      HUIUCUBUD $.
      $( [16-Nov-2013] $) $( [3-Sep-2006] $)

    $( The defining property of an open set of a metric space. $)
    isopn $p |- ( D e. Met -> ( A e. J <->
   ( A C_ X /\ A. x e. A E. y e. ran ( ball ` D ) ( x e. y /\ y C_ A ) ) ) ) $=
      ( vz cme wcel cv wss wa wrex wral cvv cdm dmexg syl cpw cbl cfv crab wceq
      crn opnfval eleq2d sseq2 anbi2d rexbidv raleqbi1dv elrab syl6bb wb elpw2g
      syl5eqel anbi1d bitrd ) DJKZCEKZCFUAZKZALBLZKZVDCMZNZBDUBUCUFZOZACPZNZCFM
      ZVJNUTVACVEVDILZMZNZBVHOZAVMPZIVBUDZKVKUTEVRCIABDEFGHUGUHVQVJICVBVPVIAVMC
      VMCUEZVOVGBVHVSVNVFVEVMCVDUIUJUKULUMUNUTVCVLVJUTFQKVCVLUOUTFDRZRZQGUTVTQK
      WAQKDJSVTQSTUQCFQUPTURUS $.
      $( [16-Nov-2013] $) $( [1-Sep-2006] $)

    $( The base set of a metric space is open.  Part of Theorem T1 of
       [Kreyszig] p. 19. $)
    opnm $p |- ( D e. Met -> X e. J ) $=
      ( vx vy cme wcel wss cv wa cbl cfv crn wrex wral c1 cr cc0 clt wbr 1re co
      lt01 blelrn blcntr blssm wceq anbi12d rcla4ev syl12anc mpanr12 r19.21aiva
      eleq2 sseq1 ssid jctil isopn mpbird ) AHIZCBICCJZFKZGKZIZVDCJZLZGAMNZOZPZ
      FCQZLVAVKVBVAVJFCVAVCCILZRSIZTRUAUBZVJUCUEVLVMVNLLVCRVHUDZVIIVCVOIZVOCJZV
      JAVCRCDUFAVCRCDUGAVCRCDUHVGVPVQLGVOVIVDVOUIVEVPVFVQVDVOVCUOVDVOCUPUJUKULU
      MUNCUQURFGCABCDEUSUT $.
      $( [3-Nov-2007] $) $( [4-Sep-2006] $)

    $( The union of all open sets in a metric space is its underlying set. $)
    uniopn2 $p |- ( D e. Met -> U. J = X ) $=
      ( cme wcel cuni cpw wss opnfss uniss unipw syl6sseq opnm elssuni eqssd
      syl ) AFGZBHZCSBCIZJZTCJABCDEKUBTUAHCBUALCMNRSCBGCTJABCDEOCBPRQ $.
      $( [3-Nov-2007] $) $( [4-Sep-2006] $)

    $( A defining property of an open set of a metric space. $)
    isopn4 $p |- ( D e. Met -> ( A e. J <-> ( A C_ X /\
           A. x e. A E. y e. RR ( 0 < y /\ ( x ( ball ` D ) y ) C_ A ) ) ) ) $=
      ( vz cme wcel wss cv wa cbl cfv crn wrex wral cc0 clt wbr co isopn blssex
      cr wb ssel2 sylan2 anassrs ralbidva pm5.32da bitrd ) DJKZCEKCFLZAMZIMZKUQ
      CLNIDOPZQRZACSZNUOTBMZUAUBUPVAURUCCLNBUFRZACSZNAICDEFGHUDUNUOUTVCUNUONUSV
      BACUNUOUPCKZUSVBUGZUOVDNUNUPFKVECFUPUHIBCDUPFGUEUIUJUKULUM $.
      $( [5-May-2007] $)

    $( An open set of a metric space is a subspace of its base set. $)
    opnss $p |- ( ( D e. Met /\ A e. J ) -> A C_ X ) $=
      ( vx vy cme wcel wss cv wa cbl cfv crn wrex wral isopn simprbda ) BIJACJA
      DKGLHLZJUAAKMHBNOPQGARGHABCDEFST $.
      $( [4-Nov-2007] $) $( [3-Sep-2006] $)
  $}

  ${
    $d f g u v w x y z A $.  $d f g u v w x y B $.  $d f g u v w x y z D $.
    $d x y P $.  $d x y R $.  $d u v w J $.
    opni.1 $e |- J = ( MetOpen ` D ) $.
    $( An open set of a metric space includes a ball around each of its
       points. $)
    opni $p |- ( ( D e. Met /\ A e. J /\ P e. A ) ->
                 E. x e. ran ( ball ` D ) ( P e. x /\ x C_ A ) ) $=
      ( vy cme wcel cv wss wa cbl cfv crn wrex cdm wral wi eqid rexbidv rcla4cv
      isopn wceq eleq1 anbi1d adantl syl6bi 3imp ) CHIZBEIZDBIZDAJZIZUMBKZLZACM
      NOZPZUJUKBCQQZKZGJZUMIZUOLZAUQPZGBRZLULURSZGABCEUSUSTFUCVEVFUTVDURGDBVADU
      DZVCUPAUQVGVBUNUOVADUMUEUFUAUBUGUHUI $.
      $( [7-Nov-2007] $) $( [3-Sep-2006] $)

    $( An open set of a metric space includes a ball around each of its
       points. $)
    opni2 $p |- ( ( D e. Met /\ A e. J /\ P e. A ) ->
                 E. x e. RR ( 0 < x /\ ( P ( ball ` D ) x ) C_ A ) ) $=
      ( vy cme wcel w3a cv wss wa cbl cfv crn wrex cc0 clt cdm wbr co opni eqid
      cr wb opnss sseld impr blssex syldan 3impb mpbid ) CHIZBEIZDBIZJDGKZIUQBL
      MGCNOZPQZRAKZSUADUTURUBBLMAUEQZGBCDEFUCUNUOUPUSVAUFZUNUOUPMDCTTZIZVBUNUOU
      PVDUNUOMBVCDBCEVCVCUDZFUGUHUIGABCDVCVEUJUKULUM $.
      $( [2-May-2007] $)

    $( An open set of a metric space includes an arbitrarily small ball around
       each of its points. $)
    opni3 $p |- ( ( ( D e. Met /\ A e. J /\ P e. A ) /\ ( R e. RR /\
   0 < R ) ) -> E. x e. RR ( 0 < x /\ x < R /\ ( P ( ball ` D ) x ) C_ A ) ) $=
      ( vy wcel w3a cr cc0 clt wbr wa cv co wss wrex cdm cme opni2 adantr simp1
      cbl cfv wi eqid opnss ssel2 sylan 3impa ssblex 3expa adantrrr sstr2 com12
      jca idd 3anim123d reximdv adantl ad2antll mpd exp32 r19.23adv ) CUAIZBFIZ
      DBIZJZEKILEMNOZOLHPZMNZDVLCUEUFZQZBRZOZHKSZLAPZMNZVSEMNZDVSVNQZBRZJZAKSZV
      JVRVKHBCDFGUBUCVJVGDCTTZIZOZVKVRWEUGVJVGWGVGVHVIUDVGVHVIWGVGVHOBWFRVIWGBC
      FWFWFUHZGUIBWFDUJUKULURWHVKOZVQWEHKWJVLKIZVQWEWJWKVQOOVTWAWBVORZJZAKSZWEW
      JWKVMWNVPWHVKWKVMOWNACDEVLWFWIUMUNUOVQWNWEUGZWJWKVPWOVMVPWMWDAKVPVTVTWAWA
      WLWCVPVTUSVPWAUSWLVPWCWBVOBUPUQUTVAVBVCVDVEVFUKVD $.
      $( [20-Sep-2007] $)

    $( The balls of a metric space are open sets. $)
    blssopn $p |- ( D e. Met -> ran ( ball ` D ) C_ J ) $=
      ( vw vv vu cme wcel cbl cfv crn cv cdm wss wa wrex wral eqid rnblssm ssid
      wceq elequ2 sseq1 anbi12d rcla4ev mpanr2 r19.21aiva adantl jca ex sylibrd
      isopn ssrdv ) AGHZDAIJKZBUNDLZUOHZUPAMMZNZELZFLZHZVAUPNZOZFUOPZEUPQZOZUPB
      HUNUQVGUNUQOUSVFUPAURURRZSUQVFUNUQVEEUPUQUTUPHZUPUPNZVEUPTVDVIVJOFUPUOVAU
      PUAVBVIVCVJFDEUBVAUPUPUCUDUEUFUGUHUIUJEFUPABURVHCULUKUM $.
      $( [13-Nov-2007] $) $( [12-Sep-2006] $)

    $( The union of a collection of open sets of a metric space is open.
       Theorem T2 of [Kreyszig] p. 19. $)
    opnuni $p |- ( ( D e. Met /\ A C_ J ) ->
                 U. A e. J ) $=
      ( vw vv vu cme wcel wss cuni cdm cv wa cbl cfv crn wrex wral wi cpw sstr2
      eqid opnfss syl5com uniss unipw syl6 eluni2 simpll ssel2 ad2ant2lr simprr
      syl6sseq opni syl111anc ssuni expcom reximdv ad2antrl mpd exp32 r19.23adv
      anim2d syl5bi ex r19.21adv jcad isopn sylibrd imp ) BHIZACJZAKZCIZVLVMVNB
      LLZJZEMZFMZIZVSVNJZNZFBOPQZRZEVNSZNVOVLVMVQWEVLVMAVPUAZJZVQVLCWFJVMWGBCVP
      VPUCZDUDACWFUBUEWGVNWFKVPAWFUFVPUGUNUHVLVMWDEVNVLVMVRVNIZWDTWIVRGMZIZGARV
      LVMNZWDGVRAUIWLWKWDGAWLWJAIZWKWDWLWMWKNZNZVTVSWJJZNZFWCRZWDWOVLWJCIZWKWRV
      LVMWNUJVMWMWSVLWKACWJUKULWLWMWKUMFWJBVRCDUOUPWMWRWDTWLWKWMWQWBFWCWMWPWAVT
      WPWMWAVSWJAUQURVDUSUTVAVBVCVEVFVGVHEFVNBCVPWHDVIVJVK $.
      $( [12-Nov-2007] $) $( [4-Sep-2006] $)

    $( The intersection of two open sets of a metric space is open. $)
    opnin $p |- ( ( D e. Met /\ A e. J /\ B e. J ) ->
               ( A i^i B ) e. J ) $=
      ( vv vu vf vg wcel wss cv wa wrex wi wceq cc0 clt cr an4s vz vw vx vy cme
      cin cdm cbl cfv crn wral ssinss1 adantr simpr a1i ssel com12 eleq1 anbi1d
      elin rexbidv rcla4va reeanv anass wbr co blss 3expib anim12d ss2in syl2an
      sstr cle eqid blin 3expb sseq1d breq2 anbi12d ifboth blelrn sylan2 blcntr
      cif anim1i eleq2 sseq1 rcla4ev syl11anc ex com3l com4l syl5 expdimp com4t
      sylbid r19.23advv syl5bir syld imp r19.23aivv sylbir sylanb com23 sylan2d
      syl2anbr com13 r19.21adv jcad exp3a com3r sylan isopn 3imtr4d 3impib ) CU
      EJZADJZBDJZABUFZDJZXPACUGUGZKZUALZFLZJZYDAKZMZFCUHUIZUJZNZUAAUKZMZBYAKZUB
      LZGLZJZYOBKZMZGYINZUBBUKZMZMZXSYAKZUCLZUDLZJZUUEXSKZMZUDYINZUCXSUKZMZXQXR
      MXTUUBXPUUKYBYMYKYTXPUUKOZYBYMMUUCYKYTMZUULYBUUCYMABYAULUMUUCUUMUULUUMXPU
      UCUUKUUMXPUUCUUKUUMXPUUCMZUUCUUJUUNUUCOUUMXPUUCUNUOUUMUUNUUIUCXSUUDXSJZUU
      NUUMUUIUUOUUCUUDYAJZXPUUMUUIOUUCUUOUUPXSYAUUDUPUQUUOUUMXPUUPMZUUIUUOUUMUU
      QUUIOZUUOUUDAJZUUDBJZMUUMUURUUDABUTUUSYKUUTYTUURUUSYKMUUDYDJZYFMZFYINZUUD
      YOJZYQMZGYINZUURUUTYTMYJUVCUAUUDAYCUUDPZYGUVBFYIUVGYEUVAYFYCUUDYDURUSVAVB
      YSUVFUBUUDBYNUUDPZYRUVEGYIUVHYPUVDYQYNUUDYOURUSVAVBUVCUVFMUVBUVEMZGYINFYI
      NUURUVBUVEFGYIYIVCUVIUURFGYIYIYDYIJZYOYIJZMUVIUURUVJUVBUVKUVEUURUVJUVBMUV
      JUVAMZYFMUVKUVDMZYQMUURUVKUVEMUVJUVAYFVDUVKUVDYQVDUVLUVMYFYQUURUVLUVMMZYF
      YQMZUURUUQUVNUVOUUIUUQUVNQHLZRVEZUUDUVPYHVFZYDKZMZHSNZQILZRVEZUUDUWBYHVFZ
      YOKZMZISNZMZUVOUUIOZXPUVNUWHOUUPXPUVLUWAUVMUWGXPUVJUVAUWAHYDCUUDVGVHXPUVK
      UVDUWGIYOCUUDVGVHVIUMUWHUVTUWFMZISNHSNUUQUWIUVTUWFHISSVCUUQUWJUWIHISSUWJU
      VOUUQUVPSJZUWBSJZMZUUIUVQUWCUVSUWEUVOUUQUWMUUIOOZOUVQUWCMZUVSUWEMZUVOUWNU
      WPUVOMUVRUWDUFZXSKZUWOUWNUVSYFUWEYQUWRUVSYFMUVRAKUWDBKUWRUWEYQMUVRYDAVLUW
      DYOBVLUVRAUWDBVJVKTUWMUWOUWRUUQUUIUWMUWOUWRUUROZUWKUVQUWLUWCUWSUUQUWKUVQM
      ZUWLUWCMZMZUWRUUIUUQUXBUWRUUIOUUQUXBMZUWRUUDUVPUWBVMVEZUVPUWBWDZYHVFZXSKZ
      UUIUXCUWQUXFXSUUQUWTUXAUWQUXFPCUUDUVPUWBYAYAVNZVOVPVQUXCUXGUUIUXCUXGMUXFY
      IJZUUDUXFJZUXGMZUUIUXCUXIUXGUXBUUQUXESJZQUXERVEZMZUXIUXDUWTUXAUXNUVPUWBUV
      PUXEPUWKUXLUVQUXMUVPUXESURUVPUXEQRVRVSUWBUXEPUWLUXLUWCUXMUWBUXESURUWBUXEQ
      RVRVSVTZCUUDUXEYAUXHWAWBUMUXCUXJUXGUXBUUQUXNUXJUXOCUUDUXEYAUXHWCWBWEUUHUX
      KUDUXFYIUUEUXFPUUFUXJUUGUXGUUEUXFUUDWFUUEUXFXSWGVSWHWIWJWPWJWKTWJWLWMWNTW
      OWQWRWSWKWTTXFTWJXAXBVKTXCWJXDXEXGXHXIXJXKWTXLTUQXPXQYLXRUUAUAFACDYAUXHEX
      MUBGBCDYAUXHEXMVSUCUDXSCDYAUXHEXMXNXO $.
      $( [13-Nov-2007] $) $( [4-Sep-2006] $)

    $( The set of open sets of a metric space is a topology.  Remark in
       [Kreyszig] p. 19.  This theorem connects the two concepts and makes
       available the theorems for topologies for use with metric spaces. $)
    opntop $p |- ( D e. Met -> J e. Top ) $=
      ( vw vv cme wcel cv wss cuni wi wal cin wral ctop opnuni ex 19.21aiv copn
      cvv opnin 3expib r19.21aivv wa wb cfv fvex eqeltri istopg ax-mp sylanbrc
      ) AFGZDHZBIZUMJBGZKZDLZUMEHZMBGZEBNDBNZBOGZULUPDULUNUOUMABCPQRULUSDEBBULU
      MBGURBGUSUMURABCUAUBUCBTGVAUQUTUDUEBASUFTCASUGUHDETBUIUJUK $.
      $( [13-Nov-2007] $) $( [28-Aug-2006] $)

    $( The topology generated by the balls of a metric space is its open
       sets. $)
    tgbl $p |- ( D e. Met -> ( topGen ` ran ( ball ` D ) ) = J ) $=
      ( vv vu vw cme wcel ctop cbl cfv crn wss cv wa wrex wral ctg wceq opntop
      blssopn opni 3expib r19.21aivv w3a ctb basgen2 simprd syl111anc ) AGHZBIH
      ZAJKLZBMZDNZENZHUOFNZMOEULPZDUPQFBQZULRKBSZABCTABCUAUJUQFDBUPUJUPBHUNUPHU
      QEUPAUNBCUBUCUDUKUMURUEULUFHUSFDEULBUGUHUI $.
      $( [3-Dec-2007] $) $( [13-Sep-2006] $)

    $( The balls of a metric space form a basis for a topology. $)
    blbas $p |- ( D e. Met -> ran ( ball ` D ) e. TopBases ) $=
      ( vw vv vu cme wcel ctop cbl cfv crn wss cv wrex wral ctb opntop blssopn
      wa opni 3expib r19.21aivv w3a ctg wceq basgen2 simpld syl111anc ) AGHZBIH
      ZAJKLZBMZDNZENZHUOFNZMTEULOZDUPPFBPZULQHZABCRABCSUJUQFDBUPUJUPBHUNUPHUQEU
      PAUNBCUAUBUCUKUMURUDUSULUEKBUFFDEULBUGUHUI $.
      $( [1-Dec-2007] $) $( [12-Sep-2006] $)
  $}

  ${
    opn0.1 $e |- J = ( MetOpen ` D ) $.
    $( The empty set is an open set of a metric space.  Part of Theorem T1 of
       [Kreyszig] p. 19. $)
    opn0 $p |- ( D e. Met -> (/) e. J ) $=
      ( cme wcel ctop c0 opntop 0opn syl ) ADEBFEGBEABCHBIJ $.
      $( [21-Feb-2008] $) $( [4-Sep-2006] $)

    $( A ball of a metric space is an open set. $)
    rnblopn $p |- ( ( D e. Met /\ B e. ran ( ball ` D ) ) ->
                B e. J ) $=
      ( cme wcel cbl cfv crn blssopn sseld imp ) BEFZABGHIZFACFMNCABCDJKL $.
      $( [13-Nov-2007] $) $( [12-Sep-2006] $)
  $}

  ${
    $d x y D $.  $d x y X $.
    unirnbl.1 $e |- X = dom dom D $.
    $( The union of the set of balls of a metric space is its base set. $)
    unirnbl $p |- ( D e. Met -> U. ran ( ball ` D ) = X ) $=
      ( vx vy cme wcel cbl cfv crn cuni copn wss eqid blssopn uniss syl uniopn2
      sseqtrd cv wrex blex ex eluni2 syl6ibr ssrdv eqssd ) AFGZAHIJZKZBUHUJALIZ
      KZBUHUIUKMUJULMAUKUKNZOUIUKPQAUKBCUMRSUHDBUJUHDTZBGZUNETGEUIUAZUNUJGUHUOU
      PEAUNBCUBUCEUNUIUDUEUFUG $.
      $( [5-Apr-2008] $) $( [12-Sep-2006] $)
  $}

  ${
    $d r x y D $.  $d r x y J $.  $d r x y P $.  $d r y N $.  $d r x y R $.
    $d x y S $.  $d r x y X $.
    blopn.1 $e |- X = dom dom D $.
    blopn.2 $e |- J = ( MetOpen ` D ) $.
    $( A ball of a metric space is an open set. $)
    blopn $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
                    ( P ( ball ` D ) R ) e. J ) $=
      ( cme wcel wa cr cc0 clt wbr cbl cfv co crn blelrn wi rnblopn ex ad2antrr
      mpd ) AHIZBEIZJCKILCMNJZJBCAOPZQZUHRIZUIDIZABCEFSUEUJUKTUFUGUEUJUKUIADGUA
      UBUCUD $.
      $( [14-Nov-2007] $) $( [9-Mar-2007] $)

    $( A neighborhood of a point defined in terms of balls. $)
    neibl $p |- ( ( D e. Met /\ P e. X ) -> ( N e. ( ( nei ` J ) ` { P } )
    <-> ( N C_ X /\ E. r e. RR ( 0 < r /\ ( P ( ball ` D ) r ) C_ N ) ) ) ) $=
      ( vy wcel wa cfv wss cv wrex cr wb adantr syl11anc wi cme csn cc0 clt wbr
      cnei cuni co ctop opntop uniopn2 eleq2d biimpar eqid isneip sseq2d anbi1d
      cbl w3a opni2 sstr2 com12 anim2d reximdv syl5com imp4a ad2antrr r19.23adv
      3exp blopn blcntr anim1i wceq eleq2 sseq1 anbi12d rcla4ev impbid pm5.32da
      exp42 3bitrd ) AUAJZBEJZKZDBUBCUFLLJZDCUGZMZBINZJZWHDMZKZICOZKZDEMZWLKWNU
      CFNZUDUEZBWOAURLUHZDMZKZFPOZKWDCUIJZBWFJZWEWMQWBXAWCACHUJRWBXBWCWBWFEBACE
      GHUKZULUMBICDWFWFUNUOSWDWGWNWLWBWGWNQWCWBWFEDXCUPRUQWDWNWLWTWDWNKZWLWTXDW
      KWTICWBWHCJZWKWTTTWCWNWBXEWIWJWTWBXEWIWJWTTWBXEWIUSWPWQWHMZKZFPOWJWTFWHAB
      CHUTWJXGWSFPWJXFWRWPXFWJWRWQWHDVAVBVCVDVEVIVFVGVHWDWTWLTWNWDWSWLFPWDWOPJZ
      WPWRWLWDXHWPWRWLWDXHWPKKZWRKWQCJZBWQJZWRKZWLXIXJWRABWOCEGHVJRXIXKWRABWOEG
      VKVLWKXLIWQCWHWQVMWIXKWJWRWHWQBVNWHWQDVOVPVQSVTVFVHRVRVSWA $.
      $( [8-Nov-2007] $)

    $( The neighborhoods around a point ` P ` of a metric space are those
       subsets containing a ball around ` P ` .  Definition of neighborhood in
       [Kreyszig] p. 19. $)
    metnei $p |- ( ( D e. Met /\ P e. X ) -> ( ( nei ` J ) ` { P } ) = { x |
         ( x C_ X /\ E. r e. RR ( 0 < r /\ ( P ( ball ` D ) r ) C_ x ) ) } ) $=
      ( cme wcel wa cv wss cc0 clt wbr cbl cfv co cr wrex cnei neibl abbi2dv
      csn ) BIJCEJKALZEMNFLZOPCUGBQRSUFMKFTUAKACUEDUBRRBCDUFEFGHUCUD $.
      $( [8-Nov-2007] $)

    $( A ball around a point is a neighborhood of the point. $)
    blnei $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
              ( P ( ball ` D ) R ) e. ( ( nei ` J ) ` { P } ) ) $=
      ( vr cme wcel wa cr cc0 clt wbr cbl cfv co csn wss cnei cv wrex wb adantr
      neibl blssm ssid wceq breq2 opreq2 sseq1d anbi12d mpanr2 adantl mpbir2and
      rcla4ev ) AIJBEJKZCLJZMCNOZKZKBCAPQZRZBSDUAQQJZVCETZMHUBZNOZBVFVBRZVCTZKZ
      HLUCZURVDVEVKKUDVAABDVCEHFGUFUEABCEFUGVAVKURUSUTVCVCTZVKVCUHVJUTVLKHCLVFC
      UIZVGUTVIVLVFCMNUJVMVHVCVCVFCBVBUKULUMUQUNUOUP $.
      $( [8-Nov-2007] $)

    $( Every ball around a limit point ` P ` of a subset ` S ` includes a
       member of ` S ` (even if ` P e/ S ` ). $)
    lpbl $p |- ( ( ( D e. Met /\ S C_ X /\ P e. ( ( limPt ` J ) ` S ) ) /\
           ( R e. RR /\ 0 < R ) ) -> E. x e. S x e. ( P ( ball ` D ) R ) ) $=
      ( vy wcel wss cfv wa cv jca cin c0 sylan wceq cme clp w3a cuni cr cc0 clt
      wbr cbl wrex simpl uniopn2 sseq2d biimpar anim1i 3impa csn cdif cnei wral
      co wne simpll ctop opntop eqid ssel2 sylanl1 ad2antrr eleqtrd blnei simpr
      lpss wb simplr islp2 syl111anc mpbid adantr ineq1 neeq1d rcla4va syl11anc
      wn disjr difss sslin ax-mp sseq0 mpan sylbir necon3ai dfrex2 sylibr syl )
      BUAKZEGLZCEFUBMMZKZUCWPEFUDZLZNZWSNZDUEKUFDUGUHNZAOCDBUIMVAZKZAEUJZWPWQWS
      XCWPWQNZXBWSXHWPXAWPWQUKWPXAWQWPWTGEBFGHIULZUMUNPUOUPXCXDNZXEECUQZURZQZRV
      BZXGXJXEXKFUSMMZKZJOZXLQZRVBZJXOUTZXNXCWPCGKZNXDXPXCWPYAWPXAWSVCXCCWTGWPF
      VDKZXAWSCWTKZBFIVEZYBXANZWRWTLWSYCEFWTWTVFZVMWRWTCVGSZVHWPWTGTXAWSXIVIVJP
      BCDFGHIVKSXCXTXDWPYBXAWSXTYDYEWSNZWSXTYEWSVLYHYBXAYCWSXTVNYBXAWSVCYBXAWSV
      OYGCEJFWTYFVPVQVRVHVSXSXNJXEXOXQXETXRXMRXQXEXLVTWAWBWCXNXFWDAEUTZWDXGYIXM
      RYIXEEQZRTZXMRTZAXEEWEXMYJLZYKYLXLELYMEXKWFXLEXEWGWHXMYJWIWJWKWLXFAEWMWNW
      OS $.
      $( [9-Nov-2007] $)
  $}

  ${
    $d o r s x C $.  $d o r s x D $.  $d o r s x J $.  $d o r s x K $.
    $d o r s x X $.  $d o r s x Y $.
    metequiv.1 $e |- X = dom dom C $.
    metequiv.2 $e |- Y = dom dom D $.
    metequiv.3 $e |- J = ( MetOpen ` C ) $.
    metequiv.4 $e |- K = ( MetOpen ` D ) $.
    $( Two ways of saying that two metrics generate the same topology.  Two
       metrics satisfying the right-hand side are said to be (topologically)
       equivalent.  (Contributed by Jeffrey Hankins, 21-Jun-2009.) $)
    metequiv $p |- ( ( C e. Met /\ D e. Met ) -> ( J = K
   <-> ( X = Y /\ A. x e. X ( A. r e. RR ( 0 < r -> E. s e. RR ( 0 < s
  /\ ( x ( ball ` D ) s ) C_ ( x ( ball ` C ) r ) ) ) /\ A. s e. RR ( 0 < s
  -> E. r e. RR ( 0 < r /\ ( x ( ball ` C ) r )
              C_ ( x ( ball ` D ) s ) ) ) ) ) ) ) $=
      ( wcel wa cr wi ax-17 hban imp vo cme wceq cc0 cv clt wbr cbl cfv co wrex
      wss wral unieq adantl uniopn2 ad2antrr ad2antlr 3eqtr3d ex blopn adantllr
      cuni simpllr eleqtrd blcntr opni2 3com23 3exp adantlr mpd expr r19.21aiva
      eleq2d biimpa adantlll eleqtrrd ad3antrrr syldan jca simprl sseq2d biimpd
      jcad adantrd hbra1 ra4 pm2.27 ssel2 w3a sstr expcom anim2d reximdvai a1dd
      a1d a1i a1i12 com24 3imp com14 syl6 com23 com13 syld imp31 exp3a r19.23ad
      sylan2 exp31 adantlrr com4t impcom com4l r19.21ai biimprd adantlrl ancoms
      raleq com12 com3l adantrl impbid wb isopn4 3bitr4d eqrdv ) BUBNZCUBNZOZDE
      UCZFGUCZUDIUEZUFUGZUDHUEZUFUGZAUEZYOCUHUIUJZYQYMBUHUIUJZULZOZHPUKZQZIPUMZ
      YPYNYSYRULZOZIPUKZQZHPUMZOZAFUMZOZYJYKYLUUKYJYKYLYJYKOZDVCZEVCZFGYKUUNUUO
      UCYJDEUNUOYHUUNFUCYIYKBDFJLUPUQYIUUOGUCYHYKCEGKMUPURUSZUTYJYKUUKUUMUUJAFU
      UMYQFNZOZUUDUUIUURUUCIPUURYMPNZYNUUBUURUUSYNOZOZYSENZUUBUVAYSDEYJUUQUUTYS
      DNZYKYHUUQUUTUVCYIBYQYMDFJLVAVBVBYJYKUUQUUTVDVEYJUUQUUTUVBUUBQZYKYJUUQOUU
      TOYQYSNZUVDYHUUQUUTUVEYIBYQYMFJVFVBYJUUTUVEUVDQZUUQYIUVFYHUUTYIUVEUVBUUBY
      IUVBUVEUUBHYSCYQEMVGVHVIURVJVKVBVKVLVMUUMUUQYQGNZUUIUUMUUQUVGUUMFGYQUUPVN
      VOUUMUVGOZUUHHPUVHYOPNZYPUUGUVHUVIYPOZOZYRDNZUUGUVKYREDYJUVGUVJYRENZYKYIU
      VGUVJUVMYHCYQYOEGKMVAVPVBYJYKUVGUVJVDVQYJUVGUVJUVLUUGQZYKYJUVGOUVJOYQYRNZ
      UVNYIUVGUVJUVOYHCYQYOGKVFVPYHUVOUVNQYIUVGUVJYHUVOUVLUUGYHUVLUVOUUGIYRBYQD
      LVGVHVIVRVKVBVKVLVMVSVTVMUTWDYJUULYKYJUULOZUADEUVPUAUEZFULZYNYSUVQULZOZIP
      UKZAUVQUMZOZUVQGULZYPYRUVQULZOZHPUKZAUVQUMZOZUVQDNZUVQENZUVPUWCUWIUVPUWCU
      WDUWHUVPUVRUWDUWBUVPUVRUWDUVPFGUVQYJYLUUKWAWBWCWEUVPUWCUWHUVPUWCOUWGAUVQU
      VPUWCAYJUULAYJARZYLUUKAYLARUUJAFWFSSUVRUWBAUVRARUWAAUVQWFSSUWCUVPYQUVQNZU
      WGQZUWBUVRUWMUWAQZUVPUWNQZUWAAUVQWGUWOUVRUWPUWMUWOUVRUVPUWGUWMUWOUWAUVRUV
      PUWGQQUWMUWAWHUVPUWAUVRUWMUWGUULYJUWAUVRUWNQQZUUKYLUUQUUJQZYJUWQQUUJAFWGY
      LUWROZYJUWQUVRUWMUWSYJOZUWAUWGUVRUWMUWTUWAUWGQZQZUVRUWMOZUUQUXBUVQFYQWIUW
      TUUQUXCUXAYLUWRYJUUQUXCUXAQZQUUQUWRYJYLUXDUUQUWRUUJYJYLUXDQZQUUQUUJWHUUQU
      UJYJUXEUUQUUDYJUXEUUIUUQUUDOZYJOZYLUXCUXAUXGYLOZUXCOZUVTUWGIPUXHUXCIUXGYL
      IUXFYJIUUQUUDIUUQIRUUCIPWFSYJIRSYLIRSUXCIRSUWGIRUXIUUSUVTUWGUXGYLUXCUUSUV
      TOZUWGQUXGUXJUXCYLUWGUXFYJUXJUXCYLUWGQZQZQZUUDUUQUUSUUCQZYJUXMQZUUCIPWGUU
      QUXNUXOUXJUXNYJUUQUXLUUSUVTUXNYJUUQUXLQZQZQUUSUXNUVTUXQUUSUXNUUCUVTUXQQUU
      SUUCWHUVTUUCUUSUXQYNUVSUUCUUSUXQQZQYNUUCUVSUXRYNUUCUUBUVSUXRQYNUUBWHYJUVS
      UUSUUBUXPYJUVSUUSUUBUXPQYJUVSUUSWJUXCUUQUUBUXKYJUVSUUSUXCUUQUUBUXKQZQZQYJ
      UXCUUSUVSUXTYJUXCUUSUVSUXTQZQUYAUUSUVSUXSUUQUVSUUBUWGYLUVSUUAUWFHPUVSUUAU
      WFQUVIUVSYTUWEYPYTUVSUWEYRYSUVQWKWLWMWPWNWOWPWQWRWSWTWSVIXAXBXCTXDXEXCTXA
      TXITWSXFXGXHXJXKXJXEXAXFXDVKUTXLUTXIXMXAXEXNXMXIXMXOUTWDUULYJUWIUWCQZYLUU
      KUUJAGUMZYJUYBQYLUUKUYCUUJAFGXSVOYJYLUYCOZUYBYJUYDOZUWIUVRUWBUYEUWDUVRUWH
      UYEUVRUWDUYEFGUVQYJYLUYCWAWBXPWEYJUYCUWIUWBQYLYJUYCOZUWIUWBUYFUWIOUWAAUVQ
      UYFUWIAYJUYCAUWLUUJAGWFSUWDUWHAUWDARUWGAUVQWFSSUWIUYFUWOUWHUWDUWNUYFUWOQZ
      UWGAUVQWGUWNUWDUYGUWMUWNUWDUYFUWAUWMUWNUWGUWDUYFUWAQQUWMUWGWHUYFUWGUWDUWM
      UWAUYCYJUVGUUJQZUWGUWDUWOQQZUUJAGWGUYHYJUYIUWDUWMUYHYJOZUWGUWAUWDUWMUYJUW
      GUWAQZQZUWDUWMOZUVGUYLUVQGYQWIUYJUVGUYMUYKUYHYJUVGUYMUYKQZQUVGUYHYJUYNUVG
      UYHUUJYJUYNQUVGUUJWHUVGUUJYJUYNUVGUUIYJUYNUUDUVGUUIOZYJOZUYMUYKUYPUYMOZUW
      FUWAHPUYPUYMHUYOYJHUVGUUIHUVGHRUUHHPWFSYJHRSUYMHRSUWAHRUYQUVIUWFUWAUYPUYM
      UVIUWFOZUWAQUYPUYRUYMUWAUYOYJUYRUYMUWAQZQZUUIUVGUVIUUHQZYJUYTQZUUHHPWGUVG
      VUAVUBUYRVUAYJUVGUYSUVIUWFVUAYJUVGUYSQZQZQUVIVUAUWFVUDUVIVUAUUHUWFVUDQUVI
      UUHWHUWFUUHUVIVUDYPUWEUUHUVIVUDQZQYPUUHUWEVUEYPUUHUUGUWEVUEQYPUUGWHYJUWEU
      VIUUGVUCUWEUVIUUGVUCQZQQYJUWEVUFUVIUYMUUGUVGUWEUWAUUGUVGUWEUWAQZQQUYMUUGV
      UGUVGUWEUUGUWAUWEUUFUVTIPUWEUUFUVTQUUSUWEUUEUVSYNUUEUWEUVSYSYRUVQWKWLWMWP
      WNXTWPWQXAWPWQXAXBXCTXDXEXCTXATXITXCTXGXHUTXQXJXEYATXDVKUTXLXRXIXAXEXNXMX
      IXMXOUTYBWDWLVSXMYCYHUWJUWCYDYIUULAIUVQBDFJLYEUQYIUWKUWIYDYHUULAHUVQCEGKM
      YEURYFYGUTYC $.
      $( [20-Jun-2009] $)
  $}

  ${
    $d m n x y D $.  $d m n x y J $.
    methausi.1 $e |- D e. Met $.
    methausi.2 $e |- J = ( MetOpen ` D ) $.
    $( The topology generated by a metric space is Hausdorff.  Remark in
       [Munkres] p. 126. $)
    methausi $p |- J e. Haus $=
      ( vx vy vm vn wcel wceq cv w3a co cin c0 wbr syl 3adant3 mpanl1 syl12anc
      cdm cuni cme eqid uniopn2 ax-mp eqcomi ctop opntop wne c2 cdiv cbl cfv cr
      wrex cc0 clt simp1 wa metcl mp3an1 rehalfcl metgt0 biimp3a halfpos2 mpbid
      wb blopn simp2 blcntr cle 3simpa leid bl2in mp3anl1 syl13anc eleq2 eqeq1d
      ineq1 3anbi13d ineq2 3anbi23d rcla42ev syl113anc ishausi ) EFGHBAUAUAZBUB
      ZWGAUCIZWHWGJCABWGWGUDZDUEUFUGWIBUHICABDUIUFEKZWGIZFKZWGIZWKWMUJZLZWKWKWM
      AMZUKULMZAUMUNZMZBIZWMWRWSMZBIZWKWTIZWMXBIZWTXBNZOJZWKHKZIZWMGKZIZXHXJNZO
      JZLZGBUPHBUPWPWLWRUOIZUQWRURPZXAWLWNWOUSZWLWNXOWOWLWNUTZWQUOIZXOWIWLWNXSC
      WKWMAWGWJVAVBZWQVCQZRZWPUQWQURPZXPWLWNWOYCWIWLWNWOYCVHCWKWMAWGWJVDVBVEWLW
      NYCXPVHZWOXRXSYDXTWQVFQRVGZWIWLXOXPUTZXACAWKWRBWGWJDVISTWPWNXOXPXCWLWNWOV
      JZYBYEWIWNYFXCCAWMWRBWGWJDVISTWPWLXOXPXDXQYBYEWIWLYFXDCAWKWRWGWJVKSTWPWNX
      OXPXEYGYBYEWIWNYFXECAWMWRWGWJVKSTWPXRXOXPWRWRVLPZXGWLWNWOVMYBYEWLWNYHWOXR
      XOYHYAWRVNQRWIWLWNXOXPYHLXGCAWKWMWRWGWJVOVPVQXNXDXEXGLXDXKWTXJNZOJZLHGWTX
      BBBXHWTJZXIXDXMYJXKXHWTWKVRYKXLYIOXHWTXJVTVSWAXJXBJZXKXEYJXGXDXJXBWMVRYLY
      IXFOXJXBWTWBVSWCWDWEWF $.
      $( [1-Dec-2007] $) $( [9-Mar-2007] $)
  $}

  ${
    methaus.1 $e |- J = ( MetOpen ` D ) $.
    $( The topology generated by a metric space is Hausdorff.  Remark in
       [Munkres] p. 126. $)
    methaus $p |- ( D e. Met -> J e. Haus ) $=
      ( cme wcel cha cif copn cfv wceq fveq2 syl5eq eleq1d 0met elimel methausi
      c0 eqid dedth ) ADEZBFETAQGZHIZFEAQAUAJZBUBFUCBAHIUBCAUAHKLMUAUBAQDNOUBRP
      S $.
      $( [3-Dec-2007] $) $( [15-Mar-2007] $)
  $}


