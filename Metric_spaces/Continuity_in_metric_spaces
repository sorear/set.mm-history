$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       Continuity in metric spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    metcnf.1 $e |- X = dom dom C $.
    metcnf.2 $e |- Y = dom dom D $.
    metcnf.3 $e |- J = ( Open ` C ) $.
    metcnf.4 $e |- K = ( Open ` D ) $.
    $( A continuous function at point ` P ` is a mapping (metric space version
       of ~ cnpf ). $)
    metcnpf $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\
                F e. ( ( J CnP K ) ` P ) ) -> F : X --> Y ) $=
      ( cme wcel w3a ccnp co cfv wa cuni wf ctop eqid cnpf opntop 3ad2ant1
      3ad2ant2 uniopn2 eleq2d biimpar 3adant2 3jca sylan wb feq2d wceq feq3
      syl sylan9bb 3adant3 adantr mpbid ) AMNZBMNZCGNZOZDCEFPQRNZSETZFTZDUAZGHD
      UAZEUBNZFUBNZCVHNZOVGVJVFCDEFVHVIVHUCVIUCUDVFVLVMVNVCVDVLVEAEKUEUFVDVCVMV
      EBFLUEUGVCVEVNVDVCVNVEVCVHGCAEGIKUHZUIUJUKULUMVFVJVKUNZVGVCVDVPVEVCVJGVID
      UAZVDVKVCVHGVIDVOUOVDVIHUPVQVKUNBFHJLUHVIHGDUQURUSUTVAVB $.
      $( [30-Dec-2007] $) $( [16-Dec-2007] $)

    $( A continuous function is a mapping (metric space version of ~ cnf ). $)
    metcnf $p |- ( ( C e. Met /\ D e. Met /\
                F e. ( J Cn K ) ) -> F : X --> Y ) $=
      ( cme wcel ccn co w3a cuni wf ctop eqid cnf opntop id syl3an wb uniopn2
      feq2d wceq feq3 syl sylan9bb 3adant3 mpbid ) ALMZBLMZCDENOMZPDQZEQZCRZFGC
      RZDSMESMUPUSUNUOUPCDEUQURUQTURTUAADJUBBEKUBUPUCUDUNUOUSUTUEUPUNUSFURCRZUO
      UTUNUQFURCADFHJUFUGUOURGUHVAUTUEBEGIKUFURGFCUIUJUKULUM $.
      $( [19-Nov-2007] $) $( [1-Aug-2007] $)

    $( A constant function is continuous (metric space version of
       ~ cnconst ). $)
    metcnconst $p |- ( ( ( C e. Met /\ D e. Met ) /\ ( B e. Y /\
             F : X --> { B } ) ) -> F e. ( J Cn K ) ) $=
      ( ctop wcel wa cuni csn wf ccn co cme eqid cnconst opntop anim12i adantr
      uniopn2 eleq2d feq2d bi2anan9r biimpar sylanc ) EMNZFMNZOZAFPZNZEPZAQZDRZ
      OZDEFSTNBUANZCUANZOZAHNZGUSDRZOZOADEFURUPURUBUPUBUCVDUOVGVBUMVCUNBEKUDCFL
      UDUEUFVDVAVGVCUQVEVBUTVFVCUPHACFHJLUGUHVBURGUSDBEGIKUGUIUJUKUL $.
      $( [22-Nov-2007] $) $( [10-Aug-2007] $)
  $}

  ${
    $d u v w x y z F $.  $d u v w x y z C $.  $d u v w x y z D $.
    $d u v w x y z X $.  $d u v w x y z Y $.  $d u v w x y z P $.
    $d u v w x y z J $.  $d v x y K $.
    metcn.1 $e |- X = dom dom C $.
    metcn.2 $e |- J = ( Open ` C ) $.
    metcn.3 $e |- Y = dom dom D $.
    metcn.4 $e |- K = ( Open ` D ) $.
    $( MAY BE REVISED - avoid funimass3 here to simplify metcnp2 , and also
       use iscnp2 for metcnp $)
    $( Lemma for ~ metcnp . $)
    metcnplem $p |- ( ( ( C e. Met /\ D e. Met ) /\ ( F : X --> Y /\ P e. X ) )
     -> ( A. y e. RR ( 0 < y -> E. z e. RR ( 0 < z /\ ( P ( ball ` C ) z ) C_
    ( `' F " ( ( F ` P ) ( ball ` D ) y ) ) ) ) <-> A. y e. RR ( 0 < y ->
      E. z e. RR ( 0 < z /\ A. w e. X ( ( P C w ) < z ->
    ( ( F ` P ) D ( F ` w ) ) < y ) ) ) ) ) $=
      ( cme wcel wa wf cc0 cv clt wbr cbl cfv co ccnv cima wss cr wrex wi wral
      wb wfun cdm funimass5 ffun adantr ad2antlr blssm pm3.26 pm3.27 anim12i
      simprr sylanc wceq fdm sseqtr4d ssel2 sylan biimt syl pm5.74da bi2.04
      syl6bb w3a elbl2 simpll ad2antrr 3jca simplrr simplr ffvelrn adantl
      adantlr adantll simplrl imbi12d bitrd ralbidv2 anassrs pm5.32da rexbidva
      ralbidva ) DPQZEPQZRZJKGSZFJQZRZRZTAUAZUBUCZTBUAZUBUCZFXEDUDUEUFZGUGFGUEZ
      XCEUDUEUFZUHUIZRZBUJUKZULXDXFFCUAZDUFXEUBUCZXHXMGUEZEUFXCUBUCZULZCJUMZRZB
      UJUKZULAUJXBXCUJQZRXDXLXTXBYAXDXLXTUNXBYAXDRZRZXKXSBUJYCXEUJQZRXFXJXRYCYD
      XFXJXRUNZXBYBYDXFRZYEXBYBYFRZRZXJXOXIQZCXGUMZXRGUOZXGGUPZUIXJYJUNYHCXGXIG
      UQXAYKWRYGWSYKWTJKGURUSUTYHXGJYLWPWTRZYFXGJUIZYHDFXEJLVAXBYMYGWRWPXAWTWPW
      QVBWSWTVCVDUSXBYBYFVEVFZXAYLJVGZWRYGWSYPWTJKGVHUSUTVIVFYHYIXQCXGJYHXMXGQZ
      YIULZXMJQZYRULZYSXQULYHYRYQYSYIULZULYTYHYQYIUUAYHYQRYSYIUUAUNYNYQYSYHXGJX
      MVJYOVKYSYIVLVMVNYQYSYIVOVPYHYSYRXQYHYSRZYQXNYIXPWPWTYSVQYFYQXNUNUUBXMDFX
      EJLVRUUBWPWTYSXBWPYGYSWPWQXAVSVTXBWTYGYSWRWSWTVEVTYHYSVCWAXBYBYFYSWBVFWQX
      HKQZXOKQZVQYBYIXPUNUUBXOEXHXCKNVRUUBWQUUCUUDXBWQYGYSWPWQXAWCVTXBUUCYGYSXA
      UUCWRJKFGWDWEVTXBYSUUDYGXAYSUUDWRWSYSUUDWTJKXMGWDWFWGWFWAXBYBYFYSWHVFWIVN
      WJWKWJWLWLWMWNWLVNWO $.
      $( [16-Nov-2007] $) $( [12-May-2007] $)

    $( Two ways to say a mapping from metric ` C ` to metric ` D ` is
       continuous at point ` P ` . _Warning:  The HTML proof page is 0.6
       megabyte in size_. $)
    metcnp $p |- ( ( C e. Met /\ D e. Met /\ P e. X ) ->
                ( F e. ( ( J CnP K ) ` P ) <->
          ( F : X --> Y /\ A. y e. RR ( 0 < y -> E. z e. RR ( 0 < z /\
      A. w e. X ( ( P C w ) < z -> ( ( F ` P ) D ( F ` w ) ) < y ) ) ) ) ) ) $=
      ( vv vu cme wcel w3a ccnp co cfv cuni wf cv cima wss wa wrex wi wral cc0
      clt wbr cr ctop wb eqid iscnp opntop 3ad2ant1 3ad2ant2 uniopn2 eleq2d
      biimpar 3adant2 syl3anc wceq feq23 syl2an anbi1d 3adant3 cbl ccnv eleq2
      sseq2 anbi2d rexbidv imbi12d rcla4v com23 blopn blcntr sylc ffvelrn
      sylanl2 adantlll wfun cdm funimass3 ffun ad2antlr opnss adantlr fdm
      sseqtr4d sylanc adantr sstr expcom anim2d r19.22dva opni2 3expa syl5com
      adantllr sylbid expimpd r19.23adva ad2ant2r syld imp an1rs exp32
      r19.21aiv 3expb simpllr sylan anassrs breq2 opreq2 imaeq2d sseq2d
      rcla4cva adantrr ad2antrl ad2antrr blssm adantlrl imaeq2 sseq1d anbi12d
      rcla4ev ancoms jca ex sylbird adantrl mpd r19.21aiva impbida metcnplem
      bitrd exp43 com34 3imp pm5.32d 3bitrd ) DRSZERSZFJSZTZGFHIUAUBUCSZHUDZIUD
      ZGUEZFGUCZPUFZSZFQUFZSZGUVAUGZUUSUHZUIZQHUJZUKZPIULZUIZJKGUEZUVHUIZUVJUMA
      UFZUNUOZUMBUFZUNUOZFCUFZDUBUVNUNUOUURUVPGUCEUBUVLUNUOUKCJULUIBUPUJUKAUPUL
      ZUIHUQSZIUQSZFUUOSZUUNUVIURUUMQPFGHIUUOUUPUUOUSUUPUSUTUUJUUKUVRUULDHMVAVB
      UUKUUJUVSUULEIOVAVCUUJUULUVTUUKUUJUVTUULUUJUUOJFDHJLMVDZVEVFVGVHUUJUUKUVI
      UVKURUULUUJUUKUIZUUQUVJUVHUUOJVIUUPKVIUUQUVJURUUJUUKUUOUUPJKGVJUWAEIKNOVD
      VKVLVMUUMUVJUVHUVQUUJUUKUULUVJUVHUVQURZUKUUJUUKUVJUULUWCUUJUUKUVJUULUWCUW
      BUVJUULUIZUIZUVHUVMUVOFUVNDVNUCUBZGVOZUURUVLEVNUCZUBZUGZUHZUIZBUPUJZUKZAU
      PULZUVQUWEUVHUWOUWEUVHUIZUWNAUPUWPUVLUPSZUVMUWMUWEUWQUVMUIZUVHUWMUWEUWRUI
      ZUVHUWMUWSUVHUVBUVCUWIUHZUIZQHUJZUWMUUKUWDUWRUVHUXBUKZUUJUUKUURKSZUWRUXCU
      WDUWIISZUURUWISZUXCUUKUXDUIUWRUIUXEUVHUXFUXBUVGUXFUXBUKPUWIIUUSUWIVIZUUTU
      XFUVFUXBUUSUWIUURVPUXGUVEUXAQHUXGUVDUWTUVBUUSUWIUVCVQVRVSVTWAWBEUURUVLIKN
      OWCEUURUVLKNWDWEJKFGWFWGWHUWEUXBUWMUKZUWRUUJUVJUXHUUKUULUUJUVJUIZUXAUWMQH
      UXIUVAHSZUIZUVBUWTUWMUXKUVBUIUWTUVAUWJUHZUWMUXKUWTUXLURZUVBGWIZUVAGWJZUHU
      XMUXKUVAUWIGWKUVJUXNUUJUXJJKGWLZWMUXKUVAJUXOUUJUXJUVAJUHUVJUVADHJLMWNWOUV
      JUXOJVIZUUJUXJJKGWPZWMWQWRWSUUJUXJUVBUXLUWMUKUVJUXLUVOUWFUVAUHZUIZBUPUJZU
      WMUUJUXJUIUVBUIUXLUXTUWLBUPUXLUVNUPSZUIUXSUWKUVOUXLUXSUWKUKUYBUXSUXLUWKUW
      FUVAUWJWTXAWSXBXCUUJUXJUVBUYABUVADFHMXDXEXFXGXHXIXJXKWSXLXMXNXOXPUWEUWOUI
      ZUVGPIUYCUUSISZUIZUUTUVFUYEUUTUIUMUVPUNUOZUURUVPUWHUBZUUSUHZUIZCUPUJZUVFU
      YCUYDUUTUYJUUKUYDUUTUIUYJUYCUUKUYDUUTUYJCUUSEUURIOXDXQUUJUUKUWDUWOXRXSXTU
      YCUYJUVFUKZUYDUUTUUJUWDUWOUYKUUKUUJUWDUIZUWOUIZUYIUVFCUPUYMUVPUPSZUIZUYIU
      VFUYOUYIUIUVOUWFUWGUYGUGZUHZUIZBUPUJZUVFUYOUYFUYSUYHUWOUYNUYFUYSUYLUWOUYN
      UIUYFUYSUWNUYFUYSUKAUVPUPUVLUVPVIZUVMUYFUWMUYSUVLUVPUMUNYAUYTUWLUYRBUPUYT
      UWKUYQUVOUYTUWJUYPUWFUYTUWIUYGUWGUVLUVPUURUWHYBYCYDVRVSVTYEXMWHYFUYOUYHUY
      SUVFUKZUYFUYLUYNUYHVUAUWOUYLUYHVUAUYNUYLUYHUIZUYRUVFBUPVUBUYBUIZUVOUYQUVF
      VUCUVOUIZUYQGUWFUGZUYGUHZUVFUYLUYBUVOVUFUYQURZUYHUXNUWFUXOUHVUGUYLUYBUIUV
      OUIZUWFUYGGWKUYLUXNUYBUVOUVJUXNUUJUULUXPYGYHVUHUWFJUXOUYLUYBUVOUWFJUHZUUJ
      UULUYBUVOUIZVUIUVJDFUVNJLYIYJXTUYLUXQUYBUVOUVJUXQUUJUULUXRYGYHWQWRXGVUDVU
      FUVFUWFHSZFUWFSZVUEUUSUHZUIZUVFVUDVUFUIZUVEVUNQUWFHUVAUWFVIZUVBVULUVDVUMU
      VAUWFFVPVUPUVCVUEUUSUVAUWFGYKYLYMYNVUDVUKVUFUYLUYBUVOVUKUYHUYLUYBUVOVUKUU
      JUULVUJVUKUVJDFUVNHJLMWCYJXTXGWSVUOVULVUMVUDVULVUFUYLUYBUVOVULUYHUYLUYBUV
      OVULUUJUULVUJVULUVJDFUVNJLWDYJXTXGWSVUBUVOVUFVUMUYBUYHUVOVUFVUMUYLUYHVUFV
      UMUVOVUFUYHVUMVUEUYGUUSWTYOWOWHXGYPWRYQYRXIXJWOXGYSYTYQXJXGYHYTYQUUAUUBAB
      CDEFGHIJKLMNOUUCUUDUUEUUFUUGUUHUUI $.
      $( [16-Nov-2007] $) $( [11-May-2007] $)

    $( Two ways to say a mapping from metric ` C ` to metric ` D ` is
       continuous at point ` P ` .  The distance arguments are swapped compared
       to ~ metcnp (and Munkres' ~ metcn ) for compatibility with ~ df-lm .
       Definition 1.3-3 of [Kreyszig] p. 20. $)
    metcnp2 $p |- ( ( C e. Met /\ D e. Met /\ P e. X ) ->
                ( F e. ( ( J CnP K ) ` P ) <->
          ( F : X --> Y /\ A. y e. RR ( 0 < y -> E. z e. RR ( 0 < z /\
      A. w e. X ( ( w C P ) < z -> ( ( F ` w ) D ( F ` P ) ) < y ) ) ) ) ) ) $=
      ( cme wcel w3a ccnp co cfv wf cc0 cv clt wbr wi wral wa cr wrex metcnp
      wb wceq metsym 3expa breq1d 3adantl2 adantlr 3simp2 ad2antrr ffvelrn
      ancoms 3ad2antl3 adantr adantll syl3anc imbi12d ralbidva anbi2d rexbidv
      imbi2d ralbidv pm5.32da bitrd ) DPQZEPQZFJQZRZGFHISTUAQJKGUBZUCAUDZUEUFZU
      CBUDZUEUFZFCUDZDTZWCUEUFZFGUAZWEGUAZETZWAUEUFZUGZCJUHZUIZBUJUKZUGZAUJUHZU
      IVTWBWDWEFDTZWCUEUFZWIWHETZWAUEUFZUGZCJUHZUIZBUJUKZUGZAUJUHZUIABCDEFGHIJK
      LMNOULVSVTWQXGVSVTUIZWPXFAUJXHWOXEWBXHWNXDBUJXHWMXCWDXHWLXBCJXHWEJQZUIZWG
      WSWKXAVSXIWGWSUMZVTVPVRXIXKVQVPVRUIXIUIWFWRWCUEVPVRXIWFWRUNFWEDJLUOUPUQUR
      USXJWJWTWAUEVQWHKQZWIKQZWJWTUNXJWHWIEKNUOVSVQVTXIVPVQVRUTVAXHXLXIVRVPVTXL
      VQVTVRXLJKFGVBVCVDVEVTXIXMVSJKWEGVBVFVGUQVHVIVJVKVLVMVNVO $.
      $( [17-Nov-2007] $) $( [4-Jun-2007] $)

    $( Two ways to say a mapping from metric ` C ` to metric ` D ` is
       continuous.  Theorem 10.1 of [Munkres] p. 127.  The second biconditional
       argument says that for every positive "epsilon" ` y ` there is a
       positive "delta" ` z ` such that a distance less than delta in ` C `
       maps to a distance less than epsilon in ` D ` . $)
    metcn $p |- ( ( C e. Met /\ D e. Met ) ->
                ( F e. ( J Cn K ) <-> ( F : X --> Y /\
                A. x e. X A. y e. RR ( 0 < y -> E. z e. RR ( 0 < z /\
      A. w e. X ( ( x C w ) < z -> ( ( F ` x ) D ( F ` w ) ) < y ) ) ) ) ) ) $=
      ( cme wcel wa ccn co cuni wf cv ccnp cfv wral cc0 clt wbr wi cr wrex
      ctop wb eqid cnf 3expia pm4.71rd cncnp 3expa pm5.32da bitrd opntop
      syl2an wceq feq23 uniopn2 adantr anbi1d eleq2d biimpa metcnp syldan w3a
      cnpf ex imp 3bitr2rd pm5.32 sylibr an1rs ralbidva raleq1d anbi12d 3bitrd
      ) EPQZFPQZRZGHISTQZHUAZIUAZGUBZGAUCZHIUDTUEQZAWJUFZRZWLUGBUCZUHUIUGCUCZUH
      UIWMDUCZETWRUHUIWMGUEWSGUEFTWQUHUIUJDJUFRCUKULUJBUKUFZAWJUFZRJKGUBZWTAJUF
      ZRHUMQZIUMQZWIWPUNWFWGXDXERZWIWLWIRWPXFWIWLXDXEWIWLGHIWJWKWJUOZWKUOZUPUQU
      RXFWLWIWOXDXEWLWIWOUNAGHIWJWKXGXHUSUTVAVBEHMVCZFIOVCZVDWHWLWOXAWHWLRWNWTA
      WJWHWMWJQZWLWNWTUNZWHXKRZWLXLXMWLWNRZWLWTRZUNWLXLUJXMXOXBWTRZWNXNXMWLXBWT
      WHWLXBUNZXKWJJVEZWKKVEXQWFWGWJWKJKGVFEHJLMVGZFIKNOVGVDZVHVIWHXKWMJQZWNXPU
      NZWHXKYAWHWJJWMWFXRWGXSVHZVJVKWFWGYAYBBCDEFWMGHIJKLMNOVLUTVMWHXKWNXNUNZXD
      XEXKYDUJWFWGXDXEXKYDXDXEXKVNZWNWLYEWNWLWMGHIWJWKXGXHVOVPURUQXIXJVDVQVRWLW
      NWTVSVTVQWAWBVAWHWLXBXAXCXTWHWTAWJJYCWCWDWE $.
      $( [16-Nov-2007] $) $( [15-May-2007] $)

    ${
      $d w x y z A $.  $d x y w z C $.  $d x y w z D $.  $d w z K $.
      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a continuous metric space function, with
         function arguments as in ~ metcnp . $)
      metcnpi $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\ ( F e.
           ( ( J CnP K ) ` P ) /\ A e. RR /\ 0 < A ) ) -> E. x e. RR ( 0 < x /\
        A. y e. X ( ( P C y ) < x -> ( ( F ` P ) D ( F ` y ) ) < A ) ) ) $=
        ( vz cme wcel w3a ccnp co cfv cr cc0 clt wbr cv wi wral wa wrex wf
        metcnp wceq breq2 imbi2d ralbidv anbi2d rexbidv imbi12d rcla4cv adantl
        syl6bi 3imp2 ) DQREQRFJRSZGFHITUAUBRZCUCRZUDCUEUFZUDAUGZUEUFZFBUGZDUAVI
        UEUFZFGUBVKGUBEUAZCUEUFZUHZBJUIZUJZAUCUKZVEVFJKGULZUDPUGZUEUFZVJVLVMVTU
        EUFZUHZBJUIZUJZAUCUKZUHZPUCUIZUJVGVHVRUHZUHZPABDEFGHIJKLMNOUMWHWJVSWGWI
        PCUCVTCUNZWAVHWFVRVTCUDUEUOWKWEVQAUCWKWDVPVJWKWCVOBJWKWBVNVLVTCVMUEUOUP
        UQURUSUTVAVBVCVD $.
        $( [2-Jan-2008] $) $( [17-Dec-2007] $)

      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a continuous metric space function, with
         swapped distance function arguments as in ~ metcnp2 . $)
      metcnpi2 $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\ ( F e.
           ( ( J CnP K ) ` P ) /\ A e. RR /\ 0 < A ) ) -> E. x e. RR ( 0 < x /\
        A. y e. X ( ( y C P ) < x -> ( ( F ` y ) D ( F ` P ) ) < A ) ) ) $=
        ( vz cme wcel w3a ccnp co cfv cr cc0 clt wbr cv wi wral wa wrex wf
        metcnp2 wceq breq2 imbi2d ralbidv anbi2d rexbidv imbi12d rcla4cv
        adantl syl6bi 3imp2 ) DQREQRFJRSZGFHITUAUBRZCUCRZUDCUEUFZUDAUGZUEUFZBUG
        ZFDUAVIUEUFZVKGUBFGUBEUAZCUEUFZUHZBJUIZUJZAUCUKZVEVFJKGULZUDPUGZUEUFZVJ
        VLVMVTUEUFZUHZBJUIZUJZAUCUKZUHZPUCUIZUJVGVHVRUHZUHZPABDEFGHIJKLMNOUMWHW
        JVSWGWIPCUCVTCUNZWAVHWFVRVTCUDUEUOWKWEVQAUCWKWDVPVJWKWCVOBJWKWBVNVLVTCV
        MUEUOUPUQURUSUTVAVBVCVD $.
        $( [8-Feb-2008] $) $( [16-Dec-2007] $)

      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a metric space function continuous at
         ` P ` .  A variation of ~ metcnpi2 with non-strict ordering. $)
      metcnpi3 $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\ ( F e.
           ( ( J CnP K ) ` P ) /\ A e. RR /\ 0 < A ) ) -> E. x e. RR ( 0 < x /\
        A. y e. X ( ( y C P ) <_ x -> ( ( F ` y ) D ( F ` P ) ) <_ A ) ) ) $=
        ( vz cme wcel w3a ccnp co cfv cr cc0 clt wbr wa cv wi wral wrex cle
        metcnpi2 wf metcnpf c2 cdiv wceq breq2 imbi1d ralbidv anbi12d rcla4ev
        rehalfcl ad2antrl metcl 3com23 halfpos biimpa adantl lelttr pm3.26
        pm3.27 syl3anc adantrr mpan2d ex syl 3expia com23 3adant2 ad2antrr
        imp31 ltle 3expb ffvelrn anim12i anandis ancom2s sylan2 exp45 imp
        3adant1 adantlr simplrl sylanc imim12d r19.20dva halfpos2 jctild exp32
        imp45 r19.23adv syldan 3imp2 mpd ) DQRZEQRZFJRZSZGFHITUAUBRZCUCRZUDCUEU
        FZSUGUDPUHZUEUFZBUHZFDUAZXNUEUFZXPGUBZFGUBZEUAZCUEUFZUIZBJUJZUGZPUCUKZU
        DAUHZUEUFZXQYGULUFZYACULUFZUIZBJUJZUGZAUCUKZPBCDEFGHIJKLMNOUMXJXKXLXMYF
        YNUIZXJXKXLXMYOUIUIZXJXKJKGUNZYPDEFGHIJKLNMOUOXJYQUGZXLXMYOYRXLXMUGZUGZ
        YEYNPUCYTXNUCRZYEYNXNUPUQUAZUCRZUDUUBUEUFZXQUUBULUFZYJUIZBJUJZUGZYNYTUU
        AYEUGUGYMUUHAUUBUCYGUUBURZYHUUDYLUUGYGUUBUDUEUSUUIYKUUFBJUUIYIUUEYJYGUU
        BXQULUSUTVAVBVCUUAUUCYTYEXNVDZVEYTUUAXOYDUUHYTUUAXOYDUUHUIYTUUAXOUGZUGZ
        YDUUGUUDUULYCUUFBJUULXPJRZUGUUEXRYBYJYTUUKUUMUUEXRUIZXJUUKUUMUUNUIUIZYQ
        YSXGXIUUOXHXGXIUGUUMUUKUUNXGXIUUMUUKUUNUIZXGXIUUMSXQUCRZUUPXGUUMXIUUQXP
        FDJLVFVGUUQUUKUUNUUQUUKUGUUEUUBXNUEUFZXRUUKUURUUQUUAXOUURXNVHVIVJUUQUUA
        UUEUURUGXRUIZXOUUQUUCUUAUUSUUQUUAUGXQUUBXNVKUUQUUAVLUUAUUCUUQUUJVJUUQUU
        AVMVNVOVPVQVRVSVTWAWBWCYTUUMYBYJUIZUUKYAUCRZXLUUTYTUUMUGYACWDYRUUMUVAYS
        XJYQUUMUVAXHXIYQUUMUVAUIZUIZXGXHXIUVCXHYQXIUVBXHYQXIUUMUVAXHXSKRZXTKRZU
        GZUVAYQXIUUMUGUGXHUVDUVEUVAXSXTEKNVFWEYQUUMXIUVFYQUUMXIUVFYQUUMUGUVDYQX
        IUGUVEJKXPGWFJKFGWFWGWHWIWJWKVTWLWMWCWNYRXLXMUUMWOWPWNWQWRUUKUUDYTUUAXO
        UUDXNWSVIVJWTXAXBWPXAXCXAXDVQXEXF $.
        $( [15-Feb-2008] $) $( [16-Dec-2007] $)

      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a metric space function continuous at
         ` P ` .  A variation of ~ metcnpi with non-strict ordering. $)
      metcnpi4 $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\ ( F e.
           ( ( J CnP K ) ` P ) /\ A e. RR /\ 0 < A ) ) -> E. x e. RR ( 0 < x /\
        A. y e. X ( ( P C y ) <_ x -> ( ( F ` P ) D ( F ` y ) ) <_ A ) ) ) $=
        ( vz cme wcel w3a ccnp co cfv cr cc0 clt wbr wa cv wi wral wrex cle
        metcnpi wf metcnpf c2 cdiv wceq breq2 imbi1d ralbidv anbi12d rcla4ev
        rehalfcl ad2antrl metcl halfpos biimpa adantl lelttr pm3.26 pm3.27
        syl3anc adantrr mpan2d ex syl 3expia com23 3adant2 ad2antrr imp31 ltle
        3expb ffvelrn anim12i anandis sylan2 exp45 imp 3adant1 adantlr simplrl
        sylanc imim12d r19.20dva halfpos2 jctild exp32 imp45 r19.23adv syldan
        3imp2 mpd ) DQRZEQRZFJRZSZGFHITUAUBRZCUCRZUDCUEUFZSUGUDPUHZUEUFZFBUHZDU
        AZXLUEUFZFGUBZXNGUBZEUAZCUEUFZUIZBJUJZUGZPUCUKZUDAUHZUEUFZXOYEULUFZXSCU
        LUFZUIZBJUJZUGZAUCUKZPBCDEFGHIJKLMNOUMXHXIXJXKYDYLUIZXHXIXJXKYMUIUIZXHX
        IJKGUNZYNDEFGHIJKLNMOUOXHYOUGZXJXKYMYPXJXKUGZUGZYCYLPUCYRXLUCRZYCYLXLUP
        UQUAZUCRZUDYTUEUFZXOYTULUFZYHUIZBJUJZUGZYLYRYSYCUGUGYKUUFAYTUCYEYTURZYF
        UUBYJUUEYEYTUDUEUSUUGYIUUDBJUUGYGUUCYHYEYTXOULUSUTVAVBVCYSUUAYRYCXLVDZV
        EYRYSXMYBUUFYRYSXMYBUUFUIYRYSXMUGZUGZYBUUEUUBUUJYAUUDBJUUJXNJRZUGUUCXPX
        TYHYRUUIUUKUUCXPUIZXHUUIUUKUULUIUIZYOYQXEXGUUMXFXEXGUGUUKUUIUULXEXGUUKU
        UIUULUIZXEXGUUKSXOUCRZUUNFXNDJLVFUUOUUIUULUUOUUIUGUUCYTXLUEUFZXPUUIUUPU
        UOYSXMUUPXLVGVHVIUUOYSUUCUUPUGXPUIZXMUUOUUAYSUUQUUOYSUGXOYTXLVJUUOYSVKY
        SUUAUUOUUHVIUUOYSVLVMVNVOVPVQVRVSVTWAWBYRUUKXTYHUIZUUIXSUCRZXJUURYRUUKU
        GXSCWCYPUUKUUSYQXHYOUUKUUSXFXGYOUUKUUSUIZUIZXEXFXGUVAXFYOXGUUTXFYOXGUUK
        UUSXFXQKRZXRKRZUGZUUSYOXGUUKUGUGXFUVBUVCUUSXQXREKNVFWDYOXGUUKUVDYOXGUGU
        VBYOUUKUGUVCJKFGWEJKXNGWEWFWGWHWIVSWJWKWBWLYPXJXKUUKWMWNWLWOWPUUIUUBYRY
        SXMUUBXLWQVHVIWRWSWTWNWSXAWSXBVPXCXD $.
        $( [3-Jan-2008] $) $( [17-Dec-2007] $)

      $( Epsilon-delta property of a continuous metric space function. $)
      metcni $p |- ( ( ( C e. Met /\ D e. Met /\
                  F e. ( J Cn K ) ) /\
           ( P e. X /\ A e. RR /\ 0 < A ) ) -> E. x e. RR ( 0 < x /\
        A. y e. X ( ( P C y ) < x -> ( ( F ` P ) D ( F ` y ) ) < A ) ) ) $=
        ( vw vz wcel cr cc0 clt wbr w3a cme ccn co cv cfv wi wral wa wrex wceq
        opreq1 breq1d fveq2 opreq1d imbi12d ralbidv anbi2d rexbidv imbi2d
        breq2 rcla42v wf metcn pm3.27 syl6bi 3impia syl5 com23 impcom ) FJRZCSR
        ZTCUAUBZUCDUDRZEUDRZGHIUEUFRZUCZTAUGZUAUBZFBUGZDUFZVTUAUBZFGUHZWBGUHZEU
        FZCUAUBZUIZBJUJZUKZASULZVMVNVOVSWLUIVMVNUKZVSVOWLWMTPUGZUAUBZWAQUGZWBDU
        FZVTUAUBZWPGUHZWFEUFZWNUAUBZUIZBJUJZUKZASULZUIZPSUJQJUJZVOWLUIZVSXFXHWO
        WAWDWGWNUAUBZUIZBJUJZUKZASULZUIQPFCJSWPFUMZXEXMWOXNXDXLASXNXCXKWAXNXBXJ
        BJXNWRWDXAXIXNWQWCVTUAWPFWBDUNUOXNWTWGWNUAXNWSWEWFEWPFGUPUQUOURUSUTVAVB
        WNCUMZWOVOXMWLWNCTUAVCXOXLWKASXOXKWJWAXOXJWIBJXOXIWHWDWNCWGUAVCVBUSUTVA
        URVDVPVQVRXGVPVQUKVRJKGVEZXGUKXGQPABDEGHIJKLMNOVFXPXGVGVHVIVJVKVIVL $.
        $( [19-Nov-2007] $) $( [3-Aug-2007] $)

      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a continuous metric space function. $)
      metcni2 $p |- ( ( ( C e. Met /\ D e. Met /\
                  F e. ( J Cn K ) ) /\
           ( P e. X /\ A e. RR /\ 0 < A ) ) -> E. x e. RR ( 0 < x /\
        A. y e. X ( ( P C y ) <_ x -> ( ( F ` P ) D ( F ` y ) ) <_ A ) ) ) $=
        ( vz cme wcel ccn co w3a cr cc0 clt wbr wa cv cfv wi wral wrex cle
        metcni c2 cdiv wceq breq2 imbi1d ralbidv anbi12d rcla4ev rehalfcl
        ad2antrl metcl halfpos biimpa adantl lelttr pm3.26 pm3.27 syl3anc
        adantrr mpan2d ex syl 3expia com23 3ad2antr1 3ad2antl1 imp31 ltle wf
        3expb ffvelrn anim12i anandis sylan2 exp45 imp 3adant1 metcnf syld3an3
        3simp2 ad2antlr sylanc adantlr imim12d r19.20dva halfpos2 jctild exp32
        imp45 r19.23adv mpd ) DQRZEQRZGHISTRZUAZFJRZCUBRZUCCUDUEZUAZUFZUCPUGZUD
        UEZFBUGZDTZXNUDUEZFGUHZXPGUHZETZCUDUEZUIZBJUJZUFZPUBUKUCAUGZUDUEZXQYFUL
        UEZYACULUEZUIZBJUJZUFZAUBUKZPBCDEFGHIJKLMNOUMXMYEYMPUBXMXNUBRZYEYMXNUNU
        OTZUBRZUCYOUDUEZXQYOULUEZYIUIZBJUJZUFZYMXMYNYEUFUFYLUUAAYOUBYFYOUPZYGYQ
        YKYTYFYOUCUDUQUUBYJYSBJUUBYHYRYIYFYOXQULUQURUSUTVAYNYPXMYEXNVBZVCXMYNXO
        YDUUAXMYNXOYDUUAUIXMYNXOUFZUFZYDYTYQUUEYCYSBJUUEXPJRZUFYRXRYBYIXMUUDUUF
        YRXRUIZXEXFXLUUDUUFUUGUIUIZXGXEXJXIUUHXKXEXIUFUUFUUDUUGXEXIUUFUUDUUGUIZ
        XEXIUUFUAXQUBRZUUIFXPDJLVDUUJUUDUUGUUJUUDUFYRYOXNUDUEZXRUUDUUKUUJYNXOUU
        KXNVEVFVGUUJYNYRUUKUFXRUIZXOUUJYPYNUULUUJYNUFXQYOXNVHUUJYNVIYNYPUUJUUCV
        GUUJYNVJVKVLVMVNVOVPVQVRVSVTXMUUFYBYIUIZUUDYAUBRZXJUUMXMUUFUFYACWAXMUUF
        UUNXHXJXIUUFUUNUIZXKXHXIUUOXEXFJKGWBZXIUUOUIZXGXFUUPUUQXEXFUUPUUQXFUUPX
        IUUFUUNXFXSKRZXTKRZUFZUUNUUPXIUUFUFUFXFUURUUSUUNXSXTEKNVDWCUUPXIUUFUUTU
        UPXIUFUURUUPUUFUFUUSJKFGWDJKXPGWDWEWFWGWHWIWJDEGHIJKLNMOWKWLWIVRWIXLXJX
        HUUFXIXJXKWMWNWOWPWQWRUUDYQXMYNXOYQXNWSVFVGWTXAXBWOXAXCXD $.
        $( [14-Dec-2007] $) $( [14-Dec-2007] $)
    $}

    $( Two ways to express that ` F ` is continuous at ` P ` for metric
       spaces.  Proposition 14-4.2 of [Gleason] p. 240. $)
    metcnp3 $p |- ( ( C e. Met /\ D e. Met /\ P e. X )
    -> ( F e. ( ( J CnP K ) ` P ) <-> ( F : X --> Y /\
    A. y e. RR ( 0 < y -> E. z e. RR ( 0 < z /\ ( F " ( P ( ball ` C ) z ) )
    C_ ( ( F ` P ) ( ball ` D ) y ) ) ) ) ) ) $=
      ( vw cme wcel w3a ccnp co cfv wf cc0 cv clt wbr wi wral wa cr wrex cbl
      cima wss metcnp wb ccnv wfun cdm funimass3 ffun adantr ad2antlr blssm
      adantlrl wceq fdm sseqtr4d sylanc anassrs pm5.32da rexbidva imbi2d
      ralbidv adantlr metcnplem bitr2d an1rs 3impa bitrd ) CPQZDPQZEIQZRFEGHSTU
      AQIJFUBZUCAUDZUEUFZUCBUDZUEUFZEOUDZCTWGUEUFEFUAZWIFUADTWEUEUFUGOIUHUIBUJU
      KUGAUJUHZUIZWDWFWHFEWGCULUATZUMWJWEDULUATZUNZUIZBUJUKZUGZAUJUHZUIZABOCDEF
      GHIJKLMNUOWAWBWCWLWTUPWAWBUIZWCUIWDWKWSXAWDWCWKWSUPZXAWDWCXBXAWDWCUIZUIWS
      WFWHWMFUQWNUMUNZUIZBUJUKZUGZAUJUHZWKWAXCWSXHUPWBWAXCUIZWRXGAUJXIWQXFWFXIW
      PXEBUJXIWGUJQZUIWHWOXDXIXJWHWOXDUPZFURZWMFUSZUNXKXIXJWHUIZUIZWMWNFUTXCXLW
      AXNWDXLWCIJFVAVBVCXOWMIXMWAWCXNWMIUNWDCEWGIKVDVEXCXMIVFZWAXNWDXPWCIJFVGVB
      VCVHVIVJVKVLVMVNVOABOCDEFGHIJKLMNVPVQVJVRVKVSVT $.
      $( [11-Mar-2008] $) $( [17-May-2007] $)
  $}

  ${
    $d f w x y z B $.  $d f w x y z C $.  $d f w x y z D $.  $d f w x y z J $.
    $d f x y K $.  $d f x y L $.
    metcnco.1 $e |- J = ( Open ` B ) $.
    metcnco.2 $e |- K = ( Open ` C ) $.
    metcnco.3 $e |- L = ( Open ` D ) $.
    $( Composition of two continuous functions (metric space version of
       ~ cnco ). $)
    metcnco $p |- ( ( ( B e. Met /\ C e. Met /\ D e. Met ) /\ (
        F e. ( J Cn K ) /\ G e. ( K Cn L ) ) ) -> ( G o. F ) e. ( J Cn L ) ) $=
      ( ctop wcel ccn co wa ccom cme cnco opntop syl3anl ) FLMGLMHLMDFGNOMEGHNO
      MPEDQFHNOMARMBRMCRMDEFGHSAFITBGJTCHKTUA $.
      $( [19-Nov-2007] $) $( [6-Aug-2007] $)

    $( Subset relationship for continuity of metric spaces. $)
    metcnss $p |- ( ( ( B e. Met /\ C e. Met /\ D e. Met ) /\ C C_ D ) ->
      ( J Cn K ) C_ ( J Cn L ) ) $=
      ( vf vy vz vx vw cme wcel w3a wss wa ccn co cdm cv wf cc0 clt wbr cfv wi
      wral cr wrex fss eqid metss sylan2 expcom adantl adantrd cxp cres wceq
      oprvres ffvelrn adantr adantlr sylanc adantlll metf fdm reseq2 3syl
      ad2antrr wfun funssres ffun syl sylan adantll eqtr3d opreqd breq1d
      imbi2d ralbidva anbi2d rexbidv ralbidv biimprd expimpd jcad 3adantl1 wb
      metcn 3adant3 3adant2 3imtr4d ssrdv ) AOPZBOPZCOPZQZBCRZSZJDETUAZDFTUAZXC
      AUBUBZBUBZUBZJUCZUDZUEKUCZUFUGZUELUCZUFUGZMUCZNUCZAUAXMUFUGZXOXIUHZXPXIUH
      ZBUAZXKUFUGZUIZNXFUJZSZLUKULZUIZKUKUJZMXFUJZSZXFCUBUBZXIUDZXLXNXQXRXSCUAZ
      XKUFUGZUIZNXFUJZSZLUKULZUIZKUKUJZMXFUJZSZXIXDPZXIXEPZWSWTXBYIUUAUIWRWSWTS
      ZXBSZYIYKYTUUEXJYKYHXBXJYKUIUUDXJXBYKXJXHYJRYKXBXFXHYJXIUMBCXHYJXHUNZYJUN
      ZUOUPUQURUSUUEXJYHYTUUEXJSZYTYHUUHYSYGMXFUUHXOXFPZSZYRYFKUKUUJYQYEXLUUJYP
      YDLUKUUJYOYCXNUUJYNYBNXFUUJXPXFPZSZYMYAXQUULYLXTXKUFUULXRXSCXHXHUTZVAZUAZ
      YLXTXJUUIUUKUUOYLVBZUUEXRXHPZXSXHPZUUPXJUUISZUUKSXRXSXHXHCVCUUSUUQUUKXFXH
      XOXIVDVEXJUUKUURUUIXFXHXPXIVDVFVGVHUULUUNBXRXSUUHUUNBVBZUUIUUKUUEUUTXJUUE
      CXGVAZUUNBWSUVAUUNVBZWTXBWSUUMUKBUDXGUUMVBUVBBXHUUFVIUUMUKBVJXGUUMCVKVLVM
      WTXBUVABVBZWSCVNZXBUVCWTCBVOWTYJYJUTZUKCUDUVDCYJUUGVIUVEUKCVPVQVRVSVTVEVM
      WAVTWBWCWDWEWFWCWGWDWHWIWJWKXAUUBYIWLZXBWRWSUVFWTMKLNABXIDEXFXHXFUNZGUUFH
      WMWNVEXAUUCUUAWLZXBWRWTUVHWSMKLNACXIDFXFYJUVGGUUGIWMWOVEWPWQ $.
      $( [20-Nov-2007] $) $( [29-May-2007] $)
  $}

  ${
    $d w x y z C $.  $d w x y z D $.  $d w x y z F $.  $d w x y z B $.
    $d w x y z J $.  $d w x y z K $.  $d x y L $.  $d w x y z X $.
    metcnss2.1 $e |- X = dom dom B $.
    metcnss2.2 $e |- J = ( Open ` B ) $.
    metcnss2.3 $e |- K = ( Open ` C ) $.
    metcnss2.4 $e |- L = ( Open ` D ) $.
    $( Subset relationship for continuity of metric spaces. $)
    metcnss2 $p |- ( ( ( B e. Met /\ C e. Met /\ D e. Met ) /\ ( B C_ C
                 /\ F e. ( K Cn L ) ) ) -> ( F |` X ) e. ( J Cn L ) ) $=
      ( vy vz vx vw cme wcel w3a wss ccn co cres wa cdm wf cc0 cv clt wbr cfv
      wi wral cr wrex fssres eqid metss sylan2 expcom adantl ssralv syl
      ad2antlr cxp wceq metf fdm reseq2 3syl ad2antrr wfun funssres ffun sylan
      adantll eqtr3d opreqd oprvres breq1d wb fvres opreqan12d imbi12d biimprd
      r19.20dva syld anim2d r19.22sdv imim2d r19.20sdv anim12d 3adantl3 metcn
      3adant1 adantr 3adant2 3imtr4d impr ) AQRZBQRZCQRZSZABTZDFGUAUBRZDHUCZEGU
      AUBRZXCXDUDBUEUEZCUEUEZDUFZUGMUHZUIUJZUGNUHZUIUJZOUHZPUHZBUBZXMUIUJZXODUK
      ZXPDUKZCUBZXKUIUJZULZPXHUMZUDZNUNUOZULZMUNUMZOXHUMZUDZHXIXFUFZXLXNXOXPAUB
      ZXMUIUJZXOXFUKZXPXFUKZCUBZXKUIUJZULZPHUMZUDZNUNUOZULZMUNUMZOHUMZUDZXEXGWT
      XAXDYJUUEULXBWTXAUDZXDUDZXJYKYIUUDXDXJYKULUUFXJXDYKXJHXHTZYKXDXHXIHDUPABH
      XHIXHUQZURZUSUTVAUUGYIYHOHUMZUUDXDYIUUKULZUUFXDUUHUULUUJYHOHXHVBVCVAUUGYH
      UUCOHUUGXOHRZUDZYGUUBMUNUUNYFUUAXLUUNYEYTNUNUUNYDYSXNUUNYDYCPHUMZYSXDYDUU
      OULZUUFUUMXDUUHUUPUUJYCPHXHVBVCVDUUNYCYRPHUUNXPHRZUDZYRYCUURYMXRYQYBUURYL
      XQXMUIUURXOXPBHHVEZUCZUBZYLXQUUGUVAYLVFUUMUUQUUGUUTAXOXPUUGBAUEZUCZUUTAWT
      UVCUUTVFZXAXDWTUUSUNAUFUVBUUSVFUVDAHIVGUUSUNAVHUVBUUSBVIVJVKXAXDUVCAVFZWT
      BVLZXDUVEXABAVMXAXHXHVEZUNBUFUVFBXHUUIVGUVGUNBVNVCVOVPVQVRVKUUMUUQUVAXQVF
      UUGXOXPHHBVSVPVQVTUUMUUQYQYBWAUUGUUMUUQUDYPYAXKUIUUMUUQYNXSYOXTCXOHDWBXPH
      DWBWCVTVPWDWEWFWGWHWIWJWKWFWGWLWMXCXEYJWAZXDXAXBUVHWTOMNPBCDFGXHXIUUIKXIU
      QZLWNWOWPXCXGUUEWAZXDWTXBUVJXAOMNPACXFEGHXIIJUVILWNWQWPWRWS $.
      $( [5-Dec-2007] $) $( [23-Aug-2007] $)
  $}

  ${
    metidcn.1 $e |- X = dom dom C $.
    metidcn.3 $e |- J = ( Open ` C ) $.
    metidcn.4 $e |- K = ( Open ` D ) $.
    $( The identity function is continuous (metric space version of
       ~ idcn ). $)
    metidcn $p |- ( ( C e. Met /\ D e. Met /\ C C_ D ) ->
       ( _I |` X ) e. ( J Cn K ) ) $=
      ( cme wcel wss w3a ccn co cid cres wa metcnss id 3anidm12 sylan 3impa
      cuni wceq uniopn2 reseq2 syl ctop opntop eqid idcn eqeltrrd 3ad2ant1
      sseldd ) AIJZBIJZABKZLCCMNZCDMNZOEPZUOUPUQURUSKZUOUOUPLZUQVAUOUPQAABCCDGG
      HRUOUPVBVBSTUAUBUOUPUTURJUQUOOCUCZPZUTURUOVCEUDVDUTUDACEFGUEVCEOUFUGUOCUH
      JVDURJACGUICVCVCUJUKUGULUMUN $.
      $( [3-Dec-2007] $) $( [23-Aug-2007] $)
  $}

  ${
    metdnsconst.1 $e |- X = dom dom C $.
    metdnsconst.2 $e |- Y = dom dom D $.
    metdnsconst.3 $e |- J = ( Open ` C ) $.
    metdnsconst.4 $e |- K = ( Open ` D ) $.
    $( If a continuous mapping to a metric space is constant on a dense subset,
       it is constant on the entire space (metric space version of
       ~ dnsconst ). $)
    metdnsconst $p |- ( ( ( C e. Met /\ D e. Met /\ F e. ( J Cn K ) ) /\
             ( P e. Y /\ A C_ ( `' F " { P } ) /\
                 ( ( cls ` J ) ` A ) = X ) ) -> F : X --> { P } ) $=
      ( cme wcel ccn co w3a ccnv csn cima wss ccl cfv wceq wf cuni ctop cha wi
      eqid dnsconst ex opntop 3ad2ant1 methaus 3ad2ant2 3simp3 syl3anc wb wa
      uniopn2 adantl eleq2d adantr eqeq2d 3anbi13d 3adant3 feq2d 3imtr3d imp )
      BNOZCNOZEFGPQOZRZDIOZAESDTZUAUBZAFUCUDUDZHUEZRZHVQEUFZVODGUGZOZVRVSFUGZUE
      ZRZWEVQEUFZWAWBFUHOZGUIOZVNWGWHUJVOWIWJVNRWGWHADEFGWEWCWEUKWCUKULUMVLVMWI
      VNBFLUNUOVMVLWJVNCGMUPUQVLVMVNURUSVLVMWGWAUTVNVLVMVAZWDVPWFVTVRWKWCIDVMWC
      IUEVLCGIKMVBVCVDWKWEHVSVLWEHUEVMBFHJLVBZVEVFVGVHVLVMWHWBUTVNVLWEHVQEWLVIU
      OVJVK $.
      $( [6-Dec-2007] $) $( [24-Aug-2007] $)
  $}


