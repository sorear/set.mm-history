$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       Continuity in metric spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    metcnf.1 $e |- X = dom dom C $.
    metcnf.2 $e |- Y = dom dom D $.
    metcnf.3 $e |- J = ( MetOpen ` C ) $.
    metcnf.4 $e |- K = ( MetOpen ` D ) $.
    $( A continuous function at point ` P ` is a mapping (metric space version
       of ~ cnpf ). $)
    metcnpf $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\
                F e. ( ( J CnP K ) ` P ) ) -> F : X --> Y ) $=
      ( cme wcel w3a wf cuni ctop opntop uniopn2 ccnp co cfv wa 3ad2ant1 eleq2d
      3ad2ant2 biimpa 3adant2 3jca eqid cnpf sylan feq2d wceq feq3 syl sylan9bb
      wb 3adant3 adantr mpbird ) AMNZBMNZCGNZOZDCEFUAUBUCNZUDGHDPZEQZFQZDPZVFER
      NZFRNZCVINZOVGVKVFVLVMVNVCVDVLVEAEKSUEVDVCVMVEBFLSUGVCVEVNVDVCVEVNVCGVICA
      EGIKTZUFUHUIUJCDEFVIVJVIUKVJUKULUMVFVHVKUSZVGVCVDVPVEVCVHVIHDPZVDVKVCGVIH
      DVOUNVDHVJUOVQVKUSBFHJLTHVJVIDUPUQURUTVAVB $.
      $( [16-Dec-2007] $)

    $( A continuous function is a mapping (metric space version of ~ cnf ). $)
    metcnf $p |- ( ( C e. Met /\ D e. Met /\
                F e. ( J Cn K ) ) -> F : X --> Y ) $=
      ( cme wcel wf cuni ctop opntop eqid wb uniopn2 ccn co w3a id syl3an feq2d
      cnf wceq feq3 syl sylan9bb 3adant3 mpbird ) ALMZBLMZCDEUAUBMZUCFGCNZDOZEO
      ZCNZUNDPMUOEPMUPUPUTADJQBEKQUPUDCDEURUSURRUSRUGUEUNUOUQUTSUPUNUQURGCNZUOU
      TUNFURGCADFHJTUFUOGUSUHVAUTSBEGIKTGUSURCUIUJUKULUM $.
      $( [1-Aug-2007] $)

    $( A constant function is continuous (metric space version of
       ~ cnconst ). $)
    metcnconst $p |- ( ( ( C e. Met /\ D e. Met ) /\ ( B e. Y /\
             F : X --> { B } ) ) -> F e. ( J Cn K ) ) $=
      ( cme wcel wa wf ctop cuni opntop uniopn2 csn ccn co anim12i adantr feq2d
      eleq2d bi2anan9r biimpa eqid cnconst syl11anc ) BMNZCMNZOZAHNZGAUAZDPZOZO
      EQNZFQNZOZAFRZNZERZUQDPZOZDEFUBUCNUOVBUSUMUTUNVABEKSCFLSUDUEUOUSVGUNUPVDU
      MURVFUNHVCACFHJLTUGUMGVEUQDBEGIKTUFUHUIADEFVEVCVEUJVCUJUKUL $.
      $( [10-Aug-2007] $)
  $}

  ${
    $d w x y z F $.  $d w x y z C $.  $d w x y z D $.  $d w x y z X $.
    $d w x y z Y $.  $d w x y z P $.  $d w x y z J $.  $d w x y K $.
    metcn.1 $e |- X = dom dom C $.
    metcn.2 $e |- J = ( MetOpen ` C ) $.
    metcn.3 $e |- Y = dom dom D $.
    metcn.4 $e |- K = ( MetOpen ` D ) $.
    $( MAY BE REVISED - avoid funimass3 here to simplify metcnp2 , and also
       use iscnp2 for metcnp $)
    $( Lemma for ~ metcnp . $)
    metcnplem $p |- ( ( ( C e. Met /\ D e. Met ) /\ ( F : X --> Y /\ P e. X ) )
     -> ( A. y e. RR+ E. z e. RR+ ( P ( ball ` C ) z ) C_
    ( `' F " ( ( F ` P ) ( ball ` D ) y ) ) <-> A. y e. RR+
      E. z e. RR+ A. w e. X ( ( P C w ) < z ->
    ( ( F ` P ) D ( F ` w ) ) < y ) ) ) $=
      ( vx wcel wa co crp cme wf cv cbl cfv ccnv cima wss wrex clt wi wral wfun
      wbr wb cdm simplrl ffun syl simplll simplrr blssm syl111anc wceq sseqtr4d
      simprr fdm funimass5 syl11anc w3a crab blval raleqdv opreq2 breq1d ralrab
      syl6bb ffvelrn ex simpllr simprl elbl2 expr syl21anc syld imbi2d ralbidva
      imp 3bitrd anassrs rexbidva ) DUAQZEUAQZRZJKGUBZFJQZRZRZFBUCZDUDUESZGUFFG
      UEZAUCZEUDUESZUGUHZBTUIFCUCZDSZWSUJUNZXAXEGUEZESXBUJUNZUKZCJULZBTUIATWRXB
      TQZRXDXKBTWRXLWSTQZXDXKUOWRXLXMRZRZXDXHXCQZCWTULZXGXPUKZCJULZXKXOGUMZWTGU
      PZUHXDXQUOXOWOXTWNWOWPXNUQZJKGURUSXOWTJYAXOWLWPXMWTJUHWLWMWQXNUTZWNWOWPXN
      VAZWRXLXMVFZDFWSJLVBVCXOWOYAJVDYBJKGVGUSVECWTXCGVHVIXOWLWPXMXQXSUOYCYDYEW
      LWPXMVJZXQXPCFPUCZDSZWSUJUNZPJVKZULXSYFXPCWTYJPDFWSJLVLVMYIXGXPCPJYGXEVDY
      HXFWSUJYGXEFDVNVOVPVQVCXOXRXJCJXOXEJQZRXPXIXGXOYKXPXIUOZXOYKXHKQZYLXOWOYK
      YMUKYBWOYKYMJKXEGVRVSUSXOWMXLXAKQZYMYLUKWLWMWQXNVTWRXLXMWAXOWOWPYNYBYDJKF
      GVRVIWMXLRYNYMYLXHEXAXBKNWBWCWDWEWHWFWGWIWJWKWG $.
      $( [12-Nov-2013] $) $( [12-May-2007] $)

    $( Two ways to say a mapping from metric ` C ` to metric ` D ` is
       continuous at point ` P ` . _Warning:  The HTML proof page is 0.6
       megabyte in size_. $)
    metcnp $p |- ( ( C e. Met /\ D e. Met /\ P e. X ) ->
                ( F e. ( ( J CnP K ) ` P ) <->
          ( F : X --> Y /\ A. y e. RR+ E. z e. RR+
      A. w e. X ( ( P C w ) < z -> ( ( F ` P ) D ( F ` w ) ) < y ) ) ) ) $=
      ( vx wcel wa wi crp cme w3a ccnp co cfv wf cv clt wbr wral wrex cuni ctop
      opntop 3ad2ant1 3ad2ant2 simp3 wceq uniopn2 eleqtrd eqid syl111anc feq23d
      cnpf ex sylibrd pm4.71rd ccnv cima wss cbl wb adantr eleq2d biimpa iscnp2
      3adant2 biantrurd simpll2 simpr simpl3 ffvelrn simprr blopn simprl blcntr
      eleq2 imaeq2 sseq2d anbi2d rexbidv imbi12d rcla4v 3imp simpll1 opni2 sstr
      syl11anc expcom reximdv syl5com 3exp imp4a rexlimdv syl anassrs ralrimiva
      mpd simp1r syl3an1 r19.29 simp1ll imass2 sstr2 syl5 sseq1 anbi12d rcla4ev
      expr syl9r 3expa rexlimdva imp3a ralrimiv impbida bitr3d simpl1 metcnplem
      simpl2 syl22anc 3bitrd pm5.32da bitrd ) DUAQZEUAQZFJQZUBZGFHIUCUDUEQZJKGU
      FZYRRYSFCUGZDUDBUGZUHUIFGUEZYTGUEEUDAUGZUHUISCJUJBTUKATUJZRYQYRYSYQYRHULZ
      IULZGUFZYSYQHUMQZIUMQZFUUEQZYRUUGSYNYOUUHYPDHMUNUOZYOYNUUIYPEIOUNUPZYQFJU
      UEYNYOYPUQYNYOJUUEURYPDHJLMUSZUOZUTUUHUUIUUJUBYRUUGFGHIUUEUUFUUEVAZUUFVAZ
      VDVEVBYQJKUUEUUFGUUNYOYNKUUFURYPEIKNOUSUPVCZVFVGYQYSYRUUDYQYSRZYRUUGUUBYT
      QZFPUGZQZUUTGVHZYTVIZVJZRZPHUKZSZCIUJZRZFUUADVKUEUDZUVBUUBUUCEVKUEUDZVIZV
      JZBTUKZATUJZUUDUURUUHUUIUUJYRUVIVLYQUUHYSUUKVMYQUUIYSUULVMYQUUJYSYNYPUUJY
      OYNYPUUJYNJUUEFUUMVNVOVQVMPCFGHIUUEUUFUUOUUPVPVBUURUVHUVIUVOUURUUGUVHYQYS
      UUGUUQVOVRUURUVHUVOUURUVHRUVNATUURUVHUUCTQZUVNUURUVHUVPRZRZUVAUUTUVLVJZRZ
      PHUKZUVNUVRUVKIQZUVHUUBUVKQZUWAUVRYOUUBKQZUVPUWBYNYOYPYSUVQVSZUURUWDUVQUU
      RYSYPUWDYQYSVTZYNYOYPYSWAZJKFGWBWRVMZUURUVHUVPWCZEUUBUUCIKNOWDVBUURUVHUVP
      WEUVRYOUWDUVPUWCUWEUWHUWIEUUBUUCKNWFVBUWBUVHUWCUWAUVGUWCUWASCUVKIYTUVKURZ
      UUSUWCUVFUWAYTUVKUUBWGUWJUVEUVTPHUWJUVDUVSUVAUWJUVCUVLUUTYTUVKUVBWHWIWJWK
      WLWMWNVBUVRYNUWAUVNSYNYOYPYSUVQWOYNUVTUVNPHYNUUTHQZUVAUVSUVNYNUWKUVAUVSUV
      NSYNUWKUVAUBUVJUUTVJZBTUKUVSUVNBUUTDFHMWPUVSUWLUVMBTUWLUVSUVMUVJUUTUVLWQW
      SWTXAXBXCXDXEXHXFXGUURUVORZUVGCIUWMYTIQZUUSUVFUWMUWNUUSUBZUVNUVKYTVJZRZAT
      UKZUVFUWOUVOUWPATUKZUWRUURUVOUWNUUSXIUWMYOUWNUUSUWSYNYOYPYSUVOVSAYTEUUBIO
      WPXJUVNUWPATXKWRUWOYQUWRUVFSZYQYSUVOUWNUUSXLYNYPUWTYOYNYPRZUWQUVFATUXAUWQ
      UVFSUVPUXAUVNUWPUVFUXAUVMUWPUVFSZBTYNYPUUATQZUVMUXBSUVMUWPUVJUVCVJZYNYPUX
      CUBZUVFUWPUVLUVCVJUVMUXDUVKYTUVBXMUVJUVLUVCXNXOUXEUVJHQZFUVJQZUXDUVFSDFUU
      AHJLMWDDFUUAJLWFUXFUXGUXDUVFUVEUXGUXDRPUVJHUUTUVJURUVAUXGUVDUXDUUTUVJFWGU
      UTUVJUVCXPXQXRXSWRXTYAYBYCVMYBVQXEXHXBYDYEYFUURYNYOYSYPUVOUUDVLYNYOYPYSYG
      YNYOYPYSYIUWFUWGABCDEFGHIJKLMNOYHYJYKYLYM $.
      $( [12-Nov-2013] $) $( [11-May-2007] $)

    $( Two ways to say a mapping from metric ` C ` to metric ` D ` is
       continuous at point ` P ` .  The distance arguments are swapped compared
       to ~ metcnp (and Munkres' ~ metcn ) for compatibility with ~ df-lm .
       Definition 1.3-3 of [Kreyszig] p. 20. $)
    metcnp2 $p |- ( ( C e. Met /\ D e. Met /\ P e. X ) ->
                ( F e. ( ( J CnP K ) ` P ) <->
          ( F : X --> Y /\ A. y e. RR+ E. z e. RR+
      A. w e. X ( ( w C P ) < z -> ( ( F ` w ) D ( F ` P ) ) < y ) ) ) ) $=
      ( wcel co clt crp wa cme w3a ccnp cfv wf cv wi wral wrex metcnp wb metsym
      wbr 3expa 3adantl2 breq1d adantlr simpll2 simplr simpll3 ffvelrn syl11anc
      wceq adantll syl111anc imbi12d ralbidva rexbidv ralbidv pm5.32da bitrd )
      DUAPZEUAPZFJPZUBZGFHIUCQUDPJKGUEZFCUFZDQZBUFZRUMZFGUDZVQGUDZEQZAUFZRUMZUG
      ZCJUHZBSUIZASUHZTVPVQFDQZVSRUMZWBWAEQZWDRUMZUGZCJUHZBSUIZASUHZTABCDEFGHIJ
      KLMNOUJVOVPWIWQVOVPTZWHWPASWRWGWOBSWRWFWNCJWRVQJPZTZVTWKWEWMVOWSVTWKUKVPV
      OWSTVRWJVSRVLVNWSVRWJVCZVMVLVNWSXAFVQDJLULUNUOUPUQWTWCWLWDRWTVMWAKPZWBKPZ
      WCWLVCVLVMVNVPWSURWTVPVNXBVOVPWSUSVLVMVNVPWSUTJKFGVAVBVPWSXCVOJKVQGVAVDWA
      WBEKNULVEUPVFVGVHVIVJVK $.
      $( [13-Nov-2013] $) $( [4-Jun-2007] $)

    $( Two ways to say a mapping from metric ` C ` to metric ` D ` is
       continuous.  Theorem 10.1 of [Munkres] p. 127.  The second biconditional
       argument says that for every positive "epsilon" ` y ` there is a
       positive "delta" ` z ` such that a distance less than delta in ` C `
       maps to a distance less than epsilon in ` D ` . $)
    metcn $p |- ( ( C e. Met /\ D e. Met ) ->
                ( F e. ( J Cn K ) <-> ( F : X --> Y /\
                A. x e. X A. y e. RR+ E. z e. RR+
      A. w e. X ( ( x C w ) < z -> ( ( F ` x ) D ( F ` w ) ) < y ) ) ) ) $=
      ( wcel wa co cv wral cme ccn wf clt wbr cfv wi wrex cuni ctop opntop eqid
      crp cnf 3expia syl2an wb uniopn2 feq23 sylibrd pm4.71rd ad2antrr ad2antlr
      wceq biimpa cncnp syl111anc raleqdv metcnp 3expa adantlr simplr biantrurd
      ccnp bitr4d ralbidva 3bitr2d pm5.32da bitrd ) EUAPZFUAPZQZGHIUBRPZJKGUCZW
      CQWDASZDSZERCSUDUEWEGUFWFGUFFRBSUDUEUGDJTCUMUHBUMTZAJTZQWBWCWDWBWCHUIZIUI
      ZGUCZWDVTHUJPZIUJPZWCWKUGWAEHMUKZFIOUKZWLWMWCWKGHIWIWJWIULZWJULZUNUOUPVTJ
      WIVDZKWJVDWDWKUQWAEHJLMURZFIKNOURJKWIWJGUSUPZUTVAWBWDWCWHWBWDQZWCGWEHIVNR
      UFPZAWITZXBAJTWHXAWLWMWKWCXCUQVTWLWAWDWNVBWAWMVTWDWOVCWBWDWKWTVEAGHIWIWJW
      PWQVFVGXAXBAJWIVTWRWAWDWSVBVHXAXBWGAJXAWEJPZQZXBWDWGQZWGWBXDXBXFUQZWDVTWA
      XDXGBCDEFWEGHIJKLMNOVIVJVKXEWDWGWBWDXDVLVMVOVPVQVRVS $.
      $( [13-Nov-2013] $) $( [15-May-2007] $)

    ${
      $d w x y z A $.  $d x y w z C $.  $d x y w z D $.  $d w z K $.
      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a continuous metric space function, with
         function arguments as in ~ metcnp . $)
      metcnpi $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\ ( F e.
           ( ( J CnP K ) ` P ) /\ A e. RR+ ) ) -> E. x e. RR+
        A. y e. X ( ( P C y ) < x -> ( ( F ` P ) D ( F ` y ) ) < A ) ) $=
        ( vz wcel co crp clt cme w3a ccnp cfv cv wbr wi wral wrex metcnp biimpa
        wa wf simprd wceq breq2 imbi2d ralbidv rexbidv rcla4cva sylan anasss )
        DUAQEUAQFJQUBZGFHIUCRUDQZCSQZFBUEZDRAUETUFZFGUDVFGUDERZCTUFZUGZBJUHZASU
        IZVCVDULZVGVHPUEZTUFZUGZBJUHZASUIZPSUHZVEVLVMJKGUMZVSVCVDVTVSULPABDEFGH
        IJKLMNOUJUKUNVRVLPCSVNCUOZVQVKASWAVPVJBJWAVOVIVGVNCVHTUPUQURUSUTVAVB $.
        $( [13-Nov-2013] $) $( [17-Dec-2007] $)

      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a continuous metric space function, with
         swapped distance function arguments as in ~ metcnp2 . $)
      metcnpi2 $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\ ( F e.
           ( ( J CnP K ) ` P ) /\ A e. RR+ ) ) -> E. x e. RR+
        A. y e. X ( ( y C P ) < x -> ( ( F ` y ) D ( F ` P ) ) < A ) ) $=
        ( vz wcel co crp clt cme w3a ccnp cfv cv wbr wi wral wrex wa wf metcnp2
        biimpa simprd wceq breq2 imbi2d ralbidv rexbidv rcla4cva sylan anasss )
        DUAQEUAQFJQUBZGFHIUCRUDQZCSQZBUEZFDRAUETUFZVFGUDFGUDERZCTUFZUGZBJUHZASU
        IZVCVDUJZVGVHPUEZTUFZUGZBJUHZASUIZPSUHZVEVLVMJKGUKZVSVCVDVTVSUJPABDEFGH
        IJKLMNOULUMUNVRVLPCSVNCUOZVQVKASWAVPVJBJWAVOVIVGVNCVHTUPUQURUSUTVAVB $.
        $( [13-Nov-2013] $) $( [16-Dec-2007] $)

      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a metric space function continuous at
         ` P ` .  A variation of ~ metcnpi2 with non-strict ordering. $)
      metcnpi3 $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\ ( F e.
           ( ( J CnP K ) ` P ) /\ A e. RR+ ) ) -> E. x e. RR+
        A. y e. X ( ( y C P ) <_ x -> ( ( F ` y ) D ( F ` P ) ) <_ A ) ) $=
        ( wcel crp wa wbr syl vz cme w3a ccnp co cfv clt wral wrex cle metcnpi2
        cv wi c2 cdiv cr cc0 rehalfcl rpgt0 wb halfpos2 mpbid sylanbrc ad2antrl
        rpre elrp simpll1 simprr simpll3 metcl syl111anc halfpos exp3acom23 imp
        lelttr syl31anc simpll2 wf cuni opntop 3ad2ant1 3ad2ant2 uniopn2 eleq2d
        ctop biimpa 3adant2 3jca eqid sylan simpl1 simpl2 feq23d mpbird adantrr
        cnpf wceq adantr ffvelrn syl11anc simplrr ltle imim12d anassrs ralimdva
        impr breq2 imbi1d ralbidv rcla4ev expr rexlimdva mpd ) DUBPZEUBPZFJPZUC
        ZGFHIUDUEUFPZCQPZRZRZBULZFDUEZUAULZUGSZYBGUFZFGUFZEUEZCUGSZUMZBJUHZUAQU
        IYCAULZUJSZYHCUJSZUMZBJUHZAQUIZUABCDEFGHIJKLMNOUKYAYKYQUAQYAYDQPZYKYQYA
        YRYKRRYDUNUOUEZQPZYCYSUJSZYNUMZBJUHZYQYRYTYAYKYRYSUPPZUQYSUGSZYTYRYDUPP
        ZUUDYDVEZYDURZTYRUQYDUGSZUUEYDUSZYRUUFUUIUUEUTUUGYDVATVBYSVFVCVDYAYRYKU
        UCYAYRRYJUUBBJYAYRYBJPZYJUUBUMYAYRUUKRZRZUUAYEYIYNUUMYCUPPZUUDUUFYSYDUG
        SZUUAYEUMZUUMXNUUKXPUUNXNXOXPXTUULVGYAYRUUKVHZXNXOXPXTUULVIZYBFDJLVJVKU
        UMUUFUUDYRUUFYAUUKUUGVDZUUHTUUSYRUUOYAUUKYRUUIUUOUUJYRUUFUUIUUOUTUUGYDV
        LTVBVDUUNUUDUUFUCZUUOUUPUUTUUAUUOYEYCYSYDVOVMVNVPUUMYHUPPZCUPPZYIYNUMUU
        MXOYFKPZYGKPZUVAXNXOXPXTUULVQUUMJKGVRZUUKUVCYAUVEUULXQXRUVEXSXQXRRZUVEH
        VSZIVSZGVRZXQHWEPZIWEPZFUVGPZUCXRUVIXQUVJUVKUVLXNXOUVJXPDHMVTWAXOXNUVKX
        PEIOVTWBXNXPUVLXOXNXPUVLXNJUVGFDHJLMWCZWDWFWGWHFGHIUVGUVHUVGWIUVHWIWPWJ
        UVFJKUVGUVHGUVFXNJUVGWQXNXOXPXRWKUVMTUVFXOKUVHWQXNXOXPXRWLEIKNOWCTWMWNW
        OWRZUUQJKYBGWSWTUUMUVEXPUVDUVNUURJKFGWSWTYFYGEKNVJVKUUMXSUVBXQXRXSUULXA
        CVETYHCXBWTXCXDXEXFYPUUCAYSQYLYSWQZYOUUBBJUVOYMUUAYNYLYSYCUJXGXHXIXJWTX
        KXLXM $.
        $( [13-Nov-2013] $) $( [16-Dec-2007] $)

      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a metric space function continuous at
         ` P ` .  A variation of ~ metcnpi with non-strict ordering. $)
      metcnpi4 $p |- ( ( ( C e. Met /\ D e. Met /\ P e. X ) /\ ( F e.
           ( ( J CnP K ) ` P ) /\ A e. RR+ ) ) -> E. x e. RR+
        A. y e. X ( ( P C y ) <_ x -> ( ( F ` P ) D ( F ` y ) ) <_ A ) ) $=
        ( wcel co crp wa cle cme w3a ccnp cfv cv wbr wi wral wrex metcnpi3 wceq
        simpll1 simpll3 metsym syl111anc breq1d simpll2 wf cuni opntop 3ad2ant1
        simpr ctop 3ad2ant2 uniopn2 eleq2d biimpa 3adant2 3jca eqid cnpf feq23d
        sylan wb adantr mpbird adantrr ffvelrn simpl3 syl11anc imbi12d ralbidva
        rexbidv mpbid ) DUAPZEUAPZFJPZUBZGFHIUCQUDPZCRPZSZSZBUEZFDQZAUEZTUFZWMG
        UDZFGUDZEQZCTUFZUGZBJUHZARUIFWMDQZWOTUFZWRWQEQZCTUFZUGZBJUHZARUIABCDEFG
        HIJKLMNOUJWLXBXHARWLXAXGBJWLWMJPZSZWPXDWTXFXJWNXCWOTXJWEXIWGWNXCUKWEWFW
        GWKXIULWLXIVBWEWFWGWKXIUMWMFDJLUNUOUPXJWSXECTXJWFWQKPZWRKPZWSXEUKWEWFWG
        WKXIUQWLJKGURZXIXKWHWIXMWJWHWISXMHUSZIUSZGURZWHHVCPZIVCPZFXNPZUBWIXPWHX
        QXRXSWEWFXQWGDHMUTVAWFWEXRWGEIOUTVDWEWGXSWFWEWGXSWEJXNFDHJLMVEZVFVGVHVI
        FGHIXNXOXNVJXOVJVKVMWHXMXPVNWIWHJKXNXOGWEWFJXNUKWGXTVAWFWEKXOUKWGEIKNOV
        EVDVLVOVPVQZJKWMGVRVMWLXLXIWLXMWGXLYAWEWFWGWKVSJKFGVRVTVOWQWREKNUNUOUPW
        AWBWCWD $.
        $( [13-Nov-2013] $) $( [17-Dec-2007] $)

      $( Epsilon-delta property of a continuous metric space function. $)
      metcni $p |- ( ( ( C e. Met /\ D e. Met /\
                  F e. ( J Cn K ) ) /\
           ( P e. X /\ A e. RR+ ) ) -> E. x e. RR+
        A. y e. X ( ( P C y ) < x -> ( ( F ` P ) D ( F ` y ) ) < A ) ) $=
        ( cme wcel co cfv syl ccn w3a crp wa ccnp cv clt wbr wral simpl1 simpl2
        wi wrex simprl ctop cuni opntop simpl3 wceq uniopn2 eleqtrd eqid cncnpi
        syl22anc simprr metcnpi syl32anc ) DPQZEPQZGHIUARQZUBZFJQZCUCQZUDZUDZVH
        VIVLGFHIUERSQZVMFBUFZDRAUFUGUHFGSVQGSERCUGUHULBJUIAUCUMVHVIVJVNUJZVHVIV
        JVNUKZVKVLVMUNZVOHUOQZIUOQZVJFHUPZQVPVOVHWAVRDHMUQTVOVIWBVSEIOUQTVHVIVJ
        VNURVOFJWCVTVOVHJWCUSVRDHJLMUTTVAFGHIWCIUPZWCVBWDVBVCVDVKVLVMVEABCDEFGH
        IJKLMNOVFVG $.
        $( [13-Nov-2013] $) $( [3-Aug-2007] $)

      $( MAY BE REVISED - delete this if not used $)
      $( Epsilon-delta property of a continuous metric space function. $)
      metcni2 $p |- ( ( ( C e. Met /\ D e. Met /\
                  F e. ( J Cn K ) ) /\
           ( P e. X /\ A e. RR+ ) ) -> E. x e. RR+
        A. y e. X ( ( P C y ) <_ x -> ( ( F ` P ) D ( F ` y ) ) <_ A ) ) $=
        ( cme wcel co cfv syl ccn w3a crp wa ccnp cv cle wbr wral simpl1 simpl2
        wi wrex simprl ctop cuni opntop simpl3 wceq uniopn2 eleqtrd eqid cncnpi
        syl22anc simprr metcnpi4 syl32anc ) DPQZEPQZGHIUARQZUBZFJQZCUCQZUDZUDZV
        HVIVLGFHIUERSQZVMFBUFZDRAUFUGUHFGSVQGSERCUGUHULBJUIAUCUMVHVIVJVNUJZVHVI
        VJVNUKZVKVLVMUNZVOHUOQZIUOQZVJFHUPZQVPVOVHWAVRDHMUQTVOVIWBVSEIOUQTVHVIV
        JVNURVOFJWCVTVOVHJWCUSVRDHJLMUTTVAFGHIWCIUPZWCVBWDVBVCVDVKVLVMVEABCDEFG
        HIJKLMNOVFVG $.
        $( [13-Nov-2013] $) $( [14-Dec-2007] $)
    $}

    $( Two ways to express that ` F ` is continuous at ` P ` for metric
       spaces.  Proposition 14-4.2 of [Gleason] p. 240. $)
    metcnp3 $p |- ( ( C e. Met /\ D e. Met /\ P e. X )
    -> ( F e. ( ( J CnP K ) ` P ) <-> ( F : X --> Y /\
    A. y e. RR+ E. z e. RR+ ( F " ( P ( ball ` C ) z ) )
    C_ ( ( F ` P ) ( ball ` D ) y ) ) ) ) $=
      ( vx wcel co cfv crp wa cme w3a ccnp wf cv clt wbr wral wrex cbl cima wss
      wi metcnp wb ccnv wfun cdm simplrl ffun syl simplll simpr blssm syl111anc
      simplrr wceq sseqtr4d funimass3 syl11anc rexbidva ralbidv metcnplem bitrd
      fdm ancom2s anassrs pm5.32da 3impa bitr4d ) CUAPZDUAPZEIPZUBFEGHUCQRPIJFU
      DZEOUEZCQBUEZUFUGEFRZWEFRDQAUEZUFUGUMOIUHBSUIASUHZTZWDFEWFCUJRQZUKWGWHDUJ
      RQZULZBSUIZASUHZTZABOCDEFGHIJKLMNUNWAWBWCWPWJUOWAWBTZWCTWDWOWIWQWCWDWOWIU
      OZWQWDWCWRWQWDWCTZTZWOWKFUPWLUKULZBSUIZASUHWIWTWNXBASWTWMXABSWTWFSPZTZFUQ
      ZWKFURZULWMXAUOXDWDXEWQWDWCXCUSZIJFUTVAXDWKIXFXDWAWCXCWKIULWAWBWSXCVBWQWD
      WCXCVFWTXCVCCEWFIKVDVEXDWDXFIVGXGIJFVOVAVHWKWLFVIVJVKVLABOCDEFGHIJKLMNVMV
      NVPVQVRVSVT $.
      $( [13-Nov-2013] $) $( [17-May-2007] $)
  $}

  ${
    $d f w x y z B $.  $d f w x y z C $.  $d f w x y z D $.  $d f w x y z J $.
    $d f w x y K $.  $d f w x y L $.
    metcnco.1 $e |- J = ( MetOpen ` B ) $.
    metcnco.2 $e |- K = ( MetOpen ` C ) $.
    metcnco.3 $e |- L = ( MetOpen ` D ) $.
    $( Composition of two continuous functions (metric space version of
       ~ cnco ). $)
    metcnco $p |- ( ( ( B e. Met /\ C e. Met /\ D e. Met ) /\ (
        F e. ( J Cn K ) /\ G e. ( K Cn L ) ) ) -> ( G o. F ) e. ( J Cn L ) ) $=
      ( cme wcel ctop ccn co wa ccom opntop cnco syl3anl ) ALMFNMBLMGNMCLMHNMDF
      GOPMEGHOPMQEDRFHOPMAFISBGJSCHKSDEFGHTUA $.
      $( [6-Aug-2007] $)

    $( Subset relationship for continuity of metric spaces. $)
    metcnss $p |- ( ( ( B e. Met /\ C e. Met /\ D e. Met ) /\ C C_ D ) ->
      ( J Cn K ) C_ ( J Cn L ) ) $=
      ( vx vw vz vy wcel wa co cdm cv wral crp vf cme w3a wss ccn wf clt wbr wi
      cfv wrex wb cop wfun wceq cxp simpll3 eqid metf ffun 3syl ffvelrn adantrr
      simplr adantrl opelxpi syl11anc adantl simpll2 eleqtrrd funssfv syl111anc
      cr df-opr 3eqtr4g breq1d imbi2d anassrs ralbidva rexbidv ralbidv pm5.32da
      fdm dmss expcom anim1d sylbird simpl1 simpl2 metcn simpl3 3imtr4d ssrdv
      fss ) AUBNZBUBNZCUBNZUCZBCUDZOZUADEUEPZDFUEPZWTAQQZBQZQZUARZUFZJRZKRZAPLR
      UGUHZXHXFUJZXIXFUJZBPZMRZUGUHZUIZKXCSZLTUKZMTSZJXCSZOZXCCQZQZXFUFZXJXKXLC
      PZXNUGUHZUIZKXCSZLTUKZMTSZJXCSZOZXFXANZXFXBNZWTYAXGYKOYLWTXGYKXTWTXGOZYJX
      SJXCYOXHXCNZOZYIXRMTYQYHXQLTYQYGXPKXCYOYPXIXCNZYGXPULZWTXGYPYROZYSWTXGYTO
      ZOZYFXOXJUUBYEXMXNUGUUBXKXLUMZCUJZUUCBUJZYEXMUUBCUNZWSUUCXDNUUDUUEUOUUBWQ
      YCYCUPZVMCUFUUFWOWPWQWSUUAUQCYCYCURZUSUUGVMCUTVAWRWSUUAVDUUBUUCXEXEUPZXDU
      UAUUCUUINZWTUUAXKXENZXLXENZUUJXGYPUUKYRXCXEXHXFVBVCXGYRUULYPXCXEXIXFVBVEX
      KXLXEXEVFVGVHUUBWPUUIVMBUFXDUUIUOWOWPWQWSUUAVIBXEXEURZUSUUIVMBWCVAVJUUCCB
      VKVLXKXLCVNXKXLBVNVOVPVQVRVRVSVTWAVSWBWTXGYDYKWSXGYDUIZWRWSXDYBUDXEYCUDZU
      UNBCWDXDYBWDXGUUOYDXCXEYCXFWNWEVAVHWFWGWTWOWPYMYAULWOWPWQWSWHZWOWPWQWSWIJ
      MLKABXFDEXCXEXCURZGUUMHWJVGWTWOWQYNYLULUUPWOWPWQWSWKJMLKACXFDFXCYCUUQGUUH
      IWJVGWLWM $.
      $( [13-Nov-2013] $) $( [29-May-2007] $)
  $}

  ${
    $d w x y z C $.  $d w x y z D $.  $d w x y z F $.  $d w x y z B $.
    $d w x y z J $.  $d w x y z K $.  $d w x y L $.  $d w x y z X $.
    metcnss2.1 $e |- X = dom dom B $.
    metcnss2.2 $e |- J = ( MetOpen ` B ) $.
    metcnss2.3 $e |- K = ( MetOpen ` C ) $.
    metcnss2.4 $e |- L = ( MetOpen ` D ) $.
    $( Subset relationship for continuity of metric spaces. $)
    metcnss2 $p |- ( ( ( B e. Met /\ C e. Met /\ D e. Met ) /\ ( B C_ C
                 /\ F e. ( K Cn L ) ) ) -> ( F |` X ) e. ( J Cn L ) ) $=
      ( vx vw vz vy wcel co wa wral cme w3a wss ccn cres cdm wf clt wbr cfv crp
      cv wi wrex dmss syl adantl syl5eqss fssres expcom ssralv adantr wfun wceq
      wb cop cxp cr simpll2 eqid metf ffun 3syl simplr opelxpi simpll1 eleqtrrd
      fdm funssfv syl111anc df-opr 3eqtr4g breq1d bicomd opreq12 syl2an imbi12d
      fvres anassrs ralbidva sylibrd reximdv ralimdv syld anim12d simpl2 simpl3
      ralimdva metcn syl11anc simpl1 3imtr4d impr ) AUAQZBUAQZCUAQZUBZABUCZDFGU
      DRQZDHUEZEGUDRQZXGXHSZBUFZUFZCUFUFZDUGZMULZNULZBRZOULZUHUIZXQDUJZXRDUJZCR
      ZPULZUHUIZUMZNXNTZOUKUNZPUKTZMXNTZSZHXOXJUGZXQXRARZXTUHUIZXQXJUJZXRXJUJZC
      RZYEUHUIZUMZNHTZOUKUNZPUKTZMHTZSZXIXKXLXPYMYKUUDXLHXNUCZXPYMUMXLHAUFZUFZX
      NIXHUUHXNUCZXGXHUUGXMUCUUIABUOUUGXMUOUPUQURZXPUUFYMXNXOHDUSUTUPXLYKYJMHTZ
      UUDXLUUFYKUUKUMUUJYJMHXNVAUPXLYJUUCMHXLXQHQZSZYIUUBPUKUUMYHUUAOUKUUMYHYGN
      HTZUUAUUMUUFYHUUNUMXLUUFUULUUJVBYGNHXNVAUPUUMYTYGNHXLUULXRHQZYTYGVEXLUULU
      UOSZSZYOYAYSYFUUQYAYOUUQXSYNXTUHUUQXQXRVFZBUJZUURAUJZXSYNUUQBVCZXHUURUUGQ
      UUSUUTVDUUQXEXNXNVGZVHBUGUVAXDXEXFXHUUPVIBXNXNVJZVKUVBVHBVLVMXGXHUUPVNUUQ
      UURHHVGZUUGUUPUURUVDQXLXQXRHHVOUQUUQXDUVDVHAUGUUGUVDVDXDXEXFXHUUPVPAHIVKU
      VDVHAVRVMVQUURBAVSVTXQXRBWAXQXRAWAWBWCWDUUQYRYDYEUHUUPYRYDVDZXLUULYPYBVDY
      QYCVDUVEUUOXQHDWHXRHDWHYPYBYQYCCWEWFUQWCWGWIWJWKWLWMWRWNWOXLXEXFXIYLVEXDX
      EXFXHWPXDXEXFXHWQZMPONBCDFGXNXOUVCKXOVJZLWSWTXLXDXFXKUUEVEXDXEXFXHXAUVFMP
      ONACXJEGHXOIJUVGLWSWTXBXC $.
      $( [13-Nov-2013] $) $( [23-Aug-2007] $)
  $}

  ${
    metidcn.1 $e |- X = dom dom C $.
    metidcn.3 $e |- J = ( MetOpen ` C ) $.
    metidcn.4 $e |- K = ( MetOpen ` D ) $.
    $( The identity function is continuous (metric space version of
       ~ idcn ). $)
    metidcn $p |- ( ( C e. Met /\ D e. Met /\ C C_ D ) ->
       ( _I |` X ) e. ( J Cn K ) ) $=
      ( cme wcel wss w3a ccn co cid cres simp1 simp2 simp3 metcnss cuni uniopn2
      syl31anc reseq2d ctop opntop eqid idcn syl eqeltrd 3ad2ant1 sseldd ) AIJZ
      BIJZABKZLZCCMNZCDMNZOEPZUPUMUMUNUOUQURKUMUNUOQZUTUMUNUORUMUNUOSAABCCDGGHT
      UCUMUNUSUQJUOUMUSOCUAZPZUQUMEVAOACEFGUBUDUMCUEJVBUQJACGUFCVAVAUGUHUIUJUKU
      L $.
      $( [13-Nov-2013] $) $( [23-Aug-2007] $)
  $}

  ${
    metdnsconst.1 $e |- X = dom dom C $.
    metdnsconst.2 $e |- Y = dom dom D $.
    metdnsconst.3 $e |- J = ( MetOpen ` C ) $.
    metdnsconst.4 $e |- K = ( MetOpen ` D ) $.
    $( If a continuous mapping to a metric space is constant on a dense subset,
       it is constant on the entire space (metric space version of
       ~ dnsconst ). $)
    metdnsconst $p |- ( ( ( C e. Met /\ D e. Met /\ F e. ( J Cn K ) ) /\
             ( P e. Y /\ A C_ ( `' F " { P } ) /\
                 ( ( cls ` J ) ` A ) = X ) ) -> F : X --> { P } ) $=
      ( cme wcel w3a cfv wceq wf cuni ccn ccnv csn cima wss ccl ctop cha opntop
      co wi methaus id eqid dnsconst ex syl3an uniopn2 3ad2ant2 eleq2d 3ad2ant1
      eqeq2d 3anbi13d feq2d 3imtr4d imp ) BNOZCNOZEFGUAUJOZPZDIOZAEUBDUCZUDUEZA
      FUFQQZHRZPZHVLESZVJDGTZOZVMVNFTZRZPZVTVLESZVPVQVGFUGOZVHGUHOZVIVIWBWCUKBF
      LUICGMULVIUMWDWEVIPWBWCADEFGVTVRVTUNVRUNUOUPUQVJVKVSVOWAVMVJIVRDVHVGIVRRV
      ICGIKMURUSUTVJHVTVNVGVHHVTRVIBFHJLURVAZVBVCVJHVTVLEWFVDVEVF $.
      $( [13-Nov-2013] $) $( [24-Aug-2007] $)
  $}


