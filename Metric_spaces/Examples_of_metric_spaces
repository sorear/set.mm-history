$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       Examples of metric spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d D x y z $.
    cnmet.1 $e |- D = ( abs o. - ) $.
    $( Value of the distance function of the metric space of complex
       numbers. $)
    cnmetdval $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( A D B ) = ( abs ` ( A - B ) ) ) $=
      ( cc wcel wa cop cabs cmin ccom cfv co subopr wfun cr ffun ax-mp df-opr
      wf cdm wceq cxp opelxpi fdmi syl6eleqr absf fvco mp3an12 syl fveq1i eqtri
      fveq2i 3eqtr4g ) AEFBEFGZABHZIJKZLZUPJLZILZABCMZABJMZILUOUPJUAZFZURUTUBZU
      OUPEEUCZVCABEEUDVFEJNUEUFIOZJOZVDVEEPITVGUGEPIQRVFEJTVHNVFEJQRUPIJUHUIUJV
      AUPCLURABCSUPCUQDUKULVBUSIABJSUMUN $.
      $( [7-Sep-2007] $) $( [9-Dec-2006] $)

    $( The base set of the metric for complex numbers. $)
    cnmetba $p |- CC = dom dom D $=
      ( cdm cc cxp cabs cmin ccom dmeqi cr wf absf subopr fco mp2an fdmi dmxpid
      eqtri eqtr2i ) ACZCDDEZCDTUATFGHZCUAAUBBIUAJUBDJFKUADGKUAJUBKLMUADJFGNOPR
      IDQS $.
      $( [30-Aug-2007] $)

    $( The absolute value metric determines a metric space on the complex
       numbers.  This theorem provides a link between complex numbers and
       metrics spaces, making metric space theorems available for use with
       complex numbers.  (Contributed by FL, 9-Oct-2006.) $)
    cnmet $p |- D e. Met $=
      ( vx vy vz cc cr wf cabs cmin cv wcel wa cfv cc0 wceq cnmetdval w3a caddc
      co cnex cxp ccom absf subopr fco mp2an feq1i mpbir subcl abs00 syl eqcomd
      eqeq1d subeq0 3bitr3d cle abs3dif abssub opreq1d 3adant2 breqtrd opreq12d
      wb 3adant3 3coml 3brtr4d ismeti ) CDEAFUAFFUBZGAHVIGIJUCZHZFGIHVIFJHVKUDU
      EVIFGIJUFUGVIGAVJBUHUICKZFLZDKZFLZMZVLVNJTZINZOPZVQOPZVLVNATZOPVLVNPVPVQF
      LVSVTVDVLVNUJVQUKULVPVRWAOVPWAVRVLVNABQZUMUNVLVNUOUPVMVOEKZFLZRZVRWCVLJTI
      NZWCVNJTINZSTZWAWCVLATZWCVNATZSTZUQWEVRVLWCJTINZWGSTZWHUQVLVNWCURVMWDWMWH
      PVOVMWDMWLWFWGSVLWCUSUTVAVBVMVOWAVRPWDWBVEWDVMVOWKWHPWDVMVORWIWFWJWGSWDVM
      WIWFPVOWCVLABQVEWDVOWJWGPVMWCVNABQVAVCVFVGVH $.
      $( [7-Sep-2007] $) $( [6-Oct-2006] $)
  $}

  ${
    $d A f w x y z $.  $d B f w x y z $.  $d C w x y z $.  $d D w x y z $.
    $d J f w x y z $.  $d K f w x y $.
    cncfmet.1 $e |- C = ( ( abs o. - ) |` ( A X. A ) ) $.
    cncfmet.2 $e |- D = ( ( abs o. - ) |` ( B X. B ) ) $.
    cncfmet.3 $e |- J = ( MetOpen ` C ) $.
    cncfmet.4 $e |- K = ( MetOpen ` D ) $.
    $( Relate complex function continuity to metric space continuity.
       (Contributed by Paul Chapman, 26-Nov-2007.) $)
    cncfmet $p |- ( ( A C_ CC /\ B C_ CC ) ->
                       ( A -cn-> B ) = ( J Cn K ) ) $=
      ( vx vw cc wa co wf wral wcel wceq cr vf vz vy wss ccncf ccn cv cmin cabs
      cfv clt wbr wi crp wrex cdm wb cxp ccom cres absf subopr fco mp2an xpss12
      anidms fssres sylancr feq1i sylibr fdm dmeqd dmxpid syl6eq feq23 ad2antrr
      syl2an anbi1d raleqdv adantr simplll simprl simprr opreqi syl5eq ad2ant2l
      oprvres ssel2 eqid cnmetdval eqtrd syl22anc breq1d simplr ffvelrn syl2anc
      syl simpllr sseldd imbi12d anassrs ralbidva bitrd rexbidv pm5.32da bitr2d
      ralbidv elcncf cme cnmet metres ax-mp eqeltri metcn a1i 3bitr4d eqrdv ) A
      MUDZBMUDZNZUAABUEOZEFUFOZXTABUAUGZPZKUGZLUGZUHOUIUJZUBUGZUKULZYEYCUJZYFYC
      UJZUHOUIUJZUCUGZUKULZUMZLAQZUBUNUOZUCUNQZKAQZNZCUPZUPZDUPZUPZYCPZYEYFCOZY
      HUKULZYJYKDOZYMUKULZUMZLUUBQZUBUNUOZUCUNQZKUUBQZNZYCYARYCYBRZXTUUOYDUUNNY
      TXTUUEYDUUNXRUUBASZUUDBSZUUEYDUQXSXRAAURZTCPZUUQXRUUSTUIUHUSZUUSUTZPZUUTX
      RMMURZTUVAPZUUSUVDUDZUVCMTUIPUVDMUHPUVEVAVBUVDMTUIUHVCVDZXRUVFAMAMVEVFUVD
      TUUSUVAVGVHUUSTCUVBGVIVJUUTUUBUUSUPAUUTUUAUUSUUSTCVKVLAVMVNWQZXSBBURZTDPZ
      UURXSUVITUVAUVIUTZPZUVJXSUVEUVIUVDUDZUVLUVGXSUVMBMBMVEVFUVDTUVIUVAVGVHUVI
      TDUVKHVIVJUVJUUDUVIUPBUVJUUCUVIUVITDVKVLBVMVNWQUUBUUDABYCVOVQVRXTYDUUNYSX
      TYDNZUUNUUMKAQYSUVNUUMKUUBAXRUUQXSYDUVHVPZVSUVNUUMYRKAUVNYEARZNZUULYQUCUN
      UVQUUKYPUBUNUVQUUKUUJLAQYPUVQUUJLUUBAUVNUUQUVPUVOVTVSUVQUUJYOLAUVNUVPYFAR
      ZUUJYOUQUVNUVPUVRNZNZUUGYIUUIYNUVTUUFYGYHUKUVTXRUVPXRUVRUUFYGSXRXSYDUVSWA
      ZUVNUVPUVRWBZUWAUVNUVPUVRWCZXRUVPNZXRUVRNZNUUFYEYFUVAOZYGUVPUVRUUFUWFSXRX
      RUVSUUFYEYFUVBOUWFCUVBYEYFGWDYEYFAAUVAWGWEWFUWDYEMRYFMRUWFYGSUWEAMYEWHAMY
      FWHYEYFUVAUVAWIZWJVQWKWLWMUVTUUHYLYMUKUVTUUHYJYKUVAOZYLUVTYJBRZYKBRZUUHUW
      HSUVTYDUVPUWIXTYDUVSWNZUWBABYEYCWOWPZUVTYDUVRUWJUWKUWCABYFYCWOWPZUWIUWJNU
      UHYJYKUVKOUWHDUVKYJYKHWDYJYKBBUVAWGWEWPUVTYJMRYKMRUWHYLSUVTBMYJXRXSYDUVSW
      RZUWLWSUVTBMYKUWNUWMWSYJYKUVAUWGWJWPWKWMWTXAXBXCXDXGXBXCXEXFKUCUBLABYCXHU
      UPUUOUQZXTCXIRDXIRUWOCUVBXIGUVAXIRZUVBXIRUVAUWGXJZUVAAXKXLXMDUVKXIHUWPUVK
      XIRUWQUVABXKXLXMKUCUBLCDYCEFUUBUUDUUBWIIUUDWIJXNVDXOXPXQ $.
      $( [13-Nov-2013] $) $( [11-Oct-2007] $)
  $}

  ${
    cncfmet1.1 $e |- D = ( abs o. - ) $.
    cncfmet1.2 $e |- J = ( MetOpen ` D ) $.
    $( Relate complex function continuity to metric space continuity.
       (Contributed by Paul Chapman, 28-Nov-2007.) $)
    cncfmet1 $p |- ( CC -cn-> CC ) = ( J Cn J ) $=
      ( cc wss ccncf co ccn wceq ssid cabs cmin ccom cxp cdm subopr ax-mp mp2an
      fdmi cres wrel relco crn wf absf sseqtr4i dmcosseq eqtri eqimssi relssres
      frn cr eqtr4i cncfmet ) EEFZUPEEGHBBIHJEKZUQEEAABBALMNZUREEOZUAZCURUBURPZ
      USFUTURJLMUCVAUSVAMPZUSMUDZLPZFVAVBJVCEVDUSEMUEVCEFQUSEMULREUMLUFTUGLMUHR
      USEMQTUIUJURUSUKSUNZVEDDUOS $.
      $( [28-Nov-2007] $)
  $}

  ${
    $d x y z C $.
    cn2met.1 $e |- C = ( abs o. - ) $.
    cn2met.2 $e |- D = { <. <. x , y >. , z >. | ( ( x e. ( CC X. CC ) /\
             y e. ( CC X. CC ) ) /\ z = sup ( { ( ( 1st ` x ) C ( 1st ` y ) ) ,
                  ( ( 2nd ` x ) C ( 2nd ` y ) ) } , RR , < ) ) } $.
    $( The standard metric space on ` CC X. CC ` . $)
    cn2met $p |- D e. Met $=
      ( cc cnmetba cnmet metxp ) ABCDDEHHDFIZLDFJZMGK $.
      $( [21-Jun-2007] $)
  $}


  ${
    $d r x y z D $.  $d x A $.  $d x B $.
    remet.1 $e |- D = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    $( Value of the distance function of the metric space of real numbers. $)
    remetdval $p |- ( ( A e. RR /\ B e. RR ) ->
                  ( A D B ) = ( abs ` ( A - B ) ) ) $=
      ( cr wcel wa co cop cabs cmin ccom cxp cres cfv df-opr fveq1i wceq recn
      cc eqtri opelxpi fvres syl eqid cnmetdval syl2an syl5eqr eqtrd syl5eq ) A
      EFZBEFZGZABCHZABIZJKLZEEMZNZOZABKHJOZUNUOCOUSABCPUOCURDQUAUMUSUOUPOZUTUMU
      OUQFUSVARABEEUBUOUQUPUCUDUMVAABUPHZUTABUPPUKATFBTFVBUTRULASBSABUPUPUEUFUG
      UHUIUJ $.
      $( [16-May-2007] $)

    $( The base set for the metric for real numbers. $)
    remetba $p |- RR = dom dom D $=
      ( cdm cr cxp cabs cmin ccom cres cin dmeqi dmres wceq cc ax-resscn xpss12
      wss mp2an wf absf subopr fdmi sseqtr4i df-ss mpbi 3eqtri dmxpid eqtr2i
      fco ) ACZCDDEZCDUJUKUJFGHZUKIZCUKULCZJZUKAUMBKULUKLUKUNQUOUKMUKNNEZUNDNQZ
      UQUKUPQOODNDNPRUPDULNDFSUPNGSUPDULSTUAUPNDFGUIRUBUCUKUNUDUEUFKDUGUH $.
      $( [30-Aug-2007] $)

    $( The absolute value metric determines a metric space on the reals. $)
    remet $p |- D e. Met $=
      ( cabs cmin ccom cr cxp cres cme wcel eqid cnmet metres ax-mp eqeltri ) A
      CDEZFFGHZIBPIJQIJPPKLPFMNO $.
      $( [10-Feb-2007] $)

    $( A ball in terms of an open interval of reals. $)
    bl2ioo $p |- ( ( A e. RR /\ B e. RR+ ) ->
                   ( A ( ball ` D ) B ) = ( ( A - B ) (,) ( A + B ) ) ) $=
      ( vx cr wcel wa cfv co cmin clt wbr wb cabs cc recn syl2an sylan2 cxr crp
      cbl caddc cioo cv w3a rpre remetdval abssub eqtrd breq1d adantlr absdiflt
      wceq 3expb ancoms bitrd pm5.32da 3anass syl6bbr remet remetba elbl mp3an1
      cme resubcl ax-addrcl rexr elioo2 syl2anc 3bitr4d eqrdv ) AFGZBUAGZHZEABC
      UBIJZABKJZABUCJZUDJZVOEUEZFGZAVTCJZBLMZHZWAVQVTLMZVTVRLMZUFZVTVPGZVTVSGZV
      OWDWAWEWFHZHZWGVNVMBFGZWDWKNBUGZVMWLHZWAWCWJWNWAHWCVTAKJOIZBLMZWJVMWAWCWP
      NWLVMWAHZWBWOBLWQWBAVTKJOIZWOAVTCDUHVMAPGVTPGWRWOUNWAAQVTQAVTUIRUJUKULWAW
      NWPWJNZWAVMWLWSVTABUMUOUPUQURSWAWEWFUSUTCVEGVMVNWHWDNCDVAVTCABFCDVBVCVDVN
      VMWLWIWGNZWMWNVQFGZVRFGZWTABVFABVGXAVQTGVRTGWTXBVQVHVRVHVQVRVTVIRVJSVKVL
      $.
      $( [13-Nov-2013] $) $( [18-May-2007] $)

    $( An open interval of reals in terms of a ball. $)
    ioo2bl $p |- ( ( A e. RR /\ B e. RR /\ A < B ) -> ( A (,) B ) =
     ( ( ( A + B ) / 2 ) ( ball ` D ) ( ( B - A ) / 2 ) ) ) $=
      ( cr wcel clt wbr cioo co caddc c2 cdiv cmin wceq wa cc recn 3adant3 crp
      w3a cbl halfaddsub syl2anr simprd simpld opreq12d eqcomd ax-addrcl ancoms
      cfv cc0 wne 2re 2ne0 redivcl mp3an23 syl resubcl posdif biimpa 3impa elrp
      sylanbrc 2pos elrpii rpdivcl sylancl bl2ioo syl2anc addcom opreq1d 3eqtrd
      ) AEFZBEFZABGHZUAZABIJZBAKJZLMJZBANJZLMJZNJZVTWBKJZIJZVTWBCUBUKZJZABKJZLM
      JZWBWFJVQWEVRVQWCAWDBIVQWDBOZWCAOZVNVOWJWKPZVPVOBQFZAQFZWLVNBRZARZBAUCUDS
      ZUEVQWJWKWQUFUGUHVQWGWEVQVTEFZWBTFZWGWEOVNVOWRVPVNVOPZVSEFZWRVOVNXABAUIUJ
      XALEFLULUMWRUNUOVSLUPUQURSVQWATFZLTFWSVQWAEFZULWAGHZXBVNVOXCVPVOVNXCBAUSU
      JSVNVOVPXDWTVPXDABUTVAVBWAVCVDLUNVEVFWALVGVHVTWBCDVIVJUHVQVTWIWBWFVQVSWHL
      MVNVOVSWHOZVPVOWMWNXEVNWOWPBAVKUDSVLVLVM $.
      $( [13-Nov-2013] $) $( [18-May-2007] $)

    $( An open interval of reals in terms of a ball. $)
    ioo2blex $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
                     ( A (,) B ) e. ran ( ball ` D ) ) $=
      ( cr wcel clt wbr w3a cioo co caddc c2 cdiv cmin cbl crp cc0 2re 3adant3
      cfv crn ioo2bl wa ax-addrcl wne redivcl mp3an23 syl resubcl ancoms posdif
      2ne0 biimpa 3impa elrp sylanbrc elrpii rpdivcl sylancl cxp wfn cme cpw wf
      2pos remet remetba blf ffn mp2b fnoprvrn mp3an1 syl2anc eqeltrd ) AEFZBEF
      ZABGHZIZABJKABLKZMNKZBAOKZMNKZCPUAZKZWDUBZABCDUCVSWAEFZWCQFZWEWFFZVPVQWGV
      RVPVQUDZVTEFZWGABUEWKMEFMRUFWGSUMVTMUGUHUITVSWBQFZMQFWHVSWBEFZRWBGHZWLVPV
      QWMVRVQVPWMBAUJUKTVPVQVRWNWJVRWNABULUNUOWBUPUQMSVFURWBMUSUTWDEQVAZVBZWGWH
      WICVCFWOEVDZWDVEWPCDVGCECDVHVIWOWQWDVJVKEQWAWCWDVLVMVNVO $.
      $( [14-Nov-2013] $)

    $( The balls of the standard real metric space are included in the open
       real intervals. $)
    blssioo $p |- ran ( ball ` D ) C_ ran (,) $=
      ( vz vy vr cbl cfv crn cioo cv wcel co wceq crp wrex cr ax-mp wa cxr rexr
      cme wb remet remetba blrn cmin caddc bl2ioo resubcl ax-addrcl cxp wfn cpw
      rpre wf ioof ffn fnoprvrn mp3an1 syl2an syl2anc sylan2 eqeltrd syl5ibrcom
      eleq1 rexlimivv sylbi ssriv ) CAFGZHZIHZCJZVJKZVLDJZEJZVILZMZENODPOZVLVKK
      ZAUAKVMVRUBABUCDVLAPEABUDUEQVQVSDEPNVNPKZVONKZRZVSVQVPVKKWBVPVNVOUFLZVNVO
      UGLZILZVKVNVOABUHWAVTVOPKZWEVKKZVOUNVTWFRWCPKZWDPKZWGVNVOUIVNVOUJWHWCSKZW
      DSKZWGWIWCTWDTISSUKZULZWJWKWGWLPUMZIUOWMUPWLWNIUQQSSWCWDIURUSUTVAVBVCVLVP
      VKVEVDVFVGVH $.
      $( [13-Nov-2013] $) $( [8-May-2007] $)

    ${
      $d a b v x y z $.  $d a b x y z D $.  $d v J $.
      tgioo.2 $e |- J = ( MetOpen ` D ) $.
      $( The topology generated by open intervals of reals is the same as the
         open sets of the standard metric space on the reals. $)
      tgioo $p |- ( topGen ` ran (,) ) = J $=
        ( vz cioo wcel wceq ax-mp wss cv cr co wa c1 cxr syl syl2anc wbr cmnf
        vv vx vy va cbl cfv crn ctg cme remet opnval ctb blbas retopbas blssioo
        crp wrex wral cuni elssuni unirnioo syl6sseqr cin a1i simpl ssel2 sylan
        vb cmin caddc 1rp bl2ioo mpan2 peano2rem rexr peano2re cxp wfn cpw ioof
        wf ffn fnoprvrn mp3an1 eqeltrd simpr remetba blcntr mp3an13 elin basis2
        sylanbrc syl22anc wb oprvelrn wi eleq2 sseq1 anbi12d cle cif inss2 sstr
        adantl elioore adantr sseqtrd dfss sylib eliooxr jca iooin eqtrd simpld
        clt mnfxr ifcl simprd mnflt xrmax2 xrltletr syl3anc mp2and eleqtrd ne0i
        c0 wne ioon0 syl5ib mpcom xrre2 syl32anc mnfle xrlelttr xrmin2 ioo2blex
        xrre inss1 rcla4ev syl12anc blssex mpbid syl6bi rexlimivv imp rexlimiva
        sylancr sylanb ralrimiva isopn4 ssriv sseqtri 2basgen mp4an eqtr2i ) BA
        UEUFZUGZUHUFZFUGZUHUFZAUIGZBUURHACUJZABDUKIZUUQULGZUUSULGZUUQUUSJUUSUUR
        JUURUUTHUVAUVDUVBAUMIUNACUOUUSBUURUAUUSBUAKZUUSGZUVFLJZUBKZUCKUUPMUVFJU
        CUPUQZUBUVFURZUVFBGZUVGUVFUUSUSLUVFUUSUTVAVBZUVGUVJUBUVFUVGUVIUVFGZNZUV
        IEKZGZUVPUVFUVIOUUPMZVCZJZNZEUUSUQZUVJUVOUVEUVGUVRUUSGZUVIUVSGZUWBUVEUV
        OUNVDUVGUVNVEUVOUVILGZUWCUVGUVHUVNUWEUVMUVFLUVIVFVGZUWEUVRUVIOVIMZUVIOV
        JMZFMZUUSUWEOUPGZUVRUWIHZVKUVIOACVLVMZUWEUWGPGZUWHPGZUWIUUSGZUWEUWGLGZU
        WMUVIVNZUWGVOZQZUWEUWHLGZUWNUVIVPZUWHVOZQZFPPVQZVRZUWMUWNUWOUXDLVSZFWAU
        XEVTUXDUXFFWBIZPPUWGUWHFWCWDRWEQUVOUVNUVIUVRGZUWDUVGUVNWFUVOUWEUXHUWFUV
        AUWEUWJUXHUVBVKAUVIOLACWGZWHWIQUVIUVFUVRWJWLEUVIUUSUVFUVRWKWMUWAUVJEUUS
        UVPUUSGZUVPUDKZVHKZFMZHZVHPUQUDPUQZUWAUVJUXEUXJUXOWNUXGUDVHPPUVPFWOIUXO
        UWAUVJUXNUWAUVJWPZUDVHPPUXNUXPWPUXKPGZUXLPGZNZUXNUWAUVIUXMGZUXMUVSJZNZU
        VJUXNUVQUXTUVTUYAUVPUXMUVIWQZUVPUXMUVSWRWSUYBUVQUVPUVFJZNZEUUQUQZUVJUYB
        UXMUUQGUXTUXMUVFJZUYFUYBUXMUXKUWGWTSZUWGUXKXAZUXLUWHWTSZUXLUWHXAZFMZUUQ
        UYBUXMUXMUWIVCZUYLUYBUXMUWIJUXMUYMHUYBUXMUVRUWIUYAUXMUVRJZUXTUYAUVSUVRJ
        UYNUVFUVRXBUXMUVSUVRXCVMXDUYBUWEUWKUXTUWEUYAUVIUXKUXLXEZXFZUWLQXGUXMUWI
        XHXIUXTUYMUYLHZUYAUXTUXSUWMUWNNZUYQUVIUXKUXLXJZUXTUWEUYRUYOUWEUWMUWNUWS
        UXCXKQUXKUXLUWGUWHXLRXFXMZUYBUYILGZUYKLGZUYIUYKXOSZUYLUUQGUYBTPGZUYIPGZ
        UYKPGZTUYIXOSZVUCVUAVUDUYBXPVDZUYBUWMUXQVUEUYBUWEUWMUYPUWSQZUYBUXQUXRUX
        TUXSUYAUYSXFZXNZUYHUWGUXKPXQRZUYBUXRUWNVUFUYBUXQUXRVUJXRZUYBUWTUWNUYBUW
        EUWTUYPUXAQZUXBQZUYJUXLUWHPXQRZUYBTUWGXOSZUWGUYIWTSZVUGUYBUWPVUQUXTUWPU
        YAUXTUWEUWPUYOUWQQXFZUWGXSQUYBUXQUWMVURVUKVUIUXKUWGXTRUYBVUDUWMVUEVUQVU
        RNVUGWPVUHUYBUWPUWMVUSUWRQVULTUWGUYIYAYBYCUYBUVIUYLGZVUCUYBUVIUXMUYLUXT
        UYAVEZUYTYDVUEVUFNZVUTVUCUVIUYIUYKXJVUTUYLYFYGVVBVUCUYLUVIYEUYIUYKYHYIY
        JQZTUYIUYKYKYLUYBVUFUWTTUYKXOSZUYKUWHWTSZVUBVUPVUNUYBTUYIWTSZVUCVVDUYBV
        UEVVFVULUYIYMQVVCUYBVUDVUEVUFVVFVUCNVVDWPVUHVULVUPTUYIUYKYNYBYCUYBUXRUW
        NVVEVUMVUOUXLUWHYORUYKUWHYQWMVVCUYIUYKACYPYBWEVVAUYAUYGUXTUYAUVSUVFJUYG
        UVFUVRYRUXMUVSUVFXCVMXDUYEUXTUYGNEUXMUUQUXNUVQUXTUYDUYGUYCUVPUXMUVFWRWS
        YSYTUYBUVAUWEUYFUVJWNUVBUYPEUVFAUVILUCUXIUUAUUGUUBUUCVDUUDUUEUUHUUFQUUI
        UVAUVLUVHUVKNWNUVBUBUCUVFABLUXIDUUJIWLUUKUVCUULUUQUUSUUMUUNUUO $.
        $( [13-Nov-2013] $) $( [7-May-2007] $)

      $( ` QQ ` is dense in ` RR ` . $)
      qdensere2 $p |- ( ( cls ` J ) ` QQ ) = RR $=
        ( cq cioo crn ctg cfv ccl cr tgioo fveq2i fveq1i qdensere eqtr3i ) EFGH
        IZJIZIEBJIZIKERSQBJABCDLMNOP $.
        $( [24-Aug-2007] $)
    $}
  $}

  ${
    $d x y z $.
    $( The standard topology on the reals is Hausdorff. $)
    rehaus $p |- ( topGen ` ran (,) ) e. Haus $=
      ( cioo crn ctg cfv cabs cmin ccom cxp cres copn eqid tgioo remet methausi
      cr cha eqeltri ) ABCDEFGOOHIZJDZPRSRKZSKZLRSRTMUANQ $.
      $( [28-Jun-2008] $) $( [8-Mar-2007] $)
  $}

  ${
    $d X x y z $.  $d X u v w $.  $d D u v w $.  $d u x y z $.  $d v x y z $.
    $d w x y z $.
    dscmet.1 $e |- X e. _V $.
    dscmet.2 $e |- D = { <. <. x , y >. , z >. | ( ( x e. X /\ y e. X ) /\
                  z = if ( x = y , 0 , 1 ) ) } $.
    $( The discrete metric on any set ` X ` .  Definition 1.1-8 of [Kreyszig]
       p. 8.  (Contributed by FL, 12-Oct-2006.) $)
    dscmet $p |- D e. Met $=
      ( cv wceq cc0 c1 cif cr wcel wa 0re 1re co caddc cle vw keepel a1i foprab
      vv vu elexi eqeq1 ifbid eqeq2 oprabval2 eqeq1d wn wo simpl ax-1ne0 necomi
      eqif wne df-ne mpbi pm2.21i ancoms jaoi sylbi eqcoms iftrue impbii syl6bb
      orcanai w3a wbr imdistani 0nn0 nn0addge1i opreqan12d syl5breqr syl notbid
      equtr equequ2 biimpcd lt01 ltleii addge0i mp2an iffalse pm2.61ian eqbrtrd
      wi neeq1 biimprd 3imtr3g leidi ax-1cn addid2i syl6eq ex addid1i 1lt2 df-2
      c2 2re syl6eqr pm2.61dan pm2.61i 3adant3 3adant2 3adant1 opreq12d 3brtr4d
      a1d ismeti ) UAUEUFDEFABCEEAHZBHZIZJKLZMDGXQMNXNENXOENOXPJKMPQUBUCUDUAHZE
      NZUEHZENZOZXRXTDRZJIXRXTIZJKLZJIZYDYBYCYEJABCXRXTEEXQYEDXRXOIZJKLYEMYDJKM
      PQUBUGXNXRIXPYGJKXNXRXOUHUIXOXTIZYGYDJKXOXTXRUJUIGUKZULYFYDYDJYEJYEIYDJJI
      ZOZYDUMZJKIZOZUNYDYDJJKURYKYDYNYDYJUOYMYLYDYMYDYDYMYDYDUNJKUSYMUMKJUPUQJK
      UTVAVBVJVCVDVEVFYDJKVGZVHVIXSYAUFHZENZVKZYEYPXRIZJKLZYPXTIZJKLZSRZYCYPXRD
      RZYPXTDRZSRTYEUUCTVLZYRYDUUFYDYEJUUCTYOYSYDJUUCTVLZYSYDOYSUUAOZUUGYSYDUUA
      UFUAUEVTVMUUHJJJSRUUCTJJPVNVOYSUUAYTJUUBJSYSJKVGZUUAJKVGZVPVQVRYSUMZYDOUU
      KUUAUMZOZUUGUUKYDUULYDUUKUULYDYSUUAUAUEUFWAVSWBVMUUMJKKSRZUUCTJKTVLZUUOJU
      UNTVLJKPQWCWDZUUPKKQQWEWFUUKUULYTKUUBKSYSJKWGZUUAJKWGZVPZVQVRWHWIYLYEKUUC
      TYDJKWGYSYLKUUCTVLZWJYSYLUUTYSYLOYSUULOZUUTYSYLUULYSXRXTUSZYPXTUSZYLUULYS
      UVCUVBYPXRXTWKWLXRXTUTYPXTUTWMVMUVAKKUUCTKQWNZUVAUUCJKSRKYSUULYTJUUBKSUUI
      UURVPKWOWPWQVQVRWRUUKUUTYLUUKUUAUUTUUKUUAOZKKUUCTUVDUVEUUCKJSRKUUKUUAYTKU
      UBJSUUQUUJVPKWOWSWQVQUUMKXBUUCTKXBQXCWTWDUUMUUCUUNXBUUSXAXDVQXEXLXFWIXFUC
      XSYAYCYEIYQYIXGYRUUDYTUUEUUBSXSYQUUDYTIZYAYQXSUVFABCYPXREEXQYTDYPXOIZJKLZ
      YTMYSJKMPQUBUGXNYPIXPUVGJKXNYPXOUHUIZXOXRIUVGYSJKXOXRYPUJUIGUKVCXHYAYQUUE
      UUBIZXSYQYAUVJABCYPXTEEXQUUBDUVHUUBMUUAJKMPQUBUGUVIYHUVGUUAJKXOXTYPUJUIGU
      KVCXIXJXKXM $.
      $( [18-Apr-2008] $) $( [9-Oct-2006] $)
  $}


