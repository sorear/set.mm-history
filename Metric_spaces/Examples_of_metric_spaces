$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       Examples of metric spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d D x y z $.
    cnmet.1 $e |- D = ( abs o. - ) $.
    $( Value of the distance function of the metric space of complex
       numbers. $)
    cnmetdval $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( A D B ) = ( abs ` ( A - B ) ) ) $=
      ( cc wcel wa cop cabs cmin ccom cfv co subopr wfun cr ffun ax-mp df-opr
      wf cdm wceq cxp opelxpi fdmi syl6eleqr absf fvco mp3an12 syl fveq1i eqtri
      fveq2i 3eqtr4g ) AEFBEFGZABHZIJKZLZUPJLZILZABCMZABJMZILUOUPJUAZFZURUTUBZU
      OUPEEUCZVCABEEUDVFEJNUEUFIOZJOZVDVEEPITVGUGEPIQRVFEJTVHNVFEJQRUPIJUHUIUJV
      AUPCLURABCSUPCUQDUKULVBUSIABJSUMUN $.
      $( [7-Sep-2007] $) $( [9-Dec-2006] $)

    $( The base set of the metric for complex numbers. $)
    cnmetba $p |- CC = dom dom D $=
      ( cdm cc cxp cabs cmin ccom dmeqi cr wf absf subopr fco mp2an fdmi dmxpid
      eqtri eqtr2i ) ACZCDDEZCDTUATFGHZCUAAUBBIUAJUBDJFKUADGKUAJUBKLMUADJFGNOPR
      IDQS $.
      $( [30-Aug-2007] $)

    $( The absolute value metric determines a metric space on the complex
       numbers.  This theorem provides a link between complex numbers and
       metrics spaces, making metric space theorems available for use with
       complex numbers.  (Contributed by FL, 9-Oct-2006.) $)
    cnmet $p |- D e. Met $=
      ( vx vy vz cc cr wf cabs cmin cv wcel wa cfv cc0 wceq cnmetdval w3a caddc
      co ax-cnex cxp ccom absf subopr fco mp2an feq1i mpbir wb subcl syl eqcomd
      eqeq1d subeq0 3bitr3d cle abs3dif abssub opreq1d 3adant2 breqtrd opreq12d
      abs00 3adant3 3coml 3brtr4d ismeti ) CDEAFUAFFUBZGAHVIGIJUCZHZFGIHVIFJHVK
      UDUEVIFGIJUFUGVIGAVJBUHUICKZFLZDKZFLZMZVLVNJTZINZOPZVQOPZVLVNATZOPVLVNPVP
      VQFLVSVTUJVLVNUKVQVDULVPVRWAOVPWAVRVLVNABQZUMUNVLVNUOUPVMVOEKZFLZRZVRWCVL
      JTINZWCVNJTINZSTZWAWCVLATZWCVNATZSTZUQWEVRVLWCJTINZWGSTZWHUQVLVNWCURVMWDW
      MWHPVOVMWDMWLWFWGSVLWCUSUTVAVBVMVOWAVRPWDWBVEWDVMVOWKWHPWDVMVORWIWFWJWGSW
      DVMWIWFPVOWCVLABQVEWDVOWJWGPVMWCVNABQVAVCVFVGVH $.
      $( [7-Sep-2007] $) $( [6-Oct-2006] $)
  $}

  ${
    $d A f w x y z $.  $d B f w x y z $.  $d C w x y z $.  $d D w x y z $.
    $d J f w x y z $.  $d K f x y $.
    cncfmet.1 $e |- C = ( ( abs o. - ) |` ( A X. A ) ) $.
    cncfmet.2 $e |- D = ( ( abs o. - ) |` ( B X. B ) ) $.
    cncfmet.3 $e |- J = ( MetOpen ` C ) $.
    cncfmet.4 $e |- K = ( MetOpen ` D ) $.
    $( Relate complex function continuity to metric space continuity.
       (Contributed by Paul Chapman, 26-Nov-2007.) $)
    cncfmet $p |- ( ( A C_ CC /\ B C_ CC ) ->
                       ( A -cn-> B ) = ( J Cn K ) ) $=
      ( vz vy cc wa co wcel wi cdm cr wceq vf vx vw wss ccn ccncf cmin cabs cfv
      cv wf clt wbr wral crp wrex cc0 cme ccom cxp cres eqid cnmet metres ax-mp
      wb eqeltri metcn mp2an cin dmeqi dmres absf subopr fco fdmi ineq2i xpss12
      eqtri anidms df-ss sylib syl5eq dmeqd dmxpid syl6eq feq23 syl2an 3ad2ant1
      anbi1d w3a eleq2d imbi1d adantr opreqi oprvres ssel anim12d imp cnmetdval
      adantl syl 3ad2antl1 breq1d ffvelrn ex 3ad2antl3 3ad2antl2 syldan imbi12d
      eqtrd expr pm5.74d bitrd ralbidv2 anbi2d rexbidv anbi1i anass bitri exbii
      wex elrp df-rex 3bitr4ri syl6bb imbi2d ralbidv imbi1i impexp albii df-ral
      wal pm5.74da 3expa pm5.32da syl5bb elcncf bitr4d eqrdv eqcomd ) AMUDZBMUD
      ZNZEFUEOZABUFOZUUDUAUUEUUFUUDUAUJZUUEPZABUUGUKZUBUJZUCUJZUGOUHUIZKUJZULUM
      ZUUJUUGUIZUUKUUGUIZUGOUHUIZLUJZULUMZQZUCAUNZKUOUPZLUOUNZUBAUNZNZUUGUUFPUU
      HCRZRZDRZRZUUGUKZUQUURULUMZUQUUMULUMZUUJUUKCOZUUMULUMZUUOUUPDOZUURULUMZQZ
      UCUVGUNZNZKSUPZQZLSUNZUBUVGUNZNZUUDUVECURPDURPUUHUWDVFCUHUGUSZAAUTZVAZURG
      UWEURPZUWGURPUWEUWEVBZVCZUWEAVDVEVGDUWEBBUTZVAZURHUWHUWLURPUWJUWEBVDVEVGU
      BLKUCCDUUGEFUVGUVIUVGVBIUVIVBJVHVIUUDUWDUUIUWCNUVEUUDUVJUUIUWCUUBUVGATZUV
      IBTUVJUUIVFUUCUUBUVGUWFRAUUBUVFUWFUUBUVFUWFUWERZVJZUWFUVFUWGRUWOCUWGGVKUW
      EUWFVLVSUUBUWOUWFMMUTZVJZUWFUWNUWPUWFUWPSUWEMSUHUKUWPMUGUKUWPSUWEUKVMVNUW
      PMSUHUGVOVIVPZVQUUBUWFUWPUDZUWQUWFTUUBUWSAMAMVRVTUWFUWPWAWBWCWCWDAWEWFZUU
      CUVIUWKRBUUCUVHUWKUUCUVHUWKUWNVJZUWKUVHUWLRUXADUWLHVKUWEUWKVLVSUUCUXAUWKU
      WPVJZUWKUWNUWPUWKUWRVQUUCUWKUWPUDZUXBUWKTUUCUXCBMBMVRVTUWKUWPWAWBWCWCWDBW
      EWFUVGUVIABUUGWGWHWJUUDUUIUWCUVDUUBUUCUUIUWCUVDVFUUBUUCUUIWKZUWBUVCUBUVGA
      UXDUUJUVGPZUWBQUUJAPZUWBQUXFUVCQUXDUXEUXFUWBUXDUVGAUUJUUBUUCUWMUUIUWTWIZW
      LWMUXDUXFUWBUVCUXDUXFNZUWBUVKUVBQZLSUNZUVCUXHUWAUXILSUXHUVTUVBUVKUXHUVTUV
      LUVANZKSUPZUVBUXHUVSUXKKSUXHUVRUVAUVLUXHUVQUUTUCUVGAUXHUUKUVGPZUVQQUUKAPZ
      UVQQUXNUUTQUXHUXMUXNUVQUXHUVGAUUKUXDUWMUXFUXGWNWLWMUXHUXNUVQUUTUXDUXFUXNU
      VQUUTVFUXDUXFUXNNZNZUVNUUNUVPUUSUXPUVMUULUUMULUUBUUCUXOUVMUULTUUIUUBUXONZ
      UVMUUJUUKUWEOZUULUXOUVMUXRTUUBUXOUVMUUJUUKUWGOUXRCUWGUUJUUKGWOUUJUUKAAUWE
      WPWCXAUXQUUJMPZUUKMPZNZUXRUULTUUBUXOUYAUUBUXFUXSUXNUXTAMUUJWQAMUUKWQWRWSU
      UJUUKUWEUWIWTXBXKXCXDUXPUVOUUQUURULUXDUXOUUOBPZUUPBPZNZUVOUUQTZUUIUUBUXOU
      YDUUCUUIUXOUYDUUIUXFUYBUXNUYCUUIUXFUYBABUUJUUGXEXFUUIUXNUYCABUUKUUGXEXFWR
      WSXGUUCUUBUYDUYEUUIUUCUYDNZUVOUUOUUPUWEOZUUQUYDUVOUYGTUUCUYDUVOUUOUUPUWLO
      UYGDUWLUUOUUPHWOUUOUUPBBUWEWPWCXAUYFUUOMPZUUPMPZNZUYGUUQTUUCUYDUYJUUCUYBU
      YHUYCUYIBMUUOWQBMUUPWQWRWSUUOUUPUWEUWIWTXBXKXHXIXDXJXLXMXNXOXPXQUUMUOPZUV
      ANZKYBUUMSPZUXKNZKYBUVBUXLUYLUYNKUYLUYMUVLNZUVANUYNUYKUYOUVAUUMYCXRUYMUVL
      UVAXSXTYAUVAKUOYDUXKKSYDYEYFYGYHUURUOPZUVBQZLYMUURSPZUXIQZLYMUVCUXJUYQUYS
      LUYQUYRUVKNZUVBQUYSUYPUYTUVBUURYCYIUYRUVKUVBYJXTYKUVBLUOYLUXILSYLYEYFYNXN
      XOYOYPXNYQUBLKUCABUUGYRYSYTUUA $.
      $( [11-Oct-2007] $)
  $}

  ${
    cncfmet1.1 $e |- D = ( abs o. - ) $.
    cncfmet1.2 $e |- J = ( MetOpen ` D ) $.
    $( Relate complex function continuity to metric space continuity.
       (Contributed by Paul Chapman, 28-Nov-2007.) $)
    cncfmet1 $p |- ( CC -cn-> CC ) = ( J Cn J ) $=
      ( cc wss ccncf co ccn wceq ssid cabs cmin ccom cxp cdm subopr ax-mp mp2an
      fdmi cres wrel relco crn wf absf sseqtr4i dmcosseq eqtri eqimssi relssres
      frn cr eqtr4i cncfmet ) EEFZUPEEGHBBIHJEKZUQEEAABBALMNZUREEOZUAZCURUBURPZ
      USFUTURJLMUCVAUSVAMPZUSMUDZLPZFVAVBJVCEVDUSEMUEVCEFQUSEMULREUMLUFTUGLMUHR
      USEMQTUIUJURUSUKSUNZVEDDUOS $.
      $( [28-Nov-2007] $)
  $}

  ${
    $d x y z C $.
    cn2met.1 $e |- C = ( abs o. - ) $.
    cn2met.2 $e |- D = { <. <. x , y >. , z >. | ( ( x e. ( CC X. CC ) /\
             y e. ( CC X. CC ) ) /\ z = sup ( { ( ( 1st ` x ) C ( 1st ` y ) ) ,
                  ( ( 2nd ` x ) C ( 2nd ` y ) ) } , RR , < ) ) } $.
    $( The standard metric space on ` CC X. CC ` . $)
    cn2met $p |- D e. Met $=
      ( cc cnmetba cnmet metxp ) ABCDDEHHDFIZLDFJZMGK $.
      $( [21-Jun-2007] $)
  $}

  ${
    $d r s t u v w D $.  $d v w A $.  $d v w B $.
    remet.1 $e |- D = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    $( Value of the distance function of the metric space of real numbers. $)
    remetdval $p |- ( ( A e. RR /\ B e. RR ) ->
                  ( A D B ) = ( abs ` ( A - B ) ) ) $=
      ( cr wcel wa co cop cabs cmin ccom cxp cres cfv df-opr fveq1i wceq recn
      cc eqtri opelxpi fvres syl eqid cnmetdval syl2an syl5eqr eqtrd syl5eq ) A
      EFZBEFZGZABCHZABIZJKLZEEMZNZOZABKHJOZUNUOCOUSABCPUOCURDQUAUMUSUOUPOZUTUMU
      OUQFUSVARABEEUBUOUQUPUCUDUMVAABUPHZUTABUPPUKATFBTFVBUTRULASBSABUPUPUEUFUG
      UHUIUJ $.
      $( [16-May-2007] $)

    $( The base set for the metric for real numbers. $)
    remetba $p |- RR = dom dom D $=
      ( cdm cr cxp cabs cmin ccom cres cin dmeqi dmres wceq cc ax-resscn xpss12
      wss mp2an wf absf subopr fdmi sseqtr4i df-ss mpbi 3eqtri dmxpid eqtr2i
      fco ) ACZCDDEZCDUJUKUJFGHZUKIZCUKULCZJZUKAUMBKULUKLUKUNQUOUKMUKNNEZUNDNQZ
      UQUKUPQOODNDNPRUPDULNDFSUPNGSUPDULSTUAUPNDFGUIRUBUCUKUNUDUEUFKDUGUH $.
      $( [30-Aug-2007] $)

    $( The absolute value metric determines a metric space on the reals. $)
    remet $p |- D e. Met $=
      ( cabs cmin ccom cr cxp cres cme wcel eqid cnmet metres ax-mp eqeltri ) A
      CDEZFFGHZIBPIJQIJPPKLPFMNO $.
      $( [10-Feb-2007] $)

    $( A ball in terms of an open interval of reals. $)
    bl2ioo $p |- ( ( A e. RR /\ B e. RR /\ 0 < B ) -> ( A ( ball ` D
                  ) B ) = ( ( A - B ) (,) ( A + B ) ) ) $=
      ( vw cr wcel clt wbr w3a cfv co cmin wa wb cxr rexr syl cabs wceq cc0 cbl
      caddc cioo cv cme remet remetba elbl mpanl1 3impb resubcl elioo2 syl11anc
      readdcl 3anass remetdval recn abssub syl2an eqtrd adantlr breq1d absdiflt
      cc 3coml 3expa bitr2d pm5.32da syl5bb bitrd 3adant3 bitr4d eqrdv ) AFGZBF
      GZUABHIZJZEABCUBKLZABMLZABUCLZUDLZVREUEZVSGZWCFGZAWCCLZBHIZNZWCWBGZVOVPVQ
      WDWHOZCUFGVOVPVQNWJCDUGWCCABFCDUHUIUJUKVOVPWIWHOVQVOVPNZWIWEVTWCHIZWCWAHI
      ZJZWHWKVTPGZWAPGZWIWNOWKVTFGWOABULVTQRWKWAFGWPABUOWAQRVTWAWCUMUNWNWEWLWMN
      ZNWKWHWEWLWMUPWKWEWQWGWKWENZWGWCAMLSKZBHIZWQWRWFWSBHVOWEWFWSTVPVOWENWFAWC
      MLSKZWSAWCCDUQVOAVEGWCVEGXAWSTWEAURWCURAWCUSUTVAVBVCVOVPWEWTWQOZWEVOVPXBW
      CABVDVFVGVHVIVJVKVLVMVN $.
      $( [18-May-2007] $)

    $( An open interval of reals in terms of a ball. $)
    ioo2bl $p |- ( ( A e. RR /\ B e. RR /\ A < B ) -> ( A (,) B ) =
     ( ( ( A + B ) / 2 ) ( ball ` D ) ( ( B - A ) / 2 ) ) ) $=
      ( cr wcel clt wbr cioo co caddc c2 cdiv cmin wceq cc0 wa 3adant3 opreq1d
      cc w3a cbl cfv readdcl rehalfcl resubcl wb posdif ancoms halfpos2 biimp3a
      syl bitrd bl2ioo syl111anc recn cmul pnncan 3anidm23 2times adantl eqtr4d
      addcl subcl wne 2cn 2ne0 pm3.2i divsubdir mp3an3 syl11anc divcan3 mp3an23
      3eqtr3d ppncan 3anidm13 adantr divdir opreq12d syl2an eqtr2d 3com12
      addcom ) AEFZBEFZABGHZUAZABIJZBAKJZLMJZBANJZLMJZCUBUCZJZABKJZLMJZWLWMJWEW
      DWFWHWNOWEWDWFUAZWNWJWLNJZWJWLKJZIJZWHWQWJEFZWLEFZPWLGHZWNWTOWEWDXAWFWEWD
      QZWIEFXABAUDWIUEULRWEWDXBWFXDWKEFZXBBAUFZWKUEULRWEWDWFXCXDWFPWKGHZXCWDWEW
      FXGUGABUHUIXDXEXGXCUGXFWKUJULUMUKWJWLCDUNUOWEWDWTWHOZWFWEBTFZATFZXHWDBUPZ
      AUPZXIXJQZWRAWSBIXMWIWKNJZLMJZLAUQJZLMJZWRAXMXNXPLMXMXNAAKJZXPXIXJXNXROBA
      AURUSXJXPXROXIAUTVAVBSXMWITFZWKTFZXOWROZBAVCZBAVDZXSXTLTFZLPVEZQZYAYDYEVF
      VGVHZWIWKLVIVJVKXJXQAOZXIXJYDYEYHVFVGALVLVMVAVNXMWIWKKJZLMJZLBUQJZLMJZWSB
      XMYIYKLMXMYIBBKJZYKXIXJYIYMOBABVOVPXIYKYMOXJBUTVQVBSXMXSXTYJWSOZYBYCXSXTY
      FYNYGWIWKLVRVJVKXIYLBOZXJXIYDYEYOVFVGBLVLVMVQVNVSVTRWAWBWGWPWJWLWMWDWEWPW
      JOWFWDWEQWOWILMWDXJXIWOWIOWEXLXKABWCVTSRSVB $.
      $( [15-Apr-2008] $) $( [18-May-2007] $)

    $( The balls of the standard real metric space are included in the open
       real intervals. $)
    blssioo $p |- ran ( ball ` D ) C_ ran (,) $=
      ( vw vv vu vt vs crn cioo cv co wceq cc0 wrex cr cxr wcel wa eqeq1d ax-mp
      cbl cfv cpnf clt wbr wi repos w3a cmin bl2ioo resubcl readdcl rexr opreq1
      caddc opreq2 rcla42ev 3expia syl2an syl11anc 3adant3 sylbid 3expb sylan2b
      r19.23aivv cxp wfn wb cpw cme remet remetba blf ffn oprvelrn ioof 3imtr4i
      wf ssriv ) CAUAUBZHZIHZDJZEJZVTKZCJZLZEMUCIKZNDONZFJZGJZIKZWFLZGPNFPNZWFW
      AQZWFWBQZWGWNDEOWHWDWHQWCOQZWDOQZMWDUDUEZRWGWNUFZWDUGWQWRWSWTWQWRWSUHZWGW
      CWDUIKZWCWDUOKZIKZWFLZWNXAWEXDWFWCWDABUJSWQWRXEWNUFZWSWQWRRXBOQZXCOQZXFWC
      WDUKWCWDULXGXBPQZXCPQZXFXHXBUMXCUMXIXJXEWNWMXEXBWKIKZWFLFGXBXCPPWJXBLWLXK
      WFWJXBWKIUNSWKXCLXKXDWFWKXCXBIUPSUQURUSUTVAVBVCVDVEVTOWHVFZVGZWOWIVHXLOVI
      ZVTVRZXMAVJQXOABVKAOABVLVMTXLXNVTVNTDEOWHWFVTVOTIPPVFZVGZWPWNVHXPXNIVRXQV
      PXPXNIVNTFGPPWFIVOTVQVS $.
      $( [8-May-2007] $)

    $( Lemma for ~ tgioo .  An open interval includes a ball around any of its
       points. _Warning:  The HTML proof page is 0.6MB in size_. $)
    tgioolem $p |- ( ( t e. RR* /\ s e. RR* ) -> ( v e. ( t (,) s ) -> E. u e.
          RR ( 0 < u /\ ( v ( ball ` D ) u ) C_ ( t (,) s ) ) ) ) $=
      ( wcel wa cioo co cr clt wbr wss cpnf wceq cmnf adantr ex c1 cxr w3a wrex
      cv cc0 cbl cfv elioo2 w3o wi elxr cmin ancoms ad2ant2r 3adantr3 ad2ant2lr
      resubcl posdif biimp3a 3expb adantlr caddc simp2 3adant3 bl2ioo syl111anc
      cc recn nncan syl2an opreq1d eqtrd simplll simpr readdcl syl11anc simpllr
      cle 3jca wb simplr leaddsub2 3ad2antr1 biimpar rexr iooss2 syl3an eqsstrd
      sylc breq2 opreq2 sseq1d anbi12d rcla4ev syl12anc 3com12 adantll 3adantr2
      pncan3 opreq2d simpll lesub biimpa iooss1 lecasei syl pnfge pnfxr mp3anl3
      adantl mpdan ad2antlr 3sstr4d wn nltmnf notbid mpbird pm2.21d a1d 3jaodan
      3impd pnfnlt breq1 adantrr mnfle mnfxr mp3anl1 opreq1 ad2antrr 1re ioomax
      ioossre sseqtr4i a1i lt01 mp3an23 3ad2ant1 opreq12 jctil sylancr 3jaoian
      syl2anb sylbid ) CUDZUAGZEUDZUAGZHAUDZUUDUUFIJZGUUHKGZUUDUUHLMZUUHUUFLMZU
      BZUEBUDZLMZUUHUUNDUFUGZJZUUINZHZBKUCZUUDUUFUUHUHUUEUUDKGZUUDOPZUUDQPZUIUU
      FKGZUUFOPZUUFQPZUIZUUMUUTUJZUUGUUDUKUUFUKUVAUVGUVHUVBUVCUVAUVDUVHUVEUVFUV
      AUVDHZUUMUUTUVIUUMHZUUTUUHUUDULJZUUFUUHULJZUVIUUJUUKUVKKGZUULUVAUUJUVMUVD
      UUKUUJUVAUVMUUHUUDUQUMZUNUOZUVIUUJUUKUVLKGZUULUVDUUJUVPUVAUUKUUFUUHUQZUPU
      OZUVJUVKUVLVRMZHZUVMUEUVKLMZUUHUVKUUPJZUUINZUUTUVJUVMUVSUVORUVJUWAUVSUVIU
      UJUUKUWAUULUVAUUJUUKHZUWAUVDUVAUUJUUKUWAUVAUUJUUKUWAUUDUUHURUSZUTZVAUORUV
      TUWBUUDUUHUVKVBJZIJZUUIUVJUWBUWHPZUVSUVIUUJUUKUWIUULUVAUWDUWIUVDUVAUUJUUK
      UWIUVAUUJUUKUBZUWBUUHUVKULJZUWGIJZUWHUWJUUJUVMUWAUWBUWLPUVAUUJUUKVCUVAUUJ
      UVMUUKUVNVDUWEUUHUVKDFVEVFUWJUWKUUDUWGIUVAUUJUWKUUDPZUUKUUJUVAUWMUUJUUHVG
      GZUUDVGGUWMUVAUUHVHZUUDVHUUHUUDVIVJUMVDVKVLUTZVAUORUVTUVAUWGKGZUVDUBUWGUU
      FVRMZUWHUUINZUVTUVAUWQUVDUVAUVDUUMUVSVMUVJUWQUVSUVIUUJUUKUWQUULUVAUUJUWQU
      VDUUKUVAUUJHZUUJUVMUWQUVAUUJVNUVNUUHUVKVOVPZUNUORUVAUVDUUMUVSVQVSUVJUWRUV
      SUVIUUKUUJUWRUVSVTZUULUVIUUJHZUUJUVMUVDUXBUVIUUJVNZUVAUUJUVMUVDUVNVAUVAUV
      DUUJWAUUHUVKUUFWBVFWCWDUVAUUEUWQUWGUAGZUVDUUGUWRUWSUJUUDWEZUWGWEZUUFWEZUU
      EUXEUUGUBUWRUWSUUDUWGUUFWFSWGWIWHUUSUWAUWCHBUVKKUUNUVKPZUUOUWAUURUWCUUNUV
      KUELWJUXIUUQUWBUUIUUNUVKUUHUUPWKWLWMWNZWOUVJUVLUVKVRMZHZUVPUEUVLLMZUUHUVL
      UUPJZUUINZUUTUVJUVPUXKUVRRUVJUXMUXKUVIUUJUULUXMUUKUVDUUJUULHZUXMUVAUVDUUJ
      UULUXMUUJUVDUULUXMUUJUVDUULUXMUUHUUFURUSWPZUTZWQWRRUXLUXNUUHUVLULJZUUFIJZ
      UUIUVJUXNUXTPZUXKUVIUUJUULUYAUUKUVDUXPUYAUVAUVDUUJUULUYAUVDUUJUULUBZUXNUX
      SUUHUVLVBJZIJZUXTUYBUUJUVPUXMUXNUYDPUVDUUJUULVCUVDUUJUVPUULUVQVDUXQUUHUVL
      DFVEVFUYBUYCUUFUXSIUVDUUJUYCUUFPZUULUUJUVDUYEUUJUWNUUFVGGUYEUVDUWOUUFVHUU
      HUUFWSVJUMVDWTVLUTZWQWRRUXLUVAUXSKGZUVDUBUUDUXSVRMZUXTUUINZUXLUVAUYGUVDUV
      AUVDUUMUXKVMUVJUYGUXKUVIUUJUUKUYGUULUVDUUJUYGUVAUUKUVDUUJHZUUJUVPUYGUVDUU
      JVNUVQUUHUVLUQVPZUPUORUVAUVDUUMUXKVQVSUVJUXKUYHUVIUUKUUJUXKUYHVTZUULUXCUV
      PUUJUVAUYLUVDUUJUVPUVAUVQWQUXDUVAUVDUUJXAUVLUUHUUDXBVFWCXCUVAUUEUYGUXSUAG
      ZUVDUUGUYHUYIUJUXFUXSWEZUXHUUEUYMUUGUBUYHUYIUUDUXSUUFXDSWGWIWHUUSUXMUXOHB
      UVLKUUNUVLPZUUOUXMUURUXOUUNUVLUELWJUYOUUQUXNUUIUUNUVLUUHUUPWKWLWMWNZWOXES
      UVAUVEHZUUMUUTUYQUUJUUKUUTUULUYQUWDHZUVMUWAUWCUUTUVAUUJUVMUVEUUKUVNUNUVAU
      WDUWAUVEUWFVAUYRUWHUUDOIJZUWBUUIUVAUUJUWHUYSNZUVEUUKUWTUUEUXEUYTUVAUUEUUJ
      UXFRUWTUWQUXEUXAUXGXFUUEUXEHUWGOVRMZUYTUXEVUAUUEUWGXGXJUUEUXEOUAGVUAUYTXH
      UUDUWGOWFXIXKVPUNUVAUWDUWIUVEUWPVAUVEUUIUYSPUVAUWDUUFOUUDIWKXLXMUXJWOUOSU
      VFUVHUVAUVFUUJUUKUULUUTUVFUUJUUKUULUUTUJZUJZUVFUUJHZVUBUUKVUDUULUUTVUDUUL
      XNZUUHQLMZXNZUUJVUGUVFUUJUUHUAGZVUGUUHWEZUUHXOXFXJUVFVUEVUGVTUUJUVFUULVUF
      UUFQUUHLWJXPRXQXRXSSYAZXJXTUVBUVHUVGUVBUUJUUKUULUUTUVBUUJVUCUVBUUJHZUUKVU
      BVUKUUKXNZOUUHLMZXNZUUJVUNUVBUUJVUHVUNVUIUUHYBXFXJUVBVULVUNVTUUJUVBUUKVUM
      UUDOUUHLYCXPRXQXRSYARUVCUVDUVHUVEUVFUVCUVDHZUUMUUTVUOUUJUULUUTUUKVUOUXPHZ
      UVPUXMUXOUUTUVDUUJUVPUVCUULUVQUPUVDUXPUXMUVCUXRWQVUPUXTQUUFIJZUXNUUIUVDUX
      PUXTVUQNZUVCUVDUXPHUYMUUGVURUVDUUJUYMUULUYJUYGUYMUYKUYNXFYDUVDUUGUXPUXHRU
      YMUUGHQUXSVRMZVURUYMVUSUUGUXSYERQUAGUYMUUGVUSVURYFQUXSUUFXDYGXKVPWQUVDUXP
      UYAUVCUYFWQUVCUUIVUQPUVDUXPUUDQUUFIYHYIXMUYPWOWRSUVCUVEHZUUMUUTVUTUUMHZTK
      GZUETLMZUUHTUUPJZUUINZHZUUTYJVVAVVEVVCVVAUUHTULJZUUHTVBJZIJZQOIJZVVDUUIVV
      IVVJNVVAVVIKVVJVVGVVHYLYKYMYNUUMVVDVVIPZVUTUUJUUKVVKUULUUJVVBVVCVVKYJYOUU
      HTDFVEYPYQXJVUTUUIVVJPUUMUUDQUUFOIYRRXMYOYSUUSVVFBTKUUNTPZUUOVVCUURVVEUUN
      TUELWJVVLUUQVVDUUIUUNTUUHUUPWKWLWMWNYTSUVFUVHUVCVUJXJXTUUAUUBUUC $.
      $( [21-May-2007] $)

    ${
      $d w J $.
      tgioo.2 $e |- J = ( MetOpen ` D ) $.
      $( The topology generated by open intervals of reals is the same as the
         open sets of the standard metric space on the reals. $)
      tgioo $p |- ( topGen ` ran (,) ) = J $=
        ( vu vv vt vs cfv cioo wcel wss wceq ax-mp cv cr wa wrex cxr wb cbl crn
        vw ctg ctb cme remet copn eqid blbas retopbas blssioo cc0 clt wral cuni
        wbr co elssuni unirnioo syl6sseq cxp wfn cpw ioof ffn oprvelrn tgioolem
        wi wf adantr eleq2 adantl sseq2 anbi2d rexbidv 3imtr3d r19.23aivv sylbi
        ex r19.21aiv remetba isopn4 sylanbrc ssriv tgbl sseqtr4i 2basgen eqtr3i
        mp4an ) AUAIZUBZUDIZJUBZUDIZBWLUEKZWNUEKWLWNLWNWMLWMWOMAUFKZWPACUGZAAUH
        IZWSUIUJNUKACULWNBWMUCWNBUCOZWNKZWTPLZUMEOZUNUQZFOZXCWKURZWTLZQZEPRZFWT
        UOZWTBKZXAWTWNUPPWTWNUSUTVAXAXIFWTXAGOZHOZJURZWTMZHSRGSRZXEWTKZXIVIZJSS
        VBZVCZXAXPTXSPVDZJVJXTVEXSYAJVFNGHSSWTJVGNXOXRGHSSXLSKXMSKQZXOXRYBXOQXE
        XNKZXDXFXNLZQZEPRZXQXIYBYCYFVIXOFEGAHCVHVKXOYCXQTYBXNWTXEVLVMXOYFXITYBX
        OYEXHEPXOYDXGXDXNWTXFVNVOVPVMVQVTVRVSWAWQXKXBXJQTWRFEWTABPACWBDWCNWDWEW
        QWMBMWRABDWFNZWGWLWNWHWJYGWI $.
        $( [7-May-2007] $)

      $( ` QQ ` is dense in ` RR ` . $)
      qdensere2 $p |- ( ( cls ` J ) ` QQ ) = RR $=
        ( cq cioo crn ctg cfv ccl cr tgioo fveq2i fveq1i qdensere eqtr3i ) EFGH
        IZJIZIEBJIZIKERSQBJABCDLMNOP $.
        $( [24-Aug-2007] $)
    $}
  $}

  ${
    $d x y z $.
    $( The standard topology on the reals is Hausdorff. $)
    rehaus $p |- ( topGen ` ran (,) ) e. Haus $=
      ( cioo crn ctg cfv cabs cmin ccom cxp cres copn eqid tgioo remet methausi
      cr cha eqeltri ) ABCDEFGOOHIZJDZPRSRKZSKZLRSRTMUANQ $.
      $( [28-Jun-2008] $) $( [8-Mar-2007] $)
  $}

  ${
    $d X x y z $.  $d X u v w $.  $d D u v w $.  $d u x y z $.  $d v x y z $.
    $d w x y z $.
    dscmet.1 $e |- X e. _V $.
    dscmet.2 $e |- D = { <. <. x , y >. , z >. | ( ( x e. X /\ y e. X ) /\
                  z = if ( x = y , 0 , 1 ) ) } $.
    $( The discrete metric on any set ` X ` .  Definition 1.1-8 of [Kreyszig]
       p. 8.  (Contributed by FL, 12-Oct-2006.) $)
    dscmet $p |- D e. Met $=
      ( cv wceq cc0 c1 cif cr wcel wa 0re 1re co caddc cle vw keepel a1i foprab
      vv vu elisseti eqeq1 ifbid eqeq2 oprabval2 eqeq1d wn wo simpl wne ax-1ne0
      eqif necomi df-ne mpbi pm2.21i orcanai ancoms eqcoms iftrue impbii syl6bb
      jaoi sylbi w3a wbr imdistani 0nn0 nn0addge1i opreqan12d syl5breqr equequ2
      equtr syl notbid biimpcd lt01 ltleii addge0i iffalse pm2.61ian eqbrtrd wi
      mp2an neeq1 biimprd 3imtr3g leidi ax-1cn addid2i syl6eq ex addid1i c2 2re
      1lt2 syl6eqr pm2.61dan a1d pm2.61i 3adant3 3adant2 3adant1 3brtr4d ismeti
      df-2 opreq12d ) UAUEUFDEFABCEEAHZBHZIZJKLZMDGXQMNXNENXOENOXPJKMPQUBUCUDUA
      HZENZUEHZENZOZXRXTDRZJIXRXTIZJKLZJIZYDYBYCYEJABCXRXTEEXQYEDXRXOIZJKLYEMYD
      JKMPQUBUGXNXRIXPYGJKXNXRXOUHUIXOXTIZYGYDJKXOXTXRUJUIGUKZULYFYDYDJYEJYEIYD
      JJIZOZYDUMZJKIZOZUNYDYDJJKURYKYDYNYDYJUOYMYLYDYMYDYDYMYDYDUNJKUPYMUMKJUQU
      SJKUTVAVBVCVDVIVJVEYDJKVFZVGVHXSYAUFHZENZVKZYEYPXRIZJKLZYPXTIZJKLZSRZYCYP
      XRDRZYPXTDRZSRTYEUUCTVLZYRYDUUFYDYEJUUCTYOYSYDJUUCTVLZYSYDOYSUUAOZUUGYSYD
      UUAUFUAUEVSVMUUHJJJSRUUCTJJPVNVOYSUUAYTJUUBJSYSJKVFZUUAJKVFZVPVQVTYSUMZYD
      OUUKUUAUMZOZUUGUUKYDUULYDUUKUULYDYSUUAUAUEUFVRWAWBVMUUMJKKSRZUUCTJKTVLZUU
      OJUUNTVLJKPQWCWDZUUPKKQQWEWJUUKUULYTKUUBKSYSJKWFZUUAJKWFZVPZVQVTWGWHYLYEK
      UUCTYDJKWFYSYLKUUCTVLZWIYSYLUUTYSYLOYSUULOZUUTYSYLUULYSXRXTUPZYPXTUPZYLUU
      LYSUVCUVBYPXRXTWKWLXRXTUTYPXTUTWMVMUVAKKUUCTKQWNZUVAUUCJKSRKYSUULYTJUUBKS
      UUIUURVPKWOWPWQVQVTWRUUKUUTYLUUKUUAUUTUUKUUAOZKKUUCTUVDUVEUUCKJSRKUUKUUAY
      TKUUBJSUUQUUJVPKWOWSWQVQUUMKWTUUCTKWTQXAXBWDUUMUUCUUNWTUUSXLXCVQXDXEXFWHX
      FUCXSYAYCYEIYQYIXGYRUUDYTUUEUUBSXSYQUUDYTIZYAYQXSUVFABCYPXREEXQYTDYPXOIZJ
      KLZYTMYSJKMPQUBUGXNYPIXPUVGJKXNYPXOUHUIZXOXRIUVGYSJKXOXRYPUJUIGUKVDXHYAYQ
      UUEUUBIZXSYQYAUVJABCYPXTEEXQUUBDUVHUUBMUUAJKMPQUBUGUVIYHUVGUUAJKXOXTYPUJU
      IGUKVDXIXMXJXK $.
      $( [18-Apr-2008] $) $( [9-Oct-2006] $)
  $}


