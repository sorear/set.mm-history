$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       Examples of metric spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d D x y z $.
    cnmet.1 $e |- D = ( abs o. - ) $.
    $( Value of the distance function of the metric space of complex
       numbers. $)
    cnmetdval $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( A D B ) = ( abs ` ( A - B ) ) ) $=
      ( cc wcel wa cop cabs cmin ccom cfv co cdm wceq cxp opelxpi wf subopr
      fdm ax-mp syl6eleqr wfun cr absf ffun fvco mp3an12 syl df-opr fveq1i
      eqtr fveq2i 3eqtr4g ) AEFBEFGZABHZIJKZLZUPJLZILZABCMZABJMZILUOUPJNZFZURUT
      OZUOUPEEPZVCABEEQVFEJRZVCVFOSVFEJTUAUBIUCZJUCZVDVEEUDIRVHUEEUDIUFUAVGVISV
      FEJUFUAUPIJUGUHUIVAUPCLURABCUJUPCUQDUKULVBUSIABJUJUMUN $.
      $( [7-Sep-2007] $) $( [9-Dec-2006] $)

    $( The base set of the metric for complex numbers. $)
    cnmetba $p |- CC = dom dom D $=
      ( cdm cc cxp cabs cmin ccom dmeqi cr wf wceq absf subopr fco mp2an fdm
      ax-mp eqtr dmxpid eqtr2 ) ACZCDDEZCDUBUCUBFGHZCZUCAUDBIUCJUDKZUEUCLDJFKUC
      DGKUFMNUCDJFGOPUCJUDQRSIDTUA $.
      $( [10-Sep-2007] $) $( [30-Aug-2007] $)

    $( The absolute value metric determines a metric space on the complex
       numbers.  This theorem provides a link between complex numbers and
       metrics spaces, making metric space theorems available for use with
       complex numbers.  (Contributed by FL, 9-Oct-2006.) $)
    cnmet $p |- D e. Met $=
      ( vx vy vz cc axcnex cxp cr wf cabs cmin ccom absf subopr fco mp2an wceq
      wb feq1 ax-mp mpbir cv wcel wa co cfv cc0 subclt abs00t syl cnmetdval
      eqcomd eqeq1d subeq0t 3bitr3d w3a caddc cle abs3dift abssubt opreq1d
      3adant2 breqtrd 3adant3 opreq12d 3coml 3brtr4d ismeti ) CDEAFGFFHZIAJZVJI
      KLMZJZFIKJVJFLJVMNOVJFIKLPQAVLRVKVMSBVJIAVLTUAUBCUCZFUDZDUCZFUDZUEZVNVPLU
      FZKUGZUHRZVSUHRZVNVPAUFZUHRVNVPRVRVSFUDWAWBSVNVPUIVSUJUKVRVTWCUHVRWCVTVNV
      PABULZUMUNVNVPUOUPVOVQEUCZFUDZUQZVTWEVNLUFKUGZWEVPLUFKUGZURUFZWCWEVNAUFZW
      EVPAUFZURUFZUSWGVTVNWELUFKUGZWIURUFZWJUSVNVPWEUTVOWFWOWJRVQVOWFUEWNWHWIUR
      VNWEVAVBVCVDVOVQWCVTRWFWDVEWFVOVQWMWJRWFVOVQUQWKWHWLWIURWFVOWKWHRVQWEVNAB
      ULVEWFVQWLWIRVOWEVPABULVCVFVGVHVI $.
      $( [7-Sep-2007] $) $( [6-Oct-2006] $)
  $}

  ${
    $d A f w x y z $.  $d B f w x y z $.  $d C w x y z $.  $d D w x y z $.
    $d J f w x y z $.  $d K f x y $.
    cncfmet.1 $e |- C = ( ( abs o. - ) |` ( A X. A ) ) $.
    cncfmet.2 $e |- D = ( ( abs o. - ) |` ( B X. B ) ) $.
    cncfmet.3 $e |- J = ( Open ` C ) $.
    cncfmet.4 $e |- K = ( Open ` D ) $.
    $( Relate complex function continuity to metric space continuity.
       (Contributed by Paul Chapman, 26-Nov-2007.) $)
    cncfmet $p |- ( ( A (_ CC /\ B (_ CC ) ->
                       ( A -cn-> B ) = ( J Cn K ) ) $=
      ( vf vx vw vz vy cc wss wa ccn co ccncf cv wcel wf cmin cabs cfv clt wbr
      wi wral crp wrex cdm cc0 cr wceq wb feq23 cxp ccom cin ssxp anidms df-ss
      sylib absf subopr fco mp2an fdm ax-mp ineq2i syl5eq cres dmeqi dmres
      eqtr dmeqd dmxpid syl6eq syl2an anbi1d w3a 3ad2ant1 eleq2d imbi1d adantr
      oprvalres opreqi adantl ssel anim12d imp eqid cnmetdval syl eqtrd
      3ad2antl1 breq1d ffvelrn ex 3ad2antl3 3ad2antl2 syldan imbi12d 3impb
      3expia pm5.74d bitrd ralbidv2 anbi2d rexbidv wex elrp anbi1i anass bitr
      exbii df-rex 3bitr4r syl6bb imbi2d ralbidv wal imbi1i impexp albii
      df-ral pm5.74da 3expa pm5.32da cme cnmet metres eqeltr metcn syl5bb
      elcncf bitr4d eqrdv eqcomd ) APQZBPQZRZEFSTZABUATZUUEKUUFUUGUUEKUBZUUFUCZ
      ABUUHUDZLUBZMUBZUETUFUGZNUBZUHUIZUUKUUHUGZUULUUHUGZUETUFUGZOUBZUHUIZUJZMA
      UKZNULUMZOULUKZLAUKZRZUUHUUGUCUUECUNZUNZDUNZUNZUUHUDZUOUUSUHUIZUOUUNUHUIZ
      UUKUULCTZUUNUHUIZUUPUUQDTZUUSUHUIZUJZMUVHUKZRZNUPUMZUJZOUPUKZLUVHUKZRZUVF
      UUIUUEUWEUUJUWDRUVFUUEUVKUUJUWDUVHAUQZUVJBUQUVKUUJURUUCUUDUVHUVJABUUHUSUU
      CUVHAAUTZUNAUUCUVGUWGUUCUWGUFUEVAZUNZVBZUWGUVGUUCUWGPPUTZVBZUWGUWJUUCUWGU
      WKQZUWLUWGUQUUCUWMAPAPVCVDUWGUWKVEVFUWIUWKUWGUWKUPUWHUDZUWIUWKUQPUPUFUDUW
      KPUEUDUWNVGVHUWKPUPUFUEVIVJUWKUPUWHVKVLZVMVNUVGUWHUWGVOZUNUWJCUWPGVPUWHUW
      GVQVRVNVSAVTWAZUUDUVJBBUTZUNBUUDUVIUWRUUDUWRUWIVBZUWRUVIUUDUWRUWKVBZUWRUW
      SUUDUWRUWKQZUWTUWRUQUUDUXABPBPVCVDUWRUWKVEVFUWIUWKUWRUWOVMVNUVIUWHUWRVOZU
      NUWSDUXBHVPUWHUWRVQVRVNVSBVTWAWBWCUUEUUJUWDUVEUUCUUDUUJUWDUVEURUUCUUDUUJW
      DZUWCUVDLUVHAUXCUUKUVHUCZUWCUJUUKAUCZUWCUJUXEUVDUJUXCUXDUXEUWCUXCUVHAUUKU
      UCUUDUWFUUJUWQWEZWFWGUXCUXEUWCUVDUXCUXERZUWCUVLUVCUJZOUPUKZUVDUXGUWBUXHOU
      PUXGUWAUVCUVLUXGUWAUVMUVBRZNUPUMZUVCUXGUVTUXJNUPUXGUVSUVBUVMUXGUVRUVAMUVH
      AUXGUULUVHUCZUVRUJUULAUCZUVRUJUXMUVAUJUXGUXLUXMUVRUXGUVHAUULUXCUWFUXEUXFW
      HWFWGUXGUXMUVRUVAUXCUXEUXMUVRUVAURZUXCUXEUXMUXNUXCUXEUXMRZRZUVOUUOUVQUUTU
      XPUVNUUMUUNUHUUCUUDUXOUVNUUMUQUUJUUCUXORZUVNUUKUULUWHTZUUMUXOUVNUXRUQUUCU
      XOUUKUULUWPTUXRUVNUUKUULAAUWHWICUWPUUKUULGWJVNWKUXQUUKPUCZUULPUCZRZUXRUUM
      UQUUCUXOUYAUUCUXEUXSUXMUXTAPUUKWLAPUULWLWMWNUUKUULUWHUWHWOZWPWQWRWSWTUXPU
      VPUURUUSUHUXCUXOUUPBUCZUUQBUCZRZUVPUURUQZUUJUUCUXOUYEUUDUUJUXOUYEUUJUXEUY
      CUXMUYDUUJUXEUYCABUUKUUHXAXBUUJUXMUYDABUULUUHXAXBWMWNXCUUDUUCUYEUYFUUJUUD
      UYERZUVPUUPUUQUWHTZUURUYEUVPUYHUQUUDUYEUUPUUQUXBTUYHUVPUUPUUQBBUWHWIDUXBU
      UPUUQHWJVNWKUYGUUPPUCZUUQPUCZRZUYHUURUQUUDUYEUYKUUDUYCUYIUYDUYJBPUUPWLBPU
      UQWLWMWNUUPUUQUWHUYBWPWQWRXDXEWTXFXGXHXIXJXKXLXMUUNULUCZUVBRZNXNUUNUPUCZU
      XJRZNXNUVCUXKUYMUYONUYMUYNUVMRZUVBRUYOUYLUYPUVBUUNXOXPUYNUVMUVBXQXRXSUVBN
      ULXTUXJNUPXTYAYBYCYDUUSULUCZUVCUJZOYEUUSUPUCZUXHUJZOYEUVDUXIUYRUYTOUYRUYS
      UVLRZUVCUJUYTUYQVUAUVCUUSXOYFUYSUVLUVCYGXRYHUVCOULYIUXHOUPYIYAYBYJXJXKYKY
      LXJCYMUCDYMUCUUIUWEURCUWPYMGUWHYMUCZUWPYMUCUWHUYBYNZUWHAYOVLYPDUXBYMHVUBU
      XBYMUCVUCUWHBYOVLYPLONMCDUUHEFUVHUVJUVHWOIUVJWOJYQVJYRLONMABUUHYSYTUUAUUB
      $.
      $( [27-Nov-2007] $) $( [11-Oct-2007] $)
  $}

  ${
    cncfmet1.1 $e |- D = ( abs o. - ) $.
    cncfmet1.2 $e |- J = ( Open ` D ) $.
    $( Relate complex function continuity to metric space continuity.
       (Contributed by Paul Chapman, 28-Nov-2007.) $)
    cncfmet1 $p |- ( CC -cn-> CC ) = ( J Cn J ) $=
      ( cc wss ccncf co ccn wceq ssid cabs cmin ccom cxp cres wrel cdm relco
      crn wf subopr frn ax-mp cr absf fdm sseqtr4 dmcosseq eqtr eqimssi
      relssres mp2an eqtr4 cncfmet ) EEFZUPEEGHBBIHJEKZUQEEAABBALMNZUREEOZPZCUR
      QURRZUSFUTURJLMSVAUSVAMRZUSMTZLRZFVAVBJVCEVDUSEMUAZVCEFUBUSEMUCUDEUELUAVD
      EJUFEUELUGUDUHLMUIUDVEVBUSJUBUSEMUGUDUJUKURUSULUMUNZVFDDUOUM $.
      $( [28-Nov-2007] $) $( [28-Nov-2007] $)
  $}

  ${
    $d x y z C $.
    cn2met.1 $e |- C = ( abs o. - ) $.
    cn2met.2 $e |- D = { <. <. x , y >. , z >. | ( ( x e. ( CC X. CC ) /\
             y e. ( CC X. CC ) ) /\ z = sup ( { ( ( 1st ` x ) C ( 1st ` y ) ) ,
                  ( ( 2nd ` x ) C ( 2nd ` y ) ) } , RR , < ) ) } $.
    $( The standard metric space on ` CC X. CC ` . $)
    cn2met $p |- D e. Met $=
      ( cc cnmetba cnmet metxp ) ABCDDEHHDFIZLDFJZMGK $.
      $( [7-Oct-2007] $) $( [21-Jun-2007] $)
  $}

  ${
    $d r s t u v w D $.  $d v w A $.  $d v w B $.
    remet.1 $e |- D = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    $( Value of the distance function of the metric space of real numbers. $)
    remetdval $p |- ( ( A e. RR /\ B e. RR ) ->
                  ( A D B ) = ( abs ` ( A - B ) ) ) $=
      ( cr wcel wa cop cabs cmin ccom cxp cres cfv co wceq opelxpi fvres syl
      cc eqid cnmetdval recnt syl2an df-opr syl5eqr eqtrd fveq1i eqtr syl5eq )
      AEFZBEFZGZABHZIJKZEELZMZNZABJOINZABCOZUMURUNUONZUSUMUNUPFURVAPABEEQUNUPUO
      RSUMABUOOZUSVAATFBTFVBUSPUKULABUOUOUAUBAUCBUCUDABUOUEUFUGUTUNCNURABCUEUNC
      UQDUHUIUJ $.
      $( [20-Sep-2007] $) $( [16-May-2007] $)

    $( The base set for the metric for real numbers. $)
    remetba $p |- RR = dom dom D $=
      ( cdm cr cxp cabs cmin ccom cres cin dmeqi dmres wss wceq cc axresscn
      ssxp mp2an wf absf subopr fco fdm ax-mp sseqtr4 df-ss mpbi 3eqtr dmxpid
      eqtr2 ) ACZCDDEZCDUKULUKFGHZULIZCULUMCZJZULAUNBKUMULLULUOMUPULNULOOEZUODO
      MZURULUQMPPDODOQRUQDUMSZUOUQNODFSUQOGSUSTUAUQODFGUBRUQDUMUCUDUEULUOUFUGUH
      KDUIUJ $.
      $( [20-Sep-2007] $) $( [30-Aug-2007] $)

    $( The absolute value metric determines a metric space on the reals. $)
    remet $p |- D e. Met $=
      ( cabs cmin ccom cr cxp cres cme wcel eqid cnmet metres ax-mp eqeltr ) AC
      DEZFFGHZIBPIJQIJPPKLPFMNO $.
      $( [21-Sep-2007] $) $( [10-Feb-2007] $)

    $( A ball in terms of an open interval of reals. $)
    bl2ioo $p |- ( ( A e. RR /\ B e. RR /\ 0 < B ) -> ( A ( ball ` D
                  ) B ) = ( ( A - B ) (,) ( A + B ) ) ) $=
      ( vw cr wcel cc0 clt wbr w3a cbl cfv co cmin caddc cioo cv wa wb cme
      remet remetba elbl mpanl1 3impb cxr elioo2t resubclt rexrt syl axaddrcl
      sylanc cabs wceq remetdval cc abssubt recnt syl2an eqtrd adantlr breq1d
      absdifltt 3coml 3expa bitr2d pm5.32da 3anass syl5bb bitrd 3adant3 bitr4d
      eqrdv ) AFGZBFGZHBIJZKZEABCLMNZABONZABPNZQNZVRERZVSGZWCFGZAWCCNZBIJZSZWCW
      BGZVOVPVQWDWHTZCUAGVOVPVQSWJCDUBWCCABFCDUCUDUEUFVOVPWIWHTVQVOVPSZWIWEVTWC
      IJZWCWAIJZKZWHVTUGGZWAUGGZWIWNTWKVTWAWCUHWKVTFGWOABUIVTUJUKWKWAFGWPABULWA
      UJUKUMWKWEWLWMSZSWHWNWKWEWQWGWKWESZWGWCAONUNMZBIJZWQWRWFWSBIVOWEWFWSUOVPV
      OWESWFAWCONUNMZWSAWCCDUPAUQGWCUQGXAWSUOVOWEAWCURAUSWCUSUTVAVBVCVOVPWEWTWQ
      TZWEVOVPXBWCABVDVEVFVGVHWEWLWMVIVJVKVLVMVN $.
      $( [21-Sep-2007] $) $( [18-May-2007] $)

    $( An open interval of reals in terms of a ball. $)
    ioo2bl $p |- ( ( A e. RR /\ B e. RR /\ A < B ) -> ( A (,) B ) =
     ( ( ( A + B ) / 2 ) ( ball ` D ) ( ( B - A ) / 2 ) ) ) $=
      ( cr wcel clt wbr w3a cioo co caddc c2 cdiv cmin cbl cfv wceq cc0 bl2ioo
      wa axaddrcl rehalfclt syl 3adant3 resubclt wb posdift ancoms halfpos2t
      bitrd biimp3a syl3anc cc cmul pnncant 3anidm23 2timest adantl eqtr4d
      opreq1d 2cn wne 2ne0 divsubdirt mpan2 mp3an3 axaddcl subclt sylanc
      divcan3t mp3an13 3eqtr3d ppncant 3anidm13 adantr divdirt opreq12d recnt
      syl2an eqtr2d 3com12 axaddcom ) AEFZBEFZABGHZIZABJKZBALKZMNKZBAOKZMNKZCPQ
      ZKZABLKZMNKZWLWMKWEWDWFWHWNRWEWDWFIZWNWJWLOKZWJWLLKZJKZWHWJEFZWLEFZSWLGHZ
      WNWTRWQWJWLCDTWEWDXAWFWEWDUAZWIEFXABAUBWIUCUDUEWEWDXBWFXDWKEFZXBBAUFZWKUC
      UDUEWEWDWFXCXDWFSWKGHZXCWDWEWFXGUGABUHUIXDXEXGXCUGXFWKUJUDUKULUMWEWDWTWHR
      ZWFBUNFZAUNFZXHWEWDXIXJUAZWRAWSBJXKWIWKOKZMNKZMAUOKZMNKZWRAXKXLXNMNXKXLAA
      LKZXNXIXJXLXPRBAAUPUQXJXNXPRXIAURUSUTVAWIUNFZWKUNFZXMWRRZXKXQXRMUNFZXSVBX
      QXRXTIZMSVCZXSVDWIWKMVEVFVGBAVHZBAVIZVJXJXOARZXIXTXJYBYEVBVDMAVKVLUSVMXKW
      IWKLKZMNKZMBUOKZMNKZWSBXKYFYHMNXKYFBBLKZYHXIXJYFYJRBABVNVOXIYHYJRXJBURVPU
      TVAXQXRYGWSRZXKXQXRXTYKVBYAYBYKVDWIWKMVQVFVGYCYDVJXIYIBRZXJXTXIYBYLVBVDMB
      VKVLVPVMVRBVSZAVSZVTUEWAWBWGWPWJWLWMWDWEWPWJRWFWDWEUAWOWIMNXJXIWOWIRWDWEA
      BWCYNYMVTVAUEVAUT $.
      $( [?] $) $( [18-May-2007] $)

    $( The balls of the standard real metric space are included in the open
       real intervals. $)
    blssioo $p |- ran ( ball ` D ) (_ ran (,) $=
      ( vw vv vu vt vs cbl cfv crn cioo cv co wceq cc0 cpnf wrex cr cxr wcel
      clt wbr wa wi w3a cmin caddc bl2ioo eqeq1d opreq1 opreq2 rcla42ev 3expia
      rexrt syl2an resubclt axaddrcl sylanc 3adant3 sylbid 3expb repos sylan2b
      r19.23aivv cxp wfn wb cpw wf cme remet remetba blf ax-mp ffn oprvalelrn
      ioof 3imtr4 ssriv ) CAHIZJZKJZDLZELZVTMZCLZNZEOPKMZQDRQZFLZGLZKMZWFNZGSQF
      SQZWFWATZWFWBTZWGWNDERWHWCRTZWDRTZOWDUAUBZUCWGWNUDZWDWHTWQWRWSWTWQWRWSUEZ
      WGWCWDUFMZWCWDUGMZKMZWFNZWNXAWEXDWFWCWDABUHUIWQWRXEWNUDZWSXBRTZXCRTZXFWQW
      RUCXBSTZXCSTZXFXGXHXIXJXEWNWMXEXBWKKMZWFNFGXBXCSSWJXBNWLXKWFWJXBWKKUJUIWK
      XCNXKXDWFWKXCXBKUKUIULUMXBUNXCUNUOWCWDUPWCWDUQURUSUTVAWDVBVCVDVTRWHVEZVFZ
      WOWIVGXLRVHZVTVIZXMAVJTXOABVKARABVLVMVNXLXNVTVOVNDERWHWFVTVPVNKSSVEZVFZWP
      WNVGXPXNKVIXQVQXPXNKVOVNFGSSWFKVPVNVRVS $.
      $( [18-Oct-2007] $) $( [8-May-2007] $)

    $( Lemma for ~ tgioo .  An open interval includes a ball around any of its
       points.  _Warning:  The HTML proof page is 0.6MB in size_. $)
    tgioolem $p |- ( ( t e. RR* /\ s e. RR* ) -> ( v e. ( t (,) s ) -> E. u e.
          RR ( 0 < u /\ ( v ( ball ` D ) u ) (_ ( t (,) s ) ) ) ) $=
      ( cv cxr wcel wa cioo co cr clt wbr w3a cc0 cbl cfv wss wrex elioo2t
      cpnf wceq cmnf w3o wi cmin resubclt ancoms ad2ant2r 3adantr3 ad2ant2lr
      cle breq2 opreq2 sseq1d anbi12d rcla4ev adantr posdift biimp3a 3expb
      adantlr caddc bl2ioo 3simp2 3adant3 syl3anc cc nncant recnt syl2an
      opreq1d eqtrd iooss2 ex rexrt syl3an pm3.26 ad2antrr axaddrcl pm3.27
      sylanc 3jca wb leaddsub2t simplr 3ad2antr1 biimpar sylc eqsstrd jca
      3com12 adantll 3adantr2 pncan3t opreq2d iooss1 lesubt simpll biimpa
      lecase pnfget adantl pnfxr mp3anl3 mpdan syl ad2antlr 3sstr4d wn nltmnft
      negbid mpbird pm2.21d a1d 3impd 3jaodan pnfnltt breq1 mnflet mnfxr
      mp3anl1 adantrr opreq1 c1 1re a1i ioossre ioomax sseqtr4 lt01 mp3an23
      3ad2ant1 opreq12 jctil 3jaoian elxr syl2anb sylbid ) CGZHIZEGZHIZJAGZUUBU
      UDKLZIUUFMIZUUBUUFNOZUUFUUDNOZPZQBGZNOZUUFUULDRSZLZUUGTZJZBMUAZUUBUUDUUFU
      BUUBMIZUUBUCUDZUUBUEUDZUFUUDMIZUUDUCUDZUUDUEUDZUFZUUKUURUGZUUCUUEUUSUVEUV
      FUUTUVAUUSUVBUVFUVCUVDUUSUVBJZUUKUURUVGUUKJZUURUUFUUBUHLZUUDUUFUHLZUVGUUH
      UUIUVIMIZUUJUUSUUHUVKUVBUUIUUHUUSUVKUUFUUBUIUJZUKULZUVGUUHUUIUVJMIZUUJUVB
      UUHUVNUUSUUIUUDUUFUIZUMULZUVKQUVINOZUUFUVIUUNLZUUGTZJZUURUVHUVIUVJUNOZJZU
      UQUVTBUVIMUULUVIUDZUUMUVQUUPUVSUULUVIQNUOUWCUUOUVRUUGUULUVIUUFUUNUPUQURUS
      ZUVHUVKUWAUVMUTUWBUVQUVSUVHUVQUWAUVGUUHUUIUVQUUJUUSUUHUUIJZUVQUVBUUSUUHUU
      IUVQUUSUUHUUIUVQUUBUUFVAVBZVCZVDULUTUWBUVRUUBUUFUVIVELZKLZUUGUVHUVRUWIUDZ
      UWAUVGUUHUUIUWJUUJUUSUWEUWJUVBUUSUUHUUIUWJUUSUUHUUIPZUVRUUFUVIUHLZUWHKLZU
      WIUUHUVKUVQUVRUWMUDUWKUUFUVIDFVFUUSUUHUUIVGUUSUUHUVKUUIUVLVHUWFVIUWKUWLUU
      BUWHKUUSUUHUWLUUBUDZUUIUUHUUSUWNUUFVJIZUUBVJIUWNUUHUUSUUFUUBVKUUFVLZUUBVL
      VMUJVHVNVOVCZVDULUTUUSUWHMIZUVBPUWHUUDUNOZUWIUUGTZUWBUUCUWHHIZUUEUWSUWTUG
      UUSUWRUVBUUCUXAUUEPUWSUWTUUBUWHUUDVPVQUUBVRZUWHVRZUUDVRZVSUWBUUSUWRUVBUVG
      UUSUUKUWAUUSUVBVTZWAUVHUWRUWAUVGUUHUUIUWRUUJUUSUUHUWRUVBUUIUUHUVKUWRUUSUU
      HJZUUFUVIWBUUSUUHWCUVLWDZUKULUTUVGUVBUUKUWAUUSUVBWCZWAWEUVHUWSUWAUVGUUIUU
      HUWSUWAWFZUUJUUHUVKUVBUXIUVGUUHJZUUFUVIUUDWGUVGUUHWCZUUSUUHUVKUVBUVLVDUUS
      UVBUUHWHVIWIWJWKWLWMWDUVNQUVJNOZUUFUVJUUNLZUUGTZJZUURUVHUVJUVIUNOZJZUUQUX
      OBUVJMUULUVJUDZUUMUXLUUPUXNUULUVJQNUOUXRUUOUXMUUGUULUVJUUFUUNUPUQURUSZUVH
      UVNUXPUVPUTUXQUXLUXNUVHUXLUXPUVGUUHUUJUXLUUIUVBUUHUUJJZUXLUUSUVBUUHUUJUXL
      UUHUVBUUJUXLUUHUVBUUJUXLUUFUUDVAVBWNZVCZWOWPUTUXQUXMUUFUVJUHLZUUDKLZUUGUV
      HUXMUYDUDZUXPUVGUUHUUJUYEUUIUVBUXTUYEUUSUVBUUHUUJUYEUVBUUHUUJPZUXMUYCUUFU
      VJVELZKLZUYDUUHUVNUXLUXMUYHUDUYFUUFUVJDFVFUVBUUHUUJVGUVBUUHUVNUUJUVOVHUYA
      VIUYFUYGUUDUYCKUVBUUHUYGUUDUDZUUJUUHUVBUYIUWOUUDVJIUYIUUHUVBUUFUUDWQUWPUU
      DVLVMUJVHWRVOVCZWOWPUTUUSUYCMIZUVBPUUBUYCUNOZUYDUUGTZUXQUUCUYCHIZUUEUYLUY
      MUGUUSUYKUVBUUCUYNUUEPUYLUYMUUBUYCUUDWSVQUXBUYCVRZUXDVSUXQUUSUYKUVBUVGUUS
      UUKUXPUXEWAUVHUYKUXPUVGUUHUUIUYKUUJUVBUUHUYKUUSUUIUUHUVNUYKUVBUUHJZUUFUVJ
      UIUVBUUHWCUVOWDZUMULUTUVGUVBUUKUXPUXHWAWEUVHUXPUYLUVGUUIUUHUXPUYLWFZUUJUV
      NUUHUUSUYRUXJUVJUUFUUBWTUVBUUHUVNUUSUVOWOUXKUUSUVBUUHXAVIWIXBWKWLWMWDXCVQ
      UUSUVCJZUUKUURUYSUUHUUIUURUUJUVKUVTUURUYSUWEJZUWDUUSUUHUVKUVCUUIUVLUKUYTU
      VQUVSUUSUWEUVQUVCUWGVDUYTUWIUUBUCKLZUVRUUGUUSUUHUWIVUATZUVCUUIUUCUXAVUBUX
      FUUCUXAJUWHUCUNOZVUBUXAVUCUUCUWHXDXEUUCUXAUCHIVUCVUBXFUUBUWHUCVPXGXHUUSUU
      CUUHUXBUTUXFUWRUXAUXGUXCXIWDUKUUSUWEUWJUVCUWQVDUVCUUGVUAUDUUSUWEUUDUCUUBK
      UPXJXKWMWDULVQUVDUVFUUSUVDUUHUUIUUJUURUVDUUHUUIUUJUURUGZUGZUVDUUHJZVUDUUI
      VUFUUJUURVUFUUJXLZUUFUENOZXLZUUHVUIUVDUUHUUFHIZVUIUUFVRZUUFXMXIXEUVDVUGVU
      IWFUUHUVDUUJVUHUUDUEUUFNUOXNUTXOXPXQVQXRZXEXSUUTUVFUVEUUTUUHUUIUUJUURUUTU
      UHVUEUUTUUHJZUUIVUDVUMUUIXLZUCUUFNOZXLZUUHVUPUUTUUHVUJVUPVUKUUFXTXIXEUUTV
      UNVUPWFUUHUUTUUIVUOUUBUCUUFNYAXNUTXOXPVQXRUTUVAUVBUVFUVCUVDUVAUVBJZUUKUUR
      VUQUUHUUJUURUUIUVNUXOUURVUQUXTJZUXSUVBUUHUVNUVAUUJUVOUMVURUXLUXNUVBUXTUXL
      UVAUYBWOVURUYDUEUUDKLZUXMUUGUVBUXTUYDVUSTZUVAUYNUUEVUTUVBUXTJUYNUUEJUEUYC
      UNOZVUTUYNVVAUUEUYCYBUTUEHIUYNUUEVVAVUTYCUEUYCUUDWSYDXHUVBUUHUYNUUJUYPUYK
      UYNUYQUYOXIYEUVBUUEUXTUXDUTWDWOUVBUXTUYEUVAUYJWOUVAUUGVUSUDUVBUXTUUBUEUUD
      KYFWAXKWMWDWPVQUVAUVCJZUUKUURYGMIZQYGNOZUUFYGUUNLZUUGTZJZUURVVBUUKJZUUQVV
      GBYGMUULYGUDZUUMVVDUUPVVFUULYGQNUOVVIUUOVVEUUGUULYGUUFUUNUPUQURUSVVCVVHYH
      YIVVHVVFVVDVVHUUFYGUHLZUUFYGVELZKLZUEUCKLZVVEUUGVVLVVMTVVHVVLMVVMVVJVVKYJ
      YKYLYIUUKVVEVVLUDZVVBUUHUUIVVNUUJUUHVVCVVDVVNYHYMUUFYGDFVFYNYOXEVVBUUGVVM
      UDUUKUUBUEUUDUCKYPUTXKYMYQWDVQUVDUVFUVAVULXEXSYRUUBYSUUDYSYTUUA $.
      $( [22-Oct-2007] $) $( [21-May-2007] $)

    ${
      $d w J $.
      tgioo.2 $e |- J = ( Open ` D ) $.
      $( The topology generated by open intervals of reals is the same as the
         open sets of the standard metric space on the reals. $)
      tgioo $p |- ( topGen ` ran (,) ) = J $=
        ( vw vu vv vt vs cbl cfv crn ctg cioo ctb wcel wa wss wceq cme remet
        copn eqid blbas ax-mp retopbas pm3.2i blssioo cv cr cc0 clt wbr co
        wrex wral cuni elssuni unirnioo syl6ss cxr wi cxp wfn wb cpw wf ioof
        ffn oprvalelrn tgioolem adantr eleq2 adantl sseq2 anbi2d rexbidv
        3imtr3d ex r19.23aivv sylbi r19.21aiv jca remetba isopn4 sylibr ssriv
        tgbl sseqtr4 2basgent mp2an eqtr3 ) AJKZLZMKZNLZMKZBWNOPZWPOPZQWNWPRZWP
        WORZQWOWQSWRWSATPZWRACUAZAAUBKZXDUCUDUEUFUGWTXAACUHWPBWOEWPBEUIZWPPZXEU
        JRZUKFUIZULUMZGUIZXHWMUNZXERZQZFUJUOZGXEUPZQZXEBPZXFXGXOXFXEWPUQUJXEWPU
        RUSUTXFXNGXEXFHUIZIUIZNUNZXESZIVAUOHVAUOZXJXEPZXNVBZNVAVAVCZVDZXFYBVEYE
        UJVFZNVGYFVHYEYGNVIUEHIVAVAXENVJUEYAYDHIVAVAXRVAPXSVAPQZYAYDYHYAQXJXTPZ
        XIXKXTRZQZFUJUOZYCXNYHYIYLVBYAGFHAICVKVLYAYIYCVEYHXTXEXJVMVNYAYLXNVEYHY
        AYKXMFUJYAYJXLXIXTXEXKVOVPVQVNVRVSVTWAWBWCXBXQXPVEXCGFXEABUJACWDDWEUEWF
        WGXBWOBSXCABDWHUEZWIUGWNWPWJWKYMWL $.
        $( [5-Dec-2007] $) $( [7-May-2007] $)

      $( ` QQ ` is dense in ` RR ` . $)
      qdensere2 $p |- ( ( cls ` J ) ` QQ ) = RR $=
        ( cq cioo crn ctg cfv ccl cr tgioo fveq2i fveq1i qdensere eqtr3 ) EFGHI
        ZJIZIEBJIZIKERSQBJABCDLMNOP $.
        $( [6-Dec-2007] $) $( [24-Aug-2007] $)
    $}
  $}

  ${
    $d x y z $.
    $( The standard topology on the reals is Hausdorff. $)
    rehaus $p |- ( topGen ` ran (,) ) e. Haus $=
      ( cioo crn ctg cfv cabs cmin ccom cr cxp cres copn cha eqid tgioo remet
      methausi eqeltr ) ABCDEFGHHIJZKDZLRSRMZSMZNRSRTOUAPQ $.
      $( [?] $) $( [8-Mar-2007] $)
  $}

  ${
    $d X x y z $.  $d X u v w $.  $d D u v w $.  $d u x y z $.  $d v x y z $.
    $d w x y z $.
    dscmet.1 $e |- X e. V $.
    dscmet.2 $e |- D = { <. <. x , y >. , z >. | ( ( x e. X /\ y e. X ) /\
                  z = if ( x = y , 0 , 1 ) ) } $.
    $( The discrete metric on any set ` X ` .  Definition 1.1-8 of [Kreyszig]
       p. 8.  (Contributed by FL, 12-Oct-2006.) $)
    dscmet $p |- D e. Met $=
      ( vw vv vu cv wceq cc0 c1 cif cr wcel wa 0re 1re keepel a1i foprab co
      elisseti eqeq1 ifbid eqeq2 oprabval2 eqeq1d wn wo eqif pm3.26 wne ax1ne0
      necom mpbi df-ne pm2.21i orcanai ancoms jaoi sylbi eqcoms iftrue impbi
      syl6bb w3a caddc cle wbr equtr imdistani opreqan12d 0nn0 nn0addge1
      syl5breqr syl equequ2 negbid biimpcd iffalse lt01 ltlei addge0 mp2an
      pm2.61ian eqbrtrd wi neeq1 biimprd 3imtr3g ax1cn addid2 syl6eq leid ex
      addid1 c2 df-2 syl6eqr 2re 1lt2 pm2.61dan a1d pm2.61i 3adant3 3adant2
      3adant1 opreq12d 3brtr4d ismeti ) HIJDEFABCEEAKZBKZLZMNOZPDGXQPQXNEQXOEQR
      XPMNPSTUAUBUCHKZEQZIKZEQZRZXRXTDUDZMLXRXTLZMNOZMLZYDYBYCYEMABCXRXTEEXQYED
      XRXOLZMNOYEPYDMNPSTUAUEXNXRLXPYGMNXNXRXOUFUGXOXTLZYGYDMNXOXTXRUHUGGUIZUJY
      FYDYDMYEMYELYDMMLZRZYDUKZMNLZRZULYDYDMMNUMYKYDYNYDYJUNYMYLYDYMYDYDYMYDYDU
      LMNUOZYMUKNMUOYOUPNMUQURMNUSURUTVAVBVCVDVEYDMNVFZVGVHXSYAJKZEQZVIZYEYQXRL
      ZMNOZYQXTLZMNOZVJUDZYCYQXRDUDZYQXTDUDZVJUDVKYEUUDVKVLZYSYDUUGYDYEMUUDVKYP
      YTYDMUUDVKVLZYTYDRYTUUBRZUUHYTYDUUBJHIVMVNUUIMMVJUDUUDMVKYTUUBUUAMUUCMVJY
      TMNVFZUUBMNVFZVOMMSVPVQVRVSYTUKZYDRUULUUBUKZRZUUHUULYDUUMYDUULUUMYDYTUUBH
      IJVTWAWBVNUUNNNVJUDZUUDMVKUULUUMUUANUUCNVJYTMNWCZUUBMNWCZVOZMNVKVLZUUSMUU
      OVKVLMNSTWDWEZUUTNNTTWFWGVRVSWHWIYLYENUUDVKYDMNWCYTYLNUUDVKVLZWJYTYLUVAYT
      YLRYTUUMRZUVAYTYLUUMYTXRXTUOZYQXTUOZYLUUMYTUVDUVCYQXRXTWKWLXRXTUSYQXTUSWM
      VNUVBNUUDNVKUVBUUDMNVJUDNYTUUMUUAMUUCNVJUUJUUQVONWNWOWPNTWQZVRVSWRUULUVAY
      LUULUUBUVAUULUUBRZNUUDNVKUVFUUDNMVJUDNUULUUBUUANUUCMVJUUPUUKVONWNWSWPUVEV
      RUUNWTUUDNVKUUNUUDUUOWTUURXAXBNWTTXCXDWEVRXEXFXGWIXGUBXSYAYCYELYRYIXHYSUU
      EUUAUUFUUCVJXSYRUUEUUALZYAYRXSUVGABCYQXREEXQUUADYQXOLZMNOZUUAPYTMNPSTUAUE
      XNYQLXPUVHMNXNYQXOUFUGZXOXRLUVHYTMNXOXRYQUHUGGUIVBXIYAYRUUFUUCLZXSYRYAUVK
      ABCYQXTEEXQUUCDUVIUUCPUUBMNPSTUAUEUVJYHUVHUUBMNXOXTYQUHUGGUIVBXJXKXLXM $.
      $( [?] $) $( [9-Oct-2006] $)
  $}


