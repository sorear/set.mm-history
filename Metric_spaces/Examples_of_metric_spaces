$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       Examples of metric spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d D x y z $.
    cnmet.1 $e |- D = ( abs o. - ) $.
    $( Value of the distance function of the metric space of complex
       numbers. $)
    cnmetdval $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( A D B ) = ( abs ` ( A - B ) ) ) $=
      ( cc wcel wa cop cabs cmin ccom cfv co subopr wfun cr wf ffun ax-mp df-ov
      cdm wceq cxp opelxpi fdmi syl6eleqr absf fvco mp3an12 fveq1i eqtri fveq2i
      syl 3eqtr4g ) AEFBEFGZABHZIJKZLZUPJLZILZABCMZABJMZILUOUPJUAZFZURUTUBZUOUP
      EEUCZVCABEEUDVFEJNUEUFIOZJOZVDVEEPIQVGUGEPIRSVFEJQVHNVFEJRSUPIJUHUIUMVAUP
      CLURABCTUPCUQDUJUKVBUSIABJTULUN $.
      $( [7-Sep-2007] $) $( [9-Dec-2006] $)

    $( The base set of the metric for complex numbers. $)
    cnmetba $p |- CC = dom dom D $=
      ( cdm cc cxp cabs cmin ccom dmeqi cr wf absf subopr fco mp2an fdmi dmxpid
      eqtri eqtr2i ) ACZCDDEZCDTUATFGHZCUAAUBBIUAJUBDJFKUADGKUAJUBKLMUADJFGNOPR
      IDQS $.
      $( [30-Aug-2007] $)

    $( The absolute value metric determines a metric space on the complex
       numbers.  This theorem provides a link between complex numbers and
       metrics spaces, making metric space theorems available for use with
       complex numbers.  (Contributed by FL, 9-Oct-2006.) $)
    cnmet $p |- D e. Met $=
      ( vx vy vz cc cr wf cabs cmin cv wcel wa cfv cc0 wceq cnmetdval w3a caddc
      co cnex cxp ccom absf subopr fco mp2an feq1i mpbir subcl abs00 syl eqcomd
      wb eqeq1d subeq0 3bitr3d cle abs3dif abssub 3adant2 breqtrd 3adant3 3coml
      oveq1d oveq12d 3brtr4d ismeti ) CDEAFUAFFUBZGAHVIGIJUCZHZFGIHVIFJHVKUDUEV
      IFGIJUFUGVIGAVJBUHUICKZFLZDKZFLZMZVLVNJTZINZOPZVQOPZVLVNATZOPVLVNPVPVQFLV
      SVTUNVLVNUJVQUKULVPVRWAOVPWAVRVLVNABQZUMUOVLVNUPUQVMVOEKZFLZRZVRWCVLJTINZ
      WCVNJTINZSTZWAWCVLATZWCVNATZSTZURWEVRVLWCJTINZWGSTZWHURVLVNWCUSVMWDWMWHPV
      OVMWDMWLWFWGSVLWCUTVEVAVBVMVOWAVRPWDWBVCWDVMVOWKWHPWDVMVORWIWFWJWGSWDVMWI
      WFPVOWCVLABQVCWDVOWJWGPVMWCVNABQVAVFVDVGVH $.
      $( [7-Sep-2007] $) $( [6-Oct-2006] $)
  $}

  ${
    $d A f w x y z $.  $d B f w x y z $.  $d C w x y z $.  $d D w x y z $.
    $d J f w x y z $.  $d K f w x y $.
    cncfmet.1 $e |- C = ( ( abs o. - ) |` ( A X. A ) ) $.
    cncfmet.2 $e |- D = ( ( abs o. - ) |` ( B X. B ) ) $.
    cncfmet.3 $e |- J = ( MetOpen ` C ) $.
    cncfmet.4 $e |- K = ( MetOpen ` D ) $.
    $( Relate complex function continuity to metric space continuity.
       (Contributed by Paul Chapman, 26-Nov-2007.) $)
    cncfmet $p |- ( ( A C_ CC /\ B C_ CC ) ->
                       ( A -cn-> B ) = ( J Cn K ) ) $=
      ( vx vw cc wa co wf wral wcel wceq cr vf vz vy wss ccncf ccn cv cmin cabs
      cfv clt wbr wi crp wrex cdm wb cxp ccom cres absf subopr fco mp2an xpss12
      anidms fssres sylancr feq1i sylibr fdm dmeqd dmxpid syl6eq feq23 ad2antrr
      syl syl2an anbi1d raleqdv adantr simplll simprl simprr oveqi ovres syl5eq
      ad2ant2l ssel2 eqid cnmetdval eqtrd breq1d simplr ffvelrn syl2anc simpllr
      syl22anc sseldd imbi12d anassrs ralbidva bitrd rexbidv ralbidv bitr2d cme
      pm5.32da elcncf cnmet metres ax-mp eqeltri metcn a1i 3bitr4d eqrdv ) AMUD
      ZBMUDZNZUAABUEOZEFUFOZXTABUAUGZPZKUGZLUGZUHOUIUJZUBUGZUKULZYEYCUJZYFYCUJZ
      UHOUIUJZUCUGZUKULZUMZLAQZUBUNUOZUCUNQZKAQZNZCUPZUPZDUPZUPZYCPZYEYFCOZYHUK
      ULZYJYKDOZYMUKULZUMZLUUBQZUBUNUOZUCUNQZKUUBQZNZYCYARYCYBRZXTUUOYDUUNNYTXT
      UUEYDUUNXRUUBASZUUDBSZUUEYDUQXSXRAAURZTCPZUUQXRUUSTUIUHUSZUUSUTZPZUUTXRMM
      URZTUVAPZUUSUVDUDZUVCMTUIPUVDMUHPUVEVAVBUVDMTUIUHVCVDZXRUVFAMAMVEVFUVDTUU
      SUVAVGVHUUSTCUVBGVIVJUUTUUBUUSUPAUUTUUAUUSUUSTCVKVLAVMVNVQZXSBBURZTDPZUUR
      XSUVITUVAUVIUTZPZUVJXSUVEUVIUVDUDZUVLUVGXSUVMBMBMVEVFUVDTUVIUVAVGVHUVITDU
      VKHVIVJUVJUUDUVIUPBUVJUUCUVIUVITDVKVLBVMVNVQUUBUUDABYCVOVRVSXTYDUUNYSXTYD
      NZUUNUUMKAQYSUVNUUMKUUBAXRUUQXSYDUVHVPZVTUVNUUMYRKAUVNYEARZNZUULYQUCUNUVQ
      UUKYPUBUNUVQUUKUUJLAQYPUVQUUJLUUBAUVNUUQUVPUVOWAVTUVQUUJYOLAUVNUVPYFARZUU
      JYOUQUVNUVPUVRNZNZUUGYIUUIYNUVTUUFYGYHUKUVTXRUVPXRUVRUUFYGSXRXSYDUVSWBZUV
      NUVPUVRWCZUWAUVNUVPUVRWDZXRUVPNZXRUVRNZNUUFYEYFUVAOZYGUVPUVRUUFUWFSXRXRUV
      SUUFYEYFUVBOUWFCUVBYEYFGWEYEYFAAUVAWFWGWHUWDYEMRYFMRUWFYGSUWEAMYEWIAMYFWI
      YEYFUVAUVAWJZWKVRWLWRWMUVTUUHYLYMUKUVTUUHYJYKUVAOZYLUVTYJBRZYKBRZUUHUWHSU
      VTYDUVPUWIXTYDUVSWNZUWBABYEYCWOWPZUVTYDUVRUWJUWKUWCABYFYCWOWPZUWIUWJNUUHY
      JYKUVKOUWHDUVKYJYKHWEYJYKBBUVAWFWGWPUVTYJMRYKMRUWHYLSUVTBMYJXRXSYDUVSWQZU
      WLWSUVTBMYKUWNUWMWSYJYKUVAUWGWKWPWLWMWTXAXBXCXDXEXBXCXHXFKUCUBLABYCXIUUPU
      UOUQZXTCXGRDXGRUWOCUVBXGGUVAXGRZUVBXGRUVAUWGXJZUVAAXKXLXMDUVKXGHUWPUVKXGR
      UWQUVABXKXLXMKUCUBLCDYCEFUUBUUDUUBWJIUUDWJJXNVDXOXPXQ $.
      $( [13-Nov-2013] $) $( [11-Oct-2007] $)
  $}

  ${
    cncfmet1.1 $e |- D = ( abs o. - ) $.
    cncfmet1.2 $e |- J = ( MetOpen ` D ) $.
    $( Relate complex function continuity to metric space continuity.
       (Contributed by Paul Chapman, 28-Nov-2007.) $)
    cncfmet1 $p |- ( CC -cn-> CC ) = ( J Cn J ) $=
      ( cc wss ccncf co ccn wceq ssid cabs cmin ccom cxp cdm subopr ax-mp mp2an
      fdmi cres wrel relco crn wf absf sseqtr4i dmcosseq eqtri eqimssi relssres
      frn cr eqtr4i cncfmet ) EEFZUPEEGHBBIHJEKZUQEEAABBALMNZUREEOZUAZCURUBURPZ
      USFUTURJLMUCVAUSVAMPZUSMUDZLPZFVAVBJVCEVDUSEMUEVCEFQUSEMULREUMLUFTUGLMUHR
      USEMQTUIUJURUSUKSUNZVEDDUOS $.
      $( [28-Nov-2007] $)
  $}

  ${
    $d x y z C $.
    cn2met.1 $e |- C = ( abs o. - ) $.
    cn2met.2 $e |- D = ( x e. ( CC X. CC ) , y e. ( CC X. CC ) |->
                     sup ( { ( ( 1st ` x ) C ( 1st ` y ) ) ,
                      ( ( 2nd ` x ) C ( 2nd ` y ) ) } , RR , < ) ) $.
    $( The standard metric space on ` CC X. CC ` . $)
    cn2met $p |- D e. Met $=
      ( cc cnmetba cnmet metxp ) ABCCDGGCEHZKCEIZLFJ $.
      $( [21-Jun-2007] $)

    txcc.3 $e |- J = ( MetOpen ` C ) $.
    $( The product of two copies of ` CC ` , in terms of the sup norm.
       (Contributed by Jeff Madsen, 11-Jun-2010.)  (Revised by Mario Carneiro,
       5-May-2014.) $)
    txcc $p |- ( J tX J ) = ( MetOpen ` D ) $=
      ( cc cnmetba cnmet txmet ) EEDCCIIABHHCFJZMCFKZNGL $.
      $( [5-May-2014] $) $( [11-Jun-2010] $)
  $}


  ${
    $d r x y z D $.  $d x A $.  $d x B $.
    remet.1 $e |- D = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    $( Value of the distance function of the metric space of real numbers. $)
    remetdval $p |- ( ( A e. RR /\ B e. RR ) ->
                  ( A D B ) = ( abs ` ( A - B ) ) ) $=
      ( cr wcel wa co cop cabs cmin ccom cxp cres cfv df-ov fveq1i wceq cc recn
      eqtri opelxpi fvres syl eqid cnmetdval syl2an syl5eqr eqtrd syl5eq ) AEFZ
      BEFZGZABCHZABIZJKLZEEMZNZOZABKHJOZUNUOCOUSABCPUOCURDQUAUMUSUOUPOZUTUMUOUQ
      FUSVARABEEUBUOUQUPUCUDUMVAABUPHZUTABUPPUKASFBSFVBUTRULATBTABUPUPUEUFUGUHU
      IUJ $.
      $( [16-May-2007] $)

    $( The base set for the metric for real numbers. $)
    remetba $p |- RR = dom dom D $=
      ( cdm cr cxp cabs cmin ccom cres cin dmeqi dmres wceq cc ax-resscn xpss12
      wss mp2an wf absf subopr fdmi sseqtr4i df-ss mpbi 3eqtri dmxpid eqtr2i
      fco ) ACZCDDEZCDUJUKUJFGHZUKIZCUKULCZJZUKAUMBKULUKLUKUNQUOUKMUKNNEZUNDNQZ
      UQUKUPQOODNDNPRUPDULNDFSUPNGSUPDULSTUAUPNDFGUIRUBUCUKUNUDUEUFKDUGUH $.
      $( [30-Aug-2007] $)

    $( The absolute value metric determines a metric space on the reals. $)
    remet $p |- D e. Met $=
      ( cabs cmin ccom cr cxp cres cme wcel eqid cnmet metres ax-mp eqeltri ) A
      CDEZFFGHZIBPIJQIJPPKLPFMNO $.
      $( [10-Feb-2007] $)

    $( A ball in terms of an open interval of reals. $)
    bl2ioo $p |- ( ( A e. RR /\ B e. RR+ ) ->
                   ( A ( ball ` D ) B ) = ( ( A - B ) (,) ( A + B ) ) ) $=
      ( vx cr wcel wa cfv co cmin clt wbr wb cabs cc recn syl2an sylan2 cxr crp
      cbl caddc cioo cv w3a rpre remetdval abssub eqtrd breq1d adantlr absdiflt
      wceq 3expb ancoms bitrd pm5.32da 3anass syl6bbr remet remetba elbl mp3an1
      cme resubcl ax-addrcl rexr elioo2 syl2anc 3bitr4d eqrdv ) AFGZBUAGZHZEABC
      UBIJZABKJZABUCJZUDJZVOEUEZFGZAVTCJZBLMZHZWAVQVTLMZVTVRLMZUFZVTVPGZVTVSGZV
      OWDWAWEWFHZHZWGVNVMBFGZWDWKNBUGZVMWLHZWAWCWJWNWAHWCVTAKJOIZBLMZWJVMWAWCWP
      NWLVMWAHZWBWOBLWQWBAVTKJOIZWOAVTCDUHVMAPGVTPGWRWOUNWAAQVTQAVTUIRUJUKULWAW
      NWPWJNZWAVMWLWSVTABUMUOUPUQURSWAWEWFUSUTCVEGVMVNWHWDNCDVAVTCABFCDVBVCVDVN
      VMWLWIWGNZWMWNVQFGZVRFGZWTABVFABVGXAVQTGVRTGWTXBVQVHVRVHVQVRVTVIRVJSVKVL
      $.
      $( [13-Nov-2013] $) $( [18-May-2007] $)

    $( An open interval of reals in terms of a ball. $)
    ioo2bl $p |- ( ( A e. RR /\ B e. RR /\ A < B ) -> ( A (,) B ) =
     ( ( ( A + B ) / 2 ) ( ball ` D ) ( ( B - A ) / 2 ) ) ) $=
      ( cr wcel clt wbr cioo co caddc c2 cdiv cmin wceq wa cc recn 3adant3 crp
      w3a cbl cfv halfaddsub syl2anr simprd simpld oveq12d eqcomd ax-addrcl cc0
      ancoms wne 2re 2ne0 redivcl mp3an23 syl posdif biimpa 3impa elrp sylanbrc
      resubcl 2pos elrpii rpdivcl sylancl bl2ioo syl2anc addcom oveq1d 3eqtrd )
      AEFZBEFZABGHZUAZABIJZBAKJZLMJZBANJZLMJZNJZVTWBKJZIJZVTWBCUBUCZJZABKJZLMJZ
      WBWFJVQWEVRVQWCAWDBIVQWDBOZWCAOZVNVOWJWKPZVPVOBQFZAQFZWLVNBRZARZBAUDUESZU
      FVQWJWKWQUGUHUIVQWGWEVQVTEFZWBTFZWGWEOVNVOWRVPVNVOPZVSEFZWRVOVNXABAUJULXA
      LEFLUKUMWRUNUOVSLUPUQURSVQWATFZLTFWSVQWAEFZUKWAGHZXBVNVOXCVPVOVNXCBAVDULS
      VNVOVPXDWTVPXDABUSUTVAWAVBVCLUNVEVFWALVGVHVTWBCDVIVJUIVQVTWIWBWFVQVSWHLMV
      NVOVSWHOZVPVOWMWNXEVNWOWPBAVKUESVLVLVM $.
      $( [13-Nov-2013] $) $( [18-May-2007] $)

    $( An open interval of reals in terms of a ball. $)
    ioo2blex $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
                     ( A (,) B ) e. ran ( ball ` D ) ) $=
      ( cr wcel clt wbr w3a cioo co caddc c2 cdiv cmin cbl crp cc0 2re 3adant3
      cfv crn ioo2bl wa ax-addrcl wne redivcl mp3an23 syl resubcl ancoms posdif
      2ne0 biimpa 3impa elrp sylanbrc elrpii rpdivcl sylancl cxp wfn cme cpw wf
      2pos remet remetba blf ffn mp2b fnovrn mp3an1 syl2anc eqeltrd ) AEFZBEFZA
      BGHZIZABJKABLKZMNKZBAOKZMNKZCPUAZKZWDUBZABCDUCVSWAEFZWCQFZWEWFFZVPVQWGVRV
      PVQUDZVTEFZWGABUEWKMEFMRUFWGSUMVTMUGUHUITVSWBQFZMQFWHVSWBEFZRWBGHZWLVPVQW
      MVRVQVPWMBAUJUKTVPVQVRWNWJVRWNABULUNUOWBUPUQMSVFURWBMUSUTWDEQVAZVBZWGWHWI
      CVCFWOEVDZWDVEWPCDVGCECDVHVIWOWQWDVJVKEQWAWCWDVLVMVNVO $.
      $( [14-Nov-2013] $)

    $( The balls of the standard real metric space are included in the open
       real intervals. $)
    blssioo $p |- ran ( ball ` D ) C_ ran (,) $=
      ( vz vy vr cbl cfv crn cioo cv wcel co wceq crp wrex cr ax-mp wa cxr rexr
      cme wb remet remetba blrn cmin caddc bl2ioo resubcl ax-addrcl cxp wfn cpw
      rpre wf ioof fnovrn mp3an1 syl2an syl2anc sylan2 eqeltrd eleq1 syl5ibrcom
      ffn rexlimivv sylbi ssriv ) CAFGZHZIHZCJZVJKZVLDJZEJZVILZMZENODPOZVLVKKZA
      UAKVMVRUBABUCDVLAPEABUDUEQVQVSDEPNVNPKZVONKZRZVSVQVPVKKWBVPVNVOUFLZVNVOUG
      LZILZVKVNVOABUHWAVTVOPKZWEVKKZVOUNVTWFRWCPKZWDPKZWGVNVOUIVNVOUJWHWCSKZWDS
      KZWGWIWCTWDTISSUKZULZWJWKWGWLPUMZIUOWMUPWLWNIVEQSSWCWDIUQURUSUTVAVBVLVPVK
      VCVDVFVGVH $.
      $( [13-Nov-2013] $) $( [8-May-2007] $)

    ${
      $d a b v x y z $.  $d a b x y z D $.  $d v J $.
      tgioo.2 $e |- J = ( MetOpen ` D ) $.
      $( The topology generated by open intervals of reals is the same as the
         open sets of the standard metric space on the reals. $)
      tgioo $p |- ( topGen ` ran (,) ) = J $=
        ( vz cioo wcel wceq ax-mp wss cv cr co wa c1 cxr syl syl2anc wbr cmnf
        vv vx vy va cbl cfv crn ctg cme remet opnval ctb blbas retopbas blssioo
        crp wrex wral cuni elssuni unirnioo syl6sseqr cin a1i simpl ssel2 sylan
        vb cmin caddc 1rp bl2ioo mpan2 peano2rem rexr peano2re cxp wfn cpw ioof
        wf ffn fnovrn mp3an1 eqeltrd simpr remetba blcntr mp3an13 elin sylanbrc
        basis2 syl22anc wb ovelrn eleq2 sseq1 anbi12d cle cif inss2 sstr adantl
        wi elioore adantr sseqtrd dfss sylib eliooxr jca iooin eqtrd clt simpld
        mnfxr ifcl simprd mnflt xrmax2 xrltletr syl3anc mp2and eleqtrd wne ne0i
        c0 ioon0 syl5ib mpcom xrre2 syl32anc mnfle xrlelttr xrre ioo2blex inss1
        xrmin2 rcla4ev syl12anc blssex sylancr mpbid syl6bi rexlimivv rexlimiva
        imp sylanb ralrimiva isopn4 ssriv sseqtri 2basgen mp4an eqtr2i ) BAUEUF
        ZUGZUHUFZFUGZUHUFZAUIGZBUURHACUJZABDUKIZUUQULGZUUSULGZUUQUUSJUUSUURJUUR
        UUTHUVAUVDUVBAUMIUNACUOUUSBUURUAUUSBUAKZUUSGZUVFLJZUBKZUCKUUPMUVFJUCUPU
        QZUBUVFURZUVFBGZUVGUVFUUSUSLUVFUUSUTVAVBZUVGUVJUBUVFUVGUVIUVFGZNZUVIEKZ
        GZUVPUVFUVIOUUPMZVCZJZNZEUUSUQZUVJUVOUVEUVGUVRUUSGZUVIUVSGZUWBUVEUVOUNV
        DUVGUVNVEUVOUVILGZUWCUVGUVHUVNUWEUVMUVFLUVIVFVGZUWEUVRUVIOVIMZUVIOVJMZF
        MZUUSUWEOUPGZUVRUWIHZVKUVIOACVLVMZUWEUWGPGZUWHPGZUWIUUSGZUWEUWGLGZUWMUV
        IVNZUWGVOZQZUWEUWHLGZUWNUVIVPZUWHVOZQZFPPVQZVRZUWMUWNUWOUXDLVSZFWAUXEVT
        UXDUXFFWBIZPPUWGUWHFWCWDRWEQUVOUVNUVIUVRGZUWDUVGUVNWFUVOUWEUXHUWFUVAUWE
        UWJUXHUVBVKAUVIOLACWGZWHWIQUVIUVFUVRWJWKEUVIUUSUVFUVRWLWMUWAUVJEUUSUVPU
        USGZUVPUDKZVHKZFMZHZVHPUQUDPUQZUWAUVJUXEUXJUXOWNUXGUDVHPPUVPFWOIUXOUWAU
        VJUXNUWAUVJXDZUDVHPPUXNUXPXDUXKPGZUXLPGZNZUXNUWAUVIUXMGZUXMUVSJZNZUVJUX
        NUVQUXTUVTUYAUVPUXMUVIWPZUVPUXMUVSWQWRUYBUVQUVPUVFJZNZEUUQUQZUVJUYBUXMU
        UQGUXTUXMUVFJZUYFUYBUXMUXKUWGWSSZUWGUXKWTZUXLUWHWSSZUXLUWHWTZFMZUUQUYBU
        XMUXMUWIVCZUYLUYBUXMUWIJUXMUYMHUYBUXMUVRUWIUYAUXMUVRJZUXTUYAUVSUVRJUYNU
        VFUVRXAUXMUVSUVRXBVMXCUYBUWEUWKUXTUWEUYAUVIUXKUXLXEZXFZUWLQXGUXMUWIXHXI
        UXTUYMUYLHZUYAUXTUXSUWMUWNNZUYQUVIUXKUXLXJZUXTUWEUYRUYOUWEUWMUWNUWSUXCX
        KQUXKUXLUWGUWHXLRXFXMZUYBUYILGZUYKLGZUYIUYKXNSZUYLUUQGUYBTPGZUYIPGZUYKP
        GZTUYIXNSZVUCVUAVUDUYBXPVDZUYBUWMUXQVUEUYBUWEUWMUYPUWSQZUYBUXQUXRUXTUXS
        UYAUYSXFZXOZUYHUWGUXKPXQRZUYBUXRUWNVUFUYBUXQUXRVUJXRZUYBUWTUWNUYBUWEUWT
        UYPUXAQZUXBQZUYJUXLUWHPXQRZUYBTUWGXNSZUWGUYIWSSZVUGUYBUWPVUQUXTUWPUYAUX
        TUWEUWPUYOUWQQXFZUWGXSQUYBUXQUWMVURVUKVUIUXKUWGXTRUYBVUDUWMVUEVUQVURNVU
        GXDVUHUYBUWPUWMVUSUWRQVULTUWGUYIYAYBYCUYBUVIUYLGZVUCUYBUVIUXMUYLUXTUYAV
        EZUYTYDVUEVUFNZVUTVUCUVIUYIUYKXJVUTUYLYGYEVVBVUCUYLUVIYFUYIUYKYHYIYJQZT
        UYIUYKYKYLUYBVUFUWTTUYKXNSZUYKUWHWSSZVUBVUPVUNUYBTUYIWSSZVUCVVDUYBVUEVV
        FVULUYIYMQVVCUYBVUDVUEVUFVVFVUCNVVDXDVUHVULVUPTUYIUYKYNYBYCUYBUXRUWNVVE
        VUMVUOUXLUWHYRRUYKUWHYOWMVVCUYIUYKACYPYBWEVVAUYAUYGUXTUYAUVSUVFJUYGUVFU
        VRYQUXMUVSUVFXBVMXCUYEUXTUYGNEUXMUUQUXNUVQUXTUYDUYGUYCUVPUXMUVFWQWRYSYT
        UYBUVAUWEUYFUVJWNUVBUYPEUVFAUVILUCUXIUUAUUBUUCUUDVDUUEUUGUUHUUFQUUIUVAU
        VLUVHUVKNWNUVBUBUCUVFABLUXIDUUJIWKUUKUVCUULUUQUUSUUMUUNUUO $.
        $( [13-Nov-2013] $) $( [7-May-2007] $)

      $( ` QQ ` is dense in ` RR ` . $)
      qdensere2 $p |- ( ( cls ` J ) ` QQ ) = RR $=
        ( cq cioo crn ctg cfv ccl cr tgioo fveq2i fveq1i qdensere eqtr3i ) EFGH
        IZJIZIEBJIZIKERSQBJABCDLMNOP $.
        $( [24-Aug-2007] $)
    $}
  $}

  ${
    $d x y z $.
    $( The standard topology on the reals is Hausdorff. $)
    rehaus $p |- ( topGen ` ran (,) ) e. Haus $=
      ( cioo crn ctg cfv cabs cmin ccom cxp cres copn eqid tgioo remet methausi
      cr cha eqeltri ) ABCDEFGOOHIZJDZPRSRKZSKZLRSRTMUANQ $.
      $( [28-Jun-2008] $) $( [8-Mar-2007] $)
  $}

  ${
    $d n t u v w x y z B $.  $d t v w C $.  $d t u v w y ph $.  $d t v w y R $.
    $d t u v w x y z A $.  $d w x D $.  $d t v w G $.  $d u z J $.  $d x z T $.
    $d n u v w y S $.  $d t u v w x y z U $.  $d t y V $.
    icccmp.1 $e |- J = ( topGen ` ran (,) ) $.
    icccmp.2 $e |- T = ( subSp ` <. ( A [,] B ) , J >. ) $.
    ${
      icccmp.3 $e |- D = ( ( abs o. - ) |` ( RR X. RR ) ) $.
      icccmp.4 $e |- S = { x e. ( A [,] B ) | E. z e. ( ~P U i^i Fin )
                           ( A [,] x ) C_ U. z } $.
      icccmp.5 $e |- ( ph -> A e. RR ) $.
      icccmp.6 $e |- ( ph -> B e. RR ) $.
      icccmp.7 $e |- ( ph -> A <_ B ) $.
      icccmp.8 $e |- ( ph -> U C_ J ) $.
      icccmp.9 $e |- ( ph -> ( A [,] B ) C_ U. U ) $.
      $( Lemma for ~ icccmp . $)
      icccmplem1 $p |- ( ph -> ( A e. S /\ A. y e. S y <_ B ) ) $=
        ( vu wcel cv cle wbr wral cicc cuni wss cpw cfn cin wrex lbicc2 syl3anc
        co cr sseldd eluni2 sylib csn snssi ad2antrl snex elpw sylibr snfi elin
        a1i sylanbrc cxr wceq rexr syl adantr ccid ad2antll eqsstrd unieq unisn
        wa vex syl6eq sseq2d rcla4ev syl2anc rexlimdva mpd oveq2 sseq1d rexbidv
        expr elrab2 crab ssrab2 eqsstri sseli wb elicc2 biimpa simp3d ralrimiva
        w3a sylan2 jca ) AEHUBZCUCZFUDUEZCHUFAEEFUGUPZUBZEEUGUPZDUCZUHZUIZDJUJZ
        UKULZUMZXFAEUQUBZFUQUBZEFUDUEXJPQREFUNUOZAEUAUCZUBZUAJUMZXQAEJUHZUBYCAX
        IYDETXTURUAEJUSUTAYBXQUAJAYAJUBZYBXQAYEYBWAZWAZYAVAZXPUBZXKYAUIZXQYGYHX
        OUBZYHUKUBZYIYGYHJUIZYKYEYMAYBYAJVBVCYHJYAVDVEVFYLYGYAVGVIYHXOUKVHVJYGX
        KEVAZYAYGEVKUBZXKYNVLAYOYFAXRYOPEVMVNVOEVPVNYBYNYAUIAYEEYAVBVQVRXNYJDYH
        XPXLYHVLZXMYAXKYPXMYHUHYAXLYHVSYAUAWBVTWCWDWEWFWLWGWHEBUCZUGUPZXMUIZDXP
        UMZXQBEXIHYQEVLZYSXNDXPUUAYRXKXMYQEEUGWIWJWKOWMVJAXHCHXGHUBAXGXIUBZXHHX
        IXGHYTBXIWNXIOYTBXIWOWPWQAUUBWAXGUQUBZEXGUDUEZXHAUUBUUCUUDXHXCZAXRXSUUB
        UUEWRPQEFXGWSWFWTXAXDXBXE $.
        $( [18-Jun-2014] $)

      ${
        icccmp.10 $e |- ( ph -> V e. U ) $.
        icccmp.11 $e |- ( ph -> C e. RR+ ) $.
        icccmp.12 $e |- ( ph -> ( G ( ball ` D ) C ) C_ V ) $.
        icccmp.13 $e |- G = sup ( S , RR , < ) $.
        icccmp.14 $e |- R =
          if ( ( G + ( C / 2 ) ) <_ B , ( G + ( C / 2 ) ) , B ) $.
        $( Lemma for ~ icccmp . $)
        icccmplem2 $p |- ( ph -> B e. S ) $=
          ( vy vn vv vw vt c2 cdiv co caddc cle wbr wn wceq clt cr wcel crp wss
          csup c0 wne cv wral wrex cicc cuni cpw cfn cin ssrab2 eqsstri iccssre
          crab syl2anc syl5ss icccmplem1 simpld ne0i syl simprd ralbidv rcla4ev
          breq2 suprcl syl3anc syl5eqel rphalfcl ltaddrp wb readdcl ltnle mpbid
          rpre w3a elicc2 cif ifcl suprub syl31anc syl6breqr wi ltle mpd ifboth
          letrd min2 syl5eqbr mpbir3and cmin oveq2 sseq1d rexbidv elrab2 sseq2d
          wa unieq cbvrexv cun elin sylib sylanbrc sseldd adantr adantrr simprr
          expr cxr rexr lelttrd syl5eq rexlimdv syl5bi resubcl ltsubrp syl6breq
          suprlub syl32anc csn simpr1 elpwi simpll snssd jca unss vex snex unex
          elpw sylibr snfi unfi sylancl wo biimpa simp1d lelttric simplr2 ssun1
          simplr syl6ss simp2d cbl cioo elioo2 syl2an simplr3 lttrd simp3d min1
          cfv rphalflt ltadd2 bl2ioo eleqtrrd elun2 jaod ex ssrdv unisng uneq2d
          uniun 3exp2 expimpd syl6bb cbvrabv eqtri iftrue breq1d syl5ibcom mtod
          sseqtr4d iffalse eqeltrrd ) AHEIALFUNUOUPZUQUPZEURUSZUTZHEVAAUXDUXCLU
          RUSZALUXCVBUSZUXFUTZALVCVDZUXBVEVDZUXGALIVCVBVGZVCUGAIVCVFZIVHVIZUIVJ
          ZUJVJZURUSZUIIVKZUJVCVLZUXKVCVDAIDEVMUPZVCIDBVJZVMUPZCVJZVNZVFZCKVOZV
          PVQZVLZBUXSWAZUXSRUYGBUXSVRVSADVCVDZEVCVDZUXSVCVFZSTDEVTWBZWCZADIVDZU
          XMAUYNUXNEURUSZUIIVKZABUICDEGIJKMOPQRSTUAUBUCWDZWEZIDWFWGZAUYJUYPUXRT
          AUYNUYPUYQWHUXQUYPUJEVCUXOEVAUXPUYOUIIUXOEUXNURWKWIWJWBZUJUIIWLWMWNZA
          FVEVDZUXJUEFWOWGZLUXBWPWBZAUXIUXCVCVDZUXGUXHWQVUAAUXIUXBVCVDZVUEVUAAU
          XJVUFVUCUXBXAWGZLUXBWRWBZLUXCWSWBWTAHLURUSUXDUXFAHUXKLURAUXLUXMUXRHIV
          DZHUXKURUSUYMUYSUYTAHUXSVDZDHVMUPZUXNVNZVFZUIUYFVLZVUIAVUJHVCVDZDHURU
          SZHEURUSZAUYIUYJVUJVUOVUPVUQXBWQSTDEHXCWBAHUXDUXCEXDZVCUHAVUEUYJVURVC
          VDVUHTUXDUXCEVCXEWBWNZADVURHURADUXCURUSZDEURUSZDVURURUSZADLUXCSVUAVUH
          ADUXKLURAUXLUXMUXRUYNDUXKURUSUYMUYSUYTUYRUJUIIDXFXGUGXHAUXGLUXCURUSZV
          UDAUXIVUEUXGVVCXIVUAVUHLUXCXJWBXKXMUAUXDVUTVVAVVBUXCEUXCVURDURWKEVURD
          URWKXLWBUHXHAHVUREURUHAVUEUYJVUREURUSVUHTUXCEXNWBXOXPALFXQUPZUKVJZVBU
          SZUKIVLZVUNAUXLUXMUXRVVDVCVDZVVDUXKVBUSVVGUYMUYSUYTAUXIFVCVDZVVHVUAAV
          UBVVIUEFXAWGZLFUUAWBZAVVDLUXKVBAUXIVUBVVDLVBUSVUAUELFUUBWBUGUUCUJUIUK
          IVVDUUDUUEAVVFVUNUKIVVEIVDVVEUXSVDZDVVEVMUPZUYCVFZCUYFVLZYCAVVFVUNXIZ
          UYGVVOBVVEUXSIUXTVVEVAZUYDVVNCUYFVVQUYAVVMUYCUXTVVEDVMXRXSXTZRYAAVVLV
          VOVVPVVOVVMULVJZVNZVFZULUYFVLAVVLYCZVVPVVNVWACULUYFUYBVVSVAUYCVVTVVMU
          YBVVSYDYBYEVWBVWAVVPULUYFVWBVVSUYFVDZVWAVVFVUNVWBVWCVWAVVFXBZYCZVVSNU
          UFZYFZUYFVDZVUKVWGVNZVFZVUNVWEVWGUYEVDZVWGVPVDZVWHVWEVWGKVFZVWKVWEVVS
          KVFZVWFKVFZYCVWMVWEVWNVWOVWEVVSUYEVDZVWNVWEVWPVVSVPVDZVWEVWCVWPVWQYCV
          WBVWCVWAVVFUUGVVSUYEVPYGYHZWEVVSKUUHWGVWENKVWEANKVDZAVVLVWDUUIZUDWGZU
          UJUUKVVSVWFKUULYHVWGKVVSVWFULUUMNUUNUUOUUPUUQVWEVWQVWFVPVDVWLVWEVWPVW
          QVWRWHNUURVVSVWFUUSUUTVWGUYEVPYGYIVWEVUKVVTNYFZVWIVWEUMVUKVXBVWEUMVJZ
          VUKVDZVXCVXBVDZVWEVXDYCZVXCVVEURUSZVVEVXCVBUSZUVAZVXEVXFVXCVCVDZVVEVC
          VDZVXIVXFVXJDVXCURUSZVXCHURUSZVWEVXDVXJVXLVXMXBZVWEUYIVUOVXDVXNWQVWEA
          UYIVWTSWGZVWEAVUOVWTVUSWGDHVXCXCWBUVBZUVCZVWEVXKVXDVWEUXSVCVVEVWEAUYK
          VWTUYLWGAVVLVWDUVGYJZYKVXCVVEUVDWBVXFVXGVXEVXHVWEVXDVXGVXEVWEVXDVXGYC
          ZYCZVVMVXBVXCVXTVVMVVTVXBVWCVWAVVFVWBVXSUVEVVTNUVFUVHVXTVXCVVMVDZVXJV
          XLVXGVWEVYAVXJVXLVXGXBWQZVXSVWEUYIVXKVYBVXOVXRDVVEVXCXCWBYKVWEVXDVXJV
          XGVXQYLVWEVXDVXLVXGVXFVXJVXLVXMVXPUVIYLVWEVXDVXGYMXPYJYNVWEVXDVXHVXEV
          WEVXDVXHYCZYCZVXCNVDVXEVYDLFGUVJUVRUPZNVXCVYDAVYENVFVWEAVYCVWTYKZUFWG
          VYDVXCVVDLFUQUPZUVKUPZVYEVYDVXCVYHVDZVXJVVDVXCVBUSZVXCVYGVBUSZVYDVVHV
          YGVCVDZVYIVXJVYJVYKXBWQZVYDAVVHVYFVVKWGZVYDAVYLVYFAUXIVVIVYLVUAVVJLFW
          RWBZWGZVVHVVDYOVDVYGYOVDVYMVYLVVDYPVYGYPVVDVYGVXCUVLUVMWBVWEVXDVXJVXH
          VXQYLZVYDVVDVVEVXCVYNVWEVXKVYCVXRYKVYQVWCVWAVVFVWBVYCUVNVWEVXDVXHYMUV
          OVYDVXCHVYGVYQVYDAVUOVYFVUSWGVYPVWEVXDVXMVXHVXFVXJVXLVXMVXPUVPYLVYDAH
          VYGVBUSVYFAHUXCVYGVUSVUHVYOAHVURUXCURUHAVUEUYJVURUXCURUSVUHTUXCEUVQWB
          XOAUXBFVBUSZUXCVYGVBUSZAVUBVYRUEFUVSWGAVUFVVIUXIVYRVYSWQVUGVVJVUAUXBF
          LUVTWMWTYQWGYQXPVYDUXIVUBVYEVYHVAVYDAUXIVYFVUAWGVYDAVUBVYFUEWGLFGQUWA
          WBUWBYJVXCNVVTUWCWGYNUWDXKUWEUWFVWEVWIVVTVWFVNZYFVXBVVSVWFUWIVWEVYTNV
          VTVWEVWSVYTNVAVXANKUWGWGUWHYRUWSVUMVWJUIVWGUYFUXNVWGVAVULVWIVUKUXNVWG
          YDYBWJWBUWJYSYTUWKYTYSXKVVMVULVFZUIUYFVLZVUNUKHUXSIVVEHVAZWUAVUMUIUYF
          WUCVVMVUKVULVVEHDVMXRXSXTIUYHWUBUKUXSWARUYGWUBBUKUXSVVQUYGVVOWUBVVRVV
          NWUACUIUYFUYBUXNVAUYCVULVVMUYBUXNYDYBYEUWLUWMUWNYAYIZUJUIIHXFXGUGXHUX
          DHUXCLURUXDHVURUXCUHUXDUXCEUWOYRUWPUWQUWRUXEHVUREUHUXDUXCEUWTYRWGWUDU
          XA $.
          $( [13-Jun-2014] $)
      $}

      $( Lemma for ~ icccmp . $)
      icccmplem3 $p |- ( ph -> B e. S ) $=
        ( wcel vu vy vv vw cr clt csup cv wrex cuni cicc co cle wbr w3a syl2anc
        wb elicc2 wss c0 wne wral cpw cfn cin ssrab2 eqsstri iccssre icccmplem1
        crab syl5ss simpld ne0i syl simprd breq2 ralbidv rcla4ev suprcl syl3anc
        wceq suprub syl31anc suprleub syl22anc mpbir3and sseldd eluni2 sylib wa
        cbl cfv crp wi ssel2 sylan cme remet cioo crn ctg copn eqid tgioo eqtri
        opni2 mp3an1 ex cdiv caddc cif ad2antrr simplr simprl simprr icccmplem2
        c2 expr rexlimdva syld mpd ) AGUEUFUGZUAUHZTZUAIUIZEGTZAYBIUJZTYEADEUKU
        LZYGYBSAYBYHTZYBUETZDYBUMUNZYBEUMUNZADUETZEUETZYIYJYKYLUOUQOPDEYBURUPAG
        UEUSZGUTVAZUBUHZUCUHZUMUNZUBGVBZUCUEUIZYJAGYHUEGDBUHUKULCUHUJUSCIVCVDVE
        UIZBYHVJYHNUUBBYHVFVGAYMYNYHUEUSOPDEVHUPVKZADGTZYPAUUDYQEUMUNZUBGVBZABU
        BCDEFGHIJKLMNOPQRSVIZVLZGDVMVNZAYNUUFUUAPAUUDUUFUUGVOZYTUUFUCEUEYREWAYS
        UUEUBGYREYQUMVPVQVRUPZUCUBGVSVTAYOYPUUAUUDYKUUCUUIUUKUUHUCUBGDWBWCAYOYP
        YNUUFYLUUCUUIPUUJUBGEWDWEWFWGUAYBIWHWIAYDYFUAIAYCITZWJZYDYBUDUHZFWKWLUL
        YCUSZUDWMUIZYFUUMYCJTZYDUUPWNAIJUSZUULUUQRIJYCWOWPUUQYDUUPFWQTUUQYDUUPF
        MWRUDYCFYBJJWSWTXAWLFXBWLZKFUUSMUUSXCXDXEXFXGXHVNUUMUUOYFUDWMUUMUUNWMTZ
        UUOYFUUMUUTUUOWJZWJBCDEUUNFYBUUNXQXIULXJULZEUMUNUVBEXKZGHIYBJYCKLMNAYMU
        ULUVAOXLAYNUULUVAPXLADEUMUNUULUVAQXLAUURUULUVARXLAYHYGUSUULUVASXLAUULUV
        AXMUUMUUTUUOXNUUMUUTUUOXOYBXCUVCXCXPXRXSXTXSYA $.
        $( [13-Jun-2014] $)
    $}

    $( A closed interval in ` RR ` is compact.  (Contributed by Mario Carneiro,
       13-Jun-2014.) $)
    icccmp $p |- ( ( A e. RR /\ B e. RR ) -> T e. Comp ) $=
      ( vu vz vx cr wcel wa cicc csubsp cfv ccmp cv cuni wss c0 cop cle wbr clt
      co wo lelttric cpw cfn cin wrex wi wral crab cabs cmin ccom cxp cres eqid
      simplll simpllr simplr elpwi ad2antrl simprr icccmplem3 wceq oveq2 sseq1d
      rexbidv elrab simprbi syl expr ralrimiva ctop wb cioo crn eqeltri iccssre
      ctg adantr uniretop unieqi eqtr4i cmpsub sylancr mpbird csn cxr rexr icc0
      retop syl2an opeq1d fveq2d subsp0 ax-mp syl6eq 0cmp syl6eqel jaodan mpdan
      biimpar syl5eqel ) AJKZBJKZLZCABMUEZDUAZNOZPFXJABUBUCZBAUDUCZUFXMPKZABUGX
      JXNXPXOXJXNLZXPXKGQZRSZXKHQRZSZHXRUHUIUJZUKZULZGDUHZUMZXQYDGYEXQXRYEKZXSY
      CXQYGXSLZLZBAIQZMUEZXTSZHYBUKZIXKUNZKZYCYIIHABUOUPUQJJURUSZYNCXRDEFYPUTYN
      UTXHXIXNYHVAXHXIXNYHVBXJXNYHVCYGXRDSXQXSXRDVDVEXQYGXSVFVGYOBXKKYCYMYCIBXK
      YJBVHZYLYAHYBYQYKXKXTYJBAMVIVJVKVLVMVNVOVPXQDVQKZXKJSZXPYFVRDVSVTWCOZVQEW
      OWAZXJYSXNABWBWDXKDJGHJYTRDRWEDYTEWFWGWHWIWJXJXOLZXMTWKZPUUBXMTDUAZNOZUUC
      UUBXLUUDNUUBXKTDXJXKTVHZXOXHAWLKBWLKUUFXOVRXIAWMBWMABWNWPXFWQWRYRUUEUUCVH
      UUADWSWTXAXBXCXDXEXG $.
      $( [13-Jun-2014] $)
  $}

  ${
    $d s t x y z A $.
    $( Lemma for ~ reconn .  Connectedness in the reals-easy direction.
       (Contributed by Jeff Hankins, 13-Jul-2009.) $)
    reconnlem1 $p |- ( ( A C_ RR /\ ( subSp ` <. A , ( topGen ` ran (,) ) >. )
        e.
   Con ) -> A. x e. A A. y e. A ( x [,] y ) C_ A ) $=
      ( vs vt cr wss wcel cin c0 wn wi wb wa wbr syl adantr cmnf cpnf wceq cioo
      vz crn ctg cfv cop csubsp ccon cv cicc co wral wne cdif w3a ctop uniretop
      cun retop connsub mpan cxr cle rexr adantrr adantrl elicc1 syl2anc pm4.61
      ssel2 wrex mnflt nelneq eqcom necon3abii adantlr ad2ant2l simprl1 simprl2
      clt sylibr xrleltne syl3anc mpbird 3jca mnfxr elioo2 sylancr simplrl elin
      sylanbrc ne0i df-ne adantll simprl3 ltpnf pnfxr sylancl simplrr jctl jctr
      xrleid ioodisj syl21anc 3ad2ant1 ad2antrl 0ss a1i eqsstrd csn ccid ineq2d
      disjsn biimpri ad2antll eqtrd reldisj ad2antrr mpbid cuni xrlelttr 3com12
      cpr jca mp3an3 exp4b com23 imp32 adantl ovex iooretop ineq1 neeq1d sseq1d
      sseq2d anbi12d ex rexnal imp sylbid com24 mpd notnot sylib nltpnft mtbird
      3adantr2 xrltletr mp3an1 ngtmnft con2bid sylibd sylc 3adantr3 elxr biimpi
      ecase23d sylan difreicc unipr sseqtrd 3anbi13d uneq1 ineq2 3anbi23d uneq2
      syl6eq rcla42ev mp3an12 syl31anc df-an 2rexbii rexbii 3bitri syl6ib con4d
      w3o syl5bi ssrdv ralrimdvva ) CFGZCUAUCUDUEZUFUGUEUHHZAUIZBUIZUJUKZCGZBCU
      LACULZUWAUWCDUIZCIZJUMZEUIZCIZJUMZUWIUWLIZFCUNZGZUOZCUWIUWLURZGZKLZEUWBUL
      ZDUWBULZUWHUWBUPHUWAUWCUXCMUSDECUWBFUQUTVAUWAUXCUWGABCCUWAUWDCHZUWECHZNZN
      ZUXCUWGUXGUXCNZUBUWFCUXHUBUIZUWFHZUXIVBHZUWDUXIVCOZUXIUWEVCOZUOZUXICHZUXH
      UWDVBHZUWEVBHZUXJUXNMUXGUXPUXCUWAUXDUXPUXEUWAUXDNZUWDFHZUXPCFUWDVJZUWDVDZ
      PVEZQUXGUXQUXCUWAUXEUXQUXDUWAUXENZUWEFHZUXQCFUWEVJZUWEVDZPVFZQUWDUWEUXIVG
      VHUXGUXCUXNUXOLZUXGUYHUXCUYHKUXNUXOKZNZUXGUXCKZUXNUXOVIUXGUYJUWRUWTNZEUWB
      VKDUWBVKZUYKUXGUYJUYMUXGUYJNZRUXIUAUKZCIZJUMZUXISUAUKZCIZJUMZUYOUYRIZUWPG
      ZCUYOUYRURZGZUYMUYNUWDUYPHZUYQUYNUWDUYOHZUXDVUEUYNVUFUXSRUWDVTOZUWDUXIVTO
      ZUOZUYNUXSVUGVUHUXGUXSUYJUWAUXDUXSUXEUXTVEZQUXGVUGUYJUWAUXDVUGUXEUXRUXSVU
      GUXTUWDVLZPVEQUYNVUHUXIUWDUMZUXFUYIVULUWAUXNUXDUYIVULUXEUXDUYINUWDUXITZKV
      ULUWDUXICVMVUMUXIUWDUXIUWDVNVOWAVPVQUYNUXPUXKUXLVUHVULMUXGUXPUYJUYBQUXKUX
      LUXMUYIUXGVRZUXKUXLUXMUYIUXGVSUWDUXIWBWCWDWEUYNRVBHZUXKVUFVUIMWFVUNRUXIUW
      DWGWHWDUWAUXDUXEUYJWIUWDUYOCWJWKUYPUWDWLPUYNUWEUYSHZUYTUYNUWEUYRHZUXEVUPU
      YNVUQUYDUXIUWEVTOZUWESVTOZUOZUYNUYDVURVUSUXGUYDUYJUWAUXEUYDUXDUYEVFZQUYNV
      URUWEUXIUMZUXFUYIVVBUWAUXNUXEUYIVVBUXDUXEUYINUWEUXITKVVBUWEUXICVMUWEUXIWM
      WAWNVQUYNUXKUXQUXMVURVVBMVUNUXGUXQUYJUYGQUXKUXLUXMUYIUXGWOUXIUWEWBWCWDUXG
      VUSUYJUWAUXEVUSUXDUYCUYDVUSUYEUWEWPZPVFQWEUYNUXKSVBHZVUQVUTMVUNWQUXISUWEW
      GWRWDUWAUXDUXEUYJWSUWEUYRCWJWKUYSUWEWLPUYNVUAJUWPUXNVUAJTZUXGUYIUXKUXLVVE
      UXMUXKVUOUXKNUXKVVDNUXIUXIVCOVVEUXKVUOWFWTUXKVVDWQXAUXIXBRUXIUXISXCXDXEXF
      JUWPGUYNUWPXGXHXIUYNCFUXIUXIUJUKZUNZVUCUYNCVVFIZJTZCVVGGZUYNVVHCUXIXJZIZJ
      UXNVVHVVLTZUXGUYIUXKUXLVVMUXMUXKVVFVVKCUXIXKXLXEXFUYIVVLJTZUXGUXNVVNUYICU
      XIXMXNXOXPUWAVVIVVJMUXFUYJCVVFFXQXRXSUYNVVGUYOUYRYCXTZVUCUYNUXIFHZVVPVVGV
      VOTUXGUXSUYDNZUYJVVPUXGUXSUYDVUJVVAYDVVQUXNVVPUYIVVQUXNNZVVPUXISTZUXIRTZV
      VRVVSUXISVTOZKZVVRVWAVWBKUYDUXNVWAUXSUYDUXKUXMVWAUXLUYDUXKUXMVWAUYDVUSUXK
      UXMVWALLVVCUYDUXMUXKVUSVWAUYDUXKUXMVUSVWALZUYDUXQUXKUXMVWCLLUYFUXQUXKUXMV
      USVWAUXQUXKVVDUXMVUSNVWALZWQUXKUXQVVDVWDUXIUWESYAYBYEYFPYGUUAUUBYHUUGWNVW
      AUUCUUDUXNVVSVWBMZVVQUXKUXLVWEUXMUXIUUEXEYIUUFUXSUXNVVTKZUYDUXSUXKUXLVWFU
      XMUXSUXKUXLVWFUXSUXPVUGUXKUXLVWFLZLUYAVUKUXPUXKVUGVWGUXPUXKVUGUXLVWFUXPUX
      KNVUGUXLNZRUXIVTOZVWFVUOUXPUXKVWHVWILWFRUWDUXIUUHUUIUXKVWIVWFMUXPUXKVVTVW
      IUXIUUJUUKYIUULYFYGUUMYHUUNVPUXNVVPVVSVVTUVQZVVQUXKUXLVWJUXMUXKVWJUXIUUOU
      UPXEYIUUQVEUURZVWKUXIUXIUUSVHUYOUYRRUXIUAYJUXISUAYJUUTUVGUVAUYOUWBHUYRUWB
      HUYQUYTVUBUOZVUDNZUYMRUXIYKUXISYKUYLVWMUYQUWNUYOUWLIZUWPGZUOZCUYOUWLURZGZ
      NDEUYOUYRUWBUWBUWIUYOTZUWRVWPUWTVWRVWSUWKUYQUWQVWOUWNVWSUWJUYPJUWIUYOCYLY
      MVWSUWOVWNUWPUWIUYOUWLYLYNUVBVWSUWSVWQCUWIUYOUWLUVCYOYPUWLUYRTZVWPVWLVWRV
      UDVWTUWNUYTVWOVUBUYQVWTUWMUYSJUWLUYRCYLYMVWTVWNVUAUWPUWLUYRUYOUVDYNUVEVWT
      VWQVUCCUWLUYRUYOUVFYOYPUVHUVIUVJYQUYMUXAKZEUWBVKZDUWBVKUXBKZDUWBVKUYKUYLV
      XADEUWBUWBUWRUWTUVKUVLVXBVXCDUWBUXAEUWBYRUVMUXBDUWBYRUVNUVOUVRUVPYSYTUVSY
      QUVTYTYS $.
      $( [13-Jul-2009] $)
  $}

  ${
    reconnlem2.1 $e |- ( ph <-> ( ( ( ( b e. ( u i^i A ) /\ c e. ( v i^i A ) /\
     ( u i^i v ) C_ ( RR \ A ) ) /\ ( A C_ RR /\ ( u e. ( topGen ` ran (,) ) /\
      v e. ( topGen ` ran (,) ) ) ) ) /\ b <_ c ) /\ ( b [,] c ) C_ A ) ) $.
    $( Lemma for ~ reconn .  (Contributed by Jeff Hankins, 15-Aug-2009.) $)
    reconnlem2 $p |- ( ph -> ( b e. RR /\ c e. RR ) ) $=
      ( cv cr wcel cin wss wa sylbi inss2 sstr mpan syl ad3antrrr sseldd w3a co
      cdif cioo crn ctg cfv cle wbr cicc simprl ad2antrr simp1 simp2 jca ) AEHZ
      IJFHZIJACHZDKZIUPADILZUSILZAUPUSJZUQBHZDKZJZURVCKIDUCLZUAZUTURUDUEUFUGZJV
      CVHJMZMZMZUPUQUHUIZMUPUQUJUBDLZMZUTGVKUTVLVMVGUTVIUKULNZUSDLUTVAURDOUSDIP
      QRAVNVBGVGVBVJVLVMVBVEVFUMSNTAVDIUQAUTVDILZVOVDDLUTVPVCDOVDDIPQRAVNVEGVGV
      EVJVLVMVBVEVFUNSNTUO $.
      $( [15-Aug-2009] $)
  $}

  ${
    $d t w z ph $.  $d t w z A $.  $d b t w z $.  $d c t w z $.  $d t u w z $.
    $d t v w z $.
    reconnlem3.1 $e |- ( ph <-> ( ( ( ( b e. ( u i^i A ) /\ c e. ( v i^i A ) /\
     ( u i^i v ) C_ ( RR \ A ) ) /\ ( A C_ RR /\ ( u e. ( topGen ` ran (,) ) /\
      v e. ( topGen ` ran (,) ) ) ) ) /\ b <_ c ) /\ ( b [,] c ) C_ A ) ) $.
    reconnlem3.2 $e |- S = sup ( ( u i^i ( b [,] c ) ) , RR , < ) $.
    $( Lemma for ~ reconn .  (Contributed by Jeff Hankins, 16-Aug-2009.) $)
    reconnlem3 $p |- ( ph -> S e. ( b [,] c ) ) $=
      ( vw vt vz cv wcel cr cle wbr w3a wss wa cicc co cin clt csup c0 wne wral
      wrex reconnlem2 inss2 a1i cdif crn ctg cfv inss1 sseli 3ad2ant1 ad3antrrr
      cioo biid simplr lbicc2 3expa syl2anc elin 3imtr4i iccsupr syl3anc suprcl
      jca syl syl5eqel sylanbrc suprub breq2i 3simpa simprd cxr wi rexr anim12i
      iccleub 3expia syl5 3syl ralrimiv suprleub syl12anc breq1i 3jca wb elicc2
      mpbird ) AEFMZGMZUAUBZNZEONZWPEPQZEWQPQZRZAWTXAXBAECMZWRUCZOUDUEZOIAXEOSZ
      XEUFUGZJMKMPQJXEUHKOUIZRZXFONAWPONZWQONZTZXEWRSZWPXENZXJABCDFGHUJZXNAXDWR
      UKZULWPXDDUCZNZWQBMZDUCNZXDXTUCODUMSZRZDOSXDVAUNUOUPZNXTYDNTTZTZWPWQPQZTW
      RDSZTZWPXDNZWPWRNZTAXOYIYJYKYCYJYEYGYHXSYAYJYBXRXDWPXDDUQURUSUTZYIXMYGYKY
      IBCDFGYIVBUJZYFYGYHVCXKXLYGYKWPWQVDVEVFZVLHWPXDWRVGZVHKJWPWQWPXEVIZVJKJXE
      VKVMVNYIWPXFPQZAXAYIXJXOYQYIXMXNXOXJYMXNYIXQULZYIYJYKXOYLYNYOVOZYPVJYSKJX
      EWPVPVFHEXFWPPIVQVHYIXFWQPQZAXBYIXGXHTZXLLMZWQPQZLXEUHYTYIXMXNXOUUAYMYRYS
      XMXNXORXJUUAYPXGXHXIVRVMVJYIXKXLYMVSYIUUCLXEYIXMWPVTNZWQVTNZTZUUBXENZUUCW
      AYMXKUUDXLUUEWPWBWQWBWCUUGUUBWRNZUUFUUCXEWRUUBXQURUUDUUEUUHUUCWPWQUUBWDWE
      WFWGWHLXEWQWIWJHEXFWQPIWKVHWLAXMWSXCWMXPWPWQEWNVMWO $.
      $( [16-Aug-2009] $)
  $}

  ${
    $d r ph $.  $d r t S $.  $d b r s t $.  $d c r s t $.  $d r s t u $.
    reconnlem4.1 $e |- ( ph <-> ( ( ( ( b e. ( u i^i A ) /\ c e. ( v i^i A ) /\
     ( u i^i v ) C_ ( RR \ A ) ) /\ ( A C_ RR /\ ( u e. ( topGen ` ran (,) ) /\
      v e. ( topGen ` ran (,) ) ) ) ) /\ b <_ c ) /\ ( b [,] c ) C_ A ) ) $.
    reconnlem4.2 $e |- S = sup ( ( u i^i ( b [,] c ) ) , RR , < ) $.
    $( Lemma for ~ reconn .  (Contributed by Jeff Hankins, 16-Aug-2009.) $)
    reconnlem4 $p |- ( ph -> -. S e. u ) $=
      ( wcel cle wbr co cr wa syl2anc clt adantr cc0 wb vt vs cicc cin wral wss
      vr cv wne wrex w3a reconnlem2 inss2 a1i cdif cioo crn ctg cfv inss1 sseli
      c0 3ad2ant1 ad3antrrr biid simplr lbicc2 jca elin 3imtr4i iccsupr syl3anc
      3expa csup suprub syl6breqr ralrimiva syl cmin cabs ccom cxp cres cbl crp
      wn cme eqid remet simprrl ad2antrr sylbi simprd iccssre reconnlem3 sseldd
      simpr resubcl wceq disjdif wi 3ad2ant2 eleq1 syl5ibcom jctild simp3 sseld
      syl6ibr syld n0i syl6 mt2i df-ne sylibr elicc2 mpbid simp3d leltne mpbird
      posdif elrp sylanbrc copn tgioo opni3 syl31anc cdiv caddc simprrr readdcl
      c2 ad2antrl biimpa lttrd 3jca cxr rexr 0re ltle sylc rpre rehalfcl adantl
      rpgt0 ltsubpos halfpos2 ltaddpos halfpos ltadd2 elioo2 simprl eleqtrrd cc
      bl2ioo simpld recn addid1 jctir jca32 simp2d jctil le2add leaddsub2 ltnle
      3syl eqbrtrrd breq1 notbid rcla4ev exp32 rexlimdv mpd rexnal syl6ib mt2d
      ex ) AECUHZJZUAUHZEKLZUAUVQFUHZGUHZUCMZUDZUEZAUWDNUFUWDVBUIUBUHUGUHZKLUBU
      WDUEUGNUJUKZUWEAUWANJZUWBNJZOZUWDUWCUFZUWAUWDJZUWGABCDFGHULZUWKAUVQUWCUMU
      NUWAUVQDUDZJZUWBBUHZDUDZJZUVQUWPUDZNDUOZUFZUKZDNUFZUVQUPUQURUSZJZUWPUXDJZ
      OOZOZUWAUWBKLZOZUWCDUFZOZUWAUVQJZUWAUWCJZOAUWLUXLUXMUXNUXBUXMUXGUXIUXKUWO
      UWRUXMUXAUWNUVQUWAUVQDUTVAVCVDUXLUWJUXIUXNUXLBCDFGUXLVEZULUXHUXIUXKVFUWHU
      WIUXIUXNUWAUWBVGVMPVHHUWAUVQUWCVIVJUGUBUWAUWBUWAUWDVKVLUWGUVTUAUWDUWGUVSU
      WDJOUVSUWDNQVNEKUGUBUWDUVSVOIVPVQVRAUVRUVTWFZUAUWDUJZUWEWFAUVRUXQAUVROZUW
      FUWBEVSMZQLZEUWFVTVSWANNWBWCZWDUSMZUVQUFZOZUGWEUJZUXQUXRUYAWGJZUXEUVRUXSW
      EJZUYEUYFUXRUYAUYAWHZWIUNAUXEUVRAUXLUXEHUXHUXEUXIUXKUXBUXCUXEUXFWJWKWLRAU
      VRWQZUXRUXSNJZSUXSQLZUYGAUYJUVRAUWIENJZUYJAUWHUWIUWMWMZAUWCNEAUWJUWCNUFUW
      MUWAUWBWNVRABCDEFGHIWOZWPZUWBEWRZPRUXREUWBQLZUYKUXRUYQUWBEUIZUXRUWBEWSZWF
      UYRUXRUYSDUWTUDZVBWSZDNWTUXRUYSEUYTJZVUAWFUXRUYSEDJZEUWTJZOVUBUXRUYSVUDVU
      CUXRUYSEUWSJZVUDUXRUYSUVREUWPJZOVUEUXRUYSVUFUVRAUYSVUFXAUVRAUWBUWPJZUYSVU
      FAUXLVUGHUXBVUGUXGUXIUXKUWRUWOVUGUXAUWQUWPUWBUWPDUTVAXBVDWLUWBEUWPXCXDRUY
      IXEEUVQUWPVIXHAVUEVUDXAUVRAUWSUWTEAUXLUXAHUXBUXAUXGUXIUXKUWOUWRUXAXFVDWLX
      GRXIAVUCUVRAUXLVUCHUXLUWCDEUXJUXKWQUXLBCDEFGUXOIWOWPWLRXEEDUWTVIXHUYTEXJX
      KXLUWBEXMXNAUYQUYRTZUVRAUYLUWIEUWBKLZVUHUYOUYMAUYLUWAEKLZVUIAEUWCJZUYLVUJ
      VUIUKZUYNAUWJVUKVULTUWMUWAUWBEXOVRXPZXQEUWBXRVLRXSAUYQUYKTZUVRAUYLUWIVUNU
      YOUYMEUWBXTPRXPUXSYAYBUGUVQUYAEUXSUXDUYAUYAYCUSZUYHVUOWHYDYEYFUXRUYDUXQUG
      WEUXRUWFWEJZUYDUXQUXRVUPUYDOZOZEUWFYKYGMZYHMZUWDJZVUTEKLZWFZUXQVURVUTUVQJ
      VUTUWCJZVVAVURUYBUVQVUTUXRVUPUXTUYCYIVURVUTEUWFVSMZEUWFYHMZUPMZUYBVURVUTV
      VGJZVUTNJZVVEVUTQLZVUTVVFQLZUKZVURVVIVVJVVKVURUYLVUSNJZVVIAUYLUVRVUQUYOWK
      ZVURUWFNJZVVMVUPVVOUXRUYDUWFUUAYLZUWFUUBVRZEVUSYJPZVURVVEEVUTVURUYLVVOVVE
      NJZVVNVVPEUWFWRPZVVNVVRVURSUWFQLZVVEEQLZVUPVWAUXRUYDUWFUUDYLVURVVOUYLVWAV
      WBTVVPVVNUWFEUUEPXPVURSVUSQLZEVUTQLZVUQVWCUXRVUPVWCUYDVUPVVOVWAOZVWCUWFYA
      ZVVOVWAVWCUWFUUFYMWLRUUCZVURVVMUYLVWCVWDTVVQVVNVUSEUUGPXPZYNVURVUSUWFQLZV
      VKVUPVWIUXRUYDVUPVWEVWIVWFVVOVWAVWIUWFUUHYMWLYLZVURVVMVVOUYLVWIVVKTVVQVVP
      VVNVUSUWFEUUIVLXPYOVURVVEYPJZVVFYPJZVVHVVLTVURVVSVWKVVTVVEYQVRVURVVFNJZVW
      LVURUYLVVOVWMVVNVVPEUWFYJPVVFYQVRVVEVVFVUTUUJPXSVURUYLVUPUYBVVGWSVVNUXRVU
      PUYDUUKEUWFUYAUYHUUNPUULWPVURVVDVVIUWAVUTKLZVUTUWBKLZUKZVURVVIVWNVWOVVRVU
      RUWASYHMZUWAVUTKAVWQUWAWSZUVRVUQAUWHUWAUUMJVWRAUWHUWIUWMUUOZUWAUUPUWAUUQU
      VEWKVURUWHSNJZOZUYLVVMOOVUJSVUSKLZOVWQVUTKLVURVXAUYLVVMVURUWHVWTAUWHUVRVU
      QVWSWKYRUURVVNVVQUUSVURVUJVXBAVUJUVRVUQAUYLVUJVUIVUMUUTWKVURVWTVVMOVWCVXB
      VURVVMVWTVVQYRUVAVWGSVUSYSYTVHUWASEVUSUVBYTUVFVURVWOVUSUXSKLZVURVVMUYJOVU
      SUXSQLVXCVURVVMUYJVVQVURUWIUYLUYJAUWIUVRVUQUYMWKZVVNUYPPZVHVURVUSUWFUXSVV
      QVVPVXEVWJUXRVUPUXTUYCWJYNVUSUXSYSYTVURUYLVVMUWIVWOVXCTVVNVVQVXDEVUSUWBUV
      CVLXSYOAVVDVWPTZUVRVUQAUWJVXFUWMUWAUWBVUTXOVRWKXSVUTUVQUWCVIYBVURVWDVVCVW
      HVURUYLVVIVWDVVCTVVNVVREVUTUVDPXPUXPVVCUAVUTUWDUVSVUTWSUVTVVBUVSVUTEKUVGU
      VHUVIPUVJUVKUVLUVPUVTUAUWDUVMUVNUVO $.
      $( [13-Jan-2014] $) $( [16-Aug-2009] $)
  $}

  ${
    $d m n r t ph $.  $d m n r t S $.  $d b m n r t $.  $d c m n r t $.
    $d m n r t u $.  $d m n r t v $.
    reconnlem5.1 $e |- ( ph <-> ( ( ( ( b e. ( u i^i A ) /\ c e. ( v i^i A ) /\
     ( u i^i v ) C_ ( RR \ A ) ) /\ ( A C_ RR /\ ( u e. ( topGen ` ran (,) ) /\
      v e. ( topGen ` ran (,) ) ) ) ) /\ b <_ c ) /\ ( b [,] c ) C_ A ) ) $.
    reconnlem5.2 $e |- S = sup ( ( u i^i ( b [,] c ) ) , RR , < ) $.
    $( Lemma for ~ reconn .  (Contributed by Jeff Hankins, 17-Aug-2009.) $)
    reconnlem5 $p |- ( ph -> -. S e. v ) $=
      ( vt vr cv wcel cr wss wa clt wbr adantr wi vn vm cabs cmin ccom cxp cres
      cbl cfv co crp wrex wn wral wex caddc w3a cicc cin c0 wne csup reconnlem2
      cle inss2 a1i cdif cioo inss1 sseli 3ad2ant1 ad3antrrr biid simplr lbicc2
      crn ctg 3expa syl2anc jca elin 3imtr4i iccsupr syl3anc iccssre reconnlem3
      syl sseldd rpre resubcl syl2an cc0 rpgt0 adantl wb ltsubpos syl2anr mpbid
      syl6breq suprlub syl12anc df-rex sseld syl5 simpr sylan syl6breqr ex jcad
      suprub wceq cc recn addid1 eqcomd ad2antrl simprl 0re jctir jca32 leltadd
      simprr sylc eqbrtrd syld adantrd 3jcad disjdif simprbi imp sylan2 adantrr
      a1d simp3 sylbi syl5bir exp3a eqid cxr rexr syl6ibr n0i eximdv syl5bi mpd
      syl6 bl2ioo eleq2d readdcl elioo2 bitrd anbi1d exbidv mpbird nss ralrimiv
      mt2i sylibr ralnex sylib remet simprrr ad2antrr copn tgioo opni2 mtand
      cme ) AEBLZMZEJLZUCUDUENNUFUGZUHUIUJZUVIOZJUKULZAUVNUMZJUKUNUVOUMAUVPJUKA
      UVKUKMZUVPAUVQPZKLZUVMMZUVSUVIMZUMZPZKUOZUVPUVRUWDUVSNMZEUVKUDUJZUVSQRZUV
      SEUVKUPUJZQRZUQZUWBPZKUOZUVRUWGKCLZFLZGLZURUJZUSZULZUWLUVRUWQNOUWQUTVAUAL
      UBLVDRUAUWQUNUBNULUQZUWFNMZUWFUWQNQVBZQRUWRAUWSUVQAUWNNMZUWONMZPZUWQUWPOZ
      UWNUWQMZUWSABCDFGHVCZUXEAUWMUWPVEZVFUWNUWMDUSZMZUWOUVIDUSMZUWMUVIUSZNDVGZ
      OZUQZDNOZUWMVHVPVQUIZMZUVIUXQMZPPZPZUWNUWOVDRZPZUWPDOZPZUWNUWMMZUWNUWPMZP
      AUXFUYEUYFUYGUXOUYFUXTUYBUYDUXJUXKUYFUXNUXIUWMUWNUWMDVIVJVKVLUYEUXDUYBUYG
      UYEBCDFGUYEVMVCUYAUYBUYDVNUXBUXCUYBUYGUWNUWOVOVRVSVTHUWNUWMUWPWAWBUBUAUWN
      UWOUWNUWQWCWDZSAENMZUVKNMZUWTUVQAUWPNEAUXDUWPNOUXGUWNUWOWEWGZABCDEFGHIWFW
      HZUVKWIZEUVKWJWKZUVRUWFEUXAQUVRWLUVKQRZUWFEQRZUVQUYOAUVKWMWNZUVQUYJUYIUYO
      UYPWOAUYMUYLUVKEWPWQWRIWSUBUAKUWQUWFWTXAUWRUVSUWQMZUWGPZKUOUVRUWLUWGKUWQX
      BUVRUYSUWKKUVRUYSUWJUWBUVRUYSUWEUWGUWIUYSUVSUWPMZUVRUWEUYRUYTUWGUWQUWPUVS
      UXHVJZSAUYTUWETUVQAUWPNUVSUYKXCZSXDUYSUWGTUVRUYRUWGXEVFUVRUYRUWIUWGUVRUYR
      UWEUVSEVDRZPZUWIAUYRVUDTUVQAUYRUWEVUCUYRUYTAUWEVUAVUBXDAUYRVUCAUYRPUVSUXA
      EVDAUWSUYRUVSUXAVDRUYHUBUAUWQUVSXJXFIXGXHXISUVRVUDUWIUVRVUDPZUVSUVSWLUPUJ
      ZUWHQUWEUVSVUFXKUVRVUCUWEVUFUVSUWEUVSXLMVUFUVSXKUVSXMUVSXNWGXOXPVUEUWEWLN
      MZPZUYIUYJPPVUCUYOPVUFUWHQRVUEVUHUYIUYJVUEUWEVUGUVRUWEVUCXQXRXSUVRUYIVUDA
      UYIUVQUYLSSUVRUYJVUDUVQUYJAUYMWNSXTVUEVUCUYOUVRUWEVUCYBUVRUYOVUDUYQSVTUVS
      WLEUVKYAYCYDXHYEYFYGUVRUYSUWBUVRUYSPZUWADUXMUSZUTXKZDNYHVUIUWAUVSVUJMZVUK
      UMVUIUWAUVSDMZUVSUXMMZPVULVUIUWAVUMVUNUVRUYRUWAVUMTUWGUVRUYRPVUMUWAUYRUVR
      UYTVUMVUAUVRUYTVUMAUYTVUMTUVQAUWPDUVSAUYCUYDHYIXCSYJYKYMYLUVRUYRUWAVUNTZU
      WGUVRUYRVUOUYRUVSUWMMZUVRVUOUWQUWMUVSUWMUWPVIVJUVRVUPUWAVUNVUPUWAPUVSUXLM
      ZUVRVUNUVSUWMUVIWAAVUQVUNTUVQAUXLUXMUVSAUYEUXNHUXOUXNUXTUYBUYDUXJUXKUXNYN
      VLYOXCSYPYQXDYJYLXIUVSDUXMWAUUAVUJUVSUUBUUFUUQXHXIUUCUUDUUEUVRUWCUWKKUVRU
      VTUWJUWBUVRUVTUVSUWFUWHVHUJZMZUWJUVRUVMVURUVSAUYIUVQUVMVURXKUYLEUVKUVLUVL
      YRZUUGXFUUHUVRUWTUWHNMZVUSUWJWOZUYNAUYIUYJVVAUVQUYLUYMEUVKUUIWKUWTUWFYSMU
      WHYSMVVBVVAUWFYTUWHYTUWFUWHUVSUUJWKVSUUKUULUUMUUNKUVMUVIUUOUURXHUUPUVNJUK
      UUSUUTAUVJPZUVLUVHMZUXSUVJUVOVVDVVCUVLVUTUVAVFAUXSUVJAUYEUXSHUYAUXSUYBUYD
      UXOUXPUXRUXSUVBUVCYOSAUVJXEJUVIUVLEUXQUVLUVLUVDUIZVUTVVEYRUVEUVFWDUVG $.
      $( [12-Jan-2014] $) $( [17-Aug-2009] $)
  $}

  ${
    $d b c u v x y A $.
    $( A subset of the reals is connected iff it has the interval property.
       (Contributed by Jeff Hankins, 15-Jul-2009.) $)
    reconn $p |- ( A C_ RR -> ( ( subSp ` <. A , ( topGen ` ran (,) ) >. ) e.
     Con <-> A. x e. A A. y e. A ( x [,] y ) C_ A ) ) $=
      ( vu vv vb vc cr wss wcel cv cicc co wral ex cin wi wa wceq sseq1d csubsp
      cioo crn ctg cfv cop ccon reconnlem1 c0 wne cdif w3a cun wn wex n0 cle wo
      inss2 sseli ssel2 sylan2 3ad2antr1 3ad2antr2 letric syl2anc adantrr oveq1
      wbr ancoms oveq2 rcla42v syl2an 3adant3 ad2antrr clt csup biid reconnlem4
      eqid reconnlem5 sstr2 adantl reconnlem3 ssel syld elun syl6ib mtord incom
      syl5com sseq1i biimpi 3anim3i 3com12 pm3.22 anim2i sseq2i sylnib jaod mpd
      uncom com23 3exp com12 exlimiv sylbi 3imp com13 ralrimdvva retop uniretop
      ctop wb connsub mpan sylibrd impbid ) CHIZCUBUCUDUEZUFUAUEUGJZAKZBKZLMZCI
      ZBCNACNZXSYAYFABCUHOXSYFDKZCPZUIUJZEKZCPZUIUJZYGYJPZHCUKZIZULZCYGYJUMZIZU
      NZQZEXTNDXTNZYAXSYFYTDEXTXTYPYFXSYGXTJZYJXTJZRZRZYSYIYLYOYFUUEYSQQZYIFKZY
      HJZFUOYLYOUUFQZQZFYHUPUUHUUJFYLUUHUUIYLGKZYKJZGUOUUHUUIQZGYKUPUULUUMGUUHU
      ULUUIUUHUULYOUUFUUHUULYOULZUUEYFYSUUNUUEYFYSQZUUNUUERZUUGUUKUQVIZUUKUUGUQ
      VIZURZUUOUUNXSUUSUUDXSUUNUUSXSUUNRUUGHJZUUKHJZUUSXSUULUUHUUTYOUUHXSUUGCJZ
      UUTYHCUUGYGCUSUTZCHUUGVAVBVCXSUUHUULUVAYOUULXSUUKCJZUVAYKCUUKYJCUSUTZCHUU
      KVAVBVDUUGUUKVEVFVJVGUUPUUQUUOUURUUPUUQUUOUUPUUQRZYFUUGUUKLMZCIZYSUUNYFUV
      HQZUUEUUQUUHUULUVIYOUUHUVBUVDUVIUULUVCUVEYEUVHUUGYCLMZCIABUUGUUKCCYBUUGSY
      DUVJCYBUUGYCLVHTYCUUKSUVJUVGCYCUUKUUGLVKTVLVMVNVOUVFUVHYSUVFUVHRZYRYGUVGP
      HVPVQZYGJZUVLYJJZUVKEDCUVLFGUVKVRZUVLVTZVSUVKEDCUVLFGUVOUVPWAUVKYRUVLYQJZ
      UVMUVNURUVKYRUVGYQIZUVQUVHYRUVRQUVFUVGCYQWBWCUVKUVLUVGJUVRUVQUVKEDCUVLFGU
      VOUVPWDUVGYQUVLWEWKWFUVLYGYJWGWHWIOWFOUUNUULUUHYJYGPZYNIZULZXSUUCUUBRZRZU
      URUUOQUUEUULUUHYOUWAYOUVTUULUUHYOUVTYMUVSYNYGYJWJWLWMWNWOUUDUWBXSUUBUUCWP
      WQUWAUWCRZUURUUOUWDUURRZYFUUKUUGLMZCIZYSUWAYFUWGQZUWCUURUULUUHUWHUVTUULUV
      DUVBUWHUUHUVEUVCYEUWGUUKYCLMZCIABUUKUUGCCYBUUKSYDUWICYBUUKYCLVHTYCUUGSUWI
      UWFCYCUUGUUKLVKTVLVMVNVOUWEUWGYSUWEUWGRZCYJYGUMZIZYRUWJUWLYJUWFPHVPVQZYJJ
      ZUWMYGJZUWJDECUWMGFUWJVRZUWMVTZVSUWJDECUWMGFUWPUWQWAUWJUWLUWMUWKJZUWNUWOU
      RUWJUWLUWFUWKIZUWRUWGUWLUWSQUWEUWFCUWKWBWCUWJUWMUWFJUWSUWRUWJDECUWMGFUWPU
      WQWDUWFUWKUWMWEWKWFUWMYJYGWGWHWIUWKYQCYJYGXBWRWSOWFOVMWTXAOXCXDXEXFXGXEXF
      XGXHXIXJXTXMJXSYAUUAXNXKDECXTHXLXOXPXQXR $.
      $( [15-Jul-2009] $)
  $}

  ${
    $d x y $.
    $( Corollary of ~ reconn .  The set of real numbers is connected.
       (Contributed by Jeff Hankins, 17-Aug-2009.) $)
    retopcon $p |- ( topGen ` ran (,) ) e. Con $=
      ( vx vy cr cioo crn ctg cfv cop csubsp ccon ctop wcel wceq retop uniretop
      subspid ax-mp cv wss wral cicc co iccssre rgen2a wb reconn mpbir eqeltrri
      ssid ) CDEFGZHIGZUJJUJKLUKUJMNUJCOPQUKJLZARZBRZUAUBCSZBCTACTZUOABCUMUNUCU
      DCCSULUPUECUIABCUFQUGUH $.
      $( [17-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( A closed interval is connected.  (Contributed by Jeff Hankins,
       17-Aug-2009.) $)
    iccconn $p |- ( ( A e. RR /\ B e. RR ) -> ( subSp ` <. ( A [,] B ) , (
     topGen ` ran (,) ) >. ) e. Con ) $=
      ( vx vy vz cr wcel wa cicc co cfv cv wss cle wbr w3a adantr elicc2 ex wb
      cioo crn ctg cop csubsp ccon wral wi 3ad2ant1 com12 simplll iccssre sseld
      ax-1 imp adantrr simp1 adantl biimpa simp2d simpr 3ad2antr2 letrd adantrl
      3ad2antr1 simpllr simp3 anim12i iccleub 3expa sylan 3jcad syl2anc 3imtr4d
      cxr rexr ssrdv ralrimivv reconn syl mpbird ) AFGZBFGZHZABIJZUAUBUCKUDUEKU
      FGZCLZDLZIJZWEMZDWEUGCWEUGZWDWJCDWEWEWDWGWEGZWHWEGZHZWJWDWNHZEWIWEWOELZFG
      ZWGWPNOZWPWHNOZPZWQAWPNOZWPBNOZPZWPWIGZWPWEGZWOWTWQXAXBWTWOWQWQWRWOWQUHWS
      WQWOUNUIUJWOWTXAWOWTHZAWGWPWBWCWNWTUKWOWGFGZWTWDWLXGWMWDWLXGWDWEFWGABULZU
      MUOUPZQWTWQWOWQWRWSUQURWOAWGNOZWTWDWLXJWMWDWLHXGXJWGBNOZWDWLXGXJXKPABWGRU
      SUTUPQWOWQWRWRWSWOWRVAVBVCSWOWTXBXFWPWHBWOWRWQWQWSWOWQVAVEWOWHFGZWTWDWMXL
      WLWDWMXLWDWEFWHXHUMUOVDZQWBWCWNWTVFWTWSWOWQWRWSVGURWOWHBNOZWTWDWMXNWLWDAV
      OGZBVOGZHWMXNWBXOWCXPAVPBVPVHXOXPWMXNABWHVIVJVKVDQVCSVLWOXGXLXDWTTXIXMWGW
      HWPRVMWDXEXCTWNABWPRQVNVQSVRWDWEFMWFWKTXHCDWEVSVTWA $.
      $( [17-Aug-2009] $)
  $}

  ${
    $d u v w D $.  $d u v w V $.  $d u v w x y X $.
    dscmet.1 $e |- D = ( x e. X , y e. X |-> if ( x = y , 0 , 1 ) ) $.
    $( The discrete metric on any set ` X ` .  Definition 1.1-8 of [Kreyszig]
       p. 8.  (Contributed by FL, 12-Oct-2006.) $)
    dscmet $p |- ( X e. V -> D e. Met ) $=
      ( vw vv vu wcel cr cc0 wceq wb cle wbr wa c1 ifbid cn0 cme wf cv co caddc
      cxp wral cif 0re 1re keepel a1i rgen2a fmpt2 mpbi equequ1 0nn0 1nn0 elexi
      equequ2 ovmpt2 eqeq1d wn iffalse ax-1ne0 eqnetrd df-ne sylib con4i iftrue
      impbii syl6bb cn wo nn0addcli elnn0 breq1 lt01 ltleii leidi keephyp nnge1
      nn0rei letri sylancr nn0ge0i add20i mp2an bibi12d chvarv anbi12i syl6eqbr
      wne eqtr2 3imtr4i sylbi breqtrrd jaoi ax-mp adantl eqeq12 ovmpt2a adantrr
      adantrl oveq12d 3brtr4d expcom ralrimiv jca pm3.2i cvv mpt2exg anidms cdm
      id fdm dmeqd dmxpid eqtr2i ismet syl mpbiri ) EDJZCUAJZEEUFZKCUBZGUCZHUCZ
      CUDZLMZYGYHMZNZYIIUCZYGCUDZYMYHCUDZUEUDZOPZIEUGZQZHEUGGEUGZQZYFYTAUCZBUCZ
      MZLRUHZKJZBEUGAEUGYFUUFABEUUFUUBEJUUCEJQUUDLRKUIUJUKULUMABEEUUEKCFUNUOZYS
      GHEYGEJZYHEJZQZYLYRUUJYJYKLRUHZLMZYKUUJYIUUKLABYGYHEEUUEUUKCYGUUCMZLRUHUU
      BYGMUUDUUMLRAGBUPSUUCYHMZUUMYKLRBHGUTSFUUKTYKLRTUQURUKZUSVAZVBUULYKYKUULY
      KVCZUUKLWMUULVCUUQUUKRLYKLRVDRLWMUUQVEULVFUUKLVGVHVIYKLRVJVKZVLUUJYQIEYME
      JZUUJYQUUSUUJQZUUKYMYGMZLRUHZYMYHMZLRUHZUEUDZYIYPOUUKUVEOPZUUTUVEVMJZUVEL
      MZVNZUVFUVETJUVIUVBUVDUVALRTUQURUKZUVCLRTUQURUKZVOZUVEVPUOUVGUVFUVHUVGUUK
      ROPZRUVEOPUVFYKLROPRROPUVMLRLUUKROVQRUUKROVQLRUIUJVRVSRUJVTWAUVEWBUUKRUVE
      UUKUUOWCUJUVEUVLWCWDWEUVHUUKLUVEOUVHUVBLMZUVDLMZQZUUKLOPLUVBOPLUVDOPUVHUV
      PNUVBUVJWFUVDUVKWFUVBUVDUVBUVJWCUVDUVKWCWGWHUVPUUKLLOUVAUVCQYKUVPUULYMYGY
      HWNUVNUVAUVOUVCUVOUVCNZUVNUVANHGYHYGMZUVOUVNUVCUVAUVRUVDUVBLUVRUVCUVALRHG
      IUTZSVBUVSWIUULYKNUVQGIYGYMMZUULUVOYKUVCUVTUUKUVDLUVTYKUVCLRGIHUPZSVBUWAW
      IUURWJZWJUWBWKUURWOLUIVTWLWPUVHXOWQWRWSULUUJYIUUKMUUSUUPWTUUTYNUVBYOUVDUE
      UUSUUHYNUVBMUUIABYMYGEEUUEUVBCUUBYMMZUUCYGMQUUDUVALRUUBYMUUCYGXASFUVBTUVJ
      USXBXCUUSUUIYOUVDMUUHABYMYHEEUUEUVDCUWCUUNQUUDUVCLRUUBYMUUCYHXASFUVDTUVKU
      SXBXDXEXFXGXHXIUMXJYCCXKJZYDUUANYCUWDABEEUUEDDCFXLXMGHIXKCECXNZXNZYEXNZEY
      FUWFUWGMUUGYFUWEYEYEKCXPXQWSEXRXSXTYAYB $.
      $( [18-Apr-2008] $) $( [9-Oct-2006] $)

    $( The discrete metric generates the discrete topology.  In particular, the
       discrete topology is metrizable.  (Contributed by Mario Carneiro,
       29-Jan-2014.) $)
    dscopn $p |- ( X e. V -> ( MetOpen ` D ) = ~P X ) $=
      ( vu vv vw wcel cv wss wa wb cdm wceq cc0 c1 cr clt copn cfv cpw cbl wrex
      crn wral cme dscmet cxp wfn cif 0re keepel elexi fnmpt2i fndm dmeqd ax-mp
      1re dmxpid eqtr2i eqid isopn syl wi simpll ssel2 adantll jca csn elsn wbr
      co eleq1a simpl eqeq12 ifbid ovmpt2a breq1d wn ltnri iffalse mtbiri con4i
      iftrue lt01 syl6eqbr impbii equcom bitri syl6rbb simpr biantrurd bitrd ex
      a1i pm5.21ndd adantl crp 1rp elbl mp3an3 sylan bitr4d syl5bb eqrdv blelrn
      eqeltrd snssi vex snid jctil eleq2 sseq1 anbi12d rcla4ev syl2an ralrimiva
      sylancom pm4.71 sylib elpw syl6bbr ) EDJZGCUAUBZEUCZYEGKZYFJZYHELZYHYGJYE
      YIYJHKZIKZJZYLYHLZMZICUDUBZUFZUEZHYHUGZMZYJYECUHJZYIYTNABCDEFUIZHIYHCYFEC
      OZOZEEUJZOZECUUEUKZUUDUUFPABEEAKZBKZPZQRULZCFUUKSUUJQRSUMUTUNUOUPUUGUUCUU
      EUUECUQURUSEVAVBZYFVCVDVEYEYJYSVFYJYTNYEYJYSYEYJMZYRHYHUUMYKYHJZYEYKEJZMZ
      YRUUMUUNMYEUUOYEYJUUNVGYJUUNUUOYEYHEYKVHVIVJUUPYKVKZYQJYKUUQJZUUQYHLZMZYR
      UUNUUPUUQYKRYPVNZYQUUPIUUQUVAYLUUQJYLYKPZUUPYLUVAJZIYKVLUUPUVBYLEJZYKYLCV
      NZRTVMZMZUVCUUOUVBUVGNZYEUUOUVDUVBUVGYKEYLVOUVGUVDVFUUOUVDUVFVPWQUUOUVDUV
      HUUOUVDMZUVBUVFUVGUVIUVFYKYLPZQRULZRTVMZUVBUVIUVEUVKRTABYKYLEEUUKUVKCUUHY
      KPUUIYLPMUUJUVJQRUUHYKUUIYLVQVRFUVKSUVJQRSUMUTUNUOVSVTUVLUVJUVBUVLUVJUVJU
      VLUVJWAZUVLRRTVMRUTWBUVMUVKRRTUVJQRWCVTWDWEUVJUVKQRTUVJQRWFWGWHWIHIWJWKWL
      UVIUVDUVFUUOUVDWMWNWOWPWRWSYEUUAUUOUVCUVGNZUUBUUAUUORWTJZUVNXAYLCYKREUULX
      BXCXDXEXFXGYEUUAUUOUVAYQJZUUBUUAUUOUVOUVPXACYKREUULXHXCXDXIUUNUUSUURYKYHX
      JYKHXKXLXMYOUUTIUUQYQYLUUQPYMUURYNUUSYLUUQYKXNYLUUQYHXOXPXQXRXTXSWPYJYSYA
      YBXEYHEGXKYCYDXG $.
      $( [29-Jan-2014] $)
  $}


