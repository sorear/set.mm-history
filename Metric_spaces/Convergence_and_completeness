$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       Convergence and completeness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ~~>m $.  $( Limit arrow. $)
  $c Cau $.
  $c CMet $.

  $( Extend class notation with a function on metric spaces whose value is
     the convergence relation for limit sequences in the space. $)
  clm $a class ~~>m $.

  $( Extend class notation with a function on metric spaces whose value is the
     set of all Cauchy sequences of the space. $)
  cca $a class Cau $.

  $( Extend class notation with class of complete metric spaces. $)
  cms $a class CMet $.

  ${
    $d j k m x y f z w $.
    $( Define a function on metric spaces whose value is the convergence
       relation for the space.  Although ` f ` is typically a function from
       upper integers to the metric space, it doesn't have to be.
       Unfortunately, the expression after " ` w = ` " must exist to use
       ~ fvopab4 , and we use the otherwise unnecessary conjunct
       ` f (_ ( CC X. ( 1st `` w ) ) ` to ensure that.  This could be changed
       to the more liberal (but more complex)
       ` f (_ ( CC X. ( ( 1st `` w ) u. { (/) } ) ) ` if we want to allow for
       functions with undefined values. $)
    df-lm $a |- ~~>m = { <. z , w >. | ( z e. Met /\ w = { <. f , y >. |
      ( f (_ ( CC X. ( 1st ` z ) ) /\ y e. ( 1st ` z ) /\ A. x e. RR ( 0 < x ->
       E. j e. ZZ A. k e. ZZ ( j <_ k -> ( ( f ` k ) e. ( 1st ` z ) /\
           ( ( f ` k ) ( 2nd ` z ) y ) < x ) ) ) ) } ) } $.

    $( Define a function on metric spaces whose value is the set of Cauchy
       sequences of the space. $)
    df-cau $a |- Cau = { <. z , w >. | ( z e. Met /\ w = { f |
           ( f (_ ( CC X. ( 1st ` z ) ) /\ A. x e. RR ( 0 < x ->
          E. j e. ZZ A. k e. ZZ A. m e. ZZ ( ( j <_ k /\ j <_ m ) ->
            ( ( f ` k ) e. ( 1st ` z ) /\ ( f ` m ) e. ( 1st ` z ) /\
             ( ( f ` k ) ( 2nd ` z ) ( f ` m ) ) < x ) ) ) ) } ) } $.

    $( Define the class of complete metric spaces. $)
    df-cms $a |- CMet = { x e. Met | A. f e. ( Cau ` x ) E. y e. ( 1st ` x )
                 f ( ~~>m ` x ) y } $.
  $}

  ${
    $d w z D $.  $d f j k m w x y z M $.  $d f w y z X $.
    lmfval.1 $e |- X = ( 1st ` M ) $.
    lmfval.2 $e |- D = ( 2nd ` M ) $.
    $( The relation "sequence ` f ` converges to point ` y ` " in a metric
       space. $)
    lmfval $p |- ( M e. Met -> ( ~~>m ` M ) =
      { <. f , y >. | ( f (_ ( CC X. X ) /\ y e. X /\ A. x e. RR ( 0 < x ->
             E. j e. ZZ A. k e. ZZ ( j <_ k ->
      ( ( f ` k ) e. X /\ ( ( f ` k ) D y ) < x ) ) ) ) } ) $=
      ( vz vw cv cc c1st cfv cxp wss wcel cc0 clt wbr cle c2nd co wa wi cz
      wral wrex cr w3a copab cmt clm wceq fveq2 syl6eqr xpeq2 syl sseq2d
      eleq2d opreqd breq1d anbi12d imbi2d rexralbidv ralbidv 3anbi123d
      opabbidv df-lm cpw axcnex cvv fvex eqeltr xpex pwex df-3an visset elpw
      3anbi1i bitr3 opabbii opabssxp eqsstr3 ssexi fvopab4 ) KLGDMZNKMZOPZQZRZB
      MZWKSZTAMZUAUBZEMFMZUCUBZWRWIPZWKSZWTWNWJUDPZUEZWPUAUBZUFZUGZFUHUIEUHUJZU
      GZAUKUIZULZDBUMWINHQZRZWNHSZWQWSWTHSZWTWNCUEZWPUAUBZUFZUGZFUHUIEUHUJZUGZA
      UKUIZULZDBUMZUNUOWJGUPZXJYBDBYDWMXLWOXMXIYAYDWLXKWIYDWKHUPWLXKUPYDWKGOPZH
      WJGOUQIURZWKHNUSUTVAYDWKHWNYFVBYDXHXTAUKYDXGXSWQYDXFXREFUHUHYDXEXQWSYDXAX
      NXDXPYDWKHWTYFVBYDXCXOWPUAYDXBCWTWNYDXBGUDPCWJGUDUQJURVCVDVEVFVGVFVHVIVJA
      BKLDEFVKYCXKVLZHQZYGHXKNHVMHYEVNIGOVOVPZVQVRYIVQYCWIYGSZXMUFYAUFZDBUMYHYK
      YBDBYKYJXMYAULYBYJXMYAVSYJXLXMYAWIXKDVTWAWBWCWDYADBYGHWEWFWGWH $.
      $( [18-Sep-2006] $) $( [7-Sep-2006] $)
$(
      MAY BE REVISED
      proof not requiring X m distinct
      ( vz vw cmt wcel clm cfv cop cv cc cxp wss cc0 clt wbr cle co wa wi cz
      wral wrex cr w3a copab c1st c2nd wrel wceq msrel 1st2nd mpan opeq12i
      syl6eqr fveq2d id eqeltrrd syl5eqel fveq2 cvv fvex eqeltr op1st syl6eq
      xpeq2 syl sseq2d eleq2d op2nd opreqd breq1d anbi12d imbi2d rexralbidv
      ralbidv 3anbi123d opabbidv df-lm cpw axcnex xpex pwex df-3an visset elpw
      3anbi1i bitr3 opabbii opabssxp eqsstr3 ssexi fvopab4 eqtrd ) GMNZGOPHCQZO
      PZDRZSHTZUAZBRZHNZUBARZUCUDZERFRZUEUDZXMXFPZHNZXOXICUFZXKUCUDZUGZUHZFUIUJ
      EUIUKZUHZAULUJZUMZDBUNZXCGXDOXCGGUOPZGUPPZQZXDMUQXCGYHURUSGMUTVAZHYFCYGIJ
      VBZVCVDXCXDMNXEYEURXCYHMXDXCGYHMYIXCVEVFYJVGKLXDXFSKRZUOPZTZUAZXIYLNZXLXN
      XOYLNZXOXIYKUPPZUFZXKUCUDZUGZUHZFUIUJEUIUKZUHZAULUJZUMZDBUNYEMOYKXDURZUUE
      YDDBUUFYNXHYOXJUUDYCUUFYMXGXFUUFYLHURYMXGURUUFYLXDUOPHYKXDUOVHHCHYFVIIGUO
      VJVKZVLVMZYLHSVNVOVPUUFYLHXIUUHVQUUFUUCYBAULUUFUUBYAXLUUFUUAXTEFUIUIUUFYT
      XSXNUUFYPXPYSXRUUFYLHXOUUHVQUUFYRXQXKUCUUFYQCXOXIUUFYQXDUPPCYKXDUPVHHCUUG
      CYGVIJGUPVJVKVRVMVSVTWAWBWCWBWDWEWFABKLDEFWGYEXGWHZHTZUUIHXGSHWIUUGWJWKUU
      GWJYEXFUUINZXJUGYCUGZDBUNUUJUULYDDBUULUUKXJYCUMYDUUKXJYCWLUUKXHXJYCXFXGDW
      MWNWOWPWQYCDBUUIHWRWSWTXAVOXB $.
$)

    $( The set of Cauchy sequences on a metric space. $)
    caufval $p |- ( M e. Met -> ( Cau ` M ) = { f |
           ( f (_ ( CC X. X ) /\ A. x e. RR ( 0 < x ->
          E. j e. ZZ A. k e. ZZ A. m e. ZZ ( ( j <_ k /\ j <_ m ) ->
            ( ( f ` k ) e. X /\ ( f ` m ) e. X /\
             ( ( f ` k ) D ( f ` m ) ) < x ) ) ) ) } ) $=
      ( vz vw cv cc c1st cfv cxp wss cc0 clt wbr cle wa wcel c2nd co w3a wi cz
      wral wrex cr cab cmt cca wceq fveq2 syl6eqr xpeq2 syl sseq2d eleq2d
      opreqd breq1d 3anbi123d imbi2d ralbidv rexralbidv anbi12d abbidv df-cau
      cvv axcnex fvex eqeltr xpex abssexg ax-mp fvopab4 ) KLGCMZNKMZOPZQZRZSAMZ
      TUAZDMZEMZUBUAWGFMZUBUAUCZWHVTPZWBUDZWIVTPZWBUDZWKWMWAUEPZUFZWETUAZUGZUHZ
      FUIUJZEUIUJDUIUKZUHZAULUJZUCZCUMVTNHQZRZWFWJWKHUDZWMHUDZWKWMBUFZWETUAZUGZ
      UHZFUIUJZEUIUJDUIUKZUHZAULUJZUCZCUMZUNUOWAGUPZXDXQCXSWDXFXCXPXSWCXEVTXSWB
      HUPWCXEUPXSWBGOPZHWAGOUQIURZWBHNUSUTVAXSXBXOAULXSXAXNWFXSWTXMDEUIUIXSWSXL
      FUIXSWRXKWJXSWLXGWNXHWQXJXSWBHWKYAVBXSWBHWMYAVBXSWPXIWETXSWOBWKWMXSWOGUEP
      BWAGUEUQJURVCVDVEVFVGVHVFVGVIVJAKLCDEFVKXEVLUDXRVLUDNHVMHXTVLIGOVNVOVPXPC
      XEVLVQVRVS $.
      $( [22-Sep-2006] $) $( [8-Sep-2006] $)
  $}

  ${
    $d f j k x y M $.
    $( The metric space convergence relation is a relation. $)
    lmrel $p |- ( M e. Met -> Rel ( ~~>m ` M ) ) $=
      ( vf vy vx vj vk cmt wcel clm cfv wrel cv cc c1st cxp wss cc0 clt wbr
      cle c2nd co wa wi cz wral wrex cr w3a copab relopab wceq wb eqid lmfval
      releq syl mpbiri ) AGHZAIJZKZBLZMANJZOPCLZVCHQDLZRSELFLZTSVFVBJZVCHVGVDAU
      AJZUBVERSUCUDFUEUFEUEUGUDDUHUFUIZBCUJZKZVIBCUKUSUTVJULVAVKUMDCVHBEFAVCVCU
      NVHUNUOUTVJUPUQUR $.
      $( [7-Dec-2006] $) $( [7-Dec-2006] $)
  $}

  ${
    $d f w m y z D $.  $d f j k m w x y z F $.  $d f j k m w x y z M $.
    $d j k w x z P $.  $d f m w y z X $.
    lmbr.1 $e |- X = ( 1st ` M ) $.
    lmbr.2 $e |- D = ( 2nd ` M ) $.
    $( Express the binary relation "sequence ` F ` converges to point ` P ` "
       in a metric space.  Definition 1.4-1 of [Kreyszig] p. 25.  The
       condition ` F (_ ( CC X. X ) ` allows us to use objects more general
       than sequences when convenient; see the comment in ~ df-lm . $)
    lmbr $p |- ( ( M e. Met /\ P e. A ) -> ( F ( ~~>m ` M ) P <->
    ( F (_ ( CC X. X ) /\ P e. X /\ A. x e. RR ( 0 < x -> E. j e. ZZ A. k e. ZZ
           ( j <_ k -> ( ( F ` k ) e. X /\ ( ( F ` k ) D P ) < x ) ) ) ) ) ) $=
      ( vz vw cmt wcel wa clm cfv wbr cc cxp wss cc0 cv clt cle co wi cz wral
      wrex cr w3a cvv simpll wrel brrelex lmrel sylan anim1i an1rs jca axcnex
      c1st fvex eqeltr xpex ssex ancoms anim2i anassrs adantrr 3adantr3 copab
      wceq wb lmfval breq syl sseq1 fveq1 eleq1d opreq1d breq1d anbi12d imbi2d
      rexralbidv ralbidv 3anbi13d eleq1 opreq2 anbi2d 3anbi23d eqid brabg
      sylan9bb pm5.21nd ) HNOZDBOZPZGDHQRZSZGTIUAZUBZDIOZUCAUDZUESZEUDFUDZUFSZX
      HGRZIOZXJDCUGZXFUESZPZUHZFUIUJEUIUKZUHZAULUJZUMZWRGUNOZWSPZPZWTXBPWRYAWRW
      SXBUOWRXBWSYAWRXBPXTWSXAUPXBXTWRGDXAUQHURUSUTVAVBWTXDXEYBXRWTXDYBXEWRWSXD
      YBWSXDPYAWRXDWSYAXDXTWSGXCTIVCIHVDRUNJHVDVEVFVGVHUTVIVJVKVLVMWRXBGDLUDZXC
      UBZMUDZIOZXGXIXHYCRZIOZYGYECUGZXFUESZPZUHZFUIUJEUIUKZUHZAULUJZUMZLMVNZSZY
      AXSWRXAYQVOXBYRVPAMCLEFHIJKVQGDXAYQVRVSYPXDYFXGXIXKXJYECUGZXFUESZPZUHZFUI
      UJEUIUKZUHZAULUJZUMXSLMGDUNBYQYCGVOZYDXDYOUUEYFYCGXCVTUUFYNUUDAULUUFYMUUC
      XGUUFYLUUBEFUIUIUUFYKUUAXIUUFYHXKYJYTUUFYGXJIXHYCGWAZWBUUFYIYSXFUEUUFYGXJ
      YECUUGWCWDWEWFWGWFWHWIYEDVOZYFXEUUEXRXDYEDIWJUUHUUDXQAULUUHUUCXPXGUUHUUBX
      OEFUIUIUUHUUAXNXIUUHYTXMXKUUHYSXLXFUEYEDXJCWKWDWLWFWGWFWHWMYQWNWOWPWQ $.
      $( [10-Dec-2006] $) $( [7-Dec-2006] $)

    ${
      $d j D $.  $d j k N $.  $d j X $.  $d j k Z $.
      lmbr2.3 $e |- N e. ZZ $.
      lmbr2.4 $e |- Z = ( ZZ> ` N ) $.
      $( Express the binary relation "sequence ` F ` converges to point ` P ` "
         in a metric space using an upper partition of ` ZZ ` . $)
      lmbr2 $p |-  ( ( M e. Met /\ P e. A ) -> ( F ( ~~>m ` M ) P <->
      ( F (_ ( CC X. X ) /\ P e. X /\ A. x e. RR ( 0 < x -> E. j e. Z A. k e. Z
           ( j <_ k -> ( ( F ` k ) e. X /\ ( ( F ` k ) D P ) < x ) ) ) ) ) ) $=
        ( cmt wcel wa clm cfv wbr cc cxp wss cc0 cv clt cle co wi cz wral wrex
        cr w3a lmbr cuz eqimss2i uzssz eqsstr cvg3 imbi2i ralbii 3anbi3i
        syl6bb ) HPQDBQRGDHSTUAGUBJUCUDZDJQZUEAUFZUGUAZEUFFUFZUHUAVJGTZJQVKDCUI
        VHUGUARZUJZFUKULEUKUMZUJZAUNULZUOVFVGVIVMFKULEKUMZUJZAUNULZUOABCDEFGHJL
        MUPVPVSVFVGVOVRAUNVNVQVIVLEFIIKKNKIUQTZOURZKVTUKOIUSUTZNWAWBVAVBVCVDVE
        $.
        $( [20-Dec-2006] $) $( [19-Dec-2006] $)
    $}

    $( Express the property " ` F ` is a Cauchy sequence of metric space
       ` M ` ."  Part of Definition 1.4-3 of [Kreyszig] p. 28.  The
       condition ` F (_ ( CC X. X ) ` allows us to use objects more general
       than sequences when convenient; see the comment in ~ df-lm . $)
    iscau $p |- ( M e. Met -> ( F e. ( Cau ` M ) <-> ( F (_ ( CC X. X ) /\
                 A. x e. RR ( 0 < x -> E. j e. ZZ A. k e. ZZ A. m e. ZZ (
              ( j <_ k /\ j <_ m ) -> ( ( F ` k ) e. X /\ ( F ` m ) e. X /\
                 ( ( F ` k ) D ( F ` m ) ) < x ) ) ) ) ) ) $=
      ( vf cmt wcel cca cfv cc0 cv clt wbr cle wa co w3a wi cz wral wrex cr cc
      cxp cpw crab wss cab caufval df-rab visset elpw anbi1i abbii eqtr2
      syl6eq eleq2d wb wceq fveq1 eleq1d opreq12d breq1d 3anbi123d imbi2d
      ralbidv rexralbidv elrab a1i cvv axcnex c1st fvex eqeltr xpex elpw2g
      ax-mp syl6bb bitrd ) GLMZFGNOZMFPAQZRSZCQZDQZTSWJEQZTSUAZWKKQZOZHMZWLWNOZ
      HMZWOWQBUBZWHRSZUCZUDZEUEUFZDUEUFCUEUGZUDZAUHUFZKUIHUJZUKZULZMZFXGUMZWIWM
      WKFOZHMZWLFOZHMZXLXNBUBZWHRSZUCZUDZEUEUFZDUEUFCUEUGZUDZAUHUFZUAZWFWGXIFWF
      WGWNXGUMZXFUAZKUNZXIABKCDEGHIJUOXIWNXHMZXFUAZKUNYGXFKXHUPYIYFKYHYEXFWNXGK
      UQURUSUTVAVBVCWFXJFXHMZYCUAZYDXJYKVDWFXFYCKFXHWNFVEZXEYBAUHYLXDYAWIYLXCXT
      CDUEUEYLXBXSEUEYLXAXRWMYLWPXMWRXOWTXQYLWOXLHWKWNFVFZVGYLWQXNHWLWNFVFZVGYL
      WSXPWHRYLWOXLWQXNBYMYNVHVIVJVKVLVMVKVLVNVOYJXKYCXGVPMYJXKVDUIHVQHGVROVPIG
      VRVSVTWAFXGVPWBWCUSWDWE $.
      $( [8-Dec-2006] $) $( [7-Dec-2006] $)

    ${
      $d j k x D $.  $d j k m N $.  $d j k x X $.  $d j k m x y Z $.
      iscau3.3 $e |- N e. ZZ $.
      iscau3.4 $e |- Z = ( ZZ> ` N ) $.
      $( Express the property " ` F ` is a Cauchy sequence of metric space
         ` M ` ," using an upper partition of ` ZZ ` . $)
      iscau2 $p |- ( M e. Met -> ( F e. ( Cau ` M ) <-> ( F (_ ( CC X. X ) /\
                 A. x e. RR ( 0 < x -> E. j e. Z A. k e. Z A. m e. Z (
              ( j <_ k /\ j <_ m ) -> ( ( F ` k ) e. X /\ ( F ` m ) e. X /\
                 ( ( F ` k ) D ( F ` m ) ) < x ) ) ) ) ) ) $=
        ( cmt wcel cca cfv cc cxp wss cc0 cv clt wbr cle wa co w3a wi cz wral
        wrex cr iscau cau5 imbi2i ralbii anbi2i syl6bb ) GOPFGQRPFSITUAZUBAUCZU
        DUEZCUCZDUCZUFUEVDEUCZUFUEUGVEFRZIPVFFRZIPVGVHBUHVBUDUEUIZUJZEUKULDUKUL
        CUKUMZUJZAUNULZUGVAVCVJEJULDJULCJUMZUJZAUNULZUGABCDEFGIKLUOVMVPVAVLVOAU
        NVKVNVCVIDECHJMNUPUQURUSUT $.
        $( [23-Dec-2006] $) $( [19-Dec-2006] $)

      $( Express the property " ` F ` is a Cauchy sequence of metric space
         ` M ` " with one less quantifier. $)
      iscau3 $p |- ( M e. Met -> ( F e. ( Cau ` M ) <-> ( F (_ ( CC X. X ) /\
                   A. x e. RR ( 0 < x -> E. j e. Z A. k e. Z (
                j <_ k -> ( ( F ` j ) e. X /\ ( F ` k ) e. X /\
                   ( ( F ` j ) D ( F ` k ) ) < x ) ) ) ) ) ) $=
        ( vm vy cmt wcel cca cfv cc cxp wss cc0 cv clt wbr cle wa co w3a wi
        wral wrex cr iscau2 c2 cdiv wceq fveq2 eleq1d opreq2d breq1d 3anbi23d
        opreq1d 3anbi13d breq2 3anbi3d anbi12d 3simp2 adantr a1i adantl caddc
        lt2halvest mscl 3expb adantlr adantrrr adantrrl simplr syl3anc mstri2
        exp43 imp44 ancom2s lelttrt adantrl axaddrcl sylanc mpand syld exp32
        adantld 3impd an6 syl5ib 3jcad cuz cz uzssz eqsstr cau3 anbi2d bitr4d
        ) FPQZEFRSQETHUAUBZUCAUDZUEUFZNUDZCUDZUGUFXIDUDZUGUFUHXJESZHQZXKESZHQZX
        LXNBUIZXGUEUFZUJZUKDIULCIULNIUMUKAUNULZUHXFXHXJXKUGUFXRUKDIULCIUMUKAUNU
        LZUHABNCDEFGHIJKLMUOXEXTXSXFXRXMXIESZHQZXLYABUIZXGUEUFZUJZXOYBXNYABUIZX
        GUEUFZUJXMXOXPOUDZUPUQUIZUEUFZUJZXMYBYCYIUEUFZUJZUHZXOYBYFYHUEUFZUJZAOC
        DNIXEXKXIURZXOYBXQYDXMYQXNYAHXKXIEUSZUTYQXPYCXGUEYQXNYAXLBYRVAVBVCXJXKU
        RZXMXOYDYGYBYSXLXNHXJXKEUSZUTYSYCYFXGUEYSXLXNYABYTVDVBVEXGYIURZXRYKYEYM
        UUAXQYJXMXOXGYIXPUEVFVGUUAYDYLXMYBXGYIYCUEVFVGVHXGYHURYGYOXOYBXGYHYFUEV
        FVGXEYHUNQZUHZYNYPUKXJIQXKIQXIIQUJUUCYNXOYBYOYNXOUKUUCYKXOYMXMXOYJVIVJV
        KYNYBUKUUCYMYBYKXMYBYLVIVLVKUUCXMXMUHZXOYBUHZYJYLUHZUJYOYNUUCUUDUUEUUFY
        OUUCXMUUEUUFYOUKZUKXMUUCXMUUEUUGUUCXMUUEUHZUHZUUFXPYCVMUIZYHUEUFZYOXPUN
        QZYCUNQZUUBUUFUUKUKUUIXPYCYHVNUUCXMXOUULYBXEXMXOUHUULUUBXEXMXOUULXLXNBF
        HJKVOVPZVQVRUUCXMYBUUMXOXEXMYBUHUUMUUBXEXMYBUUMXLYABFHJKVOVPZVQVSXEUUBU
        UHVTZWAUUIYFUUJUGUFZUUKYOXEUUHUUQUUBXEUUEXMUUQXEXOYBXMUUQXEXOYBXMUUQXNY
        AXLBFHJKWBWCWDWEVQYFUNQZUUJUNQZUUBUUQUUKUHYOUKUUIYFUUJYHWFXEUUHUURUUBXE
        UUEUURXMXEXOYBUURXNYABFHJKVOVPWGVQXEUUHUUSUUBUULUUMUUSXEUUHUHXPYCWHXEXM
        XOUULYBUUNVRXEXMYBUUMXOUUOVSWIVQUUPWAWJWKWLWMWNXMXOYJXMYBYLWOWPWQVJIGWR
        SWSMGWTXAXBXCXD $.
        $( [25-Dec-2006] $) $( [14-Dec-2006] $)
    $}

    ${
      $d j k x D $.  $d j k x X $.
      iscau2ALT $p |- ( M e. Met -> ( F e. ( Cau ` M ) <-> ( F (_ ( CC X. X )
                /\ A. x e. RR ( 0 < x -> E. j e. ZZ A. k e. ZZ (
                j <_ k -> ( ( F ` j ) e. X /\ ( F ` k ) e. X /\
                   ( ( F ` j ) D ( F ` k ) ) < x ) ) ) ) ) ) $=
        ( vm vy cmt wcel cca cfv cc cxp wss cc0 cv clt wbr cle wa co w3a wi cz
        wral wrex cr iscau c2 cdiv wceq fveq2 eleq1d opreq2d breq1d 3anbi23d
        opreq1d 3anbi13d breq2 3anbi3d anbi12d 3simp2 adantr a1i adantl caddc
        lt2halvest mscl 3expb adantlr adantrrr adantrrl simplr syl3anc mstri2
        exp43 imp44 ancom2s lelttrt adantrl axaddrcl sylanc mpand syld exp32
        adantld 3impd an6 syl5ib 3jcad ssid cau3 anbi2d bitr4d ) FLMZEFNOMEPGQR
        ZSATZUAUBZJTZCTZUCUBXCDTZUCUBUDXDEOZGMZXEEOZGMZXFXHBUEZXAUAUBZUFZUGDUHU
        ICUHUIJUHUJUGAUKUIZUDWTXBXDXEUCUBXLUGDUHUICUHUJUGAUKUIZUDABJCDEFGHIULWS
        XNXMWTXLXGXCEOZGMZXFXOBUEZXAUAUBZUFZXIXPXHXOBUEZXAUAUBZUFXGXIXJKTZUMUNU
        EZUAUBZUFZXGXPXQYCUAUBZUFZUDZXIXPXTYBUAUBZUFZAKCDJUHWSXEXCUOZXIXPXKXRXG
        YKXHXOGXEXCEUPZUQYKXJXQXAUAYKXHXOXFBYLURUSUTXDXEUOZXGXIXRYAXPYMXFXHGXDX
        EEUPZUQYMXQXTXAUAYMXFXHXOBYNVAUSVBXAYCUOZXLYEXSYGYOXKYDXGXIXAYCXJUAVCVD
        YOXRYFXGXPXAYCXQUAVCVDVEXAYBUOYAYIXIXPXAYBXTUAVCVDWSYBUKMZUDZYHYJUGXDUH
        MXEUHMXCUHMUFYQYHXIXPYIYHXIUGYQYEXIYGXGXIYDVFVGVHYHXPUGYQYGXPYEXGXPYFVF
        VIVHYQXGXGUDZXIXPUDZYDYFUDZUFYIYHYQYRYSYTYIYQXGYSYTYIUGZUGXGYQXGYSUUAYQ
        XGYSUDZUDZYTXJXQVJUEZYBUAUBZYIXJUKMZXQUKMZYPYTUUEUGUUCXJXQYBVKYQXGXIUUF
        XPWSXGXIUDUUFYPWSXGXIUUFXFXHBFGHIVLVMZVNVOYQXGXPUUGXIWSXGXPUDUUGYPWSXGX
        PUUGXFXOBFGHIVLVMZVNVPWSYPUUBVQZVRUUCXTUUDUCUBZUUEYIWSUUBUUKYPWSYSXGUUK
        WSXIXPXGUUKWSXIXPXGUUKXHXOXFBFGHIVSVTWAWBVNXTUKMZUUDUKMZYPUUKUUEUDYIUGU
        UCXTUUDYBWCWSUUBUULYPWSYSUULXGWSXIXPUULXHXOBFGHIVLVMWDVNWSUUBUUMYPUUFUU
        GUUMWSUUBUDXJXQWEWSXGXIUUFXPUUHVOWSXGXPUUGXIUUIVPWFVNUUJVRWGWHWIWJWKXGX
        IYDXGXPYFWLWMWNVGUHWOWPWQWR $.
        $( [?] $) $( [14-Dec-2006] $)
    $}
  $}

  ${
    $d j k x F $.  $d j k x M $.  $d j k x X $.
    caun0.1 $e |- X = ( 1st ` M ) $.
    $( A metric space with a Cauchy sequence cannot be empty. $)
    caun0 $p |- ( ( M e. Met /\ F e. ( Cau ` M ) ) -> X =/= (/) ) $=
      ( vx vj vk cmt wcel cca cfv c0 wne cc0 cv clt wbr cn wi wrex cr wral cc
      cxp wss cle c2nd co w3a wa c1 eqid 1z nnuz iscau3 3simp1 imim2i r19.20si
      wceq breq2 imbi1d rcla4cv nnret leidt pm2.27 3syl a2i r19.22si adantl
      syl6bi lt01 a1bi syl6bbr 1re ne0i com12 r19.23aiv sylbir syl syl6 imp )
      BHIZABJKIZCLMZWBWCNEOZPQZFOZRIZWGAKZCIZSZFRTZSZEUAUBZWDWBWCAUCCUDUEZWFWGG
      OZUFQZWJWPAKZCIZWIWRBUGKZUHWEPQZUIZSZGRUBZFRTZSZEUAUBZUJWNEWTFGABUKCRDWTU
      LUMUNUOXGWNWOXFWMEUAXEWLWFXDWKFRXDWQWJSZGRUBWHWGWGUFQZWJSZSWKXCXHGRXBWJWQ
      WJWSXAUPUQURXHXJGWGRWPWGUSWQXIWJWPWGWGUFUTVAVBWHXJWJWHWGUAIXIXJWJSWGVCWGV
      DXIWJVEVFVGVFVHUQURVIVJWNUKUAIZWLSZWDWMWLEUKUAWEUKUSZWMNUKPQZWLSWLXMWFXNW
      LWEUKNPUTVAXNWLVKVLVMVBXLWLWDXKWLVNVLWKWDFRWKWHWDWJWDWHCWIVOUQVPVQVRVSVTW
      A $.
      $( [25-Dec-2006] $) $( [19-Dec-2006] $)
  $}

  ${
    $d f x F $.  $d f x y M $.  $d f x y X $.
    iscms.1 $e |- X = ( 1st ` M ) $.
    $( The property " ` M ` is a complete metric space," meaning all Cauchy
       sequences converge to a point in the space.  Part of Definition 1.4-3 of
       [Kreyszig] p. 28. $)
    iscms $p |- ( M e. CMet <-> ( M e. Met /\
                 A. f e. ( Cau ` M ) E. x e. X f ( ~~>m ` M ) x ) ) $=
      ( vy cv clm cfv wbr c1st wrex cca wral cmt cms wceq fveq2 raleq1d
      syl6eqr rexeq1d wb breq syl rexbidv bitrd ralbidv df-cms elrab2 ) BGZAGZF
      GZHIZJZAULKIZLZBULMIZNZUJUKCHIZJZADLZBCMIZNZFCOPULCQZURUPBVBNVCVDUPBUQVBU
      LCMRSVDUPVABVBVDUPUNADLVAVDUNAUODVDUOCKIDULCKRETUAVDUNUTADVDUMUSQUNUTUBUL
      CHRUJUKUMUSUCUDUEUFUGUFFABUHUI $.
      $( [5-Oct-2006] $) $( [7-Sep-2006] $)

    $( The convergence of a Cauchy sequence in a complete metric space. $)
    cmscvg $p |- ( ( M e. CMet /\ F e. ( Cau ` M ) ) ->
                 E. x e. X F ( ~~>m ` M ) x ) $=
      ( vf cv clm cfv wbr wrex cca wral wcel cms wceq breq1 rexbidv rcla4cva
      cmt iscms pm3.27bd sylan ) FGZAGZCHIZJZADKZFCLIZMZBUINBUEUFJZADKZCONZUHUL
      FBUIUDBPUGUKADUDBUEUFQRSUMCTNUJAFCDEUAUBUC $.
      $( [?] $) $( [19-Dec-2006] $)
  $}

  ${
    $d j k m x F $.  $d j k m x M $.  $d j k x P $.  $d m X $.
    lmfss.1 $e |- X = ( 1st ` M ) $.
    $( Inclusion of a function having a limit (used to ensure the limit
       relation is a set), under our definition. $)
    lmfss $p |- ( ( M e. Met /\ P e. A /\ F ( ~~>m ` M ) P ) ->
               F (_ ( CC X. X ) ) $=
      ( vx vj vk cmt wcel clm cfv wbr cc cxp wss wa cc0 cv clt cle c2nd co wi
      cz wral wrex cr w3a eqid lmbr 3simp1 syl6bi 3impia ) DJKZBAKZCBDLMNZCOEPQ
      ZUPUQRURUSBEKZSGTZUANHTITZUBNVBCMZEKVCBDUCMZUDVAUANRUEIUFUGHUFUHUEGUIUGZU
      JUSGAVDBHICDEFVDUKULUSUTVEUMUNUO $.
      $( [?] $) $( [7-Dec-2006] $)

    $( Closure of a limit. $)
    lmcl $p |- ( ( M e. Met /\ P e. A /\ F ( ~~>m ` M ) P ) ->
               P e. X ) $=
      ( vx vj vk cmt wcel clm cfv wbr wa cc cxp wss cc0 cv clt cle c2nd co wi
      cz wral wrex cr w3a eqid lmbr 3simp2 syl6bi 3impia ) DJKZBAKZCBDLMNZBEKZU
      PUQOURCPEQRZUSSGTZUANHTITZUBNVBCMZEKVCBDUCMZUDVAUANOUEIUFUGHUFUHUEGUIUGZU
      JUSGAVDBHICDEFVDUKULUTUSVEUMUNUO $.
      $( [19-Dec-2006] $) $( [19-Dec-2006] $)

    $( Inclusion of a Cauchy sequence, under our definition. $)
    caufss $p |- ( ( M e. Met /\ F e. ( Cau ` M ) ) ->
               F (_ ( CC X. X ) ) $=
      ( vx vj vk vm cmt wcel cca cfv wa cc cxp wss cc0 cv clt wbr cle c2nd co
      w3a wi cz wral wrex cr eqid iscau biimpa pm3.26d ) BIJZABKLJZMANCOPZQERZS
      TFRZGRZUATURHRZUATMUSALZCJUTALZCJVAVBBUBLZUCUQSTUDUEHUFUGGUFUGFUFUHUEEUIU
      GZUNUOUPVDMEVCFGHABCDVCUJUKULUM $.
      $( [21-Dec-2006] $) $( [7-Dec-2006] $)
  $}

  ${
    $d f x M $.
    $( A complete metric space is a metric space. $)
    cmms $p |- ( M e. CMet -> M e. Met ) $=
      ( vf vx cms wcel cmt cv clm cfv wbr c1st wrex cca wral eqid iscms
      pm3.26bd ) ADEAFEBGCGAHIJCAKIZLBAMINCBARROPQ $.
      $( [18-Dec-2006] $) $( [18-Dec-2006] $)
  $}

  ${
    $d j k w x y z F $.  $d j k w M $.  $d j k w x y z P $.
    lmclim.1 $e |- D = { <. <. x , y >. , z >. | ( ( x e. CC /\ y e. CC ) /\
                 z = ( abs ` ( x - y ) ) ) } $.
    lmclim.2 $e |- M = <. CC , D >. $.
    $( Relate a limit on the metric space of complex numbers to our complex
       number limit notation. $)
    lmclim $p |- ( P e. A ->
                 ( F ( ~~>m ` M ) P <-> ( F (_ ( CC X. CC ) /\ F ~~> P ) ) ) $=
      ( vw vj vk wcel clm cfv wbr cc cxp wss cc0 cv clt cle co wa wi cz wral
      wrex cr w3a cli cmt wb cop cnms eqeltr c1st fveq2i axcnex op1st eqtr2
      c2nd cmin cabs oprabex2 op2nd lmbr mpan cvv clim xpex ssex sylan wceq
      cnmsdval ancoms breq1d pm5.32da imbi2d rexralbidv ralbidv pm5.32i
      syl6rbbr 3anass syl5bb bitrd ) FDNZGFHOPQZGRRSZTZFRNZUAKUBZUCQZLUBMUBZUDQ
      ZWPGPZRNZWRFEUEZWNUCQZUFZUGZMUHUILUHUJZUGZKUKUIZULZWLGFUMQZUFZHUNNWIWJXGU
      OHREUPZUNJABCEIUQURKDEFLMGHRHUSPXJUSPRHXJUSJUTREVAVBVCHVDPXJVDPEHXJVDJUTR
      EVAABCRRAUBBUBVEUEVFPEVAVAIVGVHVCVIVJWIWLWMXFUFZUFXIXGWIWLXKXHWLWIXKXHUOW
      LWIUFXHWMWOWQWSWRFVEUEVFPZWNUCQZUFZUGZMUHUILUHUJZUGZKUKUIZUFZXKGVKNWIXHXS
      UOWLKFVKDLMGVLGWKRRVAVAVMVNVOWMXFXRWMXEXQKUKWMXDXPWOWMXCXOLMUHUHWMXBXNWQW
      MWSXAXMWMWSUFWTXLWNUCWSWMWTXLVPABCWRFEIVQVRVSVTWAWBWAWCWDWEVRVTWLWMXFWFWG
      WH $.
      $( [20-Dec-2006] $) $( [9-Dec-2006] $)
  $}

  ${
    $d h z F $.  $d h z N $.  $d h z X $.  $d a h z $.
    iscms2lem1.1 $e |- G = { <. h , z >. | ( h e. NN /\
                       z = if ( ( F ` h ) e. X , ( F ` h ) , a ) ) } $.
    $( Lemma for ~ iscms2 .  ` G ` is a function from ` NN ` to the metric
       space that we construct from an arbitrary sequence ` F ` . $)
    iscms2lem1 $p |- ( a e. X -> G : NN --> X ) $=
      ( cv wcel cfv cif cn wral wf wa wn wo idd ancld con3d anc2ri orrd ifel
      sylibr adantr r19.21aiva fopab2 sylib ) FHZEIZBHZCJZEIZULUIKZEIZBLMLEDNUJ
      UOBLUJUOUKLIUJUMUMOZUMPZUJOZQUOUJUPURUJUPPUQUJUMUPUJUMUMUJUMRSTUAUBUMULUI
      EUCUDUEUFBALEUNDGUGUH $.
      $( [25-Dec-2006] $) $( [19-Dec-2006] $)

    $( Lemma for ~ iscms2 .  When the value of ` F ` exists, it equals the
       value of ` G ` . $)
    iscms2lem2 $p |- ( ( N e. NN /\ ( F ` N ) e. X ) ->
                     ( G ` N ) = ( F ` N ) ) $=
      ( cn wcel cfv wa cv cif wceq fveq2 eleq1d ifbid ifeq1d eqtrd fvopab4g
      iftrue id eqeltrd sylan2 adantl ) EIJZECKZFJZLEDKZUIUHGMZNZUHUGULFJUJULOU
      IBAEBMZCKZFJZUNUKNZULIFDUMEOZUPUIUNUKNULUQUOUIUNUKUQUNUHFUMECPZQRUQUIUNUH
      UKURSTHUAUIULUHFUIUHUKUBZUIUCUDUEUIULUHOUGUSUFT $.
      $( [26-Dec-2006] $) $( [19-Dec-2006] $)

    ${
      $d j k m n x y D $.  $d a j k m n x y F $.  $d g j k m n x y G $.
      $d a g j k m n x y M $.  $d a g j k m n x y X $.  $d h j k z $.
      iscms2lem3.2 $e |- X = ( 1st ` M ) $.
      iscms2lem3.3 $e |- D = ( 2nd ` M ) $.
      $( Lemma for ~ iscms2 .  If the arbitrary sequence ` F ` is Cauchy, so
         is the constructed function ` G ` . $)
      iscms2lem3 $p |- ( ( M e. Met /\ F e. ( Cau ` M ) /\ a e. X ) ->
                       G e. ( Cau ` M ) ) $=
        ( vx vj vk cmt wcel cca cfv cv w3a cc cxp wss cc0 clt wbr cle co wi cn
        wral wrex cr wa wf iscms2lem1 fssxp syl nnsscn ssid ssxp mp2an a1i
        sstrd 3ad2ant3 wb c1 1z nnuz iscau3 adantr ffvrn sylan a1d adantlr
        wceq iscms2lem2 opreqan12d an4s breq1d biimprd exp32 3impd adantll
        3jcad imim2d r19.20dva r19.22dva r19.20sdv adantl adantld sylbid
        3impia 3com23 jca 3ad2ant1 mpbird ) FOPZDFQRZPZHSGPZTZEWSPZEUAGUBZUCZUD
        LSZUEUFZMSZNSZUGUFZXHERZGPZXIERZGPZXKXMBUHZXFUEUFZTZUIZNUJUKZMUJULZUIZL
        UMUKZUNZXBXEYBXAWRXEWTXAEUJGUBZXDXAUJGEUOZEYDUCACDEGHIUPZUJGEUQURYDXDUC
        ZXAUJUAUCGGUCYGUSGUTUJUAGGVAVBVCVDVEWRXAWTYBWRXAWTYBWRXAUNZWTDXDUCZXGXJ
        XHDRZGPZXIDRZGPZYJYLBUHZXFUEUFZTZUIZNUJUKZMUJULZUIZLUMUKZUNZYBWRWTUUBVF
        XALBMNDFVGGUJJKVHVIVJVKYHUUAYBYIXAUUAYBUIWRXAYTYALUMXAYSXTXGXAYRXSMUJXA
        XHUJPZUNZYQXRNUJUUDXIUJPZUNZYPXQXJUUFYPXLXNXPUUFXLYPUUDXLUUEYEUUCXLXAUJ
        GXHEVLYFVMVKVNUUFXNYPXAUUEXNUUCYEUUEXNXAUJGXIEVLYFVMVOVNUUCUUEYPXPUIXAU
        UCUUEUNZYKYMYOXPUUGYKYMYOXPUIUUGYKYMUNUNZXPYOUUHXOYNXFUEUUCYKUUEYMXOYNV
        PUUCYKUNUUEYMUNXKYJXMYLBACDEXHGHIVQACDEXIGHIVQVRVSVTWAWBWCWDWEWFWGWHWFW
        IWJWKWLWMWNWOWRWTXCYCVFXALBMNEFVGGUJJKVHVIVJWPWQ $.
        $( [26-Dec-2006] $) $( [19-Dec-2006] $)

      $( Lemma for ~ iscms2 .  Whenever the constructed function ` G `
         converges, so does the arbitrary Cauchy sequence ` F ` . $)
      iscms2lem4 $p |- ( ( M e. Met /\ F e. ( Cau ` M ) /\
                        G ( ~~>m ` M ) x ) -> F ( ~~>m ` M ) x ) $=
        ( vy vj vk vm vn cmt wcel cca cfv cv clm wbr w3a cc cxp wss cc0 clt
        cle co wa wi cn wral wrex cr caufss 3adant3 cvv visset lmcl mp3an2
        3adant2 c1 1z nnuz iscau3 pm3.27 syl6bi wb lmbr2 mpan2 3simp3 anim12d
        cif iscms2lem2 eleq1d opreq1d breq1d anbi12d ex 3simp2 syl5 imim2d imp
        adantrd pm5.74d pm3.42 syl5bi imp3a adantl maxlet nnret syl3an 3expa
        imbi1d sylibrd exp3a r19.20dva r19.20 syl6 wceq breq1 ralbidv rcla4ev
        ifcl ancoms sylan adantr syld an1rs r19.23adva r19.23aiva imim3i
        r19.20sii 3impib 3jca 3ad2ant1 mpbird ) GRSZEGTUASZFAUBZGUCUAZUDZUEZEYD
        YEUDZEUFHUGZUHZYDHSZUIMUBZUJUDZNUBZOUBZUKUDZYOEUAZHSZYQYDCULZYLUJUDZUMZ
        UNZOUOUPZNUOUQZUNZMURUPZUEZYGYJYKUUFYBYCYJYFEGHKUSUTYBYFYKYCYBYDVASZYFY
        KAVBZVAYDFGHKVCVDVEYBYCYFUUFYBYCYFUMYMPUBZYOUKUDZUUJEUAZHSZYRUULYQCULYL
        UJUDZUEZUNZOUOUPZPUOUQZUNZMURUPZYMQUBZYOUKUDZYOFUAZHSZUVCYDCULZYLUJUDZU
        MZUNZOUOUPZQUOUQZUNZMURUPZUMUUFYBYCUUTYFUVLYBYCYJUUTUMUUTMCPOEGVFHUOKLV
        GVHVIYJUUTVJVKYBYFFYIUHZYKUVLUEZUVLYBUUHYFUVNVLUUIMVACYDQOFGVFHUOKLVGVH
        VMVNUVMYKUVLVOVKVPUUTUVLUUFUUSUVKUUEMURUURUVJUUDYMUUQUVJUUDUNPUOUUJUOSZ
        UUQUMUVIUUDQUOUVOUVAUOSZUUQUVIUUDUNUVOUVPUMZUUQUMUVIUUJUVAUKUDZUVAUUJVQ
        ZYOUKUDZUUAUNZOUOUPZUUDUVQUUQUVIUWBUNZUVQUUQUVHUWAUNZOUOUPUWCUVQUUPUWDO
        UOUVQYOUOSZUMZUUPUVHUWAUWFUUPUVHUMZUUKUVBUMZUUAUNZUWAUWEUWGUWIUNUVQUWEU
        UPUVHUWIUWEUUPUVHUWIUNUWEUUPUMZUWHUVGUNUWIUVHUWJUWHUVGUUAUWJUUKUVGUUAVL
        ZUVBUWEUUPUUKUWKUNUWEUUOUWKUUKUWEYRUWKUUOUWEYRUWKUWEYRUMZUVDYRUVFYTUWLU
        VCYQHBDEFYOHIJVRZVSUWLUVEYSYLUJUWLUVCYQYDCUWMVTWAWBWCUUMYRUUNWDWEWFWGWH
        WIUUKUVBUVGWJWKWCWLWMUWFUVTUWHUUAUVOUVPUWEUVTUWHVLZUUJURSUVAURSYOURSUWN
        UVOUVPUWEUUJUVAYOWNUUJWOUVAWOYOWOWPWQWRWSWTXAUVHUWAOUOXBXCWGUVQUWBUUDUN
        UUQUVQUWBUUDUVSUOSZUWBUUDUVQUUCUWBNUVSUOYNUVSXDZUUBUWAOUOUWPYPUVTUUAYNU
        VSYOUKXEWRXFXGUVPUVOUWOUVRUVAUUJUOXHXIXJWCXKXLXMXNXOXPXQWGXCXRXSYBYCYHU
        UGVLZYFYBUUHUWQUUIMVACYDNOEGVFHUOKLVGVHVMVNXTYA $.
        $( [26-Dec-2006] $) $( [19-Dec-2006] $)

      $( Lemma for ~ iscms2 .  If all Cauchy sequences ` g ` that are functions
         on ` NN ` converge, then any arbitrary Cauchy sequence ` F ` also
         converges. $)
      iscms2lem5 $p |- ( ( ( M e. Met /\ A. g e. ( Cau ` M ) ( g : NN --> X ->
                       E. x e. X g ( ~~>m ` M ) x ) ) /\ F e. ( Cau ` M ) ) ->
                       E. x e. X F ( ~~>m ` M ) x ) $=
        ( cmt wcel cn cv wf clm cfv wbr wrex wi cca wral wa c0 wne caun0
        adantlr wex iscms2lem3 3expa adantllr wceq feq1 breq1 rexbidv imbi12d
        rcla4cv iscms2lem1 syl7 com23 imp adantll mpd iscms2lem4 ex r19.22sdv
        adantr 19.23adv ne0 syl5ib ) HNOZPIDQZRZVOAQZHSTZUAZAIUBZUCZDHUDTZUEZUF
        ZFWBOZUFZIUGUHZFVQVRUAZAIUBZVNWEWGWCFHILUIUJWFJQIOZJUKWIWGWFWJWIJWFWJWI
        WFWJUFZGVQVRUAZAIUBZWIWKGWBOZWMVNWEWJWNWCVNWEWJWNBCEFGHIJKLMULUMUNWDWJW
        NWMUCZWEWCWJWOVNWCWJWOWCWNWJWMWCWNPIGRZWMWJWAWPWMUCDGWBVOGUOZVPWPVTWMPI
        VOGUPWQVSWLAIVOGVQVRUQURUSUTBEFGIJKVAVBVCVDVEUJVFWFWMWIUCZWJVNWEWRWCVNW
        EUFZWLWHAIWSWLWHVNWEWLWHABCEFGHIJKLMVGUMVHVIUJVJVFVHVKJIVLVMVF $.
        $( [28-Dec-2006] $) $( [19-Dec-2006] $)
    $}
  $}

  ${
    $d a f g x M $.  $d a f g m x y X $.
    iscms2.1 $e |- X = ( 1st ` M ) $.
    $( The property " ` M ` is a complete metric space" expressed in terms of
       functions on ` NN ` .  Thus we only have to look at functions on
       ` NN ` , and not all possible Cauchy sequences, to determine
       completeness. $)
    iscms2 $p |- ( M e. CMet <-> ( M e. Met /\
     A. f e. ( Cau ` M ) ( f : NN --> X -> E. x e. X f ( ~~>m ` M ) x ) ) ) $=
      ( vg vy vm va cms wcel cmt cn cv wf clm cfv wbr wrex wi cca wral wa
      iscms ax-1 r19.20si anim2i sylbi c2nd cif wceq copab eqid iscms2lem5
      r19.21aiva ex imdistani sylibr impbi ) CJKZCLKZMDBNZOZVBANZCPQZRADSZTZBCU
      AQZUBZUCZUTVAVFBVHUBZUCVJABCDEUDVKVIVAVFVGBVHVFVCUEUFUGUHVJVAFNZVDVERADSZ
      FVHUBZUCUTVAVIVNVAVIVNVJVMFVHAGCUIQZBHVLHNZMKGNVPVLQZDKVQINUJUKUCHGULZCDI
      VRUMEVOUMUNUOUPUQAFCDEUDURUS $.
      $( [29-Dec-2006] $) $( [18-Dec-2006] $)
  $}

  ${
    $d f x M $.  $d f x X $.
    iscms2i.1 $e |- X = ( 1st ` M ) $.
    iscms2i.2 $e |- M e. Met $.
    iscms2i.3 $e |- ( ( f e. ( Cau ` M ) /\ f : NN --> X ) ->
                    E. x e. X f ( ~~>m ` M ) x ) $.
    $( Properties that determine a complete metric space. $)
    iscms2i $p |- M e. CMet $=
      ( cms wcel cmt cn cv wf clm cfv wbr wrex wi cca wral iscms2 ex rgen
      mpbir2an ) CHICJIKDBLZMZUEALCNOPADQZRZBCSOZTABCDEUAFUHBUIUEUIIUFUGGUBUCUD
      $.
      $( [16-Apr-2007] $) $( [15-Apr-2007] $)
  $}

