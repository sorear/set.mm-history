$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Metric space balls
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d t u w x y z D $.  $d t u w x y z X $.  $d v y $.
    blfval.1 $e |- X = dom dom D $.
    $( The value of the ball function. $)
    blfval $p |- ( D e. Met -> ( ball ` D ) =
  { <. <. x , y >. , z >. | ( ( x e. X /\ y e. RR ) /\ ( 0 < y /\ z =
                { w e. X | ( x D w ) < y } ) ) } ) $=
      ( vt vu cme wcel cv cr wa cc0 clt wbr co crab wceq copab2 cvv cbl cfv
      cdm dmexg syl syl5eqel reex eqid oprabex2g mpan2 wss simpr anim2i
      ssoprab2i ssexg mpan 3syl dmeq dmeqd syl6eqr eleq2d anbi1d rabeq opreq
      breq1d rabbidv eqtrd eqeq2d anbi2d anbi12d oprabbidv df-bl fvopab4g
      mpdan ) EJKZALZFKZBLZMKZNZOVTPQZCLZVRDLZERZVTPQZDFSZTZNZNZABCUAZUBKZEUCUD
      WLTVQFUBKZWBWINZABCUAZUBKZWMVQEUEZUEZUBFVQWRUBKWSUBKEJUFWRUBUFUGGUHWNMUBK
      WQUIABCFMWHUBUBWPWPUJUKULWLWPUMWQWMWKWOABCWJWIWBWCWIUNUOUPWLWPUBUQURUSHIE
      VRHLZUEZUEZKZWANZWCWDVRWEWTRZVTPQZDXBSZTZNZNZABCUAWLJUBUCWTETZXJWKABCXKXD
      WBXIWJXKXCVSWAXKXBFVRXKXBWSFXKXAWRWTEUTVAGVBZVCVDXKXHWIWCXKXGWHWDXKXGXFDF
      SZWHXKXBFTXGXMTXLXFDXBFVEUGXKXFWGDFXKXEWFVTPVRWEWTEVFVGVHVIVJVKVLVMHIABCD
      VNVOVP $.
      $( [5-Sep-2007] $) $( [30-Aug-2006] $)

    $( Alternate value of the ball function that simplifies its use with
       operation theorems. $)
    blfval2 $p |- ( D e. Met -> ( ball ` D ) = { <. <. x , y >. , z >. |
                   ( ( x e. X /\ y e. { v e. RR | 0 < v } )
                 /\ z = { w e. X | ( x D w ) < y } ) } ) $=
      ( cme wcel cbl cfv cv cr wa cc0 clt wbr co crab wceq copab2 blfval anass
      breq2 elrab anbi2i bitr4i anbi1i bitr3i oprabbii syl6eq ) FIJFKLAMZGJZBMZ
      NJZOZPUOQRZCMUMDMFSUOQRDGTUAZOOZABCUBUNUOPEMZQRZENTJZOZUSOZABCUBABCDFGHUC
      UTVEABCUTUQUROZUSOVEUQURUSUDVFVDUSVFUNUPUROZOVDUNUPURUDVCVGUNVBUREUONVAUO
      PQUEUFUGUHUIUJUKUL $.
      $( [16-Sep-2007] $) $( [31-Aug-2006] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d v w x y z D $.  $d w x y z P $.  $d x Q $.
    $d v w x y z R $.  $d v w x y z X $.
    blval.1 $e |- X = dom dom D $.
    $( The ball around a point ` P ` is the set of all points whose distance
       from ` P ` is less than the ball's radius ` R ` . $)
    blval $p |- ( ( ( D e. Met /\ P e. X ) /\
                  ( R e. RR /\ 0 < R ) ) -> ( P ( ball ` D ) R ) =
                  { x e. X | ( P D x ) < R } ) $=
      ( vy vz vv vw cme wcel wa cr cc0 clt wbr cbl cfv co cv crab wceq copab2
      blfval2 opreqd ad2antrr cvv simplr breq2 elrab biimpri adantl cdm dmexg
      syl syl5eqel rabexg opreq1 breq1d rabbidv eqid oprabval2g syl111anc
      eqtrd ) BKLZCELZMZDNLODPQZMZMZCDBRSZTZCDGUAZELHUAZOIUAZPQZINUBZLMJUAVNAUA
      ZBTZVOPQZAEUBZUCMGHJUDZTZCVSBTZDPQZAEUBZVFVMWDUCVGVJVFVLWCCDGHJAIBEFUEUFU
      GVKVGDVRLZWGUHLZWDWGUCVFVGVJUIVJWHVHWHVJVQVIIDNVPDOPUJUKULUMVFWIVGVJVFEUH
      LWIVFBUNZUNZUHEVFWJUHLWKUHLBKUOWJUHUOUPFUQWFAEUHURUPUGGHJCDEVRWBWGWCWEVOP
      QZAEUBUHVNCUCZWAWLAEWMVTWEVOPVNCVSBUSUTVAVODUCWLWFAEVODWEPUJVAWCVBVCVDVE
      $.
      $( [16-Sep-2007] $) $( [31-Aug-2006] $)

    $( Membership in a ball. $)
    elbl $p |- ( ( ( D e. Met /\ P e. X ) /\
                  ( R e. RR /\ 0 < R ) ) -> ( A e. ( P ( ball ` D ) R ) <->
                  ( A e. X /\ ( P D A ) < R ) ) ) $=
      ( vx cme wcel wa cr cc0 clt wbr cbl cfv co cv crab blval eleq2d wceq
      opreq2 breq1d elrab syl6bb ) BHICEIJDKILDMNJJZACDBOPQZIACGRZBQZDMNZGESZIA
      EICABQZDMNZJUGUHULAGBCDEFTUAUKUNGAEUIAUBUJUMDMUIACBUCUDUEUF $.
      $( [16-Sep-2007] $) $( [2-Sep-2006] $)

    $( Membership in a ball. $)
    elbl2 $p |- ( ( ( D e. Met /\ P e. X /\ A e. X ) /\
                  ( R e. RR /\ 0 < R ) ) -> ( A e. ( P ( ball ` D ) R ) <->
                  ( P D A ) < R ) ) $=
      ( cme wcel w3a cr cc0 clt wbr wa cbl cfv co wb elbl 3adantl3 ibar
      3ad2ant3 adantr bitr4d ) BGHZCEHZAEHZIZDJHKDLMNZNACDBOPQHZUGCABQDLMZNZUKU
      EUFUIUJULRUGABCDEFSTUHUKULRZUIUGUEUMUFUGUKUAUBUCUD $.
      $( [9-Mar-2007] $)

    $( Membership in a ball, with reversed distance function arguments. $)
    elbl3 $p |- ( ( ( D e. Met /\ P e. X /\ A e. X ) /\
                  ( R e. RR /\ 0 < R ) ) -> ( A e. ( P ( ball ` D ) R ) <->
                  ( A D P ) < R ) ) $=
      ( cme wcel w3a cr cc0 clt wbr wa cbl cfv co elbl2 wb metsym breq1d
      adantr bitrd ) BGHCEHAEHIZDJHKDLMNZNACDBOPQHCABQZDLMZACBQZDLMZABCDEFRUDUG
      UISUEUDUFUHDLCABEFTUAUBUC $.
      $( [10-Nov-2007] $)

    $( Membership in the range of the ball function.  Note that
       ` ran ( ball `` D ) ` is the collection of all balls for metric
       ` D ` . $)
    blrn $p |- ( D e. Met -> ( B e. ran ( ball ` D ) <->
         E. x e. X E. y e. { w e. RR | 0 < w }
                B = { z e. X | ( x D z ) < y } ) ) $=
      ( vv cme wcel cbl cfv crn cv cc0 clt wbr cr crab wa co wceq copab2 wrex
      blfval2 rneqd eleq2d cvv wral wb cdm dmexg syl syl5eqel rabexg a1d
      r19.21aivv eqid elrnoprabg bitrd ) FJKZEFLMZNZKEAOZGKBOZPDOQRDSTZKUAZIOVE
      COFUBVFQRZCGTZUCUAABIUDZNZKZEVJUCBVGUEAGUEZVBVDVLEVBVCVKABICDFGHUFUGUHVBV
      JUIKZBVGUJAGUJVMVNUKVBVOABGVGVBVOVHVBGUIKVOVBFULZULZUIGVBVPUIKVQUIKFJUMVP
      UIUMUNHUOVICGUIUPUNUQURABIGVGVJEUIVKVKUSUTUNVA $.
      $( [28-Jul-2008] $) $( [31-Aug-2006] $)

    $( Membership in the range of the ball function.  Note that
       ` ran ( ball `` D ) ` is the collection of all balls for metric
       ` D ` . $)
    blrn2 $p |- ( D e. Met -> ( B e. ran ( ball ` D ) <->
         E. x e. X E. y e. RR ( 0 < y /\
                B = { z e. X | ( x D z ) < y } ) ) ) $=
      ( vw cme wcel cbl cfv crn cv co clt wbr crab wceq cc0 cr wrex wa blrn
      breq2 elrab anbi1i anass bitri rexbii2 rexbii syl6bb ) EIJDEKLMJDANCNEOBN
      ZPQCFRSZBTHNZPQZHUARZUBZAFUBTUMPQZUNUCZBUAUBZAFUBABCHDEFGUDURVAAFUNUTBUQU
      AUMUQJZUNUCUMUAJZUSUCZUNUCVCUTUCVBVDUNUPUSHUMUAUOUMTPUEUFUGVCUSUNUHUIUJUK
      UL $.
      $( [18-Sep-2007] $) $( [31-Aug-2006] $)

    $( Two balls are disjoint if they don't overlap. $)
    bl2in $p |- ( ( ( D e. Met /\ P e. X /\ Q e. X ) /\
                ( R e. RR /\ 0 < R /\ R <_ ( ( P D Q ) / 2 ) ) ) ->
                ( ( P ( ball ` D ) R ) i^i ( Q ( ball ` D ) R ) ) = (/) ) $=
      ( vx cme wcel w3a cr cc0 clt wbr co c2 cdiv cle wa cv cbl cfv wn wral
      cin c0 wceq wb elbl 3simpa syl2an wi caddc cmul metcl 3expa 3adantl3
      3adantl2 readdcl syl11anc adantr ad2antrr 2re remulcl mpan 3ad2ant1
      adantl lt2add jca simp1 exp3a imp31 cc recn 2times syl breqtrrd 2pos
      lemuldiv2OLD mpan2 mp3an1 biimpar ancom1s anasss sylan 3adantr2 adantlr
      ltletrd ex mettri3 3exp2 3imp1 lenlt mpbid a1d pm2.65d elbl2 df-3an
      biimpri notbid sylibrd an1rs expimpd sylbid r19.21aiv disj sylibr ) AHIZB
      EIZCEIZJZDKIZLDMNZDBCAOZPQORNZJZSZGTZCDAUAUBZOZIZUCZGBDXSOZUDYCXTUEUFUGXQ
      YBGYCXQXRYCIZXREIZBXRAOZDMNZSZYBXHXISXLXMSZYDYHUHXKXPXRABDEFUIXHXIXJUJXLX
      MXOUJZUKXQYEYGYBXKYEXPYGYBULXKYESZXPSZYGCXRAOZDMNZUCZYBYLYGYOYLYGSZYNYFYM
      UMOZXNMNZYPYNYRYPYNSZYQPDUNOZXNYLYQKIZYGYNYKUUAXPYKYFKIZYMKIZUUAXHXIYEUUB
      XJXHXIYEUUBBXRAEFUOUPUQZXHXJYEUUCXIXHXJYEUUCCXRAEFUOUPURZYFYMUSUTZVAVBYLY
      TKIZYGYNXPUUGYKXLXMUUGXOPKIZXLUUGVCPDVDVEVFVGVBYLXNKIZYGYNXKUUIYEXPBCAEFU
      OZVBVBYSYQDDUMOZYTMYLYGYNYQUUKMNZYLYGYNUULUUBUUCSXLXLSYGYNSUULULYKXPYFYMD
      DVHYKUUBUUCUUDUUEVIXPXLXLXLXMXOVJZUUMVIUKVKVLYLYTUUKUGZYGYNXPUUNYKXLXMUUN
      XOXLDVMIUUNDVNDVOVPVFVGVBVQYLYTXNRNZYGYNXKXPUUOYEXKXLXOUUOXMUUIXLXOSUUOXK
      UUIXLXOUUOXLUUIXOUUOXLUUISUUOXOUUHXLUUIUUOXOUHZVCUUHXLUUIJLPMNUUPVRPDXNVS
      VTWAWBWCWDUUJWEWFWGVBWHWIYKYNYRUCZULXPYGYKUUQYNYKXNYQRNZUUQXHXIXJYEUURXHX
      IXJYEUURBCXRAEFWJWKWLYKUUIUUAUURUUQUHXKUUIYEUUJVAUUFXNYQWMUTWNWOVBWPWIYLY
      AYNXHXJYEJZYIYAYNUHYKXPXRACDEFWQXHXJYEUUSXIUUSXHXJSYESXHXJYEWRWSURYJUKWTX
      AXBXCXDXEGYCXTXFXG $.
      $( [11-Mar-2007] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d v x y z D $.  $d x S $.  $d v x y z X $.
    $d x y P $.  $d x y R $.  $d w y $.
    blf.1 $e |- X = dom dom D $.
    $( Mapping of a ball. $)
    blf $p |- ( D e. Met -> ( ball ` D ) : ( X X. ( 0 (,) +oo ) ) --> ~P X ) $=
      ( vx vy vz vv vw cbl cfv cv wcel cc0 cpnf cioo co wa clt wbr crab wceq
      copab2 cme cxp cpw wf cr blfval2 ioopos eleq2i anbi2i anbi1i oprabbii
      syl6eqr feq1 wral cvv cdm dmexg syl syl5eqel wss ssrab2 elpw2g mpbiri
      a1d r19.21aivv eqid foprab2 sylib syl5bir mpcom ) AIJZDKZBLZEKZMNOPZLZQZF
      KVNGKAPVPRSZGBTZUAZQZDEFUBZUAZAUCLZBVQUDZBUEZVMUFZWFVMVOVPMHKRSHUGTZLZQZW
      BQZDEFUBWDDEFGHABCUHWCWMDEFVSWLWBVRWKVOVQWJVPHUIUJUKULUMUNWEWIWGWHWDUFZWF
      WGWHVMWDUOWFWAWHLZEVQUPDBUPWNWFWODEBVQWFWOVSWFBUQLZWOWFAURZURZUQBWFWQUQLW
      RUQLAUCUSWQUQUSUTCVAWPWOWABVBVTGBVCWABUQVDVEUTVFVGDEFBVQWAWHWDWDVHVIVJVKV
      L $.
      $( [7-May-2007] $)

    $( A ball contains its center. $)
    blcntr $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
                 P e. ( P ( ball ` D ) R ) ) $=
      ( cme wcel wa cr cc0 clt wbr cbl cfv co elbl simplr wceq met0 adantr
      simprr eqbrtrd mpbir2and ) AFGZBDGZHZCIGZJCKLZHZHZBBCAMNOGUEBBAOZCKLBABCD
      EPUDUEUIQUJUKJCKUFUKJRUIBADESTUFUGUHUAUBUC $.
      $( [22-Sep-2007] $) $( [2-Sep-2006] $)

    $( A ball is not empty. $)
    bln0 $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
                 ( P ( ball ` D ) R ) =/= (/) ) $=
      ( cme wcel wa cr cc0 clt wbr cbl cfv co c0 wne blcntr ne0i syl ) AFGBDGHC
      IGJCKLHHBBCAMNOZGUAPQABCDERUABST $.
      $( [6-Oct-2007] $)

    $( Membership in the range of the ball function.  Note that
       ` ran ( ball `` D ) ` is the collection of all balls for metric
       ` D ` . $)
    blrn3 $p |- ( D e. Met -> ( A e. ran ( ball ` D ) <->
         E. x e. X E. y e. RR ( 0 < y /\ A = ( x ( ball ` D ) y ) ) ) ) $=
      ( vz cme wcel cbl cfv crn cc0 cv clt wbr co crab wceq wa cr wrex blrn2
      blval anassrs eqeq2d pm5.32da rexbidva bitr4d ) DHIZCDJKZLIMBNZOPZCANZGND
      QULOPGERZSZTZBUAUBZAEUBUMCUNULUKQZSZTZBUAUBZAEUBABGCDEFUCUJVBURAEUJUNEITZ
      VAUQBUAVCULUAIZTZUMUTUPVEUMTUSUOCVCVDUMUSUOSGDUNULEFUDUEUFUGUHUHUI $.
      $( [22-Sep-2007] $) $( [31-Aug-2006] $)

    $( A ball belongs to the set of balls of a metric space. $)
    blelrn $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
                    ( P ( ball ` D ) R ) e. ran ( ball ` D ) ) $=
      ( vy vx cme wcel wa cr cc0 clt wbr cbl cfv co crn cv wceq wrex simplr
      simprl simprr eqid jctir opreq1 eqeq2d anbi2d breq2 opreq2 anbi12d
      rcla42ev syl111anc wb blrn3 ad2antrr mpbird ) AHIZBDIZJZCKIZLCMNZJZJZBCAO
      PZQZVFRIZLFSZMNZVGGSZVIVFQZTZJZFKUAGDUAZVEUTVBVCVGVGTZJZVOUSUTVDUBVAVBVCU
      CVEVCVPVAVBVCUDVGUEUFVNVQVJVGBVIVFQZTZJGFBCDKVKBTZVMVSVJVTVLVRVGVKBVIVFUG
      UHUIVICTZVJVCVSVPVICLMUJWAVRVGVGVICBVFUKUHULUMUNUSVHVOUOUTVDGFVGADEUPUQUR
      $.
      $( [23-Sep-2007] $) $( [2-Sep-2006] $)

    $( Any point in a metric space has a ball around it. $)
    blex $p |- ( ( D e. Met /\ P e. X ) -> E. x e. ran ( ball ` D ) P e. x ) $=
      ( cme wcel wa c1 cr cc0 clt wbr cv cbl cfv crn wrex 1re lt01 co blelrn
      blcntr eleq2 rcla4ev syl11anc mpanr12 ) BFGCDGHZIJGZKILMZCANZGZABOPZQZRZS
      TUHUIUJHHCIUMUAZUNGCUPGZUOBCIDEUBBCIDEUCULUQAUPUNUKUPCUDUEUFUG $.
      $( [28-Jan-2008] $) $( [12-Sep-2006] $)

    $( A ball is a subset of the base set of a metric space. $)
    blssm $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) )
                 -> ( P ( ball ` D ) R ) C_ X ) $=
      ( vx cme wcel wa cr cc0 clt wbr cbl cfv co cv crab blval wss ssrab2 a1i
      eqsstrd ) AGHBDHICJHKCLMIIZBCANOPBFQAPCLMZFDRZDFABCDESUFDTUDUEFDUAUBUC $.
      $( [20-Sep-2007] $) $( [31-Aug-2006] $)

    $( A ball is a subset of the base set of a metric space. $)
    rnblssm $p |- ( ( D e. Met /\ B e. ran ( ball ` D ) ) -> B C_ X ) $=
      ( vy vx cme wcel cbl cfv crn wss cc0 cv clt wbr co wceq wa cr wrex blrn3
      wi simpr blssm adantr eqsstrd exp53 imp3a imp4a r19.23advv sylbid imp )
      BGHZABIJZKHZACLZUNUPMENZOPZAFNZURUOQZRZSZETUAFCUAUQFEABCDUBUNVCUQFECTUNUT
      CHZURTHZSUSVBUQUNVDVEUSVBUQUCUCUNVDVEUSVBUQUNVDSVEUSSSZVBSAVACVFVBUDVFVAC
      LVBBUTURCDUEUFUGUHUIUJUKULUM $.
      $( [26-Sep-2007] $) $( [12-Sep-2006] $)

    $( The intersection of two balls with the same center is the smaller of
       them. $)
    blin $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) /\
  ( S e. RR /\ 0 < S ) ) -> ( ( P ( ball ` D ) R ) i^i ( P ( ball ` D ) S ) ) =
                  ( P ( ball ` D ) if ( R <_ S , R , S ) ) ) $=
      ( vx cme wcel wa cr cc0 clt wbr w3a cbl cfv co cin cv crab cle cif wceq
      blval 3adant3 3adant2 ineq12d eleq1 breq2 anbi12d ifboth sylan2 3impb wb
      ltmin metcl 3expa simpl syl3an 3expb an1rs rabbidva eqtr2d inrab syl5eq
      eqtrd ) AHIZBEIZJZCKIZLCMNZJZDKIZLDMNZJZOZBCAPQZRZBDVRRZSBGTZARZCMNZGEUAZ
      WBDMNZGEUAZSZBCDUBNZCDUCZVRRZVQVSWDVTWFVJVMVSWDUDVPGABCEFUEUFVJVPVTWFUDVM
      GABDEFUEUGUHVQWCWEJZGEUAZWJWGVQWJWBWIMNZGEUAZWLVJVMVPWJWNUDZVJWIKIZLWIMNZ
      JZWOVMVPJZGABWIEFUEWHVMVPWRCDCWIUDVKWPVLWQCWIKUICWILMUJUKDWIUDVNWPVOWQDWI
      KUIDWILMUJUKULUMUNVJVMVPWNWLUDVJWSJWMWKGEVJWAEIZWSWMWKUOZVJWTJZVMVPXAWBKI
      ZVKVNXAXBVMVPWBCDUPVHVIWTXCBWAAEFUQURVKVLUSVNVOUSUTVAVBVCUNVDWCWEGEVEVFVG
      $.
      $( [25-Sep-2007] $) $( [1-Sep-2006] $)
  $}

  ${
    $d w x z B $.  $d v w x z D $.  $d v w x z P $.
    $( Any point ` P ` in a ball ` B ` can be centered in another ball that is
       a subset of ` B ` . $)
    blss $p |- ( ( D e. Met /\ B e. ran ( ball ` D ) /\ P e. B ) ->
                E. x e. RR ( 0 < x /\ ( P ( ball ` D ) x ) C_ B ) ) $=
      ( vw vz vv cme wcel cbl cfv crn cc0 cv clt wbr co wss wa cr wrex cdm
      crab wceq wi eqid blrn2 cmin simplrr metcl 3expa adantlrr resubcl
      syl11anc adantrr eleq2 opreq2 breq1d elrab syl6bb biimpa sylan2 wb
      posdif anasss simpl anim12i blssm syl12anc elbl caddc simplll simplrl
      adantr simprl syl111anc readdcl simprr ad2antrr cle mettri syl13anc
      adantllr ltaddsub2 biimpar lelttrd ex sylbid imp ssrabdv sseqtr4d breq2
      sseq1d anbi12d rcla4ev adantrll exp43 r19.23advv 3imp ) CHIZBCJKZLIZDBIZM
      ANZOPZDXDXAQZBRZSZATUAZWTXBMENZOPZBFNZGNZCQZXJOPZGCUBUBZUCZUDZSZETUAFXPUA
      XCXIUEZFEGBCXPXPUFZUGWTXSXTFEXPTWTXLXPIZXJTIZSZXSXCXIWTYDSZXRXCXIXKYEXRXC
      SZSZXJXLDCQZUHQZTIZMYIOPZDYIXAQZBRZXIYEDXPIZYHXJOPZSZYJYFYEYNYJYOYEYNSZYC
      YHTIZYJWTYBYCYNUIZWTYBYNYRYCWTYBYNYRXLDCXPYAUJZUKULZXJYHUMUNUOZXRXCYPXRXC
      DXQIYPBXQDUPXOYOGDXPXMDUDXNYHXJOXMDXLCUQURUSUTVAZVBYEYPYKYFYEYNYOYKYQYOYK
      YQYRYCYOYKVCUUAYSYHXJVDUNVAVEZUUCVBYGYLXQBYEYPYLXQRYFYEYPSZXOGXPYLUUEWTYN
      SZYJYKYLXPRYEWTYPYNWTYDVFYNYOVFVGZUUBUUDCDYIXPYAVHVIUUEXMYLIZXOUUEUUHXMXP
      IZDXMCQZYIOPZSZXOUUEUUFYJYKUUHUULVCUUGUUBUUDXMCDYIXPYAVJVIUUEUULXOUUEUULS
      ZXNYHUUJVKQZXJUUMWTYBUUIXNTIWTYDYPUULVLZUUEYBUULWTYBYCYPVMVNZUUEUUIUUKVOZ
      XLXMCXPYAUJVPUUMYRUUJTIZUUNTIUUMWTYBYNYRUUOUUPYEYNYOUULVMZYTVPUUMWTYNUUIU
      URUUOUUSUUQDXMCXPYAUJZVPYHUUJVQUNYEYCYPUULWTYBYCVRZVSUUMWTYBUUIYNXNUUNVTP
      UUOUUPUUQUUSXLXMDCXPYAWAWBUUEUUIUUKUUNXJOPZUUEUUISZUVBUUKUVCYRUURYCUVBUUK
      VCUUEYRUUIYEYNYRYOUUAUOVNWTYPUUIUURYDWTYNUUIUURYOWTYNUUIUURUUTUKULWCYEYCY
      PUUIUVAVSYHUUJXJWDVPWEVEWFWGWHWIWJUUCVBYEXRXCVOWKXHYKYMSAYITXDYIUDZXEYKXG
      YMXDYIMOWLUVDXFYLBXDYIDXAUQWMWNWOVIWPWQWRWHWS $.
      $( [28-Jul-2008] $) $( [31-Aug-2006] $)
  $}

  ${
    $d x y A $.  $d x y D $.  $d x y P $.  $d y X $.
    blssex.1 $e |- X = dom dom D $.
    $( Two ways to express the existence of a ball subset. $)
    blssex $p |- ( ( D e. Met /\ P e. X ) -> ( E. x e. ran ( ball ` D ) ( P e.
     x /\ x C_ A ) <-> E. y e. RR ( 0 < y /\ ( P ( ball ` D ) y ) C_ A ) ) ) $=
      ( cme wcel wa cv wss cbl cfv crn wrex cc0 clt wbr co cr wi w3a r19.41v
      sstr anim2i anassrs reximi sylbir blss sylan 3exp1 imp4b r19.23adva
      adantr blelrn adantrr blcntr anim1i anasss wceq eleq2 sseq1 anbi12d
      rcla4ev syl11anc ex impbid ) DHIZEFIZJZEAKZIZVLCLZJZADMNZOZPZQBKZRSZEVSVP
      TZCLZJZBUAPZVIVRWDUBVJVIVOWDAVQVIVLVQIZVMVNWDVIWEVMVNWDVTWAVLLZJZBUAPZVNW
      DVIWEVMUCWHVNJWGVNJZBUAPWDWGVNBUAUDWIWCBUAVTWFVNWCWFVNJWBVTWAVLCUEUFUGUHU
      IBVLDEUJUKULUMUNUOVKWCVRBUAVKVSUAIZJZWCVRWKWCJWAVQIZEWAIZWBJZVRWKVTWLWBVK
      WJVTWLDEVSFGUPUGUQWKVTWBWNWKVTJWMWBVKWJVTWMDEVSFGURUGUSUTVOWNAWAVQVLWAVAV
      MWMVNWBVLWAEVBVLWACVCVDVEVFVGUNVH $.
      $( [5-May-2007] $)
  $}

  ${
    $d x D $.  $d x P $.  $d x R $.  $d x S $.  $d x X $.
    ssbl.1 $e |- X = dom dom D $.
    $( The size of a ball increases monotonically with its radius. $)
    ssbl $p |- ( ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) /\
               ( S e. RR /\ 0 < S ) ) /\ R <_ S ) ->
               ( P ( ball ` D ) R ) C_ ( P ( ball ` D ) S ) ) $=
      ( vx cme wcel wa cr cc0 clt wbr w3a cle cbl cfv co cv wi ltletr metcl
      3expa ancoms syl3an1 exp3a com23 3expb exp31 com4l imp31 imdistand ex
      3impb 3adant2r 3adant3r imp wb elbl 3adant3 adantr 3adant2 3imtr4d ssrdv
      ) AHIZBEIZJZCKIZLCMNZJZDKIZLDMNZJZOZCDPNZJZGBCAQRZSZBDVRSZVQGTZEIZBWAASZC
      MNZJZWBWCDMNZJZWAVSIZWAVTIZVOVPWEWGUAZVHVKVLVPWJUAZVMVHVIVLWKVJVHVIVLWKVH
      VIVLJZJZVPWJWMVPJWBWDWFVHWLVPWBWDWFUAZUAWBVHWLVPWNWBVHWLVPWNUAZWBVHJZVIVL
      WOWPVIVLOZWDVPWFWQWDVPWFWCKIZVIVLWDVPJWFUAWPWCCDUBVHWBWRVFVGWBWRBWAAEFUCU
      DUEUFUGUHUIUJUKULUMUNUOUPUQURVOWHWEUSZVPVHVKWSVNWAABCEFUTVAVBVOWIWGUSZVPV
      HVNWTVKWAABDEFUTVCVBVDVE $.
      $( [20-Sep-2007] $)
  $}

  ${
    $d x D $.  $d x P $.  $d x R $.  $d x S $.
    ssblex.1 $e |- X = dom dom D $.
    $( A nested ball exists whose radius is less than any desired amount. $)
    ssblex $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) /\
               ( S e. RR /\ 0 < S ) ) -> E. x e. RR ( 0 < x /\ x < R /\
               ( P ( ball ` D ) x ) C_ ( P ( ball ` D ) S ) ) ) $=
      ( cme wcel wa cr cc0 clt wbr w3a cle wo cv cbl cfv co wss wrex lelttric
      ad2ant2r 3adant1 c2 cdiv rehalfcl ad2antrr 3ad2antl2 halfpos2 biimpa
      adantr halfpos simplll simplr simpr ltletrd wi ltle sylan adantlr mpd
      adantlrr 3adantl1 ssbl jca syl3anl2 syldan wceq breq2 breq1 opreq2
      sseq1d 3anbi123d rcla4ev syl13anc simpl3l simpl3r ssid a1i jaodan mpdan
      ) BHICFIJZDKIZLDMNZJZEKIZLEMNZJZOZDEPNZEDMNZQZLARZMNZWPDMNZCWPBSTZUAZCEWS
      UAZUBZOZAKUCZWHWKWOWEWFWIWOWGWJDEUDUEUFWLWMXDWNWLWMJDUGUHUAZKIZLXEMNZXEDM
      NZCXEWSUAZXAUBZXDWHWEWMXFWKWFXFWGWMDUIZUJUKWHWEWMXGWKWHXGWMWFWGXGDULUMZUN
      UKWHWEWMXHWKWHXHWMWFWGXHDUOUMZUNUKWLWMXEEPNZXJWHWKWMXNWEWHWIWMXNWJWHWIJZW
      MJZXEEMNZXNXPXEDEWHXFWIWMWFXFWGXKUNZUJWFWGWIWMUPWHWIWMUQWHXHWIWMXMUJXOWMU
      RUSXOXQXNUTZWMWFWIXSWGXFWIXSWFXEEVAXKVBVCUNVDVEVFWEXFXGJWKXNXJWHBCXEEFGVG
      WHXFXGXRXLVHVIVJXCXGXHXJOAXEKWPXEVKZWQXGWRXHXBXJWPXELMVLWPXEDMVMXTWTXIXAW
      PXECWSVNVOVPVQVRWLWNJZWIWJWNXAXAUBZXDWIWJWEWHWNVSWIWJWEWHWNVTWLWNURYBYAXA
      WAWBXCWJWNYBOAEKWPEVKZWQWJWRWNXBYBWPELMVLWPEDMVMYCWTXAXAWPECWSVNVOVPVQVRW
      CWD $.
      $( [20-Sep-2007] $)
  $}

