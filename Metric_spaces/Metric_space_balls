$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Metric space balls
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d d r x y D $.  $d d r x y X $.
    blfval.1 $e |- X = dom dom D $.
    $( The value of the ball function. $)
    blfval $p |- ( D e. Met -> ( ball ` D ) =
                   ( x e. X , r e. RR+ |-> { y e. X | ( x D y ) < r } ) ) $=
      ( vd cme wcel crp cv co clt wbr crab cmpt2 cvv cbl wceq cdm cfv dmexg syl
      syl5eqel cr reex rpssre ssexi mpt2exga sylancl dmeqd syl6eqr eqidd breq1d
      dmeq opreq rabeqbidv mpt2eq123dv df-bl fvmptg mpdan ) CHIZAEDJAKZBKZCLZEK
      ZMNZBDOZPZQIZCRUAVISVBDQIJQIVJVBDCTZTZQFVBVKQIVLQICHUBVKQUBUCUDJUEUFUGUHA
      EDJVHQQUIUJGCAEGKZTZTZJVCVDVMLZVFMNZBVOOZPVIHQRVMCSZAEVOJVRDJVHVSVOVLDVSV
      NVKVMCUOUKFULZVSJUMVSVQVGBVODVTVSVPVEVFMVCVDVMCUPUNUQURABEGUSUTVA $.
      $( [11-Nov-2013] $) $( [30-Aug-2006] $)
  $}

  ${
    $d x A $.  $d r x y z B $.  $d r x y z D $.  $d r x y P $.  $d x Q $.
    $d r x R $.  $d x S $.  $d r x y z X $.
    blval.1 $e |- X = dom dom D $.
    $( The ball around a point ` P ` is the set of all points whose distance
       from ` P ` is less than the ball's radius ` R ` . $)
    blval $p |- ( ( D e. Met /\ P e. X /\ R e. RR+ ) ->
                  ( P ( ball ` D ) R ) = { x e. X | ( P D x ) < R } ) $=
      ( vy vr cme wcel crp co cv clt wbr crab wceq cvv cdm dmexg w3a cbl blfval
      cfv cmpt2 opreqd 3ad2ant1 syl5eqel rabexg 3syl opreq1 breq1d rabbidv eqid
      breq2 ovmpt2g 3comr syl3an1 eqtrd ) BIJZCEJZDKJZUACDBUBUDZLZCDGHEKGMZAMZB
      LZHMZNOZAEPZUEZLZCVFBLZDNOZAEPZUTVAVDVLQVBUTVCVKCDGABEHFUCUFUGUTVORJZVAVB
      VLVOQZUTBSZRJZERJVPBITVSEVRSRFVRRTUHVNAERUIUJVAVBVPVQGHCDEKVJVOVKVMVHNOZA
      EPRVECQZVIVTAEWAVGVMVHNVECVFBUKULUMVHDQVTVNAEVHDVMNUOUMVKUNUPUQURUS $.
      $( [11-Nov-2013] $) $( [31-Aug-2006] $)

    $( Membership in a ball. $)
    elbl $p |- ( ( D e. Met /\ P e. X /\ R e. RR+ ) ->
             ( A e. ( P ( ball ` D ) R ) <-> ( A e. X /\ ( P D A ) < R ) ) ) $=
      ( vx cme wcel crp w3a cbl cfv co cv clt wbr crab wa blval eleq2d opreq2
      wceq breq1d elrab syl6bb ) BHICEIDJIKZACDBLMNZIACGOZBNZDPQZGERZIAEICABNZD
      PQZSUGUHULAGBCDEFTUAUKUNGAEUIAUCUJUMDPUIACBUBUDUEUF $.
      $( [11-Nov-2013] $) $( [2-Sep-2006] $)

    $( Membership in a ball. $)
    elbl2 $p |- ( ( ( D e. Met /\ R e. RR+ ) /\ ( P e. X /\ A e. X ) ) ->
        ( A e. ( P ( ball ` D ) R ) <-> ( P D A ) < R ) ) $=
      ( cme wcel crp wa cbl cfv co clt wbr wb w3a df-3an elbl 3com23 sylan9bb
      ibar bicomd sylanbr anasss ) BGHZDIHZJZCEHZAEHZACDBKLMHZCABMDNOZPZUHUIJUF
      UGUIQZUJUMUFUGUIRUNUKUJULJZUJULUFUIUGUKUOPABCDEFSTUJULUOUJULUBUCUAUDUE $.
      $( [9-Mar-2007] $)

    $( Membership in a ball, with reversed distance function arguments. $)
    elbl3 $p |- ( ( ( D e. Met /\ R e. RR+ ) /\ ( P e. X /\ A e. X ) ) ->
        ( A e. ( P ( ball ` D ) R ) <-> ( A D P ) < R ) ) $=
      ( cme wcel crp wa cbl cfv co clt wbr elbl2 wceq metsym 3expb adantlr
      breq1d bitrd ) BGHZDIHZJCEHZAEHZJZJZACDBKLMHCABMZDNOACBMZDNOABCDEFPUHUIUJ
      DNUCUGUIUJQZUDUCUEUFUKCABEFRSTUAUB $.
      $( [10-Nov-2007] $)

    $( Commute the arguments to the ball function.  (Contributed by Mario
       Carneiro, 22-Jan-2014.) $)
    blcom $p |- ( ( ( D e. Met /\ R e. RR+ ) /\ ( P e. X /\ A e. X ) ) ->
               ( A e. ( P ( ball ` D ) R ) <-> P e. ( A ( ball ` D ) R ) ) ) $=
      ( cme wcel crp wa cbl cfv co clt wbr elbl2 wb elbl3 ancom2s bitr4d ) BGHD
      IHJZCEHZAEHZJJACDBKLZMHCABMDNOZCADUDMHZABCDEFPUAUCUBUFUEQCBADEFRST $.
      $( [22-Jan-2014] $)

    $( Two balls are disjoint if the center-to-center distance is more than the
       sum of the radii.  (Contributed by Mario Carneiro, 30-Dec-2013.) $)
    bldisj $p |- ( ( ( D e. Met /\ P e. X /\ Q e. X ) /\
                ( R e. RR+ /\ S e. RR+ /\ ( R + S ) <_ ( P D Q ) ) ) ->
                ( ( P ( ball ` D ) R ) i^i ( Q ( ball ` D ) S ) ) = (/) ) $=
      ( vx wcel w3a crp caddc co wbr wa clt cr wb metcl wi cme cle cv cbl wn c0
      cfv cin wal wceq simpr3 rpre ax-addrcl syl2an 3adant3 lenlt syl2anr mpbid
      simpl1 simpl2 simprl elbl syl3anc simpl3 simprr anbi12d elin anandi 3expa
      3bitr4g 3adantl3 3adantl2 jca adantlr anim12i ad2antlr lt2add syl2anc syl
      ad2antrr simpll1 simpll2 simpll3 mettri3 syl13anc lelttr expdimp syl31anc
      simpr syld expimpd sylbid 3adantr3 mtod alrimiv eq0 sylibr ) AUAIZBFIZCFI
      ZJZDKIZEKIZDELMZBCAMZUBNZJZOZHUCZBDAUDUGZMZCEXJMZUHZIZUEZHUIXMUFUJXHXOHXH
      XNXEXDPNZXHXFXPUEZXAXBXCXFUKXGXDQIZXEQIZXFXQRXAXBXCXRXFXBDQIZEQIZXRXCDULZ
      EULZDEUMZUNUOBCAFGSZXDXEUPUQURXAXBXCXNXPTXFXAXBXCOZOZXNXIFIZBXIAMZDPNZCXI
      AMZEPNZOZOZXPYGXIXKIZXIXLIZOYHYJOZYHYLOZOXNYNYGYOYQYPYRYGWRWSXBYOYQRWRWSW
      TYFUSZWRWSWTYFUTXAXBXCVAXIABDFGVBVCYGWRWTXCYPYRRYSWRWSWTYFVDXAXBXCVEXIACE
      FGVBVCVFXIXKXLVGYHYJYLVHVJYGYHYMXPYGYHOZYMYIYKLMZXDPNZXPYTYIQIZYKQIZOZXTY
      AOZYMUUBTXAYHUUEYFXAYHOUUCUUDWRWSYHUUCWTWRWSYHUUCBXIAFGSVIVKWRWTYHUUDWSWR
      WTYHUUDCXIAFGSVIVLVMVNZYFUUFXAYHXBXTXCYAYBYCVOVPZYIYKDEVQVRYTXSUUAQIZXRXE
      UUAUBNZUUBXPTXAXSYFYHYEVTYTUUEUUIUUGYIYKUMVSYTUUFXRUUHYDVSYTWRWSWTYHUUJWR
      WSWTYFYHWAWRWSWTYFYHWBWRWSWTYFYHWCYGYHWIBCXIAFGWDWEXSUUIXRJUUJUUBXPXEUUAX
      DWFWGWHWJWKWLWMWNWOHXMWPWQ $.
      $( [30-Dec-2013] $)

    $( Two balls are disjoint if they don't overlap. $)
    bl2in $p |- ( ( ( D e. Met /\ P e. X /\ Q e. X ) /\
                ( R e. RR+ /\ R <_ ( ( P D Q ) / 2 ) ) ) ->
                ( ( P ( ball ` D ) R ) i^i ( Q ( ball ` D ) R ) ) = (/) ) $=
      ( cme wcel w3a crp co c2 cdiv cle wbr wa caddc wceq syl cr cbl cfv cin c0
      simprl cmul cc rpcn 2times rpre metcl cc0 clt 2re pm3.2i lemuldiv2 mp3an3
      wb 2pos syl2anr biimprd impr eqbrtrrd 3jca bldisj syldan ) AGHBEHCEHIZDJH
      ZDBCAKZLMKNOZPZVHVHDDQKZVINOZIBDAUAUBZKCDVNKUCUDRVGVKPZVHVHVMVGVHVJUEZVPV
      OLDUFKZVLVINVODUGHZVQVLRVOVHVRVPDUHSDUISVGVHVJVQVINOZVGVHPVSVJVHDTHZVITHZ
      VSVJURZVGDUJBCAEFUKVTWALTHZULLUMOZPWBWCWDUNUSUODVILUPUQUTVAVBVCVDABCDDEFV
      EVF $.
      $( [12-Nov-2013] $) $( [11-Mar-2007] $)

    $( One ball is contained in another if the center-to-center distance is
       less than the difference of the radii.  (Contributed by Mario Carneiro,
       15-Jan-2014.) $)
    blss2 $p |- ( ( ( D e. Met /\ P e. X /\ Q e. X ) /\
                ( R e. RR+ /\ S e. RR+ /\ ( P D Q ) <_ ( S - R ) ) ) ->
                ( P ( ball ` D ) R ) C_ ( Q ( ball ` D ) S ) ) $=
      ( vx wcel w3a co cle wbr wa clt wi cr wb metcl syl3anc cme crp cbl cfv cv
      cmin caddc simpl1 simpl2 simpr3 simpr1 rpre simpl3 ltadd2 simpr2 leaddsub
      syl bicomd anbi12d ax-addrcl syl2anc ltletr mettri2 syl13anc lelttr mpand
      syld sylbid exp3acom23 3exp2 com45 3imp2 3simpa simp1 3expa syl2an 3simpb
      imdistand elbl simp2 3imtr4d ssrdv ) AUAIZBFIZCFIZJZDUBIZEUBIZBCAKZEDUFKL
      MZJZNZHBDAUCUDZKZCEWMKZWLHUEZFIZBWPAKZDOMZNZWQCWPAKZEOMZNZWPWNIZWPWOIZWLW
      QWSXBWFWGWHWJWQWSXBPZPWFWGWHWQWJXFWFWGWHWQWJXFPWFWGWHWQJZNZWSWJXBXHWSWJNW
      IWRUGKZWIDUGKZOMZXJELMZNZXBXHWSXKWJXLXHWRQIZDQIZWIQIZWSXKRXHWCWDWQXNWCWDW
      EXGUHZWCWDWEXGUIZWFWGWHWQUJZBWPAFGSTZXHWGXOWFWGWHWQUKDULUQZXHWCWDWEXPXQXR
      WCWDWEXGUMZBCAFGSTZWRDWIUNTXHXLWJXHXPXOEQIZXLWJRYCYAXHWHYDWFWGWHWQUOEULUQ
      ZWIDEUPTURUSXHXMXIEOMZXBXHXIQIZXJQIZYDXMYFPXHXPXNYGYCXTWIWRUTVAZXHXPXOYHY
      CYAWIDUTVAYEXIXJEVBTXHXAXILMZYFXBXHWCWDWEWQYJXQXRYBXSCWPBAFGVCVDXHXAQIZYG
      YDYJYFNXBPXHWCWEWQYKXQYBXSCWPAFGSTYIYEXAXIEVETVFVGVHVIVJVKVLVRWFWCWDNWGXD
      WTRZWKWCWDWEVMWGWHWJVNWCWDWGYLWPABDFGVSVOVPWFWCWENWHXEXCRZWKWCWDWEVQWGWHW
      JVTWCWEWHYMWPACEFGVSVOVPWAWB $.
      $( [15-Jan-2014] $)
  $}

  ${
    $d X s $.  $d M s $.  $d R s $.  $d Y s $.  $d Z s $.
    blhalf.1 $e |- X = dom dom M $.
    $( A ball of radius ` R / 2 ` is contained in a ball of radius ` R `
       centered at any point inside the smaller ball.  (Contributed by Jeff
       Madsen, 2-Sep-2009.)  (The proof was shortened by Mario Carneiro,
       14-Jan-2014.) $)
    blhalf $p |- ( ( ( M e. Met /\ Y e. X ) /\ ( R e. RR+ /\
                    Z e. ( Y ( ball ` M ) ( R / 2 ) ) ) ) ->
              ( Y ( ball ` M ) ( R / 2 ) ) C_ ( Z ( ball ` M ) R ) ) $=
      ( cme wcel wa crp c2 co cle wbr wb syl cr syl3anc wceq cc cdiv cbl simpll
      cfv cmin wss simplr clt 2re 2pos elrpii rpdivcl mpan2 3expa sylan2 biimpd
      elbl impr simpld simprl simprd metcl rpre ltle syl2anc caddc rpcn 2halves
      wi mpd recnd subadd mpbird breqtrrd blss2 syl33anc ) BGHZDCHZIZAJHZEDAKUA
      LZBUBUDZLZHZIZIZVQVRECHZWAJHZVTDEBLZAWAUELZMNWCEAWBLUFVQVRWEUCZVQVRWEUGZW
      FWGWIWAUHNZVSVTWDWGWMIZVSVTIWDWNVTVSWHWDWNOZVTKJHWHKUIUJUKAKULUMZVQVRWHWO
      EBDWACFUQUNUOUPURZUSZWFVTWHVSVTWDUTZWPPZWSWFWIWAWJMWFWMWIWAMNZWFWGWMWQVAW
      FWIQHZWAQHZWMXAVIWFVQVRWGXBWKWLWRDEBCFVBRWFWHXCWTWAVCPZWIWAVDVEVJWFWJWASZ
      WAWAVFLASZWFATHZXFWFVTXGWSAVGPZAVHPWFXGWATHZXIXEXFOXHWFWAXDVKZXJAWAWAVLRV
      MVNBDEWAACFVOVP $.
      $( [14-Jan-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d r x y A $.  $d r x B $.  $d r x y D $.  $d x S $.  $d r x y X $.
    $d r x P $.  $d r x R $.
    blf.1 $e |- X = dom dom D $.
    $( Mapping of a ball. $)
    blf $p |- ( D e. Met -> ( ball ` D ) : ( X X. RR+ ) --> ~P X ) $=
      ( vx vr vy cme wcel crp cxp cpw cbl cfv wf cv co wral cdm cvv dmexg cmpt2
      clt wbr crab wa wss ssrab2 syl5eqel elpw2g 3syl mpbiri a1d ralrimivv eqid
      wb fmpt2 sylib blfval feq1d mpbird ) AGHZBIJZBKZALMZNVBVCDEBIDOZFOAPEOZUB
      UCZFBUDZUAZNZVAVHVCHZEIQDBQVJVAVKDEBIVAVKVEBHVFIHUEVAVKVHBUFZVGFBUGVAARZS
      HZBSHVKVLUOAGTVNBVMRSCVMSTUHVHBSUIUJUKULUMDEBIVHVCVIVIUNUPUQVAVBVCVDVIDFA
      BECURUSUT $.
      $( [12-Nov-2013] $) $( [7-May-2007] $)

    $( Membership in the range of the ball function.  Note that
       ` ran ( ball `` D ) ` is the collection of all balls for metric
       ` D ` . $)
    blrn $p |- ( D e. Met -> ( A e. ran ( ball ` D ) <->
         E. x e. X E. r e. RR+ A = ( x ( ball ` D ) r ) ) ) $=
      ( vy cme wcel cbl cfv crn cv wceq crp cxp wrex co cpw syl6bb wfn blf 3syl
      wf wb ffn fvelrnb cop fveq2 df-opr syl6eqr eqeq1d eqcom rexxp ) CHIZBCJKZ
      LIZGMZUPKZBNZGDOPZQZBAMZEMZUPRZNZEOQADQUOVADSZUPUDUPVAUAUQVBUECDFUBVAVGUP
      UFGVABUPUGUCUTVFGAEDOURVCVDUHZNZUTVEBNVFVIUSVEBVIUSVHUPKVEURVHUPUIVCVDUPU
      JUKULVEBUMTUNT $.
      $( [12-Nov-2013] $) $( [31-Aug-2006] $)

    $( A ball contains its center. $)
    blcntr $p |- ( ( D e. Met /\ P e. X /\ R e. RR+ ) ->
                 P e. ( P ( ball ` D ) R ) ) $=
      ( cme wcel crp w3a cbl cfv co clt wbr elbl simp2 cc0 wceq met0 3adant3
      rpgt0 3ad2ant3 eqbrtrd mpbir2and ) AFGZBDGZCHGZIZBBCAJKLGUFBBALZCMNBABCDE
      OUEUFUGPUHUIQCMUEUFUIQRUGBADESTUGUEQCMNUFCUAUBUCUD $.
      $( [12-Nov-2013] $) $( [2-Sep-2006] $)

    $( A ball is not empty. $)
    bln0 $p |- ( ( D e. Met /\ P e. X /\ R e. RR+ ) ->
                 ( P ( ball ` D ) R ) =/= (/) ) $=
      ( cme wcel crp w3a cbl cfv co c0 wne blcntr ne0i syl ) AFGBDGCHGIBBCAJKLZ
      GRMNABCDEORBPQ $.
      $( [12-Nov-2013] $) $( [6-Oct-2007] $)

    $( A ball belongs to the set of balls of a metric space. $)
    blelrn $p |- ( ( D e. Met /\ P e. X /\ R e. RR+ ) ->
                    ( P ( ball ` D ) R ) e. ran ( ball ` D ) ) $=
      ( vx vr cme wcel crp w3a cbl cfv co crn cv wceq wrex eqid eqeq2d rcla42ev
      opreq1 opreq2 mp3an3 3adant1 wb blrn 3ad2ant1 mpbird ) AHIZBDIZCJIZKBCALM
      ZNZUMOIZUNFPZGPZUMNZQZGJRFDRZUKULUTUJUKULUNUNQZUTUNSUSVAUNBUQUMNZQFGBCDJU
      PBQURVBUNUPBUQUMUBTUQCQVBUNUNUQCBUMUCTUAUDUEUJUKUOUTUFULFUNADGEUGUHUI $.
      $( [12-Nov-2013] $) $( [2-Sep-2006] $)

    $( A ball is a subset of the base set of a metric space. $)
    blssm $p |- ( ( D e. Met /\ P e. X /\ R e. RR+ ) ->
                  ( P ( ball ` D ) R ) C_ X ) $=
      ( vx cme wcel crp w3a cbl cfv co cv clt wbr crab blval wss ssrab2 eqsstrd
      a1i ) AGHBDHCIHJZBCAKLMBFNAMCOPZFDQZDFABCDERUEDSUCUDFDTUBUA $.
      $( [12-Nov-2013] $) $( [31-Aug-2006] $)

    $( The union of the set of balls of a metric space is its base set. $)
    unirnbl $p |- ( D e. Met -> U. ran ( ball ` D ) = X ) $=
      ( vx cme wcel cbl cfv crn cuni cpw wss crp cxp wf blf frn syl sspwuni c1
      sylib cv 1rp w3a co blcntr blelrn elunii syl2anc mp3an3 ex ssrdv eqssd )
      AEFZAGHZIZJZBUNUPBKZLZUQBLUNBMNZURUOOUSABCPUTURUOQRUPBSUAUNDBUQUNDUBZBFZV
      AUQFZUNVBTMFZVCUCUNVBVDUDVAVATUOUEZFVEUPFVCAVATBCUFAVATBCUGVAVEUPUHUIUJUK
      ULUM $.
      $( [12-Nov-2013] $) $( [12-Sep-2006] $)

    $( The intersection of two balls with the same center is the smaller of
       them. $)
    blin $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR+ /\ S e. RR+ ) ) ->
      ( ( P ( ball ` D ) R ) i^i ( P ( ball ` D ) S ) ) =
                  ( P ( ball ` D ) if ( R <_ S , R , S ) ) ) $=
      ( vx wcel wa crp co wbr clt crab cin cr rpre syl3anc wceq blval cme cv wb
      cle cif cbl cfv metcl 3expa adantlr simprl syl adantr simprr ltmin simpll
      rabbidva simplr ifcl adantl ineq12d inrab syl6eq 3eqtr4rd ) AUAHZBEHZIZCJ
      HZDJHZIZIZBGUBZAKZCDUDLZCDUEZMLZGENZVMCMLZVMDMLZIZGENZBVOAUFUGZKZBCWBKZBD
      WBKZOZVKVPVTGEVKVLEHZIVMPHZCPHZDPHZVPVTUCVGWGWHVJVEVFWGWHBVLAEFUHUIUJVKWI
      WGVKVHWIVGVHVIUKZCQULUMVKWJWGVKVIWJVGVHVIUNZDQULUMVMCDUORUQVKVEVFVOJHZWCV
      QSVEVFVJUPZVEVFVJURZVJWMVGVNCDJUSUTGABVOEFTRVKWFVRGENZVSGENZOWAVKWDWPWEWQ
      VKVEVFVHWDWPSWNWOWKGABCEFTRVKVEVFVIWEWQSWNWOWLGABDEFTRVAVRVSGEVBVCVD $.
      $( [12-Nov-2013] $) $( [1-Sep-2006] $)
  $}

  ${
    $d r x y z B $.  $d r x y z D $.  $d r x y z P $.
    $( Any point ` P ` in a ball ` B ` can be centered in another ball that is
       a subset of ` B ` . $)
    blss $p |- ( ( D e. Met /\ B e. ran ( ball ` D ) /\ P e. B ) ->
                E. x e. RR+ ( P ( ball ` D ) x ) C_ B ) $=
      ( vy vr wcel cv co wss crp wrex wceq cdm wi clt wbr cr syl2anc cle cme wa
      cbl cfv crn eqid blrn w3a elbl cmin cc0 simpl3 simpl1 simpl2 simprl metcl
      rpre syl syl3anc resubcl simprr wb posdif mpbid elrp sylanbrc leid metsym
      cc recnd nncan 3brtr4d blss2 syl33anc opreq2 sseq1d ex sylbid eleq2 sseq2
      rcla4ev rexbidv imbi12d syl5ibrcom 3expib rexlimdvv 3imp ) CUAGZBCUCUDZUE
      GZDBGZDAHZWIIZBJZAKLZWHWJBEHZFHZWIIZMZFKLECNNZLWKWOOZEBCWTFWTUFZUGWHWSXAE
      FWTKWHWPWTGZWQKGZWSXAOWHXCXDUHZXAWSDWRGZWMWRJZAKLZOXEXFDWTGZWPDCIZWQPQZUB
      ZXHDCWPWQWTXBUIXEXLXHXEXLUBZWQXJUJIZKGZDXNWIIZWRJZXHXMXNRGZUKXNPQZXOXMWQR
      GZXJRGZXRXMXDXTWHXCXDXLULZWQUQURZXMWHXCXIYAWHXCXDXLUMZWHXCXDXLUNZXEXIXKUO
      ZWPDCWTXBUPUSZWQXJUTSXMXKXSXEXIXKVAXMYAXTXKXSVBYGYCXJWQVCSVDXNVEVFZXMWHXI
      XCXOXDDWPCIZWQXNUJIZTQXQYDYFYEYHYBXMXJXJYIYJTXMYAXJXJTQYGXJVGURXMWHXIXCYI
      XJMYDYFYEDWPCWTXBVHUSXMWQVIGXJVIGYJXJMXMWQYCVJXMXJYGVJWQXJVKSVLCDWPXNWQWT
      XBVMVNXGXQAXNKWLXNMWMXPWRWLXNDWIVOVPWASVQVRWSWKXFWOXHBWRDVSWSWNXGAKBWRWMV
      TWBWCWDWEWFVRWG $.
      $( [12-Nov-2013] $) $( [31-Aug-2006] $)
  $}

  ${
    $d r x A $.  $d r x D $.  $d r x P $.  $d r x X $.
    blssex.1 $e |- X = dom dom D $.
    $( Two ways to express the existence of a ball subset. $)
    blssex $p |- ( ( D e. Met /\ P e. X ) ->
                   ( E. x e. ran ( ball ` D ) ( P e. x /\ x C_ A ) <->
                     E. r e. RR+ ( P ( ball ` D ) r ) C_ A ) ) $=
      ( cme wcel wa cv wss cbl cfv crn wrex crp wi rexlimdva syl3anc w3a expcom
      co blss reximdv syl5com 3expa expimpd adantlr simpll simplr simprl blelrn
      sstr blcntr simprr wceq eleq2 sseq1 anbi12d rcla4ev syl12anc expr impbid
      ) CHIZDEIZJZDAKZIZVHBLZJZACMNZOZPZDFKZVLUCZBLZFQPZVGVKVRAVMVEVHVMIZVKVRRV
      FVEVSJVIVJVRVEVSVIVJVRRVEVSVIUAVPVHLZFQPVJVRFVHCDUDVJVTVQFQVTVJVQVPVHBUNU
      BUEUFUGUHUISVGVQVNFQVGVOQIZVQVNVGWAVQJZJZVPVMIZDVPIZVQVNWCVEVFWAWDVEVFWBU
      JZVEVFWBUKZVGWAVQULZCDVOEGUMTWCVEVFWAWEWFWGWHCDVOEGUOTVGWAVQUPVKWEVQJAVPV
      MVHVPUQVIWEVJVQVHVPDURVHVPBUSUTVAVBVCSVD $.
      $( [12-Nov-2013] $) $( [5-May-2007] $)
  $}

  ${
    $d x D $.  $d x P $.  $d x R $.  $d x S $.  $d x X $.
    ssbl.1 $e |- X = dom dom D $.
    $( The size of a ball increases monotonically with its radius. $)
    ssbl $p |- ( ( ( D e. Met /\ P e. X ) /\
                   ( R e. RR+ /\ S e. RR+ ) /\ R <_ S ) ->
               ( P ( ball ` D ) R ) C_ ( P ( ball ` D ) S ) ) $=
      ( cme wcel wa crp cle wbr w3a co cmin cbl cc0 cr rpre syl cfv simp1l wceq
      simp1r simp2l simp2r met0 3ad2ant1 simp3 wb subge0 syl2anc mpbird eqbrtrd
      wss blss2 syl33anc ) AGHZBEHZIZCJHZDJHZIZCDKLZMZURUSUSVAVBBBANZDCONZKLBCA
      PUAZNBDVHNUOURUSVCVDUBURUSVCVDUDZVIUTVAVBVDUEZUTVAVBVDUFZVEVFQVGKUTVCVFQU
      CVDBAEFUGUHVEQVGKLZVDUTVCVDUIVEDRHZCRHZVLVDUJVEVBVMVKDSTVEVAVNVJCSTDCUKUL
      UMUNABBCDEFUPUQ $.
      $( [12-Nov-2013] $) $( [20-Sep-2007] $)
  $}

  ${
    $d x D $.  $d x P $.  $d x R $.  $d x S $.
    ssblex.1 $e |- X = dom dom D $.
    $( A nested ball exists whose radius is less than any desired amount. $)
    ssblex $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR+ /\ S e. RR+ ) ) ->
     E. x e. RR+ ( x < R /\ ( P ( ball ` D ) x ) C_ ( P ( ball ` D ) S ) ) ) $=
      ( wcel wa crp c2 co cle wbr clt wss syl2anc cr rpre syl cme cdiv cif wrex
      cbl cfv simprl 2re 2pos elrpii rpdivcl sylancl simprr ifcl min1 cc0 rpgt0
      cv wb halfpos mpbid lelttrd simpl min2 ssbl syl121anc breq1 opreq2 sseq1d
      wceq anbi12d rcla4ev syl12anc ) BUAHCFHIZDJHZEJHZIZIZDKUBLZEMNZVSEUCZJHZW
      ADONZCWABUEUFZLZCEWDLZPZAURZDONZCWHWDLZWFPZIZAJUDVRVSJHZVPWBVRVOKJHWMVNVO
      VPUGZKUHUIUJDKUKULZVNVOVPUMZVTVSEJUNQZVRWAVSDVRWBWARHWQWASTVRWMVSRHZWOVSS
      TZVRVODRHZWNDSTZVRWRERHZWAVSMNWSVRVPXBWPESTZVSEUOQVRUPDONZVSDONZVRVOXDWND
      UQTVRWTXDXEUSXADUTTVAVBVRVNWBVPWAEMNZWGVNVQVCWQWPVRWRXBXFWSXCVSEVDQBCWAEF
      GVEVFWLWCWGIAWAJWHWAVJZWIWCWKWGWHWADOVGXGWJWEWFWHWACWDVHVIVKVLVM $.
      $( [12-Nov-2013] $) $( [20-Sep-2007] $)
  $}

  ${
    $d x y z B $.  $d x y z C $.  $d x y z D $.  $d x y z P $.
    blin2.1 $e |- X = dom dom D $.
    $( Given any two balls and a point in their intersection, there is a ball
       contained in the intersection with the given center point. $)
    blin2 $p |- ( ( ( D e. Met /\ P e. ( B i^i C ) ) /\
                  ( B e. ran ( ball ` D ) /\ C e. ran ( ball ` D ) ) ) ->
                    E. x e. RR+ ( P ( ball ` D ) x ) C_ ( B i^i C ) ) $=
      ( vy vz wcel cin wa cv co wss crp wrex blss syl3anc ad2ant2r cme cbl elin
      cfv crn simpll simprl simplr sylib simpld simprr simprd cle wbr ifcl wceq
      wi cif inss1 cpw cxp blf frn 3syl sseldd elpwi syl syl5ss jca blin syl2an
      wf id simprlr simprrr ss2in syl2anc eqsstr3d expcom opreq2 sseq1d rcla4ev
      ex sylsyld impcom anassrs expr rexlimdva mp2d ) DUAJZEBCKZJZLZBDUBUDZUEZJ
      ZCWOJZLZLZEHMZWNNZBOZHPQZEIMZWNNZCOZIPQZEAMZWNNZWKOZAPQZWSWJWPEBJZXCWJWLW
      RUFZWMWPWQUGZWSXLECJZWSWLXLXOLWJWLWRUHZEBCUCUIZUJHBDERSWSWJWQXOXGXMWMWPWQ
      UKWSXLXOXQULICDERSWSXBXGXKUQZHPWSWTPJZXBXRWSXSXBLZLZXFXKIPYAXDPJZXFXKWSXT
      YBXFLZXKXTYCLZWSXKYDWTXDUMUNZWTXDURZPJZWSEYFWNNZWKOZXKXSYBYGXBXFYEWTXDPUO
      TWSYDYIWSYDLZYHXAXEKZWKWSWJEFJZLXSYBLZYKYHUPYDWSWJYLXMWSWKFEWSWKBFBCUSWSB
      FUTZJBFOWSWOYNBWSWJFPVAZYNWNVLWOYNOXMDFGVBYOYNWNVCVDXNVEBFVFVGVHXPVEVIXSY
      BYMXBXFYMVMTDEWTXDFGVJVKYJXBXFYKWKOWSXSXBYCVNWSXTYBXFVOXABXECVPVQVRVSYGYI
      XKXJYIAYFPXHYFUPXIYHWKXHYFEWNVTWAWBWCWDWEWFWGWHWGWHWI $.
      $( [12-Nov-2013] $)
  $}

  ${
    $d b r x y z D $.
    $( The balls of a metric space form a basis for a topology. $)
    blbas $p |- ( D e. Met -> ran ( ball ` D ) e. TopBases ) $=
      ( vz vb vx vy vr cme wcel cv wss wa cbl wrex wral crp cdm wi wceq syl2anc
      cvv cin cfv crn ctb co eqid blin2 simpll cuni inss1 sseli sylan ad2ant2lr
      elunii unirnbl ad2antrr eleqtrd blelrn blcntr eleq2 sseq1 anbi12d rcla4ev
      w3a expr 3expia rexlimdv ex ralrimdva ralrimivv fvex rnex isbasis2g ax-mp
      mpd wb sylibr ) AGHZBIZCIZHZVTDIZEIZUAZJZKZCALUBZUCZMZBWDNZEWHNDWHNZWHUDH
      ZVRWJDEWHWHVRWBWHHZWCWHHZKZWIBWDVRVSWDHZKZWOWIWQWOKZVSFIZWGUEZWDJZFOMZWIF
      WBWCAVSAPPZXCUFZUGWRVRVSXCHZXBWIQVRWPWOUHWRVSWHUIZXCWPWMVSXFHZVRWNWPVSWBH
      WMXGWDWBVSWBWCUJUKVSWBWHUNULUMVRXFXCRWPWOAXCXDUOUPUQVRXEKXAWIFOVRXEWSOHZX
      AWIQZVRXEXHVDWTWHHZVSWTHZXIAVSWSXCXDURAVSWSXCXDUSXJXKXAWIWFXKXAKCWTWHVTWT
      RWAXKWEXAVTWTVSUTVTWTWDVAVBVCVESVFVGSVOVHVIVJWHTHWLWKVPWGALVKVLDEBCWHTVMV
      NVQ $.
      $( [15-Jan-2014] $) $( [12-Sep-2006] $)
  $}

  ${
    $d x C $.  $d x D $.  $d x P $.  $d x R $.  $d x X $.  $d x Y $.
    blres.1 $e |- X = dom dom D $.
    blres.2 $e |- C = ( D |` ( Y X. Y ) ) $.
    $( A ball in a restricted metric space.  (Contributed by Mario Carneiro,
       5-Jan-2014.) $)
    blres $p |- ( ( D e. Met /\ P e. ( X i^i Y ) /\ R e. RR+ ) ->
                  ( P ( ball ` C ) R ) = ( ( P ( ball ` D ) R ) i^i Y ) ) $=
      ( vx cme wcel cin cbl cfv co cdm clt wbr wa wb crp w3a cres metssba dmeqi
      cxp syl6reqr eleq2d elin ancom bitri syl6bb anbi1d anass wceq inss2 sseli
      opreqi oprvres syl5eq sylan breq1d anbi2d pm5.32da 3adant3 syl6bbr metres
      sylan9bb syl5eqel 3ad2ant1 biimpar simp3 eqid elbl syl3anc syl3an2 syl5bb
      cv inss1 3bitr4d eqrdv ) BJKZCEFLZKZDUAKZUBZICDAMNOZCDBMNOZFLZWFIVRZAPZPZ
      KZCWJAOZDQRZSZWJEKZCWJBOZDQRZSZWJFKZSZWJWGKZWJWIKZWFWPXAWTSZXBWBWDWPXETWE
      WBWPXAWQWOSZSZWDXEWBWPXAWQSZWOSXGWBWMXHWOWBWMWJWCKZXHWBWLWCWJWBWCBFFUFUCZ
      PZPWLBEFGUDWKXKAXJHUEUEUGZUHXIWQXASXHWJEFUIWQXAUJUKULUMXAWQWOUNULWDXAXFWT
      WDXASZWOWSWQXMWNWRDQWDCFKZXAWNWRUOWCFCEFUPUQXNXASWNCWJXJOWRAXJCWJHURCWJFF
      BUSUTVAVBVCVDVHVEWTXAUJVFWFAJKZCWLKZWEXCWPTWBWDXOWEWBAXJJHBFVGVIVJWBWDXPW
      EWBXPWDWBWLWCCXLUHVKVEWBWDWEVLWJACDWLWLVMVNVOXDWJWHKZXASWFXBWJWHFUIWFXQWT
      XAWDWBCEKWEXQWTTWCECEFVSUQWJBCDEGVNVPUMVQVTWA $.
      $( [30-Dec-2013] $)
  $}


