$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Metric space balls
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d t u z D $.  $d t u w x y z M $.  $d t u w x y z X $.  $d v y $.
    blfval.1 $e |- X = ( 1st ` M ) $.
    blfval.2 $e |- D = ( 2nd ` M ) $.
    $( The value of the ball function. $)
    blfval $p |- ( M e. Met -> ( ball ` M ) =
  { <. <. x , y >. , z >. | ( ( x e. X /\ y e. RR ) /\ ( 0 < y /\ z =
                { w e. X | ( x D w ) < y } ) ) } ) $=
      ( vt vu cv c1st cfv wcel cr wa cc0 clt wbr c2nd co crab wceq copab2 cmt
      cbl fveq2 syl6eqr eleq2d anbi1d rabeq syl opreqd breq1d rabbisdv eqtrd
      eqeq2d anbi2d anbi12d oprabbidv df-bl cvv fvex eqeltr reex eqid oprabex2
      pm3.27 anim2i ssoprab2i ssexi fvopab4 ) JKFALZJLZMNZOZBLZPOZQZRVRSTZCLZVN
      DLZVOUANZUBZVRSTZDVPUCZUDZQZQZABCUEVNGOZVSQZWAWBVNWCEUBZVRSTZDGUCZUDZQZQZ
      ABCUEZUFUGVOFUDZWJWRABCWTVTWLWIWQWTVQWKVSWTVPGVNWTVPFMNZGVOFMUHHUIZUJUKWT
      WHWPWAWTWGWOWBWTWGWFDGUCZWOWTVPGUDWGXCUDXBWFDVPGULUMWTWFWNDGWTWEWMVRSWTWD
      EVNWCWTWDFUANEVOFUAUHIUIUNUOUPUQURUSUTVAJKABCDVBWSWLWPQZABCUEZABCGPWOXEGX
      AVCHFMVDVEVFXEVGVHWRXDABCWQWPWLWAWPVIVJVKVLVM $.
      $( [5-Sep-2006] $) $( [30-Aug-2006] $)

    $( Alternate value of the ball function  that simplifies its use with
       operation theorems. $)
    blfval2 $p |- ( M e. Met -> ( ball ` M ) = { <. <. x , y >. , z >. |
                   ( ( x e. X /\ y e. { v e. RR | 0 < v } )
                 /\ z = { w e. X | ( x D w ) < y } ) } ) $=
      ( cmt wcel cbl cfv cv cr wa cc0 clt wbr co crab wceq copab2 blfval anass
      breq2 elrab anbi2i bitr4 anbi1i bitr3 oprabbii syl6eq ) GKLGMNAOZHLZBOZPL
      ZQZRUQSTZCOUODOFUAUQSTDHUBUCZQQZABCUDUPUQREOZSTZEPUBLZQZVAQZABCUDABCDFGHI
      JUEVBVGABCVBUSUTQZVAQVGUSUTVAUFVHVFVAVHUPURUTQZQVFUPURUTUFVEVIUPVDUTEUQPV
      CUQRSUGUHUIUJUKULUMUN $.
      $( [6-Sep-2006] $) $( [31-Aug-2006] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d x C $.  $d u v w x y z D $.  $d u v w x y z M $.
    $d w x y z P $.  $d x Q $.  $d v w x y z R $.  $d u v w x y z X $.
    blval.1 $e |- X = ( 1st ` M ) $.
    blval.2 $e |- D = ( 2nd ` M ) $.
    $( The ball around a point ` P ` is the set of all points whose distance
       from ` P ` is less than the ball's radius ` R ` . $)
    blval $p |- ( ( ( M e. Met /\ P e. X ) /\
                  ( R e. RR /\ 0 < R ) ) -> ( P ( ball ` M ) R ) =
                  { x e. X | ( P D x ) < R } ) $=
      ( vy vz vv vw cmt wcel wa cr cc0 clt wbr cbl cfv co cv crab wceq copab2
      blfval2 opreqd ad2antrr c1st cvv fvex eqeltr rabex opreq1 breq1d
      rabbisdv breq2 eqid oprabval2 elrab sylan2br adantll eqtrd ) EMNZCFNZODPN
      QDRSZOZOCDETUAZUBZCDIUCZFNJUCZQKUCZRSZKPUDZNOLUCVKAUCZBUBZVLRSZAFUDZUEOIJ
      LUFZUBZCVPBUBZDRSZAFUDZVEVJWAUEVFVHVEVIVTCDIJLAKBEFGHUGUHUIVFVHWAWDUEZVEV
      FDVONWEVHIJLCDFVOVSWDVTWBVLRSZAFUDWCAFFEUJUAUKGEUJULUMUNVKCUEZVRWFAFWGVQW
      BVLRVKCVPBUOUPUQVLDUEWFWCAFVLDWBRURUQVTUSUTVNVGKDPVMDQRURVAVBVCVD $.
      $( [8-Sep-2006] $) $( [31-Aug-2006] $)

    $( Membership in a ball. $)
    elbl $p |- ( ( ( M e. Met /\ P e. X ) /\
                  ( R e. RR /\ 0 < R ) ) -> ( A e. ( P ( ball ` M ) R ) <->
                  ( A e. X /\ ( P D A ) < R ) ) ) $=
      ( vx cmt wcel wa cr cc0 clt wbr cbl cfv co cv crab blval eleq2d wceq
      opreq2 breq1d elrab syl6bb ) EJKCFKLDMKNDOPLLZACDEQRSZKACITZBSZDOPZIFUAZK
      AFKCABSZDOPZLUIUJUNAIBCDEFGHUBUCUMUPIAFUKAUDULUODOUKACBUEUFUGUH $.
      $( [8-Sep-2006] $) $( [2-Sep-2006] $)

    $( Membership in a ball. $)
    elbl2 $p |- ( ( ( M e. Met /\ P e. X /\ A e. X ) /\
                  ( R e. RR /\ 0 < R ) ) -> ( A e. ( P ( ball ` M ) R ) <->
                  ( P D A ) < R ) ) $=
      ( cmt wcel w3a cr cc0 clt wbr wa cbl cfv co wb elbl 3adantl3 ibar
      3ad2ant3 adantr bitr4d ) EIJZCFJZAFJZKZDLJMDNOPZPACDEQRSJZUICABSDNOZPZUMU
      GUHUKULUNTUIABCDEFGHUAUBUJUMUNTZUKUIUGUOUHUIUMUCUDUEUF $.
      $( [15-Mar-2007] $) $( [9-Mar-2007] $)

    $( A ball contains its center. $)
    blcntr $p |- ( ( ( M e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
                 P e. ( P ( ball ` M ) R ) ) $=
      ( cmt wcel wa cr cc0 clt wbr cbl cfv co simplr wceq ms0 adantr simprr
      eqbrtrd jca elbl mpbird ) DHIZBEIZJZCKIZLCMNZJZJZBBCDOPQIUHBBAQZCMNZJUMUH
      UOUGUHULRUMUNLCMUIUNLSULBADEFGTUAUIUJUKUBUCUDBABCDEFGUEUF $.
      $( [9-Sep-2006] $) $( [2-Sep-2006] $)

    $( Membership in the range of the ball function.  Note that
       ` ran ( ball `` M ) ` is the collection of all balls for metric space
       ` M ` .  $)
    blrn $p |- ( M e. Met -> ( B e. ran ( ball ` M ) <->
         E. x e. X E. y e. { w e. RR | 0 < w }
                B = { z e. X | ( x D z ) < y } ) ) $=
      ( vv cmt wcel cbl cfv crn cv cc0 clt wbr cr crab wa co wceq copab2 wrex
      blfval2 rneqd eleq2d c1st cvv fvex eqeltr rabex eqid elrnoprab syl6bb )
      GLMZEGNOZPZMEAQZHMBQZRDQSTDUAUBZMUCKQVBCQFUDVCSTZCHUBZUEUCABKUFZPZMEVFUEB
      VDUGAHUGUSVAVHEUSUTVGABKCDFGHIJUHUIUJABKHVDVFEVGVECHHGUKOULIGUKUMUNUOVGUP
      UQUR $.
      $( [11-Sep-2006] $) $( [31-Aug-2006] $)

    $( Membership in the range of the ball function.  Note that
       ` ran ( ball `` M ) ` is the collection of all balls for metric space
       ` M ` .  $)
    blrn2 $p |- ( M e. Met -> ( B e. ran ( ball ` M ) <->
         E. x e. X E. y e. RR ( 0 < y /\
                B = { z e. X | ( x D z ) < y } ) ) ) $=
      ( vw cmt wcel cbl cfv crn cv co clt wbr crab wceq cc0 cr wrex wa blrn
      breq2 elrab anbi1i anass bitr rexbii2 rexbii syl6bb ) FKLDFMNOLDAPCPEQBPZ
      RSCGTUAZBUBJPZRSZJUCTZUDZAGUDUBUORSZUPUEZBUCUDZAGUDABCJDEFGHIUFUTVCAGUPVB
      BUSUCUOUSLZUPUEUOUCLZVAUEZUPUEVEVBUEVDVFUPURVAJUOUCUQUOUBRUGUHUIVEVAUPUJU
      KULUMUN $.
      $( [13-Sep-2006] $) $( [31-Aug-2006] $)

    $( Two balls are disjoint if they don't overlap. $)
    bl2in $p |- ( ( ( M e. Met /\ P e. X /\ Q e. X ) /\
                ( R e. RR /\ 0 < R /\ R <_ ( ( P D Q ) / 2 ) ) ) ->
                ( ( P ( ball ` M ) R ) i^i ( Q ( ball ` M ) R ) ) = (/) ) $=
      ( vx cmt wcel w3a cr cc0 clt wbr co c2 cdiv cle wa cv cbl cfv wn wral
      cin c0 wceq wb elbl 3simpa syl2an wi caddc cmul axaddrcl mscl 3expa
      3adantl3 3adantl2 sylanc adantr ad2antrr 2re axmulrcl mpan 3ad2ant1
      adantl lt2addt jca 3simp1 exp3a imp31 cc recnt 2timest syl breqtrrd 2pos
      lemuldiv2t mpan2 mp3an1 biimpar ancom1s anasss sylan 3adantr2 adantlr
      ltletrd ex mstri3 exp43 3imp1 lenltt mpbid a1d pm2.65d elbl2 df-3an
      biimpr negbid sylibrd an1rs imp3a sylbid r19.21aiv disj sylibr ) EJKZBFKZ
      CFKZLZDMKZNDOPZDBCAQZRSQTPZLZUAZIUBZCDEUCUDZQZKZUEZIBDYAQZUFYEYBUGUHUIXSY
      DIYEXSXTYEKZXTFKZBXTAQZDOPZUAZYDXJXKUAXNXOUAZYFYJUJXMXRXTABDEFGHUKXJXKXLU
      LXNXOXQULZUMXSYGYIYDXSYGYIYDUNZXMYGXRYMXMYGUAZXRUAZYICXTAQZDOPZUEZYDYOYIY
      RYOYIUAZYQYHYPUOQZXPOPZYSYQUUAYSYQUAZYTRDUPQZXPYOYTMKZYIYQYNUUDXRYHMKZYPM
      KZUUDYNYHYPUQXJXKYGUUEXLXJXKYGUUEBXTAEFGHURUSUTZXJXLYGUUFXKXJXLYGUUFCXTAE
      FGHURUSVAZVBZVCVDYOUUCMKZYIYQXRUUJYNXNXOUUJXQRMKZXNUUJVERDVFVGVHVIVDYOXPM
      KZYIYQXMUULYGXRBCAEFGHURZVDVDUUBYTDDUOQZUUCOYOYIYQYTUUNOPZYOYIYQUUOUUEUUF
      UAXNXNUAYIYQUAUUOUNYNXRYHYPDDVJYNUUEUUFUUGUUHVKXRXNXNXNXOXQVLZUUPVKUMVMVN
      YOUUCUUNUIZYIYQXRUUQYNXNXOUUQXQXNDVOKUUQDVPDVQVRVHVIVDVSYOUUCXPTPZYIYQXMX
      RUURYGXMXNXQUURXOUULXNXQUAUURXMUULXNXQUURXNUULXQUURXNUULUAUURXQUUKXNUULUU
      RXQUJZVEUUKXNUULLNROPUUSVTRDXPWAWBWCWDWEWFUUMWGWHWIVDWJWKYNYQUUAUEZUNXRYI
      YNUUTYQYNXPYTTPZUUTXJXKXLYGUVAXJXKXLYGUVABCXTAEFGHWLWMWNUULUUDUVAUUTUJYNX
      PYTWOXMUULYGUUMVCUUIVBWPWQVDWRWKYOYCYQXJXLYGLZYKYCYQUJYNXRXTACDEFGHWSXJXL
      YGUVBXKUVBXJXLUAYGUAXJXLYGWTXAVAYLUMXBXCXDWKXEXFXGIYEYBXHXI $.
      $( [13-Apr-2007] $) $( [11-Mar-2007] $)
  $}

  ${
    $d x y z A $.  $d x y B $.  $d x S $.  $d x y z M $.  $d x y z X $.
    $d x y P $.  $d x y R $.
    blrn3.1 $e |- X = ( 1st ` M ) $.
    $( Membership in the range of the ball function.  Note that
       ` ran ( ball `` M ) ` is the collection of all balls for metric space
       ` M ` .  $)
    blrn3 $p |- ( M e. Met -> ( A e. ran ( ball ` M ) <->
         E. x e. X E. y e. RR ( 0 < y /\ A = ( x ( ball ` M ) y ) ) ) ) $=
      ( vz cmt wcel cbl cfv crn cc0 cv clt wbr c2nd co crab wceq wa cr wrex
      eqid blrn2 blval anassrs eqeq2d pm5.32da rexbidva bitr4d ) DHIZCDJKZLIMBN
      ZOPZCANZGNDQKZRUNOPGESZTZUAZBUBUCZAEUCUOCUPUNUMRZTZUAZBUBUCZAEUCABGCUQDEF
      UQUDZUEULVEVAAEULUPEIUAZVDUTBUBVGUNUBIZUAZUOVCUSVIUOUAVBURCVGVHUOVBURTGUQ
      UPUNDEFVFUFUGUHUIUJUJUK $.
      $( [15-Sep-2006] $) $( [31-Aug-2006] $)

    $( A ball belongs to the set of balls of a metric space. $)
    blelrn $p |- ( ( ( M e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
                    ( P ( ball ` M ) R ) e. ran ( ball ` M ) ) $=
      ( vy vx cmt wcel wa cr cc0 clt wbr cbl cfv co crn cv wceq wrex opreq1
      eqeq2d anbi2d breq2 opreq2 anbi12d rcla42ev simplr simprl simprr eqid
      jctir syl3anc wb blrn3 ad2antrr mpbird ) CHIZADIZJZBKIZLBMNZJZJZABCOPZQZV
      FRIZLFSZMNZVGGSZVIVFQZTZJZFKUAGDUAZUTVBVCVGVGTZJZVOVEVNVQVJVGAVIVFQZTZJGF
      ABDKVKATZVMVSVJVTVLVRVGVKAVIVFUBUCUDVIBTZVJVCVSVPVIBLMUEWAVRVGVGVIBAVFUFU
      CUGUHUSUTVDUIVAVBVCUJVEVCVPVAVBVCUKVGULUMUNUSVHVOUOUTVDGFVGCDEUPUQUR $.
      $( [15-Sep-2006] $) $( [2-Sep-2006] $)

    $( Any point in a metric space has a ball around it. $)
    blex $p |- ( ( M e. Met /\ P e. X ) -> E. x e. ran ( ball ` M ) P e. x ) $=
      ( cmt wcel wa c1 cr cc0 clt wbr cv cbl cfv crn wrex 1re lt01 pm3.2i co
      eleq2 rcla4ev blelrn c2nd eqid blcntr sylanc mpan2 ) CFGBDGHZIJGZKILMZHZB
      ANZGZACOPZQZRZULUMSTUABIUQUBZURGBUTGZUSUKUNHUPVAAUTURUOUTBUCUDBICDEUECUFP
      ZBICDEVBUGUHUIUJ $.
      $( [6-Oct-2006] $) $( [12-Sep-2006] $)

    $( A ball is a subset of the base set of the metric space. $)
    blssm $p |- ( ( ( M e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) )
                 -> ( P ( ball ` M ) R ) (_ X ) $=
      ( vx cmt wcel wa cr cc0 clt wbr cbl cfv co cv c2nd crab eqid blval wss
      ssrab2 a1i eqsstrd ) CGHADHIBJHKBLMIIZABCNOPAFQCROZPBLMZFDSZDFUGABCDEUGTU
      AUIDUBUFUHFDUCUDUE $.
      $( [13-Sep-2006] $) $( [31-Aug-2006] $)

    $( A ball is a subset of the base set of the metric space. $)
    rnblssm $p |- ( ( M e. Met /\ B e. ran ( ball ` M ) ) -> B (_ X ) $=
      ( vy vx cmt wcel cbl cfv crn wss cc0 cv clt wbr co wceq wa cr wrex blrn3
      wi pm3.27 blssm adantr eqsstrd ex exp43 imp3a imp4a r19.23advv sylbid
      imp ) BGHZABIJZKHZACLZUOUQMENZOPZAFNZUSUPQZRZSZETUAFCUAURFEABCDUBUOVDURFE
      CTUOVACHZUSTHZSUTVCURUOVEVFUTVCURUCZUCUOVEVFUTVGUOVESVFUTSSZVCURVHVCSAVBC
      VHVCUDVHVBCLVCVAUSBCDUEUFUGUHUIUJUKULUMUN $.
      $( [17-Sep-2006] $) $( [12-Sep-2006] $)

    $( The intersection of two balls with the same center is the smaller
       of them. $)
    blin $p |- ( ( ( M e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) /\
  ( S e. RR /\ 0 < S ) ) -> ( ( P ( ball ` M ) R ) i^i ( P ( ball ` M ) S ) ) =
                  ( P ( ball ` M ) if ( R <_ S , R , S ) ) ) $=
      ( vx cmt wcel wa cr cc0 clt wbr w3a cbl cfv co cin cv c2nd crab cle cif
      wceq eqid blval 3adant3 3adant2 ineq12d eleq1 breq2 anbi12d ifboth
      sylan2 3impb wb ltmint mscl 3expa pm3.26 syl3an 3expb an1rs rabbidv
      eqtr2d inrab syl5eq eqtrd ) DHIZAEIZJZBKIZLBMNZJZCKIZLCMNZJZOZABDPQZRZACV
      TRZSAGTZDUAQZRZBMNZGEUBZWECMNZGEUBZSZABCUCNZBCUDZVTRZVSWAWGWBWIVLVOWAWGUE
      VRGWDABDEFWDUFZUGUHVLVRWBWIUEVOGWDACDEFWNUGUIUJVSWFWHJZGEUBZWMWJVSWMWEWLM
      NZGEUBZWPVLVOVRWMWRUEZVLWLKIZLWLMNZJZWSVOVRJZGWDAWLDEFWNUGWKVOVRXBBCBWLUE
      VMWTVNXABWLKUKBWLLMULUMCWLUEVPWTVQXACWLKUKCWLLMULUMUNUOUPVLVOVRWRWPUEVLXC
      JWQWOGEVLWCEIZXCWQWOUQZVLXDJZVOVRXEWEKIZVMVPXEXFVOVRWEBCURVJVKXDXGAWCWDDE
      FWNUSUTVMVNVAVPVQVAVBVCVDVEUPVFWFWHGEVGVHVI $.
      $( [13-Sep-2006] $) $( [1-Sep-2006] $)
  $}

  ${
    $d v w x z B $.  $d v w x z M $.  $d v w x z P $.
    $( Any point ` P ` in a ball ` B ` can be centered in another ball that
       is a subset of ` B ` . $)
    blss $p |- ( ( M e. Met /\ B e. ran ( ball ` M ) /\ P e. B ) ->
                E. x e. RR ( 0 < x /\ ( P ( ball ` M ) x ) (_ B ) ) $=
      ( vw vz vv cmt wcel cbl cfv crn cc0 cv clt wbr co wss wa cr wrex c2nd
      c1st crab wceq wi eqid blrn2 cmin breq2 opreq2 sseq1d anbi12d rcla4ev
      resubclt pm3.27 ad2antlr mscl 3expa adantlrr sylanc adantrr eleq2 breq1d
      elrab syl6bb biimpa sylan2 wb posdift anasss blssm pm3.26 anim12i jca
      elbl caddc ad2antrr adantr simprl syl3anc axaddrcl simprr cle mstri
      ltaddsub2t adantllr biimpar lelttrd ex sylbid imp ssrabdv sseqtr4d
      adantrll exp43 r19.23advv 3imp ) DHIZBDJKZLIZCBIZMANZOPZCXCWTQZBRZSZATUAZ
      WSXAMENZOPZBFNZGNZDUBKZQZXIOPZGDUCKZUDZUEZSZETUAFXPUAXBXHUFZFEGBXMDXPXPUG
      ZXMUGZUHWSXSXTFEXPTWSXKXPIZXITIZSZXSXBXHWSYESZXRXBXHXJXIXKCXMQZUIQZTIZMYH
      OPZCYHWTQZBRZSZXHYFXRXBSZSZXGYMAYHTXCYHUEZXDYJXFYLXCYHMOUJYPXEYKBXCYHCWTU
      KULUMUNYFCXPIZYGXIOPZSZYIYNYFYQYIYRYDYGTIZYIYFYQSZXIYGUOYEYDWSYQYCYDUPUQZ
      WSYCYQYTYDWSYCYQYTXKCXMDXPYAYBURZUSUTZVAVBZXRXBYSXRXBCXQIYSBXQCVCXOYRGCXP
      XLCUEXNYGXIOXLCXKXMUKVDVEVFVGZVHYOYJYLYFYSYJYNYFYQYRYJUUAYRYJYTYDYRYJVIUU
      AYGXIVJUUDUUBVAVGVKZUUFVHYOYKXQBYFYSYKXQRYNYFYSSZXOGXPYKWSYQSZYIYJSZYKXPR
      UUHCYHDXPYAVLYFWSYSYQWSYEVMZYQYRVMZVNZUUHYIYJUUEUUGVOZVAUUHXLYKIZXOUUHUUO
      XLXPIZCXLXMQZYHOPZSZXOUUIUUJUUOUUSVIUUHXLXMCYHDXPYAYBVPUUMUUNVAUUHUUSXOUU
      HUUSSZXNYGUUQVQQZXIWSYCUUPXNTIUUTXKXLXMDXPYAYBURYFWSYSUUSUUKVRZUUHYCUUSYE
      YCWSYSYCYDVMUQVSZUUHUUPUURVTZWAYTUUQTIZUVATIUUTYGUUQWBWSYCYQYTUUTUUCUVBUV
      CYSYQYFUUSUULUQZWAWSYQUUPUVEUUTCXLXMDXPYAYBURZUVBUVFUVDWAVAYFYDYSUUSWSYCY
      DWCZVRWSYCSUUPYQSXNUVAWDPUUTXKXLCXMDXPYAYBWEUUTWSYCUVBUVCVOUUTUUPYQUVDUVF
      VOVAUUHUUPUURUVAXIOPZUUHUUPSZUVIUURYTUVEYDUVIUURVIUVJYGUUQXIWFUUHYTUUPYFY
      QYTYRUUDVBVSWSYSUUPUVEYEWSYQUUPUVEYRWSYQUUPUVEUVGUSUTWGYFYDYSUUPUVHVRWAWH
      VKWIWJWKWLWMUUFVHYFXRXBVTWNVOVAWOWPWQWKWR $.
      $( [14-Sep-2006] $) $( [31-Aug-2006] $)
  $}

