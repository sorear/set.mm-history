$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Metric space balls
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d d r x y D $.  $d d r x y X $.
    blfval.1 $e |- X = dom dom D $.
    $( The value of the ball function. $)
    blfval $p |- ( D e. Met -> ( ball ` D ) =
                   ( x e. X , r e. RR+ |-> { y e. X | ( x D y ) < r } ) ) $=
      ( vd cme wcel crp cv co clt wbr crab cmpt2 cvv cbl wceq cdm cfv dmexg syl
      syl5eqel reex rpssre ssexi mpt2exg sylancl dmeq dmeqd syl6eqr eqidd opreq
      cr breq1d rabeqbidv mpt2eq123dv df-bl fvmptg mpdan ) CHIZAEDJAKZBKZCLZEKZ
      MNZBDOZPZQIZCRUAVISVBDQIJQIVJVBDCTZTZQFVBVKQIVLQICHUBVKQUBUCUDJUOUEUFUGAE
      DJVHQQUHUIGCAEGKZTZTZJVCVDVMLZVFMNZBVOOZPVIHQRVMCSZAEVOJVRDJVHVSVOVLDVSVN
      VKVMCUJUKFULZVSJUMVSVQVGBVODVTVSVPVEVFMVCVDVMCUNUPUQURABEGUSUTVA $.
      $( [11-Nov-2013] $) $( [30-Aug-2006] $)
  $}

  ${
    $d x A $.  $d r x y B $.  $d r w x y z D $.  $d r x y P $.  $d x Q $.
    $d r x R $.  $d r w x y z X $.
    blval.1 $e |- X = dom dom D $.
    $( The ball around a point ` P ` is the set of all points whose distance
       from ` P ` is less than the ball's radius ` R ` . $)
    blval $p |- ( ( ( D e. Met /\ P e. X ) /\
                  ( R e. RR /\ 0 < R ) ) -> ( P ( ball ` D ) R ) =
                  { x e. X | ( P D x ) < R } ) $=
      ( vy vr wcel clt wbr wa cme crp co cv crab wceq cvv cdm cc0 cbl cfv cmpt2
      elrp w3a blfval opreqd 3ad2ant1 dmexg syl5eqel rabexg 3syl opreq1 rabbidv
      cr breq1d breq2 eqid ovmpt2g 3comr syl3an1 eqtrd 3expa sylan2br ) DUPIUAD
      JKLBMIZCEIZLDNIZCDBUBUCZOZCAPZBOZDJKZAEQZRZDUEVFVGVHVOVFVGVHUFVJCDGHENGPZ
      VKBOZHPZJKZAEQZUDZOZVNVFVGVJWBRVHVFVIWACDGABEHFUGUHUIVFVNSIZVGVHWBVNRZVFB
      TZSIZESIWCBMUJWFEWETSFWESUJUKVMAESULUMVGVHWCWDGHCDENVTVNWAVLVRJKZAEQSVPCR
      ZVSWGAEWHVQVLVRJVPCVKBUNUQUOVRDRWGVMAEVRDVLJURUOWAUSUTVAVBVCVDVE $.
      $( [16-Sep-2007] $) $( [31-Aug-2006] $)

    $( Membership in a ball. $)
    elbl $p |- ( ( ( D e. Met /\ P e. X ) /\
                  ( R e. RR /\ 0 < R ) ) -> ( A e. ( P ( ball ` D ) R ) <->
                  ( A e. X /\ ( P D A ) < R ) ) ) $=
      ( vx cme wcel wa cr cc0 clt wbr cbl cfv co cv crab blval eleq2d opreq2
      wceq breq1d elrab syl6bb ) BHICEIJDKILDMNJJZACDBOPQZIACGRZBQZDMNZGESZIAEI
      CABQZDMNZJUGUHULAGBCDEFTUAUKUNGAEUIAUCUJUMDMUIACBUBUDUEUF $.
      $( [16-Sep-2007] $) $( [2-Sep-2006] $)

    $( Membership in a ball. $)
    elbl2 $p |- ( ( ( D e. Met /\ P e. X /\ A e. X ) /\
                  ( R e. RR /\ 0 < R ) ) -> ( A e. ( P ( ball ` D ) R ) <->
                  ( P D A ) < R ) ) $=
      ( cme wcel w3a cr cc0 clt wbr wa cbl cfv co wb elbl 3adantl3 ibar adantr
      3ad2ant3 bitr4d ) BGHZCEHZAEHZIZDJHKDLMNZNACDBOPQHZUGCABQDLMZNZUKUEUFUIUJ
      ULRUGABCDEFSTUHUKULRZUIUGUEUMUFUGUKUAUCUBUD $.
      $( [9-Mar-2007] $)

    $( Membership in a ball, with reversed distance function arguments. $)
    elbl3 $p |- ( ( ( D e. Met /\ P e. X /\ A e. X ) /\
                  ( R e. RR /\ 0 < R ) ) -> ( A e. ( P ( ball ` D ) R ) <->
                  ( A D P ) < R ) ) $=
      ( cme wcel w3a cr cc0 clt wbr wa cbl cfv co elbl2 wb metsym breq1d adantr
      bitrd ) BGHCEHAEHIZDJHKDLMNZNACDBOPQHCABQZDLMZACBQZDLMZABCDEFRUDUGUISUEUD
      UFUHDLCABEFTUAUBUC $.
      $( [10-Nov-2007] $)

    $( Membership in the range of the ball function.  Note that
       ` ran ( ball `` D ) ` is the collection of all balls for metric
       ` D ` . $)
    blrn $p |- ( D e. Met -> ( B e. ran ( ball ` D ) <->
         E. x e. X E. y e. { w e. RR | 0 < w }
                B = { z e. X | ( x D z ) < y } ) ) $=
      ( vr cme wcel crn cv clt wceq crp wrex cvv wi a1i cbl cfv co wbr crab cab
      cc0 cr cmpt2 blfval rneqd eqid rngop syl6eq eleq2d elisset dmexg syl5eqel
      wa cdm rabexg 3syl eleq1 syl5ibrcom a1d r19.23advv wb df-rp eqeq1 rexbidv
      rexeqbidv elabg pm5.21ndd bitrd ) FJKZEFUAUBZLZKEIMZAMZCMFUCBMZNUDZCGUEZO
      ZBPQZAGQZIUFZKZEWBOZBUGDMNUDDUHUEZQZAGQZVOVQWFEVOVQABGPWBUIZLWFVOVPWLACFG
      BHUJUKABIGPWBWLWLULUMUNUOVOERKZWGWKWGWMSVOEWFUPTVOWHWMABGWIVOWHWMSVSGKVTW
      IKUSVOWMWHWBRKZVOFUTZRKZGRKWNFJUQWPGWOUTRHWORUQURWACGRVAVBEWBRVCVDVEVFWMW
      GWKVGSVOWEWKIERVREOZWDWJAGWQWCWHBPWIPWIOWQDVHTVREWBVIVKVJVLTVMVN $.
      $( [16-Nov-2013] $) $( [31-Aug-2006] $)

    $( Membership in the range of the ball function.  Note that
       ` ran ( ball `` D ) ` is the collection of all balls for metric
       ` D ` . $)
    blrn2 $p |- ( D e. Met -> ( B e. ran ( ball ` D ) <->
         E. x e. X E. y e. RR ( 0 < y /\
                B = { z e. X | ( x D z ) < y } ) ) ) $=
      ( vw cme wcel cbl cfv cv clt wbr crab cc0 cr wrex wa crn wceq breq2 elrab
      co blrn anbi1i anass bitri rexbii2 rexbii syl6bb ) EIJDEKLUAJDAMCMEUEBMZN
      OCFPUBZBQHMZNOZHRPZSZAFSQUMNOZUNTZBRSZAFSABCHDEFGUFURVAAFUNUTBUQRUMUQJZUN
      TUMRJZUSTZUNTVCUTTVBVDUNUPUSHUMRUOUMQNUCUDUGVCUSUNUHUIUJUKUL $.
      $( [18-Sep-2007] $) $( [31-Aug-2006] $)

    $( Two balls are disjoint if they don't overlap. $)
    bl2in $p |- ( ( ( D e. Met /\ P e. X /\ Q e. X ) /\
                ( R e. RR /\ 0 < R /\ R <_ ( ( P D Q ) / 2 ) ) ) ->
                ( ( P ( ball ` D ) R ) i^i ( Q ( ball ` D ) R ) ) = (/) ) $=
      ( vx wcel w3a cr clt wbr co c2 cle wa wn wb syl2an ad2antrr cme cdiv wral
      cc0 cv cbl cfv cin c0 wceq 3simpa elbl wi caddc cmul metcl 3expa 3adantl3
      3adantl2 readdcl syl11anc ad3antrrr 2re remulcl 3ad2ant1 adantl jca simp1
      mpan lt2add exp3a imp31 recn 2times syl breqtrrd 2pos lemuldiv2OLD mp3an1
      cc mpan2 biimpar ancom1s anasss sylan 3adantr2 adantlr ltletrd ex mettri3
      3exp2 3imp1 adantr lenlt mpbid pm2.65d df-3an biimpri elbl2 sylibrd an32s
      a1d notbid expimpd sylbid r19.21aiv disj sylibr ) AUAHZBEHZCEHZIZDJHZUDDK
      LZDBCAMZNUBMOLZIZPZGUEZCDAUFUGZMZHZQZGBDXTMZUCYDYAUHUIUJXRYCGYDXRXSYDHZXS
      EHZBXSAMZDKLZPZYCXLXIXJPXMXNPZYEYIRXQXIXJXKUKXMXNXPUKZXSABDEFULSXRYFYHYCX
      LYFXQYHYCUMXLYFPZXQPZYHCXSAMZDKLZQZYCYMYHYPYMYHPZYOYGYNUNMZXOKLZYQYOYSYQY
      OPZYRNDUOMZXOYLYRJHZXQYHYOYLYGJHZYNJHZUUBXIXJYFUUCXKXIXJYFUUCBXSAEFUPUQUR
      ZXIXKYFUUDXJXIXKYFUUDCXSAEFUPUQUSZYGYNUTVAZVBYMUUAJHZYHYOXQUUHYLXMXNUUHXP
      NJHZXMUUHVCNDVDVIVEVFTYMXOJHZYHYOXLUUJYFXQBCAEFUPZTTYTYRDDUNMZUUAKYMYHYOY
      RUULKLZYMYHYOUUMYLUUCUUDPXMXMPYHYOPUUMUMXQYLUUCUUDUUEUUFVGXQXMXMXMXNXPVHZ
      UUNVGYGYNDDVJSVKVLYMUUAUULUJZYHYOXQUUOYLXMXNUUOXPXMDVTHUUODVMDVNVOVEVFTVP
      YMUUAXOOLZYHYOXLXQUUPYFXLXMXPUUPXNXLUUJXMXPPUUPUUKUUJXMXPUUPXMUUJXPUUPXMU
      UJPUUPXPUUIXMUUJUUPXPRZVCUUIXMUUJIUDNKLUUQVQNDXOVRWAVSWBWCWDWEWFWGTWHWIYL
      YOYSQZUMXQYHYLUURYOYLXOYROLZUURXIXJXKYFUUSXIXJXKYFUUSBCXSAEFWJWKWLYLUUJUU
      BUUSUURRXLUUJYFUUKWMUUGXOYRWNVAWOXBTWPWIYMYBYOYLXIXKYFIZYJYBYORXQXIXKYFUU
      TXJUUTXIXKPYFPXIXKYFWQWRUSYKXSACDEFWSSXCWTXAXDXEXFGYDYAXGXH $.
      $( [11-Mar-2007] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d r x y z D $.  $d x S $.  $d r x y z X $.
    $d x y P $.  $d x y R $.
    blf.1 $e |- X = dom dom D $.
    $( Mapping of a ball. $)
    blf $p |- ( D e. Met -> ( ball ` D ) : ( X X. ( 0 (,) +oo ) ) --> ~P X ) $=
      ( vx vr vy cme wcel cc0 cpnf cioo co cxp wf crp cv wral cdm cvv dmexg cpw
      cbl cfv clt wbr crab cmpt2 wa ssrab2 wb syl5eqel elpw2g mpbiri r19.21aivv
      wss 3syl a1d eqid foprab2a sylib blfval wceq ioorp xpeq2i feq12d mpbird
      a1i ) AGHZBIJKLZMZBUAZAUBUCZNBOMZVKDEBODPZFPALEPZUDUEZFBUFZUGZNZVHVQVKHZE
      OQDBQVSVHVTDEBOVHVTVNBHVOOHUHVHVTVQBUOZVPFBUIVHARZSHZBSHVTWAUJAGTWCBWBRSC
      WBSTUKVQBSULUPUMUQUNDEBOVQVKVRVRURUSUTVHVJVMVKVLVRDFABECVAVJVMVBVHVIOBVCV
      DVGVEVF $.
      $( [16-Nov-2013] $) $( [7-May-2007] $)

    $( A ball contains its center. $)
    blcntr $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
                 P e. ( P ( ball ` D ) R ) ) $=
      ( cme wcel wa cr cc0 clt wbr cbl cfv co elbl simplr wceq met0 adantr
      simprr eqbrtrd mpbir2and ) AFGZBDGZHZCIGZJCKLZHZHZBBCAMNOGUEBBAOZCKLBABCD
      EPUDUEUIQUJUKJCKUFUKJRUIBADESTUFUGUHUAUBUC $.
      $( [22-Sep-2007] $) $( [2-Sep-2006] $)

    $( A ball is not empty. $)
    bln0 $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
                 ( P ( ball ` D ) R ) =/= (/) ) $=
      ( cme wcel wa cr cc0 clt wbr cbl cfv co c0 wne blcntr ne0i syl ) AFGBDGHC
      IGJCKLHHBBCAMNOZGUAPQABCDERUABST $.
      $( [6-Oct-2007] $)

    $( Membership in the range of the ball function.  Note that
       ` ran ( ball `` D ) ` is the collection of all balls for metric
       ` D ` . $)
    blrn3 $p |- ( D e. Met -> ( A e. ran ( ball ` D ) <->
         E. x e. X E. y e. RR ( 0 < y /\ A = ( x ( ball ` D ) y ) ) ) ) $=
      ( vz cme wcel cbl cfv cv clt wbr co wceq wa cr wrex rexbidva crn cc0 crab
      blrn2 blval anassrs eqeq2d pm5.32da bitr4d ) DHIZCDJKZUAIUBBLZMNZCALZGLDO
      ULMNGEUCZPZQZBRSZAESUMCUNULUKOZPZQZBRSZAESABGCDEFUDUJVBURAEUJUNEIQZVAUQBR
      VCULRIZQZUMUTUPVEUMQUSUOCVCVDUMUSUOPGDUNULEFUEUFUGUHTTUI $.
      $( [22-Sep-2007] $) $( [31-Aug-2006] $)

    $( A ball belongs to the set of balls of a metric space. $)
    blelrn $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) ) ->
                    ( P ( ball ` D ) R ) e. ran ( ball ` D ) ) $=
      ( vy vx cme wcel wa cr cc0 clt wbr cbl co cv wceq wrex eqeq2d simplr eqid
      cfv crn simprl simprr jctir opreq1 anbi2d breq2 opreq2 rcla42ev syl111anc
      anbi12d wb blrn3 ad2antrr mpbird ) AHIZBDIZJZCKIZLCMNZJZJZBCAOUCZPZVFUDIZ
      LFQZMNZVGGQZVIVFPZRZJZFKSGDSZVEUTVBVCVGVGRZJZVOUSUTVDUAVAVBVCUEVEVCVPVAVB
      VCUFVGUBUGVNVQVJVGBVIVFPZRZJGFBCDKVKBRZVMVSVJVTVLVRVGVKBVIVFUHTUIVICRZVJV
      CVSVPVICLMUJWAVRVGVGVICBVFUKTUNULUMUSVHVOUOUTVDGFVGADEUPUQUR $.
      $( [23-Sep-2007] $) $( [2-Sep-2006] $)

    $( Any point in a metric space has a ball around it. $)
    blex $p |- ( ( D e. Met /\ P e. X ) -> E. x e. ran ( ball ` D ) P e. x ) $=
      ( cme wcel wa c1 cr cc0 clt wbr cv cbl cfv crn wrex 1re lt01 blelrn eleq2
      co blcntr rcla4ev syl11anc mpanr12 ) BFGCDGHZIJGZKILMZCANZGZABOPZQZRZSTUH
      UIUJHHCIUMUCZUNGCUPGZUOBCIDEUABCIDEUDULUQAUPUNUKUPCUBUEUFUG $.
      $( [28-Jan-2008] $) $( [12-Sep-2006] $)

    $( A ball is a subset of the base set of a metric space. $)
    blssm $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) )
                 -> ( P ( ball ` D ) R ) C_ X ) $=
      ( vx cme wcel wa cr cc0 clt wbr cbl cfv co cv crab blval wss a1i eqsstrd
      ssrab2 ) AGHBDHICJHKCLMIIZBCANOPBFQAPCLMZFDRZDFABCDESUFDTUDUEFDUCUAUB $.
      $( [20-Sep-2007] $) $( [31-Aug-2006] $)

    $( A ball is a subset of the base set of a metric space. $)
    rnblssm $p |- ( ( D e. Met /\ B e. ran ( ball ` D ) ) -> B C_ X ) $=
      ( vy vx cme wcel cbl cfv crn wss cc0 cv clt wbr wa cr wrex wi blrn3 simpr
      co wceq blssm adantr eqsstrd exp53 imp3a imp4a r19.23advv sylbid imp ) BG
      HZABIJZKHZACLZUNUPMENZOPZAFNZURUOUCZUDZQZERSFCSUQFEABCDUAUNVCUQFECRUNUTCH
      ZURRHZQUSVBUQUNVDVEUSVBUQTTUNVDVEUSVBUQUNVDQVEUSQQZVBQAVACVFVBUBVFVACLVBB
      UTURCDUEUFUGUHUIUJUKULUM $.
      $( [26-Sep-2007] $) $( [12-Sep-2006] $)

    $( The intersection of two balls with the same center is the smaller of
       them. $)
    blin $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) /\
  ( S e. RR /\ 0 < S ) ) -> ( ( P ( ball ` D ) R ) i^i ( P ( ball ` D ) S ) ) =
                  ( P ( ball ` D ) if ( R <_ S , R , S ) ) ) $=
      ( vx wcel wa cr cc0 clt wbr co cin crab wceq blval eleq1 breq2 cme w3a cv
      cbl cfv cle cif 3adant3 3adant2 ineq12d inrab anbi12d ifboth sylan2 3impb
      metcl 3expa simpl ltmin syl3an 3expb an32s rabbidva eqtr2d syl5eq eqtrd
      wb ) AUAHZBEHZIZCJHZKCLMZIZDJHZKDLMZIZUBZBCAUDUEZNZBDVRNZOBGUCZANZCLMZGEP
      ZWBDLMZGEPZOZBCDUFMZCDUGZVRNZVQVSWDVTWFVJVMVSWDQVPGABCEFRUHVJVPVTWFQVMGAB
      DEFRUIUJVQWGWCWEIZGEPZWJWCWEGEUKVQWJWBWILMZGEPZWLVJVMVPWJWNQZVMVPIZVJWIJH
      ZKWILMZIZWOWHVMVPWSCDCWIQVKWQVLWRCWIJSCWIKLTULDWIQVNWQVOWRDWIJSDWIKLTULUM
      GABWIEFRUNUOVJVMVPWNWLQVJWPIWMWKGEVJWAEHZWPWMWKVGZVJWTIZVMVPXAXBWBJHZVMVK
      VPVNXAVHVIWTXCBWAAEFUPUQVKVLURVNVOURWBCDUSUTVAVBVCUOVDVEVF $.
      $( [25-Sep-2007] $) $( [1-Sep-2006] $)
  $}

  ${
    $d w x z B $.  $d v w x z D $.  $d v w x z P $.
    $( Any point ` P ` in a ball ` B ` can be centered in another ball that is
       a subset of ` B ` . $)
    blss $p |- ( ( D e. Met /\ B e. ran ( ball ` D ) /\ P e. B ) ->
                E. x e. RR ( 0 < x /\ ( P ( ball ` D ) x ) C_ B ) ) $=
      ( vw vz vv wcel cc0 cv clt wbr co wss wa cr wrex wceq metcl syl111anc cme
      cbl cfv crn cdm crab wi eqid blrn2 cmin eleq2 opreq2 breq1d syl6bb biimpa
      elrab simplrr 3expa adantlrr resubcl syl11anc adantrr sylan2 posdif simpl
      wb anasss anim12i blssm syl12anc elbl caddc simplll simplrl adantr simprl
      readdcl simprr ad2antrr cle mettri syl13anc adantllr ltaddsub2 biimpar ex
      lelttrd sylbid imp ssrabdv sseqtr4d breq2 sseq1d anbi12d rcla4ev adantrll
      exp43 r19.23advv 3imp ) CUAHZBCUBUCZUDHZDBHZIAJZKLZDXDXAMZBNZOZAPQZWTXBIE
      JZKLZBFJZGJZCMZXJKLZGCUEUEZUFZRZOZEPQFXPQXCXIUGZFEGBCXPXPUHZUIWTXSXTFEXPP
      WTXLXPHZXJPHZOZXSXCXIWTYDOZXRXCXIXKYEXRXCOZOZXJXLDCMZUJMZPHZIYIKLZDYIXAMZ
      BNZXIYFYEDXPHZYHXJKLZOZYJXRXCYPXRXCDXQHYPBXQDUKXOYOGDXPXMDRXNYHXJKXMDXLCU
      LUMUPUNUOZYEYNYJYOYEYNOZYCYHPHZYJWTYBYCYNUQZWTYBYNYSYCWTYBYNYSXLDCXPYASZU
      RUSZXJYHUTVAVBZVCYFYEYPYKYQYEYNYOYKYRYOYKYRYSYCYOYKVFUUBYTYHXJVDVAUOVGZVC
      YGYLXQBYFYEYPYLXQNYQYEYPOZXOGXPYLUUEWTYNOZYJYKYLXPNYEWTYPYNWTYDVEYNYOVEVH
      ZUUCUUDCDYIXPYAVIVJUUEXMYLHZXOUUEUUHXMXPHZDXMCMZYIKLZOZXOUUEUUFYJYKUUHUUL
      VFUUGUUCUUDXMCDYIXPYAVKVJUUEUULXOUUEUULOZXNYHUUJVLMZXJUUMWTYBUUIXNPHWTYDY
      PUULVMZUUEYBUULWTYBYCYPVNVOZUUEUUIUUKVPZXLXMCXPYASTUUMYSUUJPHZUUNPHUUMWTY
      BYNYSUUOUUPYEYNYOUULVNZUUATUUMWTYNUUIUURUUOUUSUUQDXMCXPYASZTYHUUJVQVAYEYC
      YPUULWTYBYCVRZVSUUMWTYBUUIYNXNUUNVTLUUOUUPUUQUUSXLXMDCXPYAWAWBUUEUUIUUKUU
      NXJKLZUUEUUIOZUVBUUKUVCYSUURYCUVBUUKVFUUEYSUUIYEYNYSYOUUBVBVOWTYPUUIUURYD
      WTYNUUIUURYOWTYNUUIUURUUTURUSWCYEYCYPUUIUVAVSYHUUJXJWDTWEVGWGWFWHWIWJVCYE
      XRXCVPWKXHYKYMOAYIPXDYIRZXEYKXGYMXDYIIKWLUVDXFYLBXDYIDXAULWMWNWOVJWPWQWRW
      HWS $.
      $( [28-Jul-2008] $) $( [31-Aug-2006] $)
  $}

  ${
    $d x y A $.  $d x y D $.  $d x y P $.  $d y X $.
    blssex.1 $e |- X = dom dom D $.
    $( Two ways to express the existence of a ball subset. $)
    blssex $p |- ( ( D e. Met /\ P e. X ) -> ( E. x e. ran ( ball ` D ) ( P e.
     x /\ x C_ A ) <-> E. y e. RR ( 0 < y /\ ( P ( ball ` D ) y ) C_ A ) ) ) $=
      ( cme wcel wa cv wss cbl cfv crn wrex cc0 cr anassrs r19.23adva clt co wi
      wbr w3a blss r19.41v anim2i reximi sylbir sylan 3exp1 imp4b adantr blelrn
      sstr adantrr blcntr anim1i anasss eleq2 sseq1 anbi12d rcla4ev syl11anc ex
      wceq impbid ) DHIZEFIZJZEAKZIZVLCLZJZADMNZOZPZQBKZUAUDZEVSVPUBZCLZJZBRPZV
      IVRWDUCVJVIVOWDAVQVIVLVQIZVMVNWDVIWEVMVNWDVIWEVMUEVTWAVLLZJZBRPZVNWDBVLDE
      UFWHVNJWGVNJZBRPWDWGVNBRUGWIWCBRVTWFVNWCWFVNJWBVTWAVLCUPUHSUIUJUKULUMTUNV
      KWCVRBRVKVSRIZJZWCVRWKWCJWAVQIZEWAIZWBJZVRWKVTWLWBVKWJVTWLDEVSFGUOSUQWKVT
      WBWNWKVTJWMWBVKWJVTWMDEVSFGURSUSUTVOWNAWAVQVLWAVGVMWMVNWBVLWAEVAVLWACVBVC
      VDVEVFTVH $.
      $( [5-May-2007] $)
  $}

  ${
    $d x D $.  $d x P $.  $d x R $.  $d x S $.  $d x X $.
    ssbl.1 $e |- X = dom dom D $.
    $( The size of a ball increases monotonically with its radius. $)
    ssbl $p |- ( ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) /\
               ( S e. RR /\ 0 < S ) ) /\ R <_ S ) ->
               ( P ( ball ` D ) R ) C_ ( P ( ball ` D ) S ) ) $=
      ( vx cme wcel wa cr cc0 clt wbr w3a co wi wb elbl adantr cle cbl cv metcl
      3expa ancoms ltletr syl3an1 exp3a com23 3expb exp31 com4l imp31 imdistand
      cfv ex 3impb 3adant2r 3adant3r imp 3adant3 3adant2 3imtr4d ssrdv ) AHIZBE
      IZJZCKIZLCMNZJZDKIZLDMNZJZOZCDUANZJZGBCAUBUPZPZBDVRPZVQGUCZEIZBWAAPZCMNZJ
      ZWBWCDMNZJZWAVSIZWAVTIZVOVPWEWGQZVHVKVLVPWJQZVMVHVIVLWKVJVHVIVLWKVHVIVLJZ
      JZVPWJWMVPJWBWDWFVHWLVPWBWDWFQZQWBVHWLVPWNWBVHWLVPWNQZWBVHJZVIVLWOWPVIVLO
      ZWDVPWFWQWDVPWFWPWCKIZVIVLWDVPJWFQVHWBWRVFVGWBWRBWAAEFUDUEUFWCCDUGUHUIUJU
      KULUMUNUOUQURUSUTVAVOWHWERZVPVHVKWSVNWAABCEFSVBTVOWIWGRZVPVHVNWTVKWAABDEF
      SVCTVDVE $.
      $( [20-Sep-2007] $)
  $}

  ${
    $d x D $.  $d x P $.  $d x R $.  $d x S $.
    ssblex.1 $e |- X = dom dom D $.
    $( A nested ball exists whose radius is less than any desired amount. $)
    ssblex $p |- ( ( ( D e. Met /\ P e. X ) /\ ( R e. RR /\ 0 < R ) /\
               ( S e. RR /\ 0 < S ) ) -> E. x e. RR ( 0 < x /\ x < R /\
               ( P ( ball ` D ) x ) C_ ( P ( ball ` D ) S ) ) ) $=
      ( wcel wa cr cc0 clt wbr w3a cle co wss ad2antrr 3ad2antl2 adantr cme cbl
      wo cv cfv wrex lelttric ad2ant2r 3adant1 c2 cdiv rehalfcl halfpos2 biimpa
      halfpos ad3antrrr simplll simplr simpr ltletrd wi ltle sylan mpd adantlrr
      adantlr 3adantl1 ssbl syl3anl2 syldan breq2 breq1 opreq2 sseq1d 3anbi123d
      jca wceq rcla4ev syl13anc simpl3l simpl3r ssid a1i jaodan mpdan ) BUAHCFH
      IZDJHZKDLMZIZEJHZKELMZIZNZDEOMZEDLMZUCZKAUDZLMZWQDLMZCWQBUBUEZPZCEWTPZQZN
      ZAJUFZWIWLWPWFWGWJWPWHWKDEUGUHUIWMWNXEWOWMWNIDUJUKPZJHZKXFLMZXFDLMZCXFWTP
      ZXBQZXEWIWFWNXGWLWGXGWHWNDULZRSWIWFWNXHWLWIXHWNWGWHXHDUMUNZTSWIWFWNXIWLWI
      XIWNWGWHXIDUOUNZTSWMWNXFEOMZXKWIWLWNXOWFWIWJWNXOWKWIWJIZWNIZXFELMZXOXQXFD
      EWGXGWHWJWNXLUPWGWHWJWNUQWIWJWNURWIXIWJWNXNRXPWNUSUTXPXRXOVAZWNWGWJXSWHWG
      XGWJXSXLXFEVBVCVFTVDVEVGWIWFXGXHIWLXOXKWIXGXHWGXGWHXLTXMVPBCXFEFGVHVIVJXD
      XHXIXKNAXFJWQXFVQZWRXHWSXIXCXKWQXFKLVKWQXFDLVLXTXAXJXBWQXFCWTVMVNVOVRVSWM
      WOIZWJWKWOXBXBQZXEWJWKWFWIWOVTWJWKWFWIWOWAWMWOUSYBYAXBWBWCXDWKWOYBNAEJWQE
      VQZWRWKWSWOXCYBWQEKLVKWQEDLVLYCXAXBXBWQECWTVMVNVOVRVSWDWE $.
      $( [20-Sep-2007] $)
  $}

