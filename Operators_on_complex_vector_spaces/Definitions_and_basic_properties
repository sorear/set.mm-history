$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Definitions and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c LnOp $.
  $c normOp $.
  $c BLnOp $.
  $c 0op $.

  $( Extend class notation with the class of linear operators on normed complex
     vector spaces. $)
  clno $a class LnOp $.

  $( Extend class notation with the class of operator norms on normed complex
     vector spaces. $)
  cnmo $a class normOp $.

  $( Extend class notation with the class of bounded linear operators on normed
     complex vector spaces. $)
  cblo $a class BLnOp $.

  $( Extend class notation with the class of zero operators on normed complex
     vector spaces. $)
  c0o $a class 0op $.

  ${
    $d t u w x y z $.
    $( Define the class of linear operators between two normed complex vector
       spaces.  In the literature, an operator may be a partial function, i.e.
       the domain of an operator is not necessarily the entire vector space.
       However, since the domain of a linear operator is a vector subspace, we
       define it with a complete function for convenience and will use subset
       relations to specify the partial function case. $)
    df-lno $a |- LnOp = ( u e. NrmCVec , w e. NrmCVec |->
    { t e. ( ( BaseSet ` w ) ^m ( BaseSet ` u ) ) |
       A. x e. CC A. y e. ( BaseSet ` u ) A. z e. ( BaseSet ` u )
      ( t ` ( ( x ( .s ` u ) y ) ( +v ` u ) z ) ) =
         ( ( x ( .s ` w ) ( t ` y ) ) ( +v ` w ) ( t ` z ) ) } ) $.

    $( Define the norm of an operator between two normed complex vector
       spaces.  This definition produces an operator norm function for each
       pair of vector spaces ` <. u , w >. ` .  Based on definition of linear
       operator norm in [AkhiezerGlazman] p. 39, although we define it for all
       operators for convenience.  It isn't necessarily meaningful for
       nonlinear operators, since it doesn't take into account operator values
       at vectors with norm greater than 1.  See Equation 2 of [Kreyszig] p. 92
       for a definition that does (although it ignores the value at the zero
       vector).  However, operator norms are rarely if ever used for nonlinear
       operators. $)
    df-nmo $a |- normOp = ( u e. NrmCVec , w e. NrmCVec |->
                ( t e. ( ( BaseSet ` w ) ^m ( BaseSet ` u ) ) |->
        sup ( { x | E. z e. ( BaseSet ` u ) ( ( ( norm ` u ) ` z ) <_ 1 /\
            x = ( ( norm ` w ) ` ( t ` z ) ) ) } , RR* , < ) ) ) $.

    $( Define the class of bounded linear operators between two normed complex
       vector spaces. $)
    df-blo $a |- BLnOp = ( u e. NrmCVec , w e. NrmCVec |->
      { t e. ( u LnOp w ) | ( ( u normOp w ) ` t ) < +oo } ) $.

    $( Define the zero operator between two normed complex vector spaces. $)
    df-0o $a |- 0op = ( u e. NrmCVec , w e. NrmCVec |->
                        ( ( BaseSet ` u ) X. { ( 0vec ` w ) } ) ) $.
  $}

  $c adj $.
  $( Adjoint of an operator. $)
  caj $a class adj $.

  $c HmOp $.
  $( Set of Hermitional (self-adjoint) operators. $)
  chmo $a class HmOp $.

  ${
    $d a o s t u w x y $.
    $( Define the adjoint of an operator (if it exists).  The domain of
       ` U adj W ` is the set of all operators from ` U ` to ` W ` that have an
       adjoint.  Definition 3.9-1 of [Kreyszig] p. 196, although we don't
       require that ` U ` and ` W ` be Hilbert spaces nor that the operators be
       linear.  Although we define it for any normed vector space for
       convenience, the definition is meaningful only for inner product
       spaces. $)
    df-aj $a |- adj = ( u e. NrmCVec , w e. NrmCVec |->
        { <. t , s >. | ( t : ( BaseSet ` u ) --> ( BaseSet ` w ) /\
                          s : ( BaseSet ` w ) --> ( BaseSet ` u ) /\
        A. x e. ( BaseSet ` u ) A. y e. ( BaseSet ` w )
          ( ( t ` x ) ( .i ` w ) y ) = ( x ( .i ` u ) ( s ` y ) ) ) } ) $.

    $( Define the set of Hermitian (self-adjoint) operators on a normed complex
       vector space (normally a Hilbert space).  Although we define it for any
       normed vector space for convenience, the definition is meaningful only
       for inner product spaces. $)
    df-hmo $a |- HmOp = ( u e. NrmCVec |->
                { t e. dom ( u adj u ) | ( ( u adj u ) ` t ) = t } ) $.

$(
    @( Alternate definition of the set of Hermitian (self-adjoint) operators on
       a normed complex vector space (normally a Hilbert space). @)
    dfhm2 @p |- HmOp = { <. u , o >. | ( u e. NrmCVec /\ o = { t | ( t :
  ( BaseSet ` u ) --> ( BaseSet ` u ) /\ A. x e. ( BaseSet ` u ) A. y e.
  ( BaseSet ` u ) ( x ( .i ` u ) ( t ` y ) ) = ( ( t ` x ) ( .i ` u ) y ) )
  } ) } @=
      (  ) ? @.
      @( [ ?] @) @( [8-Feb-2008] @)
$)
  $}

  ${
    $d t u w x y z U $.  $d t u w x y z W $.  $d t u w y z X $.  $d t w Y $.
    $d t u w G $.  $d t u w R $.  $d t u w H $.  $d t u w S $.
    $d t u w x y z T $.
    lnoval.1 $e |- X = ( BaseSet ` U ) $.
    lnoval.2 $e |- Y = ( BaseSet ` W ) $.
    lnoval.3 $e |- G = ( +v ` U ) $.
    lnoval.4 $e |- H = ( +v ` W ) $.
    lnoval.5 $e |- R = ( .s ` U ) $.
    lnoval.6 $e |- S = ( .s ` W ) $.
    lnoval.7 $e |- L = ( U LnOp W ) $.
    $( The set of linear operators between two normed complex vector spaces. $)
    lnoval $p |- ( ( U e. NrmCVec /\ W e. NrmCVec ) -> L =
      { t e. ( Y ^m X ) | A. x e. CC A. y e. X A. z e. X
        ( t ` ( ( x R y ) G z ) ) = ( ( x S ( t ` y ) ) H ( t ` z ) ) } ) $=
      ( vu vw cnv wcel wa clno co cv cfv wceq wral cc cmap crab cns cpv syl6eqr
      cba fveq2 opreq2d opreqd eqidd opreq123d fveq2d raleqbidv ralbidv opreq1d
      eqeq1d rabeqbidv eqeq2d 2ralbidv df-lno oprex rabex ovmpt2 syl5eq ) GUCUD
      KUCUDUEJGKUFUGAUHZBUHZEUGZCUHZHUGZDUHZUIZVQVRWBUIZFUGZVTWBUIZIUGZUJZCLUKB
      LUKZAULUKZDMLUMUGZUNZTUAUBGKUCUCVQVRUAUHZUOUIZUGZVTWMUPUIZUGZWBUIZVQWDUBU
      HZUOUIZUGZWFWSUPUIZUGZUJZCWMURUIZUKZBXEUKZAULUKZDWSURUIZXEUMUGZUNWLUFWCXC
      UJZCLUKZBLUKZAULUKZDXILUMUGZUNWMGUJZXHXNDXJXOXPXELXIUMXPXEGURUILWMGURUSNU
      QZUTXPXGXMAULXPXFXLBXELXQXPXDXKCXELXQXPWRWCXCXPWQWAWBXPWOVSVTVTWPHXPWPGUP
      UIHWMGUPUSPUQXPWNEVQVRXPWNGUOUIEWMGUOUSRUQVAXPVTVBVCVDVHVEVEVFVIWSKUJZXNW
      JDXOWKXRXIMLUMXRXIKURUIMWSKURUSOUQVGXRXMWIAULXRXKWHBCLLXRXCWGWCXRXAWEWFWF
      XBIXRXBKUPUIIWSKUPUSQUQXRWTFVQWDXRWTKUOUIFWSKUOUSSUQVAXRWFVBVCVJVKVFVIABC
      UBUADVLWJDWKMLUMVMVNVOVP $.
      $( [16-Nov-2013] $) $( [6-Nov-2007] $)

    $( The predicate "is a linear operator." $)
    islno $p |- ( ( U e. NrmCVec /\ W e. NrmCVec ) -> ( T e. L <->
      ( T : X --> Y /\ A. x e. CC A. y e. X A. z e. X
      ( T ` ( ( x R y ) G z ) ) = ( ( x S ( T ` y ) ) H ( T ` z ) ) ) ) ) $=
      ( vw cnv wcel wa cv co cfv wceq wral cc cmap crab wf lnoval fveq1 opreq2d
      eleq2d opreq12d eqeq12d 2ralbidv ralbidv elrab cba cvv fvex eqeltri elmap
      anbi1i bitri syl6bb ) GUBUCKUBUCUDZFJUCFAUEZBUEZDUFCUEZHUFZUAUEZUGZVLVMVP
      UGZEUFZVNVPUGZIUFZUHZCLUIBLUIZAUJUIZUAMLUKUFZULZUCZLMFUMZVOFUGZVLVMFUGZEU
      FZVNFUGZIUFZUHZCLUIBLUIZAUJUIZUDZVKJWFFABCUADEGHIJKLMNOPQRSTUNUQWGFWEUCZW
      PUDWQWDWPUAFWEVPFUHZWCWOAUJWSWBWNBCLLWSVQWIWAWMVOVPFUOWSVSWKVTWLIWSVRWJVL
      EVMVPFUOUPVNVPFUOURUSUTVAVBWRWHWPMLFMKVCUGVDOKVCVEVFLGVCUGVDNGVCVEVFVGVHV
      IVJ $.
      $( [16-Nov-2013] $) $( [4-Dec-2007] $)

    $d t u w A $.  $d t w B $.  $d t C $.
    $( Basic linearity property of a linear operator. $)
    lnolin $p |- ( ( ( U e. NrmCVec /\ W e. NrmCVec /\ T e. L ) /\
       ( A e. CC /\ B e. X /\ C e. X ) ) ->
      ( T ` ( ( A R B ) G C ) ) = ( ( A S ( T ` B ) ) H ( T ` C ) ) ) $=
      ( vu vw vt cnv wcel w3a cc co cfv wceq cv wf wa islno biimpa 3impa simprd
      wral opreq1 opreq1d fveq2d eqeq12d opreq2 fveq2 opreq2d rcla43v syl5com
      imp ) GUDUEZKUDUEZFJUEZUFZAUGUEBLUECLUEUFZABDUHZCHUHZFUIZABFUIZEUHZCFUIZI
      UHZUJZVLUAUKZUBUKZDUHZUCUKZHUHZFUIZWBWCFUIZEUHZWEFUIZIUHZUJZUCLURUBLURUAU
      GURZVMWAVLLMFULZWMVIVJVKWNWMUMZVIVJUMVKWOUAUBUCDEFGHIJKLMNOPQRSTUNUOUPUQW
      LWAAWCDUHZWEHUHZFUIZAWHEUHZWJIUHZUJVNWEHUHZFUIZVRWJIUHZUJUAUBUCABCUGLLWBA
      UJZWGWRWKWTXDWFWQFXDWDWPWEHWBAWCDUSUTVAXDWIWSWJIWBAWHEUSUTVBWCBUJZWRXBWTX
      CXEWQXAFXEWPVNWEHWCBADVCUTVAXEWSVRWJIXEWHVQAEWCBFVDVEUTVBWECUJZXBVPXCVTXF
      XAVOFWECVNHVCVAXFWJVSVRIWECFVDVEVBVFVGVH $.
      $( [16-Nov-2013] $) $( [4-Dec-2007] $)
  $}

  ${
    $d x y z T $.  $d x y z U $.  $d x y z W $.  $d x y z X $.
    lnof.1 $e |- X = ( BaseSet ` U ) $.
    lnof.2 $e |- Y = ( BaseSet ` W ) $.
    lnof.7 $e |- L = ( U LnOp W ) $.
    $( A linear operator is a mapping. $)
    lnof $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T e. L ) -> T : X --> Y ) $=
      ( vx vy vz cnv wcel wa cv cfv co wral eqid wf cns cpv islno biimpa simpld
      wceq cc 3impa ) BMNZDMNZACNZEFAUAZUJUKOZULOUMJPZKPZBUBQZRLPZBUCQZRAQUOUPA
      QDUBQZRURAQDUCQZRUGLESKESJUHSZUNULUMVBOJKLUQUTABUSVACDEFGHUSTVATUQTUTTIUD
      UEUFUI $.
      $( [18-Nov-2013] $) $( [4-Dec-2007] $)
  $}

  ${
    lno0.1 $e |- X = ( BaseSet ` U ) $.
    lno0.2 $e |- Y = ( BaseSet ` W ) $.
    lno0.5 $e |- Q = ( 0vec ` U ) $.
    lno0.z $e |- Z = ( 0vec ` W ) $.
    lno0.7 $e |- L = ( U LnOp W ) $.
    $( The value of a linear operator at zero is zero. $)
    lno0 $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T e. L )
         -> ( T ` Q ) = Z ) $=
      ( cnv wcel w3a cfv co wceq eqid c1 cneg cns cpv cc ax-1cn negcli 3ad2ant1
      nvzcl 3jca lnolin mpdan nvlinv fveq2d simp2 lnof ffvelrn syl11anc 3eqtr3d
      a1i wf ) CNOZENOZBDOZPZUAUBZACUCQZRACUDQZRZBQZVFABQZEUCQZRVKEUDQZRZVKHVEV
      FUEOZAFOZVPPVJVNSVEVOVPVPVOVEUAUFUGUTVBVCVPVDCFAIKUIZUHZVRUJVFAAVGVLBCVHV
      MDEFGIJVHTZVMTZVGTZVLTZMUKULVBVCVJVKSVDVBVIABVBVPVIASVQAVGCVHFAIVSWAKUMUL
      UNUHVEVCVKGOZVNHSVBVCVDUOVEFGBVAVPWCBCDEFGIJMUPVRFGABUQURVKVLEVMGHJVTWBLU
      MURUS $.
      $( [18-Nov-2013] $) $( [4-Dec-2007] $)
  $}

  ${
    $d x y z S $.  $d x y z T $.  $d x y z U $.  $d x y z X $.
    lnocoi.l $e |- L = ( U LnOp W ) $.
    lnocoi.m $e |- M = ( W LnOp X ) $.
    lnocoi.n $e |- N = ( U LnOp X ) $.
    lnocoi.u $e |- U e. NrmCVec $.
    lnocoi.w $e |- W e. NrmCVec $.
    lnocoi.x $e |- X e. NrmCVec $.
    lnocoi.s $e |- S e. L $.
    lnocoi.t $e |- T e. M $.
    $( The composition of two linear operators is linear. $)
    lnocoi $p |- ( T o. S ) e. N $=
      ( wcel cfv co eqid vx vy vz ccom cba wf cv cns cpv wceq wral cc cnv wa wb
      islno mp2an lnof mp3an fco w3a 3pm3.2i lnolin mpan fveq2d ffvelrni syl3an
      id eqtrd nvscl mp3an1 nvgcl sylan 3impa wfun ffun ax-mp fvco3 mp3an12 syl
      3ad2ant2 opreq2d 3ad2ant3 opreq12d 3eqtr4d rgen3 mpbir2an ) BAUDZFQZCUERZ
      HUERZWHUFZUAUGZUBUGZCUHRZSZUCUGZCUIRZSZWHRZWMWNWHRZHUHRZSZWQWHRZHUIRZSZUJ
      ZUCWJUKUBWJUKUAULUKZCUMQZHUMQZWIWLXHUNUOLNUAUBUCWOXBWHCWRXEFHWJWKWJTZWKTZ
      WRTZXETZWOTZXBTZKUPUQGUERZWKBUFZWJXQAUFZWLGUMQZXJBEQZXRMNPBGEHXQWKXQTZXLJ
      URUSZXIXTADQZXSLMOACDGWJXQXKYBIURUSZWJXQWKBAUTUQXGUAUBUCULWJWJWMULQZWNWJQ
      ZWQWJQZVAZWSARZBRZWMWNARZBRZXBSZWQARZBRZXESZWTXFYIYKWMYLGUHRZSYOGUIRZSZBR
      ZYQYIYJYTBXIXTYDVAYIYJYTUJXIXTYDLMOVBWMWNWQWOYRACWRYSDGWJXQXKYBXMYSTZXOYR
      TZIVCVDVEYFYFYGYLXQQZYHYOXQQZUUAYQUJZYFVHWJXQWNAYEVFWJXQWQAYEVFXTXJYAVAYF
      UUDUUEVAUUFXTXJYAMNPVBWMYLYOYRXBBGYSXEEHXQWKYBXLUUBXNUUCXPJVCVDVGVIYIWSWJ
      QZWTYKUJZYFYGYHUUGYFYGUNWPWJQZYHUUGXIYFYGUUILWMWNWOCWJXKXOVJVKXIUUIYHUUGL
      WPWQCWRWJXKXMVLVKVMVNBVOZXSUUGUUHXRUUJYCXQWKBVPVQZYEWJXQWSBAVRVSVTYIXCYNX
      DYPXEYIXAYMWMXBYGYFXAYMUJZYHUUJXSYGUULUUKYEWJXQWNBAVRVSWAWBYHYFXDYPUJZYGU
      UJXSYHUUMUUKYEWJXQWQBAVRVSWCWDWEWFWG $.
      $( [19-Nov-2013] $) $( [12-Jan-2008] $)
  $}

  ${
    lnoadd.1 $e |- X = ( BaseSet ` U ) $.
    lnoadd.5 $e |- G = ( +v ` U ) $.
    lnoadd.6 $e |- H = ( +v ` W ) $.
    lnoadd.7 $e |- L = ( U LnOp W ) $.
    $( Addition property of a linear operator. $)
    lnoadd $p |- ( ( ( U e. NrmCVec /\ W e. NrmCVec /\ T e. L ) /\
  ( A e. X /\ B e. X ) ) -> ( T ` ( A G B ) ) = ( ( T ` A ) H ( T ` B ) ) ) $=
      ( cnv wcel c1 cfv co wceq eqid w3a wa cns ax-1cn cba lnolin mp3anr1 simp1
      cc simpl nvsid syl2an opreq1d fveq2d simpl2 lnof ffvelrn syl11anc 3eqtr3d
      wf ) DNOZHNOZCGOZUAZAIOZBIOZUBZUBZPADUCQZRZBERZCQZPACQZHUCQZRZBCQZFRZABER
      ZCQVMVPFRVDPUIOVEVFVLVQSUDPABVIVNCDEFGHIHUEQZJVSTZKLVITZVNTZMUFUGVHVKVRCV
      HVJABEVDVAVEVJASVGVAVBVCUHVEVFUJZAVIDIJWAUKULUMUNVHVOVMVPFVHVBVMVSOZVOVMS
      VAVBVCVGUOVDIVSCUTVEWDVGCDGHIVSJVTMUPWCIVSACUQULVMVNHVSVTWBUKURUMUS $.
      $( [19-Nov-2013] $) $( [7-Dec-2007] $)
  $}

  ${
    lnosub.1 $e |- X = ( BaseSet ` U ) $.
    lnosub.5 $e |- M = ( -v ` U ) $.
    lnosub.6 $e |- N = ( -v ` W ) $.
    lnosub.7 $e |- L = ( U LnOp W ) $.
    $( Subtraction property of a linear operator. $)
    lnosub $p |- ( ( ( U e. NrmCVec /\ W e. NrmCVec /\ T e. L ) /\
  ( A e. X /\ B e. X ) ) -> ( T ` ( A M B ) ) = ( ( T ` A ) N ( T ` B ) ) ) $=
      ( cnv wcel wa cfv co wceq eqid w3a c1 cns cpv cc ax-1cn negcli cba lnolin
      cneg mp3anr1 ancom2s nvmval2 3expb 3ad2antl1 fveq2d wf lnof simpl ffvelrn
      simpl2 syl2an simpr syl111anc 3eqtr4d ) DNOZHNOZCEOZUAZAIOZBIOZPZPZUBUJZB
      DUCQZRADUDQZRZCQZVNBCQZHUCQZRACQZHUDQZRZABFRZCQWAVSGRZVIVKVJVRWCSZVIVNUEO
      VKVJWFUBUFUGVNBAVOVTCDVPWBEHIHUHQZJWGTZVPTZWBTZVOTZVTTZMUIUKULVMWDVQCVFVG
      VLWDVQSZVHVFVJVKWMABVODVPFIJWIWKKUMUNUOUPVMVGWAWGOZVSWGOZWEWCSVFVGVHVLVAV
      IIWGCUQZVJWNVLCDEHIWGJWHMURZVJVKUSIWGACUTVBVIWPVKWOVLWQVJVKVCIWGBCUTVBWAV
      SVTHWBGWGWHWJWLLUMVDVE $.
      $( [19-Nov-2013] $) $( [7-Dec-2007] $)
  $}

  ${
    lnomul.1 $e |- X = ( BaseSet ` U ) $.
    lnomul.5 $e |- R = ( .s ` U ) $.
    lnomul.6 $e |- S = ( .s ` W ) $.
    lnomul.7 $e |- L = ( U LnOp W ) $.
    $( Scalar multiplication property of a linear operator. $)
    lnomul $p |- ( ( ( U e. NrmCVec /\ W e. NrmCVec /\ T e. L ) /\
       ( A e. CC /\ B e. X ) ) -> ( T ` ( A R B ) ) = ( A S ( T ` B ) ) ) $=
      ( cnv wcel co cfv wceq eqid syl11anc w3a cc cn0v cpv simprl simprr simpl1
      wa simpl nvzcl syl cba lnolin syl13anc nvscl syl111anc nv0rid fveq2d lno0
      opreq2d adantr simpl2 wf lnof ffvelrn eqtrd 3eqtr3d ) FNOZHNOZEGOZUAZAUBO
      ZBIOZUHZUHZABCPZFUCQZFUDQZPZEQZABEQZDPZVQEQZHUDQZPZVPEQWBVOVKVLVMVQIOZVTW
      ERVKVNUIVKVLVMUEZVKVLVMUFZVOVHWFVHVIVJVNUGZFIVQJVQSZUJUKABVQCDEFVRWDGHIHU
      LQZJWKSZVRSZWDSZKLMUMUNVOVSVPEVOVHVPIOZVSVPRWIVOVHVLVMWOWIWGWHABCFIJKUOUP
      VPFVRIVQJWMWJUQTURVOWEWBHUCQZWDPZWBVKWEWQRVNVKWCWPWBWDVQEFGHIWKWPJWLWJWPS
      ZMUSUTVAVOVIWBWKOZWQWBRVHVIVJVNVBZVOVIVLWAWKOZWSWTWGVOIWKEVCZVMXAVKXBVNEF
      GHIWKJWLMVDVAWHIWKBEVETAWADHWKWLLUOUPWBHWDWKWPWLWNWRUQTVFVG $.
      $( [19-Nov-2013] $) $( [5-Dec-2007] $)
  $}

  ${
    $d x y A $.  $d x y C $.  $d x y D $.  $d x y J $.  $d x y K $.
    $d x y P $.  $d x y T $.  $d x y U $.  $d x y W $.  $d x y X $.
    nvcnpi3.1 $e |- X = ( BaseSet ` U ) $.
    nvcnpi3.m $e |- M = ( norm ` U ) $.
    nvcnpi3.n $e |- N = ( norm ` W ) $.
    nvcnpi3.r $e |- R = ( -v ` U ) $.
    nvcnpi3.8 $e |- C = ( IndMet ` U ) $.
    nvcnpi3.d $e |- D = ( IndMet ` W ) $.
    nvcnpi3.j $e |- J = ( MetOpen ` C ) $.
    nvcnpi3.k $e |- K = ( MetOpen ` D ) $.
    nvcnpi3.l $e |- L = ( U LnOp W ) $.
    nvcnpi3.t $e |- T e. L $.
    $( MAY BE REVISED - delete this if not used $)
    $( Epsilon-delta property of a linear operator continuous at a point.) $)
    nvcnpi3 $p |- ( ( ( U e. NrmCVec /\ W e. NrmCVec /\ P e. X ) /\ ( T e.
  ( ( J CnP K ) ` P ) /\ A e. RR /\ 0 < A ) ) -> E. x e. RR ( 0 < x /\
  A. y e. X ( ( M ` ( y R P ) ) <_ x -> ( N ` ( T ` ( y R P ) ) ) <_ A ) ) ) $=
      ( cnv wcel w3a ccnp co cfv cr cc0 clt wbr wa cle wral wrex cdm cme imsmet
      cv 3ad2ant1 3ad2ant2 imsba eleq2d biimpa 3adant2 3jca eqid metcnpi3 sylan
      wi raleqdv anbi2d rexbidv adantr mpbird wceq imsdval 3expb adantlr breq1d
      wb cnsb simplr wf mp3an3 ffvelrn adantrr adantrl syl111anc lnosub mp3anl3
      cba lnof fveq2d eqtr4d imbi12d exp43 com34 3imp1 ralbidva mpbid ) IUGUHZO
      UGUHZFPUHZUIZHFJKUJUKULUHCUMUHUNCUOUPUIZUQZUNAVDZUOUPZBVDZFDUKZXMURUPZXOH
      ULZFHULZEUKZCURUPZVOZBPUSZUQZAUMUTZXNXOFGUKZMULZXMURUPZYFHULZNULZCURUPZVO
      ZBPUSZUQZAUMUTZXLYEXNYBBDVAVAZUSZUQZAUMUTZXJDVBUHZEVBUHZFYPUHZUIXKYSXJYTU
      UAUUBXGXHYTXIDIUAVCVEXHXGUUAXIEOUBVCVFXGXIUUBXHXGXIUUBXGPYPFDIPQUAVGZVHVI
      VJVKABCDEFHJKYPEVAVAZYPVLUCUUDVLUDVMVNXJYEYSWFZXKXGXHUUEXIXGYDYRAUMXGYCYQ
      XNXGYBBPYPUUCVPVQVRVEVSVTXJYEYOWFXKXJYDYNAUMXJYCYMXNXJYBYLBPXGXHXIXOPUHZY
      BYLWFZXGXHUUFXIUUGXGXHUUFXIUUGXGXHUQZUUFXIUQZUQZXQYHYAYKUUJXPYGXMURXGUUIX
      PYGWAZXHXGUUFXIUUKXOFDIGMPQTRUAWBWCWDWEUUJXTYJCURUUJXTXRXSOWGULZUKZNULZYJ
      UUJXHXROWQULZUHZXSUUOUHZXTUUNWAXGXHUUIWHUUHUUFUUPXIUUHPUUOHWIZUUFUUPXGXHH
      LUHZUURUFHILOPUUOQUUOVLZUEWRWJZPUUOXOHWKVNWLUUHXIUUQUUFUUHUURXIUUQUVAPUUO
      FHWKVNWMXRXSEOUULNUUOUUTUULVLZSUBWBWNUUJYIUUMNXGXHUUSUUIYIUUMWAUFXOFHILGU
      ULOPQTUVBUEWOWPWSWTWEXAXBXCXDXEVQVRVSXF $.
      $( [9-Aug-2008] $) $( [16-Dec-2007] $)

    $( MAY BE REVISED - delete this if not used $)
    $( Epsilon-delta property of a linear operator continuous at a point.) $)
    nvcnpi4 $p |- ( ( ( U e. NrmCVec /\ W e. NrmCVec /\ P e. X ) /\ ( T e. ( (
    J CnP K ) ` P ) /\ A e. RR /\ 0 < A ) ) -> E. x e. RR ( 0 < x /\ A. y e. X
    ( ( M ` ( P R y ) ) <_ x -> ( N ` ( T ` ( P R y ) ) ) <_ A ) ) ) $=
      ( cnv wcel w3a ccnp co cfv cr cc0 clt wbr wa cle wral wrex cdm cme imsmet
      cv 3ad2ant1 3ad2ant2 imsba eleq2d biimpa 3adant2 3jca eqid metcnpi4 sylan
      wi raleqdv anbi2d rexbidv adantr mpbird wceq imsdval 3expb adantlr breq1d
      wb cnsb simplr wf mp3an3 ffvelrn adantrr adantrl syl111anc lnosub mp3anl3
      cba lnof fveq2d eqtr4d imbi12d exp43 3imp1 ralbidva mpbid ) IUGUHZOUGUHZF
      PUHZUIZHFJKUJUKULUHCUMUHUNCUOUPUIZUQZUNAVDZUOUPZFBVDZDUKZXLURUPZFHULZXNHU
      LZEUKZCURUPZVOZBPUSZUQZAUMUTZXMFXNGUKZMULZXLURUPZYEHULZNULZCURUPZVOZBPUSZ
      UQZAUMUTZXKYDXMYABDVAVAZUSZUQZAUMUTZXIDVBUHZEVBUHZFYOUHZUIXJYRXIYSYTUUAXF
      XGYSXHDIUAVCVEXGXFYTXHEOUBVCVFXFXHUUAXGXFXHUUAXFPYOFDIPQUAVGZVHVIVJVKABCD
      EFHJKYOEVAVAZYOVLUCUUCVLUDVMVNXIYDYRWFZXJXFXGUUDXHXFYCYQAUMXFYBYPXMXFYABP
      YOUUBVPVQVRVEVSVTXIYDYNWFXJXIYCYMAUMXIYBYLXMXIYAYKBPXFXGXHXNPUHZYAYKWFZXF
      XGXHUUEUUFXFXGUQZXHUUEUQZUQZXPYGXTYJUUIXOYFXLURXFUUHXOYFWAZXGXFXHUUEUUJFX
      NDIGMPQTRUAWBWCWDWEUUIXSYICURUUIXSXQXROWGULZUKZNULZYIUUIXGXQOWQULZUHZXRUU
      NUHZXSUUMWAXFXGUUHWHUUGXHUUOUUEUUGPUUNHWIZXHUUOXFXGHLUHZUUQUFHILOPUUNQUUN
      VLZUEWRWJZPUUNFHWKVNWLUUGUUEUUPXHUUGUUQUUEUUPUUTPUUNXNHWKVNWMXQXREOUUKNUU
      NUUSUUKVLZSUBWBWNUUIYHUULNXFXGUURUUHYHUULWAUFFXNHILGUUKOPQTUVAUEWOWPWSWTW
      EXAXBXCXDVQVRVSXE $.
      $( [16-Dec-2007] $)
  $}

  ${
    nvo00.1 $e |- X = ( BaseSet ` U ) $.
    $( Two ways to express a zero operator. $)
    nvo00 $p |- ( ( U e. NrmCVec /\ T : X --> Y ) ->
                  ( T = ( X X. { Z } ) <-> ran T = { Z } ) ) $=
      ( wf cnv wcel csn cxp wceq crn wb wfn c0 wne ffn cn0v cfv eqid nvzcl ne0i
      syl fconst5 syl2an ancoms ) CDAGZBHIZACEJZKLAMUJLNZUHACOCPQZUKUICDARUIBST
      ZCIULBCUMFUMUAUBCUMUCUDCEAUEUFUG $.
      $( [27-Nov-2007] $)
  $}

  ${
    $d t u w L $.  $d t w M $.  $d t x y z T $.  $d t u w x z U $.
    $d t w x z W $.  $d t u w z X $.  $d t w x Y $.
    nmofval.1 $e |- X = ( BaseSet ` U ) $.
    nmofval.2 $e |- Y = ( BaseSet ` W ) $.
    nmofval.3 $e |- L = ( norm ` U ) $.
    nmofval.4 $e |- M = ( norm ` W ) $.
    nmofval.6 $e |- N = ( U normOp W ) $.
    $( The operator norm function. $)
    nmofval $p |- ( ( U e. NrmCVec /\ W e. NrmCVec ) -> N =
       ( t e. ( Y ^m X ) |-> sup ( { x | E. z e. X
         ( ( L ` z ) <_ 1 /\ x = ( M ` ( t ` z ) ) ) } , RR* , < ) ) ) $=
      ( cmap cv cfv cba cnm vu vw cnv wcel wa cnmo co cle wbr wceq wrex cab cxr
      clt csup cmpt fveq2 syl6eqr opreq2d fveq1d breq1d anbi1d rexeqbidv abbidv
      c1 supeq1d mpteq12dv opreq1d eqeq2d anbi2d rexbidv df-nmo cvv oprex ax-mp
      mptexg ovmpt2 syl5eq ) DUCUDHUCUDUEGDHUFUGCJIPUGZBQZERZVEUHUIZAQZVTCQRZFR
      ZUJZUEZBIUKZAULZUMUNUOZUPZOUAUBDHUCUCCUBQZSRZUAQZSRZPUGZVTWNTRZRZVEUHUIZW
      CWDWLTRZRZUJZUEZBWOUKZAULZUMUNUOZUPWKUFCWMIPUGZWBXBUEZBIUKZAULZUMUNUOZUPW
      NDUJZCWPXFXGXKXLWOIWMPXLWODSRIWNDSUQKURZUSXLUMXEXJUNXLXDXIAXLXCXHBWOIXMXL
      WSWBXBXLWRWAVEUHXLVTWQEXLWQDTREWNDTUQMURUTVAVBVCVDVFVGWLHUJZCXGXKVSWJXNWM
      JIPXNWMHSRJWLHSUQLURVHXNUMXJWIUNXNXIWHAXNXHWGBIXNXBWFWBXNXAWEWCXNWDWTFXNW
      THTRFWLHTUQNURUTVIVJVKVDVFVGABUBUACVLVSVMUDWKVMUDJIPVNCVSWJVMVPVOVQVR $.
      $( [16-Nov-2013] $) $( [6-Nov-2007] $)

    $( The operator norm function. $)
    nmoval $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T : X --> Y ) ->
      ( N ` T ) = sup ( { x | E. z e. X
        ( ( L ` z ) <_ 1 /\ x = ( M ` ( T ` z ) ) ) } , RR* , < ) ) $=
      ( vt cfv wceq cxr clt cnv wcel wf cv c1 cle wbr wa wrex cab csup cmap cba
      co cvv fvex eqeltri elmap cmpt nmofval fveq1d fveq1 fveq2d eqeq2d rexbidv
      anbi2d abbidv supeq1d eqid xrltso supex fvmpt sylan9eq sylan2br 3impa ) D
      UAUBZHUAUBZIJCUCZCGQZBUDZEQUEUFUGZAUDZVTCQZFQZRZUHZBIUIZAUJZSTUKZRZVRVPVQ
      UHZCJIULUNZUBZWJJICJHUMQUOLHUMUPUQIDUMQUOKDUMUPUQURWKWMVSCPWLWAWBVTPUDZQZ
      FQZRZUHZBIUIZAUJZSTUKZUSZQWIWKCGXBABPDEFGHIJKLMNOUTVAPCXAWIWLXBWNCRZSWTWH
      TXCWSWGAXCWRWFBIXCWQWEWAXCWPWDWBXCWOWCFVTWNCVBVCVDVFVEVGVHXBVISWHTVJVKVLV
      MVNVO $.
      $( [16-Nov-2013] $) $( [27-Nov-2007] $)
  $}

  ${
    $d x z T $.  $d x z W $.  $d x z X $.  $d x z Y $.
    nmosetre.2 $e |- Y = ( BaseSet ` W ) $.
    nmosetre.4 $e |- N = ( norm ` W ) $.
    $( The set in the supremum of the operator norm definition ~ df-nmo is a
       set of reals. $)
    nmosetre $p |- ( ( W e. NrmCVec /\ T : X --> Y ) -> {
     x | E. z e. X ( ( M ` z ) <_ 1 /\ x = ( N ` ( T ` z ) ) ) } C_ RR ) $=
      ( cnv wcel wf wa cv cfv c1 cle wbr cr wceq wrex ffvelrn nvcl sylan2 eleq1
      anassrs syl5ibr impcom adantrl exp31 r19.23adv abssdv ) FKLZGHCMZNZBOZDPQ
      RSZAOZUQCPZEPZUAZNZBGUBATUPVCUSTLZBGUPUQGLZVCVDUPVENZVBVDURVBVFVDVFVDVBVA
      TLZUNUOVEVGUOVENUNUTHLVGGHUQCUCUTFEHIJUDUEUGUSVATUFUHUIUJUKULUM $.
      $( [13-Nov-2007] $)
  $}

  ${
    $d x y M $.  $d x y N $.  $d x y T $.  $d x y X $.  $d x y Z $.
    nmosetn0.1 $e |- X = ( BaseSet ` U ) $.
    nmosetn0.5 $e |- Z = ( 0vec ` U ) $.
    nmosetn0.4 $e |- M = ( norm ` U ) $.
    $( The set in the supremum of the operator norm definition ~ df-nmo is
       nonempty. $)
    nmosetn0 $p |- ( U e. NrmCVec -> ( N ` ( T ` Z ) ) e. { x | E. y e. X (
       ( M ` y ) <_ 1 /\ x = ( N ` ( T ` y ) ) ) } ) $=
      ( wcel cv cfv c1 cle wbr wceq wa wrex cnv cab nvzcl cc0 nvz0 0re 1re lt01
      ltleii syl6eqbr jctir fveq2 breq1d fveq2d eqeq2d anbi12d rcla4ev syl11anc
      eqid fvex eqeq1 anbi2d rexbidv elab sylibr ) DUALZBMZENZOPQZHCNZFNZVGCNZF
      NZRZSZBGTZVKVIAMZVMRZSZBGTZAUBLVFHGLHENZOPQZVKVKRZSZVPDGHIJUCVFWBWCVFWAUD
      OPDEHJKUEUDOUFUGUHUIUJVKUSUKVOWDBHGVGHRZVIWBVNWCWEVHWAOPVGHEULUMWEVMVKVKW
      EVLVJFVGHCULUNUOUPUQURVTVPAVKVJFUTVQVKRZVSVOBGWFVRVNVIVQVKVMVAVBVCVDVE $.
      $( [8-Dec-2007] $)
  $}

  ${
    $d x z T $.  $d x z U $.  $d x z W $.  $d x z X $.  $d x z Y $.
    nmoxr.1 $e |- X = ( BaseSet ` U ) $.
    nmoxr.2 $e |- Y = ( BaseSet ` W ) $.
    nmoxr.3 $e |- N = ( U normOp W ) $.
    $( The norm of an operator is an extended real. $)
    nmoxr $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T : X --> Y ) ->
              ( N ` T ) e. RR* ) $=
      ( vz vx cnv wcel wf cfv cv cnm wa cxr eqid w3a cle wbr wceq wrex cab csup
      c1 clt nmoval wss cr nmosetre ressxr syl6ss supxrcl syl 3adant1 eqeltrd )
      BLMZDLMZEFANZUAACOJPZBQOZOUHUBUCKPVCAODQOZOUDRJEUEKUFZSUIUGZSKJABVDVECDEF
      GHVDTVETZIUJVAVBVGSMZUTVAVBRZVFSUKVIVJVFULSKJAVDVEDEFHVHUMUNUOVFUPUQURUS
      $.
      $( [27-Nov-2007] $)

    $( The norm of an operator is nonnegative. $)
    nmoge0 $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T : X --> Y ) ->
                  0 <_ ( N ` T ) ) $=
      ( vz vx wcel cc0 cfv cle wbr eqid wa cxr cr cnv wf w3a cn0v simp2 ffvelrn
      cnm nvzcl sylan2 ancoms 3adant2 nvge0 syl11anc wceq wrex cab clt csup wss
      cv c1 nmosetre ressxr sstr sylancl nmosetn0 supxrub syl2an 3impa breqtrrd
      3comr nmoval wi 0re rexr ax-mp a1i nvcl syl nmoxr xrletr syl111anc mp2and
      ) BUALZDUALZEFAUBZUCZMBUDNZANZDUGNZNZOPZWKACNZOPZMWMOPZWGWEWIFLZWLWDWEWFU
      EZWDWFWPWEWFWDWPWDWFWHELWPBEWHGWHQZUHEFWHAUFUIUJUKZWIDWJFHWJQZULUMWGWKJUT
      ZBUGNZNVAOPKUTXAANWJNUNRJEUOKUPZSUQURZWMOWEWFWDWKXDOPZWEWFWDXEWEWFRZXCSUS
      ZWKXCLXEWDXFXCTUSTSUSXGKJAXBWJDEFHWTVBVCXCTSVDVEKJABXBWJEWHGWRXBQZVFXCWKV
      GVHVIVKKJABXBWJCDEFGHXHWTIVLVJWGMSLZWKSLZWMSLWLWNRWOVMXIWGMTLXIVNMVOVPVQW
      GWKTLZXJWGWEWPXKWQWSWIDWJFHWTVRUMWKVOVSABCDEFGHIVTMWKWMWAWBWC $.
      $( [8-Dec-2007] $)

    $( The norm of an operator is either real or plus infinity. $)
    nmorepnf $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T : X --> Y ) ->
                   ( ( N ` T ) e. RR <-> ( N ` T ) =/= +oo ) ) $=
      ( vz vx cnv wcel cv cnm cfv cr cpnf wne eqid wf w3a cle wbr wceq wrex cab
      c1 wa cxr clt csup wb wss nmosetre cn0v nmosetn0 ne0i syl supxrre2 syl2an
      c0 ancoms 3impb nmoval eleq1d neeq1d 3bitr4d ) BLMZDLMZEFAUAZUBZJNZBOPZPU
      HUCUDKNVMAPDOPZPUEUIJEUFKUGZUJUKULZQMZVQRSZACPZQMVTRSVIVJVKVRVSUMZVJVKUIZ
      VIWAWBVPQUNVPVBSZWAVIKJAVNVODEFHVOTZUOVIBUPPZAPVOPZVPMWCKJABVNVOEWEGWETVN
      TZUQVPWFURUSVPUTVAVCVDVLVTVQQKJABVNVOCDEFGHWGWDIVEZVFVLVTVQRWHVGVH $.
      $( [8-Dec-2007] $)

    $( The norm of any operator is real iff it is less than plus infinity. $)
    nmoreltpnf $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T : X --> Y ) ->
                   ( ( N ` T ) e. RR <-> ( N ` T ) < +oo ) ) $=
      ( cnv wcel wf w3a cfv cr cpnf wne clt wbr nmorepnf cxr wceq wn wb nltpnft
      nmoxr syl necon2abid bitr4d ) BJKDJKEFALMZACNZOKUKPQUKPRSZABCDEFGHITUJULU
      KPUJUKUAKUKPUBULUCUDABCDEFGHIUFUKUEUGUHUI $.
      $( [8-Dec-2007] $)

    $( The norm of an operator is greater than minus infinity. $)
    nmogtmnf $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T : X --> Y ) ->
                  -oo < ( N ` T ) ) $=
      ( cnv wcel wf w3a cfv cr cpnf wceq wn wb wo cmnf clt wbr wne df-ne syl6bb
      nmorepnf xor3 wa xor2 simplbi sylbir mnfltxr 3syl ) BJKDJKEFALMZACNZOKZUP
      PQZRZSZUQURTZUAUPUBUCUOUQUPPUDUSABCDEFGHIUGUPPUEUFUTUQURSRZVAUQURUHVBVAUQ
      URUIRUQURUJUKULUPUMUN $.
      $( [8-Dec-2007] $)
  $}

  ${
    $d x y A $.  $d x y L $.  $d x y M $.  $d x y T $.  $d x y U $.
    $d x y W $.  $d x y X $.  $d x y Y $.
    nmolb.1 $e |- X = ( BaseSet ` U ) $.
    nmolb.2 $e |- Y = ( BaseSet ` W ) $.
    nmolb.l $e |- L = ( norm ` U ) $.
    nmolb.m $e |- M = ( norm ` W ) $.
    nmolb.3 $e |- N = ( U normOp W ) $.
    $( A lower bound for an operator norm. $)
    nmolb $p |- ( ( ( U e. NrmCVec /\ W e. NrmCVec /\ T : X --> Y ) /\
         ( A e. X /\ ( L ` A ) <_ 1 ) ) -> ( M ` ( T ` A ) ) <_ ( N ` T ) ) $=
      ( vy vx cfv cle wa wceq cnv wcel wf w3a c1 wbr cv wrex cab cxr clt wss cr
      nmosetre ressxr syl6ss 3adant1 fveq2 breq1d fveq2d eqeq2d anbi12d biantru
      csup eqid syl6bbr rcla4ev fvex eqeq1 anbi2d rexbidv sylibr supxrub syl2an
      elab nmoval adantr breqtrrd ) CUAUBZGUAUBZHIBUCZUDZAHUBADQZUERUFZSZSABQZE
      QZOUGZDQZUERUFZPUGZWHBQZEQZTZSZOHUHZPUIZUJUKVDZBFQZRWBWQUJULZWGWQUBZWGWRR
      UFWEVTWAWTVSVTWASWQUMUJPOBDEGHIKMUNUOUPUQWEWJWGWMTZSZOHUHZXAXCWDOAHWHATZX
      CWDWGWGTZSWDXEWJWDXBXFXEWIWCUERWHADURUSXEWMWGWGXEWLWFEWHABURUTVAVBXFWDWGV
      EVCVFVGWPXDPWGWFEVHWKWGTZWOXCOHXGWNXBWJWKWGWMVIVJVKVOVLWQWGVMVNWBWSWRTWEP
      OBCDEFGHIJKLMNVPVQVR $.
      $( [8-Dec-2007] $)
  $}

  ${
    $d w x z A $.  $d f k r w x y z L $.  $d f k r w x y z M $.  $d k r y N $.
    $d f k r w x y z T $.  $d y z U $.  $d y z W $.  $d f k r w x y z X $.
    $d k r x y z Y $.
    nmoubi.1 $e |- X = ( BaseSet ` U ) $.
    nmoubi.y $e |- Y = ( BaseSet ` W ) $.
    nmoubi.l $e |- L = ( norm ` U ) $.
    nmoubi.m $e |- M = ( norm ` W ) $.
    nmoubi.3 $e |- N = ( U normOp W ) $.
    nmoubi.u $e |- U e. NrmCVec $.
    nmoubi.w $e |- W e. NrmCVec $.
    $( An upper bound for an operator norm. $)
    nmoubi $p |- ( ( T : X --> Y /\ A e. RR* /\ A. x e. X ( ( L ` x )
             <_ 1 -> ( M ` ( T ` x ) ) <_ A ) ) -> ( N ` T ) <_ A ) $=
      ( vz cfv cle vy vw wf cxr wcel cv c1 wbr wi wral w3a wceq wa wrex cab clt
      csup cnv nmoval mp3an12 3ad2ant1 wss cr nmosetre mpan ressxr syl6ss simp2
      visset eqeq1 anbi2d rexbidv elab fveq2 breq1d fveq2d imbi12d rcla4cv syl8
      biimprcd r19.23adv syl5bi r19.21aiv 3ad2ant3 supxrleub syl111anc eqbrtrd
      breq1 imp4a ) IJCUCZBUDUEZAUFZESZUGTUHZWLCSZFSZBTUHZUIZAIUJZUKZCGSZRUFZES
      ZUGTUHZUAUFZXBCSZFSZULZUMZRIUNZUAUOZUDUPUQZBTWJWKXAXLULZWSDURUEHURUEZWJXM
      PQUARCDEFGHIJKLMNOUSUTVAWTXKUDVBZWKUBUFZBTUHZUBXKUJZXLBTUHWJWKXOWSWJXKVCU
      DXNWJXKVCVBQUARCEFHIJLNVDVEVFVGVAWJWKWSVHWSWJXRWKWSXQUBXKXPXKUEXDXPXGULZU
      MZRIUNZWSXQXJYAUAXPUBVIXEXPULZXIXTRIYBXHXSXDXEXPXGVJVKVLVMWSXTXQRIWSXBIUE
      ZXDXSXQWSYCXDXGBTUHZXSXQUIWRXDYDUIAXBIWLXBULZWNXDWQYDYEWMXCUGTWLXBEVNVOYE
      WPXGBTYEWOXFFWLXBCVNVPVOVQVRXSXQYDXPXGBTWHVTVSWIWAWBWCWDUBXKBWEWFWG $.
      $( [11-Dec-2007] $)

    $( An upper bound for an operator norm. $)
    nmoub3i $p |- ( ( T : X --> Y /\ A e. RR /\ A. x e. X
  ( M ` ( T ` x ) ) <_ ( A x. ( L ` x ) ) ) -> ( N ` T ) <_ ( abs ` A ) ) $=
      ( wcel cle wbr wf cr cv cfv cmul co wral cabs wa c1 cnv nvcl mpan remulcl
      wi sylan2 adantr cc recn abscl syl syl2an ad2antrr cc0 simpl nvge0 adantl
      jca leabs lemul1a syl31anc w3a 1re absge0 3jca lemul2a sylan recnd mulid1
      wceq breqtrd letrd adantlll ffvelrn sylancr adantlr adantll ad2antlr letr
      a1i syl111anc mpan2d ex com23 ralimdva imp cxr rexr nmoubi syl3an2 syldan
      3expa 3impa ) IJCUAZBUBRZAUCZCUDZFUDZBXFEUDZUEUFZSTZAIUGZCGUDBUHUDZSTZXDX
      EUIZXLXIUJSTZXHXMSTZUOZAIUGZXNXOXLXSXOXKXRAIXOXFIRZUIZXPXKXQYAXPXKXQUOYAX
      PUIXKXJXMSTZXQXEXTXPYBXDXEXTUIZXPUIZXJXMXIUEUFZXMYCXJUBRZXPXTXEXIUBRZYFDU
      KRZXTYGPXFDEIKMULUMZBXIUNUPZUQYCYEUBRZXPXEXMUBRZYGYKXTXEBURRZYLBUSZBUTVAZ
      YIXMXIUNVBUQXEYLXTXPYOVCYCXJYESTZXPYCXEYLYGVDXISTZUIZBXMSTZYPXEXTVEXEYLXT
      YOUQZXTYRXEXTYGYQYIYHXTYQPXFDEIKMVFUMVHVGXEYSXTBVIUQBXMXIVJVKUQYDYEXMUJUE
      UFZXMSYCYGUJUBRZYLVDXMSTZUIZVLXPYEUUASTYCYGUUBUUDXTYGXEYIVGUUBYCVMWJYCYLU
      UCYTXEUUCXTXEYMUUCYNBVNVAUQVHVOXIUJXMVPVQXEUUAXMVTZXTXPXEXMURRUUEXEXMYOVR
      XMVSVAVCWAWBWCYAXKYBUIXQUOZXPYAXHUBRZYFYLUUFXDXTUUGXEXDXTUIHUKRXGJRUUGQIJ
      XFCWDXGHFJLNULWEWFXEXTYFXDYJWGXEYLXDXTYOWHXHXJXMWIWKUQWLWMWNWOWPXDXEXSXNX
      EXDXMWQRZXSXNXEYLUUHYOXMWRVAAXMCDEFGHIJKLMNOPQWSWTXBXAXC $.
      $( [12-Dec-2007] $)

    $( An upper bound for an operator norm. $)
    nmoub2i $p |- ( ( T : X --> Y /\ ( A e. RR /\ 0 <_ A ) /\ A. x e. X
  ( M ` ( T ` x ) ) <_ ( A x. ( L ` x ) ) ) -> ( N ` T ) <_ A ) $=
      ( cle wbr cfv wf cr wcel cc0 wa cv cmul co wral w3a cabs nmoub3i 3adant2r
      wceq absid 3ad2ant2 breqtrd ) IJCUAZBUBUCZUDBRSZUEZAUFZCTFTBVBETUGUHRSAIU
      IZUJCGTZBUKTZBRURUSVCVDVERSUTABCDEFGHIJKLMNOPQULUMVAURVEBUNVCBUOUPUQ $.
      $( [11-Dec-2007] $)

    $( Two ways to express that an operator is bounded. $)
    nmobndi $p |- ( T : X --> Y -> ( ( N ` T ) e. RR <->
     E. r e. RR A. y e. X ( ( L ` y ) <_ 1 -> ( M ` ( T ` y ) ) <_ r ) ) ) $=
      ( cr wcel cle wf cfv cv c1 wbr wi wral wrex wa nmolb mp3anl1 mpanl1 exp32
      cnv r19.21aiv wceq breq2 imbi2d ralbidv rcla4ev sylan2 expcom w3a cxr clt
      cmnf nmoxr mp3an12 3ad2ant1 simp2 nmogtmnf nmoubi syl3an2 syl22anc 3expia
      rexr xrre r19.23adva impbid ) HIBUAZBFUBZRSZAUCZDUBUDTUEZWCBUBEUBZJUCZTUE
      ZUFZAHUGZJRUHZWBVTWJVTWBWDWEWATUEZUFZAHUGZWJVTWLAHVTWCHSZWDWKGUNSZVTWNWDU
      IZWKQCUNSZWOVTWPWKPWCBCDEFGHIKLMNOUJUKULUMUOWIWMJWARWFWAUPZWHWLAHWRWGWKWD
      WFWAWETUQURUSUTVAVBVTWIWBJRVTWFRSZWIWBVTWSWIVCWAVDSZWSVFWAVEUEZWAWFTUEZWB
      VTWSWTWIWQWOVTWTPQBCFGHIKLOVGVHVIVTWSWIVJVTWSXAWIWQWOVTXAPQBCFGHIKLOVKVHV
      IWSVTWFVDSWIXBWFVPAWFBCDEFGHIKLMNOPQVLVMWAWFVQVNVOVRVS $.
      $( [11-Jan-2008] $)

    $( Two ways two express that an operator is unbounded. $)
    nmounbi $p |- ( T : X --> Y -> ( ( N ` T ) = +oo <->
      A. r e. RR E. y e. X ( ( L ` y ) <_ 1 /\ r < ( M ` ( T ` y ) ) ) ) ) $=
      ( cfv cr wcel wf cv c1 cle wbr clt wa wrex wral cpnf wi wne wn nmobndi wb
      cnv nmorepnf mp3an12 ffvelrn nvcl sylancr lenlt sylan an32s imbi2d syl6bb
      imnan ralbidva ralnex rexbidva rexnal 3bitr3d necon4abid ) HIBUAZAUBZDRUC
      UDUEZJUBZVOBRZERZUFUEZUGZAHUHZJSUIZBFRZUJVNWDSTZVPVSVQUDUEZUKZAHUIZJSUHZW
      DUJULZWCUMZABCDEFGHIJKLMNOPQUNCUPTGUPTZVNWEWJUOPQBCFGHIKLOUQURVNWIWBUMZJS
      UHWKVNWHWMJSVNVQSTZUGZWHWAUMZAHUIWMWOWGWPAHWOVOHTZUGZWGVPVTUMZUKWPWRWFWSV
      PVNWQWNWFWSUOZVNWQUGZVSSTZWNWTXAWLVRITXBQHIVOBUSVRGEILNUTVAVSVQVBVCVDVEVP
      VTVGVFVHWAAHVIVFVJWBJSVKVFVLVM $.
      $( [11-Jan-2008] $)

    $( An unbounded operator determines an unbounded sequence.  (Revised by
       Mario Carneiro, 7-Apr-2013.) $)
    nmounbseqi $p |- ( ( T : X --> Y /\ ( N ` T ) = +oo ) ->
         E. f ( f : NN --> X /\ A. k e. NN ( ( L ` ( f ` k ) ) <_ 1 /\
          k < ( M ` ( T ` ( f ` k ) ) ) ) ) ) $=
      ( vy cfv cn wf cpnf wceq wa cv c1 cle wbr clt wrex cr wral nmounbi biimpa
      wex wcel nnre imim1i ralimi2 cba fvex eqeltri nnenom breq1d fveq2d breq2d
      cvv fveq2 anbi12d axcc4 3syl ) IJAUAZAGSUBUCZUDRUEZESZUFUGUHZDUEZVNASZFSZ
      UIUHZUDZRIUJZDUKULZWBDTULTICUEZUAVQWDSZESZUFUGUHZVQWEASZFSZUIUHZUDZDTULUD
      CUOVLVMWCRABEFGHIJDKLMNOPQUMUNWBWBDUKTVQTUPVQUKUPWBVQUQURUSWAWKRICDTIBUTS
      VGKBUTVAVBVCVNWEUCZVPWGVTWJWLVOWFUFUGVNWEEVHVDWLVSWIVQUIWLVRWHFVNWEAVHVEV
      FVIVJVK $.
      $( [7-Apr-2013] $) $( [11-Jan-2008] $)

    $( An unbounded operator determines an unbounded sequence. $)
    nmounbseqiOLD $p |- ( ( T : X --> Y /\ ( N ` T ) = +oo ) ->
         E. f ( f : NN --> X /\ A. k e. NN ( ( L ` ( f ` k ) ) <_ 1 /\
          k < ( M ` ( T ` ( f ` k ) ) ) ) ) ) $=
      ( vy cfv cn wf cpnf wceq wa cv c1 cle wbr clt wrex cr wral nmounbi biimpa
      wex wcel nnre imim1i ralimi2 nnex fveq2 breq1d fveq2d breq2d anbi12d ac6s
      3syl ) IJAUAZAGSUBUCZUDRUEZESZUFUGUHZDUEZVJASZFSZUIUHZUDZRIUJZDUKULZVRDTU
      LTICUEZUAVMVTSZESZUFUGUHZVMWAASZFSZUIUHZUDZDTULUDCUOVHVIVSRABEFGHIJDKLMNO
      PQUMUNVRVRDUKTVMTUPVMUKUPVRVMUQURUSVQWGDRTICUTVJWAUCZVLWCVPWFWHVKWBUFUGVJ
      WAEVAVBWHVOWEVMUIWHVNWDFVJWAAVAVCVDVEVFVG $.
      $( [3-Apr-2009] $) $( [11-Jan-2008] $)

    $( A bounded sequence determines a bounded operator.  (Revised by Mario
       Carneiro, 7-Apr-2013.) $)
    nmobndseqi $p |- ( ( T : X --> Y /\
         A. f ( ( f : NN --> X /\ A. k e. NN ( L ` ( f ` k ) ) <_ 1 ) ->
        E. k e. NN ( M ` ( T ` ( f ` k ) ) ) <_ k ) ) -> ( N ` T ) e. RR ) $=
      ( cn cfv wi vy wf cv c1 cle wbr wral wa wrex cr wcel impexp r19.35 imbi2i
      wal bitr4i albii wex cba cvv fvex eqeltri nnenom wceq fveq2 breq1d fveq2d
      wn imbi12d notbid axcc4 con3i dfrex2 alinexa dfral2 rexbii rexnal 3imtr4i
      bitri nnre anim1i reximi2 syl sylbi nmobndi syl5ibr imp ) IJAUBZRICUCZUBZ
      DUCZWISZESZUDUEUFZDRUGZUHWLASZFSZWKUEUFZDRUIZTZCUOZAGSUJUKZXAXBWHUAUCZESZ
      UDUEUFZXCASZFSZWKUEUFZTZUAIUGZDUJUIZXAWJWNWRTZDRUIZTZCUOZXKWTXNCWTWJWOWST
      ZTXNWJWOWSULXMXPWJWNWRDRUMUNUPUQXOXJDRUIZXKWJXLVHZDRUGZUHCURZVHZXIVHZUAIU
      IZDRUGZVHZXOXQYDXTYBXRUAICDRIBUSSUTKBUSVAVBVCXCWLVDZXIXLYFXEWNXHWRYFXDWMU
      DUEXCWLEVEVFYFXGWQWKUEYFXFWPFXCWLAVEVGVFVIVJVKVLXOWJXSVHZTZCUOYAXNYHCXMYG
      WJXLDRVMUNUQWJXSCVNVSXQYCVHZDRUIYEXJYIDRXIUAIVOVPYCDRVQVSVRXJXJDRUJWKRUKW
      KUJUKXJWKVTWAWBWCWDUAABEFGHIJDKLMNOPQWEWFWG $.
      $( [7-Apr-2013] $) $( [18-Jan-2008] $)

    $( A bounded sequence determines a bounded operator. $)
    nmobndseqiOLD $p |- ( ( T : X --> Y /\
         A. f ( ( f : NN --> X /\ A. k e. NN ( L ` ( f ` k ) ) <_ 1 ) ->
        E. k e. NN ( M ` ( T ` ( f ` k ) ) ) <_ k ) ) -> ( N ` T ) e. RR ) $=
      ( cn cfv cle vy wf cv c1 wbr wral wa wrex wi cr wcel impexp r19.35 imbi2i
      wal bitr4i albii nnex wceq breq1d fveq2d imbi12d ac6n nnre anim1i reximi2
      fveq2 syl sylbi nmobndi syl5ibr imp ) IJAUBZRICUCZUBZDUCZVNSZESZUDTUEZDRU
      FZUGVQASZFSZVPTUEZDRUHZUIZCUOZAGSUJUKZWFWGVMUAUCZESZUDTUEZWHASZFSZVPTUEZU
      IZUAIUFZDUJUHZWFVOVSWCUIZDRUHZUIZCUOZWPWEWSCWEVOVTWDUIZUIWSVOVTWDULWRXAVO
      VSWCDRUMUNUPUQWTWODRUHWPWNWQDUARICURWHVQUSZWJVSWMWCXBWIVRUDTWHVQEVGUTXBWL
      WBVPTXBWKWAFWHVQAVGVAUTVBVCWOWODRUJVPRUKVPUJUKWOVPVDVEVFVHVIUAABEFGHIJDKL
      MNOPQVJVKVL $.
      $( [18-Jan-2008] $)
  $}

  ${
    $d o t w L $.  $d o t w N $.  $d t T $.  $d o t u w U $.  $d o t u w W $.
    bloval.3 $e |- N = ( U normOp W ) $.
    bloval.4 $e |- L = ( U LnOp W ) $.
    bloval.5 $e |- B = ( U BLnOp W ) $.
    $( The class of bounded linear operators between two normed complex vector
       spaces. $)
    bloval $p |- ( ( U e. NrmCVec /\ W e. NrmCVec ) ->
                   B = { t e. L | ( N ` t ) < +oo } ) $=
      ( vu vw cnv co cv cfv cpnf clt wbr cnmo clno wcel cblo crab opreq1 fveq1d
      wa wceq breq1d rabeqbidv opreq2 syl6eqr df-blo oprex eqeltri rabex ovmpt2
      cvv syl5eq ) CLUAFLUAUFBCFUBMANZEOZPQRZADUCZIJKCFLLUSJNZKNZSMZOZPQRZAVCVD
      TMZUCVBUBUSCVDSMZOZPQRZACVDTMZUCVCCUGZVGVKAVHVLVCCVDTUDVMVFVJPQVMUSVEVIVC
      CVDSUDUEUHUIVDFUGZVKVAAVLDVNVLCFTMZDVDFCTUJHUKVNVJUTPQVNUSVIEVNVICFSMEVDF
      CSUJGUKUEUHUIKJAULVAADDVOUQHCFTUMUNUOUPUR $.
      $( [16-Nov-2013] $) $( [6-Nov-2007] $)

    $( The predicate "is a bounded linear operator." $)
    isblo $p |- ( ( U e. NrmCVec /\ W e. NrmCVec ) -> ( T e. B <->
      ( T e. L /\ ( N ` T ) < +oo ) ) ) $=
      ( vt cnv wcel wa cv cfv cpnf clt wbr crab bloval eleq2d wceq fveq2 breq1d
      elrab syl6bb ) CKLFKLMZBALBJNZEOZPQRZJDSZLBDLBEOZPQRZMUGAUKBJACDEFGHITUAU
      JUMJBDUHBUBUIULPQUHBEUCUDUEUF $.
      $( [6-Nov-2007] $)

    $( The predicate "is a bounded linear operator." $)
    isblo2 $p |- ( ( U e. NrmCVec /\ W e. NrmCVec ) -> ( T e. B <->
      ( T e. L /\ ( N ` T ) e. RR ) ) ) $=
      ( cnv wcel wa cfv cpnf clt wbr cr isblo cba eqid lnof nmoreltpnf syld3an3
      wb wf 3expa pm5.32da bitr4d ) CJKZFJKZLZBAKBDKZBEMZNOPZLULUMQKZLABCDEFGHI
      RUKULUOUNUIUJULUOUNUDZUIUJULCSMZFSMZBUEUPBCDFUQURUQTZURTZHUABCEFUQURUSUTG
      UBUCUFUGUH $.
      $( [8-Dec-2007] $)
  $}

  ${
    bloln.4 $e |- L = ( U LnOp W ) $.
    bloln.5 $e |- B = ( U BLnOp W ) $.
    $( A bounded operator is a linear operator. $)
    bloln $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T e. B ) -> T e. L ) $=
      ( cnv wcel wa cnmo co cfv cpnf clt wbr eqid isblo simprbda 3impa ) CHIZEH
      IZBAIZBDIZUAUBJUCUDBCEKLZMNOPABCDUEEUEQFGRST $.
      $( [8-Dec-2007] $)
  $}

  ${
    blof.1 $e |- X = ( BaseSet ` U ) $.
    blof.2 $e |- Y = ( BaseSet ` W ) $.
    blof.5 $e |- B = ( U BLnOp W ) $.
    $( A bounded operator is an operator. $)
    blof $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T e. B ) -> T : X --> Y ) $=
      ( cnv wcel clno co wf eqid bloln lnof syld3an3 ) CJKDJKBAKBCDLMZKEFBNABCS
      DSOZIPBCSDEFGHTQR $.
      $( [8-Dec-2007] $)
  $}

  ${
    nmblore.1 $e |- X = ( BaseSet ` U ) $.
    nmblore.2 $e |- Y = ( BaseSet ` W ) $.
    nmblore.3 $e |- N = ( U normOp W ) $.
    nmblore.5 $e |- B = ( U BLnOp W ) $.
    $( The norm of a bounded operator is a real number. $)
    nmblore $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T e. B ) ->
                ( N ` T ) e. RR ) $=
      ( cnv wcel w3a cfv cr cmnf clt wbr wa cpnf wf simp1 simp2 blof 3jca nmoxr
      cxr wb xrrebnd 3syl nmogtmnf syld3an3 clno isblo simplbda 3impa mpbir2and
      co eqid ) CLMZELMZBAMZNZBDOZPMZQVERSZVEUARSZVDVAVBFGBUBZNVEUHMVFVGVHTUIVD
      VAVBVIVAVBVCUCVAVBVCUDABCEFGHIKUEZUFBCDEFGHIJUGVEUJUKVAVBVCVIVGVJBCDEFGHI
      JULUMVAVBVCVHVAVBTVCBCEUNUSZMVHABCVKDEJVKUTKUOUPUQUR $.
      $( [8-Dec-2007] $)
  $}

  ${
    $d u w U $.  $d w W $.  $d u w X $.  $d w Z $.
    0oval.1 $e |- X = ( BaseSet ` U ) $.
    0oval.6 $e |- Z = ( 0vec ` W ) $.
    0oval.0 $e |- O = ( U 0op W ) $.
    $( The zero operator between two normed complex vector spaces. $)
    0ofval $p |- ( ( U e. NrmCVec /\ W e. NrmCVec ) -> O = ( X X. { Z } ) ) $=
      ( vu vw cnv wcel c0o csn cxp cv cba cfv cn0v wceq wa fveq2 syl6eqr xpeq1d
      co sneqd xpeq2d df-0o cvv fvex eqeltri snex xpex ovmpt2 syl5eq ) AKLCKLUA
      BACMUEDENZOZHIJACKKIPZQRZJPZSRZNZOUQMDVBOURATZUSDVBVCUSAQRZDURAQUBFUCUDUT
      CTZVBUPDVEVAEVEVACSREUTCSUBGUCUFUGJIUHDUPDVDUIFAQUJUKEULUMUNUO $.
      $( [16-Nov-2013] $) $( [28-Nov-2007] $)

    $( Value of the zero operator. $)
    0oval $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ A e. X ) ->
            ( O ` A ) = Z ) $=
      ( cnv wcel w3a cfv csn cxp wceq wa 0ofval fveq1d cn0v 3adant3 cvv eqeltri
      fvex fvconst2 3ad2ant3 eqtrd ) BJKZDJKZAEKZLACMZAEFNOZMZFUHUIUKUMPUJUHUIQ
      ACULBCDEFGHIRSUAUJUHUMFPUIEFAFDTMUBHDTUDUCUEUFUG $.
      $( [28-Nov-2007] $)
  $}

  ${
    $d o u w U $.  $d o u w W $.  $d o u w X $.  $d o w Z $.
    0oo.1 $e |- X = ( BaseSet ` U ) $.
    0oo.2 $e |- Y = ( BaseSet ` W ) $.
    0oo.0 $e |- Z = ( U 0op W ) $.
    $( The zero operator is an operator. $)
    0oo $p |- ( ( U e. NrmCVec /\ W e. NrmCVec ) -> Z : X --> Y ) $=
      ( cnv wcel wa wf cn0v cfv csn cxp wss eqid nvzcl snssi fvex fss mpan 3syl
      fconst adantl 0ofval feq1d mpbird ) AIJZBIJZKZCDELCDCBMNZOZPZLZUKUPUJUKUM
      DJUNDQZUPBDUMGUMRZSUMDTCUNUOLUQUPCUMBMUAUECUNDUOUBUCUDUFULCDEUOAEBCUMFURH
      UGUHUI $.
      $( [28-Nov-2007] $)
  $}

  ${
    $d x y z U $.  $d x y z W $.  $d x y z Z $.
    0lno.0 $e |- Z = ( U 0op W ) $.
    0lno.7 $e |- L = ( U LnOp W ) $.
    $( The zero operator is linear. $)
    0lno $p |- ( ( U e. NrmCVec /\ W e. NrmCVec ) -> Z e. L ) $=
      ( vx vy vz wcel wa cfv cv co wceq wral cc eqid syl111anc 0oval cnv cba wf
      cns cpv 0oo cn0v simpllr simplr nvsz syl11anc opreq1d nvzcl nv0rid eqtr2d
      syl simplll simprl nvscl simprr nvgcl opreq2d opreq12d 3eqtr4d r19.21aiva
      r19.21aivva jca islno mpbird ) AUAJZCUAJZKZDBJAUBLZCUBLZDUCZGMZHMZAUDLZNZ
      IMZAUELZNZDLZVPVQDLZCUDLZNZVTDLZCUELZNZOZIVMPHVMPZGQPZKVLVOWLACVMVNDVMRZV
      NRZEUFVLWKGQVLVPQJZKZWJHIVMVMWPVQVMJZVTVMJZKZKZCUGLZVPXAWENZXAWHNZWCWIWTX
      CXAXAWHNZXAWTXBXAXAWHWTVKWOXBXAOVJVKWOWSUHZVLWOWSUIZVPWECXAWERZXARZUJUKUL
      WTVKXAVNJZXDXAOXEWTVKXIXECVNXAWNXHUMUPXACWHVNXAWNWHRZXHUNUKUOWTVJVKWBVMJZ
      WCXAOVJVKWOWSUQZXEWTVJVSVMJZWRXKXLWTVJWOWQXMXLXFWPWQWRURZVPVQVRAVMWMVRRZU
      SSWPWQWRUTZVSVTAWAVMWMWARZVASWBADCVMXAWMXHETSWTWFXBWGXAWHWTWDXAVPWEWTVJVK
      WQWDXAOXLXEXNVQADCVMXAWMXHETSVBWTVJVKWRWGXAOXLXEXPVTADCVMXAWMXHETSVCVDVFV
      EVGGHIVRWEDAWAWHBCVMVNWMWNXQXJXOXGFVHVI $.
      $( [19-Nov-2013] $) $( [28-Nov-2007] $)
  $}

  ${
    $d x z U $.  $d x z W $.  $d x z Z $.
    nmo0.3 $e |- N = ( U normOp W ) $.
    nmo0.0 $e |- Z = ( U 0op W ) $.
    $( The operator norm of the zero operator. $)
    nmo0 $p |- ( ( U e. NrmCVec /\ W e. NrmCVec ) ->
         ( N ` Z ) = 0 ) $=
      ( vz vx cnv wcel wa cfv cc0 cxr clt c1 cle wceq wrex eqid csn csup cv cnm
      wbr cba cab wf 0oo nmoval mpd3an3 df-sn wb cn0v nvzcl nvz0 0re 1re ltleii
      lt01 syl6eqbr fveq2 breq1d rcla4ev syl11anc biantrurd adantr 0oval fveq2d
      3expa ad2antlr eqtrd eqeq2d anbi2d rexbidva r19.41v syl6rbb bitrd syl5req
      abbidv supeq1d cr rexr ax-mp xrltso supsn syl6eq ) AIJZCIJZKZDBLZMUAZNOUB
      ZMWJWKGUCZAUDLZLZPQUEZHUCZWNDLZCUDLZLZRZKZGAUFLZSZHUGZNOUBZWMWHWIXDCUFLZD
      UHWKXGRACXDXHDXDTZXHTZFUIHGDAWOWTBCXDXHXIXJWOTZWTTZEUJUKWJNXFWLOWJWLWRMRZ
      HUGXFHMULWJXMXEHWJXMWQGXDSZXMKZXEWHXMXOUMWIWHXNXMWHAUNLZXDJXPWOLZPQUEZXNA
      XDXPXIXPTZUOWHXQMPQAWOXPXSXKUPMPUQURUTUSVAWQXRGXPXDWNXPRWPXQPQWNXPWOVBVCV
      DVEVFVGWJXEWQXMKZGXDSXOWJXCXTGXDWJWNXDJZKZXBXMWQYBXAMWRYBXACUNLZWTLZMYBWS
      YCWTWHWIYAWSYCRWNADCXDYCXIYCTZFVHVJVIWIYDMRWHYACWTYCYEXLUPVKVLVMVNVOWQXMG
      XDVPVQVRVTVSWAVLMNJZWMMRMWBJYFUQMWCWDNMOWEWFWDWG $.
      $( [27-Nov-2007] $)
  $}

  ${
    0blo.0 $e |- Z = ( U 0op W ) $.
    0blo.7 $e |- B = ( U BLnOp W ) $.
    $( The zero operator is a bounded linear operator. $)
    0blo $p |- ( ( U e. NrmCVec /\ W e. NrmCVec ) -> Z e. B ) $=
      ( cnv wcel wa clno co cnmo cfv cr eqid isblo2 0lno cc0 nmo0 0re mpbir2and
      syl6eqel ) BGHCGHIZDAHDBCJKZHDBCLKZMZNHADBUDUECUEOZUDOZFPBUDCDEUHQUCUFRNB
      UECDUGESTUBUA $.
      $( [8-Dec-2007] $)
  $}

  ${
    $d y z K $.  $d y z M $.  $d x N $.  $d z R $.  $d y z S $.  $d x y z T $.
    $d y z U $.  $d y z W $.  $d x y z X $.  $d y z Y $.  $d x Z $.
    nmlno0.3 $e |- N = ( U normOp W ) $.
    nmlno0.0 $e |- Z = ( U 0op W ) $.
    nmlno0.7 $e |- L = ( U LnOp W ) $.
    ${
      nmlno0lem.u $e |- U e. NrmCVec $.
      nmlno0lem.w $e |- W e. NrmCVec $.
      nmlno0lem.l $e |- T e. L $.
      nmlno0lem.1 $e |- X = ( BaseSet ` U ) $.
      nmlno0lem.2 $e |- Y = ( BaseSet ` W ) $.
      nmlno0lem.r $e |- R = ( .s ` U ) $.
      nmlno0lem.s $e |- S = ( .s ` W ) $.
      nmlno0lem.p $e |- P = ( 0vec ` U ) $.
      nmlno0lem.q $e |- Q = ( 0vec ` W ) $.
      nmlno0lem.k $e |- K = ( norm ` U ) $.
      nmlno0lem.m $e |- M = ( norm ` W ) $.
      $( Lemma for ~ nmlno0i . $)
      nmlno0lem $p |- ( ( N ` T ) = 0 <-> T = Z ) $=
        ( vx vz vy cfv cc0 wceq cv wral wcel wa wne wn c1 cdiv co clt wbr wi wo
        cc wb cnv cr nvcl mpan recnd adantr nvz fveq2 lno0 mp3an syl6eq necon3d
        syl6bi imp syl11anc wf lnof ffvelrni nvmul0or mp3an1 necon3abid neanior
        reccl syl6bbr recne0 simpr mpbir2and nvscl nvgt0 sylancr ex adantl wrex
        mpbid cle cab cxr csup nmosetre mp2an ressxr sstri simpl necon3i sylan2
        wss nv1 1re syl6eqel 3pm3.2i lnomul eqcomd fveq2d breq1d eqeq2d anbi12d
        eqle w3a rcla4ev syl12anc fvex eqeq1 anbi2d rexbidv elab sylibr supxrub
        adantll wfn ffn ax-mp nmoval eqeq1i biimpi ad2antrr breqtrd 0re sylancl
        lenlt pm2.65d nne sylib 0oval mp3an12 eqtr4d r19.21aiva 0oo nmo0 impbii
        eqfnfv2 ) EJULZUMUNZENUNZUVAUIUOZEULZUVCNULZUNZUILUPZUVBUVAUVFUILUVAUVC
        LUQZURZUVDBUVEUVIUVDBUSZUTUVDBUNZUVIUVJUMVAUVCGULZVBVCZUVDDVCZIULZVDVEZ
        UVHUVJUVPVFUVAUVHUVJUVPUVHUVJURZUVNBUSZUVPUVQUVRUVMUMUSZUVJUVQUVRUVMUMU
        NUVKVGZUTUVSUVJURUVQUVTUVNBUVQUVMVHUQZUVDMUQZUVNBUNUVTVIZUVQUVLVHUQZUVL
        UMUSZUWAUVHUWDUVJUVHUVLFVJUQZUVHUVLVKUQRUVCFGLUAUGVLVMVNVOZUVHUVJUWEUVH
        UVLUMUVDBUVHUVLUMUNZUVCAUNZUVKUWFUVHUWHUWIVIRUVCFGLAUAUEUGVPVMUWIUVDAEU
        LZBUVCAEVQUWFKVJUQZEHUQZUWJBUNRSTAEFHKLMBUAUBUEUFQVRVSVTZWBWAWCZUVLWLWD
        ZUVHUWBUVJLMUVCEUWFUWKUWLLMEWEZRSTEFHKLMUAUBQWFVSZWGVOZUWKUWAUWBUWCSUVM
        UVDDKMBUBUDUFWHWIWDWJUVMUMUVDBWKWMUVQUWDUWEUVSUWGUWNUVLWNWDUVHUVJWOWPUV
        QUWKUVNMUQZUVRUVPVISUVQUWAUWBUWSUWOUWRUWKUWAUWBUWSSUVMUVDDKMUBUDWQWIWDZ
        UVNKIMBUBUFUHWRWSXCWTXAUVIUVJUVPUTZUVIUVJURZUVOUMXDVEZUXAUXBUVOUJUOZGUL
        ZVAXDVEZUKUOZUXDEULZIULZUNZURZUJLXBZUKXEZXFVDXGZUMXDUVHUVJUVOUXNXDVEZUV
        AUVQUXMXFXOUVOUXMUQZUXOUXMVKXFUWKUWPUXMVKXOSUWQUKUJEGIKLMUBUHXHXIXJXKUV
        QUXFUVOUXIUNZURZUJLXBZUXPUVQUVMUVCCVCZLUQZUXTGULZVAXDVEZUVOUXTEULZIULZU
        NZUXSUVQUWAUVHUYAUWOUVHUVJXLZUWFUWAUVHUYARUVMUVCCFLUAUCWQWIWDUVQUYBVKUQ
        UYBVAUNZUYCUVQUYBVAVKUVJUVHUVCAUSZUYHUVCAUVDBUWMXMUWFUVHUYIUYHRUVCCFGLA
        UAUCUEUGXPWIXNZXQXRUYJUYBVAYFWDUVQUVNUYDIUVQUYDUVNUVQUWAUVHUYDUVNUNZUWO
        UYGUWFUWKUWLYGUWAUVHURUYKUWFUWKUWLRSTXSUVMUVCCDEFHKLUAUCUDQXTVMWDYAYBUX
        RUYCUYFURUJUXTLUXDUXTUNZUXFUYCUXQUYFUYLUXEUYBVAXDUXDUXTGVQYCUYLUXIUYEUV
        OUYLUXHUYDIUXDUXTEVQYBYDYEYHYIUXLUXSUKUVOUVNIYJUXGUVOUNZUXKUXRUJLUYMUXJ
        UXQUXFUXGUVOUXIYKYLYMYNYOUXMUVOYPWSYQUVAUXNUMUNZUVHUVJUVAUYNUUTUXNUMUWF
        UWKUWPUUTUXNUNRSUWQUKUJEFGIJKLMUAUBUGUHOUUAVSUUBUUCUUDUUEUVHUVJUXCUXAVI
        ZUVAUVQUVOVKUQZUMVKUQUYOUVQUWKUWSUYPSUWTUVNKIMUBUHVLWSUUFUVOUMUUHUUGYQX
        CWTUUIUVDBUUJUUKUVHUVEBUNZUVAUWFUWKUVHUYQRSUVCFNKLBUAUFPUULUUMXAUUNUUOE
        LYRZNLYRZUVBUVGVIUWPUYRUWQLMEYSYTLMNWEZUYSUWFUWKUYTRSFKLMNUAUBPUUPXILMN
        YSYTUILENUUSXIYOUVBUUTNJULZUMENJVQUWFUWKVUAUMUNRSFJKNOPUUQXIVTUUR $.
        $( [28-Nov-2007] $)
    $}

    ${
      nmlno0i.u $e |- U e. NrmCVec $.
      nmlno0i.w $e |- W e. NrmCVec $.
      $( The norm of a linear operator is zero iff the operator is zero. $)
      nmlno0i $p |- ( T e. L -> ( ( N ` T ) = 0 <-> T = Z ) ) $=
        ( wcel cfv cc0 wceq wb cn0v cns cnm eqid cif fveq2 eqeq1d eqeq1 bibi12d
        cba cnv 0lno mp2an elimel nmlno0lem dedth ) ACLZADMZNOZAFOZPUMAFUAZDMZN
        OZUQFOZPAFAUQOZUOUSUPUTVAUNURNAUQDUBUCAUQFUDUEBQMZEQMZBRMZERMZUQBBSMZCE
        SMZDEBUFMZEUFMZFGHIJKAFCBUGLEUGLFCLJKBCEFHIUHUIUJVHTVITVDTVETVBTVCTVFTV
        GTUKUL $.
        $( [6-Dec-2007] $)
    $}

    $( The norm of a linear operator is zero iff the operator is zero. $)
    nmlno0 $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T e. L ) ->
       ( ( N ` T ) = 0 <-> T = Z ) ) $=
      ( wcel cfv cc0 wceq wb wi clno co cnmo c0o opreq1 cnv caddc cmul cop cabs
      syl5eq eleq2d fveq1d eqeq1d eqeq2d bibi12d imbi12d opreq2 elimnvu nmlno0i
      cif eqid dedth2h 3impia ) BUAJZEUAJZACJZADKZLMZAFMZNZUTVAVBVFOAUTBUBUCUDU
      EUDZUPZEPQZJZAVHERQZKZLMZAVHESQZMZNZOAVHVAEVGUPZPQZJZAVHVQRQZKZLMZAVHVQSQ
      ZMZNZOBEVGVGBVHMZVBVJVFVPWFCVIAWFCBEPQVIIBVHEPTUFUGWFVDVMVEVOWFVCVLLWFADV
      KWFDBERQVKGBVHERTUFUHUIWFFVNAWFFBESQVNHBVHESTUFUJUKULEVQMZVJVSVPWEWGVIVRA
      EVQVHPUMUGWGVMWBVOWDWGVLWALWGAVKVTEVQVHRUMUHUIWGVNWCAEVQVHSUMUJUKULAVHVRV
      TVQWCVTUQWCUQVRUQBUNEUNUOURUS $.
      $( [24-Nov-2007] $)
  $}


  ${
    $d x A $.  $d x K $.  $d x L $.  $d x M $.  $d x T $.  $d x X $.  $d x W $.
    nmlnoubi.1 $e |- X = ( BaseSet ` U ) $.
    nmlnoubi.z $e |- Z = ( 0vec ` U ) $.
    nmlnoubi.k $e |- K = ( norm ` U ) $.
    nmlnoubi.m $e |- M = ( norm ` W ) $.
    nmlnoubi.3 $e |- N = ( U normOp W ) $.
    nmlnoubi.7 $e |- L = ( U LnOp W ) $.
    nmlnoubi.u $e |- U e. NrmCVec $.
    nmlnoubi.w $e |- W e. NrmCVec $.
    $( An upper bound for the operator norm of a linear operator, using only
       the properties of nonzero arguments. $)
    nmlnoubi $p |- ( ( T e. L /\ ( A e. RR /\ 0 <_ A ) /\ A. x e. X ( x =/= Z
     -> ( M ` ( T ` x ) ) <_ ( A x. ( K ` x ) ) ) ) -> ( N ` T ) <_ A ) $=
      ( cc0 wcel cr cle wbr wa cv wne cmul co wi wral wceq fveq2 fveq2d opreq2d
      cfv breq12d imp adantll 0re leidi a1i cn0v cnv cba eqid lno0 mp3an12 nvz0
      id ax-mp syl6eq adantr opreq2i cc recn mul01 syl syl5eq ad2antrl pm2.61ne
      3brtr4d ex ralimdv 3impia wf lnof nmoub2i syl3an1 syld3an3 ) CFUAZBUBUAZT
      BUCUDZUEZAUFZKUGZWOCUPZGUPZBWOEUPZUHUIZUCUDZUJZAJUKZXAAJUKZCHUPBUCUDZWKWN
      XCXDWKWNUEZXBXAAJXFXBXAXFXBUEXAKCUPZGUPZBKEUPZUHUIZUCUDZWOKWOKULZWRXHWTXJ
      UCXLWQXGGWOKCUMUNXLWSXIBUHWOKEUMUOUQXBWPXAXFXBWPXAXBVJURUSXFXKXBXFTTXHXJU
      CTTUCUDXFTUTVAVBWKXHTULWNWKXHIVCUPZGUPZTWKXGXMGDVDUAZIVDUAZWKXGXMULRSKCDF
      IJIVEUPZXMLXQVFZMXMVFZQVGVHUNXPXNTULSIGXMXSOVIVKVLVMWLXJTULWKWMWLXJBTUHUI
      ZTXITBUHXOXITULRDEKMNVIVKVNWLBVOUAXTTULBVPBVQVRVSVTWBVMWAWCWDWEWKJXQCWFZW
      NXDXEXOXPWKYARSCDFIJXQLXRQWGVHABCDEGHIJXQLXRNOPRSWHWIWJ $.
      $( [1-Jan-2008] $)
  $}

  ${
    nmlnogt0.3 $e |- N = ( U normOp W ) $.
    nmlnogt0.0 $e |- Z = ( U 0op W ) $.
    nmlnogt0.7 $e |- L = ( U LnOp W ) $.
    $( The norm of a nonzero linear operator is positive. $)
    nmlnogt0 $p |- ( ( U e. NrmCVec /\ W e. NrmCVec /\ T e. L ) ->
                   ( T =/= Z <-> 0 < ( N ` T ) ) ) $=
      ( cnv wcel w3a cfv cc0 wne clt wbr cba wb eqid nmlno0 necon3bid cxr nmoxr
      wf lnof cle nmoge0 wa wo cr rexr ax-mp xrlttri2 mpan2 adantr xrlenlt mpan
      0re wn biimpa biorf syl bitr4d syl11anc syld3an3 bitr3d ) BJKZEJKZACKZLZA
      DMZNOZAFONVLPQZVKVLNAFABCDEFGHIUAUBVHVIVJBRMZERMZAUEZVMVNSZABCEVOVPVOTZVP
      TZIUFVHVIVQLVLUCKZNVLUGQZVRABDEVOVPVSVTGUDABDEVOVPVSVTGUHWAWBUIZVMVLNPQZV
      NUJZVNWAVMWESZWBWANUCKZWFNUKKWGUSNULUMZVLNUNUOUPWCWDUTZVNWESWAWBWIWGWAWBW
      ISWHNVLUQURVAWDVNVBVCVDVEVFVG $.
      $( [10-Dec-2007] $)
  $}

  ${
    $d x L $.  $d x T $.  $d x U $.  $d x W $.  $d x X $.
    lnon0.1 $e |- X = ( BaseSet ` U ) $.
    lnon0.6 $e |- Z = ( 0vec ` U ) $.
    lnon0.0 $e |- O = ( U 0op W ) $.
    lnon0.7 $e |- L = ( U LnOp W ) $.
    $( The domain of a nonzero linear operator contains a nonzero vector. $)
    lnon0 $p |- ( ( ( U e. NrmCVec /\ W e. NrmCVec /\ T e. L ) /\ T =/= O )
                   -> E. x e. X x =/= Z ) $=
      ( cnv wcel wne wn wceq wral bitr3i cfv w3a cv wrex ralnex nne ralbii cn0v
      csn cxp wfn wa fveq2 cba eqid lno0 sylan9eqr ex ralimdv wf ffn syl jctild
      fconstfv fconst2 syl6ib 0ofval 3adant3 eqeq2d sylibrd syl5bi necon1ad imp
      lnof fvex ) CMNZFMNZBDNZUAZBEOAUBZHOZAGUCZVRWABEWAPZVSHQZAGRZVRBEQZWBVTPZ
      AGRWDVTAGUDWFWCAGVSHUEUFSVRWDBGFUGTZUHZUIZQZWEVRWDBGUJZVSBTZWGQZAGRZUKZWJ
      VRWDWNWKVRWCWMAGVRWCWMWCVRWLHBTWGVSHBULHBCDFGFUMTZWGIWPUNZJWGUNZLUOUPUQUR
      VRGWPBUSWKBCDFGWPIWQLVMGWPBUTVAVBWOGWHBUSWJAGWGBVCGWGBFUGVNVDSVEVREWIBVOV
      PEWIQVQCEFGWGIWRKVFVGVHVIVJVKVL $.
      $( [13-Aug-2008] $) $( [15-Dec-2007] $)
  $}

  ${
    nmblolbi.1 $e |- X = ( BaseSet ` U ) $.
    nmblolbi.4 $e |- L = ( norm ` U ) $.
    nmblolbi.5 $e |- M = ( norm ` W ) $.
    nmblolbi.6 $e |- N = ( U normOp W ) $.
    nmblolbi.7 $e |- B = ( U BLnOp W ) $.
    nmblolbi.u $e |- U e. NrmCVec $.
    nmblolbi.w $e |- W e. NrmCVec $.
    ${
      nmblolbii.b $e |- T e. B $.
      $( A lower bound for the norm of a bounded linear operator. $)
      nmblolbii $p |- ( A e. X ->
                ( M ` ( T ` A ) ) <_ ( ( N ` T ) x. ( L ` A ) ) ) $=
        ( wcel cfv cc0 cmul co cle wbr cn0v fveq2 fveq2d opreq2d breq12d wne wa
        wceq cdiv c1 cns cr cba cnv nvcl mpan adantr eqid nvz necon3bid biimpar
        wb rereccl syl11anc clt nvgt0 biimpa recgt0 wi 0re ltle syl mpd wf blof
        mp3an ffvelrni nvsge0 mp3an1 syl21anc cc recnd simpl clno bloln 3pm3.2i
        w3a lnomul divrec2 syl111anc 3eqtr4rd nvscl ancoms syldan nmolb eqbrtrd
        eqle nmblore 3jca ledivmul2OLD mpbid leidi lno0 fveq2i nvz0 ax-mp eqtri
        nv1 a1i opreq2i recni mul01i 3brtr4i pm2.61ne ) AIRZACSZFSZCGSZAESZUAUB
        ZUCUDZDUESZCSZFSZYBYFESZUAUBZUCUDZAYFAYFULZYAYHYDYJUCYLXTYGFAYFCUFUGYLY
        CYIYBUAAYFEUFUHUIXSAYFUJZUKZYAYCUMUBZYBUCUDZYEYNYOUNYCUMUBZADUOSZUBZCSZ
        FSZYBUCYNYQXTHUOSZUBZFSZYQYAUAUBZUUAYOYNYQUPRZTYQUCUDZXTHUQSZRZUUDUUEUL
        ZYNYCUPRZYCTUJZUUFXSUUKYMDURRZXSUUKOADEIJKUSUTZVAZXSUULYMXSYCTAYFUUMXSY
        CTULYLVFOADEIYFJYFVBZKVCUTVDVEZYCVGVHZYNTYQVIUDZUUGYNUUKTYCVIUDZUUSUUOX
        SYMUUTUUMXSYMUUTVFOADEIYFJUUPKVJUTVKZYCVLVHYNUUFUUSUUGVMZUURTUPRUUFUVBV
        NTYQVOUTVPVQXSUUIYMIUUHACUUMHURRZCBRZIUUHCVRZOPQBCDHIUUHJUUHVBZNVSVTZWA
        ZVAUVCUUFUUGUKUUIUUJPYQXTUUBHFUUHUVFUUBVBZLWBWCWDYNYTUUCFYNYQWERZXSYTUU
        CULZYNYQUURWFZXSYMWGZUUMUVCCDHWHUBZRZWKUVJXSUKUVKUUMUVCUVOOPUUMUVCUVDUV
        OOPQBCDUVNHUVNVBZNWIVTZWJYQAYRUUBCDUVNHIJYRVBZUVIUVPWLUTVHUGYNYAWERYCWE
        RUULYOUUEULYNYAXSYAUPRZYMXSUUIUVSUVHUVCUUIUVSPXTHFUUHUVFLUSUTVPZVAWFYNY
        CUUOWFUUQYAYCWMWNWOYNYSIRZYSESZUNUCUDZUUAYBUCUDZYNUVJXSUWAUVLUVMUUMUVJX
        SUWAOYQAYRDIJUVRWPWCZVHYNUWBUPRZUWBUNULZUWCYNUWAUWFXSYMUVJUWAUVLUVJXSUW
        AUWEWQWRUUMUWAUWFOYSDEIJKUSUTVPUUMXSYMUWGOAYRDEIYFJUVRUUPKXLWCUWBUNXAVH
        UUMUVCUVEWKUWAUWCUKUWDUUMUVCUVEOPUVGWJYSCDEFGHIUUHJUVFKLMWSUTVHWTYNUVSU
        UKYBUPRZWKZUUTYPYEVFXSUWIYMXSUVSUUKUWHUVTUUNUWHXSUUMUVCUVDUWHOPQBCDGHIU
        UHJUVFMNXBVTZXMXCVAUVAYAYCYBXDVHXEYKXSTTYHYJUCTVNXFYHHUESZFSZTYGUWKFUUM
        UVCUVOYGUWKULOPUVQYFCDUVNHIUUHUWKJUVFUUPUWKVBZUVPXGVTXHUVCUWLTULPHFUWKU
        WMLXIXJXKYJYBTUAUBTYITYBUAUUMYITULODEYFUUPKXIXJXNYBYBUWJXOXPXKXQXMXR $.
        $( [7-Dec-2007] $)
    $}

    $( A lower bound for the norm of a bounded linear operator. $)
    nmblolbi $p |- ( ( T e. B /\ A e. X ) ->
              ( M ` ( T ` A ) ) <_ ( ( N ` T ) x. ( L ` A ) ) ) $=
      ( wcel cfv cmul co cle wbr wi c0o wceq fveq1 fveq2d fveq2 opreq1d breq12d
      cif imbi2d cnv eqid 0blo mp2an elimel nmblolbii dedth imp ) CBQZAIQZACRZF
      RZCGRZAERZSTZUAUBZVAVBVHUCVBAVACDHUDTZUKZRZFRZVJGRZVFSTZUAUBZUCCVICVJUEZV
      HVOVBVPVDVLVGVNUAVPVCVKFACVJUFUGVPVEVMVFSCVJGUHUIUJULABVJDEFGHIJKLMNOPCVI
      BDUMQHUMQVIBQOPBDHVIVIUNNUOUPUQURUSUT $.
      $( [10-Dec-2007] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x L $.  $d x y M $.  $d x y N $.  $d x y T $.
    $d x y U $.  $d x y W $.  $d x y X $.
    isblo3i.1 $e |- X = ( BaseSet ` U ) $.
    isblo3i.m $e |- M = ( norm ` U ) $.
    isblo3i.n $e |- N = ( norm ` W ) $.
    isblo3i.4 $e |- L = ( U LnOp W ) $.
    isblo3i.5 $e |- B = ( U BLnOp W ) $.
    isblo3i.u $e |- U e. NrmCVec $.
    isblo3i.w $e |- W e. NrmCVec $.
    $( The predicate "is a bounded linear operator."  Definition 2.7-1 of
       [Kreyszig] p. 91. $)
    isblo3i $p |- ( T e. B <-> ( T e. L /\
          E. x e. RR A. y e. X ( N ` ( T ` y ) ) <_ ( x x. ( M ` y ) ) ) ) $=
      ( wcel cfv cr cv cmul co cle wbr wral wrex wa cnv bloln mp3an12 cnmo eqid
      cba nmblore nmblolbi r19.21aiva wceq opreq1 ralbidv rcla4ev syl11anc cpnf
      breq2d jca w3a clt simp1 wf lnof cabs nmoub3i cc abscl syl ltpnf 3ad2ant2
      recn cxr wi nmoxr 3ad2ant1 pnfxr a1i xrlelttr syl111anc mp2and syl3an1 wb
      rexr isblo mp2an sylanbrc 3exp r19.23adv imp impbii ) DCRZDFRZBUAZDSHSZAU
      AZWTGSZUBUCZUDUEZBJUFZATUGZUHWRWSXGEUIRZIUIRZWRWSPQCDEFINOUJUKWRDEIULUCZS
      ZTRZXAXKXCUBUCZUDUEZBJUFZXGXHXIWRXLPQCDEXJIJIUNSZKXPUMZXJUMZOUOUKWRXNBJWT
      CDEGHXJIJKLMXROPQUPUQXFXOAXKTXBXKURZXEXNBJXSXDXMXAUDXBXKXCUBUSVDUTVAVBVEW
      SXGWRWSXFWRATWSXBTRZXFWRWSXTXFVFWSXKVCVGUEZWRWSXTXFVHWSJXPDVIZXTXFYAXHXIW
      SYBPQDEFIJXPKXQNVJUKYBXTXFVFZXKXBVKSZUDUEZYDVCVGUEZYABXBDEGHXJIJXPKXQLMXR
      PQVLXTYBYFXFXTYDTRZYFXTXBVMRYGXBVRXBVNVOZYDVPVOVQYCXKVSRZYDVSRZVCVSRZYEYF
      UHYAVTYBXTYIXFXHXIYBYIPQDEXJIJXPKXQXRWAUKWBXTYBYJXFXTYGYJYHYDWJVOVQYKYCWC
      WDXKYDVCWEWFWGWHXHXIWRWSYAUHWIPQCDEFXJIXRNOWKWLWMWNWOWPWQ $.
      $( [11-Dec-2007] $)

    $( Properties that determine a bounded linear operator. $)
    blo3i $p |- ( ( T e. L /\ A e. RR /\ A. y e. X ( N ` ( T ` y ) ) <_
               ( A x. ( M ` y ) ) ) -> T e. B ) $=
      ( vx wcel cr cv cfv cmul co cle wbr wral wrex wceq opreq1 ralbidv rcla4ev
      wa breq2d isblo3i biimpri sylan2 3impb ) DFSZBTSZAUAZDUBHUBZBVAGUBZUCUDZU
      EUFZAJUGZDCSZUTVFUMUSVBRUAZVCUCUDZUEUFZAJUGZRTUHZVGVKVFRBTVHBUIZVJVEAJVMV
      IVDVBUEVHBVCUCUJUNUKULVGUSVLUMRACDEFGHIJKLMNOPQUOUPUQUR $.
      $( [13-Jan-2008] $)
  $}

  ${
    blometi.1 $e |- X = ( BaseSet ` U ) $.
    blometi.2 $e |- Y = ( BaseSet ` W ) $.
    blometi.8 $e |- C = ( IndMet ` U ) $.
    blometi.d $e |- D = ( IndMet ` W ) $.
    blometi.6 $e |- N = ( U normOp W ) $.
    blometi.7 $e |- B = ( U BLnOp W ) $.
    blometi.u $e |- U e. NrmCVec $.
    blometi.w $e |- W e. NrmCVec $.
    $( Upper bound for the distance between the values of a bounded linear
       operator. $)
    blometi $p |- ( ( T e. B /\ P e. X /\ Q e. X ) ->
         ( ( T ` P ) D ( T ` Q ) ) <_ ( ( N ` T ) x. ( P C Q ) ) ) $=
      ( cfv wcel w3a cnsb co cnm cmul cle wbr wa cnv eqid nvmcl mp3an1 nmblolbi
      3impb wceq wf blof mp3an12 ffvelrn sylan 3adant3 3adant2 imsdval syl11anc
      sylan2 bloln lnosub mp3anl1 mpanl1 syl3an1 fveq2d 3adant1 opreq2d 3brtr4d
      clno eqtr4d ) FAUAZDJUAZEJUAZUBZDEGUCTZUDZFTZIUETZTZFHTZWCGUETZTZUFUDZDFT
      ZEFTZCUDZWGDEBUDZUFUDUGVRVSVTWFWJUGUHZVSVTUIZVRWCJUAZWOGUJUAZVSVTWQRDEGWB
      JLWBUKZULUMWCAFGWHWEHIJLWHUKZWEUKZPQRSUNVFUOWAWMWKWLIUCTZUDZWETZWFWAWKKUA
      ZWLKUAZWMXDUPZVRVSXEVTVRJKFUQZVSXEWRIUJUAZVRXHRSAFGIJKLMQURUSZJKDFUTVAVBV
      RVTXFVSVRXHVTXFXJJKEFUTVAVCXIXEXFXGSWKWLCIXBWEKMXBUKZXAOVDUMVEWAWDXCWEVRF
      GIVPUDZUAZVSVTWDXCUPZWRXIVRXMRSAFGXLIXLUKZQVGUSXMVSVTXNXIXMWPXNSWRXIXMWPX
      NRDEFGXLWBXBIJLWSXKXOVHVIVJUOVKVLVQWAWNWIWGUFVSVTWNWIUPZVRWRVSVTXPRDEBGWB
      WHJLWSWTNVDUMVMVNVO $.
      $( [11-Dec-2007] $)
  $}

  ${
    $d v w x y B $.  $d w x y z C $.  $d w x y z D $.  $d w x y z J $.
    $d x y z K $.  $d v x L $.  $d w y z P $.  $d v w x y z T $.
    $d v w x y z U $.  $d v w x y z W $.  $d w x y z X $.
    blocni.8 $e |- C = ( IndMet ` U ) $.
    blocni.d $e |- D = ( IndMet ` W ) $.
    blocni.j $e |- J = ( MetOpen ` C ) $.
    blocni.k $e |- K = ( MetOpen ` D ) $.
    blocni.4 $e |- L = ( U LnOp W ) $.
    blocni.5 $e |- B = ( U BLnOp W ) $.
    blocni.u $e |- U e. NrmCVec $.
    blocni.w $e |- W e. NrmCVec $.
    blocni.l $e |- T e. L $.
    ${
      blocnilem.1 $e |- X = ( BaseSet ` U ) $.
      $( MAY BE REVISED - this needs to be broken up or shortened! $)
      $( Lemma for ~ blocni and ~ lnocni .  If a linear operator is continuous
         at any point, it is bounded. _Warning:  The HTML proof page is 0.7MB
         in size_. $)
      blocnilem $p |- ( ( P e. X /\ T e. ( ( J CnP K ) ` P ) ) -> T e. B ) $=
        ( vw vx vz vy wcel ccnp co cfv wa cv cnm cmul cle wbr wral wrex cc0 clt
        cr cnsb c1 wi cnv w3a 1re lt01 eqid nvcnpi4 mp3anr3 mpanr2 mp3anl1 cdiv
        mpanl1 gt0ne0 rereccl syldan ad2antlr cn0v fveq2 fveq2d opreq2d breq12d
        wne wceq cns simpll cc nvcl adantr wb nvz necon3bid biimpar jca redivcl
        mpan 3expb sylan2 recnd simprl mp3an1 syl11anc adantll nvnncan adantlrr
        nvscl adantlr ltle imdistani nvgt0 biimpa divge0 syl2an nvsge0 syl21anc
        recn ad2antrr adantl divcan1 syl111anc eqtrd leid eqbrtrd opreq2 breq1d
        0re nvmcl imbi12d rcla4v syl 3pm3.2i lnomul mp3an ad2antrl breq2d bitrd
        mpid nvz0 ax-mp cba wf lnof ffvelrni sylancr divgt0 lemuldiv2OLD recdiv
        mp3anl3 divrec2 sylibd exp32 com23 com24 imp41 lno0 eqtri leidi opreq2i
        fveq2i mul01 syl5req syl5breq syl5eqbr pm2.61ne r19.21aiva opreq1 exp42
        ralbidv rcla4ev imp4a r19.23adv mpd jctil isblo3i sylibr ) DKUFZEDGHUGU
        HUIUFZUJZEIUFZUBUKZEUIZJULUIZUIZUCUKZUWAFULUIZUIZUMUHZUNUOZUBKUPZUCUTUQ
        ZUJEAUFUVSUWKUVTUVSURUDUKZUSUOZDUEUKZFVAUIZUHZUWFUIZUWLUNUOZUWPEUIZUWCU
        IZVBUNUOZVCZUEKUPZUJZUDUTUQZUWKJVDUFZUVQUVRUXESFVDUFZUXFUVQUVRUXERUXGUX
        FUVQVEZUVRVBUTUFZUXEVFUXHUVRUXIURVBUSUOUXEVGUDUEVBBCDUWOEFGHIUWFUWCJKUA
        UWFVHZUWCVHZUWOVHZLMNOPTVIVJVKVLVNUVQUXEUWKVCUVRUVQUXDUWKUDUTUVQUWLUTUF
        ZUWMUXCUWKUVQUXMUWMUXCUWKUVQUXMUWMUJZUJZUXCUJZVBUWLVMUHZUTUFZUWDUXQUWGU
        MUHZUNUOZUBKUPZUWKUXNUXRUVQUXCUXMUWMUWLURWDZUXRUWLVOZUWLVPVQZVRUXPUXTUB
        KUXPUWAKUFZUJUXTFVSUIZEUIZUWCUIZUXQUYFUWFUIZUMUHZUNUOZUWAUYFUWAUYFWEZUW
        DUYHUXSUYJUNUYLUWBUYGUWCUWAUYFEVTWAUYLUWGUYIUXQUMUWAUYFUWFVTWBWCUXOUXCU
        YEUWAUYFWDZUXTUXOUYMUYEUXCUXTUXOUYEUYMUXCUXTVCZUXOUYEUYMUYNUXOUYEUYMUJZ
        UJZUXCDDUWLUWGVMUHZUWAFWFUIZUHZUWOUHZUWOUHZEUIZUWCUIZVBUNUOZUXTUYPUXCVU
        AUWFUIZUWLUNUOZVUDUYPVUEUWLUWLUNUYPVUEUYSUWFUIZUWLUYPVUAUYSUWFUVQUXMUYO
        VUAUYSWEZUWMUVQUXMUJUYOUJUVQUYSKUFZVUHUVQUXMUYOWGUXMUYOVUIUVQUXMUYOUJZU
        YQWHUFZUYEVUIVUJUYQUYOUXMUWGUTUFZUWGURWDZUJUYQUTUFZUYOVULVUMUYEVULUYMUX
        GUYEVULRUWAFUWFKUAUXJWIWQWJZUYEVUMUYMUYEUWGURUWAUYFUXGUYEUWGURWEUYLWKRU
        WAFUWFKUYFUAUYFVHZUXJWLWQWMWNZWOUXMVULVUMVUNUWLUWGWPWRWSZWTZUXMUYEUYMXA
        UXGVUKUYEVUIRUYQUWAUYRFKUAUYRVHZXGXBXCZXDUXGUVQVUIVUHRDUYSFUWOKUAUXLXEX
        BXCXFZWAUXNUYOVUGUWLWEUVQUXNUYOUJZVUGUYQUWGUMUHZUWLVVCVUNURUYQUNUOZUYEV
        UGVVDWEZUXMUYOVUNUWMVURXHZUXNUXMURUWLUNUOZUJVULURUWGUSUOZUJZVVEUYOUXMUW
        MVVHURUTUFUXMUWMVVHVCYGURUWLXIWQXJUYOVULVVIVUOUYEUYMVVIUXGUYEUYMVVIWKRU
        WAFUWFKUYFUAVUPUXJXKWQXLWOZUWLUWGXMXNZUXNUYEUYMXAUXGVUNVVEUJZUYEVVFRUYQ
        UWAUYRFUWFKUAVUTUXJXOXBXPVVCUWLWHUFZUWGWHUFZVUMVVDUWLWEUXMVVNUWMUYOUWLX
        QZXRZUYOVVOUXNUYOUWGVUOWTZXSZUYOVUMUXNVUQXSUWLUWGXTYAYBXDYBUXNUWLUWLUNU
        OZUVQUYOUXMVVTUWMUWLYCWJVRYDUYPUYTKUFZUXCVUFVUDVCZVCUYPUVQVUIVWAUVQUXNU
        YOWGUXNUYOVUIUVQUXMUYOVUIUWMVVAXHXDUXGUVQVUIVWARDUYSFUWOKUAUXLYHXBXCUXB
        VWBUEUYTKUWNUYTWEZUWRVUFUXAVUDVWCUWQVUEUWLUNVWCUWPVUAUWFUWNUYTDUWOYEZWA
        YFVWCUWTVUCVBUNVWCUWSVUBUWCVWCUWPVUAEVWDWAWAYFYIYJYKYRUYPVUDUYQUWDUMUHZ
        VBUNUOZUXTUYPVUCVWEVBUNUYPVUCUYQUWBJWFUIZUHZUWCUIZVWEUYPVUBVWHUWCUYPVUB
        UYSEUIZVWHUYPVUAUYSEVVBWAUYPVUKUYEVWJVWHWEZUXNUYOVUKUVQUXMUYOVUKUWMVUSX
        HXDUXOUYEUYMXAUXGUXFUVTVEVUKUYEUJVWKUXGUXFUVTRSTYLUYQUWAUYRVWGEFIJKUAVU
        TVWGVHZPYMWQXCYBWAUXNUYOVWIVWEWEZUVQVVCVUNVVEUWBJUUAUIZUFZVWMVVGVVLUYEV
        WOUXNUYMKVWNUWAEUXGUXFUVTKVWNEUUBRSTEFIJKVWNUAVWNVHZPUUCYNUUDZYOUXFVVMV
        WOVWMSUYQUWBVWGJUWCVWNVWPVWLUXKXOXBXPXDYBYFUXNUYOVWFUXTWKUVQVVCVWFUWDVB
        UYQVMUHZUNUOZUXTVVCVUNUWDUTUFZURUYQUSUOZVWFVWSWKZVVGUYEVWTUXNUYMUYEUXFV
        WOVWTSVWQUWBJUWCVWNVWPUXKWIUUEYOUYOUXNVVJVXAVVKUWLUWGUUFWSVUNVWTUXIVXAV
        XBVFUYQUWDVBUUGUUIXPVVCVWRUXSUWDUNVVCVWRUWGUWLVMUHZUXSUXNVVNUYBUJVVOVUM
        UJVWRVXCWEUYOUXNVVNUYBUXMVVNUWMVVPWJUYCWOUYOVVOVUMVVRVUQWOUWLUWGUUHXNVV
        CVVOVVNUYBVXCUXSWEVVSVVQUXNUYBUYOUYCWJUWGUWLUUJYAYBYPYQXDYQUUKUULUUMUUN
        UUOUXOUYKUXCUYEUXNUYKUVQUXNUYHURUYJUNUYHJVSUIZUWCUIZURUYGVXDUWCUXGUXFUV
        TUYGVXDWERSTUYFEFIJKVWNVXDUAVWPVUPVXDVHZPUUPYNUUTUXFVXEURWESJUWCVXDVXFU
        XKYSYTUUQUXNURURUYJUNURYGUURUXNUYJUXQURUMUHZURUYIURUXQUMUXGUYIURWERFUWF
        UYFVUPUXJYSYTUUSUXNUXQWHUFVXGURWEUXNUXQUYDWTUXQUVAYKUVBUVCUVDXSXRUVEUVF
        UWJUYAUCUXQUTUWEUXQWEZUWIUXTUBKVXHUWHUXSUWDUNUWEUXQUWGUMUVGYPUVIUVJXCUV
        HUVKUVLWJUVMTUVNUCUBAEFIUWFUWCJKUAUXJUXKPQRSUVOUVP $.
        $( [17-Dec-2007] $)
    $}

    $( A linear operator is continuous iff it is bounded.  Theorem 2.7-9(a) of
       [Kreyszig] p. 97. $)
    blocni $p |- ( T e. ( J Cn K ) <-> T e. B ) $=
      ( wcel wa vy vz vx vw ccn cn0v cfv cba ccnp cnv eqid nvzcl ax-mp cuni cme
      co wceq imsmet imsbai uniopn2 eleqtrri ctop opntop cncnpi mpan2 blocnilem
      mpanl12 sylancr c0o eleq1 wne wf cc0 cv clt wbr wi wral cr wrex cnmo cdiv
      simpr nmblore mp3an12 ad2antrr wb nmlno0i biimpi necon3i ad2antlr redivcl
      ad2ant2r nmlnogt0 mp3an anim12i adantr divgt0 syl11anc adantlr cmul metcl
      syl111anc mp3an1 adantlll simplrl ltmuldiv2OLD syl31anc biimpar cle 3expa
      blometi adantllr ffvelrni syl2an adantll remulcl anassrs lelttr mpand imp
      lnof syldan exp31 r19.21aiv breq2 imbi1d ralbidv anbi12d rcla4ev syl12anc
      exp32 r19.21aiva jctil metcn mp2an sylibr csn cxp 0ofval metcnconst mp4an
      fvex fconst eqeltri a1i pm2.61ne impbii ) DFGUEUPZSZDASZUUJEUFUGZEUHUGZSZ
      DUULFGUIUPUGSZUUKEUJSZUUNPEUUMUULUUMUKZUULUKULUMZUUJUULFUNZSZUUOUULUUMUUS
      UURBUOSZUUSUUMUQUUPUVAPBEJURUMZBFUUMBEUUMUUQJPUSZLUTUMVAFVBSZGVBSZUUJUUTT
      UUOUVAUVDUVBBFLVCUMCUOSZUVEIUJSZUVFQCIKURUMZCGMVCUMUULDFGUUSGUNZUUSUKUVIU
      KVDVGVEABCUULDEFGHIUUMJKLMNOPQRUUQVFVHUUKUUJEIVIUPZUUISZDUVJDUVJUUIVJUUKD
      UVJVKZTZUUMIUHUGZDVLZVMUAVNZVOVPZVMUBVNZVOVPZUCVNZUDVNZBUPZUVRVOVPZUVTDUG
      ZUWADUGZCUPZUVPVOVPZVQZUDUUMVRZTZUBVSVTZVQZUAVSVRZUCUUMVRZTZUUJUVMUWNUVOU
      VMUWMUCUUMUVMUVTUUMSZTZUWLUAVSUWQUVPVSSZUVQUWKUWQUWRUVQTZTZUVPDEIWAUPZUGZ
      WBUPZVSSZVMUXCVOVPZUWBUXCVOVPZUWGVQZUDUUMVRZUWKUVMUWRUXDUWPUVQUVMUWRTUWRU
      XBVSSZUXBVMVKZUXDUVMUWRWCUUKUXIUVLUWRUUPUVGUUKUXIPQADEUXAIUUMUVNUUQUVNUKZ
      UXAUKZOWDWEZWFUVLUXJUUKUWRUXBVMDUVJUXBVMUQZDUVJUQZDHSZUXNUXOWGRDEHUXAIUVJ
      UXLUVJUKZNPQWHUMWIWJWKUVPUXBWLXCWMUVMUWSUXEUWPUVMUWSTUWSUXIVMUXBVOVPZTZUX
      EUVMUWSWCUVMUXSUWSUUKUXIUVLUXRUXMUVLUXRUUPUVGUXPUVLUXRWGPQRDEHUXAIUVJUXLU
      XQNWNWOWIZWPWQUVPUXBWRWSWTUWTUXGUDUUMUWTUWAUUMSZUXFUWGUWTUYATZUXFUXBUWBXA
      UPZUVPVOVPZUWGUYBUYDUXFUYBUXIUWBVSSZUWRUXRUYDUXFWGUWQUXIUWSUYAUUKUXIUVLUW
      PUXMWFWFUWPUWSUYAUYEUVMUWPUYAUYEUWSUVAUWPUYAUYEUVBUVTUWABUUMUVCXBXDZWTXEU
      WQUWRUVQUYAXFZUWQUXRUWSUYAUVLUXRUUKUWPUXTWKWFUXBUWBUVPXGXHXIUYBUYDUWGUYBU
      WFUYCXJVPZUYDUWGUWQUYAUYHUWSUUKUWPUYAUYHUVLUUKUWPUYAUYHABCUVTUWADEUXAIUUM
      UVNUUQUXKJKUXLOPQXLXKXMWTUYBUWFVSSZUYCVSSZUWRUYHUYDTUWGVQUWQUYAUYIUWSUWPU
      YAUYIUVMUWPUWDUVNSZUWEUVNSZUYIUYAUUMUVNUVTDUUPUVGUXPUVOPQRDEHIUUMUVNUUQUX
      KNYBWOZXNUUMUVNUWADUYMXNUVFUYKUYLUYIUVHUWDUWECUVNCIUVNUXKKQUSZXBXDXOXPWTU
      WQUYAUYJUWSUUKUWPUYAUYJUVLUUKUWPUYAUYJUUKUXIUYEUYJUWPUYATUXMUYFUXBUWBXQXO
      XRXMWTUYGUWFUYCUVPXSXCXTYAYCYDYEUWJUXEUXHTUBUXCVSUVRUXCUQZUVSUXEUWIUXHUVR
      UXCVMVOYFUYOUWHUXGUDUUMUYOUWCUXFUWGUVRUXCUWBVOYFYGYHYIYJYKYLYEYMUYMYNUVAU
      VFUUJUWOWGUVBUVHUCUAUBUDBCDFGUUMUVNUVCLUYNMYOYPYQUVKUUKUVJUUMIUFUGZYRZYSZ
      UUIUUPUVGUVJUYRUQPQEUVJIUUMUYPUUQUYPUKZUXQYTYPUVAUVFUYPUVNSZUUMUYQUYRVLUY
      RUUISUVBUVHUVGUYTQIUVNUYPUXKUYSULUMUUMUYPIUFUUCUUDUYPBCUYRFGUUMUVNUVCUYNL
      MUUAUUBUUEUUFUUGUUH $.
      $( [18-Dec-2007] $)

    ${
      lnocni.1 $e |- X = ( BaseSet ` U ) $.
      $( If a linear operator is continuous at any point, it is continuous
         everywhere.  Theorem 2.7-9(b) of [Kreyszig] p. 97. $)
      lnocni $p |- ( ( P e. X /\ T e. ( ( J CnP K ) ` P ) ) ->
                T e. ( J Cn K ) ) $=
        ( wcel ccnp co cfv wa ccn blocnilem blocni sylibr ) DKUBEDGHUCUDUEUBUFE
        AUBEGHUGUDUBABCDEFGHIJKLMNOPQRSTUAUHABCEFGHIJLMNOPQRSTUIUJ $.
        $( [18-Dec-2007] $)
    $}
  $}

  ${
    blocn.8 $e |- C = ( IndMet ` U ) $.
    blocn.d $e |- D = ( IndMet ` W ) $.
    blocn.j $e |- J = ( MetOpen ` C ) $.
    blocn.k $e |- K = ( MetOpen ` D ) $.
    blocn.5 $e |- B = ( U BLnOp W ) $.
    blocn.u $e |- U e. NrmCVec $.
    blocn.w $e |- W e. NrmCVec $.
    ${
      blocn.4 $e |- L = ( U LnOp W ) $.
      $( A linear operator is continuous iff it is bounded.  Theorem 2.7-9(a)
         of [Kreyszig] p. 97. $)
      blocn $p |- ( T e. L -> ( T e. ( J Cn K ) <-> T e. B ) ) $=
        ( wcel co wb ccn c0o cif wceq eleq1 bibi12d cnv eqid 0lno elimel blocni
        mp2an dedth ) DHRZDFGUASZRZDARZTUNDEIUBSZUCZUORZUSARZTDURDUSUDUPUTUQVAD
        USUOUEDUSAUEUFABCUSEFGHIJKLMQNOPDURHEUGRIUGRURHROPEHIURURUHQUIULUJUKUM
        $.
        $( [25-Dec-2007] $)
    $}

    $( A bounded linear operator is continuous. $)
    blocn2 $p |- ( T e. B -> T e. ( J Cn K ) ) $=
      ( clno co wcel ccn cnv eqid bloln mp3an12 blocn biimprd mpcom ) DEHPQZRZD
      ARZDFGSQRZETRHTRUIUHNOADEUGHUGUAZMUBUCUHUJUIABCDEFGUGHIJKLMNOUKUDUEUF $.
      $( [25-Dec-2007] $)
  $}

  ${
    $d s t u w x y U $.  $d s t w x y W $.  $d s t u w x X $.  $d s t w y Y $.
    $d u w P $.  $d w Q $.
    ajfval.1 $e |- X = ( BaseSet ` U ) $.
    ajfval.2 $e |- Y = ( BaseSet ` W ) $.
    ajfval.3 $e |- P = ( .i ` U ) $.
    ajfval.4 $e |- Q = ( .i ` W ) $.
    ajfval.5 $e |- A = ( U adj W ) $.
    $( The adjoint function. $)
    ajfval $p |- ( ( U e. NrmCVec /\ W e. NrmCVec ) -> A =
     { <. t , s >. | ( t : X --> Y /\ s : Y --> X /\ A. x e. X A. y e. Y
    ( ( t ` x ) Q y ) = ( x P ( s ` y ) ) ) } ) $=
      ( co cv cfv cba vu vw cnv wcel wa caj wf wceq w3a copab cip fveq2 syl6eqr
      wral feq2d eqidd feq23d opreqd eqeq2d raleqbidv 3anbi123d opabbidv eqeq1d
      ralbidv df-aj cmap cxp oprex xpex cvv fvex eqeltri elmap anbi12i ssopab2i
      biimpri 3adant3 df-xp sseqtr4i ssexi ovmpt2 syl5eq ) GUCUDHUCUDUEDGHUFQIJ
      CRZUGZJIKRZUGZARZWCSZBRZFQZWGWIWESZEQZUHZBJUNZAIUNZUIZCKUJZPUAUBGHUCUCUAR
      ZTSZUBRZTSZWCUGZXAWSWEUGZWHWIWTUKSZQZWGWKWRUKSZQZUHZBXAUNZAWSUNZUIZCKUJWQ
      UFIXAWCUGZXAIWEUGZXEWLUHZBXAUNZAIUNZUIZCKUJWRGUHZXKXQCKXRXBXLXCXMXJXPXRWS
      IXAWCXRWSGTSZIWRGTULLUMZUOXRXAWSXAIWEXRXAUPXTUQXRXIXOAWSIXTXRXHXNBXAXRXGW
      LXEXRXFEWGWKXRXFGUKSEWRGUKULNUMURUSVDUTVAVBWTHUHZXQWPCKYAXLWDXMWFXPWOYAIX
      AIJWCYAIUPYAXAHTSZJWTHTULMUMZUQYAXAJIWEYCUOYAXOWNAIYAXNWMBXAJYCYAXEWJWLYA
      XDFWHWIYAXDHUKSFWTHUKULOUMURVCUTVDVAVBABUBUACKVEWQJIVFQZIJVFQZVGZYDYEJIVF
      VHIJVFVHVIWQWCYDUDZWEYEUDZUEZCKUJYFWPYICKWDWFYIWOYIWDWFUEYGWDYHWFJIWCJYBV
      JMHTVKVLZIXSVJLGTVKVLZVMIJWEYKYJVMVNVPVQVOCKYDYEVRVSVTWAWB $.
      $( [16-Nov-2013] $) $( [25-Jan-2008] $)
  $}

  ${
    $d o t u A $.  $d t T $.  $d o t u U $.
    hmoval.8 $e |- H = ( HmOp ` U ) $.
    hmoval.9 $e |- A = ( U adj U ) $.
    $( The set of Hermitian (self-adjoint) operators on a normed complex vector
       space. $)
    hmoval $p |- ( U e. NrmCVec -> H = { t e. dom A | ( A ` t ) = t } ) $=
      ( vu cnv wcel chmo cfv cv wceq cdm crab caj co opreq12 anidms syl6eqr cvv
      dmeqd fveq1d eqeq1d rabeqbidv df-hmo oprex eqeltri rabex fvmpt syl5eq
      dmex ) CHIDCJKALZBKZUMMZABNZOZEGCUMGLZURPQZKZUMMZAUSNZOUQHJURCMZVAUOAVBUP
      VCUSBVCUSCCPQZBVCUSVDMURCURCPRSFTZUBVCUTUNUMVCUMUSBVEUCUDUEGAUFUOAUPBBVDU
      AFCCPUGUHULUIUJUK $.
      $( [16-Nov-2013] $) $( [26-Jan-2008] $)

    $( The predicate "is a hermitian operator." $)
    ishmo $p |- ( U e. NrmCVec ->
      ( T e. H <-> ( T e. dom A /\ ( A ` T ) = T ) ) ) $=
      ( vt cnv wcel cv cfv wceq cdm crab wa hmoval eleq2d fveq2 id eqeq12d
      elrab syl6bb ) CHIZBDIBGJZAKZUDLZGAMZNZIBUGIBAKZBLZOUCDUHBGACDEFPQUFUJGBU
      GUDBLZUEUIUDBUDBARUKSTUAUB $.
      $( [29-Apr-2009] $) $( [26-Jan-2008] $)
  $}


