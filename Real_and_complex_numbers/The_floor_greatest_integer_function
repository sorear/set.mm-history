$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The floor (greatest integer) function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c |_ $.  $( Floor function. $)

  $( Extend class notation with floor (greatest integer) function. $)
  cfl $a class |_ $.

  ${
    $d x y $.
    $( Define the floor (greatest integer) function.  See ~ flval for its
       value, ~ fllelt for its basic property, and ~ flcl for its closure.

       The term "floor" was coined by Ken Iverson.  He also invented a
       mathematical notation for floor, consisting of an L-shaped left bracket
       and its reflection as a right bracket.  In APL, the left-bracket alone
       is used, and we borrow this idea.  (Thanks to Paul Chapman for this
       information.) $)
    df-fl $a |- |_ = ( x e. RR |->
                ( iota_ y e. ZZ ( y <_ x /\ x < ( y + 1 ) ) ) ) $.
  $}

  ${
    $d x y A $.
    $( Value of the floor (greatest integer) function.  The floor of ` A ` is
       the (unique) integer less than or equal to ` A ` whose successor is
       strictly greater than ` A ` . $)
    flval $p |- ( A e. RR ->
              ( |_ ` A ) = ( iota_ x e. ZZ ( x <_ A /\ A < ( x + 1 ) ) ) ) $=
      ( vy cv cle wbr c1 caddc co clt wa cz crio cfl wceq breq2 breq1 riotabidv
      cr anbi12d df-fl riotaex fvmpt ) CBADZCDZEFZUEUDGHIZJFZKZALMUDBEFZBUGJFZK
      ZALMSNUEBOZUIULALUMUFUJUHUKUEBUDEPUEBUGJQTRCAUAULALUBUC $.
      $( [2-Nov-2013] $) $( [14-Nov-2004] $)

    $( The floor (greatest integer) function is an integer (closure law). $)
    flcl $p |- ( A e. RR -> ( |_ ` A ) e. ZZ ) $=
      ( vx cr wcel cfl cfv cv cle wbr c1 caddc co wa cz crio flval wreu rebtwnz
      clt riotacl syl eqeltrd ) ACDZAEFBGZAHIAUDJKLSIMZBNOZNBAPUCUEBNQUFNDBARUE
      BNTUAUB $.
      $( [2-Nov-2013] $) $( [15-Nov-2004] $)

    $( The floor (greatest integer) function is real. $)
    reflcl $p |- ( A e. RR -> ( |_ ` A ) e. RR ) $=
      ( cr wcel cfl cfv cz flcl zre syl ) ABCADEZFCJBCAGJHI $.
      $( [15-Jul-2008] $)

    $( A basic property of the floor (greatest integer) function. $)
    fllelt $p |- ( A e. RR ->
                  ( ( |_ ` A ) <_ A /\ A < ( ( |_ ` A ) + 1 ) ) ) $=
      ( vx vy cr wcel cfl cfv cle wbr c1 caddc co clt wa cv cz crio wceq eqcomd
      flval wreu wb flcl rebtwnz ax-17 a17d breq1 opreq1 breq2d anbi12d riota2f
      syl2anc mpbird ) ADEZAFGZAHIZAUOJKLZMIZNZBOZAHIZAUTJKLZMIZNZBPQZUORZUNUOV
      EBATSUNUOPEZVDBPUAUSVFUBAUCBAUDVDUSBCPUOCOUOEBUEVGUSBUFUTUORZVAUPVCURUTUO
      AHUGVHVBUQAMUTUOJKUHUIUJUKULUM $.
      $( [2-Nov-2013] $) $( [15-Nov-2004] $)
  $}

  $( A basic property of the floor (greatest integer) function. $)
  flle $p |- ( A e. RR -> ( |_ ` A ) <_ A ) $=
    ( cr wcel cfl cfv cle wbr c1 caddc co clt fllelt simpld ) ABCADEZAFGANHIJKG
    ALM $.
    $( [24-Feb-2005] $)

  $( A basic property of the floor (greatest integer) function. $)
  flltp1 $p |- ( A e. RR -> A < ( ( |_ ` A ) + 1 ) ) $=
    ( cr wcel cfl cfv cle wbr c1 caddc co clt fllelt simprd ) ABCADEZAFGANHIJKG
    ALM $.
    $( [24-Feb-2005] $)

  $( The fractional part of a real number is less than one. $)
  fraclt1 $p |- ( A e. RR -> ( A - ( |_ ` A ) ) < 1 ) $=
    ( cr wcel cfl cfv co c1 clt wbr caddc flltp1 wb reflcl 1re ltsubadd2 mp3an3
    cmin mpdan mpbird ) ABCZAADEZQFGHIZAUAGJFHIZAKTUABCZUBUCLZAMTUDGBCUENAUAGOP
    RS $.
    $( [15-Jul-2008] $)

  $( The fractional part of a real number is nonnegative. $)
  fracge0 $p |- ( A e. RR -> 0 <_ ( A - ( |_ ` A ) ) ) $=
    ( cr wcel cc0 cfl cfv cmin co cle wbr flle wb reflcl subge0 mpdan mpbird )
    ABCZDAAEFZGHIJZRAIJZAKQRBCSTLAMARNOP $.
    $( [17-Jul-2008] $)

  $( The floor function value is the greatest integer less than or equal to its
     argument. $)
  flge $p |- ( ( A e. RR /\ B e. ZZ ) -> ( B <_ A <-> B <_ ( |_ ` A ) ) ) $=
    ( cz cr cle wbr cfl cfv wb wa c1 caddc co clt adantl wi 3expb syl2an mpan2d
    wcel flltp1 zre reflcl peano2re syl lelttr flcl zleltp1 sylan2 sylibrd flle
    ancli ancri letr impbid ancoms ) BCTZADTZBAEFZBAGHZEFZIUQURJZUSVAVBUSBUTKLM
    ZNFZVAVBUSAVCNFZVDURVEUQAUAOUQBDTZURVCDTZJUSVEJVDPZURBUBZURVGURUTDTZVGAUCZU
    TUDUEULVFURVGVHBAVCUFQRSURUQUTCTVAVDIAUGBUTUHUIUJVBVAUTAEFZUSURVLUQAUKOUQVF
    VJURJVAVLJUSPZURVIURVJVKUMVFVJURVMBUTAUNQRSUOUP $.
    $( [15-Nov-2004] $)

  $( The floor function value is less than the next integer. $)
  fllt $p |- ( ( A e. RR /\ B e. ZZ ) -> ( A < B <-> ( |_ ` A ) < B ) ) $=
    ( cr wcel cz wa clt wbr cfl cfv wn flge wb lenlt sylan ancoms reflcl syl2an
    cle zre 3bitr3d con4bid ) ACDZBEDZFZABGHZAIJZBGHZUEBASHZBUGSHZUFKZUHKZABLUD
    UCUIUKMZUDBCDZUCUMBTZBANOPUDUCUJULMZUDUNUGCDUPUCUOAQBUGNRPUAUB $.
    $( [24-Feb-2005] $)

  $( An integer is its own floor. $)
  flid $p |- ( A e. ZZ -> ( |_ ` A ) = A ) $=
    ( cz wcel cfl cfv wceq cle wbr cr wa wb zre reflcl letri3 syl2anc flle leid
    syl flge mpancom mpbid mpbir2and ) ABCZADEZAFZUDAGHZAUDGHZUCUDICZAICZUEUFUG
    JKUCUIUHALZAMRUJUDANOUCUIUFUJAPRUCAAGHZUGUCUIUKUJAQRUIUCUKUGKUJAASTUAUB $.
    $( [15-Nov-2004] $)

  $( The floor function is idempotent. $)
  flidm $p |- ( A e. RR -> ( |_ ` ( |_ ` A ) ) = ( |_ ` A ) ) $=
    ( cr wcel cfl cfv cz wceq flcl flid syl ) ABCADEZFCKDEKGAHKIJ $.
    $( [17-Aug-2008] $)

  $( A real number equals its floor iff it is an integer. $)
  flidz $p |- ( A e. RR -> ( ( |_ ` A ) = A <-> A e. ZZ ) ) $=
    ( cr wcel cfl cfv wceq cz flcl eleq1 syl5ibcom flid impbid1 ) ABCZADEZAFZAG
    CZMNGCOPAHNAGIJAKL $.
    $( [11-Nov-2008] $)

  $( Ordering relationship for the greatest integer function. $)
  flwordi $p |- ( ( A e. RR /\ B e. RR /\ A <_ B ) ->
                 ( |_ ` A ) <_ ( |_ ` B ) ) $=
    ( cr wcel cle wbr cfl cfv wa flle adantr wi reflcl simpl simpr letr syl3anc
    mpand wb cz flcl flge sylan2 ancoms sylibd 3impia ) ACDZBCDZABEFZAGHZBGHEFZ
    UGUHIZUIUJBEFZUKULUJAEFZUIUMUGUNUHAJKULUJCDZUGUHUNUIIUMLUGUOUHAMKUGUHNUGUHO
    UJABPQRUHUGUMUKSZUGUHUJTDUPAUABUJUBUCUDUEUF $.
    $( [31-Dec-2005] $)

  ${
    $d x y z w A $.  $d x y B $.
    $( An alternate way to define the floor (greatest integer) function. $)
    flval2 $p |- ( A e. RR -> ( |_ ` A ) =
            U. { x e. ZZ | ( x <_ A /\ A. y e. ZZ ( y <_ A -> y <_ x ) ) } ) $=
      ( cr wcel cv cle wbr wi cz wral wa crab cuni cfl cfv wceq wreu wb flcl
      zmax breq1 breq2 imbi2d ralbidv anbi12d reuuni2 flle flge biimpd mpbi2and
      syl2anc ralrimiva eqcomd ) CDEZAFZCGHZBFZCGHZURUPGHZIZBJKZLZAJMNZCOPZUOVE
      CGHZUSURVEGHZIZBJKZVDVEQZUOVEJEVCAJRVFVILZVJSCTABCUAVCVKAJVEUPVEQZUQVFVBV
      IUPVECGUBVLVAVHBJVLUTVGUSUPVEURGUCUDUEUFUGULCUHUOVHBJUOURJELUSVGCURUIUJUM
      UKUN $.
      $( [16-Nov-2004] $)

    $( An alternate way to define the floor (greatest integer) function, as the
       supremum of all integers less than or equal to its argument. $)
    flval3 $p |- ( A e. RR ->
                  ( |_ ` A ) = sup ( { x e. ZZ | x <_ A } , RR , < ) ) $=
      ( vz vy vw cr wcel cv cle wbr cz clt wceq wn wa wb wral breq1 elrab flge
      cfl cfv crab csup reflcl wss c0 wne wrex ssrab2 zssre sstri a1i flcl flle
      sylanbrc syl biimpd expimpd syl5bi ralrimiv breq2 ralbidv rcla4ev syl2anc
      ne0i suprcl syl3anc eqlelt suprub syl31anc zre lenlt syl2an bitrd suprnub
      ancoms syl22anc mpbir2and ) BFGZBUAUBZAHZBIJZAKUCZFLUDZMZWAWEIJZWAWELJNZV
      TWAFGZWEFGZWFWGWHOPBUEZVTWDFUFZWDUGUHZCHZDHZIJZCWDQZDFUIZWJWLVTWDKFWCAKUJ
      UKULUMZVTWAWDGZWMVTWAKGWABIJZWTBUNBUOWCXAAWAKWBWABIRSUPZWDWAVFUQZVTWIWNWA
      IJZCWDQZWRWKVTXDCWDWNWDGWNKGZWNBIJZOVTXDWCXGAWNKWBWNBIRSVTXFXGXDVTXFOXGXD
      BWNTURUSUTVAWQXEDWAFWOWAMWPXDCWDWOWAWNIVBVCVDVEZDCWDVGVHWAWEVIVEVTWLWMWRW
      TWGWSXCXHXBDCWDWAVJVKVTWLWMWIWAEHZLJNZEWDQWHWSXCWKVTXJEWDXIWDGXIKGZXIBIJZ
      OVTXJWCXLAXIKWBXIBIRSVTXKXLXJVTXKOZXLXJXMXLXIWAIJZXJBXITXKVTXNXJPZXKXIFGW
      IXOVTXIVLWKXIWAVMVNVQVOURUSUTVAEWDWAVPVRVS $.
      $( [15-Nov-2004] $)

    $( A condition equivalent to floor. $)
    flbi $p |- ( ( A e. RR /\ B e. ZZ ) ->
                ( ( |_ ` A ) = B <-> ( B <_ A /\ A < ( B + 1 ) ) ) ) $=
      ( vx vy cr wcel cz wa cfl cfv wceq cv cle wbr c1 caddc co clt crio wb
      flval eqeq1d adantr wreu rebtwnz ax-17 a17d opreq1 breq2d anbi12d riota2f
      breq1 sylan2 ancoms bitr4d ) AEFZBGFZHAIJZBKZCLZAMNZAUTOPQZRNZHZCGSZBKZBA
      MNZABOPQZRNZHZUPUSVFTUQUPURVEBCAUAUBUCUQUPVJVFTZUPUQVDCGUDVKCAUEVDVJCDGBD
      LBFCUFUQVJCUGUTBKZVAVGVCVIUTBAMULVLVBVHARUTBOPUHUIUJUKUMUNUO $.
      $( [2-Nov-2013] $) $( [11-Mar-2005] $)
  $}

  $( A condition equivalent to floor. $)
  flbi2 $p |- ( ( N e. ZZ /\ F e. RR ) ->
              ( ( |_ ` ( N + F ) ) = N <-> ( 0 <_ F /\ F < 1 ) ) ) $=
    ( cz wcel cr wa caddc co cfl cfv wceq cle wbr c1 clt cc0 zre readdcl sylan
    wb simpl flbi syl2anc addge01 1re ltadd2 mp3an2 ancoms anbi12d bitr4d ) BCD
    ZAEDZFZBAGHZIJBKZBUNLMZUNBNGHOMZFZPALMZANOMZFZUMUNEDZUKUOURTUKBEDZULVBBQZBA
    RSUKULUAUNBUBUCUKVCULVAURTVDVCULFUSUPUTUQBAUDULVCUTUQTZULNEDVCVEUEANBUFUGUH
    UISUJ $.
    $( [15-Aug-2008] $)

  $( The floor of a number greater than or equal to 0 is a nonnegative
     integer. $)
  flge0nn0 $p |- ( ( A e. RR /\ 0 <_ A ) -> ( |_ ` A ) e. NN0 ) $=
    ( cr wcel cc0 cle wbr wa cfl cfv cz cn0 flcl adantr wb 0z flge mpan2 biimpa
    elnn0z sylanbrc ) ABCZDAEFZGAHIZJCZDUCEFZUCKCUAUDUBALMUAUBUEUADJCUBUENOADPQ
    RUCST $.
    $( [26-Apr-2005] $)

  $( The floor of a number greater than or equal to 1 is a natural number. $)
  flge1nn $p |- ( ( A e. RR /\ 1 <_ A ) -> ( |_ ` A ) e. NN ) $=
    ( cr wcel c1 cle wbr wa cfl cfv cz cn flcl adantr wb 1z mpan2 biimpa elnnz1
    flge sylanbrc ) ABCZDAEFZGAHIZJCZDUCEFZUCKCUAUDUBALMUAUBUEUADJCUBUENOADSPQU
    CRT $.
    $( [26-Apr-2005] $)

  $( An integer can be moved in and out of the floor of a sum. $)
  fladdz $p |- ( ( A e. RR /\ N e. ZZ ) -> ( |_ ` ( A + N ) ) =
                ( ( |_ ` A ) + N ) ) $=
    ( cr wcel cz wa caddc co cfl cfv wceq cle wbr c1 clt wb adantr 3expa syl2an
    cc zre readdcl sylan2 flcl zaddcl sylan flbi flle reflcl ancri leadd1 mpbid
    syl2anc flltp1 peano2re syl ancli ltadd1 recnd zcn ax-1cn breqtrd mpbir2and
    add32 mp3an2 ) ACDZBEDZFZABGHZIJAIJZBGHZKZVKVILMZVIVKNGHZOMZVHVICDZVKEDZVLV
    MVOFPVGVFBCDZVPBUAZABUBUCVFVJEDVGVQAUDVJBUEUFVIVKUGUMVHVJALMZVMVFVTVGAUHQVF
    VJCDZVFFVRVTVMPZVGVFWAAUIZUJVSWAVFVRWBVJABUKRSULVHVIVJNGHZBGHZVNOVHAWDOMZVI
    WEOMZVFWFVGAUNQVFVFWDCDZFVRWFWGPZVGVFWHVFWAWHWCVJUOUPUQVSVFWHVRWIAWDBURRSUL
    VFVJTDZBTDZWEVNKZVGVFVJWCUSBUTWJNTDWKWLVAVJNBVDVESVBVC $.
    $( [27-Apr-2005] $)

  $( An integer can be moved in and out of the floor of a sum. $)
  flzadd $p |- ( ( N e. ZZ /\ A e. RR ) -> ( |_ ` ( N + A ) ) =
                ( N + ( |_ ` A ) ) ) $=
    ( cr wcel cz caddc co cfl cfv wceq wa fladdz cc addcom syl2an fveq2d reflcl
    recn zcn recnd 3eqtr3d ancoms ) ACDZBEDZBAFGZHIZBAHIZFGZJUCUDKZABFGZHIUGBFG
    ZUFUHABLUIUJUEHUCAMDBMDZUJUEJUDARBSZABNOPUCUGMDULUKUHJUDUCUGAQTUMUGBNOUAUB
    $.
    $( [2-Jan-2009] $)

  $( A real bounded between an integer and its successor is nonnegative iff the
     integer is nonnegative.  Second half of Lemma 13-4.1 of [Gleason] p. 217.
     (For the first half see ~ rebtwnz .) $)
  btwnzge0 $p |- ( ( ( A e. RR /\ N e. ZZ ) /\ ( N <_ A /\ A < ( N + 1 ) ) )
                -> ( 0 <_ A <-> 0 <_ N ) ) $=
    ( cr wcel cz wa cle wbr c1 caddc co clt cc0 cfl cfv wb flge mpan2 ad2antrr
    0z wceq flbi biimpar breq2d bitrd ) ACDZBEDZFZBAGHABIJKLHFZFZMAGHZMANOZGHZM
    BGHUFUKUMPZUGUIUFMEDUNTAMQRSUJULBMGUHULBUAUIABUBUCUDUE $.
    $( [12-Mar-2005] $)

  $( Ordering relation for the floor of half of an integer. $)
  flhalf $p |- ( N e. ZZ -> N <_ ( 2 x. ( |_ ` ( ( N + 1 ) / 2 ) ) ) ) $=
    ( cz wcel c2 cdiv co cfl cfv cmul cle wbr wa cc cc0 syl zre 3syl adantr 2re
    cr c1 caddc wo zeo flid opreq2d wceq zcn wne 2ne0 divcan2 mp3an23 sylan9eqr
    2cn rehalfcl flcl peano2re lep1 clt 2pos pm3.2i lediv1 mp3an3 mpbid syl2anc
    wb flwordi syl3anc ltleii lemul2a mp3anl3 syl21anc eqbrtrrd peano2cn jaodan
    0re breqtrrd mpdan ) ABCZADEFZBCZAUAUBFZDEFZBCZUCADWCGHZIFZJKZAUDVSWAWGWDVS
    WALDVTGHZIFZAWFJWAVSWIDVTIFZAWAWHVTDIVTUEUFVSAMCZWJAUGZAUHZWKDMCZDNUIZWLUNU
    JADUKULOUMVSWIWFJKZWAVSWHTCZWETCZWHWEJKZWPVSVTTCZWHBCWQVSATCZWTAPZAUOOZVTUP
    WHPQVSWCTCZWEBCWRVSXAWBTCZXDXBAUQZWBUOQZWCUPWEPQVSWTXDVTWCJKZWSXCXGVSXAXEXH
    XBVSXAXEXBXFOXAXELAWBJKZXHXAXIXEAURZRXAXEDTCZNDUSKZLXIXHVFXKXLSUTVAAWBDVBVC
    VDVEVTWCVGVHWQWRXKNDJKZLWSWPXKXMSNDVPSUTVIVAWHWEDVJVKVLRVMVSWDLAWBWFJVSXIWD
    VSXAXIXBXJORWDVSWFDWCIFZWBWDWEWCDIWCUEUFVSWKWBMCZXNWBUGZWMAVNXOWNWOXPUNUJWB
    DUKULQUMVQVOVR $.
    $( [1-Jan-2006] $)

  $( The ceiling function returns an integer (closure law).  (Contributed by
     Jeffrey Hankins, 10-Jun-2007.) $)
  ceicl $p |- ( A e. RR -> -u ( |_ ` -u A ) e. ZZ ) $=
    ( cr wcel cneg cfl cfv cz renegcl flcl znegcl 3syl ) ABCADZBCLEFZGCMDGCAHLI
    MJK $.
    $( [8-Jun-2007] $)

  $( The ceiling of a real number is greater than or equal to that number.
     (Contributed by Jeffrey Hankins, 10-Jun-2007.) $)
  ceige $p |- ( A e. RR -> A <_ -u ( |_ ` -u A ) ) $=
    ( wcel cneg cfl cfv cle wbr renegcl reflcl syl flle adantr lenegcon2 mpbird
    cr wa mpdan ) AOBZACZDEZOBZATCFGZRSOBZUAAHZSIJRUAPUBTSFGZRUEUARUCUEUDSKJLAT
    MNQ $.
    $( [9-Jun-2007] $)

  $( One less than the ceiling of a real number is strictly less than that
     number.  (Contributed by Jeffrey Hankins, 10-Jun-2007.) $)
  ceim1l $p |- ( A e. RR -> ( -u ( |_ ` -u A ) - 1 ) < A ) $=
    ( cr wcel cneg cfl cfv c1 cmin co caddc cc wceq renegcl reflcl recnd ax-1cn
    clt syl negdi wbr sylancl negcl negsub mpan2 3syl eqtr2d peano2re wa flltp1
    adantr ltnegcon1 mpbid mpdan eqbrtrd ) ABCZADZEFZDZGHIZUQGJIZDZAQUOVAURGDJI
    ZUSUOUQKCZGKCZVAVBLUOUQUOUPBCZUQBCZAMZUPNRZOZPUQGSUAUOVCURKCZVBUSLZVIUQUBVJ
    VDVKPURGUCUDUEUFUOUTBCZVAAQTZUOVFVLVHUQUGRUOVLUHUPUTQTZVMUOVNVLUOVEVNVGUPUI
    RUJAUTUKULUMUN $.
    $( [9-Jun-2007] $)

  $( The ceiling of a real number is the smallest integer greater than or equal
     to it.  (Contributed by Jeffrey Hankins, 10-Jun-2007.) $)
  ceile $p |- ( ( A e. RR /\ B e. ZZ /\ A <_ B ) -> -u ( |_ ` -u A ) <_ B ) $=
    ( cr wcel cz cle wbr cneg cfl cfv wa c1 cmin co clt ceim1l adantr ceicl zre
    wi peano2rem 3syl simpl adantl ltletr syl3anc mpand wb zlem1lt sylan 3impia
    sylibrd ) ACDZBEDZABFGZAHIJHZBFGZUMUNKZUOUPLMNZBOGZUQURUSAOGZUOUTUMVAUNAPQU
    RUSCDZUMBCDZVAUOKUTTUMVBUNUMUPEDZUPCDVBARZUPSUPUAUBQUMUNUCUNVCUMBSUDUSABUEU
    FUGUMVDUNUQUTUHVEUPBUIUJULUK $.
    $( [10-Jun-2007] $)

  ${
    quorem.1 $e |- Q = ( |_ ` ( A / B ) ) $.
    quorem.2 $e |- R = ( A - ( B x. Q ) ) $.
    $( MAY BE REVISED - is this really needed for anything?  Should we use
       mod to simplify it? $)
    $( Quotient and remainder of an integer divided by a natural number. $)
    quoremz $p |- ( ( A e. ZZ /\ B e. NN ) -> ( ( Q e. ZZ /\ R e. NN0 )
          /\ ( R < B /\ A = ( ( B x. Q ) + R ) ) ) ) $=
      ( cz wcel clt wbr co wceq cdiv cr adantl syl3anc syl cmin cle cc cn0 cmul
      cn wa caddc cfl cfv cc0 wne zre adantr nnre nnne0 redivcl syl5eqel zmulcl
      flcl nnz syl2anc simpl nncn divcan3 flle syl5eqbr eqbrtrd wb nngt0 lediv1
      zcn syl112anc mpbird znn0sub2 opreq2i fraclt1 opreq1i jca divsubdir eqtrd
      c1 opreq2d syl5eq divid 3brtr4d nn0re ltdiv1 pncan3 syl5req jca31 ) AGHZB
      UCHZUDZCGHZDUAHZDBIJZABCUBKZDUEKZLZUDWKCABMKZUFUGZGEWKWRNHZWSGHWKANHZBNHZ
      BUHUIZWTWIXAWJAUJUKZWJXBWIBULOZWJXCWIBUMZOZABUNPZWRUQQUOZWKDAWORKZUAFWKWO
      GHZWIWOASJZXJUAHWKBGHZWLXKWJXMWIBUROXIBCUPUSZWIWJUTWKXLWOBMKZWRSJZWKXOCWR
      SWKCTHZBTHZXCXOCLWKWLXQXICVIQWJXRWIBVAZOXGCBVBPZWKCWSWRSEWKWTWSWRSJXHWRVC
      QVDVEWKWONHZXAXBUHBIJZXLXPVFWKXKYAXNWOUJQXDXEWJYBWIBVGOZWOABVHVJVKWOAVLPU
      OZWKWNWQWKWNDBMKZBBMKZIJZWKWRCRKZVSYEYFIWKYHWRWSRKZVSICWSWRREVMWKWTYIVSIJ
      XHWRVNQVDWKYEXJBMKZYHDXJBMFVOWKYJWRXORKZYHWKATHZWOTHZXRXCUDZYJYKLWIYLWJAV
      IUKZWKXKYMXNWOVIQZWJYNWIWJXRXCXSXFVPOAWOBVQPWKXOCWRRXTVTVRWAWJYFVSLZWIWJX
      RXCYQXSXFBWBUSOWCWKDNHZXBXBYBWNYGVFWKWMYRYDDWDQXEXEYCDBBWEVJVKWKWPWOXJUEK
      ZADXJWOUEFVMWKYMYLYSALYPYOWOAWFUSWGVPWH $.
      $( [14-Aug-2008] $)

    $( Quotient and remainder of a nonnegative integer divided by a natural
       number. $)
    quoremnn0ALT $p |- ( ( A e. NN0 /\ B e. NN ) -> ( ( Q e. NN0 /\ R e. NN0 )
          /\ ( R < B /\ A = ( ( B x. Q ) + R ) ) ) ) $=
      ( cn0 wcel wa cz clt wbr co cr cc0 cle adantl jca syl2anc anim1i cn caddc
      cmul wceq cdiv cfl cfv wne nn0re adantr nnre nnne0 redivcl syl3anc nn0ge0
      nngt0 divge0 syl2an flge0nn0 syl5eqel nn0z quoremz sylan simpl anasss ) A
      GHZBUAHZIZCGHZCJHZDGHZIZDBKLABCUCMDUBMUDIZIZVIVKIZVMIZVHCABUEMZUFUGZGEVHV
      QNHZOVQPLZVRGHVHANHZBNHZBOUHZVSVFWAVGAUIZUJVGWBVFBUKZQVGWCVFBULQABUMUNVFW
      AOAPLZIWBOBKLZIVTVGVFWAWFWDAUORVGWBWGWEBUPRABUQURVQUSSUTVFAJHVGVNAVAABCDE
      FVBVCVIVLVMVPVIVLIVOVMVIVJVKVOVIVJIVIVKVIVJVDTVETVES $.
      $( [14-Aug-2008] $)

    $( MAY BE REVISED - keep either quoremnn0ALT or quoremnn0 (if we don't
       keep quoremz $)
    $( Quotient and remainder of a nonnegative integer divided by a natural
       number. $)
    quoremnn0 $p |- ( ( A e. NN0 /\ B e. NN ) -> ( ( Q e. NN0 /\ R e. NN0 )
          /\ ( R < B /\ A = ( ( B x. Q ) + R ) ) ) ) $=
      ( cn0 wcel wa clt wbr co wceq cdiv cr cc0 cle adantl cmin syl cn cmul cfl
      caddc cfv wne nn0re adantr nnre nnne0 redivcl syl3anc nn0ge0 nngt0 divge0
      jca syl2an flge0nn0 syl2anc syl5eqel nnnn0 nn0mulcl simpl cc nncn divcan3
      nn0cn flle syl5eqbr eqbrtrd wb lediv1 syl112anc mpbird nn0sub2 c1 opreq2i
      fraclt1 opreq1i divsubdir opreq2d eqtrd syl5eq divid 3brtr4d ltdiv1 jca31
      pncan3 syl5req ) AGHZBUAHZIZCGHZDGHZDBJKZABCUBLZDUDLZMZIWLCABNLZUCUEZGEWL
      WSOHZPWSQKZWTGHWLAOHZBOHZBPUFZXAWJXCWKAUGZUHZWKXDWJBUIZRZWKXEWJBUJZRZABUK
      ULZWJXCPAQKZIXDPBJKZIXBWKWJXCXMXFAUMUPWKXDXNXHBUNZUPABUOUQWSURUSUTZWLDAWP
      SLZGFWLWPGHZWJWPAQKZXQGHWLBGHZWMXRWKXTWJBVARXPBCVBUSZWJWKVCWLXSWPBNLZWSQK
      ZWLYBCWSQWLCVDHZBVDHZXEYBCMWLWMYDXPCVGTWKYEWJBVEZRXKCBVFULZWLCWTWSQEWLXAW
      TWSQKXLWSVHTVIVJWLWPOHZXCXDXNXSYCVKWLXRYHYAWPUGTXGXIWKXNWJXORZWPABVLVMVNW
      PAVOULUTZWLWOWRWLWODBNLZBBNLZJKZWLWSCSLZVPYKYLJWLYNWSWTSLZVPJCWTWSSEVQWLX
      AYOVPJKXLWSVRTVIWLYKXQBNLZYNDXQBNFVSWLYPWSYBSLZYNWLAVDHZWPVDHZYEXEIZYPYQM
      WJYRWKAVGUHZWLXRYSYAWPVGTZWKYTWJWKYEXEYFXJUPRAWPBVTULWLYBCWSSYGWAWBWCWKYL
      VPMZWJWKYEXEUUCYFXJBWDUSRWEWLDOHZXDXDXNWOYMVKWLWNUUDYJDUGTXIXIYIDBBWFVMVN
      WLWQWPXQUDLZADXQWPUDFVQWLYSYRUUEAMUUBUUAWPAWHUSWIUPWG $.
      $( [14-Aug-2008] $)
  $}

  ${
    intfrac2.1 $e |- Z = ( |_ ` A ) $.
    intfrac2.2 $e |- F = ( A - Z ) $.
    $( MAY BE REVISED - should we replace this with ~ intfrac ? $)
    $( Decompose a real into integer and fractional parts. $)
    intfrac2 $p |- ( A e. RR -> ( 0 <_ F /\ F < 1 /\ A = ( Z + F ) ) ) $=
      ( cr wcel cc0 cle wbr c1 clt caddc co wceq cfl cmin opreq2i cc cz fracge0
      cfv eqtri syl6breqr fraclt1 syl5eqbr flcl syl5eqel zcn syl pncan3 syl2anc
      recn syl5req 3jca ) AFGZHBIJBKLJACBMNZOUPHAAPUBZQNZBIAUABACQNZUSECURAQDRU
      CZUDUPBUSKLVAAUEUFUPUQCUTMNZABUTCMERUPCSGZASGVBAOUPCTGVCUPCURTDAUGUHCUIUJ
      AUMCAUKULUNUO $.
      $( [16-Aug-2008] $)
  $}

  ${
    intfracq.1 $e |- Z = ( |_ ` ( M / N ) ) $.
    intfracq.2 $e |- F = ( ( M / N ) - Z ) $.
    $( Decompose a rational number, expressed as a ratio, into integer and
       fractional parts.  The fractional part has a tighter bound than that of
       ~ intfrac2 . $)
    intfracq $p |- ( ( M e. ZZ /\ N e. NN )
         -> ( 0 <_ F /\ F <_ ( ( N - 1 ) / N ) /\ ( M / N ) = ( Z + F ) ) ) $=
      ( cz wcel wbr c1 cmin co wceq clt cr adantl syl3anc syl cmul syl2anc cdiv
      cn wa cc0 cle caddc w3a wne zre adantr nnre nnne0 redivcl intfrac2 simp1d
      cfl cfv fraclt1 opreq2i eqtri a1i cc nncn divid 3brtr4d wb reflcl resubcl
      syl5eqel nngt0 jca ltmuldiv2 mpbird recnd flcl subdi syl5eq divcan2 simpl
      zcn eqeltrd zmulcl zsubcl zltlem1 mpbid peano2rem lemuldiv2 simp3d 3jca
      nnz ) BGHZCUBHZUCZUDAUEIZACJKLZCUALUEIZBCUALZDAUFLMZWMWNAJNIZWRWMWQOHZWNW
      SWRUGWMBOHZCOHZCUDUHZWTWKXAWLBUIUJWLXBWKCUKZPZWLXCWKCULZPZBCUMQZWQADEFUNR
      ZUOWMCASLZWOUEIZWPWMXJCNIZXKWMXLACCUALZNIZWMWQWQUPUQZKLZJAXMNWMWTXPJNIXHW
      QURRAXPMWMAWQDKLZXPFDXOWQKEUSUTVAWLXMJMZWKWLCVBHZXCXRCVCZXFCVDTPVEWMAOHZX
      BXBUDCNIZUCZXLXNVFWMAXQOFWMWTDOHXQOHXHWMDXOOEWMWTXOOHXHWQVGRVIWQDVHTVIZXE
      WLYCWKWLXBYBXDCVJVKPZACCVLQVMWMXJGHCGHZXLXKVFWMXJCWQSLZCDSLZKLZGWMXJCXQSL
      ZYIAXQCSFUSWMXSWQVBHDVBHZYJYIMWLXSWKXTPZWMWQXHVNWMDGHZYKWMDXOGEWMWTXOGHXH
      WQVORVIZDVTRCWQDVPQVQWMYGGHYHGHZYIGHWMYGBGWMBVBHZXSXCYGBMWKYPWLBVTUJYLXGB
      CVRQWKWLVSWAWMYFYMYOWLYFWKCWJPZYNCDWBTYGYHWCTWAYQXJCWDTWEWMYAWOOHZYCXKWPV
      FYDWLYRWKWLXBYRXDCWFRPYEAWOCWGQWEWMWNWSWRXIWHWI $.
      $( [16-Aug-2008] $)
  $}

  $( Cancellation of the embedded floor of a real divided by an integer. $)
  fldiv $p |- ( ( A e. RR /\ N e. NN )
                  -> ( |_ ` ( ( |_ ` A ) / N ) ) = ( |_ ` ( A / N ) ) ) $=
    ( cr wcel wa cdiv co cfl caddc wceq cc0 cle wbr c1 clt adantr cc jca adantl
    recnd cn cfv cmin eqid intfrac2 simp3d opreq1d wne resubcl mpdan nncn nnne0
    reflcl divdir syl3anc eqtrd cz flcl intfracq sylan nnre redivcl syl syl2anc
    addass 3eqtrd fveq2d wb readdcl flbi2 simp1d fracge0 divge0 syl2an syl22anc
    nngt0 addge0 peano2rem nnrecre jca31 simp2d fraclt1 1re ltdiv1 mp3an2 mpbid
    leltadd sylc ax-1cn npcan sylancl syl12anc 3eqtr3d breqtrd mpbir2and eqtr2d
    divid ) ACDZBUADZEZABFGZHUBAHUBZBFGZHUBZXCXDUCGZAXBUCGZBFGZIGZIGZHUBZXDWTXA
    XIHWTXAXCXGIGZXDXEIGZXGIGZXIWTXAXBXFIGZBFGZXKWTAXNBFWRAXNJZWSWRKXFLMZXFNOMZ
    XPAXFXBXBUDXFUDUEUFPUGWTXBQDZXFQDZBQDZBKUHZEZXOXKJWRXSWSWRXBAUMZTPWRXTWSWRX
    FWRXBCDZXFCDZYDAXBUIUJZTPWSYCWRWSYAYBBUKZBULZRSXBXFBUNUOUPWTXCXLXGIWRXBUQDZ
    WSXCXLJZAURZYJWSEZKXELMZXEBNUCGZBFGZLMZYKXEXBBXDXDUDXEUDUSZUFUTUGWTXDQDXEQD
    XGQDXMXIJWTXDWTXCCDZXDCDZWTYEBCDZYBYSWRYEWSYDPWSUUAWRBVAZSZWSYBWRYISZXBBVBU
    OZXCUMVCZTWTXEWTYSYTXECDZUUEUUFXCXDUIVDZTWTXGWTYFUUAYBXGCDZWRYFWSYGPUUCUUDX
    FBVBUOZTXDXEXGVEUOVFVGWTXJXDJZKXHLMZXHNOMZWTXDUQDZXHCDZUUKUULUUMEVHWTYSUUNU
    UEXCURVCWTUUGUUIUUOUUHUUJXEXGVIVDXHXDVJVDWTUUGUUIYNKXGLMZUULUUHUUJWRYJWSYNY
    LYMYNYQYKYRVKUTWRYFXQEUUAKBOMZEZUUPWSWRYFXQYGAVLRWSUUAUUQUUBBVPRZXFBVMVNXEX
    GVQVOWTXHYPNBFGZIGZNOWTUUGUUIEYPCDZUUTCDZEZEYQXGUUTOMZEXHUVAOMWTUUGUUIUVDUU
    HUUJWSUVDWRWSUVBUVCWSYOCDZUUAYBUVBWSUUAUVFUUBBVRVCZUUBYIYOBVBUOBVSRSVTWTYQU
    VEWRYJWSYQYLYMYNYQYKYRWAUTWTXRUVEWRXRWSAWBPWRYFUURXRUVEVHZWSYGUUSYFNCDUURUV
    HWCXFNBWDWEVNWFRXEXGYPUUTWGWHWSUVANJWRWSYONIGZBFGZBBFGZUVANWSUVIBBFWSYANQDZ
    UVIBJYHWIBNWJWKUGWSYOQDZYAYBUVJUVAJZWSYOUVGTYHYIUVMUVLYCUVNWIYONBUNWEWLWSYA
    YBUVKNJYHYIBWQVDWMSWNWOWP $.
    $( [16-Aug-2008] $)

  $( Cancellation of an embedded floor of a ratio.  Generalization of Equation
     2.4 in [CormenLeisersonRivest] p. 33 (where ` A ` must be an integer). $)
  fldiv2 $p |- ( ( A e. RR /\ M e. NN /\ N e. NN )
      -> ( |_ ` ( ( |_ ` ( A / M ) ) / N ) ) = ( |_ ` ( A / ( M x. N ) ) ) ) $=
    ( cr wcel cn w3a cdiv co cfl cfv wceq wa cc0 wne adantl nnne0 cc nncn jca
    cmul simpl nnre redivcl syl3anc fldiv sylan 3impa recn divdiv1 syl3an eqtrd
    fveq2d ) ADEZBFEZCFEZGZABHIZJKCHIJKZURCHIZJKZABCUAIHIZJKUNUOUPUSVALZUNUOMZU
    RDEZUPVCVDUNBDEZBNOZVEUNUOUBUOVFUNBUCPUOVGUNBQZPABUDUEURCUFUGUHUQUTVBJUNARE
    UOBREZVGMUPCREZCNOZMUTVBLAUIUOVIVGBSVHTUPVJVKCSCQTABCUJUKUMUL $.
    $( [9-Nov-2008] $)


