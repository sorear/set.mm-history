$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The floor (greatest integer) function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c |_ $.  $( Floor function. $)

  $( Extend class notation with floor (greatest integer) function. $)
  cfl $a class |_ $.

  ${
    $d x y z $.
    $( Define the floor (greatest integer) function.  See ~ flval for its
       value, ~ fllelt for its basic property, and ~ flcl for its closure.

       The term "floor" was coined by Ken Iverson.  He also invented a
       mathematical notation for floor, consisting of an L-shaped left bracket
       and its reflection as a right bracket.  In APL, the left-bracket alone
       is used, and we borrow this idea.  (Thanks to Paul Chapman for this
       information.) $)
    df-fl $a |- |_ = { <. x , y >. | ( x e. RR /\
                y = U. { z e. ZZ | ( z <_ x /\ x < ( z + 1 ) ) } ) } $.
  $}

  ${
    $d x y z A $.
    $( Value of the floor (greatest integer) function.  The floor of ` A ` is
       the (unique) integer less than or equal to ` A ` whose successor is
       strictly greater than ` A ` . $)
    flval $p |- ( A e. RR ->
              ( |_ ` A ) = U. { x e. ZZ | ( x <_ A /\ A < ( x + 1 ) ) } ) $=
      ( vy vz cv cle wbr c1 caddc co clt wa cz crab cuni cr cfl wceq breq2
      breq1 anbi12d rabbidv unieqd df-fl zex rabex uniex fvopab4 ) CDBAEZCEZFGZ
      UJUIHIJZKGZLZAMNZOUIBFGZBULKGZLZAMNZOPQUJBRZUOUSUTUNURAMUTUKUPUMUQUJBUIFS
      UJBULKTUAUBUCCDAUDUSURAMUEUFUGUH $.
      $( [28-Sep-2007] $) $( [14-Nov-2004] $)

    $( The floor (greatest integer) function is an integer (closure law). $)
    flcl $p |- ( A e. RR -> ( |_ ` A ) e. ZZ ) $=
      ( vz cr wcel cfl cfv cv cle wbr c1 caddc co clt wa cz crab cuni flval
      wreu rebtwnz reucl syl eqeltrd ) ACDZAEFBGZAHIAUEJKLMINZBOPQZOBARUDUFBOSU
      GODBATUFBOUAUBUC $.
      $( [15-Nov-2004] $)

    $( The floor (greatest integer) function is real. $)
    reflcl $p |- ( A e. RR -> ( |_ ` A ) e. RR ) $=
      ( cr wcel cfl cfv cz flcl zre syl ) ABCADEZFCJBCAGJHI $.
      $( [15-Jul-2008] $)

    $( A basic property of the floor (greatest integer) function. $)
    fllelt $p |- ( A e. RR ->
                  ( ( |_ ` A ) <_ A /\ A < ( ( |_ ` A ) + 1 ) ) ) $=
      ( vy vx cr wcel cfl cfv cle wbr c1 caddc co clt wa cv cz crab cuni wreu
      rebtwnz wceq breq1 opreq1 breq2d anbi12d reuuni3 syl flval breq1d
      opreq1d mpbird ) ADEZAFGZAHIZAUMJKLZMIZNBOZAHIZAUQJKLZMIZNZBPQRZAHIZAVBJK
      LZMIZNZULCOZAHIZAVGJKLZMIZNZCPSVFCATVKVAVFCBPVGUQUAZVHURVJUTVGUQAHUBVLVIU
      SAMVGUQJKUCUDUEVGVBUAZVHVCVJVEVGVBAHUBVMVIVDAMVGVBJKUCUDUEUFUGULUNVCUPVEU
      LUMVBAHBAUHZUIULUOVDAMULUMVBJKVNUJUDUEUK $.
      $( [15-Nov-2004] $)
  $}

  $( A basic property of the floor (greatest integer) function. $)
  flle $p |- ( A e. RR -> ( |_ ` A ) <_ A ) $=
    ( cr wcel cfl cfv cle wbr c1 caddc co clt fllelt simpld ) ABCADEZAFGANHIJKG
    ALM $.
    $( [24-Feb-2005] $)

  $( A basic property of the floor (greatest integer) function. $)
  flltp1 $p |- ( A e. RR -> A < ( ( |_ ` A ) + 1 ) ) $=
    ( cr wcel cfl cfv cle wbr c1 caddc co clt fllelt simprd ) ABCADEZAFGANHIJKG
    ALM $.
    $( [24-Feb-2005] $)

  $( The fractional part of a real number is less than one. $)
  fraclt1 $p |- ( A e. RR -> ( A - ( |_ ` A ) ) < 1 ) $=
    ( cr wcel cfl cfv cmin co c1 clt wbr caddc flltp1 wb reflcl 1re ltsubadd2
    mp3an3 mpdan mpbird ) ABCZAADEZFGHIJZAUAHKGIJZALTUABCZUBUCMZANTUDHBCUEOAUAH
    PQRS $.
    $( [15-Jul-2008] $)

  $( The fractional part of a real number is nonnegative. $)
  fracge0 $p |- ( A e. RR -> 0 <_ ( A - ( |_ ` A ) ) ) $=
    ( cr wcel cc0 cfl cfv cmin co cle wbr flle wb reflcl subge0 mpdan mpbird )
    ABCZDAAEFZGHIJZRAIJZAKQRBCSTLAMARNOP $.
    $( [17-Jul-2008] $)

  $( The floor function value is the greatest integer less than or equal to its
     argument. $)
  flge $p |- ( ( A e. RR /\ B e. ZZ ) -> ( B <_ A <-> B <_ ( |_ ` A ) ) ) $=
    ( cz wcel cr cle wbr cfl cfv wb wa c1 caddc co clt flltp1 adantl wi zre
    reflcl peano2re syl ancli lelttr 3expb syl2an mpan2d flcl zleltp1 sylan2
    sylibrd flle ancri letr impbid ancoms ) BCDZAEDZBAFGZBAHIZFGZJUQURKZUSVAVBU
    SBUTLMNZOGZVAVBUSAVCOGZVDURVEUQAPQUQBEDZURVCEDZKUSVEKVDRZURBSZURVGURUTEDZVG
    ATZUTUAUBUCVFURVGVHBAVCUDUEUFUGURUQUTCDVAVDJAUHBUTUIUJUKVBVAUTAFGZUSURVLUQA
    ULQUQVFVJURKVAVLKUSRZURVIURVJVKUMVFVJURVMBUTAUNUEUFUGUOUP $.
    $( [15-Nov-2004] $)

  $( The floor function value is less than the next integer. $)
  fllt $p |- ( ( A e. RR /\ B e. ZZ ) -> ( A < B <-> ( |_ ` A ) < B ) ) $=
    ( cr wcel cz wa clt wbr cfl cfv cle wn flge wb zre lenlt sylan ancoms
    reflcl syl2an 3bitr3d con4bid ) ACDZBEDZFZABGHZAIJZBGHZUEBAKHZBUGKHZUFLZUHL
    ZABMUDUCUIUKNZUDBCDZUCUMBOZBAPQRUDUCUJULNZUDUNUGCDUPUCUOASBUGPTRUAUB $.
    $( [24-Feb-2005] $)

  $( An integer is its own floor. $)
  flid $p |- ( A e. ZZ -> ( |_ ` A ) = A ) $=
    ( cz wcel cfl cfv wceq cle wbr cr wa wb zre reflcl syl letri3 syl11anc
    flle leid flge mpancom mpbid mpbir2and ) ABCZADEZAFZUDAGHZAUDGHZUCUDICZAICZ
    UEUFUGJKUCUIUHALZAMNUJUDAOPUCUIUFUJAQNUCAAGHZUGUCUIUKUJARNUIUCUKUGKUJAASTUA
    UB $.
    $( [15-Nov-2004] $)

  $( The floor function is idempotent. $)
  flidm $p |- ( A e. RR -> ( |_ ` ( |_ ` A ) ) = ( |_ ` A ) ) $=
    ( cr wcel cfl cfv cz wceq flcl flid syl ) ABCADEZFCKDEKGAHKIJ $.
    $( [17-Aug-2008] $)

  $( A real number equals its floor iff it is an integer. $)
  flidz $p |- ( A e. RR -> ( ( |_ ` A ) = A <-> A e. ZZ ) ) $=
    ( cr wcel cfl cfv wceq cz eleq1 flcl syl5cbi flid impbid1 ) ABCZADEZAFZAGCZ
    ONGCPMNAGHAIJAKL $.
    $( [11-Nov-2008] $)

  $( Ordering relationship for the greatest integer function. $)
  flwordi $p |- ( ( A e. RR /\ B e. RR /\ A <_ B ) ->
                 ( |_ ` A ) <_ ( |_ ` B ) ) $=
    ( cr wcel cle wbr cfl cfv wa flle adantr wi reflcl simpl simpr letr
    syl111anc mpand wb cz flcl flge sylan2 ancoms sylibd 3impia ) ACDZBCDZABEFZ
    AGHZBGHEFZUGUHIZUIUJBEFZUKULUJAEFZUIUMUGUNUHAJKULUJCDZUGUHUNUIIUMLUGUOUHAMK
    UGUHNUGUHOUJABPQRUHUGUMUKSZUGUHUJTDUPAUABUJUBUCUDUEUF $.
    $( [31-Dec-2005] $)

  ${
    $d x y z w A $.  $d x B $.
    $( An alternate way to define the floor (greatest integer) function. $)
    flval2 $p |- ( A e. RR -> ( |_ ` A ) =
            U. { x e. ZZ | ( x <_ A /\ A. y e. ZZ ( y <_ A -> y <_ x ) ) } ) $=
      ( cr wcel cv cle wbr wi cz wral wa crab cuni cfl cfv wceq wreu wb flcl
      zmax breq1 breq2 imbi2d ralbidv anbi12d reuuni2 syl11anc flle flge
      biimpd r19.21aiva mpbi2and eqcomd ) CDEZAFZCGHZBFZCGHZURUPGHZIZBJKZLZAJMN
      ZCOPZUOVECGHZUSURVEGHZIZBJKZVDVEQZUOVEJEVCAJRVFVILZVJSCTABCUAVCVKAJVEUPVE
      QZUQVFVBVIUPVECGUBVLVAVHBJVLUTVGUSUPVEURGUCUDUEUFUGUHCUIUOVHBJUOURJELUSVG
      CURUJUKULUMUN $.
      $( [16-Nov-2004] $)

    $( An alternate way to define the floor (greatest integer) function, as the
       supremum of all integers less than or equal to its argument. $)
    flval3 $p |- ( A e. RR ->
                  ( |_ ` A ) = sup ( { x e. ZZ | x <_ A } , RR , < ) ) $=
      ( vz vy vw cr wcel cfl cfv cv cle wbr cz crab clt csup wceq wn wa wb
      reflcl wss c0 wne wral wrex ssrab2 zssre sstri a1i flcl flle breq1 elrab
      sylanbrc ne0i syl flge biimpd expimpd syl5ib r19.21aiv breq2 ralbidv
      rcla4ev syl11anc suprcl syl111anc eqlelt suprub syl31anc zre lenlt
      syl2an ancoms bitrd suprnub syl32anc mpbir2and ) BFGZBHIZAJZBKLZAMNZFOPZQ
      ZWAWEKLZWAWEOLRZVTWAFGZWEFGZWFWGWHSTBUAZVTWDFUBZWDUCUDZCJZDJZKLZCWDUEZDFU
      FZWJWLVTWDMFWCAMUGUHUIUJZVTWAWDGZWMVTWAMGWABKLZWTBUKBULWCXAAWAMWBWABKUMUN
      UOZWDWAUPUQZVTWIWNWAKLZCWDUEZWRWKVTXDCWDVTWNMGZWNBKLZSXDWNWDGVTXFXGXDVTXF
      SXGXDBWNURUSUTWCXGAWNMWBWNBKUMUNVAVBWQXEDWAFWOWAQWPXDCWDWOWAWNKVCVDVEVFZD
      CWDVGVHWAWEVIVFVTWLWMWRWTWGWSXCXHXBDCWDWAVJVKVTWLWMWRWIWAEJZOLRZEWDUEWHWS
      XCXHWKVTXJEWDVTXIMGZXIBKLZSXJXIWDGVTXKXLXJVTXKSZXLXJXMXLXIWAKLZXJBXIURXKV
      TXNXJTZXKXIFGWIXOVTXIVLWKXIWAVMVNVOVPUSUTWCXLAXIMWBXIBKUMUNVAVBDCEWDWAVQV
      RVS $.
      $( [15-Nov-2004] $)

    $( A condition equivalent to floor. $)
    flbi $p |- ( ( A e. RR /\ B e. ZZ ) ->
                ( ( |_ ` A ) = B <-> ( B <_ A /\ A < ( B + 1 ) ) ) ) $=
      ( vx cr wcel cz wa cfl cfv wceq cv cle wbr c1 caddc co clt crab cuni wb
      flval eqeq1d adantr wreu rebtwnz breq1 opreq1 breq2d anbi12d reuuni2
      sylan2 ancoms bitr4d ) ADEZBFEZGAHIZBJZCKZALMZAURNOPZQMZGZCFRSZBJZBALMZAB
      NOPZQMZGZUNUQVDTUOUNUPVCBCAUAUBUCUOUNVHVDTZUNUOVBCFUDVICAUEVBVHCFBURBJZUS
      VEVAVGURBALUFVJUTVFAQURBNOUGUHUIUJUKULUM $.
      $( [11-Mar-2005] $)
  $}

  $( A condition equivalent to floor. $)
  flbi2 $p |- ( ( N e. ZZ /\ F e. RR ) ->
              ( ( |_ ` ( N + F ) ) = N <-> ( 0 <_ F /\ F < 1 ) ) ) $=
    ( cz wcel cr wa caddc co cfl cfv wceq cle wbr c1 clt cc0 wb zre readdcl
    sylan simpl flbi syl11anc addge01 1re ltadd2 mp3an2 ancoms anbi12d bitr4d
    ) BCDZAEDZFZBAGHZIJBKZBUNLMZUNBNGHOMZFZPALMZANOMZFZUMUNEDZUKUOURQUKBEDZULVB
    BRZBASTUKULUAUNBUBUCUKVCULVAURQVDVCULFUSUPUTUQBAUDULVCUTUQQZULNEDVCVEUEANBU
    FUGUHUITUJ $.
    $( [15-Aug-2008] $)

  $( The floor of a number greater than or equal to 0 is a nonnegative
     integer. $)
  flge0nn0 $p |- ( ( A e. RR /\ 0 <_ A ) -> ( |_ ` A ) e. NN0 ) $=
    ( cr wcel cc0 cle wbr wa cfl cfv cz cn0 flcl adantr wb 0z flge mpan2
    biimpa elnn0z sylanbrc ) ABCZDAEFZGAHIZJCZDUCEFZUCKCUAUDUBALMUAUBUEUADJCUBU
    ENOADPQRUCST $.
    $( [26-Apr-2005] $)

  $( The floor of a number greater than or equal to 1 is a natural number. $)
  flge1nn $p |- ( ( A e. RR /\ 1 <_ A ) -> ( |_ ` A ) e. NN ) $=
    ( cr wcel c1 cle wbr wa cfl cfv cz cn flcl adantr wb 1z flge mpan2 biimpa
    elnnz1 sylanbrc ) ABCZDAEFZGAHIZJCZDUCEFZUCKCUAUDUBALMUAUBUEUADJCUBUENOADPQ
    RUCST $.
    $( [26-Apr-2005] $)

  $( An integer can be moved in and out of the floor of a sum. $)
  fladdz $p |- ( ( A e. RR /\ N e. ZZ ) -> ( |_ ` ( A + N ) ) =
                ( ( |_ ` A ) + N ) ) $=
    ( cr wcel cz wa caddc co cfl cfv wceq cle wbr c1 clt wb zre readdcl sylan2
    flcl zaddcl sylan flbi syl11anc flle adantr reflcl ancri leadd1 3expa
    syl2an mpbid flltp1 peano2re syl ancli ltadd1 cc recnd zcn ax1cn add32
    mp3an2 breqtrd mpbir2and ) ACDZBEDZFZABGHZIJAIJZBGHZKZVKVILMZVIVKNGHZOMZVHV
    ICDZVKEDZVLVMVOFPVGVFBCDZVPBQZABRSVFVJEDVGVQATVJBUAUBVIVKUCUDVHVJALMZVMVFVT
    VGAUEUFVFVJCDZVFFVRVTVMPZVGVFWAAUGZUHVSWAVFVRWBVJABUIUJUKULVHVIVJNGHZBGHZVN
    OVHAWDOMZVIWEOMZVFWFVGAUMUFVFVFWDCDZFVRWFWGPZVGVFWHVFWAWHWCVJUNUOUPVSVFWHVR
    WIAWDBUQUJUKULVFVJURDZBURDZWEVNKZVGVFVJWCUSBUTWJNURDWKWLVAVJNBVBVCUKVDVE $.
    $( [27-Apr-2005] $)

  $( An integer can be moved in and out of the floor of a sum. $)
  flzadd $p |- ( ( N e. ZZ /\ A e. RR ) -> ( |_ ` ( N + A ) ) =
                ( N + ( |_ ` A ) ) ) $=
    ( cr wcel cz caddc co cfl cfv wceq wa fladdz cc recn zcn addcom syl2an
    fveq2d reflcl recnd 3eqtr3d ancoms ) ACDZBEDZBAFGZHIZBAHIZFGZJUCUDKZABFGZHI
    UGBFGZUFUHABLUIUJUEHUCAMDBMDZUJUEJUDANBOZABPQRUCUGMDULUKUHJUDUCUGASTUMUGBPQ
    UAUB $.
    $( [2-Jan-2009] $)

  $( A real bounded between an integer and its successor is nonnegative iff the
     integer is nonnegative.  Second half of Lemma 13-4.1 of [Gleason] p. 217.
     (For the first half see ~ rebtwnz .) $)
  btwnzge0 $p |- ( ( ( A e. RR /\ N e. ZZ ) /\ ( N <_ A /\ A < ( N + 1 ) ) )
                -> ( 0 <_ A <-> 0 <_ N ) ) $=
    ( cr wcel cz wa cle wbr c1 caddc co clt cc0 cfl cfv wb 0z flge mpan2
    ad2antrr wceq flbi biimpar breq2d bitrd ) ACDZBEDZFZBAGHABIJKLHFZFZMAGHZMAN
    OZGHZMBGHUFUKUMPZUGUIUFMEDUNQAMRSTUJULBMGUHULBUAUIABUBUCUDUE $.
    $( [12-Mar-2005] $)

  $( Ordering relation for the floor of half of an integer. $)
  flhalf $p |- ( N e. ZZ -> N <_ ( 2 x. ( |_ ` ( ( N + 1 ) / 2 ) ) ) ) $=
    ( cz wcel c2 cdiv co c1 caddc wo cfl cfv cmul cle wbr zeo wa flid opreq2d
    cc wceq zcn cc0 wne 2cn 2ne0 divcan2 mp3an23 syl sylan9eqr cr zre rehalfcl
    reflcl peano2re 3syl flcl 2re a1i lep1 adantr wb w3a clt 2pos lediv1OLD
    mpan2 mp3an3 mpbid syl11anc flwordi syl111anc 0re ltleii jctil lemul2aOLD
    syl31anc eqbrtrrd peano2cn breqtrrd jaodan mpdan ) ABCZADEFZBCZAGHFZDEFZBCZ
    IADWFJKZLFZMNZAOWBWDWJWGWBWDPDWCJKZLFZAWIMWDWBWLDWCLFZAWDWKWCDLWCQRWBASCZWM
    ATZAUAZWNDSCZDUBUCZWOUDUEADUFUGUHUIWBWLWIMNZWDWBWKUJCZWHUJCZDUJCZUBDMNZWKWH
    MNZPWSWBWCUJCZWTWBAUJCZXEAUKZAULUHZWCUMUHWBWFUJCZWHBCXAWBXFWEUJCZXIXGAUNZWE
    ULUOZWFUPWHUKUOXBWBUQURWBXDXCWBXEXIWCWFMNZXDXHXLWBXFXJXMXGWBXFXJXGXKUHXFXJP
    AWEMNZXMXFXNXJAUSZUTXFXJXBXNXMVAZUQXFXJXBVBUBDVCNXPVDAWEDVEVFVGVHVIWCWFVJVK
    UBDVLUQVDVMVNWKWHDVOVPUTVQWBWGPAWEWIMWBXNWGWBXFXNXGXOUHUTWGWBWIDWFLFZWEWGWH
    WFDLWFQRWBWNWESCZXQWETZWPAVRXRWQWRXSUDUEWEDUFUGUOUIVSVTWA $.
    $( [1-Jan-2006] $)

  $( The ceiling function returns an integer (closure law).  (Contributed by
     Jeffrey Hankins, 10-Jun-2007.) $)
  ceicl $p |- ( A e. RR -> -u ( |_ ` -u A ) e. ZZ ) $=
    ( cr wcel cneg cfl cfv cz renegcl flcl znegcl 3syl ) ABCADZBCLEFZGCMDGCAHLI
    MJK $.
    $( [8-Jun-2007] $)

  $( The ceiling of a real number is greater than or equal to that number.
     (Contributed by Jeffrey Hankins, 10-Jun-2007.) $)
  ceige $p |- ( A e. RR -> A <_ -u ( |_ ` -u A ) ) $=
    ( cr wcel cneg cfl cfv cle wbr renegcl reflcl syl wa flle adantr lenegcon2
    mpbird mpdan ) ABCZADZEFZBCZATDGHZRSBCZUAAIZSJKRUALUBTSGHZRUEUARUCUEUDSMKNA
    TOPQ $.
    $( [9-Jun-2007] $)

  $( One less than the ceiling of a real number is strictly less than that
     number.  (Contributed by Jeffrey Hankins, 10-Jun-2007.) $)
  ceim1l $p |- ( A e. RR -> ( -u ( |_ ` -u A ) - 1 ) < A ) $=
    ( cr wcel cneg cfl cfv c1 cmin co caddc clt cc wceq renegcl reflcl syl
    recnd ax1cn negdi sylancl negcl negsub mpan2 3syl eqtr2d wbr peano2re wa
    flltp1 adantr ltnegcon1 mpbid mpdan eqbrtrd ) ABCZADZEFZDZGHIZUQGJIZDZAKUOV
    AURGDJIZUSUOUQLCZGLCZVAVBMUOUQUOUPBCZUQBCZANZUPOPZQZRUQGSTUOVCURLCZVBUSMZVI
    UQUAVJVDVKRURGUBUCUDUEUOUTBCZVAAKUFZUOVFVLVHUQUGPUOVLUHUPUTKUFZVMUOVNVLUOVE
    VNVGUPUIPUJAUTUKULUMUN $.
    $( [9-Jun-2007] $)

  $( The ceiling of a real number is the smallest integer greater than or equal
     to it.  (Contributed by Jeffrey Hankins, 10-Jun-2007.) $)
  ceile $p |- ( ( A e. RR /\ B e. ZZ /\ A <_ B ) -> -u ( |_ ` -u A ) <_ B ) $=
    ( cr wcel cz cle wbr cneg cfl cfv wa c1 cmin co clt ceim1l adantr wi ceicl
    zre peano2rem 3syl simpl adantl ltletr syl111anc mpand wb zlem1lt sylan
    sylibrd 3impia ) ACDZBEDZABFGZAHIJHZBFGZUMUNKZUOUPLMNZBOGZUQURUSAOGZUOUTUMV
    AUNAPQURUSCDZUMBCDZVAUOKUTRUMVBUNUMUPEDZUPCDVBASZUPTUPUAUBQUMUNUCUNVCUMBTUD
    USABUEUFUGUMVDUNUQUTUHVEUPBUIUJUKUL $.
    $( [10-Jun-2007] $)

  ${
    quorem.1 $e |- Q = ( |_ ` ( A / B ) ) $.
    quorem.2 $e |- R = ( A - ( B x. Q ) ) $.
    $( MAY BE REVISED - is this really needed for anything?  Should we use
       mod to simplify it? $)
    $( Quotient and remainder of an integer divided by a natural number. $)
    quoremz $p |- ( ( A e. ZZ /\ B e. NN ) -> ( ( Q e. ZZ /\ R e. NN0 )
          /\ ( R < B /\ A = ( ( B x. Q ) + R ) ) ) ) $=
      ( cz wcel cn wa cn0 clt wbr cmul co caddc wceq cdiv cfl cfv cr cc0 wne
      zre adantr nnre adantl nnne0 redivcl syl111anc flcl syl syl5eqel cmin
      cle nnz zmulcl syl11anc simpl cc zcn nncn divcan3 flle syl5eqbr eqbrtrd
      wb nngt0 lediv1 syl112anc mpbird znn0sub2 c1 fraclt1 opreq2i jca
      divsubdir opreq2d eqtrd opreq1i syl5eq divid 3brtr4d nn0re ltdiv1 pncan3
      syl5req jca31 ) AGHZBIHZJZCGHZDKHZDBLMZABCNOZDPOZQZJWKABROZSTZGCWKWRUAHZW
      SGHWKAUAHZBUAHZBUBUCZWTWIXAWJAUDUEZWJXBWIBUFUGZWJXCWIBUHZUGZABUIUJZWRUKUL
      EUMZWKAWOUNOZKDWKWOGHZWIWOAUOMZXJKHWKBGHZWLXKWJXMWIBUPUGXIBCUQURZWIWJUSWK
      XLWOBROZWRUOMZWKXOCWRUOWKCUTHZBUTHZXCXOCQWKWLXQXICVAULWJXRWIBVBZUGXGCBVCU
      JZWKWSWRCUOWKWTWSWRUOMXHWRVDULEVEVFWKWOUAHZXAXBUBBLMZXLXPVGWKXKYAXNWOUDUL
      XDXEWJYBWIBVHUGZWOABVIVJVKWOAVLUJFUMZWKWNWQWKWNDBROZBBROZLMZWKWRCUNOZVMYE
      YFLWKWRWSUNOZVMYHLWKWTYIVMLMXHWRVNULCWSWRUNEVOVEWKXJBROZYHYEWKYJWRXOUNOZY
      HWKAUTHZWOUTHZXRXCJZYJYKQWIYLWJAVAUEZWKXKYMXNWOVAULZWJYNWIWJXRXCXSXFVPUGA
      WOBVQUJWKXOCWRUNXTVRVSDXJBRFVTWAWJYFVMQZWIWJXRXCYQXSXFBWBURUGWCWKDUAHZXBX
      BYBWNYGVGWKWMYRYDDWDULXEXEYCDBBWEVJVKWKWOXJPOZAWPWKYMYLYSAQYPYOWOAWFURDXJ
      WOPFVOWGVPWH $.
      $( [14-Aug-2008] $)

    $( Quotient and remainder of a nonnegative integer divided by a natural
       number. $)
    quoremnn0ALT $p |- ( ( A e. NN0 /\ B e. NN ) -> ( ( Q e. NN0 /\ R e. NN0 )
          /\ ( R < B /\ A = ( ( B x. Q ) + R ) ) ) ) $=
      ( cn0 wcel cn wa cz clt wbr cmul co caddc wceq cdiv cfl cfv cr cc0 cle
      wne nn0re adantr nnre adantl nnne0 redivcl syl111anc nn0ge0 jca nngt0
      divge0 syl2an flge0nn0 syl11anc syl5eqel nn0z quoremz sylan simpl anim1i
      anasss ) AGHZBIHZJZCGHZCKHZDGHZJZDBLMABCNODPOQJZJZVIVKJZVMJZVHABROZSTZGCV
      HVQUAHZUBVQUCMZVRGHVHAUAHZBUAHZBUBUDZVSVFWAVGAUEZUFVGWBVFBUGZUHVGWCVFBUIU
      HABUJUKVFWAUBAUCMZJWBUBBLMZJVTVGVFWAWFWDAULUMVGWBWGWEBUNUMABUOUPVQUQUREUS
      VFAKHVGVNAUTABCDEFVAVBVIVLVMVPVIVLJVOVMVIVJVKVOVIVJJVIVKVIVJVCVDVEVDVEUR
      $.
      $( [14-Aug-2008] $)

    $( MAY BE REVISED - keep either quoremnn0ALT or quoremnn0 (if we don't
       keep quoremz $)
    $( Quotient and remainder of a nonnegative integer divided by a natural
       number. $)
    quoremnn0 $p |- ( ( A e. NN0 /\ B e. NN ) -> ( ( Q e. NN0 /\ R e. NN0 )
          /\ ( R < B /\ A = ( ( B x. Q ) + R ) ) ) ) $=
      ( cn0 wcel cn wa clt wbr cmul co caddc wceq cdiv cfl cfv cr cc0 cle wne
      nn0re adantr nnre adantl nnne0 redivcl syl111anc nn0ge0 jca nngt0 divge0
      syl2an flge0nn0 syl11anc syl5eqel cmin nnnn0 nn0mulcl simpl cc nn0cn syl
      nncn divcan3 flle syl5eqbr eqbrtrd wb lediv1 syl112anc mpbird nn0sub2 c1
      fraclt1 opreq2i divsubdir opreq2d eqtrd opreq1i syl5eq divid 3brtr4d
      ltdiv1 pncan3 syl5req jca31 ) AGHZBIHZJZCGHZDGHZDBKLZABCMNZDONZPZJWLABQNZ
      RSZGCWLWSTHZUAWSUBLZWTGHWLATHZBTHZBUAUCZXAWJXCWKAUDZUEZWKXDWJBUFZUGZWKXEW
      JBUHZUGZABUIUJZWJXCUAAUBLZJXDUABKLZJXBWKWJXCXMXFAUKULWKXDXNXHBUMZULABUNUO
      WSUPUQEURZWLAWPUSNZGDWLWPGHZWJWPAUBLZXQGHWLBGHZWMXRWKXTWJBUTUGXPBCVAUQZWJ
      WKVBWLXSWPBQNZWSUBLZWLYBCWSUBWLCVCHZBVCHZXEYBCPWLWMYDXPCVDVEWKYEWJBVFZUGX
      KCBVGUJZWLWTWSCUBWLXAWTWSUBLXLWSVHVEEVIVJWLWPTHZXCXDXNXSYCVKWLXRYHYAWPUDV
      EXGXIWKXNWJXOUGZWPABVLVMVNWPAVOUJFURZWLWOWRWLWODBQNZBBQNZKLZWLWSCUSNZVPYK
      YLKWLWSWTUSNZVPYNKWLXAYOVPKLXLWSVQVECWTWSUSEVRVIWLXQBQNZYNYKWLYPWSYBUSNZY
      NWLAVCHZWPVCHZYEXEJZYPYQPWJYRWKAVDUEZWLXRYSYAWPVDVEZWKYTWJWKYEXEYFXJULUGA
      WPBVSUJWLYBCWSUSYGVTWADXQBQFWBWCWKYLVPPZWJWKYEXEUUCYFXJBWDUQUGWEWLDTHZXDX
      DXNWOYMVKWLWNUUDYJDUDVEXIXIYIDBBWFVMVNWLWPXQONZAWQWLYSYRUUEAPUUBUUAWPAWGU
      QDXQWPOFVRWHULWI $.
      $( [14-Aug-2008] $)
  $}

  ${
    intfrac2.1 $e |- Z = ( |_ ` A ) $.
    intfrac2.2 $e |- F = ( A - Z ) $.
    $( MAY BE REVISED - should we replace this with ~ intfrac ? $)
    $( Decompose a real into integer and fractional parts. $)
    intfrac2 $p |- ( A e. RR -> ( 0 <_ F /\ F < 1 /\ A = ( Z + F ) ) ) $=
      ( cr wcel cc0 cle wbr c1 clt caddc co wceq cfl cfv cmin fracge0 opreq2i
      eqtri syl6breqr fraclt1 syl5eqbr cc cz flcl syl5eqel zcn syl recn pncan3
      syl11anc syl5req 3jca ) AFGZHBIJBKLJACBMNZOUPHAAPQZRNZBIASBACRNZUSECURARD
      TUAZUBUPUSKBLAUCVAUDUPCUTMNZAUQUPCUEGZAUEGVBAOUPCUFGVCUPURUFCAUGDUHCUIUJA
      UKCAULUMBUTCMETUNUO $.
      $( [16-Aug-2008] $)
  $}

  ${
    intfracq.1 $e |- Z = ( |_ ` ( M / N ) ) $.
    intfracq.2 $e |- F = ( ( M / N ) - Z ) $.
    $( Decompose a rational number, expressed as a ratio, into integer and
       fractional parts.  The fractional part has a tighter bound than that of
       ~ intfrac2 . $)
    intfracq $p |- ( ( M e. ZZ /\ N e. NN )
         -> ( 0 <_ F /\ F <_ ( ( N - 1 ) / N ) /\ ( M / N ) = ( Z + F ) ) ) $=
      ( cz wcel cn wa cc0 cle wbr c1 cmin co cdiv caddc wceq clt cr w3a wne
      zre adantr nnre adantl nnne0 redivcl syl111anc intfrac2 syl simp1d cmul
      cfl cfv fraclt1 opreq2i eqtri a1i cc nncn divid syl11anc 3brtr4d wb
      reflcl syl5eqel resubcl nngt0 jca ltmuldiv2 mpbird recnd flcl eqeltrd
      zcn subdi syl5eq divcan2 simpl nnz zmulcl zsubcl zltlem1 mpbid peano2rem
      lemuldiv2 simp3d 3jca ) BGHZCIHZJZKALMZACNOPZCQPLMZBCQPZDARPSZWMWNANTMZWR
      WMWQUAHZWNWSWRUBWMBUAHZCUAHZCKUCZWTWKXAWLBUDUEWLXBWKCUFZUGZWLXCWKCUHZUGZB
      CUIUJZWQADEFUKULZUMWMCAUNPZWOLMZWPWMXJCTMZXKWMXLACCQPZTMZWMWQWQUOUPZOPZNA
      XMTWMWTXPNTMXHWQUQULAXPSWMAWQDOPZXPFDXOWQOEURUSUTWLXMNSZWKWLCVAHZXCXRCVBZ
      XFCVCVDUGVEWMAUAHZXBXBKCTMZJZXLXNVFWMXQUAAWMWTDUAHXQUAHXHWMXOUADWMWTXOUAH
      XHWQVGULEVHWQDVIVDFVHZXEWLYCWKWLXBYBXDCVJVKUGZACCVLUJVMWMXJGHCGHZXLXKVFWM
      XJCWQUNPZCDUNPZOPZGWMCXQUNPZYIXJWMXSWQVAHDVAHZYJYISWLXSWKXTUGZWMWQXHVNWMD
      GHZYKWMDXOGDXOSWMEUTWMWTXOGHXHWQVOULVPZDVQULCWQDVRUJAXQCUNFURVSWMYGGHYHGH
      ZYIGHWMYGBGWMBVAHZXSXCYGBSWKYPWLBVQUEYLXGBCVTUJWKWLWAVPWMYFYMYOWLYFWKCWBU
      GZYNCDWCVDYGYHWDVDVPYQXJCWEVDWFWMYAWOUAHZYCXKWPVFYDWLYRWKWLXBYRXDCWGULUGY
      EAWOCWHUJWFWMWNWSWRXIWIWJ $.
      $( [16-Aug-2008] $)
  $}

  $( Cancellation of the embedded floor of a real divided by an integer. $)
  fldiv $p |- ( ( A e. RR /\ N e. NN )
                  -> ( |_ ` ( ( |_ ` A ) / N ) ) = ( |_ ` ( A / N ) ) ) $=
    ( cr wcel cn wa cdiv co cfl cfv cmin caddc wceq cc0 cle wbr c1 clt eqid
    intfrac2 simp3d adantr opreq1d cc wne reflcl recnd resubcl mpdan nncn
    nnne0 jca adantl divdir syl111anc eqtrd cz flcl intfracq sylan nnre
    redivcl syl syl11anc addass 3eqtrd fveq2d wb readdcl flbi2 simp1d fracge0
    nngt0 divge0 syl2an addge0 syl22anc peano2rem nnrecre jca31 simp2d fraclt1
    1re ltdiv1 mp3an2 mpbid leltadd sylc ax1cn npcan sylancl syl12anc divid
    3eqtr3d breqtrd mpbir2and eqtr2d ) ACDZBEDZFZABGHZIJAIJZBGHZIJZXCXDKHZAXBKH
    ZBGHZLHZLHZIJZXDWTXAXIIWTXAXCXGLHZXDXELHZXGLHZXIWTXAXBXFLHZBGHZXKWTAXNBGWRA
    XNMZWSWRNXFOPZXFQRPZXPAXFXBXBSXFSTUAUBUCWTXBUDDZXFUDDZBUDDZBNUEZFZXOXKMWRXS
    WSWRXBAUFZUGUBWRXTWSWRXFWRXBCDZXFCDZYDAXBUHUIZUGUBWSYCWRWSYAYBBUJZBUKZULUMX
    BXFBUNUOUPWTXCXLXGLWRXBUQDZWSXCXLMZAURZYJWSFZNXEOPZXEBQKHZBGHZOPZYKXEXBBXDX
    DSXESUSZUAUTUCWTXDUDDXEUDDXGUDDXMXIMWTXDWTXCCDZXDCDZWTYEBCDZYBYSWRYEWSYDUBW
    SUUAWRBVAZUMZWSYBWRYIUMZXBBVBUOZXCUFVCZUGWTXEWTYSYTXECDZUUEUUFXCXDUHVDZUGWT
    XGWTYFUUAYBXGCDZWRYFWSYGUBUUCUUDXFBVBUOZUGXDXEXGVEUOVFVGWTXJXDMZNXHOPZXHQRP
    ZWTXDUQDZXHCDZUUKUULUUMFVHWTYSUUNUUEXCURVCWTUUGUUIUUOUUHUUJXEXGVIVDXHXDVJVD
    WTUUGUUIYNNXGOPZUULUUHUUJWRYJWSYNYLYMYNYQYKYRVKUTWRYFXQFUUANBRPZFZUUPWSWRYF
    XQYGAVLULWSUUAUUQUUBBVMULZXFBVNVOXEXGVPVQWTXHYPQBGHZLHZQRWTUUGUUIFYPCDZUUTC
    DZFZFYQXGUUTRPZFXHUVARPWTUUGUUIUVDUUHUUJWSUVDWRWSUVBUVCWSYOCDZUUAYBUVBWSUUA
    UVFUUBBVRVCZUUBYIYOBVBUOBVSULUMVTWTYQUVEWRYJWSYQYLYMYNYQYKYRWAUTWTXRUVEWRXR
    WSAWBUBWRYFUURXRUVEVHZWSYGUUSYFQCDUURUVHWCXFQBWDWEVOWFULXEXGYPUUTWGWHWSUVAQ
    MWRWSYOQLHZBGHZBBGHZUVAQWSUVIBBGWSYAQUDDZUVIBMYHWIBQWJWKUCWSYOUDDZYAYBUVJUV
    AMZWSYOUVGUGYHYIUVMUVLYCUVNWIYOQBUNWEWLWSYAYBUVKQMYHYIBWMVDWNUMWOWPWQ $.
    $( [16-Aug-2008] $)

  $( Cancellation of an embedded floor of a ratio.  Generalization of Equation
     2.4 in [CormenLeisersonRivest] p. 33 (where ` A ` must be an integer). $)
  fldiv2 $p |- ( ( A e. RR /\ M e. NN /\ N e. NN )
      -> ( |_ ` ( ( |_ ` ( A / M ) ) / N ) ) = ( |_ ` ( A / ( M x. N ) ) ) ) $=
    ( cr wcel cn w3a cdiv co cfl cfv cmul wceq wa cc0 wne simpl nnre adantl
    nnne0 redivcl syl111anc fldiv sylan 3impa cc divdiv1 recn nncn jca syl3an
    fveq2d eqtrd ) ADEZBFEZCFEZGZABHIZJKCHIJKZURCHIZJKZABCLIHIZJKUNUOUPUSVAMZUN
    UONZURDEZUPVCVDUNBDEZBOPZVEUNUOQUOVFUNBRSUOVGUNBTZSABUAUBURCUCUDUEUQUTVBJAU
    FEBUFEZVGNCUFEZCOPZNUTVBMUNUOUPABCUGAUHUOVIVGBUIVHUJUPVJVKCUICTUJUKULUM $.
    $( [9-Nov-2008] $)


