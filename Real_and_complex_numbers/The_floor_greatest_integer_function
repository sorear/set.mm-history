$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The floor (greatest integer) function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c floor $.  $( Floor function. $)

  $( Extend class notation with floor (greatest integer) function. $)
  cfl $a class floor $.

  ${
    $d x y z $.
    $( Define the floor (greatest integer) function.  See ~ flleltt for its
       basic property and ~ flclt for its closure. $)
    df-fl $a |- floor = { <. x , y >. | ( x e. RR /\
                y = U. { z e. ZZ | ( z <_ x /\ x < ( z + 1 ) ) } ) } $.
  $}

  ${
    $d x y z w A $.
    $( Value of the floor (greatest integer) function.  The floor of ` A ` is
       the (unique) integer less than or equal to ` A ` whose successor is
       strictly greater than ` A ` . $)
    flvalt $p |- ( A e. RR ->
              ( floor ` A ) = U. { x e. ZZ | ( x <_ A /\ A < ( x + 1 ) ) } ) $=
      ( vy vz cv cle wbr c1 caddc co clt wa cz crab cuni cr cfl wceq breq2
      breq1 anbi12d rabbisdv unieqd df-fl zex rabex uniex fvopab4 ) CDBAEZCEZFG
      ZUJUIHIJZKGZLZAMNZOUIBFGZBULKGZLZAMNZOPQUJBRZUOUSUTUNURAMUTUKUPUMUQUJBUIF
      SUJBULKTUAUBUCCDAUDUSURAMUEUFUGUH $.
      $( [9-Dec-2004] $) $( [14-Nov-2004] $)

    $( The floor (greatest integer) function is an integer (closure law). $)
    flclt $p |- ( A e. RR -> ( floor ` A ) e. ZZ ) $=
      ( vz cr wcel cfl cfv cv cle wbr c1 caddc co clt wa cz crab cuni flvalt
      wreu rebtwnz reucl syl eqeltrd ) ACDZAEFBGZAHIAUEJKLMINZBOPQZOBARUDUFBOSU
      GODBATUFBOUAUBUC $.
      $( [10-Dec-2004] $) $( [15-Nov-2004] $)

    $( A basic property of the floor (greatest integer) function. $)
    flleltt $p |- ( A e. RR ->
                  ( ( floor ` A ) <_ A /\ A < ( ( floor ` A ) + 1 ) ) ) $=
      ( vy vx cr wcel cfl cfv cle wbr c1 caddc co clt wa cv cz crab cuni wreu
      rebtwnz wceq breq1 opreq1 breq2d anbi12d reuuni3 syl flvalt breq1d
      opreq1d mpbird ) ADEZAFGZAHIZAUMJKLZMIZNBOZAHIZAUQJKLZMIZNZBPQRZAHIZAVBJK
      LZMIZNZULCOZAHIZAVGJKLZMIZNZCPSVFCATVKVAVFCBPVGUQUAZVHURVJUTVGUQAHUBVLVIU
      SAMVGUQJKUCUDUEVGVBUAZVHVCVJVEVGVBAHUBVMVIVDAMVGVBJKUCUDUEUFUGULUNVCUPVEU
      LUMVBAHBAUHZUIULUOVDAMULUMVBJKVNUJUDUEUK $.
      $( [9-Dec-2004] $) $( [15-Nov-2004] $)

    $( A basic property of the floor (greatest integer) function. $)
    fllet $p |- ( A e. RR -> ( floor ` A ) <_ A ) $=
      ( cr wcel cfl cfv cle wbr c1 caddc co clt flleltt pm3.26d ) ABCADEZAFGANH
      IJKGALM $.
      $( [27-Feb-2005] $) $( [24-Feb-2005] $)

    $( A basic property of the floor (greatest integer) function. $)
    flltp1t $p |- ( A e. RR -> A < ( ( floor ` A ) + 1 ) ) $=
      ( cr wcel cfl cfv cle wbr c1 caddc co clt flleltt pm3.27d ) ABCADEZAFGANH
      IJKGALM $.
      $( [27-Feb-2005] $) $( [24-Feb-2005] $)

    $( The floor function value is the greatest integer less than or equal to
       its argument. $)
    flget $p |- ( ( A e. RR /\ B e. ZZ ) ->
                ( B <_ A <-> B <_ ( floor ` A ) ) ) $=
      ( cz wcel cr cle wbr cfl cfv wb wa c1 caddc co clt flltp1t adantl wi
      lelttrt 3expb zret flclt peano2re 3syl ancli syl2an mpan2d zleltp1t
      sylan2 sylibrd fllet letrt syl ancri impbid ancoms ) BCDZAEDZBAFGZBAHIZFG
      ZJUQURKZUSVAVBUSBUTLMNZOGZVAVBUSAVCOGZVDURVEUQAPQBEDZURVCEDZKUSVEKVDRZUQU
      RVFURVGVHBAVCSTBUAZURVGURUTCDZUTEDZVGAUBZUTUAZUTUCUDUEUFUGUQVJVAVDJURBUTU
      HVLUIUJVBVAUTAFGZUSURVNUQAUKQVFVKURKVAVNKUSRZUQURVFVKURVOBUTAULTVIURVKURV
      JVKVLVMUMUNUFUGUOUP $.
      $( [27-Dec-2004] $) $( [15-Nov-2004] $)

    $( The floor function value is less than the next integer. $)
    flltt $p |- ( ( A e. RR /\ B e. ZZ ) ->
                ( A < B <-> ( floor ` A ) < B ) ) $=
      ( cr wcel cz wa clt wbr cfl cfv cle wn flget wb lenltt zret sylan ancoms
      flclt syl syl2an 3bitr3d con4bid ) ACDZBEDZFZABGHZAIJZBGHZUFBAKHZBUHKHZUG
      LZUILZABMUEUDUJULNZBCDZUDUNUEBAOBPZQRUEUDUKUMNZUOUHCDZUQUEUDBUHOUPUDUHEDU
      RASUHPTUARUBUC $.
      $( [13-Mar-2005] $) $( [24-Feb-2005] $)

    $( An integer is its own floor. $)
    flidt $p |- ( A e. ZZ -> ( floor ` A ) = A ) $=
      ( cz wcel cfl cfv wceq cle wbr wa cr zret fllet syl leidt wb flget
      mpancom mpbid jca letri3t flclt 3syl sylanc mpbird ) ABCZADEZAFZUFAGHZAUF
      GHZIZUEUHUIUEAJCZUHAKZALMUEAAGHZUIUEUKUMULANMUKUEUMUIOULAAPQRSUFJCZUKUGUJ
      OUEUFATUEUKUFBCUNULAUAUFKUBULUCUD $.
      $( [27-Dec-2004] $) $( [15-Nov-2004] $)

    $( An alternate way to define the floor (greatest integer) function. $)
    flval2t $p |- ( A e. RR -> ( floor ` A ) =
            U. { x e. ZZ | ( x <_ A /\ A. y e. ZZ ( y <_ A -> y <_ x ) ) } ) $=
      ( cr wcel cv cle wbr wi cz wral wa crab cuni cfl cfv wceq fllet flget
      biimpd r19.21aiva jca wreu wb breq1 breq2 imbi2d ralbidv anbi12d reuuni2
      flclt zmax sylanc mpbid eqcomd ) CDEZAFZCGHZBFZCGHZUSUQGHZIZBJKZLZAJMNZCO
      PZUPVFCGHZUTUSVFGHZIZBJKZLZVEVFQZUPVGVJCRUPVIBJUPUSJELUTVHCUSSTUAUBVFJEVD
      AJUCVKVLUDUPVDVKAJVFUQVFQZURVGVCVJUQVFCGUEVMVBVIBJVMVAVHUTUQVFUSGUFUGUHUI
      UJCUKABCULUMUNUO $.
      $( [8-Feb-2005] $) $( [16-Nov-2004] $)

    $( An alternate way to define the floor (greatest integer) function, as the
       supremum of all integers less than or equal to its argument. $)
    flval3t $p |- ( A e. RR ->
                  ( floor ` A ) = sup ( { x e. ZZ | x <_ A } , RR , < ) ) $=
      ( vz vy vw cr wcel cfl cfv cv cle wbr cz crab clt csup wceq wn wa wss c0
      wral wrex w3a suprub ssrab2 zssre sstri a1i flclt fllet jca breq1 elrab
      sylibr n0i syl breq2 ralbidv rcla4ev zret wi flget biimpd ex imp3a
      syl5ib r19.21aiv sylanc 3jca suprnub wb lenltt syl2an ancoms bitrd
      eqleltt suprcl syl3anc mpbird ) BFGZBHIZAJZBKLZAMNZFOPZQZWBWFKLZWBWFOLRZS
      ZWAWHWIWEFTZWEUAQRZCJZDJZKLZCWEUBZDFUCZUDZWBWEGZWHWADCWEWBUEWAWKWLWQWKWAW
      EMFWDAMUFUGUHUIZWAWSWLWAWBMGZWBBKLZSWSWAXAXBBUJZBUKULWDXBAWBMWCWBBKUMUNUO
      ZWEWBUPUQZWBFGZWMWBKLZCWEUBZWQWAWPXHDWBFWNWBQWOXGCWEWNWBWMKURUSUTWAXAXFXC
      WBVAUQZWAXGCWEWAWMMGZWMBKLZSXGWMWEGWAXJXKXGWAXJXKXGVBWAXJSXKXGBWMVCVDVEVF
      WDXKAWMMWCWMBKUMUNVGVHVIZVJZXDVIWRXFWBEJZOLRZEWEUBZSWIWADCEWEWBVKXMWAXFXP
      XIWAXOEWEWAXNMGZXNBKLZSXOXNWEGWAXQXRXOWAXQXRXOVBWAXQSZXRXOXSXRXNWBKLZXOBX
      NVCXQWAXTXOVLZXNFGXFYAXQWAXNWBVMXNVAXIVNVOVPVDVEVFWDXRAXNMWCXNBKUMUNVGVHU
      LVIULXFWFFGZWGWJVLWAWBWFVQXIWKWLWQYBWADCWEVRWTXEXLVSVIVT $.
      $( [30-May-2005] $) $( [15-Nov-2004] $)
  $}

  $( Ordering relationship for the greatest integer function. $)
  flwordit $p |- ( ( A e. RR /\ B e. RR /\ A <_ B ) ->
                 ( floor ` A ) <_ ( floor ` B ) ) $=
    ( cr wcel cle wbr cfl cfv wa fllet adantr wi letrt cz flclt zret syl
    pm3.26 pm3.27 syl3anc mpand wb flget sylan2 ancoms sylibd 3impia ) ACDZBCDZ
    ABEFZAGHZBGHEFZUHUIIZUJUKBEFZULUMUKAEFZUJUNUHUOUIAJKUKCDZUHUIUOUJIUNLUMUKAB
    MUHUPUIUHUKNDZUPAOZUKPQKUHUIRUHUISTUAUIUHUNULUBZUIUQUSUHBUKUCURUDUEUFUG $.
    $( [2-Feb-2006] $) $( [31-Dec-2005] $)

  ${
    $d x A $.  $d x B $.
    $( A condition equivalent to floor. $)
    flbit $p |- ( ( A e. RR /\ B e. ZZ ) ->
                ( ( floor ` A ) = B <-> ( B <_ A /\ A < ( B + 1 ) ) ) ) $=
      ( vx cr wcel cz wa cfl cfv wceq cv cle wbr c1 caddc co clt crab cuni wb
      flvalt eqeq1d adantr wreu breq1 opreq1 breq2d anbi12d reuuni2 rebtwnz
      sylan2 ancoms bitr4d ) ADEZBFEZGAHIZBJZCKZALMZAURNOPZQMZGZCFRSZBJZBALMZAB
      NOPZQMZGZUNUQVDTUOUNUPVCBCAUAUBUCUOUNVHVDTZUOVBCFUDVIUNVBVHCFBURBJZUSVEVA
      VGURBALUEVJUTVFAQURBNOUFUGUHUICAUJUKULUM $.
      $( [28-Apr-2005] $) $( [11-Mar-2005] $)
  $}

  $( The floor of a number greater than or equal to 0 is a nonnegative
     integer. $)
  flge0nn0t $p |- ( ( A e. RR /\ 0 <_ A ) -> ( floor ` A ) e. NN0 ) $=
    ( cr wcel cc0 cle wbr wa cfl cfv cz cn0 flclt adantr wb 0z flget mpan2
    biimpa jca elnn0z sylibr ) ABCZDAEFZGZAHIZJCZDUEEFZGUEKCUDUFUGUBUFUCALMUBUC
    UGUBDJCUCUGNOADPQRSUETUA $.
    $( [2-May-2005] $) $( [26-Apr-2005] $)

  $( The floor of a number greater than or equal to 1 is a natural number. $)
  flge1nnt $p |- ( ( A e. RR /\ 1 <_ A ) -> ( floor ` A ) e. NN ) $=
    ( cr wcel c1 cle wbr wa cfl cfv cz cn flclt adantr wb 1z flget mpan2
    biimpa jca elnnz1 sylibr ) ABCZDAEFZGZAHIZJCZDUEEFZGUEKCUDUFUGUBUFUCALMUBUC
    UGUBDJCUCUGNOADPQRSUETUA $.
    $( [24-Jun-2005] $) $( [26-Apr-2005] $)

  $( An integer can be moved in and out of a sum with a floor. $)
  fladdzt $p |- ( ( A e. RR /\ B e. ZZ ) -> ( floor ` ( A + B ) ) =
                ( ( floor ` A ) + B ) ) $=
    ( cr wcel cz wa caddc co cfl cfv wceq cle wbr c1 clt fllet adantr wb
    leadd1t 3expa flclt zret syl ancri syl2an mpbid flltp1t ltadd1t peano2z
    3syl ancli cc ax1cn add23t mp3an2 zcnt breqtrd jca flbit axaddrcl sylan2
    zaddclt sylan sylanc mpbird ) ACDZBEDZFZABGHZIJAIJZBGHZKZVKVILMZVIVKNGHZOMZ
    FZVHVMVOVHVJALMZVMVFVQVGAPQVJCDZVFFBCDZVQVMRZVFVGVRVFVSVTVJABSTVFVRVFVJEDZV
    RAUAZVJUBUCUDBUBZUEUFVHVIVJNGHZBGHZVNOVHAWDOMZVIWEOMZVFWFVGAUGQVFWDCDZFVSWF
    WGRZVFVGVFWHVSWIAWDBUHTVFWHVFWAWDEDWHWBVJUIWDUBUJUKWCUEUFVJULDZBULDZWEVNKZV
    FVGWJNULDWKWLUMVJNBUNUOVFWAWJWBVJUPUCBUPUEUQURVICDZVKEDZVLVPRVHVIVKUSVFVSWM
    VGABUTWCVAWAVGWNVFVJBVBWBVCVDVE $.
    $( [13-Jun-2005] $) $( [27-Apr-2005] $)

  $( A real bounded between an integer and its successor is nonnegative iff
     the integer is nonnegative.  Second half of Lemma 13-4.1 of [Gleason]
     p. 217.  (For the first half see ~ rebtwnz .) $)
  btwnzge0t $p |- ( ( ( A e. RR /\ B e. ZZ ) /\ ( B <_ A /\ A < ( B + 1 ) ) )
                -> ( 0 <_ A <-> 0 <_ B ) ) $=
    ( cr wcel cz wa cle wbr c1 caddc co clt cc0 cfl cfv wb 0z flget mpan2
    ad2antrr wceq flbit biimpar breq2d bitrd ) ACDZBEDZFZBAGHABIJKLHFZFZMAGHZMA
    NOZGHZMBGHUFUKUMPZUGUIUFMEDUNQAMRSTUJULBMGUHULBUAUIABUBUCUDUE $.
    $( [28-Apr-2005] $) $( [12-Mar-2005] $)

  $( Ordering relation for the floor of half of an integer. $)
  flhalft $p |- ( N e. ZZ -> N <_ ( 2 x. ( floor ` ( ( N + 1 ) / 2 ) ) ) ) $=
    ( cz wcel c2 cdiv co c1 caddc wo cfl cfv cmul cle wbr zeot wa flidt
    opreq2d cc wceq zcnt cc0 wne 2cn 2re 2pos gt0ne0i divcan2t mp3an13 syl
    sylan9eqr cr w3a lemul2it zret rehalfclt flclt 3syl peano2re a1i 3jca
    flwordit lep1t adantr wb clt lediv1t mpan2 mp3an3 mpbid sylanc syl3anc 0re
    ltlei jctil eqbrtrrd peano2cn breqtrrd jaodan mpdan ) ABCZADEFZBCZAGHFZDEFZ
    BCZIADWEJKZLFZMNZAOWAWCWIWFWAWCPDWBJKZLFZAWHMWCWAWKDWBLFZAWCWJWBDLWBQRWAASC
    ZWLATZAUAZDSCZWMDUBUCZWNUDDUEUFUGZDAUHUIUJUKWAWKWHMNZWCWJULCZWGULCZDULCZUMU
    BDMNZWJWGMNZPWSWAWJWGDUNWAWTXAXBWAWBULCZWJBCWTWAAULCZXEAUOZAUPUJZWBUQWJUOUR
    WAWEULCZWGBCXAWAXFWDULCZXIXGAUSZWDUPURZWEUQWGUOURXBWAUEUTVAWAXDXCXEXIWBWEMN
    ZXDWAWBWEVBXHXLXFXJXMWAXFXJPAWDMNZXMXFXNXJAVCZVDXFXJXBXNXMVEZUEXFXJXBUMUBDV
    FNXPUFAWDDVGVHVIVJXGWAXFXJXGXKUJVKVLUBDVMUEUFVNVOVKVDVPWAWFPAWDWHMWAXNWFWAX
    FXNXGXOUJVDWFWAWHDWELFZWDWFWGWEDLWEQRWAWMWDSCZXQWDTZWOAVQWPXRWQXSUDWRDWDUHU
    IURUKVRVSVT $.
    $( [7-Feb-2006] $) $( [1-Jan-2006] $)

