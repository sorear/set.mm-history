$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Real and complex number postulates
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u f $.
    $( Addition is an operation on the complex numbers.  This theorem can be
       used as an alternate axiom for complex numbers in place of the less
       specific ~ axaddcl . $)
    axaddopr $p |- + : ( CC X. CC ) --> CC $=
      ( vx vy vw vv vu vf vz cc cxp caddc wf wfn cv co wcel wral ffnoprval
      wfun cdm wceq df-fn wa cop cplr wex copab2 wmo moeq mosubop anass 2exbii
      19.42vv bitr mobii mpbir moani funoprab wb df-plus funeq ax-mp dmeqi
      dmoprabss eqsstr 0ncn cnr df-c opreq1 eleq1d opreq2 addcnsr addclsr
      anim12i an4s opelxpi syl eqeltrd 2optocl syl6eleqr oprssdm eqssi
      mpbir2an rgen2 ) HHIZHJKJWDLZAMZBMZJNZHOZBHPAHPABHHHJQWEJRZJSZWDTJWDUAWJW
      FHOWGHOUBZWFCMZDMZUCTZWGEMZFMZUCTZUBGMZWMWPUDNZWNWQUDNUCZTZUBZFUEEUEZDUEC
      UEZUBZABGUFZRZXFABGXEWLGXEGUGWOWRXBUBZFUEEUEZUBZDUECUEZGUGXJGCDWFXBGEFWGG
      XAUHUIUIXEXLGXDXKCDXDWOXIUBZFUEEUEXKXCXMEFWOWRXBUJUKWOXIEFULUMUKUNUOUPUQJ
      XGTWJXHURABGCDEFUSZJXGUTVAUOWKWDWKXGSWDJXGXNVBXEABGHHVCVDABHJVEWLWHVFVFIZ
      HWSWMUCZWNWPUCZJNZXOOWFXQJNZXOOWHXOOGCDEWFWGVFVFHVGXPWFTXRXSXOXPWFXQJVHVI
      XQWGTXSWHXOXQWGWFJVJVIWSVFOZWMVFOZUBWNVFOZWPVFOZUBUBZXRWSWNUDNZWTUCZXOWSW
      MWNWPVKYDYEVFOZWTVFOZUBZYFXOOXTYBYAYCYIXTYBUBYGYAYCUBYHWSWNVLWMWPVLVMVNYE
      WTVFVFVOVPVQVRVGVSZVTWAWBWIABHYJWCWB $.
      $( [9-Feb-2005] $) $( [8-Feb-2005] $)

    $( Multiplication is an operation on the complex numbers.  This theorem can
       be used as an alternate axiom for complex numbers in place of the less
       specific ~ axmulcl . $)
    axmulopr $p |- x. : ( CC X. CC ) --> CC $=
      ( vx vy vw vv vu vf vz cc cxp cmul wf wfn cv co wcel wral ffnoprval wfun
      cdm wceq df-fn wa cop cmr cm1r cplr wex copab2 wmo moeq mosubop anass
      2exbii 19.42vv bitr mobii mpbir moani funoprab wb df-mul funeq ax-mp
      dmeqi dmoprabss eqsstr 0ncn cnr df-c opreq1 eleq1d opreq2 mulcnsr
      opelxpi addclsr mulclsr m1r mpan syl syl2an an4s ancoms an42s sylanc
      eqeltrd 2optocl syl6eleqr oprssdm eqssi mpbir2an rgen2 ) HHIZHJKJWLLZAMZB
      MZJNZHOZBHPAHPABHHHJQWMJRZJSZWLTJWLUAWRWNHOWOHOUBZWNCMZDMZUCTZWOEMZFMZUCT
      ZUBGMZXAXDUDNZUEXBXEUDNUDNUFNXBXDUDNXAXEUDNUFNUCZTZUBZFUGEUGZDUGCUGZUBZAB
      GUHZRZXNABGXMWTGXMGUIXCXFXJUBZFUGEUGZUBZDUGCUGZGUIXRGCDWNXJGEFWOGXIUJUKUK
      XMXTGXLXSCDXLXCXQUBZFUGEUGXSXKYAEFXCXFXJULUMXCXQEFUNUOUMUPUQURUSJXOTWRXPU
      TABGCDEFVAZJXOVBVCUQWSWLWSXOSWLJXOYBVDXMABGHHVEVFABHJVGWTWPVHVHIZHXGXAUCZ
      XBXDUCZJNZYCOWNYEJNZYCOWPYCOGCDEWNWOVHVHHVIYDWNTYFYGYCYDWNYEJVJVKYEWOTYGW
      PYCYEWOWNJVLVKXGVHOZXAVHOZUBXBVHOZXDVHOZUBUBZYFXGXBUDNZUEXHUDNZUFNZXAXBUD
      NZXGXDUDNZUFNZUCZYCXGXAXBXDVMYOVHOZYRVHOZYSYCOYLYOYRVHVHVNYHYJYIYKYTYMVHO
      YNVHOZYTYHYJUBYIYKUBZYMYNVOXGXBVPUUCXHVHOZUUBXAXDVPUEVHOUUDUUBVQUEXHVPVRV
      SVTWAYHYKYIYJUUAYIYJUBZYHYKUBZUUAYPVHOYQVHOUUAUUEUUFYPYQVOXAXBVPXGXDVPVTW
      BWCWDWEWFVIWGZWHWIWJWQABHUUGWKWJ $.
      $( [9-Feb-2005] $) $( [8-Feb-2005] $)
  $}

  $( The class of complex numbers is a set, i.e. it is a member of the universe
     of sets ` V ` (see ~ isset ).  Axiom 1 of 27 for real and complex
     numbers, derived from ZF set theory. $)
  axcnex $p |- CC e. V $=
    ( cc cnr cxp cvv df-c srex xpex eqeltr ) ABBCDEBBFFGH $.
    $( [13-Aug-1995] $)

  $( The real numbers are a subset of the complex numbers.  Axiom 2 of 27
     for real and complex numbers, derived from ZF set theory. $)
  axresscn $p |- RR (_ CC $=
    ( cnr c0r csn cxp cr cc wss ssid wcel 0r snssi ax-mp ssxp mp2an df-r df-c
    3sstr4 ) ABCZDZAADZEFAAGRAGZSTGAHBAIUAJBAKLAARAMNOPQ $.
    $( [1-Mar-1995] $)

  $( 1 is a complex number.  Axiom 3 of 27 for real and complex numbers,
     derived from ZF set theory. $)
  ax1cn $p |- 1 e. CC $=
    ( cr cc c1 axresscn c1r c0r cop df-1 wcel cnr 1r opelreal mpbir eqeltr
    sselii ) ABCDCEFGZAHPAIEJIKELMNO $.
    $( [12-Apr-2007] $) $( [12-Apr-2007] $)

  $( ` i ` is a complex number.  Axiom 4 of 27 for real and complex
     numbers, derived from ZF set theory. $)
  axicn $p |- i e. CC $=
    ( ci cc wcel c0r cnr c1r cop wa df-i eleq1i 1r elisseti opelcn bitr 0r
    mpbir2an ) ABCZDECZFECZQDFGZBCRSHATBIJDFFEKLMNOKP $.
    $( [23-Feb-1996] $)

  ${
    $d x y A $.  $d x y B $.
    $( Closure law for addition of complex numbers.  Axiom 5 of 27 for
       real and complex numbers, derived from ZF set theory. $)
    axaddcl $p |- ( ( A e. CC /\ B e. CC ) -> ( A + B ) e. CC ) $=
      ( cc caddc axaddopr foprcl ) ABCCCDEF $.
      $( [14-Jun-1995] $)

    $( Closure law for addition in the real subfield of complex numbers.  Axiom
       6 of 27 for real and complex numbers, derived from ZF set
       theory. $)
    axaddrcl $p |- ( ( A e. RR /\ B e. RR ) -> ( A + B ) e. RR ) $=
      ( vx vy cv c0r cop caddc co cr wcel cnr elreal wceq opreq1 eleq1d opreq2
      wa cplr addresr addclsr opelreal sylibr eqeltrd 2gencl ) CEZFGZDEZFGZHIZJ
      KAUIHIZJKABHIZJKCDUGUIABLJCAMDBMUGANUJUKJUGAUIHOPUIBNUKULJUIBAHQPUFLKUHLK
      RZUJUFUHSIZFGZJUFUHTUMUNLKUOJKUFUHUAUNUBUCUDUE $.
      $( [31-Mar-1996] $)

    $( Closure law for multiplication of complex numbers.  Axiom 7 of 27
       for real and complex numbers, derived from ZF set theory. $)
    axmulcl $p |- ( ( A e. CC /\ B e. CC ) -> ( A x. B ) e. CC ) $=
      ( cc cmul axmulopr foprcl ) ABCCCDEF $.
      $( [10-Aug-1995] $)

    $( Closure law for multiplication in the real subfield of complex numbers.
       Axiom 8 of 27 for real and complex numbers, derived from ZF set
       theory. $)
    axmulrcl $p |- ( ( A e. RR /\ B e. RR ) -> ( A x. B ) e. RR ) $=
      ( vx vy cv c0r cop cmul co cr wcel cnr elreal wceq opreq1 eleq1d opreq2
      wa cmr visset mulresr mulclsr opelreal sylibr eqeltrd 2gencl ) CEZFGZDEZF
      GZHIZJKAUJHIZJKABHIZJKCDUHUJABLJCAMDBMUHANUKULJUHAUJHOPUJBNULUMJUJBAHQPUG
      LKUILKRZUKUGUISIZFGZJUGUIDTUAUNUOLKUPJKUGUIUBUOUCUDUEUF $.
      $( [31-Mar-1996] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Addition of complex numbers is commutative.  Axiom 9 of 27 for
       real and complex numbers, derived from ZF set theory. $)
    axaddcom $p |- ( ( A e. CC /\ B e. CC ) -> ( A + B ) = ( B + A ) ) $=
      ( vx vy vz vw cc cv cplr co cep ccnv cnr caddc dfcnqs addcnsrec visset
      addcomsr ecoprcom ) CDEFABGCHZEHZIJKLMNDHZFHZIJUATIJUCUBIJOTUBUAUCPUAUCTU
      BPTUACQEQRUBUCDQFQRS $.
      $( [31-Aug-1995] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Multiplication of complex numbers is commutative.  Axiom 10 of 27
       for real and complex numbers, derived from ZF set theory. $)
    axmulcom $p |- ( ( A e. CC /\ B e. CC ) -> ( A x. B ) = ( B x. A ) ) $=
      ( vx vy vz vw cc cv cmr co cm1r cplr cep ccnv cnr cmul dfcnqs mulcnsrec
      visset mulcomsr opreq2i opreq12i oprex addcomsr eqtr ecoprcom ) CDEFABGCH
      ZEHZIJZKDHZFHZIJZIJZLJMNOPUJUHIJZUGUKIJZLJZUHUGIJZKUKUJIJZIJZLJUKUGIJZUHU
      JIJZLJZQUGUJUHUKRUHUKUGUJRUIUQUMUSLUGUHCSZESZTULURKIUJUKDSZFSZTUAUBUPVAUT
      LJVBUNVAUOUTLUJUHVEVDTUGUKVCVFTUBVAUTUHUJIUCUKUGIUCUDUEUF $.
      $( [31-Aug-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.

    $( Addition of complex numbers is associative.  This theorem transfers the
       associative laws for the real and imaginary signed real components of
       complex number pairs, to complex number addition itself.  Axiom 11 of
       27 for real and complex numbers, derived from ZF set theory. $)
    axaddass $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( ( A + B ) + C ) = ( A + ( B + C ) ) ) $=
      ( vx vy vz vw vv vu cc cv cplr co cep ccnv cnr caddc dfcnqs addcnsrec
      wcel wa addclsr anim12i an4s visset addasssr ecoprass ) DEFGHIABCJGKZIKZL
      MZNOPQDKZFKZLMZEKZUHLMZUMHKZLMUOUILMUKULUPLMZLMUNUJLMUQRUKUNULUHSULUHUPUI
      SUMUOUPUISUKUNUQUJSUKPTZULPTZUNPTZUHPTZUMPTZUOPTZUAURUSUAVBUTVAUAVCUKULUB
      UNUHUBUCUDUSUPPTZVAUIPTZUQPTZUJPTZUAUSVDUAVFVAVEUAVGULUPUBUHUIUBUCUDUKULU
      PFUEHUEUFUNUHUIGUEIUEUFUG $.
      $( [2-Sep-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u f g h $.
    $( Multiplication of complex numbers is associative.  Axiom 12 of 27
       for real and complex numbers, derived from ZF set theory. $)
    axmulass $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( ( A x. B ) x. C ) = ( A x. ( B x. C ) ) ) $=
      ( vx vy vz vw vv vu vf vg vh cc cv cmr co cplr cep ccnv cnr cmul cm1r
      dfcnqs mulcnsrec wcel wa addclsr mulclsr m1r mpan syl syl2an an4s ancoms
      an42s jca oprex visset addcomsr addasssr caopr42 distrsr opreq2i eqtr
      opreq12i elisseti mulcomsr mulasssr caoprdilem caopr12 3eqtr 3eqtr4r
      ecoprass ) DEFGHIABCMGNZHNZOPZFNZINZOPZQPZRSTUADNZVQOPZUBENZVNOPZOPZQPZWC
      VQOPZWAVNOPZQPZWFVOOPZUBWIVROPZOPZQPZWIVOOPZWFVROPZQPZWAVQVOOPZUBVNVROPZO
      PZQPZOPZUBWCVTOPZOPZQPZWCWTOPZWAVTOPZQPZWTUCWAWCVQVNUDVQVNVOVRUDWFWIVOVRU
      DWAWCWTVTUDWATUEZWCTUEZUFVQTUEZVNTUEZUFZUFWFTUEZWITUEZXHXJXIXKXMWBTUEWETU
      EZXMXHXJUFXIXKUFZWBWEUGWAVQUHXPWDTUEZXOWCVNUHUBTUEZXQXOUIUBWDUHUJUKULUMXH
      XKXIXJXNXIXJUFZXHXKUFZXNWGTUEWHTUEXNXSXTWGWHUGWCVQUHWAVNUHULUNUOUPXLVOTUE
      ZVRTUEZUFUFWTTUEZVTTUEZXJYAXKYBYCWQTUEWSTUEZYCXJYAUFXKYBUFZWQWSUGVQVOUHYF
      WRTUEZYEVNVRUHXRYGYEUIUBWRUHUJUKULUMXJYBXKYAYDXKYAUFZXJYBUFZYDVPTUEVSTUEY
      DYHYIVPVSUGVNVOUHVQVRUHULUNUOUPWAWQOPZWAWSOPZQPZUBWCVPOPZOPZUBWCVSOPZOPZQ
      PZQPYJYNQPZYPYKQPZQPXDWMJKLYJYKYNYPQWAWQOUQWAWSOUQUBYMOUQJNZKNZJURZKURZUS
      ZYTUUALNZUUCLURZUTZUBYOOUQVAXAYLXCYQQWAWQWSVQVOOUQZUBWROUQZVBXCUBYMYOQPZO
      PYQXBUUJUBOWCVPVSVNVOOUQZVQVROUQZVBVCUBYMYOWCVPOUQWCVSOUQZVBVDVEWJYRWLYSQ
      WJYJUBWDVOOPZOPZQPYRJKLWAUBVQWDQOVODURZUBTUIVFZFURZYTUUAUUBUUCVGZYTUUAUUE
      UUCUUFVBZWCVNOUQZHURZYTUUAUUEUUCUUFVHZVIUUOYNYJQUUNYMUBOWCVNVOGURZUVBVHVC
      VCVDWLUBYOWAWROPZQPZOPYPUBUVEOPZQPYSWKUVFUBOJKLWCWAVQVNQOVREURZUUPUURUUSU
      UTUVDIURZUVCVIVCUBYOUVEUUMWAWROUQVBUVGYKYPQJKLUBWAWROUUQUUPVNVROUQZUUSUVC
      VJVCVKVEVLWCWQOPZWCWSOPZQPZWAVPOPZWAVSOPZQPZQPUVKUVNQPZUVOUVLQPZQPXGWPJKL
      UVKUVLUVNUVOQWCWQOUQWCWSOUQWAVPOUQUUDUUGWAVSOUQVAXEUVMXFUVPQWCWQWSUUHUUIV
      BWAVPVSUUKUULVBVEWNUVQWOUVRQJKLWCWAVQVNQOVOUVHUUPUURUUSUUTUVDUVBUVCVIWOUV
      OUBWDVROPZOPZQPUVRJKLWAUBVQWDQOVRUUPUUQUURUUSUUTUVAUVIUVCVIUVTUVLUVOQUVTU
      BWCWROPZOPUVLUVSUWAUBOWCVNVRUVDUVIVHVCJKLUBWCWROUUQUVHUVJUUSUVCVJVDVCVDVE
      VLVM $.
      $( [3-Sep-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u f g h $.
    $( Distributive law for complex numbers.  Axiom 13 of 27 for real and
       complex numbers, derived from ZF set theory. $)
    axdistr $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( A x. ( B + C ) ) = ( ( A x. B ) + ( A x. C ) ) ) $=
      ( vx vy vz vw vv vu vf vg vh cc cep ccnv cnr caddc cmul cv cplr co cmr
      cm1r dfcnqs addcnsrec mulcnsrec wcel wa addclsr anim12i an4s mulclsr m1r
      mpan syl syl2an ancoms an42s jca visset distrsr opreq2i oprex eqtr
      opreq12i addcomsr addasssr caopr4 ecoprdi ) DEFGHIABCMNOPQRDSZFSZHSZTUAZU
      BUAZUCESZGSZISZTUAZUBUAZUBUAZTUAZVOVMUBUAZVJVRUBUAZTUAZVJVKUBUAZUCVOVPUBU
      AZUBUAZTUAZVJVLUBUAZUCVOVQUBUAZUBUAZTUAZTUAZVOVKUBUAZVJVPUBUAZTUAZVOVLUBU
      AZVJVQUBUAZTUAZTUAZVMVRWHWPWLWSUDVKVPVLVQUEVJVOVMVRUFVJVOVKVPUFVJVOVLVQUF
      WHWPWLWSUEVKPUGZVLPUGZVPPUGZVQPUGZVMPUGZVRPUGZUHXAXBUHXEXCXDUHXFVKVLUIVPV
      QUIUJUKVJPUGZVOPUGZUHZXAXCUHUHWHPUGZWPPUGZXGXAXHXCXJWEPUGWGPUGZXJXGXAUHXH
      XCUHZWEWGUIVJVKULXMWFPUGZXLVOVPULUCPUGZXNXLUMUCWFULUNUOUPUKXGXCXHXAXKXHXA
      UHZXGXCUHZXKWNPUGWOPUGXKXPXQWNWOUIVOVKULVJVPULUPUQURUSXIXBXDUHUHWLPUGZWSP
      UGZXGXBXHXDXRWIPUGWKPUGZXRXGXBUHXHXDUHZWIWKUIVJVLULYAWJPUGZXTVOVQULXOYBXT
      UMUCWJULUNUOUPUKXGXDXHXBXSXHXBUHZXGXDUHZXSWQPUGWRPUGXSYCYDWQWRUIVOVLULVJV
      QULUPUQURUSWAWEWITUAZWGWKTUAZTUAWMVNYEVTYFTVJVKVLFUTZHUTZVAVTUCWFWJTUAZUB
      UAYFVSYIUCUBVOVPVQGUTZIUTZVAVBUCWFWJVOVPUBVCVOVQUBVCVAVDVEJKLWEWIWGWKTVJV
      KUBVCVJVLUBVCUCWFUBVCJSZKSZJUTKUTZVFZYLYMLSYNLUTVGZUCWJUBVCVHVDWDWNWQTUAZ
      WOWRTUAZTUAWTWBYQWCYRTVOVKVLYGYHVAVJVPVQYJYKVAVEJKLWNWQWOWRTVOVKUBVCVOVLU
      BVCVJVPUBVCYOYPVJVQUBVCVHVDVI $.
      $( [2-Sep-1995] $)
  $}

  $( 1 and 0 are distinct.  Axiom 14 of 27 for real and complex numbers,
     derived from ZF set theory. $)
  ax1ne0 $p |- 1 =/= 0 $=
    ( c1 cc0 wne wceq wn c1r c0r cop 1ne0sr cnr 1r elisseti eqresr mtbir df-1
    df-0 eqeq12i df-ne mpbir ) ABCABDZETFGHZGGHZDZUCFGDIFGFJKLMNAUABUBOPQNABRS
    $.
    $( [19-Mar-1996] $)

  ${
    $d x y A $.
    $( 0 is an identity element for addition.  Axiom 15 of 27 for real
       and complex numbers, derived from ZF set theory. $)
    ax0id $p |- ( A e. CC -> ( A + 0 ) = A ) $=
      ( vx vy cv cop cc0 caddc co wceq cnr cc df-c opreq1 id eqeq12d wcel wa
      c0r cplr 0r pm3.2i addcnsr mpan2 opeq12 0idsr syl2an eqtrd df-0 opreq2i
      syl5eq optocl ) BDZCDZEZFGHZUNIAFGHZAIBCAJJKLUNAIZUOUPUNAUNAFGMUQNOULJPZU
      MJPZQZUNRREZGHZUNUOUTVBULRSHZUMRSHZEZUNUTRJPZVFQVBVEIVFVFTTUAULUMRRUBUCVC
      ULIVDUMIVEUNIURUSVCVDULUMUDULUEUMUEUFUGFVAUNGUHUIUJUK $.
      $( [31-Mar-1996] $)
  $}

  ${
    $d x y A $.
    $( 1 is an identity element for multiplication.  Axiom 16 of 27 for
       real and complex numbers, derived from ZF set theory. $)
    ax1id $p |- ( A e. CC -> ( A x. 1 ) = A ) $=
      ( vx vy cv cop c1 cmul co wceq cnr cc df-c opreq1 id eqeq12d wcel wa c1r
      c0r cmr cm1r cplr 1r 0r pm3.2i mulcnsr mpan2 00sr opreq2d m1r ax-mp
      syl6eq 1idsr opreq1d 0idsr eqtrd sylan9eqr sylan9eq opeq12d df-1 opreq2i
      syl5eq optocl ) BDZCDZEZFGHZVFIAFGHZAIBCAJJKLVFAIZVGVHVFAVFAFGMVINOVDJPZV
      EJPZQZVFRSEZGHZVFVGVLVNVDRTHZUAVESTHZTHZUBHZVERTHZVDSTHZUBHZEZVFVLRJPZSJP
      ZQVNWBIWCWDUCUDUEVDVERSUFUGVLVRVDWAVEVKVJVRVOSUBHZVDVKVQSVOUBVKVQUASTHZSV
      KVPSUATVEUHUIUAJPWFSIUJUAUHUKULUIVJWEVDSUBHVDVJVOVDSUBVDUMUNVDUOUPUQVJVKW
      AVSSUBHZVEVJVTSVSUBVDUHUIVKWGVESUBHVEVKVSVESUBVEUMUNVEUOUPURUSUPFVMVFGUTV
      AVBVC $.
      $( [31-Mar-1996] $)
  $}

  ${
    $d x y z w v A $.
    $( Existence of negative of complex number.  Axiom 17 of 27 for real
       and complex numbers, derived from ZF set theory. $)
    axnegex $p |- ( A e. CC -> E. x e. CC ( A + x ) = 0 ) $=
      ( vy vz vw vv cc wcel cv caddc co cc0 wceq wa wex wrex cop cnr df-c
      opreq1 eqeq1d anbi2d exbidv cplr c0r negexsr anim12i eeanv sylibr opex
      eleq1 opreq2 anbi12d cla4ev cxp opelxpi syl6eleqr ad2antlr addcnsr
      opeq12 df-0 syl6eqr sylan9eq sylanc exp31 imp3a an4 syl5ib 19.23advv mpd
      optocl df-rex ) BGHAIZGHZBVMJKZLMZNZAOZVPAGPVNCIZDIZQZVMJKZLMZNZAOZVRCDBR
      RGSWABMZWDVQAWFWCVPVNWFWBVOLWABVMJTUAUBUCVSRHZVTRHZNZEIZRHZVSWJUDKZUEMZNZ
      FIZRHZVTWOUDKZUEMZNZNZFOEOZWEWIWNEOZWSFOZNXAWGXBWHXCEVSUFFVTUFUGWNWSEFUHU
      IWIWTWEEFWIWKWPNZWMWRNZNWEWTWIXDXEWEWIXDXEWEWJWOQZGHZWAXFJKZLMZWEWIXDNZXE
      NWDXGXINAXFWJWOUJVMXFMZVNXGWCXIVMXFGUKXKWBXHLVMXFWAJULUAUMUNXDXGWIXEXDXFR
      RUOGWJWORRUPSUQURXJXEXHWLWQQZLVSVTWJWOUSXEXLUEUEQLWLWQUEUEUTVAVBVCVDVEVFW
      KWMWPWRVGVHVIVJVKVPAGVLUI $.
      $( [3-May-1996] $)
  $}

  ${
    $d x y z A $.  $d x y z w v u $.
    $( Existence of reciprocal of nonzero complex number.  Axiom 18 of 27
       for real and complex numbers, derived from ZF set theory. $)
    axrecex $p |- ( ( A e. CC /\ A =/= 0 ) -> E. x e. CC ( A x. x ) = 1 ) $=
      ( vy vz vw vv vu cc wcel cc0 wne cv cmul co c1 wceq wrex cop wi cnr df-c
      neeq1 opreq1 eqeq1d rexbidv imbi12d wa wex cmr cplr c1r c0r wn cltr wbr
      visset ssgt0sr oprex recexsrlem syl6 df-ne df-0 eqeq2i 0r elisseti opth
      bitr negbii syl5ib cm1r opex eleq1 opreq2 anbi12d cla4ev mulclsr m1r
      jctil syl anim12i anandir opelcn 3imtr4 adantrr pm3.26 anandirs jca
      mulcnsr eqeq2 mulasssr eqcomi a1i 1idsr 3syl m1m1sr opreq1i mulcomsr
      caopr12 opreq2i eqtr4 1r 3eqtr4 3eqtr4g opreq12d distrsr caoprdistrr
      syl6eqr syl5bi exp3a com3l imp32 adantll sylan2 anassrs pn0sr eqtr
      opreq12i syl5eq opeq12d eqtrd df-1 sylanc ex 19.23adv syld df-rex
      syl6ibr optocl imp ) BHIBJKZBALZMNZOPZAHQZCLZDLZRZJKZYQYKMNZOPZAHQZSYJYNS
      CDBTTHUAYQBPZYRYJUUAYNYQBJUBUUBYTYMAHUUBYSYLOYQBYKMUCUDUEUFYOTIZYPTIZUGZY
      RYKHIZYTUGZAUHZUUAUUEYRELZTIZYOYOUINZYPYPUINZUJNZUUIUINZUKPZUGZEUHZUUHUUE
      YOULPYPULPUGZUMZUUQYRUUEUUSULUUMUNUOUUQYOYPCUPZDUPZUQEUUMUUKUULUJURUSUTYR
      YQJPZUMUUSYQJVAUVBUURUVBYQULULRZPUURJUVCYQVBVCYOYPULULUUTUVAULTVDVEVFVGVH
      VGVIUUEUUPUUHEUUEUUPUUHYOUUIUINZVJYPUUIUINZUINZRZHIZYQUVGMNZOPZUUHUUEUUPU
      GZUUGUVHUVJUGAUVGUVDUVFVKYKUVGPZUUFUVHYTUVJYKUVGHVLUVLYSUVIOYKUVGYQMVMUDV
      NVOUUEUUJUVHUUOUUCUUJUGZUUDUUJUGZUGUVDTIZUVFTIZUGZUUEUUJUGZUVHUVMUVOUVNUV
      PYOUUIVPUVNVJTIZUVETIZUGUVPUVNUVTUVSYPUUIVPZVQVRVJUVEVPVSVTZUUCUUDUUJWAUV
      DUVFVJUVEUIURWBWCWDUVKUVIUKULRZOUVKUVIYOUVDUINZVJYPUVFUINZUINZUJNZYPUVDUI
      NZYOUVFUINZUJNZRZUWCUVKUUEUVQUGZUVIUWKPUUEUUJUWLUUOUVRUUEUVQUUEUUJWEUUCUU
      DUUJUVQUWBWFWGWDYOYPUVDUVFWHVSUVKUWGUKUWJULUUDUUPUWGUKPZUUCUUDUUJUUOUWMUU
      OUUDUUJUWMUUOUUDUUJUWMUUOUWGUUNPUWMUVNUUNUKUWGWIUVNUWGUUKUUIUINZUULUUIUIN
      ZUJNUUNUVNUWDUWNUWFUWOUJUWDUWNPUVNUWNUWDYOYOUUIUUTEUPZWJWKWLUVNYPUVEUINZU
      KUINZUWQUWFUWOUVNUUDUVTUGUWQTIUWRUWQPUVNUUDUVTUUDUUJWEUWAWGYPUVEVPUWQWMWN
      VJVJUINZUWQUINZUKUWQUINUWFUWRUWSUKUWQUIWOWPUWFVJVJUWQUINZUINUWTUWEUXAVJUI
      FGAYPVJUVEUIUVAVJTVQVEZYPUUIUIURZFLZGLZFUPGUPZWQZUXDUXEYKUXFAUPZWJZWRWSVJ
      VJUWQUXBYPUVEUIURZWJWTUWQUKUXJUKTXAVEWQXBYPYPUUIUVAUWPWJXCXDFGAUUKUULUUIU
      JUIYOYOUIURYPYPUIURUWPUXGUXDUXEYKUXFUXHXEXFXGXHXIXJXKXLUVKYOUVEUINZTIZUWJ
      ULPUUEUUJUXLUUOUUCUUDUUJUXLUUCUVTUXLUVNYOUVEVPUWAXMXNWDUXLUXKUXKVJUINZUJN
      ULUWJUXKXOUWHUXKUWIUXMUJFGAYPYOUUIUIUVAUUTUWPUXGUXIWRUWIVJUXKUINUXMFGAYOV
      JUVEUIUUTUXBUXCUXGUXIWRVJUXKUXBYOUVEUIURWQXPXQXRVSXSXTYAXGYBYCYDYEYTAHYFY
      GYHYI $.
      $( [14-May-1996] $)
  $}

  ${
    $d x y A $.  $d x y z $.
    $( Existence of negative of real number.  Axiom 19 of 27 for real and
       complex numbers, derived from ZF set theory. $)
    axrnegex $p |- ( A e. RR -> E. x e. RR ( A + x ) = 0 ) $=
      ( vy vz cv c0r cop caddc co cc0 wceq cr wrex cnr wcel elreal opreq1
      eqeq1d rexbidv cplr wa wex negexsr addresr df-0 eqeq2i oprex eqresr bitr
      syl6bb pm5.32da opex eleq1 opreq2 anbi12d cla4ev opelreal sylanbr df-rex
      sylibr syl6bir 19.23adv mpd gencl ) CEZFGZAEZHIZJKZALMZBVGHIZJKZALMVENOZB
      LOCVFBCBPVFBKZVIVLALVNVHVKJVFBVGHQRSVMDEZNOZVEVOTIZFKZUAZDUBVJDVEUCVMVSVJ
      DVMVSVPVFVOFGZHIZJKZUAZVJVMVPWBVRVMVPUAZWBVQFGZJKZVRWDWAWEJVEVOUDRWFWEFFG
      ZKVRJWGWEUEUFVQFVEVOTUGUHUIUJUKWCVGLOZVIUAZAUBZVJVTLOZWBWJVPWIWKWBUAAVTVO
      FULVGVTKZWHWKVIWBVGVTLUMWLVHWAJVGVTVFHUNRUOUPVOUQURVIALUSUTVAVBVCVD $.
      $( [15-May-1996] $)
  $}

  ${
    $d x y A $.  $d x y z $.
    $( Existence of reciprocal of nonzero real number.  Axiom 20 of 27
       for real and complex numbers, derived from ZF set theory. $)
    axrrecex $p |- ( ( A e. RR /\ A =/= 0 ) -> E. x e. RR ( A x. x ) = 1 ) $=
      ( vy vz cr wcel cc0 wne cv cmul co c1 wceq wrex c0r cop wi cnr elreal
      neeq1 opreq1 eqeq1d rexbidv imbi12d wn wa wex cmr c1r visset recexsr
      mulresr df-1 eqeq2i oprex eqresr bitr syl6bb pm5.32da opelreal anbi1i
      syl5bb opex eleq1 opreq2 anbi12d cla4ev syl6bir 19.23adv syld df-0
      negbii syl5ib df-ne df-rex 3imtr4g gencl imp ) BEFZBGHZBAIZJKZLMZAENZCIZO
      PZGHZWFWAJKZLMZAENZQVTWDQWERFZVSCWFBCBSWFBMZWGVTWJWDWFBGTWLWIWCAEWLWHWBLW
      FBWAJUAUBUCUDWKWFGMZUEZWAEFZWIUFZAUGZWGWJWKWEOMZUEZWQWNWKWSDIZRFZWEWTUHKZ
      UIMZUFZDUGWQDWECUJZUKWKXDWQDWKXDWTOPZEFZWFXFJKZLMZUFZWQWKXAXIUFXDXJWKXAXI
      XCWKXAUFZXIXBOPZLMZXCXKXHXLLWEWTDUJULUBXMXLUIOPZMXCLXNXLUMUNXBUIWEWTUHUOU
      PUQURUSXGXAXIWTUTVAVBWPXJAXFWTOVCWAXFMZWOXGWIXIWAXFEVDXOWHXHLWAXFWFJVEUBV
      FVGVHVIVJWMWRWMWFOOPZMWRGXPWFVKUNWEOXEUPUQVLVMWFGVNWIAEVOVPVQVR $.
      $( [15-May-1996] $)
  $}

  $( i-squared equals -1 (expressed as i-squared plus 1 is 0).  Axiom 21 of 27
     for real and complex numbers, derived from ZF set theory. $)
  axi2m1 $p |- ( ( i x. i ) + 1 ) = 0 $=
    ( c0r c1r cop cmul co caddc ci c1 cc0 cm1r cplr cmr cnr wcel wa wceq 0r 1r
    pm3.2i mulcnsr mp2an oprex elisseti opth 00sr ax-mp 1idsr opreq2i m1r eqtr
    opreq12i addcomsr 0idsr 3eqtr mpbir2an opreq1i addresr m1p1sr eqresr mpbir
    df-i df-1 df-0 3eqtr4 ) ABCZVEDEZBACZFEZAACZGGDEZHFEIVHJACZVGFEZJBKEZACZVIV
    FVKVGFVFAALEZJBBLEZLEZKEZBALEZABLEZKEZCZVKAMNZBMNZOZWEVFWBPWCWDQRSZWFABABTU
    AWBVKPVRJPWAAPVRWAJAVOVQKUBVSVTKUBAMQUCZUDVRAJKEJAKEZJVOAVQJKWCVOAPQAUEUFVQ
    JBLEZJVPBJLWDVPBPRBUGUFUHJMNZWIJPUIJUGUFUJUKAJWGJMUIUCULWJWHJPUIJUMUFUNWAAA
    KEZAVSAVTAKWDVSAPRBUEUFWCVTAPQAUGUFUKWCWKAPQAUMUFUJUOUJUPWJWDVLVNPUIRJBUQUA
    VNVIPVMAPURVMAJBKUBUSUTUNVJVFHVGFGVEGVEDVAVAUKVBUKVCVD $.
    $( [5-May-1996] $)

  $( Ordering on reals satisfies strict trichotomy.  Axiom 22 of 27 for
     real and complex numbers, derived from ZF set theory.  Note:  The more
     general version for extended reals is ~ axlttri . $)
  pre-axlttri $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A <RR B <-> -. ( A = B \/ B <RR A ) ) ) $=
    ( cr cltrr wor wcel wa wbr wceq wo wn wb ltsor pm3.26i sotric mpan ) CDEZAC
    FBCFGABDHABIBADHJKLQCCIMNCABDOP $.
    $( [19-May-1996] $)

  $( Ordering on reals is transitive.  Axiom 23 of 27 for real and
     complex numbers, derived from ZF set theory.  Note:  The more general
     version for extended reals is ~ axlttrn . $)
  pre-axlttrn $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A <RR B /\ B <RR C ) -> A <RR C ) ) $=
    ( cr cltrr wor wcel w3a wbr wa wi wceq ltsor pm3.26i sotr mpan ) DEFZADGBDG
    CDGHABEIBCEIJACEIKQDDLMNDABCEOP $.
    $( [19-May-1996] $)

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
    $( Ordering property of addition on reals.  Axiom 24 of 27 for real
       and complex numbers, derived from ZF set theory.  Note:  The more
       general version for extended reals is ~ axltadd . $)
    pre-axltadd $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( A <RR B -> ( C + A ) <RR ( C + B ) ) ) $=
      ( vx vy vz cr wcel w3a cltrr wbr caddc co cv c0r cop wb cnr elreal wceq
      breq1 opreq2 breq1d bibi12d breq2 breq2d opreq1 breq12d bibi2d wa cltr
      cplr visset ltasr adantr ltresr a1i addresr breqan12d anandis oprex
      syl6bb 3bitr4d ancoms 3impa 3gencl biimpd ) AGHBGHCGHIABJKZCALMZCBLMZJKZD
      NZOPZENZOPZJKZFNZOPZVMLMZVRVOLMZJKZQZAVOJKZVRALMZVTJKZQVHWDVRBLMZJKZQVHVK
      QDEFVMVOVRARGBCDASEBSFCSVMATZVPWCWAWEVMAVOJUAWHVSWDVTJVMAVRLUBUCUDVOBTZWC
      VHWEWGVOBAJUEWIVTWFWDJVOBVRLUBUFUDVRCTZWGVKVHWJWDVIWFVJJVRCALUGVRCBLUGUHU
      IVLRHZVNRHZVQRHZWBWMWKWLUJZWBWMWNUJZVLVNUKKZVQVLULMZVQVNULMZUKKZVPWAWMWPW
      SQWNVLVNVQDUMZEUMZUNUOVPWPQWOVLVNWTXAUPUQWOWAWQOPZWROPZJKZWSWMWKWLWAXDQWM
      WKUJWMWLUJVSXBVTXCJVQVLURVQVNURUSUTWQWRVQVLULVAVQVNULVAUPVBVCVDVEVFVG $.
      $( [11-May-1996] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The product of two positive reals is positive.  Axiom 25 of 27 for
       real and complex numbers, derived from ZF set theory.  Note:  The more
       general version for extended reals is ~ axmulgt0 . $)
    pre-axmulgt0 $p |- ( ( A e. RR /\ B e. RR ) ->
                  ( ( 0 <RR A /\ 0 <RR B ) -> 0 <RR ( A x. B ) ) ) $=
      ( vx vy cc0 cv c0r cop cltrr wbr wa cmul co wi cnr cr elreal wceq breq2
      anbi1d opreq1 breq2d imbi12d anbi2d opreq2 wcel cmr cltr df-0 a1i visset
      mulresr breq12d 0r elisseti oprex ltresr syl6bb mulgt0sr breq1i bitr
      syl2anb syl5bir 2gencl ) ECFZGHZIJZEDFZGHZIJZKZEVFVILMZIJZNEAIJZVJKZEAVIL
      MZIJZNVNEBIJZKZEABLMZIJZNCDVFVIABOPCAQDBQVFARZVKVOVMVQWBVGVNVJVFAEISTWBVL
      VPEIVFAVILUAUBUCVIBRZVOVSVQWAWCVJVRVNVIBEISUDWCVPVTEIVIBALUEUBUCVEOUFVHOU
      FKZVMGVEVHUGMZUHJZVKWDVMGGHZWEGHZIJWFWDEWGVLWHIEWGRWDUIUJVEVHDUKZULUMGWEG
      OUNUOZVEVHUGUPUQURGVEUHJZGVHUHJZWFVGVJVEVHCUKZWIUSVGWGVFIJWKEWGVFIUIUTGVE
      WJWMUQVAVJWGVIIJWLEWGVIIUIUTGVHWJWIUQVAVBVCVD $.
      $( [13-May-1996] $)
  $}

  ${
    $d x y z w A $.
    $( A complex number can be expressed in terms of two reals.  Definition
       10-1.1(v) of [Gleason] p. 130.  Axiom 26 of 27 for real and
       complex numbers, derived from ZF set theory. $)
    axcnre $p |- ( A e. CC -> E. x e. RR E. y e. RR A = ( x + ( y x. i ) ) ) $=
      ( vz vw cv cop ci cmul co caddc wceq cr wrex cnr cc df-c eqeq1 2rexbidv
      wcel wa wex c0r opex eleq1 bi2anan9 opreq1 opreq2d sylan9eq eqeq2d
      anbi12d cla4e2v opelreal anbi12i biimpr cplr c1r cmr cm1r 0r 1r pm3.2i
      mulcnsr mpan2 00sr opreq1d 1idsr ax-mp opreq2i m1r eqtr 0idsr syl6eq
      opreq1i elisseti visset addcomsr syl5eq eqtrd opeq12d df-i adantl
      addcnsr mpanl2 mpanr1 opeq12 syl2an 3eqtrrd sylanc r2ex sylibr optocl )
      DFZEFZGZAFZBFZHIJZKJZLZBMNAMNZCWSLZBMNAMNDECOOPQWOCLWTXBABMMWOCWSRSWMOTZW
      NOTZUAZWPMTZWQMTZUAZWTUAZBUBAUBZXAWMUCGZMTZWNUCGZMTZUAZWOXKXMHIJZKJZLZXJX
      EXIXOXRUAABXKXMWMUCUDWNUCUDWPXKLZWQXMLZUAZXHXOWTXRXSXFXLXTXGXNWPXKMUEWQXM
      MUEUFYAWSXQWOXSXTWSXKWRKJXQWPXKWRKUGXTWRXPXKKWQXMHIUGUHUIUJUKULXOXEXLXCXN
      XDWMUMWNUMUNUOXEXQXKUCWNGZKJZWMUCUPJZUCWNUPJZGZWOXDXQYCLXCXDXPYBXKKXDXMUC
      UQGZIJZYBXPXDYHWNUCURJZUSUCUQURJZURJZUPJZUCUCURJZWNUQURJZUPJZGZYBXDUCOTZY
      HYPLZUTXDYQUAYQUQOTZUAYRYQYSUTVAVBWNUCUCUQVCVDVDXDYLUCYOWNXDYLUCYKUPJZUCX
      DYIUCYKUPWNVEVFYTUCUCUPJZUCYKUCUCUPYKUSUCURJZUCYJUCUSURYQYJUCLUTUCVGVHVIU
      SOTUUBUCLVJUSVEVHVKVIYQUUAUCLUTUCVLVHVKVMXDYOYMWNUPJZWNXDYNWNYMUPWNVGUHXD
      WNUCUPJZWNUUCWNVLZUUCYEUUDYMUCWNUPYQYMUCLUTUCVEVHVNUCWNUCOUTVOEVPVQZVKVRV
      SVTVSHYGXMIWAVIVRUHWBXCYQXDYCYFLZUTXCYQYQXDUAUUGUTWMUCUCWNWCWDWEYDWMLYEWN
      LYFWOLXCXDYDYEWMWNWFWMVLXDUUDWNYEUUEUUFVRWGWHWIWTABMMWJWKWL $.
      $( [13-May-1996] $)
  $}

  ${
    $d x y z w v A $.
    $( A non-empty, bounded-above set of reals has a supremum.  Axiom 27 of 27
       for real and complex numbers, derived from ZF set theory.  Note:  The
       more general version with ordering on extended reals is ~ axsup . $)
    pre-axsup $p |- ( ( A (_ RR /\ A =/= (/) /\ E. x e. RR
                    A. y e. A y <RR x ) -> E. x e. RR ( A. y e. A -. x <RR y /\
                    A. y e. RR ( y <RR x -> E. z e. A y <RR z ) ) ) $=
      ( vw vv cr wss c0 wne cv cltrr wbr wral wrex wn wi wa wceq wcel wal wex
      ssel imim1d pm2.43 syl6 19.20dv df-ral syl6ibr pm3.27 19.22i df-rex
      sylibr imim2i 19.20i a1i anim12d jcab albii 19.26 bitr syl5ib anim2d
      19.22dv adantr c0r cop cab opeq1 eleq1d cbvabv supre syl5 anabsi5 ax-1
      sylbi anim2i sylan2 df-ne anbi2i sylanb 3impa ) DGHZDIJZBKZAKZLMZBDNZAGOZ
      WFWELMPZBDNZWGWECKZLMZCDOZQZBGNZRZAGOZWCDISPZRZWIWRWCWDRWTWFGTZWEGTZWEDTZ
      WGQZQZBUAZRZAUBZWRWIWTXHWRWTXAXBXCWJQZWGWLGTZWLDTWMRZRZCUBZQZRQZBUAZRZAUB
      ZWRWTXHRWCXRWRQWSWCXRXAWQRZAUBWRWCXQXSAWCXPWQXAWCXBXIQZBUAZXBXNQZBUAZRZWQ
      XPWCYAWKYCWPWCYAXIBUAWKWCXTXIBWCXTXCXIQXIWCXCXBXIDGWEUCUDXCWJUEUFUGWJBDUH
      UIYCWPQWCYCXBWOQZBUAWPYBYEBXNWOXBXMWNWGXMXKCUBWNXLXKCXJXKUJUKWMCDULUMUNUN
      UOWOBGUHUMUPUQXPXTYBRZBUAYDXOYFBXBXIXNURUSXTYBBUTVAVBVCVDWQAGULUIVEABCEDF
      KZVFVGZDTZFVHYIEKZVFVGZDTFEYGYJSYHYKDYGYJVFVIVJVKVLVMVNWIXAWHRZAUBXHWHAGU
      LYLXGAWHXFXAWHXDBUAXFWGBDUHXDXEBXDXBVOUOVPVQUKVPVRWDWSWCDIVSVTWAWB $.
      $( [19-May-1996] $)
  $}

