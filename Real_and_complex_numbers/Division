$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Division
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( Define division.  Theorem ~ divmuli relates it to multiplication, and
       ~ divcli and ~ redivcli prove its closure laws. $)
    df-div $a |- / = ( x e. CC , y e. ( CC \ { 0 } ) |->
                   ( iota_ z e. CC ( y x. z ) = x ) ) $.
    $(
    Future simpler alternative?
    df-div $a |- / = { <. <. x , y >. , z >. |
                ( ( y e. ( CC \ { 0 } ) /\ z e. CC ) /\  x = ( y x. z ) ) } $.
    $)
  $}

  ${
    $d x y z A $.  $d x z B $.
    divval.1 $e |- A e. CC $.
    divval.2 $e |- B e. CC $.
    divval.3 $e |- B =/= 0 $.
    $( Value of division: the (unique) element ` x ` such that
       ` ( B x. x ) = A ` .  This is meaningful only when ` B ` is nonzero. $)
    divvali $p |- ( A / B ) = ( iota_ x e. CC ( B x. x ) = A ) $=
      ( vy vz cc wcel cc0 csn cdif cdiv co cv cmul wceq crio riotabidv mpbir2an
      wne eldifsn eqeq2 opreq1 eqeq1d df-div riotaex ovmpt2 mp2an ) BIJCIKLMZJZ
      BCNOCAPZQOZBRZAISZRDULCIJCKUBCIKUCEFUAGHBCIUKHPZUMQOZGPZRZAISUPNURBRZAISU
      SBRUTVAAIUSBURUDTUQCRZVAUOAIVBURUNBUQCUMQUEUFTGHAUGUOAIUHUIUJ $.
      $( [2-Nov-2013] $) $( [8-May-1999] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    divmul.1 $e |- A e. CC $.
    divmul.2 $e |- B e. CC $.
    divmul.3 $e |- C e. CC $.
    divmul.4 $e |- B =/= 0 $.
    $( Relationship between division and multiplication. $)
    divmuli $p |- ( ( A / B ) = C <-> ( B x. C ) = A ) $=
      ( vx vy cdiv co wceq cv cmul cc crio divvali eqeq1i wcel ax-17 receui wal
      wreu wb wi a1i opreq2 eqeq1d riota2f mp2an bitr4i ) ABJKZCLBHMZNKZALZHOPZ
      CLZBCNKZALZULUPCHABDEGQRCOSZUOHOUCUSUQUDFHBAEDGUAUOUSHIOCIMCSHTUSUSHUBUEU
      TUSHTUFUMCLUNURAUMCBNUGUHUIUJUK $.
      $( [24-Nov-2011] $) $( [2-Feb-1995] $)
  $}

  ${
    divmulz.1 $e |- A e. CC $.
    divmulz.2 $e |- B e. CC $.
    divmulz.3 $e |- C e. CC $.
    $( Relationship between division and multiplication. $)
    divmulzi $p |- ( B =/= 0 -> ( ( A / B ) = C <-> ( B x. C ) = A ) ) $=
      ( cc0 wne cdiv co wceq cmul wb c1 cif opreq2 eqeq1d opreq1 bibi12d cc
      ax-1cn keepel elimne0 divmuli dedth ) BGHZABIJZCKZBCLJZAKZMAUFBNOZIJZCKZU
      KCLJZAKZMBNBUKKZUHUMUJUOUPUGULCBUKAIPQUPUIUNABUKCLRQSAUKCDUFBNTEUAUBFBUCU
      DUE $.
      $( [8-May-1999] $)
  $}

  $( Relationship between division and multiplication. $)
  divmul $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
               -> ( ( A / C ) = B <-> ( C x. B ) = A ) ) $=
    ( cc wcel cc0 wne cdiv co wceq wb wi cif opreq1 eqeq1d eqeq2 bibi12d elimel
    cmul 0cn imbi2d neeq1 opreq2 imbi12d divmulzi dedth3h 3exp com23 imp4a 3imp
    wa ) ADEZBDEZCDEZCFGZUKACHIZBJZCBSIZAJZKZULUMUNUOUTULUNUMUOUTLZULUNUMVAULUN
    UMVAUOULAFMZCHIZBJZURVBJZKZLUNCFMZFGZVBVGHIZBJZVGBSIZVBJZKZLVHVIUMBFMZJZVGV
    NSIZVBJZKZLACBFFFAVBJZUTVFUOVSUQVDUSVEVSUPVCBAVBCHNOAVBURPQUACVGJZUOVHVFVMC
    VGFUBVTVDVJVEVLVTVCVIBCVGVBHUCOVTURVKVBCVGBSNOQUDBVNJZVMVRVHWAVJVOVLVQBVNVI
    PWAVKVPVBBVNVGSUCOQUAVBVGVNAFDTRCFDTRBFDTRUEUFUGUHUIUJ $.
    $( [15-Nov-2008] $) $( [2-Aug-2004] $)

  $( Relationship between division and multiplication. $)
  divmul2 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
              -> ( ( A / C ) = B <-> A = ( C x. B ) ) ) $=
    ( cc wcel cc0 wne wa w3a cdiv co wceq cmul divmul eqcom syl6bb ) ADEBDECDEC
    FGHIACJKBLCBMKZALAQLABCNQAOP $.
    $( [16-Nov-2008] $) $( [7-Feb-2006] $)

  $( Relationship between division and multiplication. $)
  divmul3 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
              -> ( ( A / C ) = B <-> A = ( B x. C ) ) ) $=
    ( cc wcel cc0 wne wa w3a cdiv co wceq divmul2 mulcom adantrr 3adant1 eqeq2d
    cmul bitr4d ) ADEZBDEZCDEZCFGZHZIZACJKBLACBRKZLABCRKZLABCMUEUGUFAUAUDUGUFLZ
    TUAUBUHUCBCNOPQS $.
    $( [27-Nov-2008] $) $( [13-Feb-2006] $)

  ${
    $d x A $.  $d x B $.
    divcl.1 $e |- A e. CC $.
    divcl.2 $e |- B e. CC $.
    divcl.3 $e |- B =/= 0 $.
    $( Closure law for division. $)
    divcli $p |- ( A / B ) e. CC $=
      ( vx cdiv co cv cmul wceq cc crio divvali wreu wcel ax-cnex riotaclb mpbi
      receui eqeltri ) ABGHBFIJHAKZFLMZLFABCDENUBFLOUCLPFBADCETUBFLQRSUA $.
      $( [24-Nov-2011] $) $( [2-Feb-1995] $)
  $}

  ${
    divclz.1 $e |- A e. CC $.
    divclz.2 $e |- B e. CC $.
    $( Closure law for division. $)
    divclzi $p |- ( B =/= 0 -> ( A / B ) e. CC ) $=
      ( cc0 wne cdiv co cc wcel c1 cif wceq opreq2 eleq1d ax-1cn keepel elimne0
      divcli dedth ) BEFZABGHZIJAUABKLZGHZIJBKBUCMUBUDIBUCAGNOAUCCUABKIDPQBRST
      $.
      $( [7-May-1999] $)
  $}

  $( Closure law for division. $)
  divcl $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) -> ( A / B ) e. CC ) $=
    ( cc wcel cc0 wne cdiv co wi wceq opreq1 eleq1d imbi2d neeq1 opreq2 imbi12d
    cif 0cn elimel divclzi dedth2h 3impia ) ACDZBCDZBEFZABGHZCDZUCUDUEUGIUEUCAE
    QZBGHZCDZIUDBEQZEFZUHUKGHZCDZIABEEAUHJZUGUJUEUOUFUICAUHBGKLMBUKJZUEULUJUNBU
    KENUPUIUMCBUKUHGOLPUHUKAECRSBECRSTUAUB $.
    $( [4-May-2005] $) $( [21-Jul-2001] $)

  ${
    reccl.1 $e |- A e. CC $.
    reccl.2 $e |- A =/= 0 $.
    $( Closure law for reciprocal. $)
    reccli $p |- ( 1 / A ) e. CC $=
      ( c1 ax-1cn divcli ) DAEBCF $.
      $( [30-Apr-2005] $)
  $}

  ${
    recclz.1 $e |- A e. CC $.
    $( Closure law for reciprocal. $)
    recclzi $p |- ( A =/= 0 -> ( 1 / A ) e. CC ) $=
      ( c1 ax-1cn divclzi ) CADBE $.
      $( [30-Apr-2005] $)
  $}

  $( Closure law for reciprocal. $)
  reccl $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 1 / A ) e. CC ) $=
    ( c1 cc wcel cc0 wne cdiv co ax-1cn divcl mp3an1 ) BCDACDAEFBAGHCDIBAJK $.
    $( [30-Apr-2005] $)

  ${
    divcan.1 $e |- A e. CC $.
    divcan.2 $e |- B e. CC $.
    ${
      divcan.3 $e |- B =/= 0 $.
      $( A cancellation law for division. $)
      divcan2i $p |- ( B x. ( A / B ) ) = A $=
        ( cdiv co wceq cmul eqid divcli divmuli mpbi ) ABFGZNHBNIGAHNJABNCDABCD
        EKELM $.
        $( [18-Aug-2008] $) $( [9-Feb-1995] $)

      $( A cancellation law for division. $)
      divcan1i $p |- ( ( A / B ) x. B ) = A $=
        ( cdiv co cmul divcli mulcomi divcan2i eqtri ) ABFGZBHGBMHGAMBABCDEIDJA
        BCDEKL $.
        $( [18-Aug-2008] $) $( [18-May-1999] $)
    $}

    $( A cancellation law for division. $)
    divcan1zi $p |- ( B =/= 0 -> ( ( A / B ) x. B ) = A ) $=
      ( cc0 wne cdiv co cmul wceq c1 opreq2 id opreq12d eqeq1d cc ax-1cn keepel
      cif elimne0 divcan1i dedth ) BEFZABGHZBIHZAJAUCBKSZGHZUFIHZAJBKBUFJZUEUHA
      UIUDUGBUFIBUFAGLUIMNOAUFCUCBKPDQRBTUAUB $.
      $( [18-Aug-2008] $) $( [2-Oct-1999] $)

    $( A cancellation law for division.  We eliminate the third hypothesis of
       ~ divcan2i using the weak deduction theorem ~ dedth and keep the other
       two.  Because the first hypothesis shares the class variable ` B ` with
       the hypothesis we're eliminating, we need to use ~ keepel in order to
       keep the first hypothesis. $)
    divcan2zi $p |- ( B =/= 0 -> ( B x. ( A / B ) ) = A ) $=
      ( cc0 wne cdiv co cmul wceq c1 id opreq2 opreq12d eqeq1d cc ax-1cn keepel
      cif elimne0 divcan2i dedth ) BEFZBABGHZIHZAJUCBKSZAUFGHZIHZAJBKBUFJZUEUHA
      UIBUFUDUGIUILBUFAGMNOAUFCUCBKPDQRBTUAUB $.
      $( [21-Aug-2008] $) $( [10-Aug-1999] $)
  $}

  $( A cancellation law for division. $)
  divcan1 $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 )
        -> ( ( A / B ) x. B ) = A ) $=
    ( cc wcel cc0 wne cdiv co cmul wceq wi cif opreq1 opreq1d id eqeq12d imbi2d
    neeq1 0cn elimel opreq2 opreq12d eqeq1d imbi12d divcan1zi dedth2h 3impia )
    ACDZBCDZBEFZABGHZBIHZAJZUHUIUJUMKUJUHAELZBGHZBIHZUNJZKUIBELZEFZUNURGHZURIHZ
    UNJZKABEEAUNJZUMUQUJVCULUPAUNVCUKUOBIAUNBGMNVCOPQBURJZUJUSUQVBBURERVDUPVAUN
    VDUOUTBURIBURUNGUAVDOUBUCUDUNURAECSTBECSTUEUFUG $.
    $( [18-Aug-2008] $) $( [5-Jun-2004] $)

  $( A cancellation law for division. $)
  divcan2 $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                 ( B x. ( A / B ) ) = A ) $=
    ( cc wcel cc0 wne w3a cdiv co cmul wceq simp2 divcl mulcom syl11anc divcan1
    eqtrd ) ACDZBCDZBEFZGZBABHIZJIZUBBJIZAUASUBCDUCUDKRSTLABMBUBNOABPQ $.
    $( [21-Aug-2008] $) $( [3-Feb-2004] $)

  $( The ratio of nonzero numbers is nonzero. $)
  divne0b $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                  ( A =/= 0 <-> ( A / B ) =/= 0 ) ) $=
    ( cc wcel cc0 wne cdiv co wceq cmul wb mul01 eqeq1d eqcom syl6rbbr 3ad2ant2
    w3a wa 0cn divmul mp3an2 3impb bitr4d necon3bid ) ACDZBCDZBEFZQZAEABGHZEUHA
    EIZBEJHZAIZUIEIZUFUEUJULKUGUFULEAIUJUFUKEABLMAENOPUEUFUGUMULKZUEECDUFUGRUNS
    AEBTUAUBUCUD $.
    $( [1-Jun-2005] $) $( [2-Aug-2004] $)

  $( The ratio of nonzero numbers is nonzero. $)
  divne0 $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) ) ->
                  ( A / B ) =/= 0 ) $=
    ( cc wcel cc0 wne cdiv co wi w3a divne0b biimpd 3exp com23 com24 imp43 ) AC
    DZAEFZBCDZBEFZABGHEFZQTSRUAQSTRUAIZQSTUBQSTJRUAABKLMNOP $.
    $( [28-Dec-2007] $)

  ${
    divneq0.1 $e |- A e. CC $.
    divneq0.2 $e |- B e. CC $.
    divneq0.3 $e |- A =/= 0 $.
    divneq0.4 $e |- B =/= 0 $.
    $( The ratio of nonzero numbers is nonzero. $)
    divne0i $p |- ( A / B ) =/= 0 $=
      ( cc wcel cc0 wne cdiv co w3a divne0b mpbii mp3an ) AGHZBGHZBIJZABKLIJZCD
      FQRSMAIJTEABNOP $.
      $( [9-Feb-1995] $)
  $}

  ${
    recneq0z.1 $e |- A e. CC $.
    $( The reciprocal of a nonzero number is nonzero. $)
    recne0zi $p |- ( A =/= 0 -> ( 1 / A ) =/= 0 ) $=
      ( cc0 wne c1 cdiv co cif wceq opreq2 neeq1d ax-1cn keepel ax-1ne0 elimne0
      cc divne0i dedth ) ACDZEAFGZCDESAEHZFGZCDAEAUAITUBCAUAEFJKEUALSAEPBLMNAOQ
      R $.
      $( [14-May-1999] $)
  $}

  $( The reciprocal of a nonzero number is nonzero. $)
  recne0 $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 1 / A ) =/= 0 ) $=
    ( cc wcel cc0 wne c1 cdiv co wi cif wceq neeq1 opreq2 neeq1d imbi12d elimel
    0cn recne0zi dedth imp ) ABCZADEZFAGHZDEZUAUBUDIUAADJZDEZFUEGHZDEZIADAUEKZU
    BUFUDUHAUEDLUIUCUGDAUEFGMNOUEADBQPRST $.
    $( [9-Feb-2006] $)

  ${
    recid.1 $e |- A e. CC $.
    recid.2 $e |- A =/= 0 $.
    $( Multiplication of a number and its reciprocal. $)
    recidi $p |- ( A x. ( 1 / A ) ) = 1 $=
      ( c1 ax-1cn divcan2i ) DAEBCF $.
      $( [9-Feb-1995] $)
  $}

  ${
    recidz.1 $e |- A e. CC $.
    $( Multiplication of a number and its reciprocal. $)
    recidzi $p |- ( A =/= 0 -> ( A x. ( 1 / A ) ) = 1 ) $=
      ( c1 ax-1cn divcan2zi ) CADBE $.
      $( [14-May-1999] $)
  $}

  $( Multiplication of a number and its reciprocal. $)
  recid $p |- ( ( A e. CC /\ A =/= 0 ) -> ( A x. ( 1 / A ) ) = 1 ) $=
    ( cc wcel cc0 wne c1 cdiv co cmul wceq wi cif neeq1 opreq2 opreq12d imbi12d
    id eqeq1d 0cn elimel recidzi dedth imp ) ABCZADEZAFAGHZIHZFJZUDUEUHKUDADLZD
    EZUIFUIGHZIHZFJZKADAUIJZUEUJUHUMAUIDMUNUGULFUNAUIUFUKIUNQAUIFGNORPUIADBSTUA
    UBUC $.
    $( [25-Oct-1999] $)

  $( Multiplication of a number and its reciprocal. $)
  recid2 $p |- ( ( A e. CC /\ A =/= 0 ) -> ( ( 1 / A ) x. A ) = 1 ) $=
    ( cc wcel cc0 wne wa c1 cdiv co cmul wceq reccl simpl mulcom syl11anc recid
    eqtrd ) ABCZADEZFZGAHIZAJIZAUAJIZGTUABCRUBUCKALRSMUAANOAPQ $.
    $( [22-Jun-2006] $)

  ${
    divrec.1 $e |- A e. CC $.
    divrec.2 $e |- B e. CC $.
    ${
      divrec.3 $e |- B =/= 0 $.
      $( Relationship between division and reciprocal.  Theorem I.9 of
         [Apostol] p. 18. $)
      divreci $p |- ( A / B ) = ( A x. ( 1 / B ) ) $=
        ( cdiv co c1 cmul reccli mulcli mulcomi mulassi ax-1cn divcan1i opreq2i
        wceq mulid1i eqtri 3eqtri divmuli mpbir ) ABFGAHBFGZIGZQBUDIGZAQUEUDBIG
        AUCBIGZIGZABUDDAUCCBDEJZKZLAUCBCUHDMUGAHIGAUFHAIHBNDEOPACRSTABUDCDUIEUA
        UB $.
        $( [9-Feb-1995] $)
    $}

    $( Relationship between division and reciprocal.  Theorem I.9 of [Apostol]
       p. 18. $)
    divreczi $p |- ( B =/= 0 -> ( A / B ) = ( A x. ( 1 / B ) ) ) $=
      ( cc0 wne cdiv cmul wceq cif opreq2 opreq2d eqeq12d ax-1cn keepel elimne0
      co c1 cc divreci dedth ) BEFZABGQZARBGQZHQZIAUBBRJZGQZARUFGQZHQZIBRBUFIZU
      CUGUEUIBUFAGKUJUDUHAHBUFRGKLMAUFCUBBRSDNOBPTUA $.
      $( [11-Oct-1999] $)
  $}

  $( Relationship between division and reciprocal.  Theorem I.9 of [Apostol]
     p. 18. $)
  divrec $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                ( A / B ) = ( A x. ( 1 / B ) ) ) $=
    ( cc wcel cc0 wne cdiv co c1 cmul wi cif opreq1 eqeq12d imbi2d neeq1 opreq2
    wceq 0cn elimel opreq2d imbi12d divreczi dedth2h 3impia ) ACDZBCDZBEFZABGHZ
    AIBGHZJHZRZUFUGUHULKUHUFAELZBGHZUMUJJHZRZKUGBELZEFZUMUQGHZUMIUQGHZJHZRZKABE
    EAUMRZULUPUHVCUIUNUKUOAUMBGMAUMUJJMNOBUQRZUHURUPVBBUQEPVDUNUSUOVABUQUMGQVDU
    JUTUMJBUQIGQUANUBUMUQAECSTBECSTUCUDUE $.
    $( [4-May-2005] $) $( [2-Aug-2004] $)

  $( Relationship between division and reciprocal. $)
  divrec2 $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                ( A / B ) = ( ( 1 / B ) x. A ) ) $=
    ( cc wcel cc0 wne w3a cdiv co cmul divrec wceq simp1 reccl 3adant1 syl11anc
    c1 mulcom eqtrd ) ACDZBCDZBEFZGZABHIAQBHIZJIZUDAJIZABKUCTUDCDZUEUFLTUAUBMUA
    UBUGTBNOAUDRPS $.
    $( [7-Feb-2006] $)

  $( An associative law for division. $)
  divass $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
                ( ( A x. B ) / C ) = ( A x. ( B / C ) ) ) $=
    ( cc wcel cc0 wne wa cmul co c1 cdiv wceq reccl mulass syl3an3 mulcl divrec
    w3a syl111anc 3adant3 simp3l simp3r simp2 opreq2d 3eqtr4d ) ADEZBDEZCDEZCFG
    ZHZSZABIJZKCLJZIJZABUNIJZIJZUMCLJZABCLJZIJUKUGUHUNDEUOUQMCNABUNOPULUMDEZUIU
    JURUOMUGUHUTUKABQUAUGUHUIUJUBZUGUHUIUJUCZUMCRTULUSUPAIULUHUIUJUSUPMUGUHUKUD
    VAVBBCRTUEUF $.
    $( [12-Oct-2008] $) $( [2-Aug-2004] $)

  $( A commutative/associative law for division. $)
  div23 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
            -> ( ( A x. B ) / C ) = ( ( A / C ) x. B ) ) $=
    ( cc wcel cc0 wne wa w3a cmul co cdiv wceq ax-mulcom opreq1d 3adant3 divass
    3com12 simp2 divcl 3expb 3adant2 mulcom syl11anc 3eqtrd ) ADEZBDEZCDEZCFGZH
    ZIZABJKZCLKZBAJKZCLKZBACLKZJKZUPBJKZUFUGUMUOMUJUFUGHULUNCLABNOPUGUFUJUOUQMB
    ACQRUKUGUPDEZUQURMUFUGUJSUFUJUSUGUFUHUIUSACTUAUBBUPUCUDUE $.
    $( [27-Dec-2008] $) $( [2-Aug-2004] $)

  $( A commutative/associative law for division. $)
  div13 $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) /\ C e. CC )
            -> ( ( A / B ) x. C ) = ( ( C / B ) x. A ) ) $=
    ( cc wcel cc0 wne wa w3a cmul co cdiv wceq ax-mulcom opreq1d 3adant2 3com23
    div23 3coml 3eqtr3d ) ADEZBDEBFGHZCDEZIACJKZBLKZCAJKZBLKZABLKCJKZCBLKAJKZUA
    UCUEUGMUBUAUCHUDUFBLACNOPUAUCUBUEUHMACBRQUCUAUBUGUIMCABRST $.
    $( [22-Apr-2005] $)

  $( A commutative/associative law for division. $)
  div12 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
            -> ( A x. ( B / C ) ) = ( B x. ( A / C ) ) ) $=
    ( cc wcel cc0 wne wa w3a cdiv co cmul divcl 3expb mulcom sylan2 3impb div13
    wceq 3comr sylan 3impa 3com23 3eqtrd ) ADEZBDEZCDEZCFGZHZIABCJKZLKZUJALKZAC
    JKZBLKZBUMLKZUEUFUIUKULSZUFUIHUEUJDEZUPUFUGUHUQBCMNAUJOPQUFUIUEULUNSBCARTUE
    UIUFUNUOSZUEUIUFURUEUIHUMDEZUFURUEUGUHUSACMNUMBOUAUBUCUD $.
    $( [11-Feb-2009] $) $( [30-Apr-2005] $)

  ${
    divass.1 $e |- A e. CC $.
    divass.2 $e |- B e. CC $.
    divass.3 $e |- C e. CC $.
    $( An associative law for division. $)
    divasszi $p |- ( C =/= 0 -> ( ( A x. B ) / C ) = ( A x. ( B / C ) ) ) $=
      ( cc wcel cc0 wne cmul co cdiv wceq wa divass mp3an12 mpan ) CGHZCIJZABKL
      CMLABCMLKLNZFAGHBGHSTOUADEABCPQR $.
      $( [12-Aug-1999] $)

    ${
      divass.4 $e |- C =/= 0 $.
      $( An associative law for division. $)
      divassi $p |- ( ( A x. B ) / C ) = ( A x. ( B / C ) ) $=
        ( cc0 wne cmul co cdiv wceq divasszi ax-mp ) CHIABJKCLKABCLKJKMGABCDEFN
        O $.
        $( [15-Feb-1995] $)

      $( Distribution of division over addition. $)
      divdiri $p |- ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) $=
        ( caddc co c1 cdiv cmul reccli adddiri addcli divreci opreq12i 3eqtr4i
        ) ABHIZJCKIZLIATLIZBTLIZHISCKIACKIZBCKIZHIABTDECFGMNSCABDEOFGPUCUAUDUBH
        ACDFGPBCEFGPQR $.
        $( [16-Feb-1995] $)

      $( A commutative/associative law for division. $)
      div23i $p |- ( ( A x. B ) / C ) = ( ( A / C ) x. B ) $=
        ( cmul co cdiv mulcomi opreq1i divassi divcli 3eqtri ) ABHIZCJIBAHIZCJI
        BACJIZHIRBHIPQCJABDEKLBACEDFGMBREACDFGNKO $.
        $( [3-Sep-1999] $)
    $}

    $( Distribution of division over addition. $)
    divdirzi $p |- ( C =/= 0 ->
                    ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) ) $=
      ( cc0 wne caddc co cdiv wceq c1 cif opreq2 opreq12d eqeq12d ax-1cn keepel
      cc elimne0 divdiri dedth ) CGHZABIJZCKJZACKJZBCKJZIJZLUEUDCMNZKJZAUJKJZBU
      JKJZIJZLCMCUJLZUFUKUIUNCUJUEKOUOUGULUHUMICUJAKOCUJBKOPQABUJDEUDCMTFRSCUAU
      BUC $.
      $( [31-Jul-2004] $)
  $}

  $( Distribution of division over addition. $)
  divdir $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
               -> ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) ) $=
    ( cc wcel cc0 wne caddc co cdiv wi cif opreq1 opreq1d eqeq12d imbi2d opreq2
    wceq 0cn elimel opreq2d neeq1 opreq12d imbi12d divdirzi dedth3h 3exp imp4a
    wa 3imp ) ADEZBDEZCDEZCFGZUIABHIZCJIZACJIZBCJIZHIZRZUKULUMUNUTUKULUMUNUTKZU
    KULUMVAUNUKAFLZBHIZCJIZVBCJIZURHIZRZKUNVBULBFLZHIZCJIZVEVHCJIZHIZRZKUMCFLZF
    GZVIVNJIZVBVNJIZVHVNJIZHIZRZKABCFFFAVBRZUTVGUNWAUPVDUSVFWAUOVCCJAVBBHMNWAUQ
    VEURHAVBCJMNOPBVHRZVGVMUNWBVDVJVFVLWBVCVICJBVHVBHQNWBURVKVEHBVHCJMUAOPCVNRZ
    UNVOVMVTCVNFUBWCVJVPVLVSCVNVIJQWCVEVQVKVRHCVNVBJQCVNVHJQUCOUDVBVHVNAFDSTBFD
    STCFDSTUEUFUGUHUJ $.
    $( [20-Aug-2008] $) $( [31-Jul-2004] $)

  ${
    divcan3.1 $e |- A e. CC $.
    divcan3.2 $e |- B e. CC $.
    ${
      divcan3.3 $e |- B =/= 0 $.
      $( A cancellation law for division. $)
      divcan3i $p |- ( ( B x. A ) / B ) = A $=
        ( cmul co cdiv divassi divcan2i eqtri ) BAFGBHGBABHGFGABABDCDEIABCDEJK
        $.
        $( [22-Aug-2008] $) $( [16-Feb-1995] $)

      $( A cancellation law for division. $)
      divcan4i $p |- ( ( A x. B ) / B ) = A $=
        ( cmul co cdiv mulcomi opreq1i divcan3i eqtri ) ABFGZBHGBAFGZBHGAMNBHAB
        CDIJABCDEKL $.
        $( [22-Aug-2008] $) $( [18-May-1999] $)
    $}

    $( A cancellation law for division.  (Eliminates a hypothesis of ~ divcan3i
       with the weak deduction theorem.) $)
    divcan3zi $p |- ( B =/= 0 -> ( ( B x. A ) / B ) = A ) $=
      ( cc0 wne cmul co cdiv wceq c1 opreq1 id opreq12d eqeq1d cc ax-1cn keepel
      cif elimne0 divcan3i dedth ) BEFZBAGHZBIHZAJUCBKSZAGHZUFIHZAJBKBUFJZUEUHA
      UIUDUGBUFIBUFAGLUIMNOAUFCUCBKPDQRBTUAUB $.
      $( [23-Aug-2008] $) $( [3-Feb-2004] $)

    $( A cancellation law for division. $)
    divcan4zi $p |- ( B =/= 0 -> ( ( A x. B ) / B ) = A ) $=
      ( cc0 wne cmul co cdiv mulcomi opreq1i divcan3zi syl5eq ) BEFABGHZBIHBAGH
      ZBIHANOBIABCDJKABCDLM $.
      $( [23-Aug-2008] $) $( [12-Oct-1999] $)

  $}

  $( A cancellation law for division. $)
  divcan3 $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                 ( ( B x. A ) / B ) = A ) $=
    ( cc wcel cc0 wne cmul co cdiv wceq wi cif opreq2 opreq1d id eqeq12d imbi2d
    neeq1 0cn elimel opreq1 opreq12d eqeq1d imbi12d divcan3zi dedth2h 3impia )
    ACDZBCDZBEFZBAGHZBIHZAJZUHUIUJUMKUJBUHAELZGHZBIHZUNJZKUIBELZEFZURUNGHZURIHZ
    UNJZKABEEAUNJZUMUQUJVCULUPAUNVCUKUOBIAUNBGMNVCOPQBURJZUJUSUQVBBURERVDUPVAUN
    VDUOUTBURIBURUNGUAVDOUBUCUDUNURAECSTBECSTUEUFUG $.
    $( [23-Aug-2008] $) $( [3-Feb-2004] $)

  $( A cancellation law for division. $)
  divcan4 $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                 ( ( A x. B ) / B ) = A ) $=
    ( cc wcel cc0 wne w3a cmul cdiv wceq mulcom 3adant3 opreq1d divcan3 eqtrd
    co ) ACDZBCDZBEFZGZABHPZBIPBAHPZBIPATUAUBBIQRUAUBJSABKLMABNO $.
    $( [23-Aug-2008] $) $( [8-Feb-2005] $)

  ${
    div11.1 $e |- A e. CC $.
    div11.2 $e |- B e. CC $.
    div11.3 $e |- C e. CC $.
    div11.4 $e |- C =/= 0 $.
    $( One-to-one relationship for division. $)
    div11i $p |- ( ( A / C ) = ( B / C ) <-> A = B ) $=
      ( cdiv co wceq cmul divcli mulcani divcan2i eqeq12i bitr3i ) ACHIZBCHIZJC
      QKIZCRKIZJABJQRCACDFGLBCEFGLFGMSATBACDFGNBCEFGNOP $.
      $( [20-Aug-2001] $)
  $}

  $( One-to-one relationship for division. $)
  div11 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
               ( ( A / C ) = ( B / C ) <-> A = B ) ) $=
    ( cc wcel cc0 wne wa cdiv co wceq wb c1 opreq1 bibi12d opreq2 ax-1cn elimel
    cif eleq1 eqeq1d eqeq1 eqeq2d eqeq12d bibi1d anbi12d ax-1ne0 pm3.2i elimhyp
    eqeq2 neeq1 simpli simpri div11i dedth3h ) ADEZBDEZCDEZCFGZHZACIJZBCIJZKZAB
    KZLUPAMSZCIJZVBKZVEBKZLVFUQBMSZCIJZKZVEVIKZLVEUTCMSZIJZVIVMIJZKZVLLABCMMMAV
    EKZVCVGVDVHVQVAVFVBAVECINUAAVEBUBOBVIKZVGVKVHVLVRVBVJVFBVICINUCBVIVEUJOCVMK
    ZVKVPVLVSVFVNVJVOCVMVEIPCVMVIIPUDUEVEVIVMAMDQRBMDQRVMDEZVMFGZUTVTWAHMDEZMFG
    ZHCMVSURVTUSWACVMDTCVMFUKUFMVMKWBVTWCWAMVMDTMVMFUKUFWBWCQUGUHUIZULVTWAWDUMU
    NUO $.
    $( [20-Apr-2006] $)

  $( A number divided by itself is one. $)
  divid $p |- ( ( A e. CC /\ A =/= 0 ) -> ( A / A ) = 1 ) $=
    ( cc wcel cc0 wne wa cdiv co c1 cmul wceq divrec 3anidm12 recid eqtrd ) ABC
    ZADEZFAAGHZAIAGHJHZIPQRSKAALMANO $.
    $( [1-Aug-2004] $)

  $( Division into zero is zero. $)
  div0 $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 0 / A ) = 0 ) $=
    ( cc wcel cc0 wne wa cdiv co c1 cmul wceq 0cn divrec mp3an1 reccl mul02 syl
    eqtrd ) ABCZADEZFZDAGHZDIAGHZJHZDDBCSTUBUDKLDAMNUAUCBCUDDKAOUCPQR $.
    $( [14-Mar-2005] $)

  $( A ratio is zero iff the numerator is zero. $)
  diveq0 $p |- ( ( A e. CC /\ C e. CC /\ C =/= 0 ) ->
               ( ( A / C ) = 0 <-> A = 0 ) ) $=
    ( cc wcel cc0 wne cdiv co wceq wb wa div0 eqeq2d 3adant1 div11 mp3an2 3impb
    w3a 0cn bitr3d ) ACDZBCDZBEFZRABGHZEBGHZIZUDEIZAEIZUBUCUFUGJUAUBUCKZUEEUDBL
    MNUAUBUCUFUHJZUAECDUIUJSAEBOPQT $.
    $( [20-Apr-2006] $)

  ${
    recrec.1 $e |- A e. CC $.
    recrec.2 $e |- A =/= 0 $.
    $( A number is equal to the reciprocal of its reciprocal.  Theorem I.10 of
       [Apostol] p. 18. $)
    recreci $p |- ( 1 / ( 1 / A ) ) = A $=
      ( c1 cdiv co reccli ax-1cn ax-1ne0 divne0i recidi opreq2i opreq1i mulassi
      cmul mulid2i 3eqtr3i mulid1i ) ADAEFZDSEFZOFZOFZADOFTAUADAOSABCGZDAHBICJZ
      KLASOFZTOFDTOFUBTUEDTOABCKMASTBUCSUCUDGZNTUFPQABRQ $.
      $( [9-Feb-1995] $)

    $( A number divided by itself is one. $)
    dividi $p |- ( A / A ) = 1 $=
      ( cdiv co c1 cmul divreci recidi eqtri ) AADEAFADEGEFAABBCHABCIJ $.
      $( [9-Feb-1995] $)

    $( Division into zero is zero. $)
    div0i $p |- ( 0 / A ) = 0 $=
      ( cc wcel cc0 wne cdiv co wceq div0 mp2an ) ADEAFGFAHIFJBCAKL $.
      $( [12-Aug-1999] $)
  $}

  ${
    div1.1 $e |- A e. CC $.
    $( A number divided by 1 is itself. $)
    div1i $p |- ( A / 1 ) = A $=
      ( c1 cdiv co wceq cmul mulid2i ax-1cn ax-1ne0 divmuli mpbir ) ACDEAFCAGEA
      FABHACABIBJKL $.
      $( [9-Jan-2002] $)
  $}

  $( A number divided by 1 is itself. $)
  div1 $p |- ( A e. CC -> ( A / 1 ) = A ) $=
    ( cc wcel c1 cdiv co wceq cif opreq1 id eqeq12d ax-1cn elimel div1i dedth )
    ABCZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRADBLMNO $.
    $( [9-Jan-2002] $)

  $( Move negative sign inside of a division. $)
  divneg $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                -u ( A / B ) = ( -u A / B ) ) $=
    ( cc wcel cc0 wne w3a cneg c1 cdiv co cmul wceq wa reccl sylan2 3impb negcl
    mulneg1 divrec syl3an1 negeqd 3eqtr4rd ) ACDZBCDZBEFZGZAHZIBJKZLKZAUILKZHZU
    HBJKZABJKZHUDUEUFUJULMZUEUFNUDUICDUOBOAUISPQUDUHCDUEUFUMUJMARUHBTUAUGUNUKAB
    TUBUC $.
    $( [3-Jun-2005] $) $( [17-Sep-2004] $)

  $( Distribution of division over subtraction. $)
  divsubdir $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) )
               -> ( ( A - B ) / C ) = ( ( A / C ) - ( B / C ) ) ) $=
    ( cc wcel cc0 wne wa w3a cneg caddc co cdiv cmin negcl negsub 3expb 3adant1
    wceq divcl divdir syl3an2 opreq1d 3adant3 opreq2d 3adant2 syl11anc 3eqtr3d
    divneg eqtr3d ) ADEZBDEZCDEZCFGZHZIZABJZKLZCMLZACMLZUQCMLZKLZABNLZCMLZUTBCM
    LZNLZULUKUQDEUOUSVBSBOAUQCUAUBUKULUSVDSUOUKULHURVCCMABPUCUDUPUTVEJZKLZVBVFU
    PVGVAUTKULUOVGVASZUKULUMUNVIBCUIQRUEUPUTDEZVEDEZVHVFSUKUOVJULUKUMUNVJACTQUF
    ULUOVKUKULUMUNVKBCTQRUTVEPUGUJUH $.
    $( [6-Sep-2008] $) $( [4-Mar-2005] $)

  $( A number is equal to the reciprocal of its reciprocal.  Theorem I.10 of
     [Apostol] p. 18. $)
  recrec $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 1 / ( 1 / A ) ) = A ) $=
    ( cc wcel cc0 wne wa c1 cdiv co wceq cif opreq2 opreq2d eqeq12d eleq1 neeq1
    id anbi12d ax-1cn ax-1ne0 pm3.2i elimhyp simpli simpri recreci dedth ) ABCZ
    ADEZFZGGAHIZHIZAJGGUIAGKZHIZHIZULJAGAULJZUKUNAULUOUJUMGHAULGHLMUOQNULULBCZU
    LDEZUIUPUQFGBCZGDEZFAGUOUGUPUHUQAULBOAULDPRGULJURUPUSUQGULBOGULDPRURUSSTUAU
    BZUCUPUQUTUDUEUF $.
    $( [26-Sep-1999] $)

  ${
    rec11.1 $e |- A e. CC $.
    rec11.2 $e |- B e. CC $.
    ${
      rec11i.3 $e |- A =/= 0 $.
      rec11i.4 $e |- B =/= 0 $.
      $( Reciprocal is one-to-one. $)
      rec11ii $p |- ( ( 1 / A ) = ( 1 / B ) <-> A = B ) $=
        ( c1 cdiv wceq cmul opreq2 reccli mul32i recidi opreq1i mulid2i mulassi
        co 3eqtri opreq2i mulid1i eqeq12i eqcom bitri sylib impbii ) GAHRZGBHRZ
        IZABIZUIABJRZUGJRZUKUHJRZIZUJUGUHUKJKUNBAIUJULBUMAULAUGJRZBJRGBJRBABUGC
        DACELMUOGBJACENOBDPSUMABUHJRZJRAGJRAABUHCDBDFLQUPGAJBDFNTACUASUBBAUCUDU
        EABGHKUF $.
        $( [16-Sep-1999] $)
    $}

    $( Reciprocal is one-to-one. $)
    rec11i $p |- ( ( A =/= 0 /\ B =/= 0 ) ->
               ( ( 1 / A ) = ( 1 / B ) <-> A = B ) ) $=
      ( cc0 wne c1 cdiv co wceq wb opreq2 eqeq1d eqeq1 bibi12d cc ax-1cn keepel
      cif elimne0 eqeq2d eqeq2 rec11ii dedth2h ) AEFZBEFZGAHIZGBHIZJZABJZKGUEAG
      SZHIZUHJZUKBJZKULGUFBGSZHIZJZUKUOJZKABGGAUKJZUIUMUJUNUSUGULUHAUKGHLMAUKBN
      OBUOJZUMUQUNURUTUHUPULBUOGHLUABUOUKUBOUKUOUEAGPCQRUFBGPDQRATBTUCUD $.
      $( [16-Sep-1999] $)
  $}

  $( Mutual reciprocals.  (Contributed by Paul Chapman, 18-Oct-2007.) $)
  rec11r $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) )
                  -> ( ( 1 / A ) = B <-> ( 1 / B ) = A ) ) $=
    ( cc wcel cc0 wne wa c1 cdiv co wceq wb ax-1cn divmul mp3an1 ancoms adantrr
    cmul mulcom bitr4d eqeq1d adantlr ) ACDZAEFZGZBCDZBEFZGZGHAIJBKZABRJZHKZHBI
    JAKZUEUFUIUKLZUGUFUEUMHCDZUFUEUMMHBANOPQUCUHULUKLUDUCUHGZULBARJZHKZUKUNUCUH
    ULUQLMHABNOUOUJUPHUCUFUJUPKUGABSQUATUBT $.
    $( [27-Dec-2008] $) $( [18-Oct-2007] $)

  $( Multiplication of two ratios.  Theorem I.14 of [Apostol] p. 18. $)
  divmuldiv $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( ( C e. CC /\ C =/= 0 )
                    /\ ( D e. CC /\ D =/= 0 ) ) )
               -> ( ( A / C ) x. ( B / D ) ) = ( ( A x. B ) / ( C x. D ) ) ) $=
    ( cc wcel cc0 wne wa cdiv co cmul w3a 3anass divcl syl2an 3adantr1 3adantl1
    wceq simp2 mulcl ax-mulcl ad2ant2r mulne0 divcan3 syl111anc mul4 opreqan12d
    jca divcan2 eqtr3d opreq1d syl2anbr an4s ) AEFZCEFZCGHZIZBEFZDEFZDGHZIZACJK
    ZBDJKZLKZABLKZCDLKZJKZSZUOURIUOUPUQMZUSUTVAMZVIUSVBIUOUPUQNUSUTVANVJVKIZVGV
    ELKZVGJKZVEVHVLVEEFZVGEFZVGGHZVNVESVJVCEFZVDEFZVOVKACOZBDOZVCVDUAPUPUQVKVPU
    OURUTVAVPUSUPUTVPUQVACDUBUCQRUPUQVKVQUOURUTVAVQUSCDUDQRVEVGUEUFVLVMVFVGJVLC
    VCLKZDVDLKZLKZVMVFVJUPVRIUTVSIWDVMSVKVJUPVRUOUPUQTVTUIVKUTVSUSUTVATWAUICVCD
    VDUGPVJVKWBAWCBLACUJBDUJUHUKULUKUMUN $.
    $( [28-Dec-2008] $) $( [1-Aug-2004] $)

  $( Cancellation of common factor in a ratio. $)
  divcan5 $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) /\
       ( C e. CC /\ C =/= 0 ) ) -> ( ( C x. A ) / ( C x. B ) ) = ( A / B ) ) $=
    ( cc wcel cc0 wne wa w3a cdiv co cmul c1 wceq divid opreq1d 3ad2ant3 simp3l
    simp1 simp3 simp2 divmuldiv syl22anc divcl 3expb mulid2 syl 3adant3 3eqtr3d
    ) ADEZBDEZBFGZHZCDEZCFGZHZIZCCJKZABJKZLKZMUSLKZCALKCBLKJKZUSUPUJUTVANUMUPUR
    MUSLCOPQUQUNUJUPUMUTVBNUJUMUNUORUJUMUPSUJUMUPTUJUMUPUACACBUBUCUJUMVAUSNZUPU
    JUMHUSDEZVCUJUKULVDABUDUEUSUFUGUHUI $.
    $( [9-Jan-2006] $)

  $( Swap the denominators in the product of two ratios. $)
  divmul13 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( ( C e. CC /\ C =/= 0 )
                    /\ ( D e. CC /\ D =/= 0 ) ) )
                -> ( ( A / C ) x. ( B / D ) ) = ( ( B / C ) x. ( A / D ) ) ) $=
    ( cc wcel cc0 wne cmul cdiv wceq ax-mulcom adantr opreq1d divmuldiv ancom1s
    wa co 3eqtr4d ) AEFZBEFZQZCEFCGHQDEFDGHQQZQZABIRZCDIRZJRBAIRZUFJRZACJRBDJRI
    RBCJRADJRIRZUDUEUGUFJUBUEUGKUCABLMNABCDOUATUCUIUHKBACDOPS $.
    $( [14-Feb-2009] $) $( [3-May-2005] $)

  $( Swap the numerators in the product of two ratios. $)
  divmul24 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( ( C e. CC /\ C =/= 0 )
                    /\ ( D e. CC /\ D =/= 0 ) ) )
                -> ( ( A / C ) x. ( B / D ) ) = ( ( A / D ) x. ( B / C ) ) ) $=
    ( cc wcel wa cc0 wne cmul co cdiv wceq ax-mulcom ad2ant2r opreq2d divmuldiv
    adantl ancom2s 3eqtr4d ) AEFBEFGZCEFZCHIZGZDEFZDHIZGZGZGZABJKZCDJKZLKUJDCJK
    ZLKZACLKBDLKJKADLKBCLKJKZUIUKULUJLUHUKULMZUAUBUEUOUCUFCDNORPABCDQUAUGUDUNUM
    MABDCQST $.
    $( [18-Feb-2009] $) $( [3-May-2005] $)

  $( Addition of two ratios.  Theorem I.13 of [Apostol] p. 18. $)
  divadddiv $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( ( C e. CC /\ C =/= 0 )
                    /\ ( D e. CC /\ D =/= 0 ) ) ) ->
  ( ( A / C ) + ( B / D ) ) = ( ( ( A x. D ) + ( C x. B ) ) / ( C x. D ) ) ) $=
    ( cc wcel wa cc0 wne cmul caddc cdiv wceq ax-mulcl ad2ant2r adantrl adantrr
    co c1 divid ancoms ad2ant2lr mulne0 adantl divdir syl111anc ad2antll simprl
    jca opreq2d anim2i divmuldiv sylancom w3a divcl mulid1 syl 3eqtr3d ad2antrl
    3expb adantlr opreq1d pm3.22 mulid2 adantll opreq12d eqtr2d ) AEFZBEFZGZCEF
    ZCHIZGZDEFZDHIZGZGZGZADJRZCBJRZKRCDJRZLRZVSWALRZVTWALRZKRZACLRZBDLRZKRVRVSE
    FZVTEFZWAEFZWAHIZGZWBWEMVJVPWHVMVHVNWHVIVOADNOPVIVMWIVHVPVIVKWIVLVKVIWICBNU
    AQUBVQWLVJVQWJWKVKVNWJVLVOCDNOCDUCUIUDVSVTWAUEUFVRWCWFWDWGKVHVQWCWFMVIVHVQG
    ZWFDDLRZJRZWFSJRZWCWFWMWNSWFJVPWNSMVHVMDTUGUJVHVQVHVNGWOWCMVQVNVHVMVNVOUHUK
    ADCDULUMVHVMWPWFMZVPVHVKVLWQVHVKVLUNWFEFWQACUOWFUPUQUTQURVAVIVQWDWGMVHVIVQG
    ZCCLRZWGJRZSWGJRZWDWGWRWSSWGJVMWSSMVIVPCTUSVBVIVQVKVIGZWTWDMVIVMXBVPVIVKXBV
    LVIVKVCQQCBCDULUMVIVPXAWGMZVMVIVNVOXCVIVNVOUNWGEFXCBDUOWGVDUQUTPURVEVFVG $.
    $( [21-Feb-2009] $) $( [1-Aug-2004] $)

  $( Division of two ratios.  Theorem I.15 of [Apostol] p. 18. $)
  divdivdiv $p |- ( ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) ) /\
                  ( ( C e. CC /\ C =/= 0 ) /\ ( D e. CC /\ D =/= 0 ) ) )
                -> ( ( A / B ) / ( C / D ) ) = ( ( A x. D ) / ( B x. C ) ) ) $=
    ( cc wcel cc0 wa cdiv co cmul wceq divcl syl111anc mulcom syl11anc syl22anc
    wne c1 mulcl simprrl simprll simprlr simpll simplrl simplr simprl divmuldiv
    simplrr eqtrd opreq2d simprr opreq1d simprrr mulne0 divid 3eqtrd mulass syl
    mulid2 3eqtr3d eqtr3d wb ad2ant2lr divne0 adantl divmul syl112anc mpbird )
    AEFZBEFZBGRZHZHZCEFZCGRZHZDEFZDGRZHZHZHZABIJZCDIJZIJADKJZBCKJZIJZLZWDWGKJZW
    CLZWBWDDCIJZWCKJZKJZWIWCWBWLWGWDKWBWLWCWKKJZWGWBWKEFZWCEFZWLWNLWBVRVOVPWOVN
    VQVRVSUAZVNVOVPVTUBZVNVOVPVTUCZDCMNZWBVJVKVLWPVJVMWAUDZVJVKVLWAUEZVJVKVLWAU
    IABMNZWKWCOPWBVJVRVMVQWNWGLXAWQVJVMWAUFVNVQVTUGZADBCUHQUJUKWBWDWKKJZWCKJZSW
    CKJZWMWCWBXESWCKWBXECDKJZDCKJZIJZXIXIIJZSWBVOVRVTVQXEXJLWRWQVNVQVTULXDCDDCU
    HQWBXHXIXIIWBVOVRXHXILWRWQCDOPUMWBXIEFZXIGRZXKSLWBVRVOXLWQWRDCTPWBVRVSVOVPX
    MWQVNVQVRVSUNZWRWSDCUOQXIUPPUQUMWBWDEFZWOWPXFWMLWBVOVRVSXOWRWQXNCDMNZWTXCWD
    WKWCURNWBWPXGWCLXCWCUTUSVAVBWBWPWGEFZXOWDGRZWHWJVCXCWBWEEFZWFEFZWFGRZXQWBVJ
    VRXSXAWQADTPWBVKVOXTXBWRBCTPVMVQYAVJVTBCUOVDWEWFMNXPWAXRVNCDVEVFWCWGWDVGVHV
    I $.
    $( [15-Mar-2012] $) $( [2-Aug-2004] $)

  $( Division of two ratios.  Theorem I.15 of [Apostol] p. 18. $)
  divdivdivOLD $p |- ( ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) ) /\
                  ( ( C e. CC /\ C =/= 0 ) /\ ( D e. CC /\ D =/= 0 ) ) )
                -> ( ( A / B ) / ( C / D ) ) = ( ( A x. D ) / ( B x. C ) ) ) $=
    ( cc wcel cc0 wne wa cdiv co cmul wceq divcl 3expb ancoms ad2ant2r ax-mulcl
    adantl c1 adantrr adantr mulcom simpl simprl anim12i simplr divmuldiv eqtrd
    syl11anc syl12anc opreq2d simpll pm3.22 opreq1d mulne0 divid 3eqtrd adantlr
    syl21anc mulass syl111anc mulid2 3eqtr3d eqtr3d wb adantrl ad2ant2lr divne0
    syl divmul syl112anc mpbird ) AEFZBEFZBGHZIZIZCEFZCGHZIZDEFZDGHZIZIZIZABJKZ
    CDJKZJKADLKZBCLKZJKZMZWHWKLKZWGMZWFWHDCJKZWGLKZLKZWMWGWFWPWKWHLWFWPWGWOLKZW
    KWFWOEFZWGEFZWPWRMWEWSVRWAWBWSWCWBWAWSWBVSVTWSDCNOPUASZVRWTWEVNVOVPWTABNOUB
    ZWOWGUCUJWFVNWBIVQWAWRWKMVRVNWEWBVNVQUDWAWBWCUEZUFVNVQWEUGVRWAWDUEADBCUHUKU
    IULWFWHWOLKZWGLKZTWGLKZWQWGWFXDTWGLWEXDTMVRWEXDCDLKZDCLKZJKZXHXHJKZTWEVSWBW
    DWAIXDXIMVSVTWDUMXCWAWDUNCDDCUHUTWEXGXHXHJVSWBXGXHMVTWCCDUCQUOWEXHEFZXHGHZX
    JTMVSWBXKVTWCWBVSXKDCRPQWDWAXLDCUPPXHUQUJURSUOWFWHEFZWSWTXEWQMWEXMVRVSWDXMV
    TVSWBWCXMCDNOUSSZXAXBWHWOWGVAVBWFWTXFWGMXBWGVCVJVDVEWFWTWKEFZXMWHGHZWLWNVFX
    BWFWIEFZWJEFZWJGHZXOVRWDXQWAVNWBXQVQWCADRQVGVQWAXRVNWDVOVSXRVPVTBCRQVHVQWAX
    SVNWDBCUPVHWIWJNVBXNWEXPVRCDVISWGWKWHVKVLVM $.
    $( [28-Dec-2008] $) $( [2-Aug-2004] $)

  ${
    divmuldiv.1 $e |- A e. CC $.
    divmuldiv.2 $e |- B e. CC $.
    divmuldiv.3 $e |- C e. CC $.
    divmuldiv.4 $e |- D e. CC $.
    divmuldiv.5 $e |- B =/= 0 $.
    divmuldiv.6 $e |- D =/= 0 $.
    $( Multiplication of two ratios.  Theorem I.14 of [Apostol] p. 18. $)
    divmuldivi $p |- ( ( A / B ) x. ( C / D ) ) =
                      ( ( A x. C ) / ( B x. D ) ) $=
      ( cc wcel cc0 wne wa cdiv co cmul wceq pm3.2i divmuldiv mp4an ) AKLCKLBKL
      ZBMNZODKLZDMNZOABPQCDPQRQACRQBDRQPQSEGUCUDFITUEUFHJTACBDUAUB $.
      $( [16-Feb-1995] $)

    $( Swap denominators of two ratios. $)
    divmul13i $p |- ( ( A / B ) x. ( C / D ) ) =
                   ( ( C / B ) x. ( A / D ) ) $=
      ( cmul co cdiv mulcomi opreq1i divmuldivi 3eqtr4ri ) CAKLZBDKLZMLACKLZSML
      CBMLADMLKLABMLCDMLKLRTSMCAGENOCBADGFEHIJPABCDEFGHIJPQ $.
      $( [6-Aug-1999] $)

    $( Addition of two ratios.  Theorem I.13 of [Apostol] p. 18. $)
    divadddivi $p |- ( ( A / B ) + ( C / D ) ) =
                      ( ( ( A x. D ) + ( B x. C ) ) / ( B x. D ) ) $=
      ( cc wcel cc0 wne cdiv co caddc cmul wa mpanl12 wceq divadddiv mp2an ) DK
      LZDMNZABOPCDOPQPADRPBCRPQPBDRPOPUAZHJBKLZBMNZUDUESZUFFIAKLCKLUGUHSUISUFEG
      ACBDUBTTUC $.
      $( [21-Feb-1995] $)

    ${
      divdivdiv.7 $e |- C =/= 0 $.
      $( Division of two ratios.  Theorem I.15 of [Apostol] p. 18. $)
      divdivdivi $p |- ( ( A / B ) / ( C / D ) )
                        = ( ( A x. D ) / ( B x. C ) ) $=
        ( cc wcel cc0 wne wa cdiv co cmul pm3.2i wceq divdivdiv mp4an ) ALMBLMZ
        BNOZPCLMZCNOZPDLMZDNOZPABQRCDQRQRADSRBCSRQRUAEUDUEFITUFUGGKTUHUIHJTABCD
        UBUC $.
        $( [22-Feb-1995] $)
    $}
  $}

  $( The reciprocal of a ratio. $)
  recdiv $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) ) ->
                ( 1 / ( A / B ) ) = ( B / A ) ) $=
    ( cc wcel cc0 wne wa c1 cdiv co cmul ax-1cn div1i opreq1i ax-1ne0 divdivdiv
    wceq pm3.2i mpanl12 mulid2 syl5eqr opreqan12rd ad2ant2r eqtrd ) ACDZAEFZGBC
    DZBEFZGGZHABIJZIJZHBKJZHAKJZIJZBAIJZUIUKHHIJZUJIJZUNUPHUJIHLMNHCDZURHEFZGUI
    UQUNQLURUSLORHHABPSUAUEUGUNUOQUFUHUGUEULBUMAIBTATUBUCUD $.
    $( [12-Aug-2007] $) $( [3-Aug-2004] $)

  $( Cancellation of inverted fractions. $)
  divcan6 $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) ) ->
         ( ( A / B ) x. ( B / A ) ) = 1 ) $=
    ( cc wcel cc0 wne wa cdiv co c1 cmul recdiv opreq2d wceq divcl 3expb divne0
    adantlr recid syl11anc eqtr3d ) ACDZAEFZGBCDZBEFZGZGZABHIZJUHHIZKIZUHBAHIZK
    IJUGUIUKUHKABLMUGUHCDZUHEFUJJNUBUFULUCUBUDUEULABOPRABQUHSTUA $.
    $( [28-Dec-2007] $)

  $( Swap denominators in a division. $)
  divdiv23 $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 )
      /\ ( C e. CC /\ C =/= 0 ) ) -> ( ( A / B ) / C ) = ( ( A / C ) / B ) ) $=
    ( cc wcel cc0 wne wa w3a c1 cdiv cmul wceq reccl div23 syl3an2 divrec 3expb
    co 3adant3 opreq1d divcl syl3an1 3impa 3com23 3eqtr4d ) ADEZBDEZBFGZHZCDEZC
    FGZHZIZAJBKSZLSZCKSZACKSZUOLSZABKSZCKSURBKSZUJUGUODEUMUQUSMBNAUOCOPUNUTUPCK
    UGUJUTUPMZUMUGUHUIVBABQRTUAUGUMUJVAUSMZUGUMUJVCUGUMHZUHUIVCVDURDEZUHUIVCUGU
    KULVEACUBRURBQUCRUDUEUF $.
    $( [28-Dec-2008] $) $( [2-Aug-2004] $)

  ${
    divdiv23.1 $e |- A e. CC $.
    divdiv23.2 $e |- B e. CC $.
    divdiv23.3 $e |- C e. CC $.
    ${
      divdiv23.4 $e |- B =/= 0 $.
      divdiv23.5 $e |- C =/= 0 $.
      $( Swap denominators in a division. $)
      divdiv23i $p |- ( ( A / B ) / C ) = ( ( A / C ) / B ) $=
        ( c1 cdiv co cmul reccli div23i divreci opreq1i divcli 3eqtr4i ) AIBJKZ
        LKZCJKACJKZSLKABJKZCJKUABJKASCDBEGMFHNUBTCJABDEGOPUABACDFHQEGOR $.
        $( [15-Sep-1999] $)
    $}
    $( Swap denominators in a division. $)
    divdiv23zi $p |- ( ( B =/= 0 /\ C =/= 0 ) ->
                ( ( A / B ) / C ) = ( ( A / C ) / B ) ) $=
      ( cc wcel cc0 wne cdiv co wceq wa divdiv23 mp3an1 mpanr1 mpanl1 ) BGHZBIJ
      ZCIJZABKLCKLACKLBKLMZESTNZCGHZUAUBFAGHUCUDUANUBDABCOPQR $.
      $( [15-Sep-1999] $)
  $}

  $( Division into a fraction. $)
  divdiv1 $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) /\
       ( C e. CC /\ C =/= 0 ) ) -> ( ( A / B ) / C ) = ( A / ( B x. C ) ) ) $=
    ( cc wcel cc0 wne wa cdiv co c1 cmul ax-1cn ax-1ne0 pm3.2i divdivdiv mpanr2
    w3a wceq 3impa opreq2d ad2antrl 3adant1 mulid1 opreq1d 3ad2ant1 3eqtr3d
    div1 ) ADEZBDEBFGHZCDEZCFGZHZRABIJZCKIJZIJZAKLJZBCLJZIJZUNCIJZAURIJZUIUJUMU
    PUSSZUIUJHUMKDEZKFGZHVBVCVDMNOABCKPQTUJUMUPUTSZUIUKVEUJULUKUOCUNICUHUAUBUCU
    IUJUSVASUMUIUQAURIAUDUEUFUG $.
    $( [31-Dec-2007] $)

  $( Division by a fraction. $)
  divdiv2 $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) /\ ( C e. CC /\ C =/= 0 )
              ) -> ( A / ( B / C ) ) = ( ( A x. C ) / B ) ) $=
    ( cc wcel cc0 wne wa c1 cdiv co cmul ax-1cn ax-1ne0 pm3.2i divdivdiv mpanl2
    w3a wceq 3impb 3ad2ant1 opreq1d mulid2 ad2antrl 3adant3 opreq2d 3eqtr3d
    div1 ) ADEZBDEZBFGZHZCDECFGHZRZAIJKZBCJKZJKZACLKZIBLKZJKZAUPJKURBJKUIULUMUQ
    UTSZUIIDEZIFGZHULUMHVAVBVCMNOAIBCPQTUNUOAUPJUIULUOASUMAUHUAUBUNUSBURJUIULUS
    BSZUMUJVDUIUKBUCUDUEUFUG $.
    $( [27-Dec-2008] $)

  $( Division into a reciprocal. $)
  recdiv2 $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) ) ->
                ( ( 1 / A ) / B ) = ( 1 / ( A x. B ) ) ) $=
    ( c1 cc wcel cc0 wne wa cdiv co cmul wceq ax-1cn divdiv1 mp3an1 ) CDEADEAFG
    HBDEBFGHCAIJBIJCABKJIJLMCABNO $.
    $( [19-Oct-2007] $)

  $( Two numbers whose reciprocals sum to 1 are called "conjugates" and satisfy
     this relationship.  Equation 5 of [Kreyszig] p. 12. $)
  conjmul $p |- ( ( ( P e. CC /\ P =/= 0 ) /\ ( Q e. CC /\ Q =/= 0 ) ) ->
      ( ( ( 1 / P ) + ( 1 / Q ) ) = 1 <-> ( ( P - 1 ) x. ( Q - 1 ) ) = 1 ) ) $=
    ( cc wcel cc0 wne wa cmul co c1 cdiv caddc wceq cmin reccl adantr syl111anc
    recid 3eqtrd ad2ant2r simpll simprl opreq1d mulid2 ad2antrl adantl ad2antrr
    mul32 mulass opreq2d mulid1 opreq12d mulcl adddi addcom 3eqtr4d syl eqeq12d
    wb addcl syl2an mulne0 ax-1cn mulcan mp3an2 syl12anc eqcom muleqadd 3bitr3d
    syl5bb ) ACDZAEFZGZBCDZBEFZGZGZABHIZJAKIZJBKIZLIZHIZVRJHIZMZABLIZVRMZWAJMZA
    JNIBJNIHIJMZVQWBWEWCVRVQVRVSHIZVRVTHIZLIZBALIZWBWEVQWIBWJALVQWIAVSHIZBHIZJB
    HIZBVQVKVNVSCDZWIWNMVKVLVPUAZVMVNVOUBZVMWPVPAOZPZABVSUHQVMWNWOMVPVMWMJBHARU
    CPVNWOBMVMVOBUDUESVQWJABVTHIZHIZAJHIZAVQVKVNVTCDZWJXBMWQWRVPXDVMBOZUFZABVTU
    IQVPXBXCMVMVPXAJAHBRUJUFVKXCAMVLVPAUKUGSULVQVRCDZWPXDWBWKMVKVNXGVLVOABUMZTZ
    WTXFVRVSVTUNQVKVNWEWLMVLVOABUOTUPVKVNWCVRMZVLVOVKVNGZXGXJXHVRUKUQTURVQWACDZ
    XGVREFZWDWGUSZVMWPXDXLVPWSXEVSVTUTVAXIABVBXLJCDXGXMGXNVCWAJVRVDVEVFVKVNWFWH
    USVLVOWFVRWEMXKWHWEVRVGABVHVJTVI $.
    $( [12-Nov-2006] $)

  ${
    $d x y B $.
    redivcl.1 $e |- A e. RR $.
    redivcl.2 $e |- B e. RR $.
    redivcl.3 $e |- B =/= 0 $.
    $( Closure law for division of reals. $)
    redivcli $p |- ( A / B ) e. RR $=
      ( vx cdiv co c1 cmul cr recni divreci cv wceq wrex wcel cc wb ax-1cn recn
      cc0 wne ax-rrecex mp2an wex df-rex cif eqeq2 opreq2 eqeq1d bibi12d elimel
      wa divmuli dedth syl eleq1a sylbird 19.23aiv sylbi ax-mp remulcli eqeltri
      imp ) ABGHAIBGHZJHKABACLBDLZEMAVFCBFNZJHZIOZFKPZVFKQZBKQBUBUCVKDEFBUDUEVK
      VHKQZVJUNZFUFVLVJFKUGVNVLFVMVJVLVMVJVFVHOZVLVMVHRQZVOVJSZVHUAVPVQVFVPVHIU
      HZOZBVRJHZIOZSVHIVHVROZVOVSVJWAVHVRVFUIWBVIVTIVHVRBJUJUKULIBVRTVGVHIRTUME
      UOUPUQVHKVFURUSVEUTVAVBVCVD $.
      $( [9-May-1999] $)
  $}

  ${
    redivclz.1 $e |- A e. RR $.
    redivclz.2 $e |- B e. RR $.
    $( Closure law for division of reals. $)
    redivclzi $p |- ( B =/= 0 -> ( A / B ) e. RR ) $=
      ( cc0 wne cdiv co wcel cif wceq opreq2 eleq1d 1re keepel elimne0 redivcli
      cr c1 dedth ) BEFZABGHZRIAUABSJZGHZRIBSBUCKUBUDRBUCAGLMAUCCUABSRDNOBPQT
      $.
      $( [9-May-1999] $)
  $}

  $( Closure law for division of reals. $)
  redivcl $p |- ( ( A e. RR /\ B e. RR /\ B =/= 0 ) -> ( A / B ) e. RR ) $=
    ( cr wcel cc0 wne cdiv co wi wceq opreq1 eleq1d imbi2d neeq1 opreq2 imbi12d
    cif 0re elimel redivclzi dedth2h 3impia ) ACDZBCDZBEFZABGHZCDZUCUDUEUGIUEUC
    AEQZBGHZCDZIUDBEQZEFZUHUKGHZCDZIABEEAUHJZUGUJUEUOUFUICAUHBGKLMBUKJZUEULUJUN
    BUKENUPUIUMCBUKUHGOLPUHUKAECRSBECRSTUAUB $.
    $( [4-May-2005] $) $( [27-Sep-1999] $)

  ${
    rereccl.1 $e |- A e. RR $.
    rereccl.2 $e |- A =/= 0 $.
    $( Closure law for reciprocal. $)
    rereccli $p |- ( 1 / A ) e. RR $=
      ( c1 1re redivcli ) DAEBCF $.
      $( [30-Apr-2005] $)
  $}

  ${
    rerecclz.1 $e |- A e. RR $.
    $( Closure law for reciprocal. $)
    rerecclzi $p |- ( A =/= 0 -> ( 1 / A ) e. RR ) $=
      ( c1 1re redivclzi ) CADBE $.
      $( [30-Apr-2005] $)
  $}

  $( Closure law for reciprocal. $)
  rereccl $p |- ( ( A e. RR /\ A =/= 0 ) -> ( 1 / A ) e. RR ) $=
    ( c1 cr wcel cc0 wne cdiv co 1re redivcl mp3an1 ) BCDACDAEFBAGHCDIBAJK $.
    $( [30-Apr-2005] $)

  ${
    eqneg.1 $e |- A e. CC $.
    $( A number equal to its negative is zero. $)
    eqnegi $p |- ( A = -u A <-> A = 0 ) $=
      ( cneg wceq cc0 c1 caddc co wne 1re readdcli lt01 addgt0ii gt0ne0ii df-ne
      wn mpbi cmul wo opreq2 1p1timesi negidi eqcomi 3eqtr4g recni mul0ori cmin
      sylib ord mpi df-neg 0cn subidi eqtr2i id negeq 3eqtr4a impbii ) AACZDZAE
      DZUTFFGHZEDZPZVAVBEIVDVBFFJJKZFFJJLLMNVBEOQUTVCVAUTVBARHZEDVCVASUTAAGHAUS
      GHZVFEAUSAGTABUAVGEABUBUCUDVBAVBVEUEBUFUHUIUJVAEECZAUSVHEEUGHEEUKEULUMUNV
      AUOAEUPUQUR $.
      $( [29-May-1999] $)
  $}

  $( A number equal to its negative is zero. $)
  eqneg $p |- ( A e. CC -> ( A = -u A <-> A = 0 ) ) $=
    ( cc wcel cneg wceq cc0 wb cif id negeq eqeq12d eqeq1 bibi12d elimel eqnegi
    0cn dedth ) ABCZAADZEZAFEZGRAFHZUBDZEZUBFEZGAFAUBEZTUDUAUEUFAUBSUCUFIAUBJKA
    UBFLMUBAFBPNOQ $.
    $( [12-Jul-2005] $)

  $( A number is zero iff its negative is zero. $)
  negeq0 $p |- ( A e. CC -> ( A = 0 <-> -u A = 0 ) ) $=
    ( cc wcel cneg wceq cc0 negneg eqeq2d eqcom syl6rbb eqneg negcl syl 3bitr3d
    wb ) ABCZAADZEZQQDZEZAFEQFEZPTQAERPSAQAGHQAIJAKPQBCTUAOALQKMN $.
    $( [12-Jul-2005] $)

  ${
    negne0.1 $e |- A e. CC $.
    $( A number is nonzero iff its negative is nonzero. $)
    negne0bi $p |- ( A =/= 0 <-> -u A =/= 0 ) $=
      ( cc0 cneg cc wcel wceq wb negeq0 ax-mp necon3bii ) ACADZCAEFACGLCGHBAIJK
      $.
      $( [10-Aug-1999] $)
    ${
      negn0.2 $e |- A =/= 0 $.
      $( The negative of a nonzero number is nonzero. $)
      negne0i $p |- -u A =/= 0 $=
        ( cc0 wne cneg negne0bi mpbi ) ADEAFDECABGH $.
        $( [30-Jul-2004] $)
    $}
  $}

  $( Quotient of two negatives.  (Contributed by Paul Chapman, 10-Nov-2012.) $)
  div2neg $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                  ( -u A / -u B ) = ( A / B ) ) $=
    ( cc wcel cc0 wne w3a cneg cdiv co wceq cmul negcl 3ad2ant2 simp1 syl112anc
    c1 simp2 3adant1 3ad2ant1 simp3 div12 divneg syl111anc divid negeqd opreq2d
    eqtr3d ax-1cn negcli mulcom mpan2 mulm1 eqtrd 3eqtrd divcl negeq0 necon3bid
    wb biimpa divmul mpbird ) ACDZBCDZBEFZGZAHZBHZIJABIJZKZVHVILJZVGKZVFVKAVHBI
    JZLJZAQHZLJZVGVFVHCDZVCVDVEVKVNKVDVCVQVEBMNZVCVDVEOVCVDVERZVCVDVEUAZVHABUBP
    VFVMVOALVFBBIJZHZVMVOVFVDVDVEWBVMKVSVSVTBBUCUDVFWAQVDVEWAQKVCBUESUFUHUGVCVD
    VPVGKVEVCVPVOALJZVGVCVOCDVPWCKQUIUJAVOUKULAUMUNTUOVFVGCDZVICDVQVHEFZVJVLUSV
    CVDWDVEAMTABUPVRVDVEWEVCVDVEWEVDBEVHEBUQURUTSVGVIVHVAPVB $.
    $( [10-Nov-2012] $)

