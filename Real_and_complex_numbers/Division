$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Division
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Define division.  Theorem ~ divmul relates it to multiplication, and
       ~ divcl and ~ redivcl prove its closure laws. $)
    df-div $a |- / = { <. <. x , y >. , z >. |
                   ( ( x e. CC /\ y e. ( CC \ { 0 } ) ) /\
                   z = U. { w e. CC | ( y x. w ) = x } ) } $.
    $(
    Future simpler alternative?
    df-div $a |- / = { <. <. x , y >. , z >. |
                ( ( y e. CC /\ z e. CC ) /\  x = ( y x. z ) ) } $.
    $)
  $}


  ${
    $d x y z w A $.  $d x y z w B $.
    divval.1 $e |- A e. CC $.
    divval.2 $e |- B e. CC $.
    divval.3 $e |- B =/= 0 $.
    $( Value of division: the (unique) element ` x ` such that
        ` ( B x. x ) = A ` .  This is meaningful only when ` B ` is nonzero. $)
    divval $p |- ( A / B ) = U. { x e. CC | ( B x. x ) = A } $=
      ( vy vz vw cc wcel cc0 csn cdif cdiv co cv cmul wceq crab cuni wn eldif
      elisseti elsnc nemtbir mpbir2an axcnex rabex uniex eqeq2 rabbisdv unieqd
      opreq1 eqeq1d df-div oprabval2 mp2an ) BJKCJLMZNZKZBCOPCAQZRPZBSZAJTZUAZS
      DVACJKCUSKZUBCJUSUCEVGCLFCLCJEUDUEUFUGGHIBCJUTHQZVBRPZGQZSZAJTZUAVFOVIBSZ
      AJTZUAVEVDAJUHUIUJVJBSZVLVNVOVKVMAJVJBVIUKULUMVHCSZVNVEVPVMVDAJVPVIVCBVHC
      VBRUNUOULUMGHIAUPUQUR $.
      $( [8-May-1999] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    divmul.1 $e |- A e. CC $.
    divmul.2 $e |- B e. CC $.
    divmul.3 $e |- C e. CC $.
    divmul.4 $e |- B =/= 0 $.
    $( Relationship between division and multiplication. $)
    divmul $p |- ( ( A / B ) = C <-> ( B x. C ) = A ) $=
      ( vx cc wcel cdiv co wceq cmul wb cv eqeq2 opreq2 eqeq1d bibi12d crab
      cuni wreu receu reuuni1 mpan2 divval eqeq1i syl6rbbr vtoclga ax-mp ) CIJA
      BKLZCMZBCNLZAMZOZFULHPZMZBUQNLZAMZOUPHCIUQCMZURUMUTUOUQCULQVAUSUNAUQCBNRS
      TUQIJZUTUTHIUAUBZUQMZURVBUTHIUCUTVDOHBAEDGUDUTHIUEUFULVCUQHABDEGUGUHUIUJU
      K $.
      $( [2-Feb-1995] $)
  $}

  ${
    divmulz.1 $e |- A e. CC $.
    divmulz.2 $e |- B e. CC $.
    divmulz.3 $e |- C e. CC $.
    $( Relationship between division and multiplication. $)
    divmulz $p |- ( B =/= 0 -> ( ( A / B ) = C <-> ( B x. C ) = A ) ) $=
      ( cc0 wne cdiv co wceq cmul wb c1 cif opreq2 eqeq1d opreq1 bibi12d cc
      ax1cn keepel elimne0 divmul dedth ) BGHZABIJZCKZBCLJZAKZMAUFBNOZIJZCKZUKC
      LJZAKZMBNBUKKZUHUMUJUOUPUGULCBUKAIPQUPUIUNABUKCLRQSAUKCDUFBNTEUAUBFBUCUDU
      E $.
      $( [8-May-1999] $)
  $}

  $( Relationship between division and multiplication. $)
  divmult $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ B =/= 0 ) ->
               ( ( A / B ) = C <-> ( B x. C ) = A ) ) $=
    ( cc wcel w3a cc0 wne cdiv co wceq cmul wb wi cif opreq1 eqeq1d eqeq2
    bibi12d imbi2d neeq1 opreq2 imbi12d 0cn elimel divmulz dedth3h imp ) ADEZBD
    EZCDEZFBGHZABIJZCKZBCLJZAKZMZUIUJUKULUQNULUIAGOZBIJZCKZUOURKZMZNABCGGGUJBGO
    ZGHZURVCIJZCKZVCCLJZURKZMZNVDVEUKCGOZKZVCVJLJZURKZMZNAURKZUQVBULVOUNUTUPVAV
    OUMUSCAURBIPQAURUORSTBVCKZULVDVBVIBVCGUAVPUTVFVAVHVPUSVECBVCURIUBQVPUOVGURB
    VCCLPQSUCCVJKZVIVNVDVQVFVKVHVMCVJVERVQVGVLURCVJVCLUBQSTURVCVJAGDUDUEBGDUDUE
    CGDUDUEUFUGUH $.
    $( [4-Aug-2004] $) $( [2-Aug-2004] $)

  $( Relationship between division and multiplication. $)
  divmul2t $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ B =/= 0 ) ->
               ( ( A / B ) = C <-> A = ( B x. C ) ) ) $=
    ( cc wcel w3a cc0 wne wa cdiv co wceq cmul divmult eqcom syl6bb ) ADEBDECDE
    FBGHIABJKCLBCMKZALAQLABCNQAOP $.
    $( [24-Feb-2006] $) $( [7-Feb-2006] $)

  $( Relationship between division and multiplication. $)
  divmul3t $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ B =/= 0 ) ->
               ( ( A / B ) = C <-> A = ( C x. B ) ) ) $=
    ( cc wcel w3a cc0 wne wa cdiv co wceq cmul divmul2t wb axmulcom eqeq2d
    3adant1 adantr bitrd ) ADEZBDEZCDEZFZBGHZIABJKCLABCMKZLZACBMKZLZABCNUDUGUIO
    ZUEUBUCUJUAUBUCIUFUHABCPQRST $.
    $( [27-Feb-2006] $) $( [13-Feb-2006] $)

  ${
    $d x A $.  $d x B $.
    divcl.1 $e |- A e. CC $.
    divcl.2 $e |- B e. CC $.
    divcl.3 $e |- B =/= 0 $.
    $( Closure law for division. $)
    divcl $p |- ( A / B ) e. CC $=
      ( vx cdiv co cv cmul wceq cc crab cuni divval wreu wcel receu reucl
      ax-mp eqeltr ) ABGHBFIJHAKZFLMNZLFABCDEOUBFLPUCLQFBADCERUBFLSTUA $.
      $( [2-Feb-1995] $)
  $}

  ${
    divclz.1 $e |- A e. CC $.
    divclz.2 $e |- B e. CC $.
    $( Closure law for division. $)
    divclz $p |- ( B =/= 0 -> ( A / B ) e. CC ) $=
      ( cc0 wne cdiv co cc wcel c1 cif wceq opreq2 eleq1d ax1cn keepel elimne0
      divcl dedth ) BEFZABGHZIJAUABKLZGHZIJBKBUCMUBUDIBUCAGNOAUCCUABKIDPQBRST
      $.
      $( [7-May-1999] $)
  $}

  $( Closure law for division. $)
  divclt $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) -> ( A / B ) e. CC ) $=
    ( cc wcel cc0 wne cdiv co wi cif wceq opreq1 eleq1d imbi2d neeq1 opreq2
    imbi12d 0cn elimel divclz dedth2h 3impia ) ACDZBCDZBEFZABGHZCDZUCUDUEUGIUEU
    CAEJZBGHZCDZIUDBEJZEFZUHUKGHZCDZIABEEAUHKZUGUJUEUOUFUICAUHBGLMNBUKKZUEULUJU
    NBUKEOUPUIUMCBUKUHGPMQUHUKAECRSBECRSTUAUB $.
    $( [4-May-2005] $) $( [21-Jul-2001] $)

  ${
    reccl.1 $e |- A e. CC $.
    reccl.2 $e |- A =/= 0 $.
    $( Closure law for reciprocal. $)
    reccl $p |- ( 1 / A ) e. CC $=
      ( c1 ax1cn divcl ) DAEBCF $.
      $( [2-May-2005] $) $( [30-Apr-2005] $)
  $}

  ${
    recclz.1 $e |- A e. CC $.
    $( Closure law for reciprocal. $)
    recclz $p |- ( A =/= 0 -> ( 1 / A ) e. CC ) $=
      ( c1 ax1cn divclz ) CADBE $.
      $( [2-May-2005] $) $( [30-Apr-2005] $)
  $}

  $( Closure law for reciprocal. $)
  recclt $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 1 / A ) e. CC ) $=
    ( c1 cc wcel cc0 wne cdiv co ax1cn divclt mp3an1 ) BCDACDAEFBAGHCDIBAJK $.
    $( [2-May-2005] $) $( [30-Apr-2005] $)

  ${
    divcan.1 $e |- A e. CC $.
    divcan.2 $e |- B e. CC $.
    ${
      divcan.3 $e |- A =/= 0 $.
      $( A cancellation law for division. $)
      divcan2 $p |- ( A x. ( B / A ) ) = B $=
        ( cdiv co wceq cmul eqid divcl divmul mpbi ) BAFGZNHANIGBHNJBANDCBADCEK
        ELM $.
        $( [9-Feb-1995] $)

      $( A cancellation law for division. $)
      divcan1 $p |- ( ( B / A ) x. A ) = B $=
        ( cdiv co cmul divcl mulcom divcan2 eqtr ) BAFGZAHGAMHGBMABADCEICJABCDE
        KL $.
        $( [18-May-1999] $)
    $}

    $( A cancellation law for division. $)
    divcan1z $p |- ( A =/= 0 -> ( ( B / A ) x. A ) = B ) $=
      ( cc0 wne cdiv co cmul wceq c1 cif opreq2 id opreq12d eqeq1d cc ax1cn
      keepel elimne0 divcan1 dedth ) AEFZBAGHZAIHZBJBUCAKLZGHZUFIHZBJAKAUFJZUEU
      HBUIUDUGAUFIAUFBGMUINOPUFBUCAKQCRSDATUAUB $.
      $( [2-Oct-1999] $)

    $( A cancellation law for division.  We eliminate the third hypothesis of
       ~ divcan2 using the weak deduction theorem ~ dedth and keep the other
       two.  Because the first hypothesis shares the class variable ` A `
       with the hypothesis we're eliminating, we need to use ~ keepel in order
       to keep the first hypothesis. $)
    divcan2z $p |- ( A =/= 0 -> ( A x. ( B / A ) ) = B ) $=
      ( cc0 wne cdiv co cmul wceq c1 cif id opreq2 opreq12d eqeq1d cc ax1cn
      keepel elimne0 divcan2 dedth ) AEFZABAGHZIHZBJUCAKLZBUFGHZIHZBJAKAUFJZUEU
      HBUIAUFUDUGIUIMAUFBGNOPUFBUCAKQCRSDATUAUB $.
      $( [10-Aug-1999] $)
  $}

  $( A cancellation law for division. $)
  divcan1t $p |- ( ( A e. CC /\ B e. CC /\ A =/= 0 ) ->
                 ( ( B / A ) x. A ) = B ) $=
    ( cc wcel cc0 wne cdiv co cmul wceq wi cif neeq1 opreq2 id opreq12d eqeq1d
    imbi12d opreq1 opreq1d eqeq12d imbi2d 0cn elimel divcan1z dedth2h 3impia )
    ACDZBCDZAEFZBAGHZAIHZBJZUHUIUJUMKUHAELZEFZBUNGHZUNIHZBJZKUOUIBELZUNGHZUNIHZ
    USJZKABEEAUNJZUJUOUMURAUNEMVCULUQBVCUKUPAUNIAUNBGNVCOPQRBUSJZURVBUOVDUQVABU
    SVDUPUTUNIBUSUNGSTVDOUAUBUNUSAECUCUDBECUCUDUEUFUG $.
    $( [15-May-2005] $) $( [5-Jun-2004] $)

  $( A cancellation law for division. $)
  divcan2t $p |- ( ( A e. CC /\ B e. CC /\ A =/= 0 ) ->
                 ( A x. ( B / A ) ) = B ) $=
    ( cc wcel cc0 wne cdiv co cmul wceq wi cif neeq1 id opreq2 opreq12d eqeq1d
    imbi12d opreq1 opreq2d eqeq12d imbi2d 0cn elimel divcan2z dedth2h 3impia )
    ACDZBCDZAEFZABAGHZIHZBJZUHUIUJUMKUHAELZEFZUNBUNGHZIHZBJZKUOUNUIBELZUNGHZIHZ
    USJZKABEEAUNJZUJUOUMURAUNEMVCULUQBVCAUNUKUPIVCNAUNBGOPQRBUSJZURVBUOVDUQVABU
    SVDUPUTUNIBUSUNGSTVDNUAUBUNUSAECUCUDBECUCUDUEUFUG $.
    $( [29-May-2005] $) $( [3-Feb-2004] $)

  ${
    muldiv.1  $e |- ( C x. A ) = B $.
    muldiv.2  $e |- C e. CC $.
    muldiv.3  $e |- B e. CC $.
    muldiv.4  $e |- C =/= 0 $.
    muldiv.5  $e |- A e. CC $.
    $( MAY BE REVISED - replace with divmul ? $)
    $( Convert a multiplication into a division.  (Contributed by FL,
       10-Dec-2006.) $)
    muldiv  $p |- A = ( B / C ) $=
      ( cdiv co wceq cmul divmul mpbir eqcomi ) BCIJZAPAKCALJBKDBCAFEHGMNO $.
      $( [10-Dec-2006] $) $( [4-Dec-2006] $)
  $}

  $( The ratio of non-zero numbers is non-zero. $)
  divne0bt $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                  ( A =/= 0 <-> ( A / B ) =/= 0 ) ) $=
    ( cc wcel cc0 wne w3a cdiv co wceq cmul wb mul01t eqeq1d eqcom syl6rbbr
    3ad2ant2 0cn divmult mp3anl3 3impa bitr4d eqneqd ) ACDZBCDZBEFZGZAEABHIZEUG
    AEJZBEKIZAJZUHEJZUEUDUIUKLUFUEUKEAJUIUEUJEABMNAEOPQUDUEUFULUKLZUDUEECDUFUMR
    ABESTUAUBUC $.
    $( [1-Jun-2005] $) $( [2-Aug-2004] $)

  ${
    divneq0.1 $e |- A e. CC $.
    divneq0.2 $e |- B e. CC $.
    divneq0.3 $e |- A =/= 0 $.
    divneq0.4 $e |- B =/= 0 $.
    $( The ratio of non-zero numbers is non-zero. $)
    divne0 $p |- ( A / B ) =/= 0 $=
      ( cc wcel cc0 wne cdiv co w3a divne0bt mpbii mp3an ) AGHZBGHZBIJZABKLIJZC
      DFQRSMAIJTEABNOP $.
      $( [9-Feb-1995] $)
  $}

  ${
    recneq0z.1 $e |- A e. CC $.
    $( The reciprocal of a non-zero number is non-zero. $)
    recne0z $p |- ( A =/= 0 -> ( 1 / A ) =/= 0 ) $=
      ( cc0 wne c1 cdiv co cif wceq opreq2 neeq1d ax1cn cc keepel ax1ne0
      elimne0 divne0 dedth ) ACDZEAFGZCDESAEHZFGZCDAEAUAITUBCAUAEFJKEUALSAEMBLN
      OAPQR $.
      $( [14-May-1999] $)
  $}

  $( The reciprocal of a non-zero number is non-zero. $)
  recne0t $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 1 / A ) =/= 0 ) $=
    ( cc wcel cc0 wne c1 cdiv co wi cif wceq neeq1 opreq2 neeq1d imbi12d 0cn
    elimel recne0z dedth imp ) ABCZADEZFAGHZDEZUAUBUDIUAADJZDEZFUEGHZDEZIADAUEK
    ZUBUFUDUHAUEDLUIUCUGDAUEFGMNOUEADBPQRST $.
    $( [9-Mar-2006] $) $( [9-Feb-2006] $)

  ${
    recid.1 $e |- A e. CC $.
    recid.2 $e |- A =/= 0 $.
    $( Multiplication of a number and its reciprocal. $)
    recid $p |- ( A x. ( 1 / A ) ) = 1 $=
      ( c1 ax1cn divcan2 ) ADBECF $.
      $( [9-Feb-1995] $)
  $}

  ${
    recidz.1 $e |- A e. CC $.
    $( Multiplication of a number and its reciprocal. $)
    recidz $p |- ( A =/= 0 -> ( A x. ( 1 / A ) ) = 1 ) $=
      ( c1 ax1cn divcan2z ) ACBDE $.
      $( [14-May-1999] $)
  $}

  $( Multiplication of a number and its reciprocal. $)
  recidt $p |- ( ( A e. CC /\ A =/= 0 ) -> ( A x. ( 1 / A ) ) = 1 ) $=
    ( cc wcel cc0 wne c1 cdiv co cmul wceq wi cif neeq1 id opreq2 opreq12d
    eqeq1d imbi12d 0cn elimel recidz dedth imp ) ABCZADEZAFAGHZIHZFJZUDUEUHKUDA
    DLZDEZUIFUIGHZIHZFJZKADAUIJZUEUJUHUMAUIDMUNUGULFUNAUIUFUKIUNNAUIFGOPQRUIADB
    STUAUBUC $.
    $( [25-Oct-1999] $)

  $( Multiplication of a number and its reciprocal. $)
  recid2t $p |- ( ( A e. CC /\ A =/= 0 ) -> ( ( 1 / A ) x. A ) = 1 ) $=
    ( cc wcel cc0 wne wa c1 cdiv co cmul wceq axmulcom recclt pm3.26 sylanc
    recidt eqtrd ) ABCZADEZFZGAHIZAJIZAUAJIZGUABCRUBUCKTUAALAMRSNOAPQ $.
    $( [22-Jun-2006] $) $( [22-Jun-2006] $)

  ${
    divrec.1 $e |- A e. CC $.
    divrec.2 $e |- B e. CC $.
    ${
      divrec.3 $e |- B =/= 0 $.
      $( Relationship between division and reciprocal.  Theorem I.9 of
         [Apostol] p. 18. $)
      divrec $p |- ( A / B ) = ( A x. ( 1 / B ) ) $=
        ( cdiv co c1 cmul wceq reccl mulcl mulcom mulass ax1cn divcan1 opreq2i
        mulid1 eqtr 3eqtr divmul mpbir ) ABFGAHBFGZIGZJBUDIGZAJUEUDBIGAUCBIGZIG
        ZABUDDAUCCBDEKZLZMAUCBCUHDNUGAHIGAUFHAIBHDOEPQACRSTABUDCDUIEUAUB $.
        $( [9-Feb-1995] $)
    $}

    $( Relationship between division and reciprocal.  Theorem I.9 of
       [Apostol] p. 18. $)
    divrecz $p |- ( B =/= 0 -> ( A / B ) = ( A x. ( 1 / B ) ) ) $=
      ( cc0 wne cdiv co c1 cmul wceq cif opreq2 opreq2d eqeq12d cc ax1cn
      keepel elimne0 divrec dedth ) BEFZABGHZAIBGHZJHZKAUBBILZGHZAIUFGHZJHZKBIB
      UFKZUCUGUEUIBUFAGMUJUDUHAJBUFIGMNOAUFCUBBIPDQRBSTUA $.
      $( [11-Oct-1999] $)
  $}

  $( Relationship between division and reciprocal.  Theorem I.9 of
     [Apostol] p. 18. $)
  divrect $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                ( A / B ) = ( A x. ( 1 / B ) ) ) $=
    ( cc wcel cc0 wne cdiv co c1 cmul wceq wi cif opreq1 eqeq12d imbi2d neeq1
    opreq2 opreq2d imbi12d 0cn elimel divrecz dedth2h 3impia ) ACDZBCDZBEFZABGH
    ZAIBGHZJHZKZUFUGUHULLUHUFAEMZBGHZUMUJJHZKZLUGBEMZEFZUMUQGHZUMIUQGHZJHZKZLAB
    EEAUMKZULUPUHVCUIUNUKUOAUMBGNAUMUJJNOPBUQKZUHURUPVBBUQEQVDUNUSUOVABUQUMGRVD
    UJUTUMJBUQIGRSOTUMUQAECUAUBBECUAUBUCUDUE $.
    $( [4-May-2005] $) $( [2-Aug-2004] $)

  $( Relationship between division and reciprocal. $)
  divrec2t $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                ( A / B ) = ( ( 1 / B ) x. A ) ) $=
    ( cc wcel cc0 wne w3a cdiv co c1 cmul divrect wceq axmulcom 3simp1 recclt
    3adant1 sylanc eqtrd ) ACDZBCDZBEFZGZABHIAJBHIZKIZUDAKIZABLTUDCDZUEUFMUCAUD
    NTUAUBOUAUBUGTBPQRS $.
    $( [1-Mar-2006] $) $( [7-Feb-2006] $)

  $( An associative law for division. $)
  divasst $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ C =/= 0 ) ->
                ( ( A x. B ) / C ) = ( A x. ( B / C ) ) ) $=
    ( cc wcel w3a cc0 wne wa cmul co c1 cdiv wceq id recclt 3anim123i 3exp
    exp4a 3imp1 axmulass syl divrect 3expa axmulcl anim1i 3impa sylan 3adantl1
    opreq2d 3eqtr4d ) ADEZBDEZCDEZFZCGHZIZABJKZLCMKZJKZABUSJKZJKZURCMKZABCMKZJK
    UQULUMUSDEZFZUTVBNULUMUNUPVFULUMUNUPVFULUMUNUPIZVFULULUMUMVGVEULOUMOCPQRSTA
    BUSUAUBURDEZUNIZUPVCUTNZUOVHUNUPVJURCUCUDULUMUNVIULUMIVHUNABUEUFUGUHUQVDVAA
    JUMUNUPVDVANZULUMUNUPVKBCUCUDUIUJUK $.
    $( [4-Aug-2004] $) $( [2-Aug-2004] $)

  $( A commutative/associative law for division. $)
  div23t $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ C =/= 0 ) ->
               ( ( A x. B ) / C ) = ( ( A / C ) x. B ) ) $=
    ( cc wcel w3a cc0 wne wa cmul co cdiv wceq axmulcom 3adant3 adantr opreq1d
    wi divasst ex 3com12 imp 3simp2 divclt 3expa 3adantl2 sylanc 3eqtrd ) ADEZB
    DEZCDEZFZCGHZIZABJKZCLKBAJKZCLKZBACLKZJKZURBJKZUNUOUPCLULUOUPMZUMUIUJVAUKAB
    NOPQULUMUQUSMZUJUIUKUMVBRUJUIUKFUMVBBACSTUAUBUJURDEZUSUTMUNBURNULUJUMUIUJUK
    UCPUIUKUMVCUJUIUKUMVCACUDUEUFUGUH $.
    $( [4-Aug-2004] $) $( [2-Aug-2004] $)

  $( A commutative/associative law for division. $)
  div13t $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ B =/= 0 ) ->
               ( ( A / B ) x. C ) = ( ( C / B ) x. A ) ) $=
    ( cc wcel w3a cc0 wne wa cmul co cdiv wceq axmulcom opreq1d 3adant2 adantr
    wi div23t ex 3com23 imp 3coml 3eqtr3d ) ADEZBDEZCDEZFZBGHZIACJKZBLKZCAJKZBL
    KZABLKCJKZCBLKAJKZUHUKUMMZUIUEUGUPUFUEUGIUJULBLACNOPQUHUIUKUNMZUEUGUFUIUQRU
    EUGUFFUIUQACBSTUAUBUHUIUMUOMZUGUEUFUIURRUGUEUFFUIURCABSTUCUBUD $.
    $( [29-Apr-2005] $) $( [22-Apr-2005] $)

  $( A commutative/associative law for division. $)
  div12t $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ C =/= 0 ) ->
               ( A x. ( B / C ) ) = ( B x. ( A / C ) ) ) $=
    ( cc wcel w3a cc0 wne wa cdiv co cmul wceq axmulcom 3simp1 adantr divclt
    3expa 3adantl1 sylanc wi div13t ex 3comr imp 3adantl2 3simp2 3eqtrd ) ADEZB
    DEZCDEZFZCGHZIZABCJKZLKZUOALKZACJKZBLKZBURLKZUIUODEZUPUQMUNAUONULUIUMUIUJUK
    OPUJUKUMVAUIUJUKUMVABCQRSTULUMUQUSMZUJUKUIUMVBUAUJUKUIFUMVBBCAUBUCUDUEURDEZ
    UJUSUTMUNURBNUIUKUMVCUJUIUKUMVCACQRUFULUJUMUIUJUKUGPTUH $.
    $( [4-Jun-2005] $) $( [30-Apr-2005] $)

  ${
    divass.1 $e |- A e. CC $.
    divass.2 $e |- B e. CC $.
    divass.3 $e |- C e. CC $.
    $( An associative law for division. $)
    divassz $p |- ( C =/= 0 -> ( ( A x. B ) / C ) = ( A x. ( B / C ) ) ) $=
      ( cc wcel w3a cc0 wne cmul co cdiv wceq 3pm3.2i divasst mpan ) AGHZBGHZCG
      HZICJKABLMCNMABCNMLMOSTUADEFPABCQR $.
      $( [12-Aug-1999] $)

    ${
      divass.4 $e |- C =/= 0 $.
      $( An associative law for division. $)
      divass $p |- ( ( A x. B ) / C ) = ( A x. ( B / C ) ) $=
        ( cc0 wne cmul co cdiv wceq divassz ax-mp ) CHIABJKCLKABCLKJKMGABCDEFNO
        $.
        $( [15-Feb-1995] $)

      $( Distribution of division over addition. $)
      divdir $p |- ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) $=
        ( caddc co c1 cdiv cmul reccl adddir addcl divrec opreq12i 3eqtr4 ) ABH
        IZJCKIZLIATLIZBTLIZHISCKIACKIZBCKIZHIABTDECFGMNSCABDEOFGPUCUAUDUBHACDFG
        PBCEFGPQR $.
        $( [16-Feb-1995] $)

      $( A commutative/associative law for division. $)
      div23 $p |- ( ( A x. B ) / C ) = ( ( A / C ) x. B ) $=
        ( cmul co cdiv mulcom opreq1i divass divcl 3eqtr ) ABHIZCJIBAHIZCJIBACJ
        IZHIRBHIPQCJABDEKLBACEDFGMBREACDFGNKO $.
        $( [3-Sep-1999] $)
    $}

    $( Distribution of division over addition. $)
    divdirz $p |- ( C =/= 0 ->
                    ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) ) $=
      ( cc0 wne caddc co cdiv wceq c1 cif opreq2 opreq12d eqeq12d cc ax1cn
      keepel elimne0 divdir dedth ) CGHZABIJZCKJZACKJZBCKJZIJZLUEUDCMNZKJZAUJKJ
      ZBUJKJZIJZLCMCUJLZUFUKUIUNCUJUEKOUOUGULUHUMICUJAKOCUJBKOPQABUJDEUDCMRFSTC
      UAUBUC $.
      $( [2-Aug-2004] $) $( [31-Jul-2004] $)
  $}

  $( Distribution of division over addition. $)
  divdirt $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ C =/= 0 ) ->
                  ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) ) $=
    ( cc wcel w3a cc0 wne caddc co cdiv wceq wi cif opreq1 opreq1d eqeq12d
    imbi2d opreq2 opreq2d neeq1 opreq12d imbi12d 0cn elimel divdirz dedth3h
    imp ) ADEZBDEZCDEZFCGHZABIJZCKJZACKJZBCKJZIJZLZUIUJUKULURMULUIAGNZBIJZCKJZU
    SCKJZUPIJZLZMABCGGGULUSUJBGNZIJZCKJZVBVECKJZIJZLZMUKCGNZGHZVFVKKJZUSVKKJZVE
    VKKJZIJZLZMAUSLZURVDULVRUNVAUQVCVRUMUTCKAUSBIOPVRUOVBUPIAUSCKOPQRBVELZVDVJU
    LVSVAVGVCVIVSUTVFCKBVEUSISPVSUPVHVBIBVECKOTQRCVKLZULVLVJVQCVKGUAVTVGVMVIVPC
    VKVFKSVTVBVNVHVOICVKUSKSCVKVEKSUBQUCUSVEVKAGDUDUEBGDUDUECGDUDUEUFUGUH $.
    $( [2-Aug-2004] $) $( [31-Jul-2004] $)

  ${
    divcan3.1 $e |- A e. CC $.
    divcan3.2 $e |- B e. CC $.
    ${
      divcan3.3 $e |- A =/= 0 $.
      $( A cancellation law for division. $)
      divcan3 $p |- ( ( A x. B ) / A ) = B $=
        ( cmul co cdiv divass divcan2 eqtr ) ABFGAHGABAHGFGBABACDCEIABCDEJK $.
        $( [16-Feb-1995] $)

      $( A cancellation law for division. $)
      divcan4 $p |- ( ( B x. A ) / A ) = B $=
        ( cmul co cdiv mulcom opreq1i divcan3 eqtr ) BAFGZAHGABFGZAHGBMNAHBADCI
        JABCDEKL $.
        $( [18-May-1999] $)
    $}

    $( A cancellation law for division.  (Eliminates a hypothesis of
       ~ divcan3 with the weak deduction theorem.) $)
    divcan3z $p |- ( A =/= 0 -> ( ( A x. B ) / A ) = B ) $=
      ( cc0 wne cmul co cdiv wceq c1 cif opreq1 id opreq12d eqeq1d cc ax1cn
      keepel elimne0 divcan3 dedth ) AEFZABGHZAIHZBJUCAKLZBGHZUFIHZBJAKAUFJZUEU
      HBUIUDUGAUFIAUFBGMUINOPUFBUCAKQCRSDATUAUB $.
      $( [4-Feb-2004] $) $( [3-Feb-2004] $)

    $( A cancellation law for division. $)
    divcan4z $p |- ( A =/= 0 -> ( ( B x. A ) / A ) = B ) $=
      ( cc0 wne cmul co cdiv divcan3z mulcom opreq1i syl5eq ) AEFABGHZAIHBBAGHZ
      AIHABCDJONAIBADCKLM $.
      $( [12-Oct-1999] $)

  $}

  $( A cancellation law for division. $)
  divcan3t $p |- ( ( A e. CC /\ B e. CC /\ A =/= 0 ) ->
                 ( ( A x. B ) / A ) = B ) $=
    ( cc wcel cc0 wne cmul co cdiv wceq wi cif neeq1 opreq1 id opreq12d eqeq1d
    imbi12d opreq2 opreq1d eqeq12d imbi2d 0cn elimel divcan3z dedth2h 3impia )
    ACDZBCDZAEFZABGHZAIHZBJZUHUIUJUMKUHAELZEFZUNBGHZUNIHZBJZKUOUNUIBELZGHZUNIHZ
    USJZKABEEAUNJZUJUOUMURAUNEMVCULUQBVCUKUPAUNIAUNBGNVCOPQRBUSJZURVBUOVDUQVABU
    SVDUPUTUNIBUSUNGSTVDOUAUBUNUSAECUCUDBECUCUDUEUFUG $.
    $( [6-May-2005] $) $( [3-Feb-2004] $)

  $( A cancellation law for division. $)
  divcan4t $p |- ( ( A e. CC /\ B e. CC /\ A =/= 0 ) ->
                 ( ( B x. A ) / A ) = B ) $=
    ( cc wcel cc0 wne w3a cmul co cdiv wceq wa axmulcom opreq1d 3adant3
    divcan3t eqtr3d ) ACDZBCDZAEFZGABHIZAJIZBAHIZAJIZBRSUBUDKTRSLUAUCAJABMNOABP
    Q $.
    $( [6-May-2005] $) $( [8-Feb-2005] $)

  ${
    div11.1 $e |- A e. CC $.
    div11.2 $e |- B e. CC $.
    div11.3 $e |- C e. CC $.
    div11.4 $e |- C =/= 0 $.
    $( One-to-one relationship for division. $)
    div11 $p |- ( ( A / C ) = ( B / C ) <-> A = B ) $=
      ( cdiv co wceq cmul divcl mulcan divcan2 eqeq12i bitr3 ) ACHIZBCHIZJCQKIZ
      CRKIZJABJCQRFACDFGLBCEFGLGMSATBCAFDGNCBFEGNOP $.
      $( [20-Aug-2001] $)
  $}

  $( One-to-one relationship for division. $)
  div11t $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
               ( ( A / C ) = ( B / C ) <-> A = B ) ) $=
    ( cc wcel cc0 wne wa cdiv co wceq wb c1 cif opreq1 eqeq1d eqeq1 bibi12d
    eqeq2d eqeq2 opreq2 eqeq12d bibi1d ax1cn elimel eleq1 neeq1 anbi12d ax1ne0
    pm3.2i elimhyp pm3.26i pm3.27i div11 dedth3h ) ADEZBDEZCDEZCFGZHZACIJZBCIJZ
    KZABKZLUPAMNZCIJZVBKZVEBKZLABCMMMVFUQBMNZCIJZKZVEVIKZLVEUTCMNZIJZVIVMIJZKZV
    LLAVEKZVCVGVDVHVQVAVFVBAVECIOPAVEBQRBVIKZVGVKVHVLVRVBVJVFBVICIOSBVIVETRCVMK
    ZVKVPVLVSVFVNVJVOCVMVEIUACVMVIIUAUBUCVEVIVMAMDUDUEBMDUDUEVMDEZVMFGZUTVTWAHM
    DEZMFGZHCMVSURVTUSWACVMDUFCVMFUGUHMVMKWBVTWCWAMVMDUFMVMFUGUHWBWCUDUIUJUKZUL
    VTWAWDUMUNUO $.
    $( [1-May-2006] $) $( [20-Apr-2006] $)

  $( A number divided by itself is one. $)
  dividt $p |- ( ( A e. CC /\ A =/= 0 ) -> ( A / A ) = 1 ) $=
    ( cc wcel cc0 wne wa cdiv co c1 cmul wceq divrect 3expa anabsan recidt
    eqtrd ) ABCZADEZFAAGHZAIAGHJHZIQRSTKZQQRUAAALMNAOP $.
    $( [2-Aug-2004] $) $( [1-Aug-2004] $)

  $( Division into zero is zero. $)
  div0t $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 0 / A ) = 0 ) $=
    ( cc wcel cc0 wne wa cdiv co c1 cmul wceq 0cn divrect mp3an1 recclt mul02t
    syl eqtrd ) ABCZADEZFZDAGHZDIAGHZJHZDDBCSTUBUDKLDAMNUAUCBCUDDKAOUCPQR $.
    $( [16-Mar-2005] $) $( [14-Mar-2005] $)

  $( A ratio is zero iff the numerator is zero. $)
  diveq0t $p |- ( ( A e. CC /\ C e. CC /\ C =/= 0 ) ->
               ( ( A / C ) = 0 <-> A = 0 ) ) $=
    ( cc wcel cc0 wne w3a cdiv co wceq wb wa div0t eqeq2d 3adant1 0cn div11t
    mp3an2 3impb bitr3d ) ACDZBCDZBEFZGABHIZEBHIZJZUDEJZAEJZUBUCUFUGKUAUBUCLZUE
    EUDBMNOUAUBUCUFUHKZUAECDUIUJPAEBQRST $.
    $( [15-May-2006] $) $( [20-Apr-2006] $)

  ${
    recrec.1 $e |- A e. CC $.
    recrec.2 $e |- A =/= 0 $.
    $( A number is equal to the reciprocal of its reciprocal.  Theorem I.10 of
       [Apostol] p. 18. $)
    recrec $p |- ( 1 / ( 1 / A ) ) = A $=
      ( c1 cdiv co cmul reccl ax1cn ax1ne0 divne0 recid opreq2i opreq1i mulass
      mulid2 3eqtr3 mulid1 ) ADAEFZDSEFZGFZGFZADGFTAUADAGSABCHZDAIBJCKZLMASGFZT
      GFDTGFUBTUEDTGABCLNASTBUCSUCUDHZOTUFPQABRQ $.
      $( [9-Feb-1995] $)

    $( A number divided by itself is one. $)
    divid $p |- ( A / A ) = 1 $=
      ( cdiv co c1 cmul divrec recid eqtr ) AADEAFADEGEFAABBCHABCIJ $.
      $( [9-Feb-1995] $)

    $( Division into zero is zero. $)
    div0 $p |- ( 0 / A ) = 0 $=
      ( cc wcel cc0 wne cdiv co wceq div0t mp2an ) ADEAFGFAHIFJBCAKL $.
      $( [12-Aug-1999] $)
  $}

  ${
    div1.1 $e |- A e. CC $.
    $( A number divided by 1 is itself. $)
    div1 $p |- ( A / 1 ) = A $=
      ( c1 cdiv co wceq cmul mulid2 ax1cn ax1ne0 divmul mpbir ) ACDEAFCAGEAFABH
      ACABIBJKL $.
      $( [9-Jan-2002] $)
  $}

  $( A number divided by 1 is itself. $)
  div1t $p |- ( A e. CC -> ( A / 1 ) = A ) $=
    ( cc wcel c1 cdiv co wceq cif opreq1 id eqeq12d ax1cn elimel div1 dedth )
    ABCZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRADBLMNO $.
    $( [9-Jan-2002] $)

  $( Move negative sign inside of a division. $)
  divnegt $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                -u ( A / B ) = ( -u A / B ) ) $=
    ( cc wcel cc0 wne w3a cneg c1 cdiv co cmul wceq wa mulneg1t recclt sylan2
    3impb divrect negclt syl3an1 negeqd 3eqtr4rd ) ACDZBCDZBEFZGZAHZIBJKZLKZAUI
    LKZHZUHBJKZABJKZHUDUEUFUJULMZUDUICDUOUEUFNAUIOBPQRUHCDUEUFUMUJMUDUHBSATUAUG
    UNUKABSUBUC $.
    $( [3-Jun-2005] $) $( [17-Sep-2004] $)

  $( Distribution of division over subtraction. $)
  divsubdirt $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ C =/= 0 ) ->
                  ( ( A - B ) / C ) = ( ( A / C ) - ( B / C ) ) ) $=
    ( cc wcel w3a cc0 wne wa cneg caddc co cdiv cmin wceq divdirt negclt
    syl3anl2 negsubt 3adant3 opreq1d adantr divnegt 3expa 3adantl1 opreq2d
    divclt 3adantl2 sylanc eqtr3d 3eqtr3d ) ADEZBDEZCDEZFZCGHZIZABJZKLZCMLZACML
    ZURCMLZKLZABNLZCMLZVABCMLZNLZULURDEUNUPUTVCOUMAURCPBQRUOUTVEOUPUOUSVDCMULUM
    USVDOUNABSTUAUBUQVAVFJZKLZVCVGUQVHVBVAKUMUNUPVHVBOZULUMUNUPVJBCUCUDUEUFVADE
    ZVFDEZVIVGOUQVAVFSULUNUPVKUMULUNUPVKACUGUDUHUMUNUPVLULUMUNUPVLBCUGUDUEUIUJU
    K $.
    $( [1-Aug-2006] $) $( [4-Mar-2005] $)

  $( A number is equal to the reciprocal of its reciprocal.  Theorem I.10 of
     [Apostol] p. 18. $)
  recrect $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 1 / ( 1 / A ) ) = A ) $=
    ( cc wcel cc0 wne wa c1 cdiv co wceq cif opreq2 opreq2d id eqeq12d eleq1
    neeq1 anbi12d ax1cn ax1ne0 pm3.2i elimhyp pm3.26i pm3.27i recrec dedth ) AB
    CZADEZFZGGAHIZHIZAJGGUIAGKZHIZHIZULJAGAULJZUKUNAULUOUJUMGHAULGHLMUONOULULBC
    ZULDEZUIUPUQFGBCZGDEZFAGUOUGUPUHUQAULBPAULDQRGULJURUPUSUQGULBPGULDQRURUSSTU
    AUBZUCUPUQUTUDUEUF $.
    $( [26-Sep-1999] $)

  ${
    rec11.1 $e |- A e. CC $.
    rec11.2 $e |- B e. CC $.
    ${
      rec11i.3 $e |- A =/= 0 $.
      rec11i.4 $e |- B =/= 0 $.
      $( Reciprocal is one-to-one. $)
      rec11i $p |- ( ( 1 / A ) = ( 1 / B ) <-> A = B ) $=
        ( c1 cdiv co wceq cmul opreq2 reccl mul23 recid opreq1i mulid2 3eqtr
        mulass opreq2i mulid1 eqeq12i eqcom bitr sylib impbi ) GAHIZGBHIZJZABJZ
        UIABKIZUGKIZUKUHKIZJZUJUGUHUKKLUNBAJUJULBUMAULAUGKIZBKIGBKIBABUGCDACEMN
        UOGBKACEOPBDQRUMABUHKIZKIAGKIAABUHCDBDFMSUPGAKBDFOTACUARUBBAUCUDUEABGHL
        UF $.
        $( [16-Sep-1999] $)
    $}

    $( Reciprocal is one-to-one. $)
    rec11 $p |- ( ( A =/= 0 /\ B =/= 0 ) ->
               ( ( 1 / A ) = ( 1 / B ) <-> A = B ) ) $=
      ( cc0 wne c1 cdiv co wceq wb cif opreq2 eqeq1d eqeq1 bibi12d eqeq2d
      eqeq2 cc ax1cn keepel elimne0 rec11i dedth2h ) AEFZBEFZGAHIZGBHIZJZABJZKG
      UEAGLZHIZUHJZUKBJZKULGUFBGLZHIZJZUKUOJZKABGGAUKJZUIUMUJUNUSUGULUHAUKGHMNA
      UKBOPBUOJZUMUQUNURUTUHUPULBUOGHMQBUOUKRPUKUOUEAGSCTUAUFBGSDTUAAUBBUBUCUD
      $.
      $( [16-Sep-1999] $)
  $}

  $( Multiplication of two ratios.  Theorem I.14 of [Apostol] p. 18. $)
  divmuldivt $p |- ( ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) /\
                   ( B =/= 0 /\ D =/= 0 ) ) ->
                  ( ( A / B ) x. ( C / D ) ) = ( ( A x. C ) / ( B x. D ) ) ) $=
    ( cc wcel wa cc0 wne cmul co cdiv wceq divcan3t axmulcl ad2ant2l adantr
    divclt 3expa syl2an an4s wi muln0bt biimpd imp syl3anc mul4t w3a 3simp2
    jca simplr divcan2t 3exp impcom 3com12 opreqan12d eqtr3d opreq1d ) AEFZBEFZ
    GZCEFZDEFZGZGZBHIZDHIZGZGZBDJKZABLKZCDLKZJKZJKZVJLKZVMACJKZVJLKVJEFZVMEFZVJ
    HIZVOVMMVIVJVMNVEVQVHUTVCVQUSVBBDOPQVAVFVDVGVRVKEFZVLEFZVRVAVFGZVDVGGZVKVLO
    USUTVFVTABRZSVBVCVGWACDRSZTUAVEVHVSUTVCVHVSUBUSVBUTVCGVHVSBDUCUDPUEUFVIVNVP
    VJLVAVFVDVGVNVPMWBWCGBVKJKZDVLJKZJKZVNVPUTVTGZVCWAGWHVNMWBWCBVKDVLUGUSUTVFW
    IUSUTVFUHUTVTUSUTVFUIWDUJSWCVCWAVBVCVGUKWEUJTWBWCWFAWGCJVAVFWFAMZUTUSVFWJUB
    UTUSVFWJBAULUMUNUEVBVCVGWGCMZVCVBVGWKDCULUOSUPUQUAURUQ $.
    $( [2-Aug-2004] $) $( [1-Aug-2004] $)

  $( Cancellation of common factor in a ratio. $)
  divcan5t $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) /\
       ( C e. CC /\ C =/= 0 ) ) -> ( ( C x. A ) / ( C x. B ) ) = ( A / B ) ) $=
    ( cc wcel cc0 wne wa w3a cdiv co cmul c1 wceq dividt opreq1d 3ad2ant3
    divmuldivt pm3.26 jca anim2i anim12i 3impb 3coml pm3.27 ancoms 3adant1
    sylanc divclt 3expb mulid2t syl 3adant3 3eqtr3d ) ADEZBDEZBFGZHZCDEZCFGZHZI
    ZCCJKZABJKZLKZMVDLKZCALKCBLKJKZVDVAUOVEVFNURVAVCMVDLCOPQUSUSHZUOUPHZHZUTUQH
    ZVEVGNVBCCABRVAUOURVJVAUOURVJVAVHUOURHZVIVAUSUSUSUTSZVMTURUPUOUPUQSUAUBUCUD
    URVAVKUOVAURVKVAUTURUQUSUTUEUPUQUEUBUFUGUHUOURVFVDNZVAVLVDDEZVNUOUPUQVOABUI
    UJVDUKULUMUN $.
    $( [13-Jan-2006] $) $( [9-Jan-2006] $)

  $( Swap the denominators in the product of two ratios. $)
  divmul13t $p |- ( ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) /\
                   ( B =/= 0 /\ D =/= 0 ) ) -> ( ( A / B ) x. ( C / D ) ) =
                   ( ( C / B ) x. ( A / D ) ) ) $=
    ( cc wcel wa cc0 wne cmul co cdiv wceq axmulcom ad2ant2r opreq1d adantr
    divmuldivt ancom anbi1i an42 3bitr sylanb 3eqtr4d ) AEFZBEFZGZCEFZDEFZGZGZB
    HIDHIGZGACJKZBDJKZLKZCAJKZUNLKZABLKCDLKJKCBLKADLKJKZUKUOUQMULUKUMUPUNLUEUHU
    MUPMUFUIACNOPQABCDRUHUFGUEUIGGZULURUQMUKCBADRUKUFUEGZUJGUJUTGUSUGUTUJUEUFST
    UTUJSUHUIUFUEUAUBUCUD $.
    $( [9-Jun-2005] $) $( [3-May-2005] $)

  $( Swap the numerators in the product of two ratios. $)
  divmul24t $p |- ( ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) /\
                   ( B =/= 0 /\ D =/= 0 ) ) -> ( ( A / B ) x. ( C / D ) ) =
                   ( ( A / D ) x. ( C / B ) ) ) $=
    ( cc wcel wa cc0 wne cmul co cdiv wceq axmulcom ad2ant2l opreq2d adantr
    divmuldivt an42 an4 bitr ancom syl2anb 3eqtr4d ) AEFZBEFZGCEFZDEFZGGZBHIZDH
    IZGZGACJKZBDJKZLKZUMDBJKZLKZABLKCDLKJKADLKCBLKJKZUIUOUQMULUIUNUPUMLUFUHUNUP
    MUEUGBDNOPQABCDRUEUHGUGUFGGZUKUJGURUQMUIULADCBRUIUEUGGUHUFGGUSUEUFUGUHSUEUG
    UHUFTUAUJUKUBUCUD $.
    $( [6-May-2005] $) $( [3-May-2005] $)

  $( Addition of two ratios.  Theorem I.13 of [Apostol] p. 18. $)
  divadddivt $p |- ( ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) /\
                   ( B =/= 0 /\ D =/= 0 ) ) ->
  ( ( A / B ) + ( C / D ) ) = ( ( ( A x. D ) + ( B x. C ) ) / ( B x. D ) ) ) $=
    ( cc wcel wa cc0 wne cmul co caddc cdiv wceq wb muln0bt ad2ant2l wi w3a
    divdirt ex axmulcl adantrl adantlr adantrr adantll syl3anc sylbid imp c1
    dividt opreq2d divmuldivt pm3.27 jca sylanl2 divclt 3expa ax1id syl
    ad2ant2r 3eqtr3d opreq1d sylanl1 mulid2t opreq12d eqtr2d ) AEFZBEFZGZCEFZDE
    FZGZGZBHIZDHIZGZGZADJKZBCJKZLKBDJKZMKZVSWAMKZVTWAMKZLKZABMKZCDMKZLKVNVQWBWE
    NZVNVQWAHIZWHVIVLVQWIOVHVKBDPQVSEFZVTEFZWAEFZWIWHRVNWJWKWLSWIWHVSVTWATUAVHV
    MWJVIVHVLWJVKADUBUCUDVIVMWKVHVIVKWKVLBCUBUEUFVIVLWLVHVKBDUBQUGUHUIVRWCWFWDW
    GLVRWFDDMKZJKZWFUJJKZWCWFVRWMUJWFJVMVQWMUJNZVJVLVPWPVKVODUKQUFULVJVLVLGVQWN
    WCNVMABDDUMVMVLVLVKVLUNZWQUOUPVJVOWOWFNZVMVPVJVOGWFEFZWRVHVIVOWSABUQURWFUSU
    TVAVBVRBBMKZWGJKZUJWGJKZWDWGVRWTUJWGJVJVQWTUJNZVMVIVQXCVHVIVOXCVPBUKUEUFUDV
    CVIVIGVMVQXAWDNVJBBCDUMVJVIVIVHVIUNZXDUOVDVMVPXBWGNZVJVOVMVPGWGEFZXEVKVLVPX
    FCDUQURWGVEUTQVBVFVG $.
    $( [2-Aug-2004] $) $( [1-Aug-2004] $)

  $( Division of two ratios.  Theorem I.15 of [Apostol] p. 18. $)
  divdivdivt $p |- ( ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) /\
                   ( B =/= 0 /\ C =/= 0 /\ D =/= 0 ) ) ->
                   ( ( A / B ) / ( C / D ) ) = ( ( A x. D ) / ( B x. C ) ) ) $=
    ( cc wcel wa cc0 wne cdiv co cmul wceq w3a axmulcom divclt 3com12 3expa
    adantrr ad2ant2l ad2ant2r sylanc divmuldivt pm3.22 anim2i pm3.26 syl2an
    eqtrd opreq2d c1 ancli adantl anim12i adantll syl ad2antlr opreq1d dividt
    axmulcl ancoms wb muln0bt ancom syl5bb biimpa 3eqtrd axmulass adantrl
    syl3anc mulid2t 3eqtr3d eqtr3d divmult adantlr adantr wi biimpd sylan2i
    imp 3jca divne0bt 3exp com34 imp43 mpbird 3anass sylan2b ) AEFZBEFZGZCEFZDE
    FZGZGZBHIZCHIZDHIZGZGZABJKZCDJKZJKADLKZBCLKZJKZMZWOWPWQNWNWSGZXEXAXDLKZWTMZ
    XFXADCJKZWTLKZLKZXGWTXFXJXDXALXFXJWTXILKZXDXIEFZWTEFZXJXLMXFXIWTOWMWRXMWJWO
    WMWPXMWQWKWLWPXMWLWKWPXMDCPQRSTZWJWOXNWMWRWHWIWOXNABPRUAZUBWJWLWKGZGWOWPGZX
    LXDMWNWSABDCUCWMXQWJWKWLUDZUEWRWPWOWPWQUFZUEUGUHUIXFXAXILKZWTLKZUJWTLKZXKWT
    XFYAUJWTLXFYACDLKZDCLKZJKZYEYEJKZUJXFWMXQGZWQWPGZGZYAYFMWMWSYJWJWMYHWSYIWMX
    QXSUKWRYIWOWPWQUDULUMUNCDDCUCUOXFYDYEYEJWMYDYEMWJWSCDOUPUQYEEFZYEHIZYGUJMXF
    YEURWMYKWJWSWLWKYKDCUSUTUPWMWRYLWJWOWMWRYLWLWKWRYLVAXQYIYLWRDCVBWPWQVCVDUTV
    ETUBVFUQXAEFZXMXNYBXKMXFXAXIWTVGWMWRYMWJWOWMWQYMWPWKWLWQYMCDPRVHTZXOXPVIXFX
    NYCWTMXPWTVJUOVKVLXNYMXDEFZNXAHIZXEXHVAXFWTXAXDVMXFXNYMYOXPYNXBEFZXCEFZXCHI
    ZYOXFXBXCPWNYQWSWHWMYQWIWHWLYQWKADUSVHVNVOWNYRWSWIWMYRWHWIWKYRWLBCUSSUNVOWN
    WSYSWIWMWSYSVPZWHWIWKYTWLWIWKGZWOWPYSWRUUAXRYSBCVBVQXTVRSUNVSVIVTWMWRYPWJWO
    WKWLWPWQYPWKWLWQWPYPWKWLWQWPYPVPWKWLWQNWPYPCDWAVQWBWCWDTUBWEWOWPWQWFWG $.
    $( [4-Aug-2004] $) $( [2-Aug-2004] $)

  ${
    divmuldiv.1 $e |- A e. CC $.
    divmuldiv.2 $e |- B e. CC $.
    divmuldiv.3 $e |- C e. CC $.
    divmuldiv.4 $e |- D e. CC $.
    divmuldiv.5 $e |- B =/= 0 $.
    divmuldiv.6 $e |- D =/= 0 $.
    $( Multiplication of two ratios.  Theorem I.14 of [Apostol] p. 18. $)
    divmuldiv $p |- ( ( A / B ) x. ( C / D ) ) =
                      ( ( A x. C ) / ( B x. D ) ) $=
      ( cc wcel wa cc0 wne cdiv co cmul wceq pm3.2i divmuldivt mp2an ) AKLZBKLZ
      MZCKLZDKLZMZMBNOZDNOZMABPQCDPQRQACRQBDRQPQSUEUHUCUDEFTUFUGGHTTUIUJIJTABCD
      UAUB $.
      $( [16-Feb-1995] $)

    $( Swap denominators of two ratios. $)
    divmul13 $p |- ( ( A / B ) x. ( C / D ) ) =
                   ( ( C / B ) x. ( A / D ) ) $=
      ( cmul co cdiv mulcom opreq1i divmuldiv 3eqtr4r ) CAKLZBDKLZMLACKLZSMLCBM
      LADMLKLABMLCDMLKLRTSMCAGENOCBADGFEHIJPABCDEFGHIJPQ $.
      $( [6-Aug-1999] $)

    $( Addition of two ratios.  Theorem I.13 of [Apostol] p. 18. $)
    divadddiv $p |- ( ( A / B ) + ( C / D ) ) =
                      ( ( ( A x. D ) + ( B x. C ) ) / ( B x. D ) ) $=
      ( cc wcel wa cc0 wne cdiv co caddc cmul wceq pm3.2i divadddivt mp2an ) AK
      LZBKLZMZCKLZDKLZMZMBNOZDNOZMABPQCDPQRQADSQBCSQRQBDSQPQTUFUIUDUEEFUAUGUHGH
      UAUAUJUKIJUAABCDUBUC $.
      $( [21-Feb-1995] $)

    ${
      divdivdiv.7 $e |- C =/= 0 $.
      $( Division of two ratios.  Theorem I.15 of [Apostol] p. 18. $)
      divdivdiv $p |- ( ( A / B ) / ( C / D ) )
                        = ( ( A x. D ) / ( B x. C ) ) $=
        ( cc wcel wa cc0 wne w3a cdiv co cmul wceq pm3.2i 3pm3.2i divdivdivt
        mp2an ) ALMZBLMZNZCLMZDLMZNZNBOPZCOPZDOPZQABRSCDRSRSADTSBCTSRSUAUHUKUFU
        GEFUBUIUJGHUBUBULUMUNIKJUCABCDUDUE $.
        $( [22-Feb-1995] $)
    $}
  $}

  $( The reciprocal of a ratio. $)
  recdivt $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( A =/= 0 /\ B =/= 0 ) ) ->
                ( 1 / ( A / B ) ) = ( B / A ) ) $=
    ( cc wcel wa cc0 wne c1 cdiv co cmul wceq ax1cn pm3.2i w3a divdivdivt
    ax1ne0 jctl 3anass sylibr sylan2 mpanl1 div1 opreq1i a1i mulid2t
    opreqan12rd adantr 3eqtr3d ) ACDZBCDZEZAFGZBFGZEZEZHHIJZABIJZIJZHBKJZHAKJZI
    JZHURIJZBAIJZHCDZVEEZULUOUSVBLZVEVEMMNVFULEHFGZUMUNOZVGUOHHABPUOVHUOEVIUOVH
    QRVHUMUNSTUAUBUSVCLUPUQHURIHMUCUDUEULVBVDLUOUKUJUTBVAAIBUFAUFUGUHUI $.
    $( [29-Aug-2004] $) $( [3-Aug-2004] $)

  $( Swap denominators in a division. $)
  divdiv23t $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\
         ( B =/= 0 /\ C =/= 0 ) ) -> ( ( A / B ) / C ) = ( ( A / C ) / B ) ) $=
    ( cc wcel w3a cc0 wne wa c1 cdiv co cmul wceq div23t 3simp1 adantr recclt
    3simp2 sylan adantrr 3simp3 3jca simprr sylanc divrect 3expa 3adantl3
    opreq1d divclt 3adantl2 adantrl simprl syl3anc 3eqtr4d ) ADEZBDEZCDEZFZBGHZ
    CGHZIZIZAJBKLZMLZCKLZACKLZVDMLZABKLZCKLVGBKLZUPVDDEZURFVAVFVHNVCAVDCOVCUPVK
    URUSUPVBUPUQURPQUSUTVKVAUQUTVKUSBRUPUQURSZTUAUSURVBUPUQURUBQUCUSUTVAUDUEVCV
    IVECKUSUTVIVENZVAUPUQUTVMURUPUQUTVMABUFUGUHUAUIVGDEZUQUTVJVHNVCVGBUFUSVAVNU
    TUPURVAVNUQUPURVAVNACUJUGUKULUSUQVBVLQUSUTVAUMUNUO $.
    $( [4-Aug-2004] $) $( [2-Aug-2004] $)

  ${
    divdiv23.1 $e |- A e. CC $.
    divdiv23.2 $e |- B e. CC $.
    divdiv23.3 $e |- C e. CC $.
    ${
      divdiv23.4 $e |- B =/= 0 $.
      divdiv23.5 $e |- C =/= 0 $.
      $( Swap denominators in a division. $)
      divdiv23 $p |- ( ( A / B ) / C ) = ( ( A / C ) / B ) $=
        ( c1 cdiv co cmul reccl div23 divrec opreq1i divcl 3eqtr4 ) AIBJKZLKZCJ
        KACJKZSLKABJKZCJKUABJKASCDBEGMFHNUBTCJABDEGOPUABACDFHQEGOR $.
        $( [15-Sep-1999] $)
    $}
    $( Swap denominators in a division. $)
    divdiv23z $p |- ( ( B =/= 0 /\ C =/= 0 ) ->
                ( ( A / B ) / C ) = ( ( A / C ) / B ) ) $=
      ( cc wcel w3a cc0 wne wa cdiv co wceq 3pm3.2i divdiv23t mpan ) AGHZBGHZCG
      HZIBJKCJKLABMNCMNACMNBMNOSTUADEFPABCQR $.
      $( [15-Sep-1999] $)
  $}

  $( Two numbers whose reciprocals add to 1 are called "conjugates" and satisfy
     this relationship.  Equation 5 of [Kreyszig] p. 12. $)
  conjmult $p |- ( ( ( P e. CC /\ P =/= 0 ) /\ ( Q e. CC /\ Q =/= 0 ) ) ->
      ( ( ( 1 / P ) + ( 1 / Q ) ) = 1 <-> ( ( P - 1 ) x. ( Q - 1 ) ) = 1 ) ) $=
    ( cc wcel cc0 wne wa cmul co c1 cdiv caddc wceq cmin mul23t simpll simprl
    recclt adantr syl3anc recidt opreq1d mulid2t ad2antrl 3eqtrd axmulass
    adantl opreq2d ax1id ad2antrr opreq12d axdistr axmulcl ad2ant2r axaddcom
    3eqtr4d syl eqeq12d wb ax1cn mulcant mp3anl3 axaddcl syl2an jca muln0bt
    biimpa an4s sylanc muleqaddt eqcom syl5bb 3bitr3d ) ACDZAEFZGZBCDZBEFZGZGZA
    BHIZJAKIZJBKIZLIZHIZWAJHIZMZABLIZWAMZWDJMZAJNIBJNIHIJMZVTWEWHWFWAVTWAWBHIZW
    AWCHIZLIZBALIZWEWHVTWLBWMALVTWLAWBHIZBHIZJBHIZBVNVQWBCDZWLWQMVTABWBOVNVOVSP
    ZVPVQVRQZVPWSVSARZSZTVPWQWRMVSVPWPJBHAUAUBSVQWRBMVPVRBUCUDUEVTWMABWCHIZHIZA
    JHIZAVNVQWCCDZWMXEMVTABWCUFWTXAVSXGVPBRZUGZTVSXEXFMVPVSXDJAHBUAUHUGVNXFAMVO
    VSAUIUJUEUKWACDZWSXGWEWNMVTWAWBWCULVNVQXJVOVRABUMZUNZXCXITVNVQWHWOMVOVRABUO
    UNUPVNVQWFWAMZVOVRVNVQGZXJXMXKWAUIUQUNURXJWDCDZGWAEFZWGWJUSZVTXJXOJCDXPXQUT
    WAWDJVAVBVTXJXOXLWSXGXOVPVSWBWCVCXBXHVDVEVNVQVOVRXPXNVOVRGXPABVFVGVHVIVNVQW
    IWKUSVOVRXNWAWHMWKWIABVJWHWAVKVLUNVM $.
    $( [13-Mar-2007] $) $( [12-Nov-2006] $)

  ${
    $d x y B $.
    redivcl.1 $e |- A e. RR $.
    redivcl.2 $e |- B e. RR $.
    redivcl.3 $e |- B =/= 0 $.
    $( Closure law for division of reals. $)
    redivcl $p |- ( A / B ) e. RR $=
      ( vx cdiv co c1 cmul cr recn divrec cv wceq wrex wcel cc0 wne axrrecex
      mp2an wa wex df-rex cc wb recnt cif eqeq2 opreq2 eqeq1d bibi12d ax1cn
      elimel divmul dedth syl eleq1a sylbird imp 19.23aiv sylbi ax-mp remulcl
      eqeltr ) ABGHAIBGHZJHKABACLBDLZEMAVFCBFNZJHZIOZFKPZVFKQZBKQBRSVKDEFBTUAVK
      VHKQZVJUBZFUCVLVJFKUDVNVLFVMVJVLVMVJVFVHOZVLVMVHUEQZVOVJUFZVHUGVPVQVFVPVH
      IUHZOZBVRJHZIOZUFVHIVHVROZVOVSVJWAVHVRVFUIWBVIVTIVHVRBJUJUKULIBVRUMVGVHIU
      EUMUNEUOUPUQVHKVFURUSUTVAVBVCVDVE $.
      $( [9-May-1999] $)
  $}

  ${
    redivclz.1 $e |- A e. RR $.
    redivclz.2 $e |- B e. RR $.
    $( Closure law for division of reals. $)
    redivclz $p |- ( B =/= 0 -> ( A / B ) e. RR ) $=
      ( cc0 wne cdiv co cr wcel c1 cif wceq opreq2 eleq1d 1re keepel elimne0
      redivcl dedth ) BEFZABGHZIJAUABKLZGHZIJBKBUCMUBUDIBUCAGNOAUCCUABKIDPQBRST
      $.
      $( [9-May-1999] $)
  $}

  $( Closure law for division of reals. $)
  redivclt $p |- ( ( A e. RR /\ B e. RR /\ B =/= 0 ) -> ( A / B ) e. RR ) $=
    ( cr wcel cc0 wne cdiv co wi cif wceq opreq1 eleq1d imbi2d neeq1 opreq2
    imbi12d 0re elimel redivclz dedth2h 3impia ) ACDZBCDZBEFZABGHZCDZUCUDUEUGIU
    EUCAEJZBGHZCDZIUDBEJZEFZUHUKGHZCDZIABEEAUHKZUGUJUEUOUFUICAUHBGLMNBUKKZUEULU
    JUNBUKEOUPUIUMCBUKUHGPMQUHUKAECRSBECRSTUAUB $.
    $( [4-May-2005] $) $( [27-Sep-1999] $)

  ${
    rereccl.1 $e |- A e. RR $.
    rereccl.2 $e |- A =/= 0 $.
    $( Closure law for reciprocal. $)
    rereccl $p |- ( 1 / A ) e. RR $=
      ( c1 1re redivcl ) DAEBCF $.
      $( [2-May-2005] $) $( [30-Apr-2005] $)
  $}

  ${
    rerecclz.1 $e |- A e. RR $.
    $( Closure law for reciprocal. $)
    rerecclz $p |- ( A =/= 0 -> ( 1 / A ) e. RR ) $=
      ( c1 1re redivclz ) CADBE $.
      $( [2-May-2005] $) $( [30-Apr-2005] $)
  $}

  $( Closure law for reciprocal. $)
  rerecclt $p |- ( ( A e. RR /\ A =/= 0 ) -> ( 1 / A ) e. RR ) $=
    ( c1 cr wcel cc0 wne cdiv co 1re redivclt mp3an1 ) BCDACDAEFBAGHCDIBAJK $.
    $( [2-May-2005] $) $( [30-Apr-2005] $)

