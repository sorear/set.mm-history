$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Division
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Define division.  Theorem ~ divmul relates it to multiplication, and
       ~ divcl and ~ divrcl prove its closure laws. $)
    df-div $a |- / = { <. <. x , y >. , z >. |
                   ( ( x e. CC /\ y e. ( CC \ { 0 } ) ) /\
                   z = U. { w e. CC | ( y x. w ) = x } ) } $.
    $(
    Future simpler alternative?
    df-div $a |- / = { <. <. x , y >. , z >. |
                ( ( y e. CC /\ z e. CC ) /\  x = ( y x. z ) ) } $.
    $)
  $}


  ${
    $d x y z w A $.  $d x y z w B $.  $( ` y z w ` are dummy. $)
    divval.1 $e |- A e. CC $.
    divval.2 $e |- B e. CC $.
    divval.3 $e |- B =/= 0 $.
    $( Value of division: the (unique) element ` x ` such that
        ` ( B x. x ) = A ` .  This is meaningful only when ` B ` is nonzero. $)
    divval $p |- ( A / B ) = U. { x e. CC | ( B x. x ) = A } $=
      ( vy vz vw cc wcel cc0 csn cdif cdiv co cv cmulc wceq crab cuni wn wa
      wne df-ne mpbi elisseti elsnc mtbir pm3.2i eldif mpbir axcnex rabex
      uniex cleq2 birabsdv unieqd opreq1 cleq1d df-div oprabval2 mp2an ) BJKCJL
      MZNZKZBCOPCAQZRPZBSZAJTZUAZSDVFCJKZCVDKZUBZUCVLVNEVMCLSZCLUDVOUBFCLUEUFCL
      CJEUGUHUIUJCJVDUKULGHIBCJVEHQZVGRPZGQZSZAJTZUAVKOVQBSZAJTZUAVJVIAJUMUNUOV
      RBSZVTWBWCVSWAAJVRBVQUPUQURVPCSZWBVJWDWAVIAJWDVQVHBVPCVGRUSUTUQURGHIAVAVB
      VC $.
      $( [8-May-99] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    divmul.1 $e |- A e. CC $.
    divmul.2 $e |- B e. CC $.
    divmul.3 $e |- C e. CC $.
    divmul.4 $e |- B =/= 0 $.
    $( Relationship between division and multiplication. $)
    divmul $p |- ( ( A / B ) = C <-> ( B x. C ) = A ) $=
      ( vx cc wcel cdiv co wceq cmulc wb cv cleq2 opreq2 cleq1d bi2bid crab
      cuni wreu receu reuuni mpan2 divval cleq1i syl6rbbr vtoclga ax-mp ) CIJAB
      KLZCMZBCNLZAMZOZFULHPZMZBUQNLZAMZOUPHCIUQCMZURUMUTUOUQCULQVAUSUNAUQCBNRST
      UQIJZUTUTHIUAUBZUQMZURVBUTHIUCUTVDOHBAEDGUDUTHIUEUFULVCUQHABDEGUGUHUIUJUK
      $.
      $( [2-Feb-95] $)
  $}

  ${
    divmulz.1 $e |- A e. CC $.
    divmulz.2 $e |- B e. CC $.
    divmulz.3 $e |- C e. CC $.
    $( Relationship between division and multiplication. $)
    divmulz $p |- ( B =/= 0 -> ( ( A / B ) = C <-> ( B x. C ) = A ) ) $=
      ( cc0 wne cdiv co wceq cmulc wb c1 cded opreq2 cleq1d opreq1 bi2bid cc
      1cn keepel elimne0 divmul dedth ) BGHZABIJZCKZBCLJZAKZMAUFBNOZIJZCKZUKCLJ
      ZAKZMBNBUKKZUHUMUJUOUPUGULCBUKAIPQUPUIUNABUKCLRQSAUKCDUFBNTEUAUBFBUCUDUE
      $.
      $( [8-May-99] $)
  $}

  $( Relationship between division and multiplication. $)
  divmult $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ B =/= 0 ) ->
               ( ( A / B ) = C <-> ( B x. C ) = A ) ) $=
    ( cc wcel w3a cc0 wne cdiv co wceq cmulc wb wi cded opreq1 cleq1d cleq2
    bi2bid bilimd neeq1 opreq2 bi2imd 0cn elimel divmulz dedth3h imp ) ADEZBDEZ
    CDEZFBGHZABIJZCKZBCLJZAKZMZUIUJUKULUQNULUIAGOZBIJZCKZUOURKZMZNABCGGGUJBGOZG
    HZURVCIJZCKZVCCLJZURKZMZNVDVEUKCGOZKZVCVJLJZURKZMZNAURKZUQVBULVOUNUTUPVAVOU
    MUSCAURBIPQAURUORSTBVCKZULVDVBVIBVCGUAVPUTVFVAVHVPUSVECBVCURIUBQVPUOVGURBVC
    CLPQSUCCVJKZVIVNVDVQVFVKVHVMCVJVERVQVGVLURCVJVCLUBQSTURVCVJAGDUDUEBGDUDUECG
    DUDUEUFUGUH $.
    $( [4-Aug-04] $) $( [2-Aug-04] $)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    divcl.1 $e |- A e. CC $.
    divcl.2 $e |- B e. CC $.
    divcl.3 $e |- B =/= 0 $.
    $( Closure law for division. $)
    divcl $p |- ( A / B ) e. CC $=
      ( vx cdiv co cv cmulc wceq cc crab cuni divval wreu wcel receu reucl
      ax-mp eqeltr ) ABGHBFIJHAKZFLMNZLFABCDEOUBFLPUCLQFBADCERUBFLSTUA $.
      $( [2-Feb-95] $)
  $}

  ${
    divclz.1 $e |- A e. CC $.
    divclz.2 $e |- B e. CC $.
    $( Closure law for division. $)
    divclz $p |- ( B =/= 0 -> ( A / B ) e. CC ) $=
      ( cc0 wne cdiv co cc wcel c1 cded wceq opreq2 eleq1d 1cn keepel elimne0
      divcl dedth ) BEFZABGHZIJAUABKLZGHZIJBKBUCMUBUDIBUCAGNOAUCCUABKIDPQBRST
      $.
      $( [7-May-99] $)
  $}

  $( Closure law for division. $)
  divclt $p |- ( ( ( A e. CC /\ B e. CC ) /\ B =/= 0 ) ->
               ( A / B ) e. CC ) $=
    ( cc wcel wa cc0 wne cdiv co wi cded wceq opreq1 eleq1d bilimd neeq1
    opreq2 bi2imd 0cn elimel divclz dedth2h imp ) ACDZBCDZEBFGZABHIZCDZUDUEUFUH
    JUFUDAFKZBHIZCDZJUEBFKZFGZUIULHIZCDZJABFFAUILZUHUKUFUPUGUJCAUIBHMNOBULLZUFU
    MUKUOBULFPUQUJUNCBULUIHQNRUIULAFCSTBFCSTUAUBUC $.
    $( [21-Jul-01] $)

  ${
    divcan.1 $e |- A e. CC $.
    divcan.2 $e |- B e. CC $.
    ${
      divcan.3 $e |- A =/= 0 $.
      $( A cancellation law for division. $)
      divcan2 $p |- ( A x. ( B / A ) ) = B $=
        ( cdiv co wceq cmulc cleqid divcl divmul mpbi ) BAFGZNHANIGBHNJBANDCBAD
        CEKELM $.
        $( [9-Feb-95] $)

      $( A cancellation law for division. $)
      divcan1 $p |- ( ( B / A ) x. A ) = B $=
        ( cdiv co cmulc divcl mulcom divcan2 eqtr ) BAFGZAHGAMHGBMABADCEICJABCD
        EKL $.
        $( [18-May-99] $)
    $}

    $( A cancellation law for division. $)
    divcan1z $p |- ( A =/= 0 -> ( ( B / A ) x. A ) = B ) $=
      ( cc0 wne cdiv co cmulc wceq c1 cded opreq2 id opreq12d cleq1d cc 1cn
      keepel elimne0 divcan1 dedth ) AEFZBAGHZAIHZBJBUCAKLZGHZUFIHZBJAKAUFJZUEU
      HBUIUDUGAUFIAUFBGMUINOPUFBUCAKQCRSDATUAUB $.
      $( [2-Oct-99] $)

    $( A cancellation law for division. $)
    divcan2z $p |- ( A =/= 0 -> ( A x. ( B / A ) ) = B ) $=
      ( cc0 wne cdiv co cmulc wceq c1 cded id opreq2 opreq12d cleq1d cc 1cn
      keepel elimne0 divcan2 dedth ) AEFZABAGHZIHZBJUCAKLZBUFGHZIHZBJAKAUFJZUEU
      HBUIAUFUDUGIUIMAUFBGNOPUFBUCAKQCRSDATUAUB $.
      $( [10-Aug-99] $)
  $}

  $( A cancellation law for division. $)
  divcan1t $p |- ( ( ( A e. CC /\ B e. CC ) /\ A =/= 0 ) ->
                 ( ( B / A ) x. A ) = B ) $=
    ( cc wcel wa cc0 wne cdiv co cmulc wceq wi cded neeq1 opreq2 id opreq12d
    cleq1d bi2imd opreq1 opreq1d cleq12d bilimd 0cn elimel divcan1z dedth2h
    imp ) ACDZBCDZEAFGZBAHIZAJIZBKZUIUJUKUNLUIAFMZFGZBUOHIZUOJIZBKZLUPUJBFMZUOH
    IZUOJIZUTKZLABFFAUOKZUKUPUNUSAUOFNVDUMURBVDULUQAUOJAUOBHOVDPQRSBUTKZUSVCUPV
    EURVBBUTVEUQVAUOJBUTUOHTUAVEPUBUCUOUTAFCUDUEBFCUDUEUFUGUH $.
    $( [14-Jun-04] $) $( [5-Jun-04] $)

  $( A cancellation law for division. $)
  divcan2t $p |- ( ( ( A e. CC /\ B e. CC ) /\ A =/= 0 ) ->
                 ( A x. ( B / A ) ) = B ) $=
    ( cc wcel wa cc0 wne cdiv co cmulc wceq wi cded neeq1 id opreq2 opreq12d
    cleq1d bi2imd opreq1 opreq2d cleq12d bilimd 0cn elimel divcan2z dedth2h
    imp ) ACDZBCDZEAFGZABAHIZJIZBKZUIUJUKUNLUIAFMZFGZUOBUOHIZJIZBKZLUPUOUJBFMZU
    OHIZJIZUTKZLABFFAUOKZUKUPUNUSAUOFNVDUMURBVDAUOULUQJVDOAUOBHPQRSBUTKZUSVCUPV
    EURVBBUTVEUQVAUOJBUTUOHTUAVEOUBUCUOUTAFCUDUEBFCUDUEUFUGUH $.
    $( [20-Feb-04] $) $( [3-Feb-04] $)

  $( The ratio of non-zero numbers is non-zero. $)
  divneq0bt $p |- ( ( ( A e. CC /\ B e. CC ) /\ B =/= 0 ) ->
                  ( A =/= 0 <-> ( A / B ) =/= 0 ) ) $=
    ( cc wcel wa cc0 wne wceq wn cdiv co cmulc wb mulzer1t cleq1d cleqcom
    syl6rbbr ad2antlr wi 0cn w3a divmult exp mp3an3 imp bitr4d binegd df-ne
    3bitr4g ) ACDZBCDZEZBFGZEZAFHZIABJKZFHZIAFGUPFGUNUOUQUNUOBFLKZAHZUQUKUOUSMU
    JUMUKUSFAHUOUKURFABNOAFPQRULUMUQUSMZUJUKFCDZUMUTSTUJUKVAUAUMUTABFUBUCUDUEUF
    UGAFUHUPFUHUI $.
    $( [4-Aug-04] $) $( [2-Aug-04] $)

  ${
    divneq0.1 $e |- A e. CC $.
    divneq0.2 $e |- B e. CC $.
    divneq0.3 $e |- A =/= 0 $.
    divneq0.4 $e |- B =/= 0 $.
    $( The ratio of non-zero numbers is non-zero. $)
    divneq0 $p |- ( A / B ) =/= 0 $=
      ( cc wcel wa cc0 wne cdiv co pm3.2i divneq0bt mpbii mp2an ) AGHZBGHZIZBJK
      ZABLMJKZRSCDNFTUAIAJKUBEABOPQ $.
      $( [9-Feb-95] $)
  $}

  ${
    recneq0z.1 $e |- A e. CC $.
    $( The reciprocal of a non-zero number is non-zero. $)
    recneq0z $p |- ( A =/= 0 -> ( 1 / A ) =/= 0 ) $=
      ( cc0 wne c1 cdiv co cded wceq opreq2 neeq1d 1cn cc keepel ax1ne0
      elimne0 divneq0 dedth ) ACDZEAFGZCDESAEHZFGZCDAEAUAITUBCAUAEFJKEUALSAEMBL
      NOAPQR $.
      $( [14-May-99] $)
  $}

  ${
    recid.1 $e |- A e. CC $.
    recid.2 $e |- A =/= 0 $.
    $( Multiplication of a number and its reciprocal. $)
    recid $p |- ( A x. ( 1 / A ) ) = 1 $=
      ( c1 1cn divcan2 ) ADBECF $.
      $( [9-Feb-95] $)
  $}

  ${
    recidz.1 $e |- A e. CC $.
    $( Multiplication of a number and its reciprocal. $)
    recidz $p |- ( A =/= 0 -> ( A x. ( 1 / A ) ) = 1 ) $=
      ( c1 1cn divcan2z ) ACBDE $.
      $( [14-May-99] $)
  $}

  $( Multiplication of a number and its reciprocal. $)
  recidt $p |- ( ( A e. CC /\ A =/= 0 ) -> ( A x. ( 1 / A ) ) = 1 ) $=
    ( cc wcel cc0 wne c1 cdiv co cmulc wceq wi cded neeq1 id opreq2 opreq12d
    cleq1d bi2imd 0cn elimel recidz dedth imp ) ABCZADEZAFAGHZIHZFJZUDUEUHKUDAD
    LZDEZUIFUIGHZIHZFJZKADAUIJZUEUJUHUMAUIDMUNUGULFUNAUIUFUKIUNNAUIFGOPQRUIADBS
    TUAUBUC $.
    $( [25-Oct-99] $)

  ${
    divrec.1 $e |- A e. CC $.
    divrec.2 $e |- B e. CC $.
    ${
      divrec.3 $e |- B =/= 0 $.
      $( Relationship between division and reciprocal.  Theorem I.9 of
         [Apostol] p. 18. $)
      divrec $p |- ( A / B ) = ( A x. ( 1 / B ) ) $=
        ( cdiv co c1 cmulc wceq 1cn divcl mulcl mulcom mulass divcan1 opreq2i
        mulid1 eqtr 3eqtr divmul mpbir ) ABFGAHBFGZIGZJBUDIGZAJUEUDBIGAUCBIGZIG
        ZABUDDAUCCHBKDELZMZNAUCBCUHDOUGAHIGAUFHAIBHDKEPQACRSTABUDCDUIEUAUB $.
        $( [9-Feb-95] $)
    $}

    $( Relationship between division and reciprocal.  Theorem I.9 of
       [Apostol] p. 18. $)
    divrecz $p |- ( B =/= 0 -> ( A / B ) = ( A x. ( 1 / B ) ) ) $=
      ( cc0 wne cdiv co c1 cmulc wceq cded opreq2 opreq2d cleq12d cc 1cn
      keepel elimne0 divrec dedth ) BEFZABGHZAIBGHZJHZKAUBBILZGHZAIUFGHZJHZKBIB
      UFKZUCUGUEUIBUFAGMUJUDUHAJBUFIGMNOAUFCUBBIPDQRBSTUA $.
      $( [11-Oct-99] $)
  $}

  $( Relationship between division and reciprocal.  Theorem I.9 of
     [Apostol] p. 18. $)
  divrect $p |- ( ( ( A e. CC /\ B e. CC ) /\ B =/= 0 ) ->
                ( A / B ) = ( A x. ( 1 / B ) ) ) $=
    ( cc wcel wa cc0 wne cdiv co c1 cmulc wceq wi cded opreq1 cleq12d bilimd
    neeq1 opreq2 opreq2d bi2imd 0cn elimel divrecz dedth2h imp ) ACDZBCDZEBFGZA
    BHIZAJBHIZKIZLZUGUHUIUMMUIUGAFNZBHIZUNUKKIZLZMUHBFNZFGZUNURHIZUNJURHIZKIZLZ
    MABFFAUNLZUMUQUIVDUJUOULUPAUNBHOAUNUKKOPQBURLZUIUSUQVCBURFRVEUOUTUPVBBURUNH
    SVEUKVAUNKBURJHSTPUAUNURAFCUBUCBFCUBUCUDUEUF $.
    $( [4-Aug-04] $) $( [2-Aug-04] $)

  $( An associative law for division. $)
  divasst $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ C =/= 0 ) ->
                ( ( A x. B ) / C ) = ( A x. ( B / C ) ) ) $=
    ( cc wcel w3a cc0 wne wa cmulc co c1 cdiv wceq wi id 1cn divclt mpan11
    im3an 3exp exp4a 3imp imp axmulass syl divrect axmulcl imran 3impa sylan
    3simpc opreq2d 3eqtr4d ) ADEZBDEZCDEZFZCGHZIZABJKZLCMKZJKZABVBJKZJKZVACMKZA
    BCMKZJKUTUOUPVBDEZFZVCVENURUSVIUOUPUQUSVIOUOUPUQUSVIUOUPUQUSIZVIUOUOUPUPVJV
    HUOPUPPLDEUQUSVHQLCRSTUAUBUCUDABVBUEUFVADEZUQIZUSVFVCNURVACUGUOUPUQVLUOUPIV
    KUQABUHUIUJUKUTVGVDAJUPUQIUSVGVDNURBCUGUOUPUQULUKUMUN $.
    $( [4-Aug-04] $) $( [2-Aug-04] $)

  $( An associative/distributive law for division. $)
  div23t $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ C =/= 0 ) ->
               ( ( A x. B ) / C ) = ( ( A / C ) x. B ) ) $=
    ( cc wcel w3a cc0 wne wa cmulc co cdiv wceq axmulcom 3adant3 adantr
    opreq1d wi divasst exp 3com12 imp divclt sylan5 exp44 com12 3imp 3eqtrd )
    ADEZBDEZCDEZFZCGHZIZABJKZCLKBAJKZCLKZBACLKZJKZURBJKZUNUOUPCLULUOUPMZUMUIUJV
    AUKABNOPQULUMUQUSMZUJUIUKUMVBRUJUIUKFUMVBBACSTUAUBULUMUSUTMZUIUJUKUMVCRZUJU
    IUKVDRUJUIUKUMVCUJURDEVCUIUKIUMIBURNACUCUDUEUFUGUBUH $.
    $( [4-Aug-04] $) $( [2-Aug-04] $)

  ${
    divass.1 $e |- A e. CC $.
    divass.2 $e |- B e. CC $.
    divass.3 $e |- C e. CC $.
    $( An associative law for division. $)
    divassz $p |- ( C =/= 0 -> ( ( A x. B ) / C ) = ( A x. ( B / C ) ) ) $=
      ( cc wcel w3a cc0 wne cmulc co cdiv wceq 3pm3.2i divasst mpan ) AGHZBGHZC
      GHZICJKABLMCNMABCNMLMOSTUADEFPABCQR $.
      $( [12-Aug-99] $)

    ${
      divass.4 $e |- C =/= 0 $.
      $( An associative law for division. $)
      divass $p |- ( ( A x. B ) / C ) = ( A x. ( B / C ) ) $=
        ( cc0 wne cmulc co cdiv wceq divassz ax-mp ) CHIABJKCLKABCLKJKMGABCDEFN
        O $.
        $( [15-Feb-95] $)

      $( Distribution of division over addition. $)
      divdistr $p |- ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) $=
        ( caddc co c1 cdiv cmulc 1cn divcl distr2 addcl divrec opreq12i 3eqtr4
        ) ABHIZJCKIZLIAUALIZBUALIZHITCKIACKIZBCKIZHIABUADEJCMFGNOTCABDEPFGQUDUB
        UEUCHACDFGQBCEFGQRS $.
        $( [16-Feb-95] $)

      $( An associative/distributive law for division. $)
      div23 $p |- ( ( A x. B ) / C ) = ( ( A / C ) x. B ) $=
        ( cmulc co cdiv mulcom opreq1i divass divcl 3eqtr ) ABHIZCJIBAHIZCJIBAC
        JIZHIRBHIPQCJABDEKLBACEDFGMBREACDFGNKO $.
        $( [3-Sep-99] $)
    $}

    $( Distribution of division over addition. $)
    divdistrz $p |- ( C =/= 0 ->
                    ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) ) $=
      ( cc0 wne caddc co cdiv wceq c1 cded opreq2 opreq12d cleq12d cc 1cn
      keepel elimne0 divdistr dedth ) CGHZABIJZCKJZACKJZBCKJZIJZLUEUDCMNZKJZAUJ
      KJZBUJKJZIJZLCMCUJLZUFUKUIUNCUJUEKOUOUGULUHUMICUJAKOCUJBKOPQABUJDEUDCMRFS
      TCUAUBUC $.
      $( [2-Aug-04] $) $( [31-Jul-04] $)
  $}

  $( Distribution of division over addition. $)
  divdistrt $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ C =/= 0 ) ->
                  ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) ) $=
    ( cc wcel w3a cc0 wne caddc co cdiv wceq wi cded opreq1 opreq1d cleq12d
    bilimd opreq2 opreq2d neeq1 opreq12d bi2imd 0cn elimel divdistrz dedth3h
    imp ) ADEZBDEZCDEZFCGHZABIJZCKJZACKJZBCKJZIJZLZUIUJUKULURMULUIAGNZBIJZCKJZU
    SCKJZUPIJZLZMABCGGGULUSUJBGNZIJZCKJZVBVECKJZIJZLZMUKCGNZGHZVFVKKJZUSVKKJZVE
    VKKJZIJZLZMAUSLZURVDULVRUNVAUQVCVRUMUTCKAUSBIOPVRUOVBUPIAUSCKOPQRBVELZVDVJU
    LVSVAVGVCVIVSUTVFCKBVEUSISPVSUPVHVBIBVECKOTQRCVKLZULVLVJVQCVKGUAVTVGVMVIVPC
    VKVFKSVTVBVNVHVOICVKUSKSCVKVEKSUBQUCUSVEVKAGDUDUEBGDUDUECGDUDUEUFUGUH $.
    $( [2-Aug-04] $) $( [31-Jul-04] $)

  ${
    divcan3.1 $e |- A e. CC $.
    divcan3.2 $e |- B e. CC $.
    ${
      divcan3.3 $e |- A =/= 0 $.
      $( A cancellation law for division. $)
      divcan3 $p |- ( ( A x. B ) / A ) = B $=
        ( cmulc co cdiv divass divcan2 eqtr ) ABFGAHGABAHGFGBABACDCEIABCDEJK $.
        $( [16-Feb-95] $)

      $( A cancellation law for division. $)
      divcan4 $p |- ( ( B x. A ) / A ) = B $=
        ( cmulc co cdiv mulcom opreq1i divcan3 eqtr ) BAFGZAHGABFGZAHGBMNAHBADC
        IJABCDEKL $.
        $( [18-May-99] $)
    $}

    $( A cancellation law for division.  (Eliminates a hypothesis of
       ~ divcan3 with the weak deduction theorem.) $)
    divcan3z $p |- ( A =/= 0 -> ( ( A x. B ) / A ) = B ) $=
      ( cc0 wne cmulc co cdiv wceq c1 cded opreq1 id opreq12d cleq1d cc 1cn
      keepel elimne0 divcan3 dedth ) AEFZABGHZAIHZBJUCAKLZBGHZUFIHZBJAKAUFJZUEU
      HBUIUDUGAUFIAUFBGMUINOPUFBUCAKQCRSDATUAUB $.
      $( [4-Feb-04] $) $( [3-Feb-04] $)

    $( A cancellation law for division. $)
    divcan4z $p |- ( A =/= 0 -> ( ( B x. A ) / A ) = B ) $=
      ( cc0 wne cmulc co cdiv divcan3z mulcom opreq1i syl5eq ) AEFABGHZAIHBBAGH
      ZAIHABCDJONAIBADCKLM $.
      $( [12-Oct-99] $)

  $}

  $( A cancellation law for division. $)
  divcan3t $p |- ( ( ( A e. CC /\ B e. CC ) /\ A =/= 0 ) ->
                 ( ( A x. B ) / A ) = B ) $=
    ( cc wcel wa cc0 wne cmulc co cdiv wceq wi cded neeq1 opreq1 id opreq12d
    cleq1d bi2imd opreq2 opreq1d cleq12d bilimd 0cn elimel divcan3z dedth2h
    imp ) ACDZBCDZEAFGZABHIZAJIZBKZUIUJUKUNLUIAFMZFGZUOBHIZUOJIZBKZLUPUOUJBFMZH
    IZUOJIZUTKZLABFFAUOKZUKUPUNUSAUOFNVDUMURBVDULUQAUOJAUOBHOVDPQRSBUTKZUSVCUPV
    EURVBBUTVEUQVAUOJBUTUOHTUAVEPUBUCUOUTAFCUDUEBFCUDUEUFUGUH $.
    $( [20-Feb-04] $) $( [3-Feb-04] $)

  ${
    div11.1 $e |- A e. CC $.
    div11.2 $e |- B e. CC $.
    div11.3 $e |- C e. CC $.
    div11.4 $e |- C =/= 0 $.
    $( One-to-one relationship for division. $)
    div11 $p |- ( ( A / C ) = ( B / C ) <-> A = B ) $=
      ( cdiv co wceq cmulc divcl mulcan divcan2 cleq12i bitr3 ) ACHIZBCHIZJCQKI
      ZCRKIZJABJCQRFACDFGLBCEFGLGMSATBCAFDGNCBFEGNOP $.
      $( [20-Aug-01] $)
  $}

  ${
    recrec.1 $e |- A e. CC $.
    recrec.2 $e |- A =/= 0 $.
    $( A number is equal to the reciprocal of its reciprocal.  Theorem I.10 of
       [Apostol] p. 18. $)
    recrec $p |- ( 1 / ( 1 / A ) ) = A $=
      ( c1 cdiv co cmulc 1cn divcl ax1ne0 divneq0 recid opreq2i opreq1i mulass
      mulid2 3eqtr3 mulid1 ) ADAEFZDSEFZGFZGFZADGFTAUADAGSDAHBCIZDAHBJCKZLMASGF
      ZTGFDTGFUBTUEDTGABCLNASTBUCDSHUCUDIZOTUFPQABRQ $.
      $( [9-Feb-95] $)

    $( A number divided by itself is one. $)
    divid $p |- ( A / A ) = 1 $=
      ( cdiv co c1 cmulc divrec recid eqtr ) AADEAFADEGEFAABBCHABCIJ $.
      $( [9-Feb-95] $)

    $( Division into zero is zero. $)
    divzer $p |- ( 0 / A ) = 0 $=
      ( cc0 cdiv co c1 cmulc 0cn divrec 1cn divcl mulzer2 eqtr ) DAEFDGAEFZHFDD
      AIBCJOGAKBCLMN $.
      $( [12-Aug-99] $)
  $}

  $( A number divided by itself is one. $)
  dividt $p |- ( ( A e. CC /\ A =/= 0 ) -> ( A / A ) = 1 ) $=
    ( cc wcel cc0 wne wa cdiv co c1 wceq cded opreq12 id sylan13 cleq1d eleq1
    neeq1 bi2and 1cn ax1ne0 pm3.2i elimhyp pm3.26i pm3.27i divid dedth ) ABCZAD
    EZFZAAGHZIJUIAIKZUKGHZIJAIAUKJZUJULIUMUMUJULJUMAUKAUKGLUMMZUNNOUKUKBCZUKDEZ
    UIUOUPFIBCZIDEZFAIUMUGUOUHUPAUKBPAUKDQRIUKJUQUOURUPIUKBPIUKDQRUQURSTUAUBZUC
    UOUPUSUDUEUF $.
    $( [2-Aug-04] $) $( [1-Aug-04] $)

  ${
    div1.1 $e |- A e. CC $.
    $( A number divided by 1 is itself. $)
    div1 $p |- ( A / 1 ) = A $=
      ( c1 cdiv co wceq cmulc mulid2 1cn ax1ne0 divmul mpbir ) ACDEAFCAGEAFABHA
      CABIBJKL $.
      $( [9-Jan-02] $)
  $}

  $( A number divided by 1 is itself. $)
  div1t $p |- ( A e. CC -> ( A / 1 ) = A ) $=
    ( cc wcel c1 cdiv co wceq cded opreq1 id cleq12d 1cn elimel div1 dedth ) AB
    CZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRADBLMNO $.
    $( [9-Jan-02] $)

  $( Move negative sign inside of a division. $)
  divnegt $p |- ( ( ( A e. CC /\ B e. CC ) /\ B =/= 0 ) ->
                -u ( A / B ) = ( -u A / B ) ) $=
    ( cc wcel wa cc0 wne cneg c1 cdiv co cmulc wceq mulneg1t 1cn divclt mpan11
    sylan5 anassrs divrect negclt imran sylan negeqd 3eqtr4rd ) ACDZBCDZEZBFGZE
    ZAHZIBJKZLKZAULLKZHZUKBJKZABJKZHUFUGUIUMUOMZUFULCDZURUGUIEAULNICDUGUIUSOIBP
    QRSUKCDZUGEUIUPUMMUHUKBTUFUTUGAUAUBUCUJUQUNABTUDUE $.
    $( [18-Sep-04] $) $( [17-Sep-04] $)

  $( A number is equal to the reciprocal of its reciprocal.  Theorem I.10 of
     [Apostol] p. 18. $)
  recrect $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 1 / ( 1 / A ) ) = A ) $=
    ( cc wcel cc0 wne wa c1 cdiv co wceq cded opreq2 opreq2d id cleq12d eleq1
    neeq1 bi2and 1cn ax1ne0 pm3.2i elimhyp pm3.26i pm3.27i recrec dedth ) ABCZA
    DEZFZGGAHIZHIZAJGGUIAGKZHIZHIZULJAGAULJZUKUNAULUOUJUMGHAULGHLMUONOULULBCZUL
    DEZUIUPUQFGBCZGDEZFAGUOUGUPUHUQAULBPAULDQRGULJURUPUSUQGULBPGULDQRURUSSTUAUB
    ZUCUPUQUTUDUEUF $.
    $( [26-Sep-99] $)

  ${
    rec11.1 $e |- A e. CC $.
    rec11.2 $e |- B e. CC $.
    ${
      rec11i.3 $e |- A =/= 0 $.
      rec11i.4 $e |- B =/= 0 $.
      $( Reciprocal is one-to-one. $)
      rec11i $p |- ( ( 1 / A ) = ( 1 / B ) <-> A = B ) $=
        ( c1 cdiv co wceq cmulc opreq2 1cn divcl mul23 recid opreq1i mulid2
        3eqtr mulass opreq2i mulid1 cleq12i cleqcom bitr sylib impbi ) GAHIZGBH
        IZJZABJZUJABKIZUHKIZULUIKIZJZUKUHUIULKLUOBAJUKUMBUNAUMAUHKIZBKIGBKIBABU
        HCDGAMCENOUPGBKACEPQBDRSUNABUIKIZKIAGKIAABUICDGBMDFNTUQGAKBDFPUAACUBSUC
        BAUDUEUFABGHLUG $.
        $( [16-Sep-99] $)
    $}

    $( Reciprocal is one-to-one. $)
    rec11 $p |- ( ( A =/= 0 /\ B =/= 0 ) ->
               ( ( 1 / A ) = ( 1 / B ) <-> A = B ) ) $=
      ( cc0 wne c1 cdiv co wceq wb cded opreq2 cleq1d cleq1 bi2bid cleq2d
      cleq2 cc 1cn keepel elimne0 rec11i dedth2h ) AEFZBEFZGAHIZGBHIZJZABJZKGUE
      AGLZHIZUHJZUKBJZKULGUFBGLZHIZJZUKUOJZKABGGAUKJZUIUMUJUNUSUGULUHAUKGHMNAUK
      BOPBUOJZUMUQUNURUTUHUPULBUOGHMQBUOUKRPUKUOUEAGSCTUAUFBGSDTUAAUBBUBUCUD $.
      $( [16-Sep-99] $)
  $}

  $( Multiplication of two ratios.  Theorem I.14 of [Apostol] p. 18. $)
  divmuldivt $p |- ( ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) /\
                   ( B =/= 0 /\ D =/= 0 ) ) ->
                  ( ( A / B ) x. ( C / D ) ) = ( ( A x. C ) / ( B x. D ) ) ) $=
    ( cc wcel wa cc0 wne cmulc co cdiv wceq divcan3t axmulcl adantrl adantll
    adantr divclt syl2an an4s jca wi muln0bt biimpd imp sylan13 mul4t pm3.27
    divcan2t exp ancoms opreqan12d eqtr3d opreq1d ) AEFZBEFZGZCEFZDEFZGZGZBHIZD
    HIZGZGZBDJKZABLKZCDLKZJKZJKZVGLKZVJACJKZVGLKVGEFZVJEFZGVGHIZVLVJMVFVGVJNVFV
    NVOVBVNVEUQVAVNUPUQUTVNUSBDOPQRURVCVAVDVOVHEFZVIEFZVOURVCGZVAVDGZVHVIOABSZC
    DSZTUAUBVBVEVPUQVAVEVPUCZUPUQUTWCUSUQUTGVEVPBDUDUEPQUFUGVFVKVMVGLURVCVAVDVK
    VMMVSVTGBVHJKZDVIJKZJKZVKVMUQVQGUTVRGWFVKMVSVTBVHDVIUHVSUQVQURUQVCUPUQUIRWA
    UBVTUTVRVAUTVDUSUTUIRWBUBTVSVTWDAWECJURVCWDAMZUQUPVCWGUCUQUPGVCWGBAUJUKULUF
    VAVDWECMZUTUSVDWHUCUTUSGVDWHDCUJUKULUFUMUNUAUOUN $.
    $( [2-Aug-04] $) $( [1-Aug-04] $)

  $( Addition of two ratios.  Theorem I.13 of [Apostol] p. 18. $)
  divadddivt $p |- ( ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) /\
                   ( B =/= 0 /\ D =/= 0 ) ) ->
  ( ( A / B ) + ( C / D ) ) = ( ( ( A x. D ) + ( B x. C ) ) / ( B x. D ) ) ) $=
    ( cc wcel wa cc0 wne cmulc co caddc cdiv wceq wb muln0bt adantrl adantll
    w3a wi axmulcl adantlr adantrr 3jca divdistrt exp syl sylbid imp c1 dividt
    opreq2d divmuldivt pm3.27 jca imlan sylan divclt ax1id 3eqtr3d opreq1d
    imran mulid2t opreq12d eqtr2d ) AEFZBEFZGZCEFZDEFZGZGZBHIZDHIZGZGZADJKZBCJK
    ZLKBDJKZMKZVQVSMKZVRVSMKZLKZABMKZCDMKZLKVLVOVTWCNZVLVOVSHIZWFVGVKVOWGOZVFVG
    VJWHVIBDPQRVLVQEFZVREFZVSEFZSZWGWFTVLWIWJWKVFVKWIVGVFVJWIVIADUAQUBVGVKWJVFV
    GVIWJVJBCUAUCRVGVKWKVFVGVJWKVIBDUAQRUDWLWGWFVQVRVSUEUFUGUHUIVPWAWDWBWELVPWD
    DDMKZJKZWDUJJKZWAWDVPWMUJWDJVKVOWMUJNZVHVJVOWPVIVJVNWPVMDUKQRRULVHVJVJGZGVO
    WNWANVLABDDUMVKWQVHVKVJVJVIVJUNZWRUOUPUQVHVOWOWDNZVKVHVMWSVNVHVMGWDEFWSABUR
    WDUSUGUCUBUTVPBBMKZWEJKZUJWEJKZWBWEVPWTUJWEJVHVOWTUJNZVKVGVOXCVFVGVMXCVNBUK
    UCRUBVAVGVGGZVKGVOXAWBNVLBBCDUMVHXDVKVHVGVGVFVGUNZXEUOVBUQVKVOXBWENZVHVKVNX
    FVMVKVNGWEEFXFCDURWEVCUGQRUTVDVE $.
    $( [2-Aug-04] $) $( [1-Aug-04] $)

  $( Division of two ratios.  Theorem I.15 of [Apostol] p. 18. $)
  divdivdivt $p |- ( ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) /\
                   ( B =/= 0 /\ C =/= 0 /\ D =/= 0 ) ) ->
                   ( ( A / B ) / ( C / D ) ) = ( ( A x. D ) / ( B x. C ) ) ) $=
    ( cc wcel wa cc0 wne cdiv co cmulc wceq w3a axmulcom divclt ancom sylanb
    adantrr adantrl adantll adantlr sylan13 divmuldivt biimp imlan pm3.26
    syl2an eqtrd opreq2d c1 ancli adantl im2an syl ad2antlr opreq1d dividt
    axmulcl ancoms wb muln0bt syl5bb biimpa 3eqtrd 3jca axmulass mulid2t
    3eqtr3d eqtr3d divmult jca adantr wi biimpd sylan5d imp divneq0bt an1rs
    anasss mpbird 3anass sylan5b ) AEFZBEFZGZCEFZDEFZGZGZBHIZCHIZDHIZGZGZABJKZC
    DJKZJKADLKZBCLKZJKZMZWKWLWMNWJWOGZXAWQWTLKZWPMZXBWQDCJKZWPLKZLKZXCWPXBXFWTW
    QLXBXFWPXELKZWTXEEFZWPEFZXFXHMXBXEWPOWIWOXIWFWIWNXIWKWIWLXIWMWHWGGZWLXIWIDC
    PWGWHQZRSTUAZWFWOXJWIWFWKXJWNABPSUBZUCWFXKGWKWLGZXHWTMWJWOABDCUDWIXKWFWIXKX
    LUEZUFWNWLWKWLWMUGZUFUHUIUJXBWQXELKZWPLKZUKWPLKZXGWPXBXRUKWPLXBXRCDLKZDCLKZ
    JKZYBYBJKZUKXBWIXKGZWMWLGZGZXRYCMWIWOYGWFWIYEWOYFWIXKXPULWNYFWKWNYFWLWMQZUE
    UMUNUACDDCUDUOXBYAYBYBJWIYAYBMWFWOCDOUPUQYBEFZYBHIZYDUKMXBYBURWIYIWFWOWHWGY
    IDCUSUTUPWIWOYJWFWIWNYJWKWIWNYJWHWGWNYJVAXKYFYJWNDCVBYHVCUTVDTUAUCVEUQXBWQE
    FZXIXJNXSXGMXBYKXIXJWIWOYKWFWIWNYKWKWIWMYKWLCDPTTUAZXMXNVFWQXEWPVGUOXBXJXTW
    PMXNWPVHUOVIVJXJYKWTEFZNWQHIZXAXDVAXBWPWQWTVKXBXJYKYMXNYLWREFZWSEFZGZWSHIZY
    MXBWRWSPWJYQWOWJYOYPWDWIYOWEWDWHYOWGADUSTUBWEWIYPWDWEWGYPWHBCUSSUAVLVMWJWOY
    RWEWIWOYRVNZWDWEWGYSWHWEWGGZWKWLYRWNYTXOYRBCVBVOXQVPSUAVQUCVFWIWOYNWFWIWNYN
    WKWIWLWMYNWIWMWLYNWIWMGWLYNCDVRVDVSVTTUAUCWAWKWLWMWBWC $.
    $( [4-Aug-04] $) $( [2-Aug-04] $)

  ${
    divmuldiv.1 $e |- A e. CC $.
    divmuldiv.2 $e |- B e. CC $.
    divmuldiv.3 $e |- C e. CC $.
    divmuldiv.4 $e |- D e. CC $.
    divmuldiv.5 $e |- B =/= 0 $.
    divmuldiv.6 $e |- D =/= 0 $.
    $( Multiplication of two ratios.  Theorem I.14 of [Apostol] p. 18. $)
    divmuldiv $p |- ( ( A / B ) x. ( C / D ) ) =
                      ( ( A x. C ) / ( B x. D ) ) $=
      ( cc wcel wa cc0 wne cdiv co cmulc wceq pm3.2i divmuldivt mp2an ) AKLZBKL
      ZMZCKLZDKLZMZMBNOZDNOZMABPQCDPQRQACRQBDRQPQSUEUHUCUDEFTUFUGGHTTUIUJIJTABC
      DUAUB $.
      $( [16-Feb-95] $)

    $( Swap denominators of two ratios. $)
    divmul13 $p |- ( ( A / B ) x. ( C / D ) ) =
                   ( ( C / B ) x. ( A / D ) ) $=
      ( cmulc co cdiv mulcom opreq1i divmuldiv 3eqtr4r ) CAKLZBDKLZMLACKLZSMLCB
      MLADMLKLABMLCDMLKLRTSMCAGENOCBADGFEHIJPABCDEFGHIJPQ $.
      $( [6-Aug-99] $)

    $( Addition of two ratios.  Theorem I.13 of [Apostol] p. 18. $)
    divadddiv $p |- ( ( A / B ) + ( C / D ) ) =
                      ( ( ( A x. D ) + ( B x. C ) ) / ( B x. D ) ) $=
      ( cc wcel wa cc0 wne cdiv co caddc cmulc wceq pm3.2i divadddivt mp2an )
      AKLZBKLZMZCKLZDKLZMZMBNOZDNOZMABPQCDPQRQADSQBCSQRQBDSQPQTUFUIUDUEEFUAUGUH
      GHUAUAUJUKIJUAABCDUBUC $.
      $( [21-Feb-95] $)

    ${
      divdivdiv.7 $e |- C =/= 0 $.
      $( Division of two ratios.  Theorem I.15 of [Apostol] p. 18. $)
      divdivdiv $p |- ( ( A / B ) / ( C / D ) )
                        = ( ( A x. D ) / ( B x. C ) ) $=
        ( cc wcel wa cc0 wne w3a cdiv co cmulc wceq pm3.2i 3pm3.2i divdivdivt
        mp2an ) ALMZBLMZNZCLMZDLMZNZNBOPZCOPZDOPZQABRSCDRSRSADTSBCTSRSUAUHUKUFU
        GEFUBUIUJGHUBUBULUMUNIKJUCABCDUDUE $.
        $( [22-Feb-95] $)
    $}
  $}

  $( The reciprocal of a ratio. $)
  recdivt $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( A =/= 0 /\ B =/= 0 ) ) ->
                ( 1 / ( A / B ) ) = ( B / A ) ) $=
    ( cc wcel wa cc0 wne c1 cdiv co cmulc wceq 1cn pm3.2i w3a divdivdivt
    ax1ne0 jctl 3anass sylibr sylan5 mpan11 div1 opreq1i a1i mulid2t
    opreqan12rd adantr 3eqtr3d ) ACDZBCDZEZAFGZBFGZEZEZHHIJZABIJZIJZHBKJZHAKJZI
    JZHURIJZBAIJZHCDZVEEZULUOUSVBLZVEVEMMNVFULEHFGZUMUNOZVGUOHHABPUOVHUOEVIUOVH
    QRVHUMUNSTUAUBUSVCLUPUQHURIHMUCUDUEULVBVDLUOUKUJUTBVAAIBUFAUFUGUHUI $.
    $( [29-Aug-04] $) $( [3-Aug-04] $)

  $( Swap denominators in a division. $)
  divdiv23t $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\
         ( B =/= 0 /\ C =/= 0 ) ) -> ( ( A / B ) / C ) = ( ( A / C ) / B ) ) $=
    ( cc wcel w3a cc0 wne wa c1 cdiv co cmulc wceq div23t 3simp1 adantr 1cn
    divclt mpan11 3simp2 sylan adantrr 3simp3 3jca pm3.27 adantl sylan13
    divrect 3simpa pm3.26 syl2an opreq1d wi exp 3adant2 imp adantrl jca
    3eqtr4d ) ADEZBDEZCDEZFZBGHZCGHZIZIZAJBKLZMLZCKLZACKLZVIMLZABKLZCKLVLBKLZVA
    VIDEZVCFVFVKVMNVHAVICOVHVAVPVCVDVAVGVAVBVCPQVDVEVPVFVBVEVPVDJDEVBVEVPRJBSTV
    AVBVCUAZUBUCVDVCVGVAVBVCUDQUEVGVFVDVEVFUFUGUHVHVNVJCKVAVBIVEVNVJNVDVGABUIVA
    VBVCUJVEVFUKZULUMVLDEZVBIVEVOVMNVHVLBUIVHVSVBVDVFVSVEVDVFVSVAVCVFVSUNVBVAVC
    IVFVSACSUOUPUQURVDVBVGVQQUSVGVEVDVRUGUHUT $.
    $( [4-Aug-04] $) $( [2-Aug-04] $)

  ${
    divdiv23.1 $e |- A e. CC $.
    divdiv23.2 $e |- B e. CC $.
    divdiv23.3 $e |- C e. CC $.
    ${
      divdiv23.4 $e |- B =/= 0 $.
      divdiv23.5 $e |- C =/= 0 $.
      $( Swap denominators in a division. $)
      divdiv23 $p |- ( ( A / B ) / C ) = ( ( A / C ) / B ) $=
        ( c1 cdiv co cmulc 1cn divcl div23 divrec opreq1i 3eqtr4 ) AIBJKZLKZCJK
        ACJKZSLKABJKZCJKUABJKASCDIBMEGNFHOUBTCJABDEGPQUABACDFHNEGPR $.
        $( [15-Sep-99] $)
    $}
    $( Swap denominators in a division. $)
    divdiv23z $p |- ( ( B =/= 0 /\ C =/= 0 ) ->
                ( ( A / B ) / C ) = ( ( A / C ) / B ) ) $=
      ( cc wcel w3a cc0 wne wa cdiv co wceq 3pm3.2i divdiv23t mpan ) AGHZBGHZCG
      HZIBJKCJKLABMNCMNACMNBMNOSTUADEFPABCQR $.
      $( [15-Sep-99] $)
  $}

  ${
    $d x y B $.  $( ` x ` is dummy. $)
    divrcl.1 $e |- A e. RR $.
    divrcl.2 $e |- B e. RR $.
    divrcl.3 $e |- B =/= 0 $.
    $( Closure law for division of reals. $)
    divrcl $p |- ( A / B ) e. RR $=
      ( vx cdiv co c1 cmulc cr recn divrec cv wceq wrex wcel cc0 wne axrrecex
      mp2an wa wex df-rex cc wb recnt cded cleq2 opreq2 cleq1d bi2bid 1cn
      elimel divmul dedth syl eleq1a sylbird imp 19.23aiv sylbi ax-mp mulrcl
      eqeltr ) ABGHAIBGHZJHKABACLBDLZEMAVFCBFNZJHZIOZFKPZVFKQZBKQBRSVKDEFBTUAVK
      VHKQZVJUBZFUCVLVJFKUDVNVLFVMVJVLVMVJVFVHOZVLVMVHUEQZVOVJUFZVHUGVPVQVFVPVH
      IUHZOZBVRJHZIOZUFVHIVHVROZVOVSVJWAVHVRVFUIWBVIVTIVHVRBJUJUKULIBVRUMVGVHIU
      EUMUNEUOUPUQVHKVFURUSUTVAVBVCVDVE $.
      $( [9-May-99] $)
  $}

  ${
    divrclz.1 $e |- A e. RR $.
    divrclz.2 $e |- B e. RR $.
    $( Closure law for division of reals. $)
    divrclz $p |- ( B =/= 0 -> ( A / B ) e. RR ) $=
      ( cc0 wne cdiv co cr wcel c1 cded wceq opreq2 eleq1d ax1re keepel
      elimne0 divrcl dedth ) BEFZABGHZIJAUABKLZGHZIJBKBUCMUBUDIBUCAGNOAUCCUABKI
      DPQBRST $.
      $( [9-May-99] $)
  $}

  $( Closure law for division of reals. $)
  divrclt $p |- ( ( ( A e. RR /\ B e. RR ) /\ B =/= 0 ) ->
                ( A / B ) e. RR ) $=
    ( cr wcel wa cc0 wne cdiv co wi cded wceq opreq1 eleq1d bilimd neeq1
    opreq2 bi2imd ax0re elimel divrclz dedth2h imp ) ACDZBCDZEBFGZABHIZCDZUDUEU
    FUHJUFUDAFKZBHIZCDZJUEBFKZFGZUIULHIZCDZJABFFAUILZUHUKUFUPUGUJCAUIBHMNOBULLZ
    UFUMUKUOBULFPUQUJUNCBULUIHQNRUIULAFCSTBFCSTUAUBUC $.
    $( [27-Sep-99] $)


