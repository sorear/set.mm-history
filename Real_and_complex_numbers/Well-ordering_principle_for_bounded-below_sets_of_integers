$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Well-ordering principle for bounded-below sets of integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d z v B $.  $d x y v R $.  $d y z $.
    uzwo3lem.1 $e |- R = { z e. ZZ | B <_ z } $.
    $( Lemma for ~ uzwo3 and ~ zmin . $)
    uzwo3lem1 $p |- ( B e. RR -> E! x e. R A. y e. R x <_ y ) $=
      ( vv cr wcel cv clt wbr cz wrex cle wral wi wa crab c0 wreu btwnz wss wne
      simpld zre w3a ltletr ltle 3adant2 syld syl3an3 syl3an1 exp5o com34 imp32
      r19.21aiv ss2rab sylibr syl5eqss simprd sylan2 neeq1i rabn0 bitr2i syl6ib
      reximdva mpd ad2antrl jca ex uzwo5OLD exp3a com12 r19.23adv ) DHIZGJZDKLZ
      GMNZAJBJOLBEPAEUAZVPVSDCJZKLZCMNZGCDUBZUEVPVRVTGMVQMIZVPVRVTQWEVPVRVTWEVP
      VRRZEVQWAOLZCMSZUCZETUDZRZVTWEWFWKWEWFRZWIWJWLEDWAOLZCMSZWHFWLWMWGQZCMPWN
      WHUCWLWOCMWEVPVRWAMIZWOQWEVPWPVRWOWEVPWPVRWMWGWEVQHIZVPWPVRWMRZWGQZVQUFWP
      WQVPWAHIZWSWAUFZWQVPWTUGWRVQWAKLZWGVQDWAUHWQWTXBWGQVPVQWAUIUJUKULUMUNUOUP
      UQWMWGCMURUSUTVPWJWEVRVPWCWJVPVSWCWDVAVPWCWMCMNZWJVPWBWMCMWPVPWTWBWMQXADW
      AUIVBVGWJWNTUDXCEWNTFVCWMCMVDVEVFVHVIVJVKWEWKVTABCEVQVLVKUKVMVNVOVH $.
      $( [10-Nov-2004] $)

    ${
      $d x y z w v u t $.  $d x y A $.  $d t B $.  $d y t S $.  $d w R $.
      uzwo3lem.2 $e |- S = U. { w e. R | A. v e. R w <_ v } $.
      $( Lemma for ~ uzwo3 . $)
      uzwo3lem2 $p |- ( ( B e. RR /\ ( A C_ R /\ A =/= (/) ) )
                    -> E! x e. A A. y e. A x <_ y ) $=
        ( vt wcel wss cv cle wbr wral cz crab cr c0 wreu wi breq2 cbvrabv eqtri
        wne wa elrab2 uzwo3lem1 cuni wceq ralbidv cbvralv syl6bb reuuni3 breq1i
        breq1 ralbii sylibr syl rcla4v syl5com syl5bir exp3a r19.21aiv syl5eqss
        ss2rab sstr2 reucl syl5eqel simplbi uzwo5OLD exp32 3syl syld imp32 ) GU
        AMZFHNZFUBUHZAOZBOZPQZBFRAFUCZVSVTFILOZPQZLSTZNZWAWEUDZVSHWHNVTWIVSHGWF
        PQZLSTZWHHGCOZPQZCSTWLJWNWKCLSWMWFGPUEZUFUGZVSWKWGUDZLSRWLWHNVSWQLSVSWF
        SMZWKWGWRWKUIWFHMZVSWGWNWKCWFSHWOJUJVSIWCPQZBHRZWSWGVSWDBHRZAHUCZXAABCG
        HJUKXCDOZEOZPQZEHRZDHTULZWCPQZBHRZXAXBXGXJADHWBXDUMZXBXDWCPQZBHRXGXKWDX
        LBHWBXDWCPUSUNXLXFBEHWCXEXDPUEUOUPWBXHUMWDXIBHWBXHWCPUSUNUQWTXIBHIXHWCP
        KURUTVAVBWTWGBWFHWCWFIPUEVCVDVEVFVGWKWGLSVIVAVHFHWHVJVDVSIHMZISMZWIWJUD
        VSIXHHKVSXGDHUCXHHMDECGHJUKXGDHVKVBVLXMXNGIPQZWKXOLISHWFIGPUEWPUJVMXNWI
        WAWEABLFIVNVOVPVQVR $.
        $( [12-Nov-2004] $)
    $}
  $}

  ${
    $d x y u t A $.  $d z w v u t B $.
    $( Well-ordering principle: any non-empty subset of upper integers has a
       unique least element.  This generalization of ~ uzwo2 allows the lower
       bound ` B ` to be any real number.  See also ~ nnwo and ~ nnwos . $)
    uzwo3 $p |- ( ( B e. RR /\ ( A C_ { z e. ZZ | B <_ z } /\ A =/= (/) ) )
                  -> E! x e. A A. y e. A x <_ y ) $=
      ( vu vt vw vv cr wcel cv cle wbr cz crab wa wral wreu eqid cuni uzwo3lem2
      wss c0 wne breq2 cbvralv reubii wceq breq1 ralbidv cbvreuv bitri sylib )
      EJKDECLMNCOPZUCDUDUEQQFLZGLZMNZGDRZFDSZALZBLZMNZBDRZADSZFGCHIDEUOHLILMNIU
      ORHUOPUAZUOTVFTUBUTUPVBMNZBDRZFDSVEUSVHFDURVGGBDUQVBUPMUFUGUHVHVDFADUPVAU
      IVGVCBDUPVAVBMUJUKULUMUN $.
      $( [12-Nov-2004] $)
  $}

  ${
    $d x y z A $.
    $( There is a unique smallest integer greater than or equal to a given real
       number. $)
    zmin $p |- ( A e. RR ->
               E! x e. ZZ ( A <_ x /\ A. y e. ZZ ( A <_ y -> x <_ y ) ) ) $=
      ( vz cr wcel cv cle wbr cz crab wral wreu wi weu breq2 elrab bitri df-reu
      wa uzwo3lem1 imbi1i impexp ralbii2 anbi12i anass eubii 3bitr4ri sylibr
      eqid ) CEFAGZBGZHIZBCDGZHIZDJKZLZAUPMZCUKHIZCULHIZUMNZBJLZTZAJMZABDCUPUPU
      JUAUKUPFZUQTZAOUKJFZVCTZAOURVDVFVHAVFVGUSTZVBTVHVEVIUQVBUOUSDUKJUNUKCHPQU
      MVABUPJULUPFZUMNULJFZUTTZUMNVKVANVJVLUMUOUTDULJUNULCHPQUBVKUTUMUCRUDUEVGU
      SVBUFRUGUQAUPSVCAJSUHUI $.
      $( [12-Nov-2004] $)
  $}

  ${
    $d x y z w A $.
    $( There is a unique largest integer less than or equal to a given real
       number. $)
    zmax $p |- ( A e. RR ->
               E! x e. ZZ ( x <_ A /\ A. y e. ZZ ( y <_ A -> y <_ x ) ) ) $=
      ( vz vw cr wcel cneg cv cle wbr wi cz wral wa wreu wb znegcl wceq imbi12d
      renegcl zmin syl leneg sylan ancoms breq1 rcla4v lenegcon1 adantrr sylan2
      zre adantrl biimpd com23 syld com13 r19.21adv breq2 exbiri impbid anbi12d
      ex reubidva cc negcon2 syl2an reuhyp imbi2d ralbidv reuxfr syl6rbbr mpbid
      zcn ) CFGZCHZDIZJKZVPEIZJKZVQVSJKZLZEMNZOZDMPZAIZCJKZBIZCJKZWHWFJKZLZBMNZ
      OZAMPZVOVPFGWECUADEVPUBUCVOWNVPWFHZJKZVTWOVSJKZLZEMNZOZAMPWEVOWMWTAMVOWFM
      GZOZWGWPWLWSXAVOWGWPQZXAWFFGZVOXCWFULZWFCUDUEUFXBWLWSXBWLWREMVSMGZWLXBWRX
      FWLVSHZCJKZXGWFJKZLZXBWRLXFXGMGWLXJLVSRWKXJBXGMWHXGSWIXHWJXIWHXGCJUGWHXGW
      FJUGTUHUCXFXBXJWRXFXBXJWRLXFXBOXJWRXFVSFGZXBXJWRQVSULXKXBOXHVTXIWQXKVOXHV
      TQXAVSCUIUJXKXAXIWQQZVOXAXKXDXLXEVSWFUIUKUMTUEUNVCUOUPUQURXBWSWKBMWHMGZWS
      XBWKXMWSVPWHHZJKZWOXNJKZLZXBWKLXMXNMGWSXQLWHRWRXQEXNMVSXNSVTXOWQXPVSXNVPJ
      USVSXNWOJUSTUHUCXMXBXQWKXMXBWKXQXMWHFGZXBWKXQQWHULXRXBOWIXOWJXPXRVOWIXOQX
      AWHCUDUJXRXAWJXPQZVOXAXRXDXSXEWHWFUDUKUMTUEUTUOUPUQURVAVBVDWDWTDAWOMWFRDA
      WOVQHZMVQRVQMGVQVEGWFVEGVQWOSZWFXTSQXAVQVNWFVNVQWFVFVGVHYAVRWPWCWSVQWOVPJ
      USYAWBWREMYAWAWQVTVQWOVSJUGVIVJVBVKVLVM $.
      $( [15-Nov-2004] $)
  $}

  ${
    $d x y z A $.
    $( There is a unique integer between a real number and the number plus
       one.  Exercise 5 of [Apostol] p. 28. $)
    zbtwnre $p |- ( A e. RR -> E! x e. ZZ ( A <_ x /\ x < ( A + 1 ) ) ) $=
      ( vy cr wcel cv cle wbr wi cz wa wreu c1 co clt wb zre syl sylan2 wn wral
      caddc zmin cmin peano2rem syl3an1 3expa zlem1lt adantlr sylibrd r19.21adv
      ltletr exp4b com23 ltnr 3syl peano2zm mpdan mtbird ad2antrr ancoms adantr
      lenlt wceq breq2 imbi12d rcla4v imp sylbird ex impbid 1re ltsubadd mp3an2
      mt3d sylan bitr3d anbi2d reubidva mpbid ) BDEZBAFZGHZBCFZGHZWBWDGHZIZCJUA
      ZKZAJLWCWBBMUBNOHZKZAJLACBUCWAWIWKAJWAWBJEZKWHWJWCWLWAWHWJPWLWAKZWBMUDNZB
      OHZWHWJWMWOWHWMWOWGCJWMWDJEZWOWGWMWPWOWEWFWMWPKWOWEKZWNWDOHZWFWPWMWDDEZWQ
      WRIZWDQWLWAWSWTWLWNDEZWAWSWTWLWBDEZXAWBQZWBUEZRZWNBWDULUFUGSWLWPWFWRPWAWB
      WDUHUIUJUMUNUKWMWHWOWMWHKZWOWBWNGHZWLXGTWAWHWLXGWNWNOHZWLXBXAXHTXCXDWNUOU
      PWLWNJEZXGXHPWBUQZWBWNUHURUSUTXFWOTZBWNGHZXGWMXLXKPZWHWAWLXMWLWAXAXMXEBWN
      VCSVAVBWLWHXLXGIZWAWLWHXNWLXIWHXNIXJWGXNCWNJWDWNVDWEXLWFXGWDWNBGVEWDWNWBG
      VEVFVGRVHUIVIVOVJVKWLXBWAWOWJPZXCXBMDEWAXOVLWBMBVMVNVPVQVAVRVSVT $.
      $( [13-Nov-2004] $)
  $}

  ${
    $d x y z A $.
    $( There is a unique greatest integer less than or equal to a real number.
       Exercise 4 of [Apostol] p. 28. $)
    rebtwnz $p |- ( A e. RR -> E! x e. ZZ ( x <_ A /\ A < ( x + 1 ) ) ) $=
      ( vy cr wcel cneg cv cle wbr c1 caddc co clt wa cz wreu znegcl cc wceq wb
      renegcl zbtwnre syl zcn negcon2 syl2an reuhyp breq2 anbi12d reuxfr ancoms
      breq1 zre leneg peano2rem ltneg sylan 1re ltsubadd mp3an2 ax-1cn negsubdi
      cmin sylancl adantr breq2d 3bitr3d sylan2 bicomd reubidva syl5bb mpbid
      recn ) BDEZBFZCGZHIZVPVOJKLZMIZNZCOPZAGZBHIZBWBJKLMIZNZAOPZVNVODEWABUACVO
      UBUCWAVOWBFZHIZWGVRMIZNZAOPVNWFVTWJCAWGOWBQCAWGVPFZOVPQVPOEVPREWBREVPWGSZ
      WBWKSTWBOEZVPUDWBUDVPWBUEUFUGWLVQWHVSWIVPWGVOHUHVPWGVRMULUIUJVNWJWEAOVNWM
      NWEWJWMVNWBDEZWEWJTWBUMVNWNNZWCWHWDWIWNVNWCWHTWBBUNUKWOBJVCLZWBMIZWGWPFZM
      IZWDWIVNWPDEWNWQWSTBUOWPWBUPUQVNJDEWNWQWDTURBJWBUSUTWOWRVRWGMVNWRVRSZWNVN
      BREJREWTBVMVABJVBVDVEVFVGUIVHVIVJVKVL $.
      $( [15-Nov-2004] $)
  $}

