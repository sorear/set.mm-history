$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Well-ordering principle for bounded-below sets of integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d z v B $.  $d x y v R $.  $d y z $.
    uzwo3lem.1 $e |- R = { z e. ZZ | B <_ z } $.
    $( Lemma for ~ uzwo3 and ~ zmin . $)
    uzwo3lem1 $p |- ( B e. RR -> E! x e. R A. y e. R x <_ y ) $=
      ( vv cr wcel cv clt wbr cz wrex cle wral wreu btwnz pm3.26d wi wa crab
      wss c0 wne w3a ltletrt ltlet 3adant2 syld zret syl3an3 syl3an1 exp3a
      3exp com34 imp32 r19.21aiv ss2rab sylibr syl5ss pm3.27d sylan2 r19.22dva
      neeq1i rabn0 bitr2 syl6ib mpd ad2antrl jca ex uzwo5OLD com12 r19.23adv )
      DHIZGJZDKLZGMNZAJBJOLBEPAEQZVPVSDCJZKLZCMNZGCDRZSVPVRVTGMVQMIZVPVRVTTWEVP
      VRVTWEVPVRUAZEVQWAOLZCMUBZUCZEUDUEZUAZVTWEWFWKWEWFUAZWIWJWLDWAOLZCMUBZWHE
      WLWMWGTZCMPWNWHUCWLWOCMWEVPVRWAMIZWOTWEVPWPVRWOWEVPWPVRWOTWEVPWPUFVRWMWGV
      QHIZVPWPVRWMUAZWGTZWEWQVPWAHIZWSWPWQVPWTUFWRVQWAKLZWGVQDWAUGWQWTXAWGTVPVQ
      WAUHUIUJWAUKZULVQUKUMUNUOUPUQURWMWGCMUSUTFVAVPWJWEVRVPWCWJVPVSWCWDVBVPWCW
      MCMNZWJVPWBWMCMVPWTWBWMTWPDWAUHXBVCVDWJWNUDUEXCEWNUDFVEWMCMVFVGVHVIVJVKVL
      WEWKVTABCEVQVMVLUJUNVNVOVI $.
      $( [30-Nov-2004] $) $( [10-Nov-2004] $)

    ${
      $d x y z w v u t $.  $d x y A $.  $d t B $.  $d y t S $.  $d w R $.
      uzwo3lem.2 $e |- S = U. { w e. R | A. v e. R w <_ v } $.
      $( Lemma for ~ uzwo3 . $)
      uzwo3lem2 $p |- ( ( B e. RR /\ ( A (_ R /\ A =/= (/) ) )
                    -> E! x e. A A. y e. A x <_ y ) $=
        ( vt cr wcel wss c0 wne cv cle wbr wral wreu cz crab wi sstr2 wa breq2
        rcla4v uzwo3lem1 cuni wceq breq1 ralbidv cbvralv syl6bb reuuni3 breq1i
        ralbii sylibr syl syl5com elrab2 syl5ibr exp3a r19.21aiv ss2rab
        cbvrabv eqtr syl5ss reucl syl5eqel pm3.26bi uzwo5OLD exp32 3syl syld
        imp32 ) GMNZFHOZFPQZARZBRZSTZBFUAAFUBZVSVTFILRZSTZLUCUDZOZWAWEUEZVTHWHO
        WIVSFHWHUFVSGWFSTZLUCUDZWHHVSWKWGUEZLUCUAWLWHOVSWMLUCVSWFUCNZWKWGVSWFHN
        ZWGWNWKUGWOIWCSTZBHUAZWGVSWPWGBWFHWCWFISUHUIVSWDBHUAZAHUBZWQABCGHJUJWSD
        RZERZSTZEHUAZDHUDUKZWCSTZBHUAZWQWRXCXFADHWBWTULZWRWTWCSTZBHUAXCXGWDXHBH
        WBWTWCSUMUNXHXBBEHWCXAWTSUHUOUPWBXDULWDXEBHWBXDWCSUMUNUQWPXEBHIXDWCSKUR
        USUTVAVBGCRZSTZWKCWFUCHXIWFGSUHZJVCVDVEVFWKWGLUCVGUTHXJCUCUDWLJXJWKCLUC
        XKVHVIZVJVBVSIHNZIUCNZWIWJUEVSXDHIVSXCDHUBXDHNDECGHJUJXCDHVKVAKVLXMXNGI
        STZWKXOLIUCHWFIGSUHXLVCVMXNWIWAWEABLFIVNVOVPVQVR $.
        $( [30-Nov-2004] $) $( [12-Nov-2004] $)
    $}
  $}

  ${
    $d x y u t A $.  $d z w v u t B $.
    $( Well-ordering principle:  any non-empty subset of upper integers has a
       unique least element.  This generalization of ~ uzwo2 allows the
       lower bound ` B ` to be any real number.  See also ~ nnwo and
       ~ nnwos . $)
    uzwo3 $p |- ( ( B e. RR /\ ( A (_ { z e. ZZ | B <_ z } /\ A =/= (/) ) )
                  -> E! x e. A A. y e. A x <_ y ) $=
      ( vu vt vw vv cr wcel cv cle wbr cz crab wss c0 wne wa wral wreu cuni
      eqid uzwo3lem2 breq2 cbvralv reubii wceq breq1 ralbidv cbvreuv bitr
      sylib ) EJKDECLMNCOPZQDRSTTFLZGLZMNZGDUAZFDUBZALZBLZMNZBDUAZADUBZFGCHIDEU
      OHLILMNIUOUAHUOPUCZUOUDVFUDUEUTUPVBMNZBDUAZFDUBVEUSVHFDURVGGBDUQVBUPMUFUG
      UHVHVDFADUPVAUIVGVCBDUPVAVBMUJUKULUMUN $.
      $( [30-Nov-2004] $) $( [12-Nov-2004] $)
  $}

  ${
    $d x y z A $.
    $( There is a unique smallest integer greater than or equal to a given real
       number. $)
    zmin $p |- ( A e. RR ->
               E! x e. ZZ ( A <_ x /\ A. y e. ZZ ( A <_ y -> x <_ y ) ) ) $=
      ( vz cr wcel cv cle wbr cz crab wral wreu wi wa eqid uzwo3lem1 weu breq2
      elrab imbi1i impexp bitr ralbii2 anbi12i anass eubii df-reu 3bitr4r
      sylibr ) CEFAGZBGZHIZBCDGZHIZDJKZLZAUPMZCUKHIZCULHIZUMNZBJLZOZAJMZABDCUPU
      PPQUKUPFZUQOZARUKJFZVCOZARURVDVFVHAVFVGUSOZVBOVHVEVIUQVBUOUSDUKJUNUKCHSTU
      MVABUPJULUPFZUMNULJFZUTOZUMNVKVANVJVLUMUOUTDULJUNULCHSTUAVKUTUMUBUCUDUEVG
      USVBUFUCUGUQAUPUHVCAJUHUIUJ $.
      $( [1-Dec-2004] $) $( [12-Nov-2004] $)
  $}

  ${
    $d x y z w A $.
    $( There is a unique largest integer less than or equal to a given real
       number. $)
    zmax $p |- ( A e. RR ->
               E! x e. ZZ ( x <_ A /\ A. y e. ZZ ( y <_ A -> y <_ x ) ) ) $=
      ( vz vw cr wcel cneg cv cle wbr wi cz wral wa wreu renegclt zmin syl wb
      lenegt zret sylan ancoms znegclt wceq breq1 imbi12d rcla4v lenegcon1t
      adantrr sylan2 adantrl biimpd ex com23 syld com13 r19.21adv breq2
      biimprd impbid anbi12d reubidva cc negcon2t zcnt syl2an reuhyp imbi2d
      ralbidv reuxfr syl6rbbr mpbid ) CFGZCHZDIZJKZVPEIZJKZVQVSJKZLZEMNZOZDMPZA
      IZCJKZBIZCJKZWHWFJKZLZBMNZOZAMPZVOVPFGWECQDEVPRSVOWNVPWFHZJKZVTWOVSJKZLZE
      MNZOZAMPWEVOWMWTAMVOWFMGZOZWGWPWLWSXAVOWGWPTZWFFGZVOXCXAWFCUAWFUBZUCUDXBW
      LWSXBWLWREMVSMGZWLXBWRXFWLVSHZCJKZXGWFJKZLZXBWRLXFXGMGWLXJLVSUEWKXJBXGMWH
      XGUFWIXHWJXIWHXGCJUGWHXGWFJUGUHUISXFXBXJWRXFXBXJWRLXFXBOXJWRVSFGZXBXJWRTX
      FXKXBOXHVTXIWQXKVOXHVTTXAVSCUJUKXKXAXIWQTZVOXKXDXLXAVSWFUJXEULUMUHVSUBUCU
      NUOUPUQURUSXBWSWKBMWHMGZWSXBWKXMWSVPWHHZJKZWOXNJKZLZXBWKLXMXNMGWSXQLWHUEW
      RXQEXNMVSXNUFVTXOWQXPVSXNVPJUTVSXNWOJUTUHUISXMXBXQWKXMXBXQWKLXMXBOWKXQWHF
      GZXBWKXQTXMXRXBOWIXOWJXPXRVOWIXOTXAWHCUAUKXRXAWJXPTZVOXRXDXSXAWHWFUAXEULU
      MUHWHUBUCVAUOUPUQURUSVBVCVDWDWTDAWOMWFUEDAWOVQHZMVQUEVQVEGWFVEGVQWOUFZWFX
      TUFTVQMGXAVQWFVFVQVGWFVGVHVIYAVRWPWCWSVQWOVPJUTYAWBWREMYAWAWQVTVQWOVSJUGV
      JVKVCVLVMVN $.
      $( [1-Dec-2004] $) $( [15-Nov-2004] $)
  $}

  ${
    $d x y z A $.
    $( There is a unique integer between a real number and the number plus
       one.  Exercise 5 of [Apostol] p. 28. $)
    zbtwnre $p |- ( A e. RR -> E! x e. ZZ ( A <_ x /\ x < ( A + 1 ) ) ) $=
      ( vy cr wcel cv cle wbr wi cz wral wa wreu c1 caddc co clt zmin wb cmin
      ltletrt zret peano2rem syl syl3an1 3expa sylan2 zlem1ltt adantlr sylibrd
      exp4b com23 r19.21adv wn ltnrt 3syl peano2zm mpdan mtbird ad2antrr
      lenltt ancoms adantr wceq breq2 imbi12d rcla4v imp sylbird mt3d ex
      impbid 1re ltsubaddt mp3an2 sylan bitr3d anbi2d reubidva mpbid ) BDEZBAFZ
      GHZBCFZGHZWBWDGHZIZCJKZLZAJMWCWBBNOPQHZLZAJMACBRWAWIWKAJWAWBJEZLWHWJWCWLW
      AWHWJSWLWALZWBNTPZBQHZWHWJWMWOWHWMWOWGCJWMWDJEZWOWGWMWPWOWEWFWMWPLWOWELZW
      NWDQHZWFWMWDDEZWQWRIZWPWLWAWSWTWNDEZWAWSWTWLWNBWDUAWLWBDEZXAWBUBZWBUCZUDZ
      UEUFWDUBUGWLWPWFWRSWAWBWDUHUIUJUKULUMWMWHWOWMWHLZWOWBWNGHZWLXGUNWAWHWLXGW
      NWNQHZWLXBXAXHUNXCXDWNUOUPWLWNJEZXGXHSWBUQZWBWNUHURUSUTXFWOUNZBWNGHZXGWMX
      LXKSZWHWAWLXMWAXAXMWLBWNVAXEUGVBVCWLWHXLXGIZWAWLWHXNWLXIWHXNIXJWGXNCWNJWD
      WNVDWEXLWFXGWDWNBGVEWDWNWBGVEVFVGUDVHUIVIVJVKVLXBWAWOWJSZWLXBNDEWAXOVMWBN
      BVNVOXCVPVQVBVRVSVT $.
      $( [4-Dec-2004] $) $( [13-Nov-2004] $)
  $}

  ${
    $d x y z A $.
    $( There is a unique greatest integer less than or equal to a real number.
       Exercise 4 of [Apostol] p. 28. $)
    rebtwnz $p |- ( A e. RR -> E! x e. ZZ ( x <_ A /\ A < ( x + 1 ) ) ) $=
      ( vy cr wcel cneg cv cle wbr c1 caddc co clt wa cz wreu renegclt zbtwnre
      syl wb lenegt ancoms cmin ltnegt peano2rem sylan 1re ltsubaddt mp3an2
      wceq cc recnt ax1cn negsubdit mpan2 adantr breq2d 3bitr3d anbi12d zret
      sylan2 bicomd reubidva znegclt negcon2t zcnt syl2an reuhyp breq2 breq1
      reuxfr syl5bb mpbid ) BDEZBFZCGZHIZVPVOJKLZMIZNZCOPZAGZBHIZBWBJKLMIZNZAOP
      ZVNVODEWABQCVORSVNVOWBFZHIZWGVRMIZNZAOPWFWAVNWJWEAOVNWBOEZNWEWJVNWBDEZWEW
      JTWKVNWLNZWCWHWDWIWLVNWCWHTWBBUAUBWMBJUCLZWBMIZWGWNFZMIZWDWIWNDEWLWOWQTVN
      WNWBUDBUEUFVNJDEWLWOWDTUGBJWBUHUIWMWPVRWGMVNWPVRUJZWLVNBUKEZWRBULWSJUKEWR
      UMBJUNUOSUPUQURUSWBUTVAVBVCVTWJCAWGOWBVDCAWGVPFZOVPVDVPUKEWBUKEVPWGUJZWBW
      TUJTVPOEWKVPWBVEVPVFWBVFVGVHXAVQWHVSWIVPWGVOHVIVPWGVRMVJUSVKVLVM $.
      $( [6-Dec-2004] $) $( [15-Nov-2004] $)
  $}

