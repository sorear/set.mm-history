$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Square root
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c sqr $. $( Positive square root of a positive real number. $)

  $( Extend class notation to include positive square root of a positive real
     number. $)
  csqr $a class sqr $.

  ${
    $d x y z w A $.
    $( Define a function whose value is the square root of a nonnegative
       real number.  The square root of ` x ` is the supremum of all reals
       whose square is less than ` x ` .  See ~ sqrcli for its closure,
       ~ sqrval for its value, ~ sqrsqi and ~ sqsqri for its relationship to
       squares, and ~ sqr11i for uniqueness. $)
    df-sqr $a |- sqr = { <. x , y >. | ( ( x e. RR /\ 0 <_ x ) /\
     y = sup ( { z e. RR | ( 0 <_ z /\ ( z x. z ) <_ x ) } , RR , < ) ) } $.

    $( Value of square root function. $)
    sqrval $p |- ( ( A e. RR /\ 0 <_ A ) -> ( sqr ` A ) =
          sup ( { x e. RR | ( 0 <_ x /\ ( x x. x ) <_ A ) } , RR , < ) ) $=
      ( vw vy vz cr wcel cc0 cle wbr wa cv crab csqr cfv cmul co clt csup wceq
      breq2 elrab anbi2d rabbisdv supeq1 syl copab df-sqr anbi1i opabbii
      eqtr4i ltso supex fvopab4 sylbir ) BFGHBIJZKBHCLZIJZCFMZGBNOHALZIJZUTUTPQ
      ZBIJZKZAFMZFRSZTURUPCBFUQBHIUAUBDEBVAVBDLZIJZKZAFMZFRSZVFUSNVGBTZVJVETVKV
      FTVLVIVDAFVLVHVCVAVGBVBIUAUCUDFVJVERUEUFNVGFGHVGIJZKZELVKTZKZDEUGVGUSGZVO
      KZDEUGDEAUHVRVPDEVQVNVOURVMCVGFUQVGHIUAUBUIUJUKFVERULUMUNUO $.
      $( [23-May-1999] $)
  $}

  $( Square root of zero. $)
  sqr0 $p |- ( sqr ` 0 ) = 0 $=
    ( vx cc0 csqr cfv cv cle wbr cmul co wa cr crab clt csup csn wcel wceq 0re
    leidi sqrval mp2an cab wn wb remulcl anidms jctir lenlt syl wne msqgt0 ex
    necon1bd sylbid adantld imp 0cn mul01i remulcli eqlei ax-mp pm3.2i eleq1
    breq2 opreq12 breq1d anbi12d mpbiri impbii abbii df-rab df-sn 3eqtr4i
    supeq1 ltso supsn 3eqtri ) BCDZBAEZFGZVSVSHIZBFGZJZAKLZKMNZBOZKMNZBBKPZBBFG
    ZVRWEQRBRSZABTUAWDWFQWEWGQVSKPZWCJZAUBVSBQZAUBWDWFWLWMAWLWMWKWCWMWKWBWMVTWK
    WBBWAMGZUCZWMWKWAKPZWHJWBWOUDWKWPWHWKWPVSVSUEUFRUGWABUHUIWKWNVSBWKVSBUJWNVS
    UKULUMUNUOUPWMWLWHWIBBHIZBFGZJZJWHWSRWIWRWJWQBQWRBUQURWQBBBRRUSRUTVAVBVBWMW
    KWHWCWSVSBKVCWMVTWIWBWRVSBBFVDWMWAWQBFWMWAWQQVSBVSBHVEUFVFVGVGVHVIVJWCAKVKA
    BVLVMKWDWFMVNVAWHWGBQRKBMVOVPVAVQ $.
    $( [23-May-1999] $)

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w S $.  $d x y z C $.  $d x D $.
    sqrlem1.1 $e |- A e. RR $.
    sqrlem1.2 $e |- 0 < A $.
    $( Lemma for square root theorem. $)
    sqrlem1 $p |- A < ( ( 1 + A ) x. ( 1 + A ) ) $=
      ( c1 caddc co clt wbr cmul cc0 recni addid2i lt01 0re 1re ltadd1i mpbi
      eqbrtrri readdcli mulid2i ax1cn addid1i ltadd2i addgt0ii ltmul1ii
      remulcli lttri mp2an ) ADAEFZGHUIUIUIIFZGHAUJGHJAEFZAUIGAABKLJDGHUKUIGHMJ
      DANOBPQRDUIIFZUIUJGUIUIDAOBSZKTDUIGHULUJGHDJEFZDUIGDUAUBJAGHUNUIGHCJADNBO
      UCQRDUIUIOUMUMDAOBMCUDUEQRAUIUJBUMUIUIUMUMUFUGUH $.
      $( [16-May-1999] $)

    $( Lemma for square root theorem. $)
    sqrlem2 $p |- ( ( A / ( 1 + A ) ) x. ( A / ( 1 + A ) ) ) < A $=
      ( cmul co c1 caddc cdiv clt wbr sqrlem1 cc0 wb 1re readdcli remulcli
      ltmul2i ax-mp mpbi 0re lttri mp2an ltdiv1ii recni lt01 addgt0ii gt0ne0ii
      divmuldivi divassi dividi opreq2i mulid1i 3eqtrri 3brtr4i ) AADEZFAGEZUPD
      EZHEZAUQDEZUQHEZAUPHEZVADEAIUOUSIJZURUTIJAUQIJZVBABCKZLAIJZVCVBMCAUQABUPU
      PFANBOZVFPZBQRSUOUSUQAABBPAUQBVGPVGVEVCLUQIJCVDLAUQTBVGUAUBZUCSAUPAUPABUD
      ZUPVFUDZVIVJUPVFFANBUECUFUGZVKUHUTAUQUQHEZDEAFDEAAUQUQVIUQVGUDZVMUQVGVHUG
      ZUIVLFADUQVMVNUJUKAVIULUMUN $.
      $( [16-May-1999] $)

    $( Lemma for square root theorem. $)
    sqrlem3 $p |- 0 < ( A / ( 1 + A ) ) $=
      ( c1 caddc co 1re readdcli lt01 addgt0ii divgt0ii ) ADAEFBDAGBHCDAGBICJK
      $.
      $( [16-May-1999] $)

    ${
      sqrlem4.3 $e |- S = { x e. RR | ( 0 <_ x /\ ( x x. x ) <_ A ) } $.
      $( Lemma for square root theorem. $)
      sqrlem4 $p |- ( D e. S <->
                    ( D e. RR /\ ( 0 <_ D /\ ( D x. D ) <_ A ) ) ) $=
        ( cc0 cv cle wbr cmul co wa cr wceq breq2 opreq12 anidms breq1d
        anbi12d elrab2 ) HAIZJKZUCUCLMZBJKZNHCJKZCCLMZBJKZNACODUCCPZUDUGUFUIUCC
        HJQUJUEUHBJUJUEUHPUCCUCCLRSTUAGUB $.
        $( [21-May-1999] $)

      $( Lemma for square root theorem. $)
      sqrlem5 $p |- ( ( D e. RR /\ ( 0 < D /\ ( D x. D ) < A ) ) ->
                        D e. S ) $=
        ( cr wcel cc0 clt wbr cmul co wa cle wi 0re ltle mpan remulcl anidms
        sylan mpan2 anim12d imdistani sqrlem4 sylibr ) CHIZJCKLZCCMNZBKLZOZOUIJ
        CPLZUKBPLZOZOCDIUIUMUPUIUJUNULUOJHIUIUJUNQRJCSTUIBHIZULUOQZEUKHIZUQURUI
        UKBSUIUSCCUAUBUCUDUEUFABCDEFGUGUH $.
        $( [23-May-1999] $)

      $( Lemma for square root theorem. $)
      sqrlem6 $p |- ( S (_ RR /\ S =/= (/) /\
                     E. x e. RR A. y e. S y < x ) $=
        ( cr wss c0 wne cv clt wbr wral wrex cc0 cle cmul co wa crab ssrab2
        eqsstri wceq wn wcel sqrlem4 0re leidi 0cn mul01i ltleii eqbrtri
        pm3.2i mpbir2an n0i ax-mp df-ne mpbir c1 caddc 1re readdcli wo wi wb
        leloe mpan cif breq2 opreq12 anidms breq1d breq1 imbi12d lt01 addgt0ii
        elimel lt2msqi ltlei syl2an mpan2 biimprd dedth mpbii a1d a1i jaod
        sylbid remulcl sqrlem1 remulcli axlttrn mp3an3 mpan2i mpbiri syl syl5d
        imp32 sylbi rgen ralbidv rcla4ev mp2an 3pm3.2i ) DHIDJKZBLZALZMNZBDOZAH
        PZDQXIRNXIXISTCRNUAZAHUBHGXMAHUCUDXGDJUEUFZQDUGZXNXOQHUGZQQRNZQQSTZCRNZ
        UAACQDEFGUHUIXQXSQUIUJXRQCRQUKULQCUIEFUMUNUOUPDQUQURDJUSUTVACVBTZHUGXHX
        TMNZBDOZXLVACVCEVDZYABDXHDUGXHHUGZQXHRNZXHXHSTZCRNZUAUAYAACXHDEFGUHYDYE
        YGYAYDYEYFXTXTSTZMNZYAYGYDYEQXHMNZQXHUEZVEZYIYAVFZXPYDYEYLVGUIQXHVHVIYD
        YJYMYKYDYJYMVFQYDXHVAVJZMNZYNYNSTZYHMNZYNXTMNZVFZVFXHVAXHYNUEZYJYOYMYSX
        HYNQMVKYTYIYQYAYRYTYFYPYHMYTYFYPUEXHYNXHYNSVLVMVNXHYNXTMVOVPVPYOYRYQYOQ
        XTMNZYRYQVGZVACVCEVQFVRZQYNRNQXTRNUUBYOUUAYNXTXHVAHVCVSZYCVTQYNUIUUDWAQ
        XTUIYCWAWBWCWDWEYKYMVFYDYKYAYIYKUUAYAUUCQXHXTMVOWFWGWHWIWJYDYFHUGZYGYIV
        FZYDUUEXHXHWKVMUUECHUGZUUFEUUEUUGUAZYGYFCMNZYFCUEZVEYIYFCVHUUHUUIYIUUJU
        UHUUICYHMNZYICEFWLZUUEUUGYHHUGUUIUUKUAYIVFXTXTYCYCWMYFCYHWNWOWPUUJYIVFU
        UHUUJYIUUKUULYFCYHMVOWQWHWIWJWCWRWSWTXAXBXKYBAXTHXIXTUEXJYABDXIXTXHMVKX
        CXDXEXF $.
        $( [21-May-1999] $)

      ${
        sqrlem7.4 $e |- B = sup ( S , RR , < ) $.
        $( Lemma for square root theorem. $)
        sqrlem7 $p |- B e. RR $=
          ( vy vz cr clt csup ltso wss c0 wne cv wbr wral wrex w3a wn wi wa
          sqrlem6 axsup ax-mp supcli eqeltri ) CDKLMKHAIJKDLNDKODPQIRZARZLSZIDT
          AKUAUBULUKLSUCIDTUMUKJRLSJDUAUDIKTUEAKUAAIBDEFGUFAIJDUGUHUIUJ $.
          $( [24-May-1999] $)

        $( Lemma for square root theorem. $)
        sqrlem8 $p |- 0 < B $=
          ( vy vz cc0 cr clt csup c1 caddc co cdiv wbr cle sqrlem3 wn wcel
          cmul wa 1re readdcli lt01 addgt0ii gt0ne0ii redivcli sqrlem2 pm3.2i
          sqrlem5 mp2an ltso wss c0 wne cv wral wrex w3a wi sqrlem6 axsup
          ax-mp supubi sqrlem7 eqeltrri lenlti mpbir 0re ltletri breqtrri ) KDL
          MNZCMKBOBPQZRQZMSZVRVPTSZKVPMSBEFUAZVTVPVRMSUBZVRDUCZWBVRLUCVSVRVRUDQ
          BMSZUEWCBVQEOBUFEUGZVQWEOBUFEUHFUIUJUKZVSWDWABEFULUMABVRDEFGUNUOAIJLD
          VRMUPDLUQDURUSIUTZAUTZMSZIDVAALVBVCWHWGMSUBIDVAWIWGJUTMSJDVBVDILVAUEA
          LVBAIBDEFGVEAIJDVFVGVHVGVRVPWFCVPLHABCDEFGHVIVJZVKVLKVRVPVMWFWJVNUOHV
          O $.
          $( [24-May-1999] $)

      $}
    $}

    ${
      sqrlem9.3 $e |- B e. RR $.
      sqrlem9.4 $e |- C e. RR $.
      sqrlem9.5 $e |- 0 < B $.
      sqrlem9.6 $e |- A < ( B x. B ) $.
      sqrlem9.7 $e |- C = ( ( B + ( A / B ) ) / ( 1 + 1 ) ) $.
      $( Lemma for square root theorem. $)
      sqrlem9 $p |- 0 < C $=
        ( cc0 cdiv co caddc c1 clt gt0ne0ii redivcli readdcli 1re divgt0ii
        addgt0ii lt01 breqtrri ) KBABLMZNMZOONMZLMCPUFUGBUEFABDFBFHQRZSOOTTSBUE
        FUHHABDFEHUAUBOOTTUCUCUBUAJUD $.
        $( [17-May-1999] $)

      $( Lemma for square root theorem. $)
      sqrlem10 $p |- C < B $=
        ( cdiv co caddc c1 clt cmul wbr remulcli ltdiv1ii mpbi recni gt0ne0ii
        divcan4i breqtri redivcli ltadd2i 1re readdcli mulcomi 1p1timesi eqtri
        breqtrri lt01 addgt0ii eqbrtri ) CBABKLZMLZNNMLZKLZBOJUSBURPLZURKLZBOUQ
        UTOQUSVAOQUQBBMLZUTOUPBOQUQVBOQUPBBPLZBKLZBOAVCOQUPVDOQIAVCBDBBFFRFHSTB
        BBFUAZVEBFHUBZUCUDUPBBABDFVFUEZFFUFTUTURBPLVBBURVEURNNUGUGUHZUAZUIBVEUJ
        UKULUQUTURBUPFVGUHBURFVHRVHNNUGUGUMUMUNZSTBURVEVIURVHVJUBUCUDUO $.
        $( [17-May-1999] $)

      $( Lemma for square root theorem. $)
      sqrlem11 $p |- A < ( C x. C ) $=
        ( c1 caddc co cmul cdiv clt wbr cneg cc0 cmin remulcli recni 1re
        readdcli negcli renegcli mul12i mulneg1i 1p1timesi negeqi df-neg eqtri
        3eqtri negidi ltadd1i mpbi eqbrtrri mulgt0ii muladdi breqtri 0re
        ltsubaddi mpbir eqbrtri ltdiv1ii gt0ne0ii muln0i divcan3i mul2negi
        divdiri 3brtr3i redivcli mulcomi mulassi divcan4i eqcomi divmuldivi
        opreq1i eqtr4i opreq12i mulcli divcan2i opreq2i eqtr3i 3brtr4i lt01
        addgt0ii eqtr2i ) AKKLMZWINMZNMZWJOMZBABOMZLMZWNNMZWJOMZACCNMZPWKWOPQWL
        WPPQWIANMZWRLMZBBNMZWTNMZWTOMZARZXCNMZWTOMZLMZWRLMZWKWOPWRXFPQWSXGPQWTW
        IRZXCNMZNMZWTOMZXAXDLMZWTOMZWRXFPXJXLPQXKXMPQXJSWTXCNMZXNLMZTMZXLPXJXHX
        NNMWIXNNMZRZXPWTXHXCWTBBFFUAZUBZWIWIKKUCUCUDZUBZUEXCADUFZUBZUGWIXNYBXNW
        TXCXSYCUAZUBZUHXRXORXPXQXOXNYFUIUJXOUKULUMXPXLPQSXLXOLMZPQSWTXCLMZYHNMY
        GPYHYHWTXCXSYCUDZYIAXCLMZSYHPAADUBZUNAWTPQYJYHPQIAWTXCDXSYCUOUPUQZYLURW
        TXCWTXCXTYDXTYDUSUTSXOXLVAXNXNYEYEUDXAXDWTWTXSXSUAZXCXCYCYCUAZUDZVBVCVD
        XJXLWTWTXIXSXHXCWIYAUFYCUAZUAYOXSBBFFHHURZVEUPXKXIWRXIWTXIYPUBXTBBBFUBZ
        YRBFHVFZYSVGVHWIAYBYKVIULXAXDWTXAYMUBXDYNUBXTWTXSYQVFZVJVKWRXFWRWIAYADU
        AZXBXEXAWTYMXSYTVLXDWTYNXSYTVLUDUUAUOUPWKWJANMWIWRNMWSAWJYKWJWIWIYAYAUA
        ZUBZVMWIWIAYBYBYKVNWRWRUUAUBUIUMWOWTWMWMNMZLMZBWMNMZUUFLMZLMXGBWMBWMYRW
        MABDFYSVLZUBZYRUUIUSUUEXFUUGWRLWTXBUUDXELXBWTWTWTXTXTYTVOVPUUDAANMZWTOM
        XEABABYKYRYKYRYSYSVQXDUUJWTOAAYKYKVIVRVSVTWIUUFNMUUGWRUUFBWMYRUUIWAUIUU
        FAWINABYKYRYSWBWCWDVTULWEWKWOWJAWJDUUBUAWNWNBWMFUUHUDZUUKUAUUBWIWIYAYAK
        KUCUCWFWFWGZUULURZVEUPAWJYKUUCWJUUBUUMVFVOWQWNWIOMZUUNNMWPCUUNCUUNNJJVT
        WNWIWNWIWNUUKUBZYBUUOYBWIYAUULVFZUUPVQWHVK $.
        $( [18-May-1999] $)

      ${
        sqrlem12.8 $e |- S = { x e. RR | ( 0 <_ x /\ ( x x. x ) <_ A ) } $.

        $( Lemma for square root theorem. $)
        sqrlem12 $p |- ( D e. S -> D < C ) $=
          ( wcel cc0 clt wbr wceq wo cle cmul co cr wa sqrlem4 pm3.27bi
          pm3.26d wb pm3.26bi 0re leloe mpan syl mpbid pm3.27d sqrlem11 wi
          remulcl anidms remulcli lelttr mp3an23 3syl mpan2i mpd adantr
          sqrlem9 cif breq2 anbi1d breq1 opreq12 breq1d bibi12d imbi12d elimel
          lt2msqi ltlei syl2an dedth imp mpbird ex mpbii a1i jaod ) EFOZPEQRZPE
          SZTZEDQRZWHPEUARZWKWHWMEEUBUCZBUARZWHEUDOZWMWOUEZABEFGHNUFZUGZUHWHWPW
          MWKUIZWHWPWQWRUJZPUDOWPWTUKPEULUMUNUOWHWIWLWJWHWIWLWHWIUEWLWNDDUBUCZQ
          RZWHXCWIWHWOXCWHWMWOWSUPWHWOBXBQRZXCBCDGHIJKLMUQWHWPWNUDOZWOXDUEXCURZ
          XAWPXEEEUSUTXEBUDOXBUDOXFGDDJJVAWNBXBVBVCVDVEVFVGWHWIWLXCUIZWHWIPDQRZ
          XGBCDGHIJKLMVHZWHWPWIXHUEZXGURZXAWPXKPWPEPVIZQRZXHUEZXLDQRZXLXLUBUCZX
          BQRZUIZUREPEXLSZXJXNXGXRXSWIXMXHEXLPQVJVKXSWLXOXCXQEXLDQVLXSWNXPXBQXS
          WNXPSEXLEXLUBVMUTVNVOVPPXLUARPDUARXRXMXHXLDEPUDUKVQZJVRPXLUKXTVSPDUKJ
          VSVTWAUNVEWBWCWDWJWLURWHWJXHWLXIPEDQVLWEWFWGVF $.
          $( [23-May-1999] $)

        $( Lemma for square root theorem. $)
        sqrlem13 $p |- ( B = sup ( S , RR , < ) -> -. C < B ) $=
          ( vz vy cr clt csup wceq wbr wcel cv wn wral wo wa sqrlem12 wb cc0
          cle cmul co sqrlem4 pm3.26bi axlttri mpan2 syl mpbid ioran sylib
          pm3.27d rgen ltso wss c0 wne wrex w3a wi sqrlem6 axsup ax-mp supnubi
          mp2an breq2 mtbiri ) CEPQRZSDCQTDVQQTZDPUAZDNUBZQTZUCZNEUDVRUCIWBNEVT
          EUAZVTDSZUCZWBWCWDWAUEUCZWEWBUFWCVTDQTZWFABCDVTEFGHIJKLMUGWCVTPUAZWGW
          FUHZWCWHUIVTUJTVTVTUKULBUJTUFABVTEFGMUMUNWHVSWIIVTDUOUPUQURWDWAUSUTVA
          VBAONPEDQVCEPVDEVEVFOUBZAUBZQTZOEUDAPVGVHWKWJQTUCOEUDWLWJVTQTNEVGVIOP
          UDUFAPVGAOBEFGMVJAONEVKVLVMVNCVQDQVOVP $.
          $( [25-May-1999] $)

        $( Lemma for square root theorem. $)
        sqrlem14 $p |- -. B = sup ( S , RR , < ) $=
          ( cr clt csup wceq wbr sqrlem10 sqrlem13 mt2 ) CENOPQDCORBCDFGHIJKLSA
          BCDEFGHIJKLMTUA $.
          $( [25-May-1999] $)
      $}

    $}

    ${
      sqrlem15.3 $e |- B e. RR $.
      sqrlem15.4 $e |- 0 < B $.

      ${
        sqrlem15.5 $e |- C e. RR $.
        sqrlem15.6 $e |- 0 < C $.
        $( Lemma for square root theorem. $)
        sqrlem15 $p |- B < ( B + C ) $=
          ( cc0 caddc co clt recni addid1i wbr 0re ltadd2i mpbi eqbrtrri ) BJKL
          ZBBCKLZMBBFNOJCMPUAUBMPIJCBQHFRST $.
          $( [24-May-1999] $)

        ${
          sqrlem16.7 $e |- C < B $.
          $( Lemma for square root theorem. $)
          sqrlem16 $p |- (  C < ( ( A - ( B x. B ) ) /
                          ( ( ( 1 + 1 ) + 1 ) x. B ) ) ->
                          ( ( B + C ) x. ( B + C ) ) < A ) $=
            ( cmul co cmin c1 caddc cdiv clt wbr 1re readdcli recni mulassi
            remulcli resubcli lt01 addgt0ii gt0ne0ii muln0i divcan2i breq12i
            cc0 wb mulgt0ii redivcli ltmul2i ax-mp ltaddsubi 3bitr4i mpbi
            ltadd2i ltadd1i muladdi addcomi ax1cn adddiri 1p1timesi mulid2i
            mulcomi eqtri opreq12i 3eqtri opreq1i addassi 3eqtr4i 3brtr4i
            lttri mpan sylbi ) CABBKLZMLZNNOLZNOLZBKLZPLZQRZWBBCKLZKLZVSOLZAQRZ
            BCOLZWJKLZAQRZWCCKLZWCWDKLZQRZWGVTQRWEWIWMWGWNVTQWBBCWBWANNNSSTZSTZ
            UAZBFUAZCHUAZUBVTWCVTAVSDBBFFUCZUDZUAWCWBBWQFUCZUAWBBWRWSWBWQWANWPS
            NNSSUEUEUFUEUFZUGBFGUGUHZUIUJUKWCQRWEWOULWBBWQFXDGUMCWDWCHVTWCXBXCX
            EUNXCUOUPWGVSAWBWFWQBCFHUCZUCZXADUQURWKWHQRWIWLVSCCKLZOLZWFWFOLZOLZ
            VSCBKLZOLZXJOLZWKWHQXIXMQRZXKXNQRXHXLQRZXOCBQRZXPJUKCQRXQXPULICBCHF
            HUOUPUSXHXLVSCCHHUCZCBHFUCZXAUTUSXIXMXJVSXHXAXRTVSXLXAXSTWFWFXFXFTZ
            VAUSBCBCWSWTWSWTVBXLXJOLZVSOLVSYAOLWHXNYAVSYAXLXJXSXTTUAVSXAUAZVCWG
            YAVSOWGWAWFKLZNWFKLZOLXJXLOLYAWANWFWAWPUAVDWFXFUAZVEYCXJYDXLOWFYEVF
            YDWFXLWFYEVGBCWSWTVHVIVJXJXLXJXTUAZXLXSUAZVCVKVLVSXLXJYBYGYFVMVNVOW
            KWHAWJWJBCFHTZYHUCWGVSXGXATDVPVQVR $.
            $( [26-May-1999] $)

          ${
            sqrlem17.8 $e |- S =
                             { x e. RR | ( 0 <_ x /\ ( x x. x ) <_ A ) } $.
            $( Lemma for square root theorem. $)
            sqrlem17 $p |- (  C < ( ( A - ( B x. B ) ) /
                          ( ( ( 1 + 1 ) + 1 ) x. B ) ) ->
                          ( B + C ) e. S ) $=
              ( cmul co cmin c1 caddc cdiv clt wbr cc0 wa wcel sqrlem16
              addgt0ii jctil cr readdcli sqrlem5 mpan syl ) DBCCNOPOQQROQROCNOS
              OTUAZUBCDROZTUAZUNUNNOBTUAZUCZUNEUDZUMUPUOBCDFGHIJKLUECDHJIKUFUGU
              NUHUDUQURCDHJUIABUNEFGMUJUKUL $.
              $( [26-May-1999] $)

            $( Lemma for square root theorem. $)
            sqrlem18 $p |- (  C < ( ( A - ( B x. B ) ) /
                          ( ( ( 1 + 1 ) + 1 ) x. B ) ) ->
                          -. B = sup ( S , RR , < ) ) $=
              ( vy vz cmul co cmin c1 caddc cdiv clt wbr cr csup wceq sqrlem15
              wn breq1 notbid wcel sqrlem17 ltso wss c0 wne cv wral wrex w3a
              wi wa sqrlem6 axsup ax-mp supubi syl syl5cbir mt2i ) DBCCPQRQSSTQ
              STQCPQUAQUBUCZCEUDUBUEZUFZCCDTQZUBUCZBCDFGHIJKUGVLVNUHVKVMUBUCZUH
              ZVJVLVNVOCVKVMUBUIUJVJVMEUKVPABCDEFGHIJKLMULANOUDEVMUBUMEUDUNEUOU
              PNUQZAUQZUBUCZNEURAUDUSUTVRVQUBUCUHNEURVSVQOUQUBUCOEUSVANUDURVBAU
              DUSANBEFGMVCANOEVDVEVFVGVHVI $.
              $( [26-May-1999] $)

          $}
        $}
      $}

      ${
        sqrlem19.5 $e |- ( B x. B ) < A $.
        $( Lemma for square root theorem. $)
        sqrlem19 $p |- 0 < ( ( A - ( B x. B ) ) /
                      ( ( ( 1 + 1 ) + 1 ) x. B ) ) $=
          ( cmul co cmin c1 caddc remulcli resubcli 1re readdcli cc0 clt wbr
          recni addid2i eqbrtri 0re ltaddsubi mpbi lt01 addgt0ii mulgt0ii
          divgt0ii ) ABBHIZJIZKKLIZKLIZBHIAUJCBBEEMZNUMBULKKKOOPZOPZEMQUJLIZARS
          QUKRSUQUJARUJUJUNTUAGUBQUJAUCUNCUDUEUMBUPEULKUOOKKOOUFUFUGUFUGFUHUI
          $.
          $( [19-May-1999] $)

        ${
          sqrlem20.6 $e |- S =
                             { x e. RR | ( 0 <_ x /\ ( x x. x ) <_ A ) } $.
          $( Lemma for square root theorem. $)
          sqrlem20 $p |- -. B = sup ( S , RR , < ) $=
            ( vw cc0 cv clt wbr cmul co cmin c1 caddc cdiv wa cr wrex csup
            wceq wn remulcli resubcli 1re readdcli recni lt01 addgt0ii
            gt0ne0ii muln0i redivcli sqrlem19 posexi wcel wi w3a cif breq1
            imbi1d eleq1 breq2 3anbi123d divgt0ii halfposi mpbi 3pm3.2i
            elimhyp 3simp1i 3simp2i 3simp3i sqrlem18 dedth 3exp imp4d
            r19.23aiv ax-mp ) LKMZNOZWCCNOZWCBCCPQZRQZSSTQZSTQZCPQZUAQZNOZUBUBZ
            KUCUDCDUCNUEUFUGZKCWKGWGWJBWFECCGGUHUIWICWHSSSUJUJUKZUJUKZGUHWICWIW
            PULCGULWIWPWHSWOUJSSUJUJUMUMUNZUMUNUOCGHUOUPUQHBCEFGHIURUSWMWNKUCWC
            UCUTZWDWEWLWNWRWDWEWLWNVAZWRWDWEVBZWSWTWCCWHUAQZVCZWKNOZWNVAWCXAWCX
            BUFZWLXCWNWCXBWKNVDVEABCXBDEFGHXBUCUTZLXBNOZXBCNOZWTXEXFXGVBXAUCUTZ
            LXANOZXACNOZVBWCXAXDWRXEWDXFWEXGWCXBUCVFWCXBLNVGWCXBCNVDVHXAXBUFXHX
            EXIXFXJXGXAXBUCVFXAXBLNVGXAXBCNVDVHXHXIXJCWHGWOWHWOWQUOUQCWHGWOHWQV
            ILCNOXJHCGVJVKVLVMZVNXEXFXGXKVOXEXFXGXKVPJVQVRVSVTWAWB $.
            $( [26-May-1999] $)
        $}
      $}
    $}

    ${
      sqrlem21.3 $e |- S = { x e. RR | ( 0 <_ x /\ ( x x. x ) <_ A ) } $.
      sqrlem21.4 $e |- B = sup ( S , RR , < ) $.
      $( Lemma for square root theorem. $)
      sqrlem21 $p |- -. A < ( B x. B ) $=
        ( cmul co clt wbr cr csup wceq wn c1 caddc cif eqeq1 notbid cdiv
        sqrlem7 1re readdcli keepel wcel id opreq2 opreq12d opreq1d eleq1d
        sqrlem8 gt0ne0ii redivcli lt01 addgt0ii keephyp cc0 breq2 opreq12
        anidms breq2d sqrlem1 elimhyp eqid sqrlem14 dedth mt2 ) BCCIJZKLZCDMKNZ
        OZHVKVMPVKCQBRJZSZVLOZPCVNCVOOZVMVPCVOVLTUAABVOVOBVOUBJZRJZQQRJZUBJZDEF
        VKCVNMABCDEFGHUCZQBUDEUEZUFVKCBCUBJZRJZVTUBJZMUGVNBVNUBJZRJZVTUBJZMUGWA
        MUGCVNVQWFWAMVQWEVSVTUBVQCVOWDVRRVQUHCVOBUBUIUJUKULVNVOOZWIWAMWJWHVSVTU
        BWJVNVOWGVRRWJUHVNVOBUBUIUJUKULWEVTCWDWBBCEWBCWBABCDEFGHUMZUNUOUEQQUDUD
        UEZVTWLQQUDUDUPUPUQUNZUOWHVTVNWGWCBVNEWCVNWCQBUDEUPFUQZUNUOUEWLWMUOURVK
        USCKLUSVNKLUSVOKLCVNCVOUSKUTVNVOUSKUTWKWNURVKBVOVOIJZKLBVNVNIJZKLCVNVQV
        JWOBKVQVJWOOCVOCVOIVAVBVCWJWPWOBKWJWPWOOVNVOVNVOIVAVBVCBEFVDVEWAVFGVGVH
        VI $.
        $( [24-May-1999] $)

      $( Lemma for square root theorem. $)
      sqrlem22 $p |- -. ( B x. B ) < A $=
        ( cmul co clt wbr cr csup wceq wn c1 caddc cdiv cif eqeq1 notbid
        sqrlem7 1re readdcli lt01 addgt0ii gt0ne0ii redivcli keepel cc0 breq2
        sqrlem8 sqrlem3 keephyp opreq12 anidms breq1d sqrlem2 elimhyp sqrlem20
        dedth mt2 ) CCIJZBKLZCDMKNZOZHVEVGPVECBQBRJZSJZTZVFOZPCVICVJOZVGVKCVJVF
        UAUBABVJDEFVECVIMABCDEFGHUCBVHEQBUDEUEZVHVMQBUDEUFFUGUHUIUJVEUKCKLUKVIK
        LUKVJKLCVICVJUKKULVIVJUKKULABCDEFGHUMBEFUNUOVEVJVJIJZBKLVIVIIJZBKLCVIVL
        VDVNBKVLVDVNOCVJCVJIUPUQURVIVJOZVOVNBKVPVOVNOVIVJVIVJIUPUQURBEFUSUTGVAV
        BVC $.
        $( [26-May-1999] $)

      $( Lemma for square root theorem. $)
      sqrlem23 $p |- ( B x. B ) = A $=
        ( cmul co wceq clt wbr wn sqrlem7 remulcli lttri3i sqrlem22 sqrlem21
        mpbir2an ) CCIJZBKUABLMNBUALMNUABCCABCDEFGHOZUBPEQABCDEFGHRABCDEFGHST
        $.
        $( [26-May-1999] $)
    $}

    $( Lemma for square root closure. $)
    sqrlem24 $p |- ( sqr ` A ) e. RR $=
      ( vy vx csqr cfv cc0 cv cle wbr cmul co wa cr crab wceq breq2 opreq12
      anidms breq1d anbi12d cbvrabv wcel clt csup 0re ltleii sqrval mp2an
      sqrlem7 ) DAAFGZHEIZJKZUMUMLMZAJKZNZEOPZBCUQHDIZJKZUSUSLMZAJKZNEDOUMUSQZU
      NUTUPVBUMUSHJRVCUOVAAJVCUOVAQUMUSUMUSLSTUAUBUCAOUDHAJKULUROUEUFQBHAUGBCUH
      EAUIUJUK $.
      $( [26-May-1999] $)

    $( The square root of a positive real is positive. $)
    sqrgt0ii $p |- 0 < ( sqr ` A ) $=
      ( vy vx csqr cfv cc0 cv cle wbr cmul co wa cr crab wceq breq2 opreq12
      anidms breq1d anbi12d cbvrabv wcel clt csup 0re ltleii sqrval mp2an
      sqrlem8 ) DAAFGZHEIZJKZUMUMLMZAJKZNZEOPZBCUQHDIZJKZUSUSLMZAJKZNEDOUMUSQZU
      NUTUPVBUMUSHJRVCUOVAAJVCUOVAQUMUSUMUSLSTUAUBUCAOUDHAJKULUROUEUFQBHAUGBCUH
      EAUIUJUK $.
      $( [26-May-1999] $)

    $( Lemma for square root theorem. $)
    sqrlem26 $p |- ( ( sqr ` A ) x. ( sqr ` A ) ) = A $=
      ( vy vx csqr cfv cc0 cv cle wbr cmul co wa cr crab wceq breq2 opreq12
      anidms breq1d anbi12d cbvrabv wcel clt csup 0re ltleii sqrval mp2an
      sqrlem23 ) DAAFGZHEIZJKZUMUMLMZAJKZNZEOPZBCUQHDIZJKZUSUSLMZAJKZNEDOUMUSQZ
      UNUTUPVBUMUSHJRVCUOVAAJVCUOVAQUMUSUMUSLSTUAUBUCAOUDHAJKULUROUEUFQBHAUGBCU
      HEAUIUJUK $.
      $( [24-May-1999] $)

  $}

  ${
    sqrth.1 $e |- A e. RR $.
    $( Square root theorem.  Theorem I.35 of [Apostol] p. 29.

       (A bit of trivia:  This theorem was added to the database before the
       number ` 2 ` was defined and before exponents were defined.  Thus
       you will see ` ( 1 + 1 ) ` and ` ( x x. x ) ` throughout its lemmas.) $)
    sqrthi $p |- ( 0 <_ A -> ( ( sqr ` A ) x. ( sqr ` A ) ) = A ) $=
      ( cc0 cle wbr clt wceq wo csqr cfv cmul co 0re leloei c1 cif fveq2
      opreq12d id eqeq12d cr 1re keepel elimgt0 sqrlem26 dedth sqr0 opreq12i
      0cn mul01i eqtri mpbii jaoi sylbi ) CADECAFEZCAGZHAIJZUQKLZAGZCAMBNUOUSUP
      UOUSUOAOPZIJZVAKLZUTGAOAUTGZURVBAUTVCUQVAUQVAKAUTIQZVDRVCSTUTUOAOUABUBUCA
      UDUEUFUPCIJZVEKLZCGUSVFCCKLCVECVECKUGUGUHCUIUJUKUPVFURCAUPVEUQVEUQKCAIQZV
      GRUPSTULUMUN $.
      $( [26-May-1999] $)

    $( The square root of a nonnegative real is a real. $)
    sqrcli $p |- ( 0 <_ A -> ( sqr ` A ) e. RR ) $=
      ( cc0 cle wbr clt wceq wo csqr cfv cr wcel 0re leloei c1 cif fveq2
      eleq1d 1re keepel elimgt0 sqrlem24 dedth sqr0 syl5eqr syl6eqelr jaoi
      sylbi ) CADECAFEZCAGZHAIJZKLZCAMBNUIULUJUIULUIAOPZIJZKLAOAUMGUKUNKAUMIQRU
      MUIAOKBSTAUAUBUCUJUKCKUJCIJUKCCAIQUDUEMUFUGUH $.
      $( [26-May-1999] $)

    $( The square root of a positive real is positive. $)
    sqrgt0i $p |- ( 0 < A -> 0 < ( sqr ` A ) ) $=
      ( cc0 clt wbr csqr cfv c1 cif wceq fveq2 breq2d cr 1re keepel elimgt0
      sqrgt0ii dedth ) CADEZCAFGZDECSAHIZFGZDEAHAUAJTUBCDAUAFKLUASAHMBNOAPQR $.
      $( [26-May-1999] $)

    $( The square root of a nonnegative real is nonnegative. $)
    sqrge0i $p |- ( 0 <_ A -> 0 <_ ( sqr ` A ) ) $=
      ( cc0 cle wbr clt wceq wo csqr cfv 0re leloei cr wcel ltlei sqrcli syl
      wb leloe mpan sqrgt0i orcd syl5bir mpcom fveq2 sqr0 syl5eqr leidi
      syl5breq jaoi sylbi ) CADEZCAFEZCAGZHCAIJZDEZCAKBLUMUPUNUOMNZUMUPUMULUQCA
      KBOABPQUQUPCUOFEZCUOGZHZUMCMNUQUPUTRKCUOSTUMURUSABUAUBUCUDUNCUOCDUNCIJUOC
      CAIUEUFUGCKUHUIUJUK $.
      $( [26-May-1999] $)

    ${
      sqr11.1 $e |- B e. RR $.
      $( The square root function is one-to-one. $)
      sqr11i $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                 ( ( sqr ` A ) = ( sqr ` B ) <-> A = B ) ) $=
        ( cc0 cle wbr wa csqr cfv wceq cmul co opreq12 anidms adantl sqrthi
        ad2antrr ad2antlr 3eqtr3d ex fveq2 impbid1 ) EAFGZEBFGZHZAIJZBIJZKZABKZ
        UFUIUJUFUIHUGUGLMZUHUHLMZABUIUKULKZUFUIUMUGUHUGUHLNOPUDUKAKUEUIACQRUEUL
        BKUDUIBDQSTUAABIUBUC $.
        $( [27-Jul-1999] $)

      ${
        sqrmuli.1 $e |- 0 <_ A $.
        sqrmuli.2 $e |- 0 <_ B $.
        $( Square root distributes over multiplication. $)
        sqrmulii $p |- ( sqr ` ( A x. B ) ) = ( ( sqr ` A ) x. ( sqr ` B ) ) $=
          ( cmul co csqr cfv wceq cc0 cle wbr sqrthi ax-mp opreq12i cr wcel
          sqrcli recni mul4i mulge0i mp2an remulcli 3eqtr4ri wb sqrge0i msq11i
          mpbi ) ABGHZIJZULGHZAIJZBIJZGHZUPGHZKZULUPKZUNUNGHZUOUOGHZGHUKUQUMUTA
          VABGLAMNZUTAKEACOPLBMNZVABKFBDOPQUNUOUNUOUNVBUNRSEACTPZUAZUOVCUORSFBD
          TPZUAZVEVGUBLUKMNZUMUKKVBVCVHEFABCDUCUDZUKABCDUEZOPUFLULMNZLUPMNZURUS
          UGVHVKVIUKVJUHPLUNMNZLUOMNZVLVBVMEACUHPVCVNFBDUHPUNUOVDVFUCUDULUPVHUL
          RSVIUKVJTPUNUOVDVFUEUIUDUJ $.
          $( [30-Jul-1999] $)
      $}

      $( Square root distributes over multiplication. $)
      sqrmuli $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                 ( sqr ` ( A x. B ) ) = ( ( sqr ` A ) x. ( sqr ` B ) ) ) $=
        ( cc0 cle wbr cmul co csqr cfv wceq cif opreq1 fveq2d fveq2 opreq1d
        eqeq12d opreq2 opreq2d cr 0re keepel elimge0 sqrmulii dedth2h ) EAFGZEB
        FGZABHIZJKZAJKZBJKZHIZLUGAEMZBHIZJKZUNJKZULHIZLUNUHBEMZHIZJKZUQUSJKZHIZ
        LABEEAUNLZUJUPUMURVDUIUOJAUNBHNOVDUKUQULHAUNJPQRBUSLZUPVAURVCVEUOUTJBUS
        UNHSOVEULVBUQHBUSJPTRUNUSUGAEUACUBUCUHBEUADUBUCAUDBUDUEUF $.
        $( [30-Jul-1999] $)

      $( Relationship between square root and squares. $)
      sqrmsq2i $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
         ( ( sqr ` A ) = B <-> A = ( B x. B ) ) ) $=
        ( cc0 cle wbr wa csqr cfv cmul co wceq wb sqrmuli anidms sqrthi eqtrd
        eqeq2d adantl msqge0i remulcli sqr11i mpan2 adantr bitr3d ) EAFGZEBFGZH
        AIJZBBKLZIJZMZUIBMZAUJMZUHULUMNUGUHUKBUIUHUKBIJZUOKLZBUHUKUPMBBDDOPBDQR
        STUGULUNNZUHUGEUJFGUQBDUAAUJCBBDDUBUCUDUEUF $.
        $( [31-Jul-1999] $)

      $( Square root is monotonic. $)
      sqrlei $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
          ( A <_ B <-> ( sqr ` A ) <_ ( sqr ` B ) ) ) $=
        ( cc0 cle wbr wa csqr cfv cmul co cr wcel wb le2msq sqrcli sqrge0i jca
        syl2an sqrthi breqan12d bitr2d ) EAFGZEBFGZHAIJZBIJZFGZUFUFKLZUGUGKLZFG
        ZABFGUFMNZEUFFGZHUGMNZEUGFGZHUHUKOUDUEUFUGPUDULUMACQACRSUEUNUOBDQBDRSTU
        DUEUIAUJBFACUABDUAUBUC $.
        $( [26-Jul-2006] $) $( [3-Aug-1999] $)

      $( Square root is strictly monotonic.  (Contributed by Roy F. Longton,
         8-Aug-2005.) $)
      sqrlti $p |- ( ( 0 <_ A /\ 0 <_ B ) -> ( A < B <-> ( sqr ` A ) <
        ( sqr ` B ) ) ) $=
        ( cc0 cle wbr wa csqr cfv clt cmul co cr wcel wb lt2msq sqrcli sqrge0i
        jca syl2an sqrthi breqan12d bitr2d ) EAFGZEBFGZHAIJZBIJZKGZUGUGLMZUHUHL
        MZKGZABKGUGNOZEUGFGZHUHNOZEUHFGZHUIULPUEUFUGUHQUEUMUNACRACSTUFUOUPBDRBD
        STUAUEUFUJAUKBKACUBBDUBUCUD $.
        $( [26-Jul-2006] $) $( [8-Aug-2005] $)
    $}

    $( Square root of square. $)
    sqrmsqi $p |- ( 0 <_ A -> ( sqr ` ( A x. A ) ) = A ) $=
      ( cc0 cmul co cle wbr csqr cfv wceq msqge0i wa eqid remulcli sqrmsq2i
      mpbiri mpan ) CAADEZFGZCAFGZRHIAJZABKSTLUARRJRMRAAABBNBOPQ $.
      $( [2-Aug-1999] $)
  $}

  $( The square root of a nonnegative real is a real. $)
  sqrcl $p |- ( ( A e. RR /\ 0 <_ A ) -> ( sqr ` A ) e. RR ) $=
    ( cr wcel cc0 cle wbr csqr cfv wi cif wceq breq2 fveq2 eleq1d imbi12d 0re
    elimel sqrcli dedth imp ) ABCZDAEFZAGHZBCZUAUBUDIDUAADJZEFZUEGHZBCZIADAUEKZ
    UBUFUDUHAUEDELUIUCUGBAUEGMNOUEADBPQRST $.
    $( [29-May-1999] $)

  $( The square root of a positive real is positive. $)
  sqrgt0 $p |- ( ( A e. RR /\ 0 < A ) -> 0 < ( sqr ` A ) ) $=
    ( cr wcel cc0 clt wbr csqr cfv wi cif wceq breq2 fveq2 breq2d imbi12d 0re
    elimel sqrgt0i dedth imp ) ABCZDAEFZDAGHZEFZUAUBUDIDUAADJZEFZDUEGHZEFZIADAU
    EKZUBUFUDUHAUEDELUIUCUGDEAUEGMNOUEADBPQRST $.
    $( [29-May-1999] $)

  $( The square root of a nonnegative real is nonnegative. $)
  sqrge0 $p |- ( ( A e. RR /\ 0 <_ A ) -> 0 <_ ( sqr ` A ) ) $=
    ( cr wcel cc0 cle wbr csqr cfv wi cif wceq breq2 fveq2 breq2d imbi12d 0re
    elimel sqrge0i dedth imp ) ABCZDAEFZDAGHZEFZUAUBUDIDUAADJZEFZDUEGHZEFZIADAU
    EKZUBUFUDUHAUEDELUIUCUGDEAUEGMNOUEADBPQRST $.
    $( [29-May-1999] $)

  $( Square root is monotonic. $)
  sqrle $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 0 <_ A /\ 0 <_ B ) ) ->
      ( A <_ B <-> ( sqr ` A ) <_ ( sqr ` B ) ) ) $=
    ( cr wcel wa cc0 cle wbr csqr cfv wb wi cif wceq breq2 anbi1d breq1 fveq2
    breq1d bibi12d imbi12d anbi2d breq2d 0re elimel sqrlei dedth2h imp ) ACDZBC
    DZEFAGHZFBGHZEZABGHZAIJZBIJZGHZKZUIUJUMURLFUIAFMZGHZULEZUSBGHZUSIJZUPGHZKZL
    UTFUJBFMZGHZEZUSVFGHZVCVFIJZGHZKZLABFFAUSNZUMVAURVEVMUKUTULAUSFGOPVMUNVBUQV
    DAUSBGQVMUOVCUPGAUSIRSTUABVFNZVAVHVEVLVNULVGUTBVFFGOUBVNVBVIVDVKBVFUSGOVNUP
    VJVCGBVFIRUCTUAUSVFAFCUDUEBFCUDUEUFUGUH $.
    $( [3-Apr-2005] $) $( [17-Mar-2005] $)

  $( A square root is zero iff its argument is 0. $)
  sqr00 $p |- ( ( A e. RR /\ 0 <_ A ) ->
               ( ( sqr ` A ) = 0 <-> A = 0 ) ) $=
    ( cr wcel cc0 cle wbr wa csqr cfv wceq wb 0re leidi wi cif breq2 anbi1d
    fveq2 eqeq1d eqeq1 bibi12d imbi12d elimel sqr11i dedth mpan2i imp sqr0
    eqeq2i syl5bbr ) ABCZDAEFZGAHIZDHIZJZADJZUMDJUKULUOUPKZUKULDDEFZUQDLMUKULUR
    GZUQNDUKADOZEFZURGZUTHIZUNJZUTDJZKZNADAUTJZUSVBUQVFVGULVAURAUTDEPQVGUOVDUPV
    EVGUMVCUNAUTHRSAUTDTUAUBUTDADBLUCLUDUEUFUGUNDUMUHUIUJ $.
    $( [27-Jul-1999] $)

  $( The square root of a positive real is a postive real. $)
  rpsqrcl $p |- ( A e. RR+ -> ( sqr ` A ) e. RR+ ) $=
    ( crp wcel csqr cfv cr cc0 clt wbr wa cle sqrcl rpre rpge0 sylanc sqrgt0
    rpgt0 jca elrp sylibr ) ABCZADEZFCZGUBHIZJUBBCUAUCUDAFCZGAKIUCUAALAMZANOUEG
    AHIUDUAAPUFAQORUBST $.
    $( [12-Mar-2008] $) $( [22-Feb-2008] $)

  $( The square root of 1 is 1. $)
  sqr1 $p |- ( sqr ` 1 ) = 1 $=
    ( c1 cmul co csqr cfv ax1cn mulid1i fveq2i cc0 cle wbr wceq 0re 1re lt01
    ltleii sqrmsqi ax-mp eqtr3i ) AABCZDEZADEATADAFGHIAJKUAALIAMNOPANQRS $.
    $( [31-Jul-1999] $)

  $( The square root of 4 is 2. $)
  sqr4 $p |- ( sqr ` 4 ) = 2 $=
    ( c4 csqr cfv c2 wceq cmul co 2t2e4 eqcomi cc0 clt wbr wb 4pos 2pos cle
    4re 2re sqrmsq2i 0re ltlei syl2an mp2an mpbir ) ABCDEZADDFGZEZUFAHIJAKLZJDK
    LZUEUGMZNOJAPLJDPLUJUHUIADQRSJATQUAJDTRUAUBUCUD $.
    $( [3-Aug-1999] $)

  $( The square root of 9 is 3. $)
  sqr9 $p |- ( sqr ` 9 ) = 3 $=
    ( c9 csqr cfv c3 wceq cmul co 3t3e9 eqcomi cc0 clt wbr wb 9pos 3pos cle
    9re 3re sqrmsq2i 0re ltlei syl2an mp2an mpbir ) ABCDEZADDFGZEZUFAHIJAKLZJDK
    LZUEUGMZNOJAPLJDPLUJUHUIADQRSJATQUAJDTRUAUBUCUD $.
    $( [1-Jun-2004] $) $( [11-May-2004] $)

  $( The square root of 2 is bounded by 1 and 2.  (Contributed by Roy F.
     Longton, 8-Aug-2005.) $)
  sqr2gt1lt2 $p |- ( 1 < ( sqr ` 2 ) /\ ( sqr ` 2 ) < 2 ) $=
    ( c1 c2 csqr cfv clt wbr sqr1 1lt2 cc0 cle wb 0re 1re lt01 ltleii 2re 2pos
    sqrlti mp2an mpbi eqbrtrri c4 caddc co sqrlem15 2p2e4 breqtri 4re 4pos
    sqr4 pm3.2i ) ABCDZEFULBEFACDZAULEGABEFZUMULEFZHIAJFIBJFZUNUOKIALMNOIBLPQOZ
    ABMPRSTUAULUBCDZBEBUBEFZULUREFZBBBUCUDUBEBBBPQPQPQUEUFUGUPIUBJFUSUTKUQIUBLU
    HUIOBUBPUHRSTUJUGUK $.
    $( [8-Aug-2005] $) $( [8-Aug-2005] $)

  ${
    sqrsq.1 $e |- A e. RR $.
    $( Square root of square. $)
    sqrsqi $p |- ( 0 <_ A -> ( sqr ` ( A ^ 2 ) ) = A ) $=
      ( cc0 cle wbr cmul co csqr cfv c2 cexp sqrmsqi recni sqvali fveq2i
      syl5eq ) CADEAAFGZHIAAJKGZHIABLRQHAABMNOP $.
      $( [11-Aug-1999] $)

    $( Square of square root. $)
    sqsqri $p |- ( 0 <_ A -> ( ( sqr ` A ) ^ 2 ) = A ) $=
      ( cc0 cle wbr csqr cfv c2 cexp co cmul cc wcel wceq sqrcli recnd sqval
      syl sqrthi eqtrd ) CADEZAFGZHIJZUBUBKJZAUAUBLMUCUDNUAUBABOPUBQRABST $.
      $( [11-Aug-1999] $)
  $}

  $( Square root of square. $)
  sqrsq $p |- ( ( A e. RR /\ 0 <_ A ) -> ( sqr ` ( A ^ 2 ) ) = A ) $=
    ( cr wcel cc0 cle wbr c2 cexp co csqr cfv wceq wi cif breq2 opreq1 fveq2d
    id eqeq12d imbi12d 0re elimel sqrsqi dedth imp ) ABCZDAEFZAGHIZJKZALZUFUGUJ
    MDUFADNZEFZUKGHIZJKZUKLZMADAUKLZUGULUJUOAUKDEOUPUIUNAUKUPUHUMJAUKGHPQUPRSTU
    KADBUAUBUCUDUE $.
    $( [4-Feb-2007] $) $( [14-Jan-2006] $)

  $( Square of square root. $)
  sqsqr $p |- ( ( A e. RR /\ 0 <_ A ) -> ( ( sqr ` A ) ^ 2 ) = A ) $=
    ( cr wcel cc0 cle wbr csqr cfv c2 cexp co wceq wi cif breq2 fveq2 opreq1d
    id eqeq12d imbi12d 0re elimel sqsqri dedth imp ) ABCZDAEFZAGHZIJKZALZUFUGUJ
    MDUFADNZEFZUKGHZIJKZUKLZMADAUKLZUGULUJUOAUKDEOUPUIUNAUKUPUHUMIJAUKGPQUPRSTU
    KADBUAUBUCUDUE $.
    $( [27-Sep-1999] $)

$(   Only true for certain real values
  @{
    quad.1 @e |- A e. CC @.
    quad.2 @e |- B e. CC @.
    quad.3 @e |- C e. CC @.
    @( The quadratic equation. @)
    quad @p |- ( ( ( A ^ 2 ) + ( ( B x. A ) + C ) ) = 0 <->
    ( A = ( ( B + ( sqr ( ( B ^ 2 ) - ( 4 x. ( A x. C ) ) ) ) ) / 2 ) \/
    A = ( ( B - ( sqr ( ( B ^ 2 ) - ( 4 x. ( A x. C ) ) ) ) ) / 2 ) ) ) @=
      ( c2 cexp co cmul caddc cc0 wceq jaoi impbii ) AGHIBAJICKIKILMZ???P???NO
      @.
      @( [ ?] @) @( [25-Apr-2006] @)
  @}
$)


