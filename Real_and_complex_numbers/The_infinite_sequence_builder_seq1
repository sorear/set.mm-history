$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The infinite sequence builder "seq1"
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u $.  $d z w v u G $.  $d z w v u A $.  $d z w v u B $.
    $d x y z w v u C $.
    om2uz.1 $e |- C e. ZZ $.
    om2uz.2 $e |- G = ( rec ( { <. x , y >. | y = ( x + 1 ) } , C ) |` om ) $.
    $( The mapping ` G ` is a one-to-one mapping from ` om ` onto upper
       integers that will be used to construct a recursive definition
       generator.  Ordinal natural number 0 maps to complex number ` C `
       (normally 0 for the upper integers ` NN0 ` or 1 for the upper integers
       ` NN ` ), 1 maps to ` C ` + 1, etc.  This theorem shows the value of
       ` G ` at ordinal natural number zero.  (This series of theorems
       generalizes an earlier series for ` NN0 ` contributed by Raph Levien,
       10-Apr-2004.) $)
    om2uz0i $p |- ( G ` (/) ) = C $=
      ( c0 cfv cv c1 caddc co wceq copab crdg com cres fveq1i cz wcel fr0g
      ax-mp eqtri ) GDHGBIAIJKLMABNZCOPQZHZCGDUEFRCSTUFCMECSUDUAUBUC $.
      $( [3-Oct-2004] $)

    $( The value of ` G ` (see ~ om2uz0i ) at a successor. $)
    om2uzsuci $p |- ( A e. om -> ( G ` suc A ) = ( ( G ` A ) + 1 ) ) $=
      ( vw vv cv csuc cfv c1 caddc co wceq com suceq fveq2d fveq2 opreq1d
      eqeq12d wcel cvv oprex ax-17 copab crdg cres fveq1i opreq1i hbopab1
      hbrdg hbres hbfv hbopr hbxfr opreq1 frsucopab mpan2 vtoclga ) HJZKZELZVBE
      LZMNOZPZCKZELZCELZMNOZPHCQVBCPZVDVIVFVKVLVCVHEVBCRSVLVEVJMNVBCETUAUBVBQUC
      VFUDUCVGVEMNUEABIDVBAJZMNOZVFUDEIJZDUCAUFZVOVBUCAUFZAIVFVBBJVNPZABUGZDUHZ
      QUIZLZMNOVEWBMNVBEWAGUJUKAIWBMNAIVBWAAIVTQAIDVSVRABIULVPUMVOQUCAUFUNVQUOV
      ONUCAUFVOMUCAUFUPUQGVMVEMNURUSUTVA $.
      $( [3-Oct-2004] $)

    $( MAY BE REVISED - use ~ df-uz etc. instead of ` { z e. ZZ | C <_ z } `
       here and other om2uz* $)
    $( The value ` G ` (see ~ om2uz0i ) at an ordinal natural number is in the
       upper integers. $)
    om2uzuzi $p |- ( A e. om -> ( G ` A ) e. { z e. ZZ | C <_ z } ) $=
      ( vv vw cv cfv cle wbr cz crab wcel c0 csuc wceq fveq2 eleq1d om2uz0i
      breq2 elrab zrei leidi mpbir2an eqeltri com c1 caddc co peano2uz2 mpan
      om2uzsuci syl5ibr finds ) IKZFLZECKZMNZCOPZQRFLZVCQJKZFLZVCQZVESZFLZVCQZD
      FLZVCQIJDUSRTUTVDVCUSRFUAUBUSVETUTVFVCUSVEFUAUBUSVHTUTVIVCUSVHFUAUBUSDTUT
      VKVCUSDFUAUBVDEVCABEFGHUCEVCQEOQZEEMNZVBVMCEOVAEEMUDUEGEEGUFUGUHUIVGVJVEU
      JQZVFUKULUMZVCQZVLVGVPGCEVFUNUOVNVIVOVCABVEEFGHUPUBUQUR $.
      $( [3-Oct-2004] $)

    $( Less-than relation for ` G ` (see ~ om2uz0i ). $)
    om2uzlti $p |- ( ( A e. om /\ B e. om ) ->
                   ( A e. B -> ( G ` A ) < ( G ` B ) ) ) $=
      ( vv vw vz com wcel cfv clt wbr wi cv c0 csuc wceq eleq2 fveq2 breq2d
      imbi12d imbi2d noel pm2.21i a1i wo wa id orim12d wb elsuc2g bicomd
      adantl c1 caddc co cle om2uzsuci cz crab om2uzuzi ssrab2 sseli zleltp1
      syl2an cr zre 3syl leloe 3bitr2rd syl5ib expcom a2d finds impcom ) DLMCLM
      ZCDMZCFNZDFNZOPZQZVTCIRZMZWBWFFNZOPZQZQVTCSMZWBSFNZOPZQZQVTCJRZMZWBWOFNZO
      PZQZQVTCWOTZMZWBWTFNZOPZQZQVTWEQIJDWFSUAZWJWNVTXEWGWKWIWMWFSCUBXEWHWLWBOW
      FSFUCUDUEUFWFWOUAZWJWSVTXFWGWPWIWRWFWOCUBXFWHWQWBOWFWOFUCUDUEUFWFWTUAZWJX
      DVTXGWGXAWIXCWFWTCUBXGWHXBWBOWFWTFUCUDUEUFWFDUAZWJWEVTXHWGWAWIWDWFDCUBXHW
      HWCWBOWFDFUCUDUEUFWNVTWKWMCUGUHUIWOLMZVTWSXDVTXIWSXDQWSWPCWOUAZUJZWRWBWQU
      AZUJZQVTXIUKZXDWSWPWRXJXLWSULXJXLQWSCWOFUCUIUMXNXKXAXMXCXIXKXAUNVTXIXAXKC
      WOLUOUPUQXNXCWBWQURUSUTZOPZWBWQVAPZXMXIXCXPUNVTXIXBXOWBOABWOEFGHVBUDUQVTW
      BEKRVAPZKVCVDZMZWQXSMZXQXPUNZXIABKCEFGHVEZABKWOEFGHVEZXTWBVCMZWQVCMZYBYAX
      SVCWBXRKVCVFZVGZXSVCWQYGVGZWBWQVHVIVIVTWBVJMZWQVJMZXQXMUNXIVTXTYEYJYCYHWB
      VKVLXIYAYFYKYDYIWQVKVLWBWQVMVIVNUEVOVPVQVRVS $.
      $( [3-Oct-2004] $)

    $( The mapping ` G ` (see ~ om2uz0i ) preserves order. $)
    om2uzlt2i $p |- ( ( A e. om /\ B e. om ) ->
                   ( A e. B <-> ( G ` A ) < ( G ` B ) ) ) $=
      ( vz com wcel wa cfv clt wbr om2uzlti wceq wo wn wi fveq2 a1i orim12d
      ancoms wb con0 nnon wss onsseleq ontri1 bitr3d syl2an cr cv cle cz crab
      om2uzuzi ssrab2 zssre sstri sseli syl leloe lenlt 3imtr3d impcon4bid ) CJ
      KZDJKZLZCDKZCFMZDFMZNOZABCDEFGHPVJDCKZDCQZRZVMVLNOZVMVLQZRZVKSZVNSZVIVHVQ
      VTTVIVHLZVOVRVPVSABDCEFGHPVPVSTWCDCFUAUBUCUDVIVHVQWAUEZVIDUFKZCUFKZWDVHDU
      GCUGWEWFLDCUHVQWADCUIDCUJUKULUDVIVHVTWBUEZVIVMUMKZVLUMKZWGVHVIVMEIUNUOOZI
      UPUQZKWHABIDEFGHURWKUMVMWKUPUMWJIUPUSUTVAZVBVCVHVLWKKWIABICEFGHURWKUMVLWL
      VBVCWHWILVMVLUOOVTWBVMVLVDVMVLVEUKULUDVFVG $.
      $( [4-May-2005] $)

    $( Range of ` G ` (see ~ om2uz0i ). $)
    om2uzrani $p |- ran G = { z e. ZZ | C <_ z } $=
      ( vu vw vv crn cv cle wbr cz crab wcel cfv wceq com wrex wfn wb c1 caddc
      co copab crdg cres frfnom fneq1i mpbir fvelrnb ax-mp om2uzuzi eleq1
      syl5ibcom r19.23aiv sylbi wa breq2 elrab c0 om2uz0i peano1 fnfvelrn
      mp2an eqeltrri a1i wi w3a csuc om2uzsuci opreq1 sylan9eq peano2 sylancr
      adantr eqeltrrd r19.23aiva uzind mp3an1 impbii eqriv ) HEKZDCLZMNZCOPZHLZ
      WEQZWIWHQZWJILZERZWISZITUAZWKETUBZWJWOUCWPBLALUDUEUFSABUGZDUHTUIZTUBDWQUJ
      TEWRGUKULZITWIEUMUNWNWKITWLTQZWMWHQWNWKABCWLDEFGUOWMWIWHUPUQURUSWKWIOQZDW
      IMNZUTWJWGXBCWIOWFWIDMVAVBDOQZXAXBWJFWLWEQDWEQZJLZWEQZXEUDUEUFZWEQZWJIJDW
      IWLDWEUPWLXEWEUPWLXGWEUPWLWIWEUPXDXCVCERZDWEABDEFGVDWPVCTQXIWEQWSVETVCEVF
      VGVHVIXFXHVJXCXEOQDXEMNVKXFWMXESZITUAZXHWPXFXKUCWSITXEEUMUNXJXHITWTXJUTWL
      VLZERZXGWEWTXJXMWMUDUEUFXGABWLDEFGVMWMXEUDUEVNVOWTXMWEQZXJWTWPXLTQXNWSWLV
      PTXLEVFVQVRVSVTUSVIWAWBUSWCWD $.
      $( [3-Oct-2004] $)

    $( ` G ` (see ~ om2uz0i ) is a one-to-one onto mapping. $)
    om2uzf1oi $p |- G : om -1-1-onto-> { z e. ZZ | C <_ z } $=
      ( vw vv com cv cle wbr cz crab wf1o wf1 crn wceq dff1o5 wf cfv wi wral
      dff13 wfn wss df-f c1 caddc co copab crdg cres frfnom fneq1i mpbir
      om2uzrani eqimssi mpbir2an wcel wa clt wo wn cr wb om2uzuzi ssrab2 sseli
      zre 3syl lttri3 syl2an ioran syl6bbr word nnord ordtri3 con2bid om2uzlti
      ancoms orim12d sylbird con1d sylbid rgen2a ) JDCKLMZCNOZEPJWIEQZERZWISJWI
      ETWJJWIEUAZHKZEUBZIKZEUBZSZWMWOSZUCZIJUDHJUDHIJWIEUEWLEJUFZWKWIUGJWIEUHWT
      BKAKUIUJUKSABULZDUMJUNZJUFDXAUOJEXBGUPUQWKWIABCDEFGURZUSUTWSHIJWMJVAZWOJV
      AZVBZWQWNWPVCMZWPWNVCMZVDZVEZWRXFWQXGVEXHVEVBZXJXDWNVFVAZWPVFVAZWQXKVGXEX
      DWNWIVAWNNVAXLABCWMDEFGVHWINWNWHCNVIZVJWNVKVLXEWPWIVAWPNVAXMABCWODEFGVHWI
      NWPXNVJWPVKVLWNWPVMVNXGXHVOVPXFWRXIXFWRVEWMWOVAZWOWMVAZVDZXIXFWRXQXDWMVQW
      OVQWRXQVEVGXEWMVRWOVRWMWOVSVNVTXFXOXGXPXHABWMWODEFGWAXEXDXPXHUCABWOWMDEFG
      WAWBWCWDWEWFWGUTXCUT $.
      $( [3-Oct-2004] $)

    $( ` G ` (see ~ om2uz0i ) is an isomorphism from natural ordinals to upper
       integers. $)
    om2uzisoi $p |- G Isom _E , < ( om , { z e. ZZ | C <_ z } ) $=
      ( vw vv com cv cle wbr cz crab cep clt wiso wf1o cfv wb wral df-iso
      om2uzf1oi wcel wa epel om2uzlt2i syl5bb rgen2a mpbir2an ) JDCKLMCNOZPQERJ
      ULESHKZIKZPMZUMETUNETQMZUAZIJUBHJUBHIJULPQEUCABCDEFGUDUQHIJUOUMUNUEUMJUEU
      NJUEUFUPHIUGABUMUNDEFGUHUIUJUK $.
      $( [9-Oct-2008] $)

    $( A helper lemma for the value of a recursive definition generator on
       upper integers (typically either ` NN ` or ` NN0 ` ) with characteristic
       function ` F ` and initial value ` A ` .  Normally ` F ` is a function
       on the partition, and ` A ` is a member of the partition.  See also
       comment in ~ om2uz0i . $)
    uzrdgvali $p |- ( B e. { z e. ZZ | C <_ z } ->
                  ( ( rec ( F , A ) o. `' G ) ` B ) =
                  ( rec ( F , A ) ` ( `' G ` B ) ) ) $=
      ( cv cle wbr cz crab wcel ccnv cdm crdg ccom cfv wceq crn om2uzrani
      df-rn eqtr3i eleq2i wfun con0 wfn rdgfnon fnfun ax-mp com wf1o om2uzf1oi
      f1ocnv f1ofun fvco mp3an12 sylbi ) EFCKLMCNOZPEHQZRZPZEGDSZVCTUAEVCUAVFUA
      UBZVBVDEHUCVBVDABCFHIJUDHUEUFUGVFUHZVCUHZVEVGVFUIUJVHDGUKUIVFULUMVBUNVCUO
      ZVIUNVBHUOVJABCFHIJUPUNVBHUQUMVBUNVCURUMEVFVCUSUTVA $.
      $( [3-Oct-2004] $)

    $( Initial value of a recursive definition generator on upper integers.
       See comment in ~ uzrdgvali . $)
    uzrdginii $p |- ( A e. B -> ( ( rec ( F , A ) o. `' G ) ` C ) = A ) $=
      ( vz wcel crdg ccnv ccom cfv c0 cv cle wbr cz crab wceq breq2 elrab zrei
      leidi mpbir2an uzrdgvali ax-mp om2uz0i com wf1o wi om2uzf1oi peano1
      f1ocnvfv mp2an fveq2i eqtri rdg0g syl5eq ) CDKEFCLZGMZNOZPVBOZCVDEVCOZVBO
      ZVEEEJQZRSZJTUAZKZVDVGUBVKETKEERSZVIVLJETVHEERUCUDHEEHUEUFUGABJCEEFGHIUHU
      IVFPVBPGOEUBZVFPUBZABEGHIUJUKVJGULPUKKVMVNUMABJEGHIUNUOUKVJPEGUPUQUIURUSC
      DFUTVA $.
      $( [3-Oct-2004] $)

    $( Successor value of a recursive definition generator on upper integers.
       See comment in ~ uzrdgvali . $)
    uzrdgsuci $p |- ( B e. { z e. ZZ | C <_ z } ->
                  ( ( rec ( F , A ) o. `' G ) ` ( B + 1 ) )
                  = ( F ` ( ( rec ( F , A ) o. `' G ) ` B ) ) ) $=
      ( cv cle wbr cz crab wcel c1 caddc co crdg ccnv ccom cfv csuc wceq
      peano2uz2 mpan uzrdgvali syl com wf1o wf om2uzf1oi f1ocnv ax-mp f1of
      ffvelrni om2uzsuci f1ocnvfv2 opreq1d eqtrd wi peano2 f1ocnvfv 3syl mpd
      fveq2d con0 nnon rdgsuc 3eqtrd eqtr4d ) EFCKLMCNOZPZEQRSZGDTZHUAZUBZUCZEV
      QUCZVPUCZGUCZEVRUCZGUCVNVSVOVQUCZVPUCZVTUDZVPUCZWBVNVOVMPZVSWEUEFNPVNWHIC
      FEUFUGABCDVOFGHIJUHUIVNWDWFVPVNWFHUCZVOUEZWDWFUEZVNWIVTHUCZQRSZVOVNVTUJPZ
      WIWMUEVMUJEVQVMUJVQUKZVMUJVQULUJVMHUKZWOABCFHIJUMZUJVMHUNUOVMUJVQUPUOUQZA
      BVTFHIJURUIVNWLEQRWPVNWLEUEWQUJVMEHUSUGUTVAVNWNWFUJPZWJWKVBZWRVTVCWPWSWTW
      QUJVMWFVOHVDUGVEVFVGVNWNVTVHPWGWBUEWRVTVIDVTGVJVEVKVNWCWAGABCDEFGHIJUHVGV
      L $.
      $( [3-Oct-2004] $)

    $( A helper lemma for the value of an ` NN ` or ` NN0 ` -based recursive
       definition generator.  Value at second index.  See comment in
       ~ uzrdgvali . $)
    uzrdginip1i $p |- ( A e. B -> ( ( rec ( F , A ) o. `' G ) ` ( C + 1 ) ) =
                  ( F ` A ) ) $=
      ( vz wcel c1 caddc co crdg ccnv ccom cfv cv cle wbr cz crab wceq breq2
      elrab zrei leidi mpbir2an uzrdgsuci ax-mp uzrdginii fveq2d syl5eq ) CDKZE
      LMNFCOGPQZRZEUPRZFRZCFREEJSZTUAZJUBUCKZUQUSUDVBEUBKEETUAZVAVCJEUBUTEETUEU
      FHEEHUGUHUIABJCEEFGHIUJUKUOURCFABCDEFGHIULUMUN $.
      $( [6-Jun-2005] $) $( [3-Oct-2004] $)

    $( A helper lemma for the value of an ` NN ` or ` NN0 ` -based recursive
       definition generator.  The generated function is a function on the upper
       integers starting at ` C ` (usually 1 or 0).  See comment in
       ~ uzrdgvali . $)
    uzrdgfnuzi $p |- ( rec ( F , A ) o. `' G ) Fn { z e. ZZ | C <_ z } $=
      ( crdg ccnv ccom cv cle wbr cz crab wfn cvv wf con0 rdgfnon dffn2 mpbi
      com wss wf1o om2uzf1oi f1ocnv ax-mp f1of omsson fss mp2an fco mpbir ) FDJ
      ZGKZLZECMNOCPQZRUTSUSTZUASUQTZUTUAURTZVAUQUARVBDFUBUAUQUCUDUTUEURTZUEUAUF
      VCUTUEURUGZVDUEUTGUGVEABCEGHIUHUEUTGUIUJUTUEURUKUJULUTUEUAURUMUNUTUASUQUR
      UOUNUTUSUCUP $.
      $( [27-Jun-2005] $) $( [3-Oct-2004] $)
  $}

  ${
    $d m n G $.  $d n N $.  $d j k n $.
    fzennn.1 $e |- G = ( rec ( { <. j , k >. | k = ( j + 1 ) } , 0 ) |` om ) $.
    $( The cardinality of a finite set of sequential integers.  (See ~ om2uz0i
       for a description of the hypothesis.)  (Contributed by Mario Carneiro,
       12-Feb-2013.) $)
    fzennn $p |- ( N e. NN0 -> ( 1 ... N ) ~~ ( `' G ` N ) ) $=
      ( vn vm c1 cv cfz co ccnv cfv cen wbr cc0 caddc wceq opreq2 fveq2
      breq12d weq c0 0ex enref clt lt01 cz wcel wb 1z 0z fzn mp2an mpbi com
      cle crab wf1o wa om2uzf1oi peano1 pm3.2i om2uz0i f1ocnvfv mp2 3brtr4i
      cn0 csn cun cin simpr cvv oprex fvex en2sn a1i wn cr nn0re ltp1 peano2re
      ltnle mpdan mpbid syl elfzle2 nsyl adantr disjsn sylibr word f1ocnvdm
      mpan nn0zrab eleq2s nnord ordirr 3syl unen syl22anc nn0z ax-1cn addid2i
      nn0ge0 0re 1re leadd1 mp3an13 syl5eqbrr fzsuc mp3an1 syl11anc csuc
      peano2 jctil om2uzsuci eleq2i biimpi f1ocnvfv2 sylancr opreq1d eqtrd
      sylc df-suc syl6eq 3brtr4d ex nn0ind ) HFIZJKZYJCLZMZNOHPJKZPYLMZNOHGIZJK
      ZYPYLMZNOZHYPHQKZJKZYTYLMZNOZHDJKZDYLMZNOFGDYJPRYKYNYMYONYJPHJSYJPYLTUAFG
      UBYKYQYMYRNYJYPHJSYJYPYLTUAYJYTRYKUUAYMUUBNYJYTHJSYJYTYLTUAYJDRYKUUDYMUUE
      NYJDHJSYJDYLTUAUCUCYNYONUCUDUEPHUFOZYNUCRZUGHUHUIZPUHUIUUFUUGUJUKULHPUMUN
      UOUPPYJUQOFUHURZCUSZUCUPUIZUTUCCMPRYOUCRUUJUUKABFPCULEVAZVBVCABPCULEVDUPU
      UIUCPCVEVFVGYPVHUIZYSUUCUUMYSUTZYQYTVIZVJZYRYRVIZVJZUUAUUBNUUNYSUUOUUQNOZ
      YQUUOVKUCRZYRUUQVKUCRZUUPUURNOUUMYSVLUUSUUNYTVMUIYRVMUIUUSYPHQVNYPYLVOYTY
      RVMVMVPUNVQUUNYTYQUIZVRZUUTUUMUVCYSUUMYTYPUQOZUVBUUMYPVSUIZUVDVRZYPVTZUVE
      YPYTUFOZUVFYPWAUVEYTVSUIUVHUVFUJYPWBYPYTWCWDWEWFYTHYPWGWHWIYQYTWJWKUUNYRY
      RUIVRZUVAUUMUVIYSUUMYRUPUIZYRWLUVIUVJYPUUIVHUUJYPUUIUIZUVJUULUPUUIYPCWMWN
      FWOZWPZYRWQYRWRWSWIYRYRWJWKYQYRUUOUUQWTXAUUMUUAUUPRZYSUUMYPUHUIZHYTUQOZUV
      NYPXBUUMHPHQKZYTUQHXCXDUUMPYPUQOZUVQYTUQOZYPXEUUMUVEUVRUVSUJZUVGPVSUIUVEH
      VSUIUVTXFXGPYPHXHXIWFWEXJUUHUVOUVPUVNUKHYPXKXLXMWIUUNUUBYRXNZUURUUMUUBUWA
      RZYSUUMUUJUWAUPUIZUTUWACMZYTRUWBUUMUWCUUJUUMUVJUWCUVMYRXOWFUULXPUUMUWDYRC
      MZHQKZYTUUMUVJUWDUWFRUVMABYRPCULEXQWFUUMUWEYPHQUUMUUJUVKUWEYPRUULUUMUVKVH
      UUIYPUVLXRXSUPUUIYPCXTYAYBYCUPUUIUWAYTCVEYDWIYRYEYFYGYHYI $.
      $( [12-Feb-2013] $)
  $}

  ${
    $d n G $.  $d j k n $.
    cardfz.1 $e |- G = ( rec ( { <. j , k >. | k = ( j + 1 ) } , 0 ) |` om ) $.
    $( The cardinality of a finite set of sequential integers.  (See ~ om2uz0i
       for a description of the hypothesis.)  (Revised by Mario Carneiro,
       12-Feb-2013.) $)
    cardfz $p |- ( N e. NN0 -> ( card ` ( 1 ... N ) ) = ( `' G ` N ) ) $=
      ( vn cn0 wcel c1 cfz co ccrd cfv ccnv cvv cen wbr wceq fvex fzennn
      carden2b mpsyl com cc0 cv cle cz crab wf1o 0z om2uzf1oi nn0zrab eleq2i
      biimpi f1ocnvdm sylancr cardnn syl eqtrd ) DGHZIDJKZLMZDCNZMZLMZVDVDOHUTV
      AVDPQVBVERDVCSABCDETVAVDOUAUBUTVDUCHZVEVDRUTUCUDFUEUFQFUGUHZCUIDVGHZVFABF
      UDCUJEUKUTVHGVGDFULUMUNUCVGDCUOUPVDUQURUS $.
      $( [12-Feb-2013] $) $( [7-Nov-2008] $)
  $}

  ${
    $d m n G $.  $d n N $.  $d j k n $.
    cardfzOLD.1 $e |- G = ( rec ( { <. j , k >. | k = ( j + 1 ) } , 0 ) |` om )
        $.
    $( The cardinality of a finite set of sequential integers.  (See ~ om2uz0i
       for a description of the antecedent.) $)
    cardfzOLD $p |- ( N e. NN0 -> ( card ` ( 1 ... N ) ) = ( `' G ` N ) ) $=
      ( vn vm c1 cv cfz co ccrd cfv ccnv wceq cc0 caddc cn0 wcel wa csuc csn
      cun cz cle wbr nn0z ax-1cn addid2i nn0ge0 cr wb nn0re 0re 1re leadd1
      mp3an13 syl mpbid syl5eqbrr 1z fzsuc mp3an1 syl11anc fveq2d cen wn clt
      ltp1 peano2re ltnle mpdan elfzle2 nsyl oprex unsnen cvv snex unex fvex
      sucex carden mp2an sylibr eqtrd adantr suceq com crab nn0zrab eleq2i
      wf1o 0z om2uzf1oi f1ocnvdm mpan sylbi peano2 cardnn jctil om2uzsuci
      biimpi f1ocnvfv2 sylancr opreq1d f1ocnvfv sylc eqtr4d sylan9eqr ex c0
      card0 lt01 fzn mpbi fveq2i peano1 pm3.2i om2uz0i mp2 3eqtr4i opreq2
      fveq2 eqeq12d nn0indALT ) HFIZJKZLMZYFCNZMZOHPJKZLMZPYIMZOHGIZJKZLMZYNYIM
      ZOZHYNHQKZJKZLMZYSYIMZOZHDJKZLMZDYIMZOFGDYNRSZYRUUCUUGYRTUUAYPUAZLMZUUBUU
      GUUAUUIOYRUUGUUAYOYSUBZUCZLMZUUIUUGYTUUKLUUGYNUDSZHYSUEUFZYTUUKOZYNUGUUGH
      PHQKZYSUEHUHUIUUGPYNUEUFZUUPYSUEUFZYNUJUUGYNUKSZUUQUURULZYNUMZPUKSUUSHUKS
      UUTUNUOPYNHUPUQURUSUTHUDSZUUMUUNUUOVAHYNVBVCVDVEUUGUUKUUHVFUFZUULUUIOZUUG
      YSYOSZVGUVCUUGYSYNUEUFZUVEUUGUUSUVFVGZUVAUUSYNYSVHUFZUVGYNVIUUSYSUKSUVHUV
      GULYNVJYNYSVKVLUSURYSHYNVMVNYOYSHYNJVOZYNHQVOVPURUUKVQSUUHVQSUVDUVCULYOUU
      JUVIYSVRVSYPYOLVTWAUUKUUHVQVQWBWCWDWEWFYRUUGUUIYQUAZLMZUUBYRUUHUVJLYPYQWG
      VEUUGUVKUVJUUBUUGUVJWHSZUVKUVJOUUGYQWHSZUVLUUGYNPYFUEUFFUDWIZSZUVMRUVNYNF
      WJWKZWHUVNCWLZUVOUVMABFPCWMEWNZWHUVNYNCWOWPWQZYQWRURZUVJWSURUUGUVQUVLTUVJ
      CMZYSOUUBUVJOUUGUVLUVQUVTUVRWTUUGUWAYQCMZHQKZYSUUGUVMUWAUWCOUVSABYQPCWMEX
      AURUUGUWBYNHQUUGUVQUVOUWBYNOUVRUUGUVOUVPXBWHUVNYNCXCXDXEWEWHUVNUVJYSCXFXG
      XHXIWEXJXKLMXKYLYMXLYKXKLPHVHUFZYKXKOZXMUVBPUDSUWDUWEULVAWMHPXNWCXOXPUVQX
      KWHSZTXKCMPOYMXKOUVQUWFUVRXQXRABPCWMEXSWHUVNXKPCXFXTYAYFPOZYHYLYJYMUWGYGY
      KLYFPHJYBVEYFPYIYCYDYFYNOZYHYPYJYQUWHYGYOLYFYNHJYBVEYFYNYIYCYDYFYSOZYHUUA
      YJUUBUWIYGYTLYFYSHJYBVEYFYSYIYCYDYFDOZYHUUEYJUUFUWJYGUUDLYFDHJYBVEYFDYIYC
      YDYE $.
      $( [7-Nov-2008] $)
  $}

  ${
    $d x y z $.
    $( The nonnegative integers are equinumerous to the natural numbers. $)
    nn0ennn $p |- NN0 ~~ NN $=
      ( vx vy cn0 cn cv c1 caddc co cmin nn0ex nn0p1nn nnm1nn0 wcel wceq wb cc
      nncn nn0cn wa ax-1cn subadd mp3an2 eqcom 3bitr4g addcom mpan eqeq2d
      adantl bitrd syl2an ancoms en3 ) ABCDAEZFGHZBEZFIHZJUMKUOLUODMZUMCMZUMUPN
      ZUOUNNZOZUQUOPMZUMPMZVAURUOQUMRVBVCSZUSUOFUMGHZNZUTVDUPUMNZVEUONZUSVFVBFP
      MZVCVGVHOTUOFUMUAUBUMUPUCUOVEUCUDVCVFUTOVBVCVEUNUOVIVCVEUNNTFUMUEUFUGUHUI
      UJUKUL $.
      $( [19-Jul-2004] $)

    $( The set of natural numbers (as a subset of complex numbers) is
       equinumerous to omega (the set of finite ordinal numbers). $)
    nnenom $p |- NN ~~ om $=
      ( vy vx vz com cn0 cn nnex cv c1 caddc co wceq copab cc0 crdg cres wf1o
      cen wbr cle cz crab 0z eqid om2uzf1oi wb nn0zrab f1oeq3 ax-mp mpbir omex
      f1oen nn0ennn entr2i ) DEFGDEAHBHIJKLBAMNODPZQZDERSUPDNCHTSCUAUBZUOQZBACN
      UOUCUOUDUEEUQLUPURUFCUGEUQDUOUHUIUJDEUOUKULUIUMUN $.
      $( [31-Jul-2004] $)
  $}

  $c seq1 $.  $( Symbol for recursive sequence builder. $)

  $( Extend class notation with recursive sequence builder. $)
  cseq1 $a class seq1 $.

  ${
    $d x y z w f g h $.
    $( Define a general-purpose operation that builds an recursive sequence
       (i.e. a function on the natural numbers ` NN ` ) whose value at an index
       is a function of its previous value and the value of an input sequence
       at that index.  This definition is complicated, but fortunately it is
       not intended to be used directly.  Instead, the only purpose of this
       definition is to provide us with an object that has the properties
       expressed by ~ seq11 and ~ seq1p1 .  Typically, those are the main
       theorems that would be used in practice.

       The first operand is the operation that is applied to the previous value
       and the value of the input sequence (second operand).  For example, for
       the operation ` + ` , an input sequence ` F ` with values 1, 1/2, 1/4,
       1/8,... would be transformed into the output sequence ` ( + seq1 F ) `
       with values 1, 3/2, 7/4, 15/8,.., so that
       ` ( ( + seq1 F ) `` 1 ) = 1 ` , ` ( ( + seq1 F ) `` 2 ) = ` 3/2, etc.
       In other words, ` ( + seq1 F ) ` transforms a sequence ` F ` into an
       infinite series. ` ( + seq1 F ) ~~> 2 ` means "the sum of F(n) from n =
       1 to infinity is 2."  Since limits are unique ( ~ climuni ), then by
       ~ euabsn and ~ unisn the "sum of F(n) from n = 1 to infinity" can be
       expressed as ` U. { x | ( + seq1 F ) ~~> x } ` (provided the sequence
       converges) and evaluates to 2 in this example.

       Internally, we define a recursive function whose values are ordered
       pairs starting at ` <. 1 , ( g `` 1 ) >. ` .  The first member of the
       ordered pair is a counter used to select the appropriate value of the
       input sequence ` g ` .  The first ` rec ` constructs this function on
       ` om ` , and the converse of the second ` rec ` maps ` NN ` to ` om ` .

       This definition has its roots in a series of theorems from ~ om2uz0i
       through ~ om2uzf1oi , originally proved by Raph Levien for use with
       ~ df-exp and later generalized for arbitrary recursive sequences.  The
       related definitions ~ df-seq0 and ~ df-seqz build recursive sequences on
       ` NN0 ` and general upper integer sets respectively.  Definition
       ~ df-sum extracts the summation values from partial (finite) and
       complete (infinite) series. $)
    df-seq1 $a |- seq1 = { <. <. f , g >. , h >. |
                   h = { <. x , y >. | ( x e. NN /\
                     y = ( 2nd ` ( ( rec ( { <. z , w >. |
                         w = <. ( ( 1st ` z ) + 1 ) ,
                           ( ( 2nd ` z ) f ( g ` ( ( 1st ` z ) + 1 ) ) ) >. } ,
                       <. 1 , ( g ` 1 ) >. ) o. `'
       ( rec ( { <. z , w >. | w = ( z + 1 ) } , 1 ) |` om ) ) ` x ) ) ) } } $.
  $}

  ${
    $d x y v $.  $d z w v u f $.  $d v A $.  $d w v u f B $.  $d v u f C $.
    $d v u f G $.
    seq1lem1.1 $e |- G =
                     ( rec ( { <. x , y >. | y = ( x + 1 ) } , 1 ) |` om ) $.
    $( We prove by induction that the first member of the ordered pair value of
       the internal sequence of ` seq1 ` equals its index. $)
    seq1lem1 $p |- ( A e. NN -> ( 1st `
                ( ( rec ( { <. z , w >. | w = <. ( ( 1st ` z ) + 1 ) , B >. } ,
                <. 1 , C >. ) o. `' G ) ` A ) ) = A ) $=
      ( vv vu vf cv c1st cfv c1 caddc co cop wceq copab crdg ccnv ccom fveq2
      fveq2d id eqeq12d cvv wcel opex 1z uzrdginii ax-mp fveq2i cz elisseti
      op1st eqtri cn csb cle wbr crab nnzrab eleq2i uzrdgsuci sylbi ax-17
      visset hbcsb1 hbop hbeleq opreq1d csbeq1a opeq12d eqeq2d cbvopab1 fveq1i
      fvex csbeq1 fvopabf syl6eq oprex opreq1 sylan9eq ex nnind ) JMZDMZCMZNOZP
      QRZFSZTZCDUAZPGSZUBHUCUDZOZNOZWITPWROZNOZPTKMZWROZNOZXCTZXCPQRZWROZNOZXGT
      ZEWROZNOZETJKEWIPTZWTXBWIPXMWSXANWIPWRUEUFXMUGUHWIXCTZWTXEWIXCXNWSXDNWIXC
      WRUEUFXNUGUHWIXGTZWTXIWIXGXOWSXHNWIXGWRUEUFXOUGUHWIETZWTXLWIEXPWSXKNWIEWR
      UEUFXPUGUHXBWQNOPXAWQNWQUIUJXAWQTPGUKABWQUIPWPHULIUMUNUOPGPUPULUQURUSXCUT
      UJZXFXJXQXFXIXEPQRZXGXQXIXRCXDFVAZSZNOXRXQXHXTNXQXHXDWPOZXTXQXCPWIVBVCJUP
      VDZUJXHYATUTYBXCJVEVFABJWQXCPWPHULIVGVHYAXDWJWINOZPQRZCWIFVAZSZTZJDUAZOXT
      XDWPYHWOYGCDJWOJVICDYFCDYDYEWJYDUJCVICDWIFJVJWJWIUJCVIVKVLVMWKWITZWNYFWJY
      IWMYDFYEYIWLYCPQWKWINUEVNCWIFVOVPVQVRVSJDLXDYFXTLMZXDUJJVIYJXTUJJVIXCWRVT
      XRXSUKWIXDTZYDXRYEXSYKYCXEPQWIXDNUEVNCWIXDFWAVPWBUSWCUFXRXSXEPQWDURWCXEXC
      PQWEWFWGWH $.
      $( [21-Dec-2008] $) $( [5-Oct-2004] $)
  $}

  $( Lemma for recursive sequence builder theorems. $)
  seq1lem2 $p |- ( A e. NN -> ( A + 1 ) e. ( NN \ { 1 } ) ) $=
    ( cn wcel c1 caddc co clt wbr csn cdif peano2nn cc0 nngt0 cr wb nnre 0re
    1re ltadd1 mp3an13 ax-1cn addid2i breq1i syl6bb syl mpbid wa wne nngt1ne1
    pm5.32i eldifsn bitr4i biimpi syl11anc ) ABCZADEFZBCZDUPGHZUPBDIJCZAKUOLAGH
    ZURAMUOANCZUTUROAPVAUTLDEFZUPGHZURLNCVADNCUTVCOQRLADSTVBDUPGDUAUBUCUDUEUFUQ
    URUGZUSVDUQUPDUHZUGUSUQURVEUPUIUJUPBDUKULUMUN $.
    $( [8-Oct-2004] $)

  ${
    $d A x z $.  $d w x $.  $d S x z w $.  $d F x z w $.
    seq1rval.1 $e |- H = { <. z , w >. | w = <. ( ( 1st ` z ) + 1 ) ,
                   ( ( 2nd ` z ) S ( F ` ( ( 1st ` z ) + 1 ) ) ) >. } $.
    seq1rval.2 $e |- A e. _V $.
    $( Value of the characteristic function of the inner recursion in
       ~ df-seq1 . $)
    seq1rval $p |- ( H ` A ) = <. ( ( 1st ` A ) + 1 ) ,
                     ( ( 2nd ` A ) S ( F ` ( ( 1st ` A ) + 1 ) ) ) >. $=
      ( cfv cv c1st c1 caddc co c2nd cop wceq copab fveq1i opex fveq2 opreq1d
      fveq2d opreq12d opeq12d fvopab eqtri ) CFICBJAJZKIZLMNZUHOIZUJEIZDNZPZQAB
      RZICKIZLMNZCOIZUQEIZDNZPZCFUOGSABCUNVAHUQUTTUHCQZUJUQUMUTVBUIUPLMUHCKUAUB
      ZVBUKURULUSDUHCOUAVBUJUQEVCUCUDUEUFUG $.
      $( [5-Oct-2004] $)
  $}

  ${
    $d x y A $.  $d x y f g h G $.  $d x y f g h H $.  $d x y z w f g h F $.
    $d x y z w f g h S $.
    seq1val.1 $e |- S e. _V $.
    seq1val.2 $e |- F e. _V $.
    seq1val.3 $e |- G =
                     ( rec ( { <. z , w >. | w = ( z + 1 ) } , 1 ) |` om ) $.
    seq1val.4 $e |- H = { <. z , w >. | w = <. ( ( 1st ` z ) + 1 ) ,
                     ( ( 2nd ` z ) S ( F ` ( ( 1st ` z ) + 1 ) ) ) >. } $.
    $( Value of the recursive sequence builder operation. $)
    seq1val $p |- ( S seq1 F ) = { <. x , y >. | ( x e. NN /\ y =
        ( 2nd ` ( ( rec ( H , <. 1 , ( F ` 1 ) >. ) o. `' G ) ` x ) ) ) } $=
      ( vf vg vh cvv wcel cseq1 co cv cn c1 cfv cop crdg ccnv ccom c2nd wceq
      wa copab c1st caddc com cres nnex opabex2 opreq opeq2d eqeq2d opabbidv
      rdgeq1 syl coeq1d cnveqi coeq2i syl6eqr fveq1d fveq2d anbi2d fveq1
      opreq2d opeq2 rdgeq2 3syl eqtrd df-seq1 oprabval5 mp2an ) EPQFPQEFRSATZUA
      QZBTZVTHUBUBFUCZUDZUEZGUFZUGZUCZUHUCZUIZUJZABUKZUIIJMNOEFPPWAWBVTDTZCTZUL
      UCUBUMSZWNUHUCZWONTZUCZMTZSZUDZUIZCDUKZUBUBWQUCZUDZUEZWMWNUBUMSUICDUKUBUE
      UNUOZUFZUGZUCZUHUCZUIZUJZABUKWLRWAWBVTWMWOWPWRESZUDZUIZCDUKZXEUEZWFUGZUCZ
      UHUCZUIZUJZABUKABUAWIUPUQWSEUIZXMYCABYDXLYBWAYDXKYAWBYDXJXTUHYDVTXIXSYDXI
      XRXHUGXSYDXFXRXHYDXCXQUIXFXRUIYDXBXPCDYDXAXOWMYDWTXNWOWPWRWSEURUSUTVAXEXC
      XQVBVCVDWFXHXRGXGKVEVFVGVHVIUTVJVAWQFUIZYCWKABYEYBWJWAYEYAWIWBYEXTWHUHYEV
      TXSWGYEXRWEWFYEXRHXEUEZWEYEXQHUIXRYFUIYEXQWMWOWPWOFUCZESZUDZUIZCDUKHYEXPY
      JCDYEXOYIWMYEXNYHWOYEWRYGWPEWOWQFVKVLUSUTVALVGXEXQHVBVCYEXDWCUIXEWDUIYFWE
      UIUBWQFVKXDWCUBVMXEWDHVNVOVPVDVHVIUTVJVAABCDMNOVQVRVS $.
      $( [8-Oct-2004] $)

    $( Lemma for ~ seq1fn . $)
    seq1fnlem $p |- ( S seq1 F ) Fn NN $=
      ( vx vy cn cv c1 cfv cop crdg ccnv ccom c2nd cseq1 co fvex seq1val
      fnopab2 ) KLMKNFOODPQRESTPZUAPCDUBUCUGUAUDKLABCDEFGHIJUEUF $.
      $( [8-Oct-2004] $)

    $( Value of the value of the recursive sequence builder operation. $)
    seq1val2 $p |- ( A e. NN -> ( ( S seq1 F ) ` A ) =
          ( 2nd ` ( ( rec ( H , <. 1 , ( F ` 1 ) >. ) o. `' G ) ` A ) ) ) $=
      ( vx vy cv c1 cfv cop crdg ccnv ccom c2nd cn cseq1 co wceq fveq2 fveq2d
      seq1val fvex fvopab4 ) LMCLNZGOOEPQRFSTZPZUAPCULPZUAPUBDEUCUDUKCUEUMUNUAU
      KCULUFUGLMABDEFGHIJKUHUNUAUIUJ $.
      $( [5-Oct-2004] $)

    $( Lemma for ~ seq11 . $)
    seq11lem $p |- ( ( S seq1 F ) ` 1 ) = ( F ` 1 ) $=
      ( c1 cseq1 co cfv cop crdg ccnv ccom c2nd cn wcel wceq 1nn seq1val2
      ax-mp cvv opex 1z uzrdginii fveq2i elisseti fvex op2nd 3eqtri ) KCDLMNZKF
      KKDNZOZPEQRNZSNZUQSNUPKTUAUOUSUBUCABKCDEFGHIJUDUEURUQSUQUFUAURUQUBKUPUGAB
      UQUFKFEUHIUIUEUJKUPKTUCUKKDULUMUN $.
      $( [5-Oct-2004] $)

    $( Lemma for ~ seq1p1 . $)
    seq1suclem $p |- ( A e. NN -> ( ( S seq1 F ) ` ( A + 1 ) ) =
                ( ( ( S seq1 F ) ` A ) S ( F ` ( A + 1 ) ) ) ) $=
      ( vx vy cn wcel c1 caddc co cfv cop crdg ccnv ccom c2nd cseq1 c1st cv
      cle wbr cz crab wceq nnzrab eleq2i 1z uzrdgsuci sylbi copab fveq2
      opreq1d fveq2d opreq12d opeq12d eqeq2d eqeq1 sylan9bb cbvopabv eqtr4i
      fvex seq1rval syl6eq oprex op2nd rdgeq1 ax-mp coeq1i fveq1i fveq2i
      seq1lem1 syl5eq opreq2d eqtrd peano2nn seq1val2 syl 3eqtr4d ) CNOZCPQRZGP
      PESZTZUAZFUBZUCZSZUDSZCWMSZUDSZWHESZDRZWHDEUERZSZCWTSZWRDRWGWOWQWPUFSZPQR
      ZESZDRZWSWGWOXDXFTZUDSXFWGWNXGUDWGWNWPGSZXGWGCPLUGZUHUILUJUKZOWNXHULNXJCL
      UMUNABLWJCPGFUOJUPUQLMWPDEGGBUGZAUGZUFSZPQRZXLUDSZXNESZDRZTZULZABURZMUGZX
      IUFSZPQRZXIUDSZYCESZDRZTZULZLMURKYHXSLMABXIXLULZYHYAXRULYAXKULXSYIYGXRYAY
      IYCXNYFXQYIYBXMPQXIXLUFUSUTZYIYDXOYEXPDXIXLUDUSYIYCXNEYJVAVBVCVDYAXKXRVEV
      FVGVHCWMVIVJVKVAXDXFXCPQVLWQXEDVLVMVKWGXEWRWQDWGXDWHEWGXCCPQWGXCCXTWJUAZW
      LUCZSZUFSCWPYMUFCWMYLWKYKWLGXTULWKYKULKWJGXTVNVOVPVQVRABABCXQWIFJVSVTUTVA
      WAWBWGWHNOXAWOULCWCABWHDEFGHIJKWDWEWGXBWQWRDABCDEFGHIJKWDUTWF $.
      $( [5-Oct-2004] $)
  $}

  ${
    $d x y z w v S $.  $d x y z w v F $.  $d x y z w v C $.  $d x y z w v D $.
    seq111.1 $e |- S e. _V $.
    seq111.2 $e |- F e. _V $.
    $( Value of the recursive sequence builder at 1.  See description in
       ~ df-seq1 . $)
    seq11 $p |- ( ( S seq1 F ) ` 1 ) = ( F ` 1 ) $=
      ( vz vw cv c1 caddc co wceq copab crdg com cres c1st cfv c2nd cop eqid
      seq11lem ) EFABFGZEGZHIJKEFLHMNOZUBUCPQHIJZUCRQUEBQAJSKEFLZCDUDTUFTUA $.
      $( [5-Oct-2004] $)

    $( Value of the recursive sequence builder at a successor.  See description
       in ~ df-seq1 . $)
    seq1p1 $p |- ( A e. NN -> ( ( S seq1 F ) ` ( A + 1 ) ) =
                ( ( ( S seq1 F ) ` A ) S ( F ` ( A + 1 ) ) ) ) $=
      ( vz vw cv c1 caddc co wceq copab crdg com cres c1st cfv c2nd cop eqid
      seq1suclem ) FGABCGHZFHZIJKLFGMINOPZUCUDQRIJKZUDSRUFCRBKTLFGMZDEUEUAUGUAU
      B $.
      $( [5-Oct-2004] $)

    $( Value of the recursive sequence builder in terms of its previous
       value. $)
    seq1m1 $p |- ( ( N e. NN /\ 1 < N ) -> ( ( S seq1 F ) ` N ) =
                ( ( ( S seq1 F ) ` ( N - 1 ) ) S ( F ` N ) ) ) $=
      ( cn wcel c1 clt wbr wa cmin co caddc cseq1 cfv wceq wb 1nn nnsub mpan
      biimpa seq1p1 syl cc nncn ax-1cn npcan sylancl fveq2d adantr opreq2d
      3eqtr3d ) CFGZHCIJZKZCHLMZHNMZABOMZPZUQUSPZURBPZAMZCUSPZVACBPZAMZUPUQFGZU
      TVCQUNUOVGHFGUNUOVGRSHCTUAUBUQABDEUCUDUNUTVDQUOUNURCUSUNCUEGHUEGURCQCUFUG
      CHUHUIZUJUKUNVCVFQUOUNVBVEVAAUNURCBVHUJULUKUM $.
      $( [14-Aug-2005] $)

    $( Functionality and domain of the recursive sequence builder. $)
    seq1fn $p |- ( S seq1 F ) Fn NN $=
      ( vz vw cv c1 caddc co wceq copab crdg com cres c1st cfv c2nd cop eqid
      seq1fnlem ) EFABFGZEGZHIJKEFLHMNOZUBUCPQHIJZUCRQUEBQAJSKEFLZCDUDTUFTUA $.
      $( [8-Oct-2004] $)

    $( Range of the recursive sequence builder. $)
    seq1rn2 $p |- ( ( ( F ` 1 ) e. C /\
               ( F |` ( NN \ { 1 } ) ) : ( NN \ { 1 } ) --> D /\
               S : ( C X. D ) --> C ) -> ran ( S seq1 F ) C_ C ) $=
      ( vx vy vz vw vv c1 cfv wcel cn csn cdif cres wf cxp w3a cseq1 co wfn cv
      wral crn wss seq1fn wi caddc wceq fveq2 eleq1d imbi2d wa seq11 simpl
      syl5eqel 3adant3 seq1lem2 ffvelrn wb fvres adantl mpbid sylan2 ffnoprv
      simprbi opreq1 opreq2 rcla42v ancoms syl5 imp seq1p1 syl5ibr exp4c mpd
      ex com4r impcom 3adant1 com12 a2d nnind r19.21aiv fnfvrnss sylancr ) LDMZ
      ANZOLPQZBDWLRZSZABTZACSZUAZCDUBUCZOUDGUEZWRMZANZGOUFWRUGAUHCDEFUIWQXAGOWS
      ONWQXAWQHUEZWRMZANZUJWQLWRMZANZUJWQIUEZWRMZANZUJWQXGLUKUCZWRMZANZUJWQXAUJ
      HIWSXBLULZXDXFWQXMXCXEAXBLWRUMUNUOXBXGULZXDXIWQXNXCXHAXBXGWRUMUNUOXBXJULZ
      XDXLWQXOXCXKAXBXJWRUMUNUOXBWSULZXDXAWQXPXCWTAXBWSWRUMUNUOWKWNXFWPWKWNUPXE
      WJACDEFUQWKWNURUSUTXGONZWQXIXLWQXQXIXLUJZWNWPXQXRUJZWKWPWNXSWNXQXIWPXLWNX
      QXIWPXLUJUJZWNXQUPXJDMZBNZXTXQWNXJWLNZYBXGVAWNYCUPXJWMMZBNZYBWLBXJWMVBYCY
      EYBVCWNYCYDYABXJWLDVDUNVEVFVGXQYBXTUJWNXQYBXIWPXLYBXIUPZWPUPXLXQXHYACUCZA
      NZYFWPYHWPJUEZKUEZCUCZANZKBUFJAUFZYFYHWPCWOUDYMJKABACVHVIXIYBYMYHUJYLYHXH
      YJCUCZANJKXHYAABYIXHULYKYNAYIXHYJCVJUNYJYAULYNYGAYJYAXHCVKUNVLVMVNVOXQXKY
      GAXGCDEFVPUNVQVRVEVSVTWAWBWCWDWEWFWDWGGOAWRWHWI $.
      $( [8-Oct-2004] $)

    $( Range of the recursive sequence builder (special case of ~ seq1rn2 ). $)
    seq1rn $p |- ( ( F : NN --> C /\ S : ( C X. C ) --> C ) ->
                 ran ( S seq1 F ) C_ C ) $=
      ( cn wf c1 cfv wcel csn cdif cres wa cxp cseq1 co crn wss 1nn ffvelrn
      mpan2 difss fssres jca seq1rn2 3expa sylan ) FACGZHCIAJZFHKZLZACULMGZNAAO
      ABGZBCPQRASZUIUJUMUIHFJUJTFAHCUAUBUIULFSUMFUKUCFAULCUDUBUEUJUMUNUOAABCDEU
      FUGUH $.
      $( [8-Oct-2004] $)

    $( Mapping of the 1-based recursive sequence builder. $)
    seq1f $p |- ( ( F : NN --> C /\ S : ( C X. C ) --> C ) ->
                 ( S seq1 F ) : NN --> C ) $=
      ( cn wf cxp wa cseq1 co wfn crn wss seq1rn seq1fn jctil df-f sylibr ) FAC
      GAAHABGIZBCJKZFLZUAMANZIFAUAGTUCUBABCDEOBCDEPQFAUARS $.
      $( [17-Aug-2006] $)

    $( Mapping of the 1-based recursive sequence builder. $)
    seq1f2 $p |- ( ( ( F ` 1 ) e. C /\
               ( F |` ( NN \ { 1 } ) ) : ( NN \ { 1 } ) --> D /\
               S : ( C X. D ) --> C ) -> ( S seq1 F ) : NN --> C ) $=
      ( c1 cfv wcel cn csn cdif cres wf cxp w3a cseq1 co wfn crn wss wa
      seq1rn2 seq1fn jctil df-f sylibr ) GDHAIJGKLZBDUHMNABOACNPZCDQRZJSZUJTAUA
      ZUBJAUJNUIULUKABCDEFUCCDEFUDUEJAUJUFUG $.
      $( [23-Sep-2007] $)

    $( Closure of the value of the recursive sequence builder. $)
    seq1cl $p |- ( ( A e. NN /\ F : NN --> C /\ S : ( C X. C ) --> C ) ->
                 ( ( S seq1 F ) ` A ) e. C ) $=
      ( cn wf cxp wcel cseq1 co cfv wa seq1f ffvelrn sylan 3impa 3comr ) GBDHZB
      BIBCHZAGJZACDKLZMBJZTUAUBUDTUANGBUCHUBUDBCDEFOGBAUCPQRS $.
      $( [7-Mar-2005] $)

    $( Closure of the value of the recursive sequence builder. $)
    seq1cl2 $p |- ( ( ( ( F ` 1 ) e. C /\
               ( F |` ( NN \ { 1 } ) ) : ( NN \ { 1 } ) --> D /\
          S : ( C X. D ) --> C ) /\ A e. NN ) -> ( ( S seq1 F ) ` A ) e. C ) $=
      ( c1 cfv wcel cn csn cdif cres wf cxp w3a cseq1 co seq1f2 ffvelrn sylan
      ) HEIBJKHLMZCEUCNOBCPBDOQKBDERSZOAKJAUDIBJBCDEFGTKBAUDUAUB $.
      $( [23-Sep-2007] $)

    $( Restricting its characteristic function to ` NN ` does not affect the
       ` seq1 ` function. $)
    seq1res $p |- ( S seq1 ( F |` NN ) ) = ( S seq1 F ) $=
      ( vx vy vz cn cres cseq1 co wceq cv cfv wral c1 caddc fveq2 eqeq12d wcel
      1nn fvres ax-mp cvv resexg seq11 3eqtr4i wa id peano2nn syl opreqan12rd
      seq1p1 adantr 3eqtr4d ex nnind rgen wfn wb seq1fn eqfnfv2 mp2an mpbir )
      ABHIZJKZABJKZLZEMZVFNZVIVGNZLZEHOZVLEHFMZVFNZVNVGNZLPVFNZPVGNZLGMZVFNZVSV
      GNZLZVSPQKZVFNZWCVGNZLZVLFGVIVNPLVOVQVPVRVNPVFRVNPVGRSVNVSLVOVTVPWAVNVSVF
      RVNVSVGRSVNWCLVOWDVPWEVNWCVFRVNWCVGRSVNVILVOVJVPVKVNVIVFRVNVIVGRSPVENZPBN
      ZVQVRPHTWGWHLUAPHBUBUCAVECBUDTVEUDTDBHUDUEUCZUFABCDUFUGVSHTZWBWFWJWBUHVTW
      CVENZAKZWAWCBNZAKZWDWEWBWJVTWAWKWMAWBUIWJWCHTWKWMLVSUJWCHBUBUKULWJWDWLLWB
      VSAVECWIUMUNWJWEWNLWBVSABCDUMUNUOUPUQURVFHUSVGHUSVHVMUTAVECWIVAABCDVAEHVF
      VGVBVCVD $.
      $( [16-Aug-2005] $)
  $}

  ${
    $d S s f $.  $d F f $.
    $( Value of the recursive sequence builder at 1.  (Contributed by Jeff
       Madsen, 7-Jan-2011.) $)
    seq11g $p |- ( ( S e. A /\ F e. B ) ->
                                    ( ( S seq1 F ) ` 1 ) = ( F ` 1 ) ) $=
      ( vs vf c1 cv cseq1 co cfv wceq opreq1 fveq1d eqeq1d opreq2 fveq1
      eqeq12d visset seq11 vtocl2g ) GEHZFHZIJZKZGUCKZLGCUCIJZKZUFLGCDIJZKZGDKZ
      LEFCDABUBCLZUEUHUFULGUDUGUBCUCIMNOUCDLZUHUJUFUKUMGUGUIUCDCIPNGUCDQRUBUCES
      FSTUA $.
      $( [7-Jan-2011] $)
  $}

  ${
    $d S s f $.  $d F f $.  $d A s f $.
    $( Value of the recursive sequence builder at a successor.  (Contributed by
       Jeff Madsen, 7-Jan-2011.) $)
    seq1p1g $p |- ( ( S e. B /\ F e. C /\ A e. NN ) -> ( ( S seq1 F ) `
              ( A + 1 ) ) = ( ( ( S seq1 F ) ` A ) S ( F ` ( A + 1 ) ) ) ) $=
      ( vs vf wcel cn c1 caddc co cseq1 cfv wceq cv wi opreq1 fveq1d id eqidd
      opreq123d eqeq12d imbi2d opreq2 fveq1 opreq12d visset seq1p1 vtocl2g
      3impia ) DBHECHAIHZAJKLZDEMLZNZAUNNZUMENZDLZOZULUMFPZGPZMLZNZAVBNZUMVANZU
      TLZOZQULUMDVAMLZNZAVHNZVEDLZOZQULUSQFGDEBCUTDOZVGVLULVMVCVIVFVKVMUMVBVHUT
      DVAMRZSVMVDVJVEVEUTDVMTVMAVBVHVNSVMVEUAUBUCUDVAEOZVLUSULVOVIUOVKURVOUMVHU
      NVAEDMUEZSVOVJUPVEUQDVOAVHUNVPSUMVAEUFUGUCUDAUTVAFUHGUHUIUJUK $.
      $( [7-Jan-2011] $)
  $}

  ${
    $d f F $.
    $( An infinite series of complex terms is a function from ` NN ` to
       ` CC ` . $)
    ser1f $p |- ( F : NN --> CC -> ( + seq1 F ) : NN --> CC ) $=
      ( vf cvv wcel cn cc wf caddc cseq1 co nnex fex mpan2 cv wi wceq feq1
      opreq2 feq1d imbi12d wfn crn wss wa cxp ax-addopr addex visset seq1rn
      seq1fn jctil df-f sylibr vtoclg mpcom ) ACDZEFAGZEFHAIJZGZUQECDUPKEFCALME
      FBNZGZEFHUTIJZGZOUQUSOBACUTAPZVAUQVCUSEFUTAQVDEFVBURUTAHIRSTVAVBEUAZVBUBF
      UCZUDVCVAVFVEVAFFUEFHGVFUFFHUTUGBUHZUIMHUTUGVGUJUKEFVBULUMUNUO $.
      $( [18-Apr-2005] $)
  $}

  ${
    ser1f.1 $e |- F : NN --> CC $.
    $( An infinite series is a function from ` NN ` to ` CC ` . $)
    ser1fi $p |- ( + seq1 F ) : NN --> CC $=
      ( cn cc wf caddc cseq1 co ser1f ax-mp ) CDAECDFAGHEBAIJ $.
      $( [19-Mar-2005] $)
  $}

  ${
    $d x A $.  $d x y F $.
    ser1cl1.1 $e |- F : NN --> CC $.
    $( The partial sums in an infinite series of complex terms are complex. $)
    ser1cl1i $p |- ( A e. NN -> ( ( + seq1 F ) ` A ) e. CC ) $=
      ( cn wcel cc wf cxp caddc cseq1 co cfv ax-addopr addex cvv nnex fex
      mp2an seq1cl mp3an23 ) ADEDFBGZFFHFIGAIBJKLFECMAFIBNUADOEBOECPDFOBQRST $.
      $( [17-Apr-2005] $)
  $}

  ${
    $d x A $.  $d x y F $.
    ser1recl.1 $e |- F : NN --> RR $.
    $( The partial sums in an infinite series of real terms are real. $)
    ser1recli $p |- ( A e. NN -> ( ( + seq1 F ) ` A ) e. RR ) $=
      ( vx vy cv caddc cseq1 co cfv cr wcel c1 wceq fveq2 eleq1d addex cn wf
      cvv nnex fex mp2an seq11 1nn ffvelrn eqeltri wa peano2nn ffvelrni syl
      readdcl sylan2 wb seq1p1 adantl mpbird expcom nnind ) DFZGBHIZJZKLMVAJZKL
      EFZVAJZKLZVDMGIZVAJZKLZAVAJZKLDEAUTMNVBVCKUTMVAOPUTVDNVBVEKUTVDVAOPUTVGNV
      BVHKUTVGVAOPUTANVBVJKUTAVAOPVCMBJZKGBQRKBSZRTLBTLCUARKTBUBUCZUDVLMRLVKKLC
      UERKMBUFUCUGVFVDRLZVIVFVNUHVIVEVGBJZGIZKLZVNVFVOKLZVQVNVGRLVRVDUIRKVGBCUJ
      UKVEVOULUMVNVIVQUNVFVNVHVPKVDGBQVMUOPUPUQURUS $.
      $( [17-Mar-2005] $)

    $( The partial sums of an infinite series of reals is an infinite real
       sequence. $)
    ser1refi $p |- ( + seq1 F ) : NN --> RR $=
      ( vx cn cr caddc cseq1 co wf wfn cv cfv wcel wral ffnfv addex cvv nnex
      fex mp2an seq1fn ser1recli rgen mpbir2an ) DEFAGHZIUEDJCKZUELEMZCDNCDEUEO
      FAPDEAIDQMAQMBRDEQASTUAUGCDUFABUBUCUD $.
      $( [18-Mar-2005] $)
  $}

  ${
    $d x y z $.  $d y A $.  $d x B $.  $d z F $.
    ser1cl2.1 $e |- F = { <. x , y >. | ( x e. NN /\ y = A ) } $.
    ${
      ser1cl2.2 $e |- A. x e. NN A e. CC $.
      $( Closure of the value of the ` B ` th term of an infinite series. $)
      ser1cl2i $p |- ( B e. NN -> ( ( + seq1 F ) ` B ) e. CC ) $=
        ( cc wcel cn wral wf fopab2 mpbi ser1cl1i ) DECHIAJKJHELGABJHCEFMNO $.
        $( [7-Mar-2005] $) $( [8-Nov-2005] $)

      $( An infinite series is a function from ` NN ` to ` CC ` . $)
      ser1f2i $p |- ( + seq1 F ) : NN --> CC $=
        ( vz cn cc caddc cseq1 co wf wfn cv cfv wcel wral ffnfv addex nnex
        fopabex2 seq1fn ser1cl2i rgen mpbir2an ) HIJDKLZMUGHNGOZUGPIQZGHRGHIUGS
        JDTABHCDUAEUBUCUIGHABCUHDEFUDUEUF $.
        $( [5-Mar-2005] $)
    $}

    ${
      $d y B $.
      ser11.2 $e |- B e. _V $.
      ser11.3 $e |- ( x = 1 -> A = B ) $.
      $( The value of the first term in an infinite series. $)
      ser11i $p |- ( ( + seq1 F ) ` 1 ) = B $=
        ( c1 caddc cseq1 co cfv addex cn nnex fopabex2 seq11 wcel wceq 1nn
        fvopab4 ax-mp eqtri ) IJEKLMIEMZDJENABOCEPFQRIOSUEDTUAABICDOEHFGUBUCUD
        $.
        $( [13-Mar-2005] $) $( [8-Nov-2005] $)
    $}

    ${
      $d y B $.  $d x y C $.
      ser1p1.2 $e |- C e. _V $.
      ser1p1.3 $e |- ( x = ( B + 1 ) -> A = C ) $.
      $( The value of the next term in an infinite series. $)
      ser1p1i $p |- ( B e. NN ->
               ( ( + seq1 F ) ` ( B + 1 ) ) = ( ( ( + seq1 F ) ` B ) + C ) ) $=
        ( cn wcel c1 caddc co cseq1 cfv addex nnex fopabex2 seq1p1 wceq
        peano2nn fvopab4 syl opreq2d eqtrd ) DJKZDLMNZMFONZPDUIPZUHFPZMNUJEMNDM
        FQABJCFRGSTUGUKEUJMUGUHJKUKEUADUBABUHCEJFIGHUCUDUEUF $.
        $( [14-Mar-2005] $) $( [8-Nov-2005] $)
    $}
  $}

  ${
    $d y A $.  $d x y F $.
    ser1mono.1 $e |- F : NN --> RR $.
    ser1mono.2 $e |- ( x e. NN -> 0 <_ ( F ` x ) ) $.
    $( The partial sums in an infinite series of positive terms form a
       monotonic sequence. $)
    ser1monoi $p |- ( A e. NN ->
                  ( ( + seq1 F ) ` A ) <_ ( ( + seq1 F ) ` ( A + 1 ) ) ) $=
      ( vy cn wcel caddc cseq1 co cfv cc0 c1 cle cc wceq cr wf wss ax-resscn
      fss mp2an ser1cl1i addid1 syl wbr peano2nn cv fveq2 breq2d vtoclga wb
      ffvelrni ser1recli 0re leadd2 mp3an1 syl11anc mpbid addex cvv nnex fex
      seq1p1 breqtrrd eqbrtrrd ) BGHZBICJKZLZMIKZVJBNIKZVILZOVHVJPHVKVJQBCGRCSZ
      RPTGPCSDUAGRPCUBUCUDVJUEUFVHVKVJVLCLZIKZVMOVHMVOOUGZVKVPOUGZVHVLGHZVQBUHZ
      MFUIZCLZOUGZVQFVLGWAVLQWBVOMOWAVLCUJUKMAUIZCLZOUGWCAWAGWDWAQWEWBMOWDWACUJ
      UKEULULUFVHVORHZVJRHZVQVRUMZVHVSWFVTGRVLCDUNUFBCDUOMRHWFWGWHUPMVOVJUQURUS
      UTBICVAVNGVBHCVBHDVCGRVBCVDUCVEVFVG $.
      $( [17-Mar-2005] $)
  $}

  ${
    $d k j m F $.  $d k j m G $.  $d k j m H $.  $d j k m N $.
    ser1add2.1 $e |- F : NN --> CC $.
    ser1add2.2 $e |- G : NN --> CC $.
    ser1add2.3 $e |- H e. _V $.
    ser1add2.4 $e |- ( ( k e. NN /\ N e. NN /\ k <_ N ) ->
                   ( H ` k ) = ( ( F ` k ) + ( G ` k ) ) ) $.
    $( The sum of two infinite series. $)
    ser1add2i $p |- ( N e. NN ->
   ( ( + seq1 H ) ` N ) = ( ( ( + seq1 F ) ` N ) + ( ( + seq1 G ) ` N ) ) ) $=
      ( vj vm cn wcel caddc cseq1 co cfv wceq cle wbr cr nnre leid syl cv wa
      wi c1 breq1 anbi2d fveq2 opreq12d eqeq12d imbi12d 1nn 3expib vtoclga
      ax-mp addex seq11 cc wf cvv nnex fex mp2an opreq12i 3eqtr4g lep1 adantr
      simpl peano2re simpr letr syl111anc mpand syl2an imp adantllr peano2nn
      imbi2d 3exp imp31 seq1p1 ad3antrrr ser1cl1i ffvelrni add4 syl22anc eqtrd
      3eqtr4d ex imim2d com23 an4s anassrs mpdan exp41 pm2.43d imp3a nnind
      mpan2d pm2.43i ) ELMZENDOPZQZENBOPZQZENCOPZQZNPZRZXDXDEESTZXLXDEUAMZXMEUB
      ZEUCUDXDJUEZESTZUFZXPXEQZXPXGQZXPXIQZNPZRZUGXDUHESTZUFZUHXEQZUHXGQZUHXIQZ
      NPZRZUGXDKUEZESTZUFZYKXEQZYKXGQZYKXIQZNPZRZUGZXDYKUHNPZESTZUFZYTXEQZYTXGQ
      ZYTXIQZNPZRZUGXDXMUFZXLUGJKEXPUHRZXRYEYCYJUUIXQYDXDXPUHESUIUJUUIXSYFYBYIX
      PUHXEUKUUIXTYGYAYHNXPUHXGUKXPUHXIUKULUMUNXPYKRZXRYMYCYRUUJXQYLXDXPYKESUIU
      JUUJXSYNYBYQXPYKXEUKUUJXTYOYAYPNXPYKXGUKXPYKXIUKULUMUNXPYTRZXRUUBYCUUGUUK
      XQUUAXDXPYTESUIUJUUKXSUUCYBUUFXPYTXEUKUUKXTUUDYAUUENXPYTXGUKXPYTXIUKULUMU
      NXPERZXRUUHYCXLUULXQXMXDXPEESUIUJUULXSXFYBXKXPEXEUKUULXTXHYAXJNXPEXGUKXPE
      XIUKULUMUNYEUHDQZUHBQZUHCQZNPZYFYIUHLMYEUUMUUPRZUGZUOXDAUEZESTZUFZUUSDQZU
      USBQZUUSCQZNPZRZUGUURAUHLUUSUHRZUVAYEUVFUUQUVGUUTYDXDUUSUHESUIUJUVGUVBUUM
      UVEUUPUUSUHDUKUVGUVCUUNUVDUUONUUSUHBUKUUSUHCUKULUMUNUUSLMZXDUUTUVFIUPUQUR
      NDUSHUTYGUUNYHUUONNBUSLVABVBLVCMZBVCMFVDLVAVCBVEVFZUTNCUSLVACVBUVICVCMGVD
      LVAVCCVEVFZUTVGVHYKLMZUUBYSUUGUVLXDUUAYSUUGUGZUVLXDUUAUVMUGUVLXDXDUUAUVMU
      VLXDUFZXDUFZUUAUFYLUVMUVLXDUUAYLXDUVNUUAYLUVLYKUAMZXNUUAYLUGXDYKUBXOUVPXN
      UFZYKYTSTZUUAYLUVPUVRXNYKVIVJUVQUVPYTUAMZXNUVRUUAUFYLUGUVPXNVKUVPUVSXNYKV
      LVJUVPXNVMYKYTEVNVOVPVQVRVSUVOUUAYLUVMUVNUUAXDYLUVMUVNUUAUFZYMUVMUVTYSYMU
      UGUVTYRUUGYMUVTYRUUGUVTYRUFZYNYTDQZNPZYQYTBQZYTCQZNPZNPZUUCUUFUWAYNYQUWBU
      WFNUVTYRVMUVTUWBUWFRZYRUVLXDUUAUWHUVLYTLMZXDUUAUWHUGZUGZYKVTZXDUUTUVFUGZU
      GUWKAYTLUUSYTRZUWMUWJXDUWNUUTUUAUVFUWHUUSYTESUIUWNUVBUWBUVEUWFUUSYTDUKUWN
      UVCUWDUVDUWENUUSYTBUKUUSYTCUKULUMUNWAUVHXDUUTUVFIWBUQUDWCVJULUVLUUCUWCRXD
      UUAYRYKNDUSHWDWEUVLUUFUWGRXDUUAYRUVLUUFYOUWDNPZYPUWENPZNPZUWGUVLUUDUWOUUE
      UWPNYKNBUSUVJWDYKNCUSUVKWDULUVLYOVAMUWDVAMZYPVAMUWEVAMZUWQUWGRYKBFWFUVLUW
      IUWRUWLLVAYTBFWGUDYKCGWFUVLUWIUWSUWLLVAYTCGWGUDYOUWDYPUWEWHWIWJWEWKWLWMWN
      VRWOWPWQWRWSWTWNXAXBXC $.
      $( [18-Jul-2005] $)
  $}

  ${
    $d k j m F $.  $d k j m G $.  $d k j m H $.  $d j k m N $.
    ser1add.1 $e |- F : NN --> CC $.
    ser1add.2 $e |- G : NN --> CC $.
    ser1add.3 $e |- H e. _V $.
    ser1add.4 $e |- ( ( k e. NN /\ k <_ N ) ->
                   ( H ` k ) = ( ( F ` k ) + ( G ` k ) ) ) $.
    $( The sum of two infinite series. $)
    ser1addi $p |- ( N e. NN ->
   ( ( + seq1 H ) ` N ) = ( ( ( + seq1 F ) ` N ) + ( ( + seq1 G ) ` N ) ) ) $=
      ( vj vm cn wcel caddc cseq1 co cfv wceq cle wbr cr nnre leid syl cv wa
      wi c1 breq1 anbi2d fveq2 opreq12d eqeq12d imbi12d 1nn ex vtoclga ax-mp
      addex seq11 cc wf cvv nnex fex mp2an opreq12i 3eqtr4g adantl lep1 adantr
      simpl peano2re simpr letr syl111anc mpand syl2an imp peano2nn sylan
      seq1p1 ad2antrr ser1cl1i ffvelrni add4 syl22anc eqtrd 3eqtr4d imim2d
      com23 an4s anassrs mpdan expl nnind mpan2d pm2.43i ) ELMZENDOPZQZENBOPZQZ
      ENCOPZQZNPZRZWSWSEESTZXGWSEUAMZXHEUBZEUCUDWSJUEZESTZUFZXKWTQZXKXBQZXKXDQZ
      NPZRZUGWSUHESTZUFZUHWTQZUHXBQZUHXDQZNPZRZUGWSKUEZESTZUFZYFWTQZYFXBQZYFXDQ
      ZNPZRZUGZWSYFUHNPZESTZUFZYOWTQZYOXBQZYOXDQZNPZRZUGWSXHUFZXGUGJKEXKUHRZXMX
      TXRYEUUDXLXSWSXKUHESUIUJUUDXNYAXQYDXKUHWTUKUUDXOYBXPYCNXKUHXBUKXKUHXDUKUL
      UMUNXKYFRZXMYHXRYMUUEXLYGWSXKYFESUIUJUUEXNYIXQYLXKYFWTUKUUEXOYJXPYKNXKYFX
      BUKXKYFXDUKULUMUNXKYORZXMYQXRUUBUUFXLYPWSXKYOESUIUJUUFXNYRXQUUAXKYOWTUKUU
      FXOYSXPYTNXKYOXBUKXKYOXDUKULUMUNXKERZXMUUCXRXGUUGXLXHWSXKEESUIUJUUGXNXAXQ
      XFXKEWTUKUUGXOXCXPXENXKEXBUKXKEXDUKULUMUNXSYEWSXSUHDQZUHBQZUHCQZNPZYAYDUH
      LMXSUUHUUKRZUGZUOAUEZESTZUUNDQZUUNBQZUUNCQZNPZRZUGZUUMAUHLUUNUHRZUUOXSUUT
      UULUUNUHESUIUVBUUPUUHUUSUUKUUNUHDUKUVBUUQUUIUURUUJNUUNUHBUKUUNUHCUKULUMUN
      UUNLMUUOUUTIUPZUQURNDUSHUTYBUUIYCUUJNNBUSLVABVBLVCMZBVCMFVDLVAVCBVEVFZUTN
      CUSLVACVBUVDCVCMGVDLVAVCCVEVFZUTVGVHVIYFLMZYQYNUUBUVGWSYPYNUUBUGZUVGWSUFZ
      YPUFYGUVHUVIYPYGUVGYFUAMZXIYPYGUGWSYFUBXJUVJXIUFZYFYOSTZYPYGUVJUVLXIYFVJV
      KUVKUVJYOUAMZXIUVLYPUFYGUGUVJXIVLUVJUVMXIYFVMVKUVJXIVNYFYOEVOVPVQVRVSUVIY
      PYGUVHUVGYPWSYGUVHUVGYPUFZYHUVHUVNYNYHUUBUVNYMUUBYHUVNYMUUBUVNYMUFZYIYODQ
      ZNPZYLYOBQZYOCQZNPZNPZYRUUAUVOYIYLUVPUVTNUVNYMVNUVNUVPUVTRZYMUVGYOLMZYPUW
      BYFVTZUWCYPUWBUVAYPUWBUGAYOLUUNYORZUUOYPUUTUWBUUNYOESUIUWEUUPUVPUUSUVTUUN
      YODUKUWEUUQUVRUURUVSNUUNYOBUKUUNYOCUKULUMUNUVCUQVSWAVKULUVGYRUVQRYPYMYFND
      USHWBWCUVGUUAUWARYPYMUVGUUAYJUVRNPZYKUVSNPZNPZUWAUVGYSUWFYTUWGNYFNBUSUVEW
      BYFNCUSUVFWBULUVGYJVAMUVRVAMZYKVAMUVSVAMZUWHUWARYFBFWDUVGUWCUWIUWDLVAYOBF
      WEUDYFCGWDUVGUWCUWJUWDLVAYOCGWEUDYJUVRYKUVSWFWGWHWCWIUPWJWKVSWLWMWNWOWKWP
      WQWR $.
      $( [18-Jul-2005] $)
  $}

