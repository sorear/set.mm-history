$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The infinite sequence builder "seq1"
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u $.  $d z w v u G $.  $d z w v u A $.  $d z w v u B $.
    $d x y z w v u C $.
    om2uz.1 $e |- C e. ZZ $.
    om2uz.2 $e |- G = ( rec ( { <. x , y >. | y = ( x + 1 ) } , C ) |` om ) $.
    $( The mapping ` G ` is a one-to-one mapping from ` om ` onto upper
       integers that will be used to construct a recursive definition
       generator.  Ordinal natural number 0 maps to complex number ` C `
       (normally 0 for the upper integers ` NN0 ` or 1 for the upper integers
       ` NN ` ), 1 maps to ` C ` + 1, etc.  This theorem shows the value of
       ` G ` at ordinal natural number zero.  (This series of theorems
       generalizes an earlier series for ` NN0 ` contributed by Raph Levien,
       10-Apr-2004.) $)
    om2uz0 $p |- ( G ` (/) ) = C $=
      ( c0 cfv cv c1 caddc co wceq copab crdg com cres fveq1i cz wcel fr0t
      ax-mp eqtr ) GDHGBIAIJKLMABNZCOPQZHZCGDUEFRCSTUFCMECSUDUAUBUC $.
      $( [15-Oct-2004] $) $( [3-Oct-2004] $)

    $( The value of ` G ` (see ~ om2uz0 ) at a successor. $)
    om2uzsuc $p |- ( A e. om -> ( G ` suc A ) = ( ( G ` A ) + 1 ) ) $=
      ( vw vv cv csuc cfv c1 caddc co wceq com suceq fveq2d fveq2 opreq1d
      eqeq12d wcel cvv oprex ax-17 copab crdg cres fveq1i opreq1i hbopab1
      hbrdg hbres hbfv hbopr hbxfr opreq1 frsucopab mpan2 vtoclga ) HJZKZELZVBE
      LZMNOZPZCKZELZCELZMNOZPHCQVBCPZVDVIVFVKVLVCVHEVBCRSVLVEVJMNVBCETUAUBVBQUC
      VFUDUCVGVEMNUEABIDVBAJZMNOZVFUDEIJZDUCAUFZVOVBUCAUFZAIVFVBBJVNPZABUGZDUHZ
      QUIZLZMNOVEWBMNVBEWAGUJUKAIWBMNAIVBWAAIVTQAIDVSVRABIULVPUMVOQUCAUFUNVQUOV
      ONUCAUFVOMUCAUFUPUQGVMVEMNURUSUTVA $.
      $( [15-Oct-2004] $) $( [3-Oct-2004] $)

    $( The value ` G ` (see ~ om2uz0 ) at an ordinal natural number is in the
       upper integers. $)
    om2uzuz $p |- ( A e. om -> ( G ` A ) e. { z e. ZZ | C <_ z } ) $=
      ( vv vw cv cfv cle wbr cz crab wcel c0 csuc wceq fveq2 eleq1d om2uz0
      breq2 elrab zre leid mpbir2an eqeltr com c1 caddc co om2uzsuc peano2uz2
      mpan syl5bir finds ) IKZFLZECKZMNZCOPZQRFLZVCQJKZFLZVCQZVESZFLZVCQZDFLZVC
      QIJDUSRTUTVDVCUSRFUAUBUSVETUTVFVCUSVEFUAUBUSVHTUTVIVCUSVHFUAUBUSDTUTVKVCU
      SDFUAUBVDEVCABEFGHUCEVCQEOQZEEMNZVBVMCEOVAEEMUDUEGEEGUFUGUHUIVEUJQZVJVFUK
      ULUMZVCQZVGVNVIVOVCABVEEFGHUNUBVLVGVPGCEVFUOUPUQUR $.
      $( [15-Oct-2004] $) $( [3-Oct-2004] $)

    $( Less-than relation for ` G ` (see ~ om2uz0 ). $)
    om2uzlt $p |- ( ( A e. om /\ B e. om ) ->
                   ( A e. B -> ( G ` A ) < ( G ` B ) ) ) $=
      ( vv vw vz com wcel cfv clt wbr wi cv c0 csuc wceq eleq2 fveq2 breq2d
      imbi12d imbi2d noel pm2.21i a1i wa wo wb elsuc2g bicomd adantl c1 caddc
      co cle om2uzsuc cz crab zleltp1t ssrab2 sseli syl2an om2uzuz cr leloet
      zret 3syl 3bitr2rd id orim12d syl5bi expcom a2d finds impcom ) DLMCLMZCDM
      ZCFNZDFNZOPZQZVTCIRZMZWBWFFNZOPZQZQVTCSMZWBSFNZOPZQZQVTCJRZMZWBWOFNZOPZQZ
      QVTCWOTZMZWBWTFNZOPZQZQVTWEQIJDWFSUAZWJWNVTXEWGWKWIWMWFSCUBXEWHWLWBOWFSFU
      CUDUEUFWFWOUAZWJWSVTXFWGWPWIWRWFWOCUBXFWHWQWBOWFWOFUCUDUEUFWFWTUAZWJXDVTX
      GWGXAWIXCWFWTCUBXGWHXBWBOWFWTFUCUDUEUFWFDUAZWJWEVTXHWGWAWIWDWFDCUBXHWHWCW
      BOWFDFUCUDUEUFWNVTWKWMCUGUHUIWOLMZVTWSXDVTXIWSXDQVTXIUJZWPCWOUAZUKZWRWBWQ
      UAZUKZQXDWSXJXLXAXNXCXIXLXAULVTXIXAXLCWOLUMUNUOXJXCWBWQUPUQURZOPZWBWQUSPZ
      XNXIXCXPULVTXIXBXOWBOABWOEFGHUTUDUOWBEKRUSPZKVAVBZMZWQXSMZXQXPULZVTXIWBVA
      MZWQVAMZYBXTYAWBWQVCXSVAWBXRKVAVDZVEZXSVAWQYEVEZVFABKCEFGHVGZABKWOEFGHVGZ
      VFWBVHMZWQVHMZXQXNULVTXIWBWQVIVTXTYCYJYHYFWBVJVKXIYAYDYKYIYGWQVJVKVFVLUEW
      SWPWRXKXMWSVMXKXMQWSCWOFUCUIVNVOVPVQVRVS $.
      $( [15-Oct-2004] $) $( [3-Oct-2004] $)

    $( The mapping ` G ` (see ~ om2uz0 ) preserves order. $)
    om2uzlt2 $p |- ( ( A e. om /\ B e. om ) ->
                   ( A e. B <-> ( G ` A ) < ( G ` B ) ) ) $=
      ( vz com wcel wa cfv clt wbr om2uzlt wceq wo wn wi fveq2 a1i orim12d
      ancoms wb con0 wss onsseleq ontri1 bitr3d nnont syl2an cr cle leloet
      lenltt cv cz crab om2uzuz ssrab2 zssre sstri sseli syl 3imtr3d a3d
      impbid ) CJKZDJKZLZCDKZCFMZDFMZNOZABCDEFGHPVKVLVOVKDCKZDCQZRZVNVMNOZVNVMQ
      ZRZVLSZVOSZVJVIVRWATVJVILZVPVSVQVTABDCEFGHPVQVTTWDDCFUAUBUCUDVJVIVRWBUEZD
      UFKZCUFKZWEVJVIWFWGLDCUGVRWBDCUHDCUIUJDUKCUKULUDVJVIWAWCUEZVNUMKZVMUMKZWH
      VJVIWIWJLVNVMUNOWAWCVNVMUOVNVMUPUJVJVNEIUQUNOZIURUSZKWIABIDEFGHUTWLUMVNWL
      URUMWKIURVAVBVCZVDVEVIVMWLKWJABICEFGHUTWLUMVMWMVDVEULUDVFVGVH $.
      $( [7-May-2005] $) $( [4-May-2005] $)

    $( Range of ` G ` (see ~ om2uz0 ). $)
    om2uzran $p |- ran G = { z e. ZZ | C <_ z } $=
      ( vu vw vv crn cv cle wbr cz crab wcel cfv wceq com wrex wfn wb c1 caddc
      co copab crdg cres frfnom fneq1 ax-mp mpbir fvelrnb eleq1 om2uzuz
      syl5cbi r19.23aiv sylbi wa breq2 elrab c0 om2uz0 peano1 fnfvelrn mp2an
      eqeltrr a1i wi w3a csuc om2uzsuc opreq1 sylan9eq peano2 mpan syl adantr
      eqeltrrd r19.23aiva uzind mp3an1 impbi eqriv ) HEKZDCLZMNZCOPZHLZWFQZWJWI
      QZWKILZERZWJSZITUAZWLETUBZWKWPUCWQBLALUDUEUFSABUGZDUHTUIZTUBZDWRUJEWSSWQW
      TUCGTEWSUKULUMZITWJEUNULWOWLITWOWNWIQWLWMTQZWNWJWIUOABCWMDEFGUPUQURUSWLWJ
      OQZDWJMNZUTWKWHXDCWJOWGWJDMVAVBDOQZXCXDWKFWMWFQDWFQZJLZWFQZXGUDUEUFZWFQZW
      KIJDWJWMDWFUOWMXGWFUOWMXIWFUOWMWJWFUOXFXEVCERZDWFABDEFGVDWQVCTQXKWFQXAVET
      VCEVFVGVHVIXHXJVJXEXGOQDXGMNVKXHWNXGSZITUAZXJWQXHXMUCXAITXGEUNULXLXJITXBX
      LUTWMVLZERZXIWFXBXLXOWNUDUEUFXIABWMDEFGVMWNXGUDUEVNVOXBXOWFQZXLXBXNTQZXPW
      MVPWQXQXPXATXNEVFVQVRVSVTWAUSVIWBWCUSWDWE $.
      $( [15-Oct-2004] $) $( [3-Oct-2004] $)

    $( ` G ` (see ~ om2uz0 ) is a one-to-one onto mapping. $)
    om2uzf1o $p |- G : om -1-1-onto-> { z e. ZZ | C <_ z } $=
      ( vw vv com cv cle wbr cz crab wf1o wf1 crn wceq f1o5 wf cfv wi wral
      f1fv wfn wss df-f c1 caddc co copab crdg cres frfnom wb fneq1 ax-mp
      mpbir om2uzran eqimssi mpbir2an wcel wa clt wo wn cr lttri3t om2uzuz
      ssrab2 sseli zret 3syl syl2an ioran syl6bbr word ordtri3 nnord con2bid
      om2uzlt ancoms orim12d sylbird con1d sylbid rgen2a ) JDCKLMZCNOZEPJWJEQZE
      RZWJSJWJETWKJWJEUAZHKZEUBZIKZEUBZSZWNWPSZUCZIJUDHJUDHIJWJEUEWMEJUFZWLWJUG
      JWJEUHXABKAKUIUJUKSABULZDUMJUNZJUFZDXBUOEXCSXAXDUPGJEXCUQURUSWLWJABCDEFGU
      TZVAVBWTHIJWNJVCZWPJVCZVDZWRWOWQVEMZWQWOVEMZVFZVGZWSXHWRXIVGXJVGVDZXLWOVH
      VCZWQVHVCZWRXMUPXFXGWOWQVIXFWOWJVCWONVCXNABCWNDEFGVJWJNWOWICNVKZVLWOVMVNX
      GWQWJVCWQNVCXOABCWPDEFGVJWJNWQXPVLWQVMVNVOXIXJVPVQXHWSXKXHWSVGWNWPVCZWPWN
      VCZVFZXKXHWSXSWNVRWPVRWSXSVGUPXFXGWNWPVSWNVTWPVTVOWAXHXQXIXRXJABWNWPDEFGW
      BXGXFXRXJUCABWPWNDEFGWBWCWDWEWFWGWHVBXEVB $.
      $( [15-Oct-2004] $) $( [3-Oct-2004] $)

    $( A helper lemma for the value of a recursive definition generator on
       upper integers (typically either ` NN ` or ` NN0 ` ) with characteristic
       function ` F ` and initial value ` A ` .  Normally ` F ` is a function
       on the partition, and ` A ` is a member of the partition.  See also
       comment in ~ om2uz0 . $)
    uzrdgval $p |- ( B e. { z e. ZZ | C <_ z } ->
                  ( ( rec ( F , A ) o. `' G ) ` B ) =
                  ( rec ( F , A ) ` ( `' G ` B ) ) ) $=
      ( cv cle wbr cz crab wcel ccnv cdm crdg ccom cfv wceq crn om2uzran df-rn
      eqtr3 eleq2i wfun con0 wfn rdgfnon fnfun ax-mp com wf1o om2uzf1o f1ocnv
      f1ofun fvco mp3an12 sylbi ) EFCKLMCNOZPEHQZRZPZEGDSZVCTUAEVCUAVFUAUBZVBVD
      EHUCVBVDABCFHIJUDHUEUFUGVFUHZVCUHZVEVGVFUIUJVHDGUKUIVFULUMVBUNVCUOZVIUNVB
      HUOVJABCFHIJUPUNVBHUQUMVBUNVCURUMEVFVCUSUTVA $.
      $( [16-Oct-2004] $) $( [3-Oct-2004] $)

    $( Initial value of a recursive definition generator on upper integers.
       See comment in ~ uzrdgval . $)
    uzrdgini $p |- ( A e. B -> ( ( rec ( F , A ) o. `' G ) ` C ) = A ) $=
      ( vz wcel c0 crdg cfv ccnv ccom rdg0t cv cle wbr cz crab wceq breq2
      elrab zre leid mpbir2an uzrdgval ax-mp om2uz0 com wf1o wi om2uzf1o
      peano1 f1ocnvfv mp2an fveq2i eqtr syl5eq ) CDKLFCMZNZCEVBGOZPNZCDFQVEEVDN
      ZVBNZVCEEJRZSTZJUAUBZKZVEVGUCVKEUAKEESTZVIVLJEUAVHEESUDUEHEEHUFUGUHABJCEE
      FGHIUIUJVFLVBLGNEUCZVFLUCZABEGHIUKULVJGUMLULKVMVNUNABJEGHIUOUPULVJLEGUQUR
      UJUSUTVA $.
      $( [16-Oct-2004] $) $( [3-Oct-2004] $)

    $( Successor value of a recursive definition generator on upper integers.
       See comment in ~ uzrdgval . $)
    uzrdgsuc $p |- ( B e. { z e. ZZ | C <_ z } ->
                  ( ( rec ( F , A ) o. `' G ) ` ( B + 1 ) )
                  = ( F ` ( ( rec ( F , A ) o. `' G ) ` B ) ) ) $=
      ( cv cle wbr cz crab wcel c1 caddc co crdg ccnv ccom cfv csuc wceq
      peano2uz2 mpan uzrdgval syl com wf1o wf om2uzf1o f1ocnv ax-mp f1of
      ffvelrni om2uzsuc f1ocnvfv2 opreq1d eqtrd wi peano2 f1ocnvfv 3syl mpd
      fveq2d con0 nnont rdgsuct 3eqtrd eqtr4d ) EFCKLMCNOZPZEQRSZGDTZHUAZUBZUCZ
      EVQUCZVPUCZGUCZEVRUCZGUCVNVSVOVQUCZVPUCZVTUDZVPUCZWBVNVOVMPZVSWEUEFNPVNWH
      ICFEUFUGABCDVOFGHIJUHUIVNWDWFVPVNWFHUCZVOUEZWDWFUEZVNWIVTHUCZQRSZVOVNVTUJ
      PZWIWMUEVMUJEVQVMUJVQUKZVMUJVQULUJVMHUKZWOABCFHIJUMZUJVMHUNUOVMUJVQUPUOUQ
      ZABVTFHIJURUIVNWLEQRWPVNWLEUEWQUJVMEHUSUGUTVAVNWNWFUJPZWJWKVBZWRVTVCWPWSW
      TWQUJVMWFVOHVDUGVEVFVGVNWNVTVHPWGWBUEWRVTVIDVTGVJVEVKVNWCWAGABCDEFGHIJUHV
      GVL $.
      $( [16-Oct-2004] $) $( [3-Oct-2004] $)

    $( A helper lemma for the value of an ` NN ` or ` NN0 ` -based recursive
       definition generator.  Value at second index.  See comment in
       ~ uzrdgval . $)
    uzrdginip1 $p |- ( A e. B -> ( ( rec ( F , A ) o. `' G ) ` ( C + 1 ) ) =
                  ( F ` A ) ) $=
      ( vz wcel crdg ccnv ccom cfv c1 caddc co uzrdgini fveq2d cv cle wbr cz
      crab wceq breq2 elrab zre leid mpbir2an uzrdgsuc ax-mp syl5eq ) CDKZEFCLG
      MNZOZFOZCFOEPQRUPOZUOUQCFABCDEFGHISTEEJUAZUBUCZJUDUEKZUSURUFVBEUDKEEUBUCZ
      VAVCJEUDUTEEUBUGUHHEEHUIUJUKABJCEEFGHIULUMUN $.
      $( [6-Jun-2005] $) $( [3-Oct-2004] $)

    $( A helper lemma for the value of an ` NN ` or ` NN0 ` -based recursive
       definition generator.  The generated function is a function on the upper
       integers starting at ` C ` (usually 1 or 0).  See comment in
       ~ uzrdgval . $)
    uzrdgfnuz $p |- ( rec ( F , A ) o. `' G ) Fn { z e. ZZ | C <_ z } $=
      ( crdg ccnv ccom cv cle wbr cz crab wfn cvv wf con0 rdgfnon fnf mpbi com
      wss wf1o om2uzf1o f1ocnv ax-mp f1of omsson fss mp2an fco mpbir ) FDJZGKZL
      ZECMNOCPQZRUTSUSTZUASUQTZUTUAURTZVAUQUARVBDFUBUAUQUCUDUTUEURTZUEUAUFVCUTU
      EURUGZVDUEUTGUGVEABCEGHIUHUEUTGUIUJUTUEURUKUJULUTUEUAURUMUNUTUASUQURUOUNU
      TUSUCUP $.
      $( [27-Jun-2005] $) $( [3-Oct-2004] $)
  $}

  $c seq1 $.  $( Symbol for recursive sequence builder. $)

  $( Extend class notation with recursive sequence builder. $)
  cseq1 $a class seq1 $.

  ${
    $d x y z w f g h $.
    $( Define a general-purpose operation that builds an recursive sequence
       (i.e. a function on the natural numbers ` NN ` ) whose value at an index
       is a function of its previous value and the value of an input sequence
       at that index.  This definition is complicated, but fortunately it is
       not intended to be used directly.  Instead, the only purpose of this
       definition is to provide us with an object that has the properties
       expressed by ~ seq11 and ~ seq1p1 .  Typically, those are the main
       theorems that would be used in practice.

       The first operand is the operation that is applied to the previous value
       and the value of the input sequence (second operand).  For example, for
       the operation ` + ` , an input sequence ` F ` with values 1, 1/2, 1/4,
       1/8,... would be transformed into the output sequence ` ( + seq1 F ) `
       with values 1, 3/2, 7/4, 15/8,.., so that
       ` ( ( + seq1 F ) `` 1 ) = 1 ` , ` ( ( + seq1 F ) `` 2 ) = ` 3/2, etc.
       In other words, ` ( + seq1 F ) ` transforms a sequence ` F ` into an
       infinite series.  ` ( + seq1 F ) ~~> 2 ` means "the sum of F(n) from n =
       1 to infinity is 2."  Since limits are unique ( ~ climuni ), then
       by ~ eusn and ~ unisn the "sum of F(n) from n = 1 to infinity" can be
       expressed as ` U. { x | ( + seq1 F ) ~~> x } ` (provided the sequence
       converges) and evaluates to 2 in this example.

       Internally, we define a recursive function whose values are ordered
       pairs starting at ` <. 1 , ( g `` 1 ) >. ` . The first member of the
       ordered pair is a counter used to select the appropriate value of the
       input sequence ` g ` .  The first ` rec ` constructs this function
       on ` om ` , and the converse of the second ` rec ` maps ` NN ` to
       ` om ` .

       This definition has its roots in a series of theorems starting at
       ~ om2uz0 , originally proved by Raph Levien for use with ~ df-exp and
       later generalized for arbitrary recursive sequences.  The related
       definitions ~ df-seq0 and ~ df-seqz build recursive sequences on ` NN0 `
       and general upper integer sets respectively.  Definition ~ df-sum
       extracts the summation values from partial (finite) and complete
       (infinite) series. $)
    df-seq1 $a |- seq1 = { <. <. f , g >. , h >. |
                   h = { <. x , y >. | ( x e. NN /\
                     y = ( 2nd ` ( ( rec ( { <. z , w >. |
                         w = <. ( ( 1st ` z ) + 1 ) ,
                           ( ( 2nd ` z ) f ( g ` ( ( 1st ` z ) + 1 ) ) ) >. } ,
                       <. 1 , ( g ` 1 ) >. ) o. `'
       ( rec ( { <. z , w >. | w = ( z + 1 ) } , 1 ) |` om ) ) ` x ) ) ) } } $.
  $}

  ${
    $d x y v $.  $d z w v u t f $.  $d v A $.  $d w v u t f B $.
    $d x v u t f C $.  $d v u t f G $.
    seq1lem1.1 $e |- G =
                     ( rec ( { <. x , y >. | y = ( x + 1 ) } , 1 ) |` om ) $.
    $( We prove by induction that the first member of the ordered pair
       value of the internal sequence of ` seq1 ` equals its index. $)
    seq1lem1 $p |- ( A e. NN -> ( 1st `
                ( ( rec ( { <. z , w >. | w = <. ( ( 1st ` z ) + 1 ) , B >. } ,
                <. 1 , C >. ) o. `'  G ) ` A ) ) = A ) $=
      ( vv vu vt vf cv c1st cfv c1 caddc co cop wceq copab crdg ccnv ccom
      fveq2 fveq2d id eqeq12d cvv wcel opex 1z uzrdgini ax-mp fveq2i cz
      elisseti op1st eqtr cn wsbc cab cle wbr crab nnzrab eleq2i uzrdgsuc
      sylbi ax-17 hbs1 hbab hbop hbeleq opreq1d sbab opeq12d eqeq2d cbvopab1
      fveq1i fvex dfsbcq abbidv fvopabf syl6eq oprex opreq1 sylan9eq ex nnind
      ) JNZDNZCNZOPZQRSZFTZUAZCDUBZQGTZUCHUDUEZPZOPZWLUAQXAPZOPZQUAKNZXAPZOPZXF
      UAZXFQRSZXAPZOPZXJUAZEXAPZOPZEUAJKEWLQUAZXCXEWLQXPXBXDOWLQXAUFUGXPUHUIWLX
      FUAZXCXHWLXFXQXBXGOWLXFXAUFUGXQUHUIWLXJUAZXCXLWLXJXRXBXKOWLXJXAUFUGXRUHUI
      WLEUAZXCXOWLEXSXBXNOWLEXAUFUGXSUHUIXEWTOPQXDWTOWTUJUKXDWTUAQGULABWTUJQWSH
      UMIUNUOUPQGQUQUMURUSUTXFVAUKZXIXMXTXIXLXHQRSZXJXTXLYALNFUKZCXGVBZLVCZTZOP
      YAXTXKYEOXTXKXGWSPZYEXTXFQWLVDVEJUQVFZUKXKYFUAVAYGXFJVGVHABJWTXFQWSHUMIVI
      VJYFXGWMWLOPZQRSZYBCWLVBZLVCZTZUAZJDUBZPYEXGWSYNWRYMCDJWRJVKCDYLCDYIYKWMY
      IUKCVKYJCLDYBCJVLVMVNVOWNWLUAZWQYLWMYOWPYIFYKYOWOYHQRWNWLOUFVPCJLFVQVRVSV
      TWAJDMXGYLYEMNZXGUKJVKYPYEUKJVKXFXAWBYAYDULWLXGUAZYIYAYKYDYQYHXHQRWLXGOUF
      VPYQYJYCLYBCWLXGWCWDVRWEUTWFUGYAYDXHQRWGUSWFXHXFQRWHWIWJWK $.
      $( [17-Oct-2004] $) $( [5-Oct-2004] $)
  $}

  $( Lemma for recursive sequence builder theorems. $)
  seq1lem2 $p |- ( A e. NN -> ( A + 1 ) e. ( NN \ { 1 } ) ) $=
    ( c1 caddc co cn wcel clt wbr csn cdif wa wne nngt1ne1t pm5.32i eldifsn
    bitr4 biimp peano2nn cc0 nngt0t cr wb nnret 0re 1re ltadd1t mp3an13 ax1cn
    addid2 breq1i syl6bb syl mpbid sylanc ) ABCDZEFZBUOGHZUOEBIJFZAEFZUPUQKZURU
    TUPUOBLZKURUPUQVAUOMNUOEBOPQARUSSAGHZUQATUSAUAFZVBUQUBAUCVCVBSBCDZUOGHZUQSU
    AFVCBUAFVBVEUBUDUESABUFUGVDBUOGBUHUIUJUKULUMUN $.
    $( [17-Oct-2004] $) $( [8-Oct-2004] $)

  ${
    $d A x z $.  $d w x $.  $d S x z w $.  $d F x z w $.
    seq1rval.1 $e |- H = { <. z , w >. | w = <. ( ( 1st ` z ) + 1 ) ,
                   ( ( 2nd ` z ) S ( F ` ( ( 1st ` z ) + 1 ) ) ) >. } $.
    seq1rval.2 $e |- A e. V $.
    $( Value of the characteristic function of the inner recursion in
       ~ df-seq1 . $)
    seq1rval $p |- ( H ` A ) = <. ( ( 1st ` A ) + 1 ) ,
                     ( ( 2nd ` A ) S ( F ` ( ( 1st ` A ) + 1 ) ) ) >. $=
      ( cfv cv c1st c1 caddc co c2nd cop wceq copab fveq1i opex fveq2 opreq1d
      fveq2d opreq12d opeq12d fvopab eqtr ) CFICBJAJZKIZLMNZUHOIZUJEIZDNZPZQABR
      ZICKIZLMNZCOIZUQEIZDNZPZCFUOGSABCUNVAHUQUTTUHCQZUJUQUMUTVBUIUPLMUHCKUAUBZ
      VBUKURULUSDUHCOUAVBUJUQEVCUCUDUEUFUG $.
      $( [17-Oct-2004] $) $( [5-Oct-2004] $)
  $}

  ${
    $d x y A $.  $d x y f g h G $.  $d x y f g h H $.  $d x y z w f g h F $.
    $d x y z w f g h S $.
    seq1val.1 $e |- S e. V $.
    seq1val.2 $e |- F e. V $.
    seq1val.3 $e |- G =
                     ( rec ( { <. z , w >. | w = ( z + 1 ) } , 1 ) |` om ) $.
    seq1val.4 $e |- H = { <. z , w >. | w = <. ( ( 1st ` z ) + 1 ) ,
                     ( ( 2nd ` z ) S ( F ` ( ( 1st ` z ) + 1 ) ) ) >. } $.
    $( Value of the recursive sequence builder operation. $)
    seq1val $p |- ( S seq1 F ) = { <. x , y >. | ( x e. NN /\ y =
        ( 2nd ` ( ( rec ( H , <. 1 , ( F ` 1 ) >. ) o. `' G ) ` x ) ) ) } $=
      ( vf vg vh cvv wcel cseq1 co cv cn c1 cfv cop crdg ccnv ccom c2nd wceq
      wa copab c1st caddc com cres nnex opabex2 opreq opeq2d eqeq2d opabbidv
      rdgeq1 syl coeq1d cnveq ax-mp coeq2i syl6eqr fveq1d fveq2d anbi2d fveq1
      opreq2d opeq2 rdgeq2 3syl eqtrd df-seq1 oprabval5 mp2an ) EPQFPQEFRSATZUA
      QZBTZWAHUBUBFUCZUDZUEZGUFZUGZUCZUHUCZUIZUJZABUKZUIIJMNOEFPPWBWCWADTZCTZUL
      UCUBUMSZWOUHUCZWPNTZUCZMTZSZUDZUIZCDUKZUBUBWRUCZUDZUEZWNWOUBUMSUICDUKUBUE
      UNUOZUFZUGZUCZUHUCZUIZUJZABUKWMRWBWCWAWNWPWQWSESZUDZUIZCDUKZXFUEZWGUGZUCZ
      UHUCZUIZUJZABUKABUAWJUPUQWTEUIZXNYDABYEXMYCWBYEXLYBWCYEXKYAUHYEWAXJXTYEXJ
      XSXIUGXTYEXGXSXIYEXDXRUIXGXSUIYEXCXQCDYEXBXPWNYEXAXOWPWQWSWTEURUSUTVAXFXD
      XRVBVCVDWGXIXSGXHUIWGXIUIKGXHVEVFVGVHVIVJUTVKVAWRFUIZYDWLABYFYCWKWBYFYBWJ
      WCYFYAWIUHYFWAXTWHYFXSWFWGYFXSHXFUEZWFYFXRHUIXSYGUIYFXRWNWPWQWPFUCZESZUDZ
      UIZCDUKHYFXQYKCDYFXPYJWNYFXOYIWPYFWSYHWQEWPWRFVLVMUSUTVALVHXFXRHVBVCYFXEW
      DUIXFWEUIYGWFUIUBWRFVLXEWDUBVNXFWEHVOVPVQVDVIVJUTVKVAABCDMNOVRVSVT $.
      $( [17-Oct-2004] $) $( [8-Oct-2004] $)

    $( Lemma for ~ seq1fn . $)
    seq1fnlem $p |- ( S seq1 F ) Fn NN $=
      ( vx vy cn cv c1 cfv cop crdg ccnv ccom c2nd cseq1 co fvex seq1val
      fnopab2 ) KLMKNFOODPQRESTPZUAPCDUBUCUGUAUDKLABCDEFGHIJUEUF $.
      $( [17-Oct-2004] $) $( [8-Oct-2004] $)

    $( Value of the value of the recursive sequence builder operation. $)
    seq1val2 $p |- ( A e. NN -> ( ( S seq1 F ) ` A ) =
          ( 2nd ` ( ( rec ( H , <. 1 , ( F ` 1 ) >. ) o. `' G ) ` A ) ) ) $=
      ( vx vy cv c1 cfv cop crdg ccnv ccom c2nd cn cseq1 co wceq fveq2 fveq2d
      seq1val fvex fvopab4 ) LMCLNZGOOEPQRFSTZPZUAPCULPZUAPUBDEUCUDUKCUEUMUNUAU
      KCULUFUGLMABDEFGHIJKUHUNUAUIUJ $.
      $( [17-Oct-2004] $) $( [5-Oct-2004] $)

    $( Lemma for ~ seq11 . $)
    seq11lem $p |- ( ( S seq1 F ) ` 1 ) = ( F ` 1 ) $=
      ( c1 cseq1 co cfv cop crdg ccnv ccom c2nd cn wcel wceq 1nn seq1val2
      ax-mp cvv opex 1z uzrdgini fveq2i elisseti fvex op2nd 3eqtr ) KCDLMNZKFKK
      DNZOZPEQRNZSNZUQSNUPKTUAUOUSUBUCABKCDEFGHIJUDUEURUQSUQUFUAURUQUBKUPUGABUQ
      UFKFEUHIUIUEUJKUPKTUCUKKDULUMUN $.
      $( [17-Oct-2004] $) $( [5-Oct-2004] $)

    $( Lemma for ~ seq1p1 . $)
    seq1suclem $p |- ( A e. NN -> ( ( S seq1 F ) ` ( A + 1 ) ) =
                ( ( ( S seq1 F ) ` A ) S ( F ` ( A + 1 ) ) ) ) $=
      ( vx vy cn wcel c1 caddc co cfv cop crdg ccnv ccom c2nd cseq1 c1st cv
      cle wbr cz crab wceq nnzrab eleq2i 1z uzrdgsuc sylbi copab fveq2 opreq1d
      fveq2d opreq12d opeq12d eqeq2d eqeq1 sylan9bb cbvopabv eqtr4 fvex
      seq1rval syl6eq oprex op2nd seq1lem1 rdgeq1 ax-mp coeq1i fveq1i fveq2i
      syl5eq opreq2d eqtrd peano2nn seq1val2 syl 3eqtr4d ) CNOZCPQRZGPPESZTZUAZ
      FUBZUCZSZUDSZCWMSZUDSZWHESZDRZWHDEUERZSZCWTSZWRDRWGWOWQWPUFSZPQRZESZDRZWS
      WGWOXDXFTZUDSXFWGWNXGUDWGWNWPGSZXGWGCPLUGZUHUILUJUKZOWNXHULNXJCLUMUNABLWJ
      CPGFUOJUPUQLMWPDEGGBUGZAUGZUFSZPQRZXLUDSZXNESZDRZTZULZABURZMUGZXIUFSZPQRZ
      XIUDSZYCESZDRZTZULZLMURKYHXSLMABXIXLULZYHYAXRULYAXKULXSYIYGXRYAYIYCXNYFXQ
      YIYBXMPQXIXLUFUSUTZYIYDXOYEXPDXIXLUDUSYIYCXNEYJVAVBVCVDYAXKXRVEVFVGVHCWMV
      IVJVKVAXDXFXCPQVLWQXEDVLVMVKWGXEWRWQDWGXDWHEWGXCCPQWGCXTWJUAZWLUCZSZUFSCX
      CABABCXQWIFJVNWPYMUFCWMYLWKYKWLGXTULWKYKULKWJGXTVOVPVQVRVSVTUTVAWAWBWGWHN
      OXAWOULCWCABWHDEFGHIJKWDWEWGXBWQWRDABCDEFGHIJKWDUTWF $.
      $( [17-Oct-2004] $) $( [5-Oct-2004] $)
  $}

  ${
    $d x y z w v S $.  $d x y z w v F $.  $d x y z w v C $.  $d x y z w v D $.
    seq111.1 $e |- S e. V $.
    seq111.2 $e |- F e. V $.
    $( Value of the recursive sequence builder at 1.  See description in
       ~ df-seq1 . $)
    seq11 $p |- ( ( S seq1 F ) ` 1 ) = ( F ` 1 ) $=
      ( vz vw cv c1 caddc co wceq copab crdg com cres c1st cfv c2nd cop eqid
      seq11lem ) EFABFGZEGZHIJKEFLHMNOZUBUCPQHIJZUCRQUEBQAJSKEFLZCDUDTUFTUA $.
      $( [17-Oct-2004] $) $( [5-Oct-2004] $)

    $( Value of the recursive sequence builder at a successor.  See description
       in ~ df-seq1 . $)
    seq1p1 $p |- ( A e. NN -> ( ( S seq1 F ) ` ( A + 1 ) ) =
                ( ( ( S seq1 F ) ` A ) S ( F ` ( A + 1 ) ) ) ) $=
      ( vz vw cv c1 caddc co wceq copab crdg com cres c1st cfv c2nd cop eqid
      seq1suclem ) FGABCGHZFHZIJKLFGMINOPZUCUDQRIJKZUDSRUFCRBKTLFGMZDEUEUAUGUAU
      B $.
      $( [17-Oct-2004] $) $( [5-Oct-2004] $)

    $( Value of the recursive sequence builder in terms of its previous
       value. $)
    seq1m1 $p |- ( ( N e. NN /\ 1 < N ) -> ( ( S seq1 F ) ` N ) =
                ( ( ( S seq1 F ) ` ( N - 1 ) ) S ( F ` N ) ) ) $=
      ( cn wcel c1 clt wbr wa cmin co caddc cseq1 cfv wceq wb 1nn nnsubt mpan
      biimpa seq1p1 syl cc nncnt ax1cn npcant mpan2 fveq2d adantr opreq2d
      3eqtr3d ) CFGZHCIJZKZCHLMZHNMZABOMZPZUQUSPZURBPZAMZCUSPZVACBPZAMZUPUQFGZU
      TVCQUNUOVGHFGUNUOVGRSHCTUAUBUQABDEUCUDUNUTVDQUOUNURCUSUNCUEGZURCQZCUFVHHU
      EGVIUGCHUHUIUDZUJUKUNVCVFQUOUNVBVEVAAUNURCBVJUJULUKUM $.
      $( [18-Aug-2005] $) $( [14-Aug-2005] $)

    $( Functionality and domain of the recursive sequence builder. $)
    seq1fn $p |- ( S seq1 F ) Fn NN $=
      ( vz vw cv c1 caddc co wceq copab crdg com cres c1st cfv c2nd cop eqid
      seq1fnlem ) EFABFGZEGZHIJKEFLHMNOZUBUCPQHIJZUCRQUEBQAJSKEFLZCDUDTUFTUA $.
      $( [17-Oct-2004] $) $( [8-Oct-2004] $)

    $( Range of the recursive sequence builder. $)
    seq1rn2 $p |- ( ( ( F ` 1 ) e. C /\
               ( F |` ( NN \ { 1 } ) ) : ( NN \ { 1 } ) --> D /\
               S : ( C X. D ) --> C ) -> ran ( S seq1 F ) (_ C ) $=
      ( vx vy vz vw vv c1 cfv wcel cn csn cdif cres wf cxp w3a cv cseq1 co
      wral crn wss wi caddc wceq fveq2 eleq1d imbi2d wa pm3.26 seq11 syl5eqel
      3adant3 ffvelrn wb fvres adantl mpbid seq1lem2 sylan2 seq1p1 opreq1
      opreq2 rcla42v ancoms wfn ffnoprval pm3.27bi syl5 imp syl5bir exp4c mpd
      ex com4r impcom 3adant1 com12 a2d nnind r19.21aiv seq1fn fnfvrnss mpan
      syl ) LDMZANZOLPQZBDWMRZSZABTZACSZUAZGUBZCDUCUDZMZANZGOUEZWTUFAUGZWRXBGOW
      SONWRXBWRHUBZWTMZANZUHWRLWTMZANZUHWRIUBZWTMZANZUHWRXJLUIUDZWTMZANZUHWRXBU
      HHIWSXELUJZXGXIWRXPXFXHAXELWTUKULUMXEXJUJZXGXLWRXQXFXKAXEXJWTUKULUMXEXMUJ
      ZXGXOWRXRXFXNAXEXMWTUKULUMXEWSUJZXGXBWRXSXFXAAXEWSWTUKULUMWLWOXIWQWLWOUNW
      KAXHWLWOUOCDEFUPUQURXJONZWRXLXOWRXTXLXOUHZWOWQXTYAUHZWLWQWOYBWOXTXLWQXOWO
      XTXLWQXOUHUHZWOXTUNXMDMZBNZYCWOXMWMNZYEXTWOYFUNXMWNMZBNZYEWMBXMWNUSYFYHYE
      UTWOYFYGYDBXMWMDVAULVBVCXJVDVEXTYEYCUHWOXTYEXLWQXOXTXOXKYDCUDZANZYEXLUNZW
      QUNXTXNYIAXJCDEFVFULYKWQYJYKJUBZKUBZCUDZANZKBUEJAUEZYJWQXLYEYPYJUHYOYJXKY
      MCUDZANJKXKYDABYLXKUJYNYQAYLXKYMCVGULYMYDUJYQYIAYMYDXKCVHULVIVJWQCWPVKYPJ
      KABACVLVMVNVOVPVQVBVRVSVTWAWBWCWDWEWCWFWTOVKXCXDCDEFWGGOAWTWHWIWJ $.
      $( [17-Oct-2004] $) $( [8-Oct-2004] $)

    $( Range of the recursive sequence builder (special case of ~ seq1rn2 ). $)
    seq1rn $p |- ( ( F : NN --> C /\ S : ( C X. C ) --> C ) ->
                 ran ( S seq1 F ) (_ C ) $=
      ( c1 cfv wcel cn csn cdif cres wf wa cxp cseq1 co crn wss seq1rn2 3expa
      1nn ffvelrn mpan2 difss fssres jca sylan ) FCGAHZIFJZKZACUKLMZNAAOABMZBCP
      QRASZIACMZUIULUMUNAABCDETUAUOUIULUOFIHUIUBIAFCUCUDUOUKISULIUJUEIAUKCUFUDU
      GUH $.
      $( [9-Nov-2004] $) $( [8-Oct-2004] $)

    $( Mapping of the 1-based recursive sequence builder. $)
    seq1f $p |- ( ( F : NN --> C /\ S : ( C X. C ) --> C ) ->
                 ( S seq1 F ) : NN --> C ) $=
      ( cn wf cxp wa cseq1 co wfn crn wss seq1rn seq1fn jctil df-f sylibr ) FAC
      GAAHABGIZBCJKZFLZUAMANZIFAUAGTUCUBABCDEOBCDEPQFAUARS $.
      $( [18-Aug-2006] $) $( [17-Aug-2006] $)

    $( Mapping of the 1-based recursive sequence builder. $)
    seq1f2 $p |- ( ( ( F ` 1 ) e. C /\
               ( F |` ( NN \ { 1 } ) ) : ( NN \ { 1 } ) --> D /\
               S : ( C X. D ) --> C ) -> ( S seq1 F ) : NN --> C ) $=
      ( c1 cfv wcel cn csn cdif cres wf cxp w3a cseq1 co wfn crn wss wa
      seq1rn2 seq1fn jctil df-f sylibr ) GDHAIJGKLZBDUHMNABOACNPZCDQRZJSZUJTAUA
      ZUBJAUJNUIULUKABCDEFUCCDEFUDUEJAUJUFUG $.
      $( [10-Oct-2007] $) $( [23-Sep-2007] $)

    $( Closure of the value of the recursive sequence builder. $)
    seq1cl $p |- ( ( A e. NN /\ F : NN --> C /\ S : ( C X. C ) --> C ) ->
                 ( ( S seq1 F ) ` A ) e. C ) $=
      ( cn wf cxp wcel cseq1 co cfv wa ffvelrn seq1f sylan 3impa 3comr ) GBDHZB
      BIBCHZAGJZACDKLZMBJZTUAUBUDGBUCHUBUDTUANGBAUCOBCDEFPQRS $.
      $( [8-Mar-2005] $) $( [7-Mar-2005] $)

    $( Closure of the value of the recursive sequence builder. $)
    seq1cl2 $p |- ( ( ( ( F ` 1 ) e. C /\
               ( F |` ( NN \ { 1 } ) ) : ( NN \ { 1 } ) --> D /\
          S : ( C X. D ) --> C ) /\ A e. NN ) -> ( ( S seq1 F ) ` A ) e. C ) $=
      ( cn cseq1 co wf wcel cfv c1 csn cdif cres cxp w3a ffvelrn seq1f2 sylan
      ) HBDEIJZKAHLAUCMBLNEMBLHNOPZCEUDQKBCRBDKSHBAUCTBCDEFGUAUB $.
      $( [11-Oct-2007] $) $( [23-Sep-2007] $)

    $( Restricting its characteristic function to ` NN ` does not affect the
       ` seq1 ` function. $)
    seq1res $p |- ( S seq1 ( F |` NN ) ) = ( S seq1 F ) $=
      ( vx vy vz cn cres cseq1 co wceq cv cfv wral wfn wa wb cvv wcel resexg
      ax-mp seq1fn eqfnfv mp2an eqid c1 caddc fveq2 eqeq12d 1nn fvres seq11
      3eqtr4 id peano2nn syl opreqan12rd seq1p1 adantr 3eqtr4d ex nnind rgen
      mpbir2an ) ABHIZJKZABJKZLZHHLZEMZVGNZVKVHNZLZEHOZVGHPVHHPVIVJVOQRAVFCBSTV
      FSTDBHSUAUBZUCABCDUCEHHVGVHUDUEHUFVNEHFMZVGNZVQVHNZLUGVGNZUGVHNZLGMZVGNZW
      BVHNZLZWBUGUHKZVGNZWFVHNZLZVNFGVKVQUGLVRVTVSWAVQUGVGUIVQUGVHUIUJVQWBLVRWC
      VSWDVQWBVGUIVQWBVHUIUJVQWFLVRWGVSWHVQWFVGUIVQWFVHUIUJVQVKLVRVLVSVMVQVKVGU
      IVQVKVHUIUJUGVFNZUGBNZVTWAUGHTWJWKLUKUGHBULUBAVFCVPUMABCDUMUNWBHTZWEWIWLW
      EQWCWFVFNZAKZWDWFBNZAKZWGWHWEWLWCWDWMWOAWEUOWLWFHTWMWOLWBUPWFHBULUQURWLWG
      WNLWEWBAVFCVPUSUTWLWHWPLWEWBABCDUSUTVAVBVCVDVE $.
      $( [27-Aug-2005] $) $( [16-Aug-2005] $)
  $}

  ${
    $d f F $.
    $( An infinite series of complex terms is a function from ` NN ` to
       ` CC ` . $)
    ser1ft $p |- ( F : NN --> CC -> ( + seq1 F ) : NN --> CC ) $=
      ( vf cvv wcel cn cc wf caddc cseq1 co nnex fex mpan2 cv wi wceq feq1
      opreq2 feq1d imbi12d wfn crn wss wa cxp axaddopr addex visset seq1rn
      seq1fn jctil df-f sylibr vtoclg mpcom ) ACDZEFAGZEFHAIJZGZUQECDUPKEFCALME
      FBNZGZEFHUTIJZGZOUQUSOBACUTAPZVAUQVCUSEFUTAQVDEFVBURUTAHIRSTVAVBEUAZVBUBF
      UCZUDVCVAVFVEVAFFUEFHGVFUFFHUTUGBUHZUIMHUTUGVGUJUKEFVBULUMUNUO $.
      $( [19-Apr-2005] $) $( [18-Apr-2005] $)
  $}

  ${
    ser1f.1 $e |- F : NN --> CC $.
    $( An infinite series is a function from ` NN ` to ` CC ` . $)
    ser1f $p |- ( + seq1 F ) : NN --> CC $=
      ( cn cc wf caddc cseq1 co ser1ft ax-mp ) CDAECDFAGHEBAIJ $.
      $( [4-Apr-2005] $) $( [19-Mar-2005] $)
  $}

  ${
    $d x A $.  $d x y F $.
    ser1cl1.1 $e |- F : NN --> CC $.
    $( The partial sums in an infinite series of complex terms are complex. $)
    ser1cl1 $p |- ( A e. NN -> ( ( + seq1 F ) ` A ) e. CC ) $=
      ( cn wcel cc wf cxp caddc cseq1 co cfv axaddopr addex cvv nnex fex mp2an
      seq1cl mp3an23 ) ADEDFBGZFFHFIGAIBJKLFECMAFIBNUADOEBOECPDFOBQRST $.
      $( [18-Apr-2005] $) $( [17-Apr-2005] $)
  $}

  ${
    $d x A $.  $d x y F $.
    ser1recl.1 $e |- F : NN --> RR $.
    $( The partial sums in an infinite series of real terms are real. $)
    ser1recl $p |- ( A e. NN -> ( ( + seq1 F ) ` A ) e. RR ) $=
      ( vx vy cv caddc cseq1 co cfv cr wcel c1 wceq fveq2 eleq1d addex cn wf
      cvv nnex fex mp2an seq11 1nn ffvelrn eqeltr wa axaddrcl peano2nn
      ffvelrni syl sylan2 wb seq1p1 adantl mpbird expcom nnind ) DFZGBHIZJZKLMV
      AJZKLEFZVAJZKLZVDMGIZVAJZKLZAVAJZKLDEAUTMNVBVCKUTMVAOPUTVDNVBVEKUTVDVAOPU
      TVGNVBVHKUTVGVAOPUTANVBVJKUTAVAOPVCMBJZKGBQRKBSZRTLBTLCUARKTBUBUCZUDVLMRL
      VKKLCUERKMBUFUCUGVFVDRLZVIVFVNUHVIVEVGBJZGIZKLZVFVOKLZVQVNVEVOUIVNVGRLVRV
      DUJRKVGBCUKULUMVNVIVQUNVFVNVHVPKVDGBQVMUOPUPUQURUS $.
      $( [4-Apr-2005] $) $( [17-Mar-2005] $)

    $( The partial sums of an infinite series of reals is an infinite real
       sequence. $)
    ser1ref $p |- ( + seq1 F ) : NN --> RR $=
      ( vx cn cr caddc cseq1 co wf wfn cv cfv wcel wral ffnfv addex cvv nnex
      fex mp2an seq1fn ser1recl rgen mpbir2an ) DEFAGHZIUEDJCKZUELEMZCDNCDEUEOF
      APDEAIDQMAQMBRDEQASTUAUGCDUFABUBUCUD $.
      $( [5-Apr-2005] $) $( [18-Mar-2005] $)
  $}

  ${
    $d x y z $.  $d y A $.  $d x B $.  $d z F $.
    ser1cl2.1 $e |- F = { <. x , y >. | ( x e. NN /\ y = A ) } $.
    ${
      ser1cl2.2 $e |- A. x e. NN A e. CC $.
      $( Closure of the value of the ` B ` th term of an infinite series. $)
      ser1cl2 $p |- ( B e. NN -> ( ( + seq1 F ) ` B ) e. CC ) $=
        ( cc wcel cn wral wf fopab2 mpbi ser1cl1 ) DECHIAJKJHELGABJHCEFMNO $.
        $( [7-Mar-2005] $) $( [8-Nov-2005] $)

      $( An infinite series is a function from ` NN ` to ` CC ` . $)
      ser1f2 $p |- ( + seq1 F ) : NN --> CC $=
        ( vz cn cc caddc cseq1 co wf wfn cv cfv wcel wral ffnfv addex nnex
        fopabex2 seq1fn ser1cl2 rgen mpbir2an ) HIJDKLZMUGHNGOZUGPIQZGHRGHIUGSJ
        DTABHCDUAEUBUCUIGHABCUHDEFUDUEUF $.
        $( [25-Apr-2005] $) $( [5-Mar-2005] $)
    $}

    ${
      $d y B $.
      ser11.2 $e |- B e. V $.
      ser11.3 $e |- ( x = 1 -> A = B ) $.
      $( The value of the first term in an infinite series. $)
      ser11 $p |- ( ( + seq1 F ) ` 1 ) = B $=
        ( c1 caddc cseq1 co cfv addex cn nnex fopabex2 seq11 wcel wceq 1nn
        fvopab4 ax-mp eqtr ) IJEKLMIEMZDJENABOCEPFQRIOSUEDTUAABICDOEHFGUBUCUD
        $.
        $( [13-Mar-2005] $) $( [8-Nov-2005] $)
    $}

    ${
      $d y B $.  $d x y C $.
      ser1p1.2 $e |- C e. V $.
      ser1p1.3 $e |- ( x = ( B + 1 ) -> A = C ) $.
      $( The value of the next term in an infinite series. $)
      ser1p1 $p |- ( B e. NN ->
               ( ( + seq1 F ) ` ( B + 1 ) ) = ( ( ( + seq1 F ) ` B ) + C ) ) $=
        ( cn wcel c1 caddc co cseq1 cfv addex nnex fopabex2 seq1p1 wceq
        peano2nn fvopab4 syl opreq2d eqtrd ) DJKZDLMNZMFONZPDUIPZUHFPZMNUJEMNDM
        FQABJCFRGSTUGUKEUJMUGUHJKUKEUADUBABUHCEJFIGHUCUDUEUF $.
        $( [14-Mar-2005] $) $( [8-Nov-2005] $)
    $}
  $}

  ${
    $d y A $.  $d x y F $.
    ser1mono.1 $e |- F : NN --> RR $.
    ser1mono.2 $e |- ( x e. NN -> 0 <_ ( F ` x ) ) $.
    $( The partial sums in an infinite series of positive terms form
       a monotonic sequence. $)
    ser1mono $p |- ( A e. NN ->
                  ( ( + seq1 F ) ` A ) <_ ( ( + seq1 F ) ` ( A + 1 ) ) ) $=
      ( vy cn wcel caddc cseq1 co cfv cc0 c1 cle cc wceq cr wf wss axresscn
      fss mp2an ser1cl1 ax0id syl wbr peano2nn cv fveq2 breq2d vtoclga wb 0re
      leadd2t mp3an1 ffvelrni ser1recl sylanc mpbid addex cvv nnex fex seq1p1
      breqtrrd eqbrtrrd ) BGHZBICJKZLZMIKZVJBNIKZVILZOVHVJPHVKVJQBCGRCSZRPTGPCS
      DUAGRPCUBUCUDVJUEUFVHVKVJVLCLZIKZVMOVHMVOOUGZVKVPOUGZVHVLGHZVQBUHZMFUIZCL
      ZOUGZVQFVLGWAVLQWBVOMOWAVLCUJUKMAUIZCLZOUGWCAWAGWDWAQWEWBMOWDWACUJUKEULUL
      UFVORHZVJRHZVQVRUMZVHMRHWFWGWHUNMVOVJUOUPVHVSWFVTGRVLCDUQUFBCDURUSUTBICVA
      VNGVBHCVBHDVCGRVBCVDUCVEVFVG $.
      $( [7-Apr-2005] $) $( [17-Mar-2005] $)
  $}

  ${
    $d k j m F $.  $d k j m G $.  $d k j m H $.  $d j k m N $.
    ser1add2.1 $e |- F : NN --> CC $.
    ser1add2.2 $e |- G : NN --> CC $.
    ser1add2.3 $e |- H e. V $.
    ser1add2.4 $e |- ( ( k e. NN /\ N e. NN /\ k <_ N ) ->
                   ( H ` k ) = ( ( F ` k ) + ( G ` k ) ) ) $.
    $( The sum of two infinite series. $)
    ser1add2 $p |- ( N e. NN ->
   ( ( + seq1 H ) ` N ) = ( ( ( + seq1 F ) ` N ) + ( ( + seq1 G ) ` N ) ) ) $=
      ( vj vm cn wcel caddc cseq1 co cfv wceq cle wbr cr nnret leidt syl cv wa
      wi c1 breq1 anbi2d fveq2 opreq12d eqeq12d imbi12d 1nn 3expib vtoclga
      ax-mp addex seq11 cc wf cvv nnex fex mp2an opreq12i 3eqtr4g lep1t adantr
      letrt pm3.26 peano2re pm3.27 syl3anc mpand syl2an imp adantllr peano2nn
      imbi2d 3exp imp31 seq1p1 ad2antrr add4t ser1cl1 ffvelrni syl2anc eqtrd
      adantlr 3eqtr4d ex imim2d com23 an4s anassrs mpdan exp41 pm2.43d imp3a
      nnind mpan2d pm2.43i ) ELMZENDOPZQZENBOPZQZENCOPZQZNPZRZXEXEEESTZXMXEEUAM
      ZXNEUBZEUCUDXEJUEZESTZUFZXQXFQZXQXHQZXQXJQZNPZRZUGXEUHESTZUFZUHXFQZUHXHQZ
      UHXJQZNPZRZUGXEKUEZESTZUFZYLXFQZYLXHQZYLXJQZNPZRZUGZXEYLUHNPZESTZUFZUUAXF
      QZUUAXHQZUUAXJQZNPZRZUGXEXNUFZXMUGJKEXQUHRZXSYFYDYKUUJXRYEXEXQUHESUIUJUUJ
      XTYGYCYJXQUHXFUKUUJYAYHYBYINXQUHXHUKXQUHXJUKULUMUNXQYLRZXSYNYDYSUUKXRYMXE
      XQYLESUIUJUUKXTYOYCYRXQYLXFUKUUKYAYPYBYQNXQYLXHUKXQYLXJUKULUMUNXQUUARZXSU
      UCYDUUHUULXRUUBXEXQUUAESUIUJUULXTUUDYCUUGXQUUAXFUKUULYAUUEYBUUFNXQUUAXHUK
      XQUUAXJUKULUMUNXQERZXSUUIYDXMUUMXRXNXEXQEESUIUJUUMXTXGYCXLXQEXFUKUUMYAXIY
      BXKNXQEXHUKXQEXJUKULUMUNYFUHDQZUHBQZUHCQZNPZYGYJUHLMYFUUNUUQRZUGZUOXEAUEZ
      ESTZUFZUUTDQZUUTBQZUUTCQZNPZRZUGUUSAUHLUUTUHRZUVBYFUVGUURUVHUVAYEXEUUTUHE
      SUIUJUVHUVCUUNUVFUUQUUTUHDUKUVHUVDUUOUVEUUPNUUTUHBUKUUTUHCUKULUMUNUUTLMZX
      EUVAUVGIUPUQURNDUSHUTYHUUOYIUUPNNBUSLVABVBLVCMZBVCMFVDLVAVCBVEVFZUTNCUSLV
      ACVBUVJCVCMGVDLVAVCCVEVFZUTVGVHYLLMZUUCYTUUHUVMXEUUBYTUUHUGZUVMXEUUBUVNUG
      UVMXEXEUUBUVNUVMXEUFZXEUFZUUBUFYMUVNUVMXEUUBYMXEUVOUUBYMYLUAMZXOUUBYMUGUV
      MXEUVQXOUFZYLUUASTZUUBYMUVQUVSXOYLVIVJUVQUUAUAMZXOUVSUUBUFYMUGUVRYLUUAEVK
      UVQXOVLUVQUVTXOYLVMVJUVQXOVNVOVPYLUBXPVQVRVSUVPUUBYMUVNUVOUUBXEYMUVNUVOUU
      BUFZYNUVNUWAYTYNUUHUWAYSUUHYNUWAYSUUHUWAYSUFZYOUUADQZNPZYRUUABQZUUACQZNPZ
      NPZUUDUUGUWBYOYRUWCUWGNUWAYSVNUWAUWCUWGRZYSUVMXEUUBUWIUVMUUALMZXEUUBUWIUG
      ZUGZYLVTZXEUVAUVGUGZUGUWLAUUALUUTUUARZUWNUWKXEUWOUVAUUBUVGUWIUUTUUAESUIUW
      OUVCUWCUVFUWGUUTUUADUKUWOUVDUWEUVEUWFNUUTUUABUKUUTUUACUKULUMUNWAUVIXEUVAU
      VGIWBUQUDWCVJULUVMUUBYSUUDUWDRZXEUVMUWPUUBYSYLNDUSHWDWEVSUVOYSUUGUWHRZUUB
      UVMUWQXEYSUVMUUGYPUWENPZYQUWFNPZNPZUWHUVMUUEUWRUUFUWSNYLNBUSUVKWDYLNCUSUV
      LWDULYPVAMUWEVAMZYQVAMUWFVAMZUWTUWHRUVMYPUWEYQUWFWFYLBFWGUVMUWJUXAUWMLVAU
      UABFWHUDYLCGWGUVMUWJUXBUWMLVAUUACGWHUDWIWJWEWKWLWMWNWOVRWPWQWRWSWTXAWOXBX
      CXD $.
      $( [31-Jul-2005] $) $( [18-Jul-2005] $)
  $}

  ${
    $d k j m F $.  $d k j m G $.  $d k j m H $.  $d j k m N $.
    ser1add.1 $e |- F : NN --> CC $.
    ser1add.2 $e |- G : NN --> CC $.
    ser1add.3 $e |- H e. V $.
    ser1add.4 $e |- ( ( k e. NN /\ k <_ N ) ->
                   ( H ` k ) = ( ( F ` k ) + ( G ` k ) ) ) $.
    $( The sum of two infinite series. $)
    ser1add $p |- ( N e. NN ->
   ( ( + seq1 H ) ` N ) = ( ( ( + seq1 F ) ` N ) + ( ( + seq1 G ) ` N ) ) ) $=
      ( vj vm cn wcel caddc cseq1 co cfv wceq cle wbr cr nnret leidt syl cv wa
      wi c1 breq1 anbi2d fveq2 opreq12d eqeq12d imbi12d 1nn ex vtoclga ax-mp
      addex seq11 cc wf cvv nnex fex mp2an opreq12i 3eqtr4g adantl lep1t
      adantr letrt pm3.26 peano2re pm3.27 syl3anc mpand syl2an imp peano2nn
      sylan seq1p1 ad2antrr add4t ser1cl1 ffvelrni syl2anc eqtrd 3eqtr4d
      imim2d com23 an4s anassrs mpdan exp31 imp3a nnind mpan2d pm2.43i ) ELMZEN
      DOPZQZENBOPZQZENCOPZQZNPZRZWTWTEESTZXHWTEUAMZXIEUBZEUCUDWTJUEZESTZUFZXLXA
      QZXLXCQZXLXEQZNPZRZUGWTUHESTZUFZUHXAQZUHXCQZUHXEQZNPZRZUGWTKUEZESTZUFZYGX
      AQZYGXCQZYGXEQZNPZRZUGZWTYGUHNPZESTZUFZYPXAQZYPXCQZYPXEQZNPZRZUGWTXIUFZXH
      UGJKEXLUHRZXNYAXSYFUUEXMXTWTXLUHESUIUJUUEXOYBXRYEXLUHXAUKUUEXPYCXQYDNXLUH
      XCUKXLUHXEUKULUMUNXLYGRZXNYIXSYNUUFXMYHWTXLYGESUIUJUUFXOYJXRYMXLYGXAUKUUF
      XPYKXQYLNXLYGXCUKXLYGXEUKULUMUNXLYPRZXNYRXSUUCUUGXMYQWTXLYPESUIUJUUGXOYSX
      RUUBXLYPXAUKUUGXPYTXQUUANXLYPXCUKXLYPXEUKULUMUNXLERZXNUUDXSXHUUHXMXIWTXLE
      ESUIUJUUHXOXBXRXGXLEXAUKUUHXPXDXQXFNXLEXCUKXLEXEUKULUMUNXTYFWTXTUHDQZUHBQ
      ZUHCQZNPZYBYEUHLMXTUUIUULRZUGZUOAUEZESTZUUODQZUUOBQZUUOCQZNPZRZUGZUUNAUHL
      UUOUHRZUUPXTUVAUUMUUOUHESUIUVCUUQUUIUUTUULUUOUHDUKUVCUURUUJUUSUUKNUUOUHBU
      KUUOUHCUKULUMUNUUOLMUUPUVAIUPZUQURNDUSHUTYCUUJYDUUKNNBUSLVABVBLVCMZBVCMFV
      DLVAVCBVEVFZUTNCUSLVACVBUVECVCMGVDLVAVCCVEVFZUTVGVHVIYGLMZYRYOUUCUVHWTYQY
      OUUCUGZUVHWTYQUVIUVHWTUFZYQUFYHUVIUVJYQYHYGUAMZXJYQYHUGUVHWTUVKXJUFZYGYPS
      TZYQYHUVKUVMXJYGVJVKUVKYPUAMZXJUVMYQUFYHUGUVLYGYPEVLUVKXJVMUVKUVNXJYGVNVK
      UVKXJVOVPVQYGUBXKVRVSUVJYQYHUVIUVHYQWTYHUVIUVHYQUFZYIUVIUVOYOYIUUCUVOYNUU
      CYIUVOYNUUCUVOYNUFZYJYPDQZNPZYMYPBQZYPCQZNPZNPZYSUUBUVPYJYMUVQUWANUVOYNVO
      UVOUVQUWARZYNYPLMZYQUWCUVHUWDYQUWCUVBYQUWCUGAYPLUUOYPRZUUPYQUVAUWCUUOYPES
      UIUWEUUQUVQUUTUWAUUOYPDUKUWEUURUVSUUSUVTNUUOYPBUKUUOYPCUKULUMUNUVDUQVSYGV
      TZWAVKULUVHYSUVRRYQYNYGNDUSHWBWCUVHUUBUWBRYQYNUVHUUBYKUVSNPZYLUVTNPZNPZUW
      BUVHYTUWGUUAUWHNYGNBUSUVFWBYGNCUSUVGWBULYKVAMUVSVAMZYLVAMUVTVAMZUWIUWBRUV
      HYKUVSYLUVTWDYGBFWEUVHUWDUWJUWFLVAYPBFWFUDYGCGWEUVHUWDUWKUWFLVAYPCGWFUDWG
      WHWCWIUPWJWKVSWLWMWNWOWPWKWQWRWS $.
      $( [24-Jul-2005] $) $( [18-Jul-2005] $)
  $}

