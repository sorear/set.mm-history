$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The exponential, sine, and cosine functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c exp $. $( Exponential function. $)
  $c _e $. $( Letter e (Euler's constant = 2.71828...) $)
  $c sin $. $( Sine function. $)
  $c cos $. $( Cosine function. $)
  $c tan $. $( Tangent function. $)
  $c pi $. $( Pi = 3.14159... $)

  $( Extend class notation to include the exponential function. $)
  ce $a class exp $.

  $( Extend class notation to include Euler's constant = 2.7182818.... $)
  ceu $a class _e $.

  $( Extend class notation to include the sine function. $)
  csin $a class sin $.

  $( Extend class notation to include the cosine function. $)
  ccos $a class cos $.

  $( Extend class notation to include the tangent function. $)
  ctan $a class tan $.

  $( Extend class notation to include pi = 3.14159.... $)
  cpi $a class pi $.

  ${
    $d j k x y w $.
    $( Define the exponential function. $)
    df-ef $a |- exp = ( x e. CC |->
                        sum_ k e. NN0 ( ( x ^ k ) / ( ! ` k ) ) ) $.

    $( Define Euler's constant 2.71828.... $)
    df-e $a |- _e = ( exp ` 1 ) $.

    $( Define the sine function. $)
    df-sin $a |- sin = ( x e. CC |-> ( ( ( exp ` ( _i x. x ) ) -
                         ( exp ` ( -u _i x. x ) ) ) / ( 2 x. _i ) ) ) $.

$(
    @( Define the cosine function. @)
    df-cos @a |- cos =
        { <. x , y >. | ( x e. CC /\ y = ( sin ` ( x + ( pi / 2 ) ) ) ) } @.
$)

    $( Define the cosine function. $)
    df-cos $a |- cos = ( x e. CC |-> ( ( ( exp ` ( _i x. x ) ) +
                         ( exp ` ( -u _i x. x ) ) ) / 2 ) ) $.

    $( Define the tangent function.  We define it this way for ~ cmpt , which
       requires the form ` ( x e. A |-> B ) ` . $)
    df-tan $a |- tan = ( x e. ( `' cos " ( CC \ { 0 } ) ) |->
                       ( ( sin ` x ) / ( cos ` x ) ) ) $.

    $( Define pi = 3.14159..., which is the smallest positive number whose sine
       is zero.  Definition of pi in [Gleason] p. 311.  (We use the inverse of
       of less-than, " ` ``' < ` ", to turn supremum into infimum; currently we
       don't have infimum defined separately.) $)
    df-pi $a |- pi = sup ( ( RR+ i^i ( `' sin " { 0 } ) ) , RR , `' < ) $.
  $}

  $( Closure of a term in the series expansion of the exponential function.
     (Contributed by Paul Chapman, 11-Sep-2007.) $)
  eftcl $p |- ( ( A e. CC /\ K e. NN0 ) ->
                 ( ( A ^ K ) / ( ! ` K ) ) e. CC ) $=
    ( cc wcel cn0 wa cexp co cfa cfv cc0 cdiv expcl cn faccl nncn adantl facne0
    wne syl divcl syl3anc ) ACDZBEDZFABGHZCDBIJZCDZUFKSZUEUFLHCDABMUDUGUCUDUFND
    UGBOUFPTQUDUHUCBRQUEUFUAUB $.
    $( [11-Sep-2007] $)

  $( The terms of the series expansion of the exponential function of a real
     number are real.  (Contributed by Paul Chapman, 15-Jan-2008.) $)
  reeftcl $p |- ( ( A e. RR /\ K e. NN0 ) ->
                   ( ( A ^ K ) / ( ! ` K ) ) e. RR ) $=
    ( cr wcel cn0 wa cexp co cfa cfv cdiv reexpcl faccl adantl nndivre syl2anc
    cn ) ACDZBEDZFABGHZCDBIJZQDZTUAKHCDABLSUBRBMNTUAOP $.
    $( [15-Jan-2008] $)

  $( The absolute value of a term in the series expansion of the exponential
     function.  (Contributed by Paul Chapman, 23-Nov-2007.) $)
  eftabs $p |- ( ( A e. CC /\ K e. NN0 ) ->
                 ( abs ` ( ( A ^ K ) / ( ! ` K ) ) ) =
                 ( ( ( abs ` A ) ^ K ) / ( ! ` K ) ) ) $=
    ( cc wcel cn0 wa cexp co cfa cfv cdiv cabs cc0 wne wceq expcl cn adantl syl
    faccl nncn facne0 absdiv syl3anc absexp cr cle nnre nnnn0 nn0ge0 3syl absid
    wbr syl2anc opreq12d eqtrd ) ACDZBEDZFZABGHZBIJZKHLJZUTLJZVALJZKHZALJBGHZVA
    KHUSUTCDVACDZVAMNZVBVEOABPUSVAQDZVGURVIUQBTRZVAUASURVHUQBUBRUTVAUCUDUSVCVFV
    DVAKABUEUSVAUFDZMVAUGUMZVDVAOUSVIVKVJVAUHSUSVIVAEDVLVJVAUIVAUJUKVAULUNUOUP
    $.
    $( [23-Nov-2007] $)

  ${
    $d k n A $.  $d k F $.  $d n N $.
    eftval.1 $e |- F = ( n e. NN0 |-> ( ( A ^ n ) / ( ! ` n ) ) ) $.
    $( The value of a term in the series expansion of the exponential
       function.  (Contributed by Paul Chapman, 21-Aug-2007.) $)
    eftval $p |- ( N e. NN0 -> ( F ` N ) = ( ( A ^ N ) / ( ! ` N ) ) ) $=
      ( cv cexp co cfa cfv cdiv cn0 wceq opreq2 fveq2 opreq12d oprex fvmpt ) BD
      ABFZGHZSIJZKHADGHZDIJZKHLCSDMTUBUAUCKSDAGNSDIOPEUBUCKQR $.
      $( [28-Apr-2014] $) $( [21-Aug-2007] $)

    $( Lemma for ~ efcl .  The series that defines the exponential function
       converges, in the case where its argument is nonzero.  The ratio test
       ~ cvgrat is used to show convergence.  (The proof was shortened by Mario
       Carneiro, 28-Apr-2014.) $)
    efcllem $p |- ( A e. CC -> seq 0 ( + , F ) e. dom ~~> ) $=
      ( cc wcel c2 cdiv co cc0 cabs cfv cmul cr clt wbr cle wa wceq syl cfl cuz
      vk c1 cn0 nn0uz eqid cn 2nn nnrecre ax-mp a1i halflt1 2re remulcl sylancr
      absge0 0re 2pos ltleii mulge0 mpanl12 syl2anc flge0nn0 cv cexp cfa eftval
      abscl adantl eftcl eqeltrd caddc adantr eluznn0 sylan nn0p1nn faccl expcl
      nndivre reexpcl syldan absexp breqtrd nngt0 divge0 syl22anc jca peano2nn0
      nnre nn0re fllelt simprd eluzp1p1 eluzle ltletrd recnd 2cn mulcom sylancl
      mulid2 3brtr4d wb pm3.2i 1re lt2mul2div mpbid wi ltle mpd syl31anc fveq2d
      lemul2a wne nncn nnne0 absdiv syl3anc expp1 eqtrd nnnn0 nn0ge0 3syl absid
      facp1 opreq12d divmuldiv eqtr4d 3eqtrd recni eftabs opreq1d cvgrat ) AEFZ
      UDGHIZUCCJGAKLZMIZUALZYRUBLZUEUFYSUGYONFZYNGUHFYTUIGUJUKZULYOUDOPYNUMULYN
      YQNFZJYQQPZYRUEFZYNGNFZYPNFZUUBUNAVIZGYPUOUPZYNUUFJYPQPZUUCUUGAUQUUEJGQPU
      UFUUIRUUCUNJGURUNUSUTGYPVAVBVCYQVDVCZYNUCVEZUEFZRUUKCLZAUUKVFIZUUKVGLZHIZ
      EUULUUMUUPSZYNABCUUKDVHZVJAUUKVKVLZYNUUKYSFZRZYPUUKVFIZUUOHIZYPUUKUDVMIZH
      IZMIZUVCYOMIZUVDCLZKLZYOUUMKLZMIZQUVAUVENFZYTUVCNFZJUVCQPZRUVEYOQPZUVFUVG
      QPUVAUUFUVDUHFZUVLYNUUFUUTUUGVNZUVAUULUVPYNUUDUUTUULUUJUUKYRVOVPZUUKVQTZY
      PUVDVTVCZYTUVAUUAULZUVAUVMUVNUVAUVBNFZUUOUHFZUVMUVAUUFUULUWBUVQUVRYPUUKWA
      VCZUVAUULUWCUVRUUKVRTZUVBUUOVTVCUVAUWBJUVBQPUUONFZJUUOOPZUVNUWDUVAJUUNKLZ
      UVBQUVAUUNEFZJUWHQPYNUUTUULUWIUVRAUUKVSWBUUNUQTYNUUTUULUWHUVBSUVRAUUKWCWB
      WDUVAUWCUWFUWEUUOWJTUVAUWCUWGUWEUUOWETUVBUUOWFWGWHUVAUVEYOOPZUVOUVAYPGMIZ
      UDUVDMIZOPZUWJUVAYQUVDUWKUWLOUVAYQYRUDVMIZUVDYNUUBUUTUUHVNZYNUWNNFZUUTYNU
      WNUEFZUWPYNUUDUWQUUJYRWITUWNWKTVNUVAUVPUVDNFZUVSUVDWJTZUVAUUBYQUWNOPZUWOU
      UBYRYQQPUWTYQWLWMTUVAUVDUWNUBLFZUWNUVDQPUUTUXAYNYRUUKWNVJUWNUVDWOTWPUVAYP
      EFZGEFUWKYQSUVAYPUVQWQZWRYPGWSWTUVAUVDEFZUWLUVDSUVAUVDUWSWQZUVDXATXBUVAUU
      FUUEJGOPZRZUDNFZUWRJUVDOPZRUWMUWJXCUVQUXGUVAUUEUXFUNUSXDULUXHUVAXEULUVAUW
      RUXIUWSUVAUVPUXIUVSUVDWETWHYPGUDUVDXFWGXGUVAUVLYTUWJUVOXHUVTUWAUVEYOXIVCX
      JUVEYOUVCXMXKUVAUVIAUVDVFIZUVDVGLZHIZKLZUXJKLZUXKKLZHIZUVFUVAUVHUXLKUVAUV
      DUEFZUVHUXLSUVAUULUXQUVRUUKWITZABCUVDDVHTXLUVAUXJEFZUXKEFZUXKJXNZUXMUXPSY
      NUUTUXQUXSUXRAUVDVSWBUVAUXKUHFZUXTUVAUXQUYBUXRUVDVRTZUXKXOTUVAUYBUYAUYCUX
      KXPTUXJUXKXQXRUVAUXPUVBYPMIZUUOUVDMIZHIZUVFUVAUXNUYDUXOUYEHUVAUXNYPUVDVFI
      ZUYDYNUUTUXQUXNUYGSUXRAUVDWCWBUVAUXBUULUYGUYDSUXCUVRYPUUKXSVCXTUVAUXOUXKU
      YEUVAUXKNFZJUXKQPZUXOUXKSUVAUYBUYHUYCUXKWJTUVAUYBUXKUEFUYIUYCUXKYAUXKYBYC
      UXKYDVCUVAUULUXKUYESUVRUUKYETXTYFUVAUVBEFUXBUUOEFZUUOJXNZRUXDUVDJXNZRUVFU
      YFSUVAUVBUWDWQUXCUVAUYJUYKUVAUWCUYJUWEUUOXOTUVAUWCUYKUWEUUOXPTWHUVAUXDUYL
      UXEUVAUVPUYLUVSUVDXPTWHUVBYPUUOUVDYGWGYHYIUVAUVKUVJYOMIZUVGUVAYOEFUVJEFUV
      KUYMSYOUUAYJUVAUVJUVAUUMEFZUVJNFYNUUTUULUYNUVRUUSWBUUMVITWQYOUVJWSUPUVAUV
      JUVCYOMUVAUVJUUPKLZUVCUVAUUMUUPKUVAUULUUQUVRUURTXLYNUUTUULUYOUVCSUVRAUUKY
      KWBXTYLXTXBYM $.
      $( [28-Apr-2014] $) $( [26-Apr-2005] $)

    $( The series defining the exponential function converges in the (trivial)
       case of a zero argument.  (Revised by Mario Carneiro, 28-Apr-2014.) $)
    ef0lem $p |- ( A = 0 -> seq 0 ( + , F ) ~~> 1 ) $=
      ( vk cc0 wceq caddc cfv c1 wcel wa cn0 cexp co cdiv syl cc ax-mp a1i cseq
      cli csn cv cuz cn cif simpr nn0uz syl6eleqr elnn0 sylib cfa adantl eftval
      wo nnnn0 opreq1 0exp sylan9eq opreq1d faccl wne nncn nnne0 syl2anc 3eqtrd
      div0 wn elsn necon3bbii sylibr iffalse eqtr4d fveq2 exp0 syl6eq 0nn0 fac0
      0cn opreq2i ax-1cn ax-1ne0 dividi eqtr2i 3eqtr4g sylan9eqr iftrue eleqtri
      jaodan syldan cfz wss cz 0z fzsn eqimss2i fsumcvg2 seq1i breqtrd ) AFGZHC
      FUAZFXBIJUBXAFUCZJECFFXAEUDZFUEIZKZXDUFKZXDFGZUPZXDCIZXDXCKZJFUGZGZXAXFLZ
      XDMKZXIXNXDXEMXAXFUHUIUJXDUKULXAXGXMXHXAXGLZXJFXLXPXJAXDNOZXDUMIZPOZFXRPO
      ZFXPXOXJXSGXGXOXAXDUQUNZABCXDDUOQXPXQFXRPXAXGXQFXDNOFAFXDNURXDUSUTVAXPXRU
      FKZXTFGZXPXOYBYAXDVBQYBXRRKXRFVCYCXRVDXRVEXRVHVFQVGXPXKVIZXLFGXGYDXAXGXDF
      VCYDXDVEXKXDFEFVJZVKVLUNXKJFVMQVNXAXHLZXJJXLXHXAXJFCIZJXDFCVOXAAFNOZFUMIZ
      POZJYIPOZYGJXAYHJYIPXAYHFFNOZJAFFNURFRKYLJGVTFVPSVQVAFMKYGYJGVRABCFDUOSYK
      JJPOJYIJJPVSWAJWBWCWDWEWFZWGYFXKXLJGYFXHXKXAXHUHYEVLXKJFWHQVNWJWKFXEKXAFM
      XEVRUIWITJRKXAXKLWBTXCFFWLOZWMXAYNXCFWNKYNXCGWOFWPSWQTWRXAJHCFWOYMWSWT $.
      $( [28-Apr-2014] $) $( [7-Jun-2006] $)
  $}

  ${
    $d x k A $.
    $( Value of the exponential function. $)
    efval $p |- ( A e. CC ->
                 ( exp ` A ) = sum_ k e. NN0 ( ( A ^ k ) / ( ! ` k ) ) ) $=
      ( vx cn0 cv cexp co cfa cfv cdiv csu cc ce opreq1 opreq1d sumeq2sdv df-ef
      wceq sumex fvmpt ) CADCEZBEZFGZUBHIZJGZBKDAUBFGZUDJGZBKLMUAARZDUEUGBUHUCU
      FUDJUAAUBFNOPCBQDUGBST $.
      $( [10-Nov-2013] $) $( [8-Jan-2006] $)
  $}

  $( Value of Euler's constant ` _e ` = 2.71828...  (Contributed by Steve
     Rodriguez, 5-Mar-2006.) $)
  eval $p |- _e = sum_ k e. NN0 ( 1 / ( ! ` k ) ) $=
    ( ceu c1 ce cfv cn0 cv cexp co cfa cdiv csu df-e cc wcel ax-1cn efval ax-mp
    wceq cz nn0z 1exp syl opreq1d sumeq2i 3eqtri ) BCDEZFCAGZHIZUHJEZKIZALZFCUJ
    KIZALMCNOUGULSPCAQRFUKUMAUHFOZUICUJKUNUHTOUICSUHUAUHUBUCUDUEUF $.
    $( [29-Jan-2007] $) $( [5-Mar-2006] $)

  ${
    $d k n x $.
    $( Domain and codomain of the exponential function.  (Contributed by Paul
       Chapman, 22-Oct-2007.)  (The proof was shortened by Mario Carneiro,
       28-Apr-2014.) $)
    eff $p |- exp : CC --> CC $=
      ( vx vk vn cn0 cv cexp co cfa cfv cdiv csu cc wcel wral ce cmpt cc0 nn0uz
      wf cz 0z a1i wceq eqid eftval adantl eftcl efcllem isumcl rgen df-ef fmpt
      mpbi ) DAEZBEZFGUOHIJGZBKZLMZALNLLOSURALUNLMZUPBCDUNCEZFGUTHIJGPZQDRQTMUS
      UAUBUODMUOVAIUPUCUSUNCVAUOVAUDZUEUFUNUOUGUNCVAVBUHUIUJALLUQOABUKULUM $.
      $( [28-Apr-2014] $) $( [22-Oct-2007] $)

    $( Closure law for the exponential function. $)
    efcl $p |- ( A e. CC -> ( exp ` A ) e. CC ) $=
      ( cc ce eff ffvelrni ) BBACDE $.
      $( [10-Nov-2013] $) $( [8-Jan-2006] $)
  $}

  ${
    $d k n A $.  $d k F $.
    efcvg.1 $e |- F = ( n e. NN0 |-> ( ( A ^ n ) / ( ! ` n ) ) ) $.
    $( Value of the exponential function.  (Contributed by Mario Carneiro,
       29-Apr-2014.) $)
    efval2 $p |- ( A e. CC -> ( exp ` A ) = sum_ k e. NN0 ( F ` k ) ) $=
      ( cc wcel ce cfv cn0 cv cexp co cfa cdiv csu efval eftval sumeq2i syl6eqr
      ) AFGAHIJABKZLMUANIOMZBPJUADIZBPABQJUCUBBACDUAERST $.
      $( [29-Apr-2014] $)

    $( The series that defines the exponential function converges to it.
       (Revised by Mario Carneiro, 28-Apr-2014.) $)
    efcvg $p |- ( A e. CC -> seq 0 ( + , F ) ~~> ( exp ` A ) ) $=
      ( vk cc wcel caddc cc0 cseq cn0 cv cexp co cfa cfv cdiv csu ce cli cz a1i
      nn0uz 0z wceq eftval adantl eftcl efcllem isumclim2 efval breqtrrd ) AFGZ
      HCIJKAELZMNUNOPQNZERASPTUMUOECIKUCIUAGUMUDUBUNKGUNCPUOUEUMABCUNDUFUGAUNUH
      ABCDUIUJAEUKUL $.
      $( [28-Apr-2014] $) $( [9-Jan-2006] $)
  $}

  ${
    $d j k n A $.  $d j F $.
    efcvgfsum.1 $e |- F = ( n e. NN0 |->
                        sum_ k e. ( 0 ... n ) ( ( A ^ k ) / ( ! ` k ) ) ) $.
    $( Exponential function convergence in terms of a sequence of partial
       finite sums.  (Revised by Mario Carneiro, 28-Apr-2014.) $)
    efcvgfsum $p |- ( A e. CC -> F ~~> ( exp ` A ) ) $=
      ( vj cc wcel caddc cn0 cv cexp co cfa cfv cdiv cc0 wceq cfz wfn cmpt cseq
      ce cli wral csu opreq2 sumeq1d sumex fvmpt adantl elfznn0 eqid eftval syl
      cuz simpr nn0uz syl6eleq simpll eftcl syl2anc fsumser eqtrd ralrimiva cvv
      wa wb fnmpt a1i cz 0z seqfn ax-mp fneq2i mpbir eqfnfv2 mp2an sylibr efcvg
      mprg eqbrtrd ) AGHZDICJACKZLMWDNOPMUAZQUBZAUCOUDWCFKZDOZWGWFOZRZFJUEZDWFR
      ZWCWJFJWCWGJHZVGZWHQWGSMZABKZLMWPNOPMZBUFZWIWMWHWRRWCCWGQWDSMZWQBUFZWRJDW
      DWGRWSWOWQBWDWGQSUGUHEWOWQBUIUJUKWNWQBWEQWGWNWPWOHZVGZWPJHZWPWEOWQRXAXCWN
      WPWGULUKZACWEWPWEUMZUNUOWNWGJQUPOZWCWMUQURUSXBWCXCWQGHWCWMXAUTXDAWPVAVBVC
      VDVEDJTZWFJTZWLWKVHWTVFHZXGCJCJWTDVFEVIXIWDJHWSWQBUIVJWAXHWFXFTZQVKHXJVLI
      WEQVMVNJXFWFURVOVPFJDWFVQVRVSACWEXEVTWB $.
      $( [28-Apr-2014] $) $( [10-Jan-2006] $)
  $}

  ${
    $d k n A $.
    $( The exponential function is real if its argument is real.  (Revised by
       Mario Carneiro, 28-Apr-2014.) $)
    reefcl $p |- ( A e. RR -> ( exp ` A ) e. RR ) $=
      ( vk vn cr wcel ce cfv cn0 cv cexp co cfa cdiv csu cc wceq recn efval syl
      cc0 cmpt nn0uz cz 0z a1i eqid eftval adantl reeftcl caddc cli cdm efcllem
      cseq isumrecl eqeltrd ) ADEZAFGZHABIZJKUSLGMKZBNZDUQAOEZURVAPAQZABRSUQUTB
      CHACIZJKVDLGMKUAZTHUBTUCEUQUDUEUSHEUSVEGUTPUQACVEUSVEUFZUGUHAUSUIUQVBUJVE
      TUNUKULEVCACVEVFUMSUOUP $.
      $( [28-Apr-2014] $) $( [27-Apr-2005] $)
  $}

  $( Euler's constant ` _e ` = 2.71828... is a real number.  (Proof revised by
     Steve Rodriguez, 6-Mar-2006.) $)
  ere $p |- _e e. RR $=
    ( ceu c1 ce cfv cr df-e wcel 1re reefcl ax-mp eqeltri ) ABCDZEFBEGLEGHBIJK
    $.
    $( [8-Mar-2006] $) $( [19-Mar-2005] $)

  ${
    $d k n $.  $d k F $.  $d k G $.
    erelem1.1 $e |- F = ( n e. NN |-> ( 2 x. ( ( 1 / 2 ) ^ n ) ) ) $.
    erelem1.2 $e |- G = ( n e. NN0 |-> ( 1 / ( ! ` n ) ) ) $.
    $( Lemma for ~ egt2lt3 .  (The proof was shortened by Mario Carneiro,
       28-Apr-2014.) $)
    ege2le3 $p |- ( 2 <_ _e /\ _e <_ 3 ) $=
      ( c2 ceu cle wbr wtru c1 caddc cc0 cfv co cn0 wcel wceq cdiv syl vk nn0uz
      c3 cseq 0nn0 ax-1cn addid2i eqcomi 0z a1i cv cfa fveq2 fac0 opreq2d div1i
      syl6eq cz 1z elexi fvmpt seq1i 1nn0 fac1 seqp1i df-2 syl6eqr ce cmpt cexp
      cli cc nn0z 1exp opreq1d mpteq2ia eqtr4i efcvg df-e syl6breqr wa cr simpr
      oprex cn faccl nnrecre eqeltrd clt adantl nnre 1re 0re lt01 ltleii divge0
      nngt0 mpanl12 syl2anc breqtrrd climserle eqbrtrrd trud cmin cvv ere recnd
      nnuz clim2ser seqeq1 opreq2i 3brtr3g cmul 2cn opreq2 eqid reexpcl sylancr
      ax-mp 2nn cabs 1lt2 2re 2pos absid mp2an breqtrri georeclim opreq1i pncan
      eqtri syl6breq 2ne0 reccli sylan2 crp nnrp elrp wne syl3anc 3eqtri eqtr4d
      nnnn0 isermulc2 mulid1i remulcl faclbnd2 wb nnexpcl rpdivcl sylancl lerec
      syl2anb mpbid nnne0 divrec recdiv mpanr12 exprec 3eqtr4rd 3brtr4d isercmp
      exp0 nncn lesubaddi mpbi df-3 pm3.2i ) FGHIZGUCHIUVIJKLCMUDZNZFGHJUVKKKLO
      ZFJKKLCKMMPUBUEMKLOZKKUFUGZUHJKLCMUIJMPQZMCNKRUVOJUEUJZAMKAUKZULNZSOZKPCU
      VQMRZUVSKKSOZKUVTUVRKKSUVTUVRMULNKUVQMULUMUNUQUOKUFUPZUQEKURUSUTZVATVBZJK
      PQZKCNKRUWEJVCUJZAKUVSKPCUVQKRZUVSUWAKUWGUVRKKSUWGUVRKULNKUVQKULUMVDUQUOU
      WBUQEUWCVATVEVFVGJGUACMKPUBUWFJUVJKVHNZGVKJKVLQZUVJUWHVKIUWIJUFUJKACCAPUV
      SVIAPKUVQVJOZUVRSOZVIEAPUWKUVSUVQPQZUWJKUVRSUWLUVQURQUWJKRUVQVMUVQVNTVOVP
      VQVRTVSVTZJUAUKZPQZWAZUWNCNZKUWNULNZSOZWBUWPUWOUWQUWSRZJUWOWCZAUWNUVSUWSP
      CUVQUWNRZUVRUWRKSUVQUWNULUMUOEKUWRSWDVATZUWPUWRWEQZUWSWBQUWPUWOUXDUXAUWNW
      FZTZUWRWGTWHZUWPMUWSUWQHUWPUWRWBQZMUWRWIIZMUWSHIZUWPUXDUXHUWOUXDJUXEWJZUW
      RWKTUWPUXDUXIUXKUWRWQTKWBQMKHIUXHUXIWAZUXJWLMKWMWLWNWOKUWRWPWRWSUXCWTXAXB
      XCGFKLOZUCHGKXDOZFHIZGUXMHIUXOJUXNFUACBKXEWEXHKURQJUSUJZUXNXEQJGKXDWDUJJL
      CUVMUDZGMUVJNZXDOLCKUDZUXNVKJGUACMMWBPUBUVPGWBQJXFUJUWPUWQUXGXGUWMXIUVMKR
      ZUXQUXSRUVNLCUVMKXJXSUXRKGXDUXRKRUWDXCXKXLJLBKUDFKXMOFVKJKFUAAPKFSOZUVQVJ
      OZVIZBKURWEXHUXPUXPFVLQZJXNUJZJLUYCUVMUDZFMLUYCMUDZNZXDOZLUYCKUDZKVKJFUAU
      YCMMVLPUBUVPUYEUWPUWNUYCNZUYAUWNVJOZVLUWOUYKUYLRZJAUWNUYBUYLPUYCUVQUWNUYA
      VJXOZUYCXPZUYAUWNVJWDVAWJZUWPUYLUWPUYAWBQZUWOUYLWBQZFWEQZUYQXTFWGXSUXAUYA
      UWNXQXRZXGWHZJUYGFFKXDOZSOZFVKJFUAUYCUYEKFYANZWIIJKFVUDWIYBFWBQZMFHIVUDFR
      YCMFWMYCYDWOFYEYFYGUJUYPYHVUCFKSOFVUBKFSVUBUVLKXDOZKFUVLKXDVFYIUWIUWIVUFK
      RUFUFKKYJYFYKZXKFXNUPYKYLXIUXTUYFUYJRUVNLUYCUVMKXJXSUYIVUBKUYHKFXDUYHKRJK
      LUYCMUIMUYCNZKRJVUHUYAMVJOZKKUVOVUHVUIRUEAMUYBVUIPUYCUVQMUYAVJXOUYOUYAMVJ
      WDVAXSUYAVLQVUIKRFXNYMYNUYAUVCXSKXPUUAUJVBXCXKVUGYKXLUWNWEQZJUWOUYKVLQUWN
      UUCZVUAYOJVUJWAZUWNBNZFUYLXMOZFUYKXMOVUJVUMVUNRJAUWNFUYBXMOVUNWEBUXBUYBUY
      LFXMUYNUODFUYLXMWDVAWJZVULUYKUYLFXMVUJJUWOUYMVUKUYPYOUOUUBUUDFXNUUEYLVUJJ
      UWOUWQWBQVUKUXGYOVULVUMVUNWBVUOVUJJUWOVUNWBQZVUKUWPVUEUYRVUPYCUYTFUYLUUFX
      RYOWHVULUWSVUNUWQVUMHVUJJUWOUWSVUNHIVUKUWPUWSKFUWNVJOZFSOZSOZVUNHUWPVURUW
      RHIZUWSVUSHIZUWPUWOVUTUXAUWNUUGTUWPVURYPQZUWRYPQZVUTVVAUUHZUWPVUQYPQZFYPQ
      ZVVBUWPVUQWEQZVVEUWPUYSUWOVVGXTUXAFUWNUUIXRZVUQYQTUYSVVFXTFYQXSVUQFUUJUUK
      UWPUXDVVCUXFUWRYQTVVBVURWBQMVURWIIWAUXLVVDVVCVURYRUWRYRVURUWRUULUUMWSUUNU
      WPFVUQSOZFKVUQSOZXMOZVUSVUNUWPUYDVUQVLQZVUQMYSZVVIVVKRUYDUWPXNUJZUWPVVGVV
      LVVHVUQUVDTZUWPVVGVVMVVHVUQUUOTZFVUQUUPYTUWPVVLVVMVUSVVIRZVVOVVPVVLVVMWAU
      YDFMYSZVVQXNYMVUQFUUQUURWSUWPUYLVVJFXMUWPUYDVVRUWNURQZUYLVVJRVVNVVRUWPYMU
      JUWOVVSJUWNVMWJFUWNUUSYTUOUUTWTYOVUJJUWOUWTVUKUXCYOVUOUVAUVBXCGKFXFWLYCUV
      EUVFUVGYGUVH $.
      $( [28-Apr-2014] $) $( [20-Mar-2005] $)
  $}

  $( Value of the exponential function at 0.  Equation 2 of [Gleason] p. 308.
     (Contributed by Steve Rodriguez, 27-Jun-2006.) $)
  ef0 $p |- ( exp ` 0 ) = 1 $=
    ( vn caddc cn0 cc0 cv cexp co cfa cfv cdiv cmpt cseq ce cli c1 wceq cc eqid
    wbr ax-mp wcel 0cn efcvg ef0lem fvex cz 1z elexi climuni mp2an ) BACDAEZFGU
    KHIJGKZDLZDMIZNSZUMONSZUNOPDQUAUOUBDAULULRZUCTDDPUPDRDAULUQUDTUNOUMDMUEOUFU
    GUHUIUJ $.
    $( [28-Apr-2014] $) $( [27-Jun-2006] $)

  ${
    $d j k n A $.
    $( Exponential function of a complex conjugate.  Equation 3 of [Gleason]
       p. 308.  (Revised by Mario Carneiro, 28-Apr-2014.) $)
    efcj $p |- ( A e. CC -> ( exp ` ( * ` A ) ) = ( * ` ( exp ` A ) ) ) $=
      ( vn vj vk cc wcel caddc cn0 ccj cfv cv cexp co cdiv ce wceq nn0uz adantl
      cc0 syl cfa cmpt cseq cli wbr cjcl eqid efcvg cvv efcl seqex a1i cz 0z wf
      wa eftval eftcl eqeltrd serf ffvelrn sylan cuz simpr syl6eleq cfz elfznn0
      simpl syl2an wne expcl cn faccl nnre recnd nnne0 cjdiv syl3anc cjexp cjre
      cr opreq12d eqtr2d fveq2d 3eqtr4d sercj climcj fvex climuni syl2anc ) AEF
      ZGBHAIJZBKZLMWMUAJZNMUBZSUCZWLOJZUDUEZWPAOJZIJZUDUEWQWTPWKWLEFWRAUFWLBWOW
      OUGZUHTWKWSCGBHAWMLMWNNMUBZSUCZWPSEUIHQABXBXBUGZUHAUJWPUIFWKGWOSUKULSUMFW
      KUNULZWKHEXCUOCKZHFZXFXCJEFWKDXBSHQXEWKDKZHFZUPZXHXBJZAXHLMZXHUAJZNMZEXIX
      KXNPWKABXBXHXDUQRZAXHURUSZUTHEXFXCVAVBWKXGUPZDXBWOSXFXQXFHSVCJWKXGVDQVEXQ
      WKXIXKEFXHSXFVFMFZWKXGVHZXHXFVGZXPVIXQWKXIXHWOJZXKIJZPXRXSXTXJWLXHLMZXMNM
      ZXNIJZYAYBXJYEXLIJZXMIJZNMZYDXJXLEFXMEFXMSVJZYEYHPAXHVKXJXMXJXMVLFZXMWAFZ
      XIYJWKXHVMRZXMVNTZVOXJYJYIYLXMVPTXLXMVQVRXJYFYCYGXMNAXHVSXJYKYGXMPYMXMVTT
      WBWCXIYAYDPWKWLBWOXHXAUQRXJXKXNIXOWDWEVIWFWGWQWTWPWLOWHWSIWHWIWJ $.
      $( [28-Apr-2014] $) $( [29-Apr-2005] $)
  $}

  ${
    $d j k m n A $.  $d j k n B $.  $d j F $.  $d j k m G $.  $d j k m ph $.
    $d k H $.
    efadd.1 $e |- F = ( n e. NN0 |-> ( ( A ^ n ) / ( ! ` n ) ) ) $.
    efadd.2 $e |- G = ( n e. NN0 |-> ( ( B ^ n ) / ( ! ` n ) ) ) $.
    efadd.3 $e |- H = ( n e. NN0 |-> ( ( ( A + B ) ^ n ) / ( ! ` n ) ) ) $.
    efadd.4 $e |- ( ph -> A e. CC ) $.
    efadd.5 $e |- ( ph -> B e. CC ) $.
    $( Lemma for ~ efadd (exponential function addition law).  (Revised by
       Mario Carneiro, 29-Apr-2014.) $)
    efaddlem $p |- ( ph ->
                     ( exp ` ( A + B ) ) = ( ( exp ` A ) x. ( exp ` B ) ) ) $=
      ( co cfv cmul wceq cc wcel syl cdiv vj vk vm caddc cc0 cseq cli wbr addcl
      ce syl2anc efcvg cn0 cv cexp cfa csu cabs cmpt eftval adantl absexp sylan
      wa cn faccl cr cle nnre nnnn0 nn0ge0 absid opreq12d wne expcl nncn absdiv
      nnne0 syl3anc eqid 3eqtr4rd eftcl cfz cbc cmin adantr simpr binom opreq1d
      c1 ad2antrr fznn0sub adantll elfznn0 mulcl nnmulcl eqcomd bcval2 divdiv32
      divrec2 syl122anc divid 3eqtrd jca divmuldiv syl22anc 3eqtr4d bccl2 div23
      syl112anc nn0cn ad2antlr addid2 fveq2d nncan eqtrd eqtr4d sumeq2dv opreq2
      opreq2d cbvsumv syl6eq fzfid fsumdivc cz 0z a1i nn0z divcl fveq2 fsumrev2
      eqeltrd cdm abscl recnd efcllem mertens efval breqtrrd fvex oprex climuni
      ) AUDGUEUFZBCUDMZUJNZUGUHZUUCBUJNZCUJNZOMZUGUHUUEUUIPAUUDQRZUUFABQRZCQRZU
      UJKLBCUIUKUUDDGJULSAUUCUMBUAUNZUOMZUUMUPNZTMZUAUQZUMCUBUNZUOMUURUPNZTMZUB
      UQZOMUUIUGAUUPUUTUAUBEFGDUMBURNZDUNZUOMUVCUPNTMUSZUUMUMRZUUMENUUPPABDEUUM
      HUTVAAUVEVDZUUNURNZUUOURNZTMZUVBUUMUOMZUUOTMZUUPURNZUUMUVDNZUVFUVGUVJUVHU
      UOTAUUKUVEUVGUVJPKBUUMVBVCUVFUUOVERZUVHUUOPZUVEUVNAUUMVFZVAZUVNUUOVGRUEUU
      OVHUHZUVOUUOVIUVNUUOUMRUVRUUOVJUUOVKSUUOVLUKSVMUVFUUNQRZUUOQRZUUOUEVNZUVL
      UVIPAUUKUVEUVSKBUUMVOZVCUVFUVNUVTUVQUUOVPZSUVFUVNUWAUVQUUOVRZSUUNUUOVQVSU
      VEUVMUVKPAUVBDUVDUUMUVDVTZUTVAWAAUUKUVEUUPQRZKBUUMWBVCUURUMRZUURFNUUTPACD
      FUURIUTVAAUULUWGUUTQRLCUURWBVCAUWGVDZUURGNZUUDUURUOMZUUSTMZUEUURWCMZUURUU
      MWDMZBUURUUMWEMZUOMZCUUMUOMZOMZOMZUAUQZUUSTMZUWLUUPUWNFNZOMZUAUQZUWGUWIUW
      KPAUUDDGUURJUTVAUWHUWJUWSUUSTUWHUUKUULUWGUWJUWSPAUUKUWGKWFAUULUWGLWFAUWGW
      GBCUAUURWHVSWIUWHUWLUWRUUSTMZUAUQZUWLBUEUURUDMZUCUNZWEMZUOMZUXHUPNZTMZUUR
      UXHWEMZFNZOMZUCUQZUWTUXCUWHUXEUWLBUXFUUMWEMZUOMZUXPUPNZTMZUURUXPWEMZFNZOM
      ZUAUQUXOUWHUWLUXDUYBUAUWHUUMUWLRZVDZUXDUWOUWNUPNZTMZUUMFNZOMZUYBUYDUWMUUS
      TMZUWQOMZUYFUWPUUOTMZOMZUXDUYHUYDWJUYEUUOOMZTMZUWQOMZUWQUYMTMZUYJUYLUYDUY
      PUYOUYDUWQQRZUYMQRZUYMUEVNZUYPUYOPUYDUWOQRZUWPQRZUYQUYDUUKUWNUMRZUYTAUUKU
      WGUYCKWKZUWGUYCVUBAUMUUMUEUURWLWMZBUWNVOUKZUYDUULUVEVUAAUULUWGUYCLWKZUYCU
      VEUWHUUMUURWNVAZCUUMVOUKZUWOUWPWOUKZUYDUYMVERZUYRUYDUYEVERZUVNVUJUYDVUBVU
      KVUDUWNVFSZUYDUVEUVNVUGUVPSZUYEUUOWPUKZUYMVPSZUYDVUJUYSVUNUYMVRSZUWQUYMWT
      VSWQUYDUYIUYNUWQOUYDUYIUUSUYMTMZUUSTMZUUSUUSTMZUYMTMZUYNUYDUWMVUQUUSTUWGU
      YCUWMVUQPAUUMUURUMWRWMWIUYDUUSQRZUYRUYSVVAUUSUEVNZVURVUTPUWHVVAUYCUWHUUSV
      ERZVVAUWGVVCAUURVFVAZUUSVPSZWFZVUOVUPVVFUWHVVBUYCUWHVVCVVBVVDUUSVRSZWFZUU
      SUYMUUSWSXAUYDVUSWJUYMTUYDVVAVVBVUSWJPVVFVVHUUSXBUKWIXCWIUYDUYTVUAUYEQRZU
      YEUEVNZVDUVTUWAVDUYLUYPPVUEVUHUYDVVIVVJUYDVUKVVIVULUYEVPSZUYDVUKVVJVULUYE
      VRSZXDUYDUVTUWAUYDUVNUVTVUMUWCSZUYDUVNUWAVUMUWDSZXDUWOUWPUYEUUOXEXFXGUYDU
      WMQRZUYQVVAVVBUXDUYJPUYDUWMVERZVVOUWGUYCVVPAUUMUURUMXHWMUWMVPSZVUIVVFVVHU
      WMUWQUUSXIXJUYDUYGUYKUYFOUYDUVEUYGUYKPVUGCDFUUMIUTSXTXGUYDUXSUYFUYAUYGOUY
      DUXQUWOUXRUYETUYDUXPUWNBUOUYDUXFUURUUMWEUYDUURQRZUXFUURPUWGVVRAUYCUURXKXL
      ZUURXMSWIZXTUYDUXPUWNUPVVTXNVMUYDUXTUUMFUYDUXTUURUWNWEMZUUMUYDUXPUWNUURWE
      VVTXTUYDVVRUUMQRZVWAUUMPVVSUYDUVEVWBVUGUUMXKSUURUUMXOUKXPXNVMXQXRUWLUYBUX
      NUAUCUUMUXGPZUXSUXKUYAUXMOVWCUXQUXIUXRUXJTVWCUXPUXHBUOUUMUXGUXFWEXSZXTVWC
      UXPUXHUPVWDXNVMVWCUXTUXLFVWCUXPUXHUURWEVWDXTXNVMYAYBUWHUWLUWRUUSUAUWHUEUU
      RYCVVEUYDVVOUYQUWRQRVVQVUIUWMUWQWOUKVVGYDUWHUXBUXNUAUCUEUURUEYERUWHYFYGUW
      GUURYERAUURYHVAUYDUWFUXAQRUXBQRUYDUVSUVTUWAUWFUYDUUKUVEUVSVUCVUGUWBUKVVMV
      VNUUNUUOYIVSUYDUXACUWNUOMZUYETMZQUYDVUBUXAVWFPVUDCDFUWNIUTSUYDVWEQRZVVIVV
      JVWFQRUYDUULVUBVWGVUFVUDCUWNVOUKVVKVVLVWEUYEYIVSYLUUPUXAWOUKUUMUXHPZUUPUX
      KUXAUXMOVWHUUNUXIUUOUXJTUUMUXHBUOXSUUMUXHUPYJVMVWHUWNUXLFUUMUXHUURWEXSXNV
      MYKXGXCAUVBQRUDUVDUEUFUGYMZRAUVBAUUKUVBVGRKBYNSYOUVBDUVDUWEYPSAUULUDFUEUF
      VWIRLCDFIYPSYQAUUGUUQUUHUVAOAUUKUUGUUQPKBUAYRSAUULUUHUVAPLCUBYRSVMYSUUEUU
      IUUCUUDUJYTUUGUUHOUUAUUBUK $.
      $( [29-Apr-2014] $)
  $}

  ${
    $d n A $.  $d n B $.
    $( Sum of exponents law for exponential function.  (The proof was shortened
       by Mario Carneiro, 29-Apr-2014.) $)
    efadd $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( exp ` ( A + B ) ) = ( ( exp ` A ) x. ( exp ` B ) ) ) $=
      ( vn cc wcel wa cn0 cv cexp cfa cdiv cmpt caddc eqid simpl simpr efaddlem
      co cfv ) ADEZBDEZFABCCGACHZIRUBJSZKRLZCGBUBIRUCKRLZCGABMRUBIRUCKRLZUDNUEN
      UFNTUAOTUAPQ $.
      $( [29-Apr-2014] $) $( [10-Jan-2006] $)
  $}

  $( Cancellation of law for exponential function.  Equation 27 of [Rudin]
     p. 164. $)
  efcan $p |- ( A e. CC -> ( ( exp ` A ) x. ( exp ` -u A ) ) = 1 ) $=
    ( cc wcel cneg caddc co ce cfv cmul wceq negcl efadd mpdan cc0 negid fveq2d
    c1 ef0 syl6eq eqtr3d ) ABCZAADZEFZGHZAGHUBGHIFZQUAUBBCUDUEJAKAUBLMUAUDNGHQU
    AUCNGAOPRST $.
    $( [13-Jan-2006] $)

  $( The exponential function never vanishes.  Corollary 15-4.3 of [Gleason]
     p. 309.  (Revised by Mario Carneiro, 29-Apr-2014.) $)
  efne0 $p |- ( A e. CC -> ( exp ` A ) =/= 0 ) $=
    ( cc wcel c1 cc0 wne ce cfv ax-1ne0 wceq cneg cmul opreq1 efcan negcl mul02
    co efcl 3syl eqeq12d syl5ib necon3d mpi ) ABCZDEFAGHZEFIUDUEEDEUEEJUEAKZGHZ
    LQZEUGLQZJUDDEJUEEUGLMUDUHDUIEANUDUFBCUGBCUIEJAOUFRUGPSTUAUBUC $.
    $( [29-Apr-2014] $) $( [13-Jan-2006] $)

  $( Exponent of a negative number.  (Contributed by Mario Carneiro,
     10-May-2014.) $)
  efneg $p |- ( A e. CC -> ( exp ` -u A ) = ( 1 / ( exp ` A ) ) ) $=
    ( cc wcel ce cfv cneg cmul co cdiv c1 cc0 wne wceq negcl efcl efne0 divcan3
    syl syl3anc efcan opreq1d eqtr3d ) ABCZADEZAFZDEZGHZUDIHZUFJUDIHUCUFBCZUDBC
    UDKLUHUFMUCUEBCUIANUEORAOAPUFUDQSUCUGJUDIATUAUB $.
    $( [10-May-2014] $)

  $( The exponential function maps the complex numbers to the nonzero complex
     numbers.  (Contributed by Paul Chapman, 16-Apr-2008.) $)
  eff2 $p |- exp : CC --> ( CC \ { 0 } ) $=
    ( vx cc cc0 csn cdif ce wf wfn cfv wcel wral ffnfv eff ffn ax-mp efcl efne0
    cv wne eldifsn sylanbrc rgen mpbir2an ) BBCDEZFGFBHZARZFIZUDJZABKABUDFLBBFG
    UEMBBFNOUHABUFBJUGBJUGCSUHUFPUFQUGBCTUAUBUC $.
    $( [16-Apr-2008] $)

  $( Difference of exponents law for exponential function.  (Contributed by
     Steve Rodriguez, 25-Nov-2007.) $)
  efsub $p |- ( ( A e. CC /\ B e. CC ) ->
               ( exp ` ( A - B ) ) = ( ( exp ` A ) / ( exp ` B ) ) ) $=
    ( cc wcel wa cmin co ce cfv cdiv cmul cneg caddc wceq efcan eqcomd cc0 efcl
    c1 wne negcl syl efne0 ax-1cn divmul2 mp3an1 syl12anc mpbird opreq2d adantl
    wb efadd sylan2 negsub fveq2d 3eqtr2rd divrec syl3an 3anidm23 eqtr4d ) ACDZ
    BCDZEZABFGZHIZAHIZSBHIZJGZKGZVFVGJGZVCVIVFBLZHIZKGZAVKMGZHIZVEVBVIVMNVAVBVH
    VLVFKVBVHVLNZSVGVLKGZNZVBVQSBOPVBVLCDZVGCDZVGQTZVPVRUKZVBVKCDZVSBUAZVKRUBBR
    ZBUCZSCDVSVTWAEWBUDSVLVGUEUFUGUHUIUJVBVAWCVOVMNWDAVKULUMVCVNVDHABUNUOUPVAVB
    VJVINZVAVFCDVBVTVBWAWGARWEWFVFVGUQURUSUT $.
    $( [25-Nov-2007] $)

  ${
    $d j N $.  $d j k A $.
    $( Exponential function to an integer power.  Corollary 15-4.4 of [Gleason]
       p. 309, restricted to integers.  (Revised by Mario Carneiro,
       5-Jun-2014.) $)
    efexp $p |- ( ( A e. CC /\ N e. ZZ ) ->
                 ( exp ` ( N x. A ) ) = ( ( exp ` A ) ^ N ) ) $=
      ( cc wcel wa cmul co ce cfv cexp wceq sylan2 fveq2d cc0 c1 opreq2 eqeq12d
      caddc adantr eqtrd vj vk cz zcn mulcom cv cneg ef0 mul01 efcl syl 3eqtr4a
      exp0 cn0 opreq1 adantl nn0cn ax-1cn adddi mp3an3 mulid1 mulcl simpl efadd
      opreq2d syl2anc expp1 sylan 3eqtr4d exp31 cn wi cdiv mulneg2 efneg expneg
      nncn nnnn0 syl2an syl5ibr ex zindd imp eqtr3d ) ACDZBUCDZEZABFGZHIZBAFGZH
      IAHIZBJGZWGWHWJHWFWEBCDWHWJKBUDABUELMWEWFWIWLKZAUAUFZFGZHIZWKWNJGZKANFGZH
      IZWKNJGZKAUBUFZFGZHIZWKXAJGZKZAXAUGZFGZHIZWKXFJGZKZAXAORGZFGZHIZWKXKJGZKZ
      WMWEUAUBBWNNKZWPWSWQWTXPWOWRHWNNAFPMWNNWKJPQWNXAKZWPXCWQXDXQWOXBHWNXAAFPM
      WNXAWKJPQWNXKKZWPXMWQXNXRWOXLHWNXKAFPMWNXKWKJPQWNXFKZWPXHWQXIXSWOXGHWNXFA
      FPMWNXFWKJPQWNBKZWPWIWQWLXTWOWHHWNBAFPMWNBWKJPQWENHIOWSWTUHWEWRNHAUIMWEWK
      CDZWTOKAUJZWKUMUKULWEXAUNDZXEXOWEYCEZXEEXCWKFGZXDWKFGZXMXNXEYEYFKYDXCXDWK
      FUOUPYDXMYEKXEYDXMXBARGZHIZYEYDXLYGHYCWEXACDZXLYGKXAUQZWEYIEZXLXBAOFGZRGZ
      YGWEYIOCDXLYMKURAXAOUSUTYKYLAXBRWEYLAKYIAVASVETLMYDXBCDZWEYHYEKYCWEYIYNYJ
      AXAVBZLWEYCVCXBAVDVFTSYDXNYFKZXEWEYAYCYPYBWKXAVGVHSVIVJWEXAVKDZXEXJVLXEXJ
      WEYQEZOXCVMGZOXDVMGZKXCXDOVMPYRXHYSXIYTYRXHXBUGZHIZYSYRXGUUAHYQWEYIXGUUAK
      XAVQZAXAVNLMYRYNUUBYSKYQWEYIYNUUCYOLXBVOUKTWEYAYCXIYTKYQYBXAVRWKXAVPVSQVT
      WAWBWCWD $.
      $( [5-Jun-2014] $) $( [13-Jan-2006] $)
  $}

  $( Value of the exponential function for integers.  Special case of
     ~ efval .  Equation 30 of [Rudin] p. 164.  (Contributed by Steve
     Rodriguez, 16-Sep-2006.)  (Revised by Mario Carneiro, 5-Jun-2014.) $)
  efzval $p |- ( N e. ZZ -> ( exp ` N ) = ( _e ^ N ) ) $=
    ( cz wcel ce cfv c1 cexp co ceu cmul cc wceq zcn mulid1 fveq2d ax-1cn efexp
    syl mpan eqtr3d df-e opreq1i syl6eqr ) ABCZADEZFDEZAGHZIAGHUDAFJHZDEZUEUGUD
    UHADUDAKCUHALAMANROFKCUDUIUGLPFAQSTIUFAGUAUBUC $.
    $( [5-Jun-2014] $) $( [15-Sep-2006] $)

  $( The exponential function of a real number is greater than 0.  (Contributed
     by Paul Chapman, 21-Aug-2007.)  (Revised by Mario Carneiro,
     30-Apr-2014.) $)
  efgt0 $p |- ( A e. RR -> 0 < ( exp ` A ) ) $=
    ( cr wcel cc0 ce cfv clt wbr cle wne wa wb 0re reefcl ltlen c2 co syl wceq
    cc sylancr cdiv cexp rehalfcl sqge0 cmul cz recnd 2z efexp sylancl recn 2cn
    2ne0 divcan2 mp3an23 fveq2d eqtr3d breqtrd efne0 mpbir2and ) ABCZDAEFZGHZDV
    CIHZVCDJZVBDBCVCBCVDVEVFKLMANDVCOUAVBDAPUBQZEFZPUCQZVCIVBVHBCZDVIIHVBVGBCVJ
    AUDZVGNRVHUERVBPVGUFQZEFZVIVCVBVGTCPUGCVMVISVBVGVKUHUIVGPUJUKVBVLAEVBATCZVL
    ASZAULZVNPTCPDJVOUMUNAPUOUPRUQURUSVBVNVFVPAUTRVA $.
    $( [21-Aug-2007] $)

  $( The exponential function of a real number is a positive real. $)
  rpefcl $p |- ( A e. RR -> ( exp ` A ) e. RR+ ) $=
    ( cr wcel ce cfv cc0 clt wbr crp reefcl efgt0 elrp sylanbrc ) ABCADEZBCFNGH
    NICAJAKNLM $.
    $( [10-Nov-2013] $)

  ${
    $d j k n A $.  $d k F $.  $d j k G $.  $d j H $.  $d j k n M $.
    $d j k ph $.
    eftl.1 $e |- F = ( n e. NN0 |-> ( ( A ^ n ) / ( ! ` n ) ) ) $.
    $( The tail series of the exponential function are convergent. $)
    eftlcvg $p |- ( ( A e. CC /\ M e. NN0 ) -> seq M ( + , F ) e. dom ~~> ) $=
      ( vk cc wcel cn0 wa caddc cc0 cseq cli cdm efcllem adantr nn0uz cfv co cv
      simpr cexp cfa cdiv wceq eftval adantl eftcl adantlr eqeltrd iserex mpbid
      ) AGHZDIHZJZKCLMNOZHZKCDMUQHUNURUOABCEPQUPFCLDIRUNUOUBUPFUAZIHZJUSCSZAUSU
      CTUSUDSUETZGUTVAVBUFUPABCUSEUGUHUNUTVBGHUOAUSUIUJUKULUM $.
      $( [29-Apr-2014] $)

    $( Closure of the sum of an infinite tail of the series defining the
       exponential function.  (Contributed by Paul Chapman, 17-Jan-2008.)
       (Revised by Mario Carneiro, 30-Apr-2014.) $)
    eftlcl $p |- ( ( A e. CC /\ M e. NN0 ) ->
                    sum_ k e. ( ZZ>= ` M ) ( F ` k ) e. CC ) $=
      ( cc wcel cn0 wa cv cfv cuz eqid cz nn0z adantl eqidd cexp co cfa eluznn0
      cdiv wceq adantll eftval syl simpll eftcl syl2anc eqeltrd eftlcvg isumcl
      ) AGHZEIHZJZBKZDLZBDEEMLZUSNUOEOHUNEPQUPUQUSHZJZURRVAURAUQSTUQUALUCTZGVAU
      QIHZURVBUDUOUTVCUNUQEUBUEZACDUQFUFUGVAUNVCVBGHUNUOUTUHVDAUQUIUJUKACDEFULU
      M $.
      $( [30-Apr-2014] $) $( [17-Jan-2008] $)

    $( Closure of the sum of an infinite tail of the series defining the
       exponential function.  (Contributed by Paul Chapman, 17-Jan-2008.) $)
    reeftlcl $p |- ( ( A e. RR /\ M e. NN0 ) ->
                      sum_ k e. ( ZZ>= ` M ) ( F ` k ) e. RR ) $=
      ( cr wcel cn0 wa cv cfv cuz eqid cz nn0z adantl eqidd cexp co cfa eluznn0
      cdiv wceq adantll eftval syl simpll reeftcl syl2anc eqeltrd cc caddc cseq
      cli cdm recn eftlcvg sylan isumrecl ) AGHZEIHZJZBKZDLZBDEEMLZVFNVBEOHVAEP
      QVCVDVFHZJZVERVHVEAVDSTVDUALUCTZGVHVDIHZVEVIUDVBVGVJVAVDEUBUEZACDVDFUFUGV
      HVAVJVIGHVAVBVGUHVKAVDUIUJUKVAAULHVBUMDEUNUOUPHAUQACDEFURUSUT $.
      $( [30-Apr-2014] $) $( [17-Jan-2008] $)

    eftl.2 $e |- G = ( n e. NN0 |-> ( ( ( abs ` A ) ^ n ) / ( ! ` n ) ) ) $.
    eftl.3 $e |- H = ( n e. NN0 |->
      ( ( ( ( abs ` A ) ^ M ) / ( ! ` M ) ) x. ( ( 1 / ( M + 1 ) ) ^ n ) ) ) $.
    eftl.4 $e |- ( ph -> M e. NN ) $.
    eftl.5 $e |- ( ph -> A e. CC ) $.
    eftl.6 $e |- ( ph -> ( abs ` A ) <_ 1 ) $.
    $( An upper bound on the absolute value of the infinite tail of the series
       expansion of the exponential function on the closed unit disk.
       (Contributed by Paul Chapman, 19-Jan-2008.)  (The proof was shortened by
       Mario Carneiro, 29-Apr-2014.) $)
    eftlub $p |- ( ph -> ( abs ` sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) <_
          ( ( ( abs ` A ) ^ M ) x. ( ( M + 1 ) / ( ( ! ` M ) x. M ) ) ) ) $=
      ( co cmul wcel syl wceq cc0 vj cuz cfv cv csu cabs cexp c1 caddc cfa cdiv
      cc cr cn0 cn nnnn0 syl2anc abscl reeftlcl reexpcl peano2nn0 nn0re nnmulcl
      eftlcl faccl nndivre remulcl cvv eqid cz nnz wa eqidd sylan eftval adantl
      eluznn0 eftcl eqeltrd syldan cseq cli cdm eftlcvg isumclim2 reeftcl recnd
      sumex a1i eftabs fveq2d 3eqtr4rd iserabs cle cneg cshi nn0uz 0z nncn cmpt
      nn0cn nn0ex mptex eqeltri shftval4 syl2an nn0addcl adantr opreq2 peano2nn
      opreq2d oprex fvmpt nnrecre wbr nnre uzid uzaddcl absge0 syl32anc syl3anc
      expword2i clt wb mpbid wne nnne0 divass syl112anc divdiv1 syl122anc eqtrd
      eqcomd breqtrd letrd mpbird eqbrtrd cmin seqex breldm expge0 jca faclbnd6
      nnexpcl lemul1a syl31anc crp nnrp elrp sylib lemuldiv2 facne0 divrec nn0z
      exprec 3eqtrd ledivmul znegcl seqshft subneg mpan addid2 seqeq1d climshft
      nngt0 0cn 1nn nnleltp1 sylancr nn0ge0 breqtrrd georeclim eqtr4d isermulc2
      nnge1 absid ax-1cn pncan sylancl div23 mulcom 3eqtr2d fveq2 eleq2d biimpa
      isumcmp isumshft sumeq1d eqtr3d isumclim 3brtr3d ) AHUBUCZCUDZEUCZCUEZUFU
      CZUWLUWMFUCZCUEZBUFUCZHUGOZHUHUIOZHUJUCZHPOZUKOZPOZAUWOULQZUWPUMQABULQZHU
      NQZUXFMAHUOQZUXHLHUPRZBCDEHIVDUQUWOURRAUWSUMQZUXHUWRUMQAUXGUXKMBURRZUXJUW
      SCDFHJUSUQAUWTUMQZUXDUMQZUXEUMQAUXKUXHUXMUXLUXJUWSHUTUQZAUXAUMQZUXCUOQZUX
      NAUXAUNQZUXPAUXHUXRUXJHVARZUXAVBRZAUXBUOQZUXIUXQAUXHUYAUXJHVERZLUXBHVCUQU
      XAUXCVFUQUWTUXDVGUQAUWOUWRCEFHVHUWLUWLVIZAUWNCEHUWLUYCAUXIHVJQZLHVKRZAUWM
      UWLQZVLZUWNVMAUYFUWMUNQZUWNULQAUXHUYFUYHUXJUWMHVQVNZAUYHVLZUWNBUWMUGOUWMU
      JUCZUKOZULUYHUWNUYLSABDEUWMIVOVPZAUXGUYHUYLULQMBUWMVRVNVSVTZAUXGUXHUIEHWA
      WBWCZQMUXJBDEHIWDUQWEAUWQCFHUWLUYCUYEUYGUWQVMUYGUWQAUYFUYHUWQUMQUYIUYJUWQ
      UWSUWMUGOUYKUKOZUMUYHUWQUYPSAUWSDFUWMJVOVPZAUXKUYHUYPUMQUXLUWSUWMWFVNVSVT
      WGZAUWSULQUXHUIFHWAZUYOQAUWSUXLWGUXJUWSDFHJWDUQWEZUWOVHQAUWLUWNCWHWIUYEUY
      NAUYFUYHUWQUWNUFUCZSUYIUYJUYLUFUCZUYPVUAUWQAUXGUYHVUBUYPSMBUWMWJVNUYJUWNU
      YLUFUYMWKUYQWLVTWMAUNHUAUDZUIOZFUCZUAUEZUNUWTUXBUKOZUHUXAUKOZVUCUGOZPOZUA
      UEUWRUXEWNAVUEVUJUAFHWOZWPOZGTUNWQTVJQZAWRWIZAHULQZVUCULQVUCVULUCVUESVUCU
      NQZAUXIVUOLHWSRZVUCXAHVUCFFDUNUWSDUDZUGOVURUJUCUKOZWTVHJDUNVUSXBXCXDZXEXF
      AVUPVLZVUEUWSVUDUGOZVUDUJUCZUKOZUMVVAVUDUNQZVUEVVDSAUXHVUPVVEUXJHVUCXGVNZ
      UWSDFVUDJVORZVVAUXKVVEVVDUMQAUXKVUPUXLXHZVVFUWSVUDWFUQVSVUPVUCGUCZVUJSADV
      UCVUGVUHVURUGOZPOVUJUNGVURVUCSVVJVUIVUGPVURVUCVUHUGXIZXKKVUGVUIPXLXMVPZVV
      AVUGUMQZVUIUMQZVUJUMQZAVVMVUPAUXMUYAVVMUXOUYBUWTUXBVFUQZXHAVUHUMQZVUPVVNA
      UXAUOQZVVQAUXIVVRLHXJRZUXAXNRVUHVUCUTVNZVUGVUIVGUQZVVAVUEVVDVUJWNVVGVVAVV
      DVUJWNXOZVVBVVCVUJPOZWNXOZVVAVVBUWTVWCVVAUXKVVEVVBUMQZVVHVVFUWSVUDUTUQZAU
      XMVUPUXOXHZVVAVVCUMQZVVOVWCUMQVVAVVCUOQZVWHVVAVVEVWIVVFVUDVERZVVCXPRZVWAV
      VCVUJVGUQVVAUXKUXHVUDUWLQZTUWSWNXOZUWSUHWNXOZVVBUWTWNXOVVHAUXHVUPUXJXHAHU
      WLQZVUPVWLAUYDVWOUYEHXQRVUCHHXRVNAVWMVUPAUXGVWMMBXSRZXHAVWNVUPNXHUWSHVUDY
      BXTVVAUWTVVCUWTPOZUXBUXAVUCUGOZPOZUKOZVWCWNVVAVWSUWTPOVWQWNXOZUWTVWTWNXOZ
      VVAVWSUMQZVWHUXMTUWTWNXOZVLZVWSVVCWNXOZVXAVVAVWSUOQZVXCVVAUYAVWRUOQZVXGAU
      YAVUPUYBXHAVVRVUPVXHVVSUXAVUCUUDVNZUXBVWRVCUQZVWSXPRZVWKAVXEVUPAUXMVXDUXO
      AUXKUXHVWMVXDUXLUXJVWPUWSHUUAYAUUBXHAUXHVUPVXFUXJVUCHUUCVNVWSVVCUWTUUEUUF
      VVAUXMVWQUMQZVXCTVWSYCXOVLZVXAVXBYDVWGVVAVWHUXMVXLVWKVWGVVCUWTVGUQVVAVWSU
      UGQZVXMVVAVXGVXNVXJVWSUUHRVWSUUIUUJUWTVWQVWSUUKYAYEVVAVWTVVCUWTVWSUKOZPOZ
      VWCVVAVVCULQUWTULQZVWSULQVWSTYFZVWTVXPSVVAVVCVWKWGAVXQVUPAUWTUXOWGZXHZVVA
      VWSVXKWGVVAVXGVXRVXJVWSYGRVVCUWTVWSYHYIVVAVXOVUJVVCPVVAVXOVUGVWRUKOZVUGUH
      VWRUKOZPOZVUJVVAVYAVXOVVAVXQUXBULQZUXBTYFZVWRULQZVWRTYFZVYAVXOSVXTAVYDVUP
      AUYAVYDUYBUXBWSRZXHAVYEVUPAUXHVYEUXJHUULRZXHVVAVXHVYFVXIVWRWSRZVVAVXHVYGV
      XIVWRYGRZUWTUXBVWRYJYKYMVVAVUGULQZVYFVYGVYAVYCSAVYLVUPAVUGVVPWGZXHVYJVYKV
      UGVWRUUMYAVVAVUJVYCVVAVUIVYBVUGPVVAUXAULQZUXATYFZVUCVJQZVUIVYBSAVYNVUPAVV
      RVYNVVSUXAWSRZXHVVAVVRVYOAVVRVUPVVSXHUXAYGRVUPVYPAVUCUUNVPUXAVUCUUOYAXKYM
      UUPXKYLYNYOVVAVWEVVOVWHTVVCYCXOZVWBVWDYDVWFVWAVWKVVAVWIVYRVWJVVCUVERVVBVU
      JVVCUUQYIYPYQAUIVULTWAZUIFTVUKYROZWAZVUKWPOZUYOAVUMVUKVJQZVYSWUBSVUNAUYDW
      UCUYEHUURRZUIFTVUKVUTUUSUQAWUBUWRWBXOZWUBUYOQAWUEWUAUWRWBXOZAWUAUYSUWRWBA
      VYTHUIFAVUOVYTHSVUQVUOVYTTHUIOZHTULQVUOVYTWUGSUVFTHUUTUVAHUVBZYLRUVCUYTYQ
      AWUCWUAVHQZUWRVHQZWUEWUFYDWUDWUIAUIFVYTYSWIWUJAUWLUWQCWHWIUWRWUAVUKVHVHUV
      DYAYPWUBUWRWBWUAVUKWPXLYTRVSAUIGTWAZUXEWBXOWUKUYOQAWUKVUGUXAUXAUHYROZUKOZ
      POZUXEWBAWUMVUGUADUNVVJWTZGTVHUNWQVUNWUMVHQAUXAWULUKXLWIVYMAUXAUAWUOVYQAU
      HUXAUXAUFUCZYCAUHHWNXOZUHUXAYCXOZAUXIWUQLHUVORAUHUOQUXIWUQWURYDUVGLUHHUVH
      UVIYEAUXPTUXAWNXOZWUPUXASUXTAUXRWUSUXSUXAUVJRUXAUVPUQUVKVUPVUCWUOUCZVUISA
      DVUCVVJVUIUNWUOVVKWUOVIVUHVUCUGXLXMVPZUVLVVAWUTVUIULWVAVVAVUIVVTWGVSVVAVV
      IVUJVUGWUTPOVVLVVAWUTVUIVUGPWVAXKUVMUVNAWUNVUGUXAHUKOZPOZUWTWVBPOUXBUKOZU
      XEAWUMWVBVUGPAWULHUXAUKAVUOUHULQWULHSVUQUVQHUHUVRUVSXKXKAVXQWVBULQZVYDVYE
      WVDWVCSVXSAWVBAUXPUXIWVBUMQUXTLUXAHVFUQWGZVYHVYIUWTWVBUXBUVTYIAWVDUWTWVBU
      XBUKOZPOZUXEAVXQWVEVYDVYEWVDWVHSVXSWVFVYHVYIUWTWVBUXBYHYIAWVGUXDUWTPAWVGU
      XAHUXBPOZUKOZUXDAVYNVUOHTYFZVYDVYEWVGWVJSVYQVUQAUXIWVKLHYGRVYHVYIUXAHUXBY
      JYKAWVIUXCUXAUKAVUOVYDWVIUXCSVUQVYHHUXBUWAUQXKYLXKYLUWBYNZWUKUXEWBUIGTYSY
      TRUWFAWUGUBUCZUWQCUEVUFUWRAUWQVUECUAHTWVMUNWQWVMVIUWMVUDFUWCUYEVUNAUWMWVM
      QZUYFUWQULQAWVNUYFAWVMUWLUWMAWUGHUBAVUOWUGHSVUQWUHRWKZUWDUWEUYRVTUWGAWVMU
      WLUWQCWVOUWHUWIAVUJUXEUAGTVHUNWQVUNVVLVVAVUJVWAWGUXEVHQAUWTUXDPXLWIWVLUWJ
      UWKYO $.
      $( [29-Apr-2014] $) $( [19-Jan-2008] $)
  $}

  $(
  @{
    dfef2.1 @e |- ( ph -> F e. V ) @.
    dfef2.2 @e |- ( ph -> A e. CC ) @.
    dfef2.3 @e |- ( ( ph /\ k e. NN ) ->
                    ( F ` k ) = ( ( 1 + ( A / k ) ) ^ k ) ) @.
    @( The limit of the sequence ` ( 1 + A / k ) ^ k ` as ` k ` goes to ` +oo `
       is ` ( exp `` A ) ` . This is another common definition of ` _e ` . @)
    dfef2 @p |- ( ph -> F ~~> ( exp ` A ) ) @=
      ? @.
  @}
  $)

