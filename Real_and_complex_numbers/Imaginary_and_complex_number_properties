$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Imaginary and complex number properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The reciprocal of ` _i ` . $)
  irec $p |- ( 1 / _i ) = -u _i $=
    ( c1 ci cdiv co cneg wceq cmul ax-icn mulneg2i ixi ax-1cn mulcli negcon2i
    mpbir eqtr4i negcli ine0 divmuli ) ABCDBEZFBSGDZAFTBBGDZEZABBHHIAUBFUAAEFJA
    UAKBBHHLMNOABSKHBHPQRN $.
    $( [11-Oct-1999] $)

  $( ` _i ` squared. $)
  i2 $p |- ( _i ^ 2 ) = -u 1 $=
    ( ci c2 cexp co cmul c1 cneg ax-icn sqvali ixi eqtri ) ABCDAAEDFGAHIJK $.
    $( [29-May-2005] $) $( [6-May-1999] $)

  $( ` _i ` cubed. $)
  i3 $p |- ( _i ^ 3 ) = -u _i $=
    ( ci c3 cexp co c2 c1 caddc cmul cneg df-3 opreq2i cc wcel cn0 wceq ax-icn
    2nn0 expp1 mp2an i2 opreq1i mulm1i eqtri 3eqtri ) ABCDAEFGDZCDZAECDZAHDZAIZ
    BUEACJKALMENMUFUHOPQAERSUHFIZAHDUIUGUJAHTUAAPUBUCUD $.
    $( [31-Jan-2007] $)

  $( ` _i ` to the fourth power. $)
  i4 $p |- ( _i ^ 4 ) = 1 $=
    ( ci c2 caddc co cexp cmul c4 c1 cc wcel cn0 wceq ax-icn 2nn0 expadd mp3an
    2p2e4 opreq2i cneg i2 opreq12i ax-1cn mul2negi mulid1i 3eqtri 3eqtr3i ) ABB
    CDZEDZABEDZUIFDZAGEDHAIJBKJZUKUHUJLMNNABBOPUGGAEQRUJHSZULFDHHFDHUIULUIULFTT
    UAHHUBUBUCHUBUDUEUF $.
    $( [31-Jan-2007] $)

  $( The imaginary unit ` _i ` is not a real number. $)
  inelr $p |- -. _i e. RR $=
    ( ci cr wcel cc0 wceq wne wn ine0 df-ne mpbi cmul co clt wbr c1 lt01 0re
    1re ltnsymi ax-mp caddc cneg ixi renegcli eqeltri ltadd1i ax-1cn addid2i
    ax-i2m1 breq12i bitri mtbir msqgt0 ex necon1bd mpi mto ) ABCZADEZADFZUSGHAD
    IJURDAAKLZMNZGUSVBODMNZDOMNVCGPDOQRSTVBDOUALZVAOUALZMNVCDVAOQVAOUBBUCORUDUE
    RUFVDOVEDMOUGUHUIUJUKULURVBADURUTVBAUMUNUOUPUQ $.
    $( [6-May-1999] $)

  ${
    cru.1 $e |- A e. RR $.
    cru.2 $e |- B e. RR $.
    cru.3 $e |- C e. RR $.
    cru.4 $e |- D e. RR $.
    $( Lemma for ~ crui . $)
    crulem $p |- ( ( A + ( _i x. B ) ) = ( C + ( _i x. D ) ) -> B = D ) $=
      ( ci cmul co caddc wceq cmin cc0 cr wcel inelr wn wne df-ne wi cneg cdiv
      wa renegcli readdcli resubcli redivclzi ax-icn recni subdii mulcli
      negsubi eqtr4i opreq1 negcli add4i negidi opreq1i addcli addid2i 3eqtri
      opreq2i addid1i 3eqtr3g syl5eq divmulzi mulcomi eqeq1i syl6bb syl5ibr
      imp eleq1d syl5ib ex pm2.43b syl5bir mt3i subeq0i sylib ) AIBJKZLKZCIDJKZ
      LKZMZBDNKZOMZBDMWFWHIPQZRWHSWGOTZWFWIWGOUAWFWJWIWJWFWJWIUBWJCAUCZLKZWGUDK
      ZPQWJWFUEZWIWLWGCWKGAEUFZUGZBDFHUHZUIWNWMIPWJWFWMIMZWFWRWJIWGJKZWLMZWFWSW
      BWDUCZLKZWLWSWBWDNKXBIBDUJBFUKZDHUKZULWBWDIBUJXCUMZIDUJXDUMZUNUOWFWCWKXAL
      KZLKZWEXGLKZXBWLWCWEXGLUPXHAWKLKZXBLKOXBLKXBAWBWKXAAEUKZXEWKWOUKZWDXFUQZU
      RXJOXBLAXKUSUTXBWBXAXEXMVAVBVCXIWLWDXALKZLKWLOLKWLCWDWKXACGUKXFXLXMURXNOW
      LLWDXFUSVDWLWLWPUKZVEVCVFVGWJWRWGIJKZWLMWTWLWGIXOWGWQUKZUJVHXPWSWLWGIXQUJ
      VIVJVKVLVMVNVOVPVQVRVSBDXCXDVTWA $.
      $( [11-Mar-2008] $) $( [8-May-1999] $)

    $( The representation of complex numbers in terms of real and imaginary
       parts is unique.  Proposition 10-1.3 of [Gleason] p. 130. $)
    crui $p |- ( ( A + ( _i x. B ) ) = ( C + ( _i x. D ) ) <->
                   ( A = C /\ B = D ) ) $=
      ( ci cmul co caddc wceq wa crulem opreq2 opreq2d eqeq2d recni ax-icn
      mulcli addcomi eqeq12i addcani biimpi sylbi syl6bir mpcom jca id
      opreqan12d impbii ) AIBJKZLKZCIDJKZLKZMZACMZBDMZNUQURUSUSUQURABCDEFGHOZUS
      UQUNCUMLKZMZURUSVAUPUNUSUMUOCLBDIJPZQRVBUMALKZUMCLKZMZURUNVDVAVEAUMAESZIB
      TBFSUAZUBCUMCGSZVHUBUCVFURUMACVHVGVIUDUEUFUGUHUTUIURUSACUMUOLURUJVCUKUL
      $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)
  $}

  $( The representation of complex numbers in terms of real and imaginary parts
     is unique.  Proposition 10-1.3 of [Gleason] p. 130. $)
  cru $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
      ( ( A + ( _i x. B ) ) = ( C + ( _i x. D ) ) <-> ( A = C /\ B = D ) ) ) $=
    ( cr wcel ci cmul co caddc wceq wa wb cc0 cif opreq1 eqeq1d eqeq1 anbi1d
    bibi12d opreq2 opreq2d anbi2d eqeq2d eqeq2 0re elimel crui dedth4h ) AEFZBE
    FZCEFZDEFZAGBHIZJIZCGDHIZJIZKZACKZBDKZLZMUJANOZUNJIZUQKZVBCKZUTLZMVBGUKBNOZ
    HIZJIZUQKZVEVGDKZLZMVIULCNOZUPJIZKZVBVMKZVKLZMVIVMGUMDNOZHIZJIZKZVPVGVRKZLZ
    MABCDNNNNAVBKZURVDVAVFWDUOVCUQAVBUNJPQWDUSVEUTAVBCRSTBVGKZVDVJVFVLWEVCVIUQW
    EUNVHVBJBVGGHUAUBQWEUTVKVEBVGDRUCTCVMKZVJVOVLVQWFUQVNVICVMUPJPUDWFVEVPVKCVM
    VBUESTDVRKZVOWAVQWCWGVNVTVIWGUPVSVMJDVRGHUAUBUDWGVKWBVPDVRVGUEUCTVBVGVMVRAN
    EUFUGBNEUFUGCNEUFUGDNEUFUGUHUI $.
    $( [11-Mar-2008] $) $( [9-May-1999] $)

  ${
    crne0.1 $e |- A e. RR $.
    crne0.2 $e |- B e. RR $.
    $( The real representation of complex numbers is nonzero iff one of its
       terms is nonzero. $)
    crne0i $p |- ( ( A =/= 0 \/ B =/= 0 ) <-> ( A + ( _i x. B ) ) =/= 0 ) $=
      ( ci cmul co caddc cc0 wceq wn wa wne wo id ax-icn 0cn mulcli addid2i
      mul01i eqtr2i syl6eq 0re crui sylib opreq2 opreqan12d eqtri impbii
      notbii df-ne neorian 3bitr4ri ) AEBFGZHGZIJZKAIJZBIJZLZKUOIMAIMBIMNUPUSUP
      USUPUOIEIFGZHGZJUSUPUOIVAUPOVAUTIUTEIPQRSZEPTZUAUBABIICDUCUCUDUEUSUOVAIUQ
      URAIUNUTHUQOBIEFUFUGVAUTIVBVCUHUBUIUJUOIUKAIBIULUM $.
      $( [11-Mar-2008] $) $( [29-Apr-2005] $)
  $}

  ${
    crmul.1 $e |- A e. CC $.
    crmul.2 $e |- B e. CC $.
    crmul.3 $e |- C e. CC $.
    crmul.4 $e |- D e. CC $.
    $( Multiplication rule for complex number representation.  Remark in
       [Apostol] p. 361.  In normal use, the arguments are the real components
       of two complex numbers, but the theorem works for complex components as
       well. $)
    crmuli $p |- ( ( A + ( _i x. B ) ) x. ( C + ( _i x. D ) ) ) =
     ( ( ( A x. C ) - ( B x. D ) ) + ( _i x. ( ( A x. D ) + ( B x. C ) ) ) ) $=
      ( ci cmul co caddc cmin cneg ax-icn mulcli addcli adddiri negcli add4i
      adddii mulassi opreq2i eqtr4i mul32i c1 mul4i ixi ax-1cn mulcomi
      mulneg1i mulid2i negeqi 3eqtri opreq12i addcomi 3eqtr4i negsubi opreq1i
      3eqtr3i ) ABIJKZLKZCDIJKZLKZJKZACJKZBDJKZMKZADJKZBCJKZLKZIJKZLKZAIBJKZLKZ
      CIDJKZLKZJKVHIVKJKZLKVEAVDJKZVAVDJKZLKZVFVGNZLKZVLLKZVMAVAVDEBIFOPZCVCGDI
      HOPZQRVFVIIJKZLKZWBVJIJKZLKZLKWCWGWILKZLKWAWDVFWGWBWIACEGPZVIIADEHPZOPVGB
      DFHPZSZVJIBCFGPZOPZTVSWHVTWJLVSVFAVCJKZLKWHACVCEGWFUAWGWRVFLADIEHOUBUCUDV
      TVACJKZVAVCJKZLKWIWBLKWJVACVCWEGWFUAWSWIWTWBLBICFOGUEWTVGIIJKZJKVGUFNZJKZ
      WBBIDIFOHOUGXAXBVGJUHUCXCXBVGJKUFVGJKZNWBVGXBWNUFUISUJUFVGUIWNUKXDVGVGWNU
      LUMUNUNUOWIWBWQWOUPUNUOVLWKWCLVIVJIWMWPORUCUQWCVHVLLVFVGWLWNURUSUNVBVOVDV
      QJVAVNALBIFOUJUCVCVPCLDIHOUJUCUOVLVRVHLVKIVIVJWMWPQOUJUCUT $.
      $( [11-Mar-2008] $) $( [29-Apr-2005] $)
  $}

  ${
    crrecz.1 $e |- A e. RR $.
    crrecz.2 $e |- B e. RR $.
    $( Reciprocal of a complex number in terms of real and imaginary
       components.  Remark in [Apostol] p. 361. $)
    crreczi $p |- ( ( A =/= 0 \/ B =/= 0 ) -> ( 1 / ( A + ( _i x. B ) ) ) =
                    ( ( A - ( _i x. B ) ) / ( ( A ^ 2 ) + ( B ^ 2 ) ) ) ) $=
      ( cc0 wne wo c1 ci cmul co caddc cdiv cmin c2 cexp wceq cneg renegcli
      crne0i recni negne0bi orbi2i ax-icn mulneg2i opreq2i mulcli negsubi
      eqtr2i neeq1i 3bitr4i cc wcel subcli divid mpan sylbi opreq2d addcli wa
      ax-1cn divmuldiv mpanl12 mpanr1 mpanl1 sylancb recclzi mulid1 syl
      3eqtr3rd mulid2i sqcli sqmuli i2 opreq1i mulneg1i 3eqtri negeqi negnegi
      subsqi 3eqtr3ri opreq12i syl6eq ) AEFZBEFZGZHAIBJKZLKZMKZHAWGNKZJKZWHWJJK
      ZMKZWJAOPKZBOPKZLKZMKWFWIWJWJMKZJKZWIHJKZWMWIWFWQHWIJWFWJEFZWQHQZWDBRZEFZ
      GAIXBJKZLKZEFWFWTAXBCBDSTWEXCWDBBDUAZUBUCWJXEEXEAWGRZLKWJXDXGALIBUDXFUEUF
      AWGACUAZIBUDXFUGZUHUIUJUKZWJULUMZWTXAAWGXHXIUNZWJUOUPUQURWFWHEFZWTWRWMQZA
      BCDTZXJWHULUMZXMWTXNAWGXHXIUSZXPXMUTZXKWTXNXLHULUMXKXRXKWTUTUTXNVAXLHWJWH
      WJVBVCVDVEVFWFWIULUMZWSWIQWFXMXSXOWHXQVGUQWIVHVIVJWKWJWLWPMWJXLVKWNWGOPKZ
      RZLKWNXTNKWPWLWNXTAXHVLWGXIVLUHYAWOWNLYAHWOJKZRZRYBWOXTYCXTIOPKZWOJKHRZWO
      JKYCIBUDXFVMYDYEWOJVNVOHWOVABXFVLZVPVQVRYBHWOVAYFUGVSWOYFVKVQUFAWGXHXIVTW
      AWBWC $.
      $( [11-Mar-2008] $) $( [29-Apr-2005] $)
  $}

  ${
    $d x y z w A $.
    $( The real part of a complex number is unique.  Proposition 10-1.3 of
       [Gleason] p. 130. $)
    creur $p |- ( A e. CC -> E! x e. RR E. y e. RR A = ( x + ( _i x. y ) ) ) $=
      ( vz vw cc wcel cv ci cmul co caddc wceq cr wrex wa wi wral wreu ax-cnre
      wex df-rex anbi12i eeanv bitr4i an4 eqtr2 cru simpl syl6bi syl5 an4s
      expimpd syl5bi 19.23advv rgen2a jctir opreq1 eqeq2d rexbidv opreq2
      opreq2d cbvrexv syl6bb reu4 sylibr ) CFGZCAHZIBHZJKZLKZMZBNOZANOZVMCDHZIE
      HZJKZLKZMZENOZPZVHVOMZQZDNRANRZPVMANSVGVNWDABCTWCADNWAVINGZVLPZVPNGZVSPZP
      ZEUABUAZVHNGZVONGZPZWBWAWFBUAZWHEUAZPWJVMWNVTWOVLBNUBVSENUBUCWFWHBEUDUEWM
      WIWBBEWIWEWGPZVLVSPZPWMWBWEVLWGVSUFWMWPWQWBWKWEWLWGWQWBQWQVKVRMZWKWEPWLWG
      PPZWBCVKVRUGWSWRWBVIVPMZPWBVHVIVOVPUHWBWTUIUJUKULUMUNUOUNUPUQVMVTADNWBVMC
      VOVJLKZMZBNOVTWBVLXBBNWBVKXACVHVOVJLURUSUTXBVSBENWTXAVRCWTVJVQVOLVIVPIJVA
      VBUSVCVDVEVF $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)

    $( The imaginary part of a complex number is unique.  Proposition 10-1.3 of
       [Gleason] p. 130. $)
    creui $p |- ( A e. CC -> E! y e. RR E. x e. RR A = ( x + ( _i x. y ) ) ) $=
      ( vz vw cc wcel cv ci cmul co caddc wceq cr wrex wa wi wral wreu ax-cnre
      rexcom sylib wex df-rex anbi12i eeanv bitr4i an4 eqtr2 cru simpr syl6bi
      syl5 an4s expcom imp3a syl5bi 19.23advv rgen2a jctir opreq2 opreq2d
      eqeq2d rexbidv opreq1 cbvrexv syl6bb reu4 sylibr ) CFGZCAHZIBHZJKZLKZMZAN
      OZBNOZVPCDHZIEHZJKZLKZMZDNOZPZVLVSMZQZENRBNRZPVPBNSVJVQWGVJVOBNOANOVQABCT
      VOABNNUAUBWFBENWDVKNGZVOPZVRNGZWBPZPZDUCAUCZVLNGZVSNGZPZWEWDWIAUCZWKDUCZP
      WMVPWQWCWRVOANUDWBDNUDUEWIWKADUFUGWPWLWEADWLWHWJPZVOWBPZPWPWEWHVOWJWBUHWP
      WSWTWEWSWPWTWEQZWHWNWJWOXAWTVNWAMZWHWNPWJWOPPZWECVNWAUIXCXBVKVRMZWEPWEVKV
      LVRVSUJXDWEUKULUMUNUOUPUQURUQUSUTVPWCBENWEVPCVKVTLKZMZANOWCWEVOXFANWEVNXE
      CWEVMVTVKLVLVSIJVAVBVCVDXFWBADNXDXEWACVKVRVTLVEVCVFVGVHVI $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)
  $}

  $( A real number times the imaginary unit is real only if the number is 0. $)
  rimul $p |- ( ( A e. RR /\ ( _i x. A ) e. RR ) -> A = 0 ) $=
    ( cr wcel ci cmul co wa cc0 wceq caddc cc recn ax-icn mul01i opreq1i 0cn
    mulcli addcom mpan syl5eqr syl adantl wb 0re cru mpanr2 mpanl1 mpbid
    simprd ) ABCZDAEFZBCZGZHUKIZAHIZUMHUKJFZUKDHEFZJFZIZUNUOGZULUSUJULUKKCZUSUK
    LVAUPUQUKJFZURUQHUKJDMNOUQKCVAVBURIDHMPQUQUKRSTUAUBHBCZUJULUSUTUCZUDVCUJGUL
    VCVDUDHAUKHUEUFUGUHUI $.
    $( [11-Mar-2008] $) $( [28-May-1999] $)

