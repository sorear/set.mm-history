$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Imaginary and complex number properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The imaginary unit ` _i ` is not a real number. $)
  inelr $p |- -. _i e. RR $=
    ( ci cr wcel cc0 wceq wne wn ine0 df-ne mpbi cmul co clt wbr c1 0re ltnsymi
    lt01 1re caddc cneg renegcli eqeltri ltadd1i ax-1cn addid2i ax-i2m1 breq12i
    ax-mp ixi bitri mtbir msqgt0 ex necon1bd mpi mto ) ABCZADEZADFZUSGHADIJURDA
    AKLZMNZGUSVBODMNZDOMNVCGRDOPSQUIVBDOTLZVAOTLZMNVCDVAOPVAOUABUJOSUBUCSUDVDOV
    EDMOUEUFUGUHUKULURVBADURUTVBAUMUNUOUPUQ $.
    $( [6-May-1999] $)

  ${
    cru.1 $e |- A e. RR $.
    cru.2 $e |- B e. RR $.
    cru.3 $e |- C e. RR $.
    cru.4 $e |- D e. RR $.
    $( Lemma for ~ crui . $)
    crulem $p |- ( ( A + ( _i x. B ) ) = ( C + ( _i x. D ) ) -> B = D ) $=
      ( ci cmul co caddc wceq cmin cc0 cr wcel cneg ax-icn recni inelr wn df-ne
      wne wi cdiv wa renegcli readdcli resubcli redivclzi subdii mulcli negsubi
      eqtr4i opreq1 negcli negidi opreq1i addcli addid2i 3eqtri opreq2i addid1i
      add4i 3eqtr3g syl5eq divmulzi mulcomi eqeq1i syl6bb syl5ibr eleq1d syl5ib
      imp ex pm2.43b syl5bir mt3i subeq0i sylib ) AIBJKZLKZCIDJKZLKZMZBDNKZOMZB
      DMWFWHIPQZUAWHUBWGOUDZWFWIWGOUCWFWJWIWJWFWJWIUEWJCARZLKZWGUFKZPQWJWFUGZWI
      WLWGCWKGAEUHZUIZBDFHUJZUKWNWMIPWJWFWMIMZWFWRWJIWGJKZWLMZWFWSWBWDRZLKZWLWS
      WBWDNKXBIBDSBFTZDHTZULWBWDIBSXCUMZIDSXDUMZUNUOWFWCWKXALKZLKZWEXGLKZXBWLWC
      WEXGLUPXHAWKLKZXBLKOXBLKXBAWBWKXAAETZXEWKWOTZWDXFUQZVEXJOXBLAXKURUSXBWBXA
      XEXMUTVAVBXIWLWDXALKZLKWLOLKWLCWDWKXACGTXFXLXMVEXNOWLLWDXFURVCWLWLWPTZVDV
      BVFVGWJWRWGIJKZWLMWTWLWGIXOWGWQTZSVHXPWSWLWGIXQSVIVJVKVLVOVMVNVPVQVRVSBDX
      CXDVTWA $.
      $( [11-Mar-2008] $) $( [8-May-1999] $)

    $( The representation of complex numbers in terms of real and imaginary
       parts is unique.  Proposition 10-1.3 of [Gleason] p. 130. $)
    crui $p |- ( ( A + ( _i x. B ) ) = ( C + ( _i x. D ) ) <->
                   ( A = C /\ B = D ) ) $=
      ( ci cmul co caddc wceq wa crulem opreq2 opreq2d eqeq2d recni addcomi jca
      ax-icn mulcli eqeq12i addcani biimpi sylbi syl6bir id opreqan12d impbii
      mpcom ) AIBJKZLKZCIDJKZLKZMZACMZBDMZNUQURUSUSUQURABCDEFGHOZUSUQUNCUMLKZMZ
      URUSVAUPUNUSUMUOCLBDIJPZQRVBUMALKZUMCLKZMZURUNVDVAVEAUMAESZIBUBBFSUCZTCUM
      CGSZVHTUDVFURUMACVHVGVIUEUFUGUHULUTUAURUSACUMUOLURUIVCUJUK $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)
  $}

  $( The representation of complex numbers in terms of real and imaginary parts
     is unique.  Proposition 10-1.3 of [Gleason] p. 130. $)
  cru $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
      ( ( A + ( _i x. B ) ) = ( C + ( _i x. D ) ) <-> ( A = C /\ B = D ) ) ) $=
    ( cr wcel ci cmul co caddc wceq wa cc0 cif opreq1 eqeq1d bibi12d 0re elimel
    wb eqeq1 anbi1d opreq2 opreq2d anbi2d eqeq2d eqeq2 crui dedth4h ) AEFZBEFZC
    EFZDEFZAGBHIZJIZCGDHIZJIZKZACKZBDKZLZTUJAMNZUNJIZUQKZVBCKZUTLZTVBGUKBMNZHIZ
    JIZUQKZVEVGDKZLZTVIULCMNZUPJIZKZVBVMKZVKLZTVIVMGUMDMNZHIZJIZKZVPVGVRKZLZTAB
    CDMMMMAVBKZURVDVAVFWDUOVCUQAVBUNJOPWDUSVEUTAVBCUAUBQBVGKZVDVJVFVLWEVCVIUQWE
    UNVHVBJBVGGHUCUDPWEUTVKVEBVGDUAUEQCVMKZVJVOVLVQWFUQVNVICVMUPJOUFWFVEVPVKCVM
    VBUGUBQDVRKZVOWAVQWCWGVNVTVIWGUPVSVMJDVRGHUCUDUFWGVKWBVPDVRVGUGUEQVBVGVMVRA
    MERSBMERSCMERSDMERSUHUI $.
    $( [11-Mar-2008] $) $( [9-May-1999] $)

  ${
    crne0.1 $e |- A e. RR $.
    crne0.2 $e |- B e. RR $.
    $( The real representation of complex numbers is nonzero iff one of its
       terms is nonzero. $)
    crne0i $p |- ( ( A =/= 0 \/ B =/= 0 ) <-> ( A + ( _i x. B ) ) =/= 0 ) $=
      ( ci cmul co caddc cc0 wceq wn wa wne wo id ax-icn 0cn mulcli syl6eq 0re
      addid2i mul01i eqtr2i sylib opreq2 opreqan12d eqtri impbii notbii neorian
      crui df-ne 3bitr4ri ) AEBFGZHGZIJZKAIJZBIJZLZKUOIMAIMBIMNUPUSUPUSUPUOIEIF
      GZHGZJUSUPUOIVAUPOVAUTIUTEIPQRUAZEPUBZUCSABIICDTTUKUDUSUOVAIUQURAIUNUTHUQ
      OBIEFUEUFVAUTIVBVCUGSUHUIUOIULAIBIUJUM $.
      $( [11-Mar-2008] $) $( [29-Apr-2005] $)
  $}

  ${
    crmul.1 $e |- A e. CC $.
    crmul.2 $e |- B e. CC $.
    crmul.3 $e |- C e. CC $.
    crmul.4 $e |- D e. CC $.
    $( Multiplication rule for complex number representation.  Remark in
       [Apostol] p. 361.  In normal use, the arguments are the real components
       of two complex numbers, but the theorem works for complex components as
       well. $)
    crmuli $p |- ( ( A + ( _i x. B ) ) x. ( C + ( _i x. D ) ) ) =
     ( ( ( A x. C ) - ( B x. D ) ) + ( _i x. ( ( A x. D ) + ( B x. C ) ) ) ) $=
      ( ci cmul co caddc cneg ax-icn mulcli opreq2i c1 mulcomi 3eqtri opreq12i
      addcli adddiri negcli add4i adddii mulassi eqtr4i mul32i mul4i ixi ax-1cn
      cmin mulneg1i mulid2i negeqi addcomi 3eqtr4i negsubi opreq1i 3eqtr3i ) AB
      IJKZLKZCDIJKZLKZJKZACJKZBDJKZULKZADJKZBCJKZLKZIJKZLKZAIBJKZLKZCIDJKZLKZJK
      VHIVKJKZLKVEAVDJKZVAVDJKZLKZVFVGMZLKZVLLKZVMAVAVDEBIFNOZCVCGDIHNOZUAUBVFV
      IIJKZLKZWBVJIJKZLKZLKWCWGWILKZLKWAWDVFWGWBWIACEGOZVIIADEHOZNOVGBDFHOZUCZV
      JIBCFGOZNOZUDVSWHVTWJLVSVFAVCJKZLKWHACVCEGWFUEWGWRVFLADIEHNUFPUGVTVACJKZV
      AVCJKZLKWIWBLKWJVACVCWEGWFUEWSWIWTWBLBICFNGUHWTVGIIJKZJKVGQMZJKZWBBIDIFNH
      NUIXAXBVGJUJPXCXBVGJKQVGJKZMWBVGXBWNQUKUCRQVGUKWNUMXDVGVGWNUNUOSSTWIWBWQW
      OUPSTVLWKWCLVIVJIWMWPNUBPUQWCVHVLLVFVGWLWNURUSSVBVOVDVQJVAVNALBIFNRPVCVPC
      LDIHNRPTVLVRVHLVKIVIVJWMWPUANRPUT $.
      $( [11-Mar-2008] $) $( [29-Apr-2005] $)
  $}

  ${
    $d x y z w A $.
    $( The real part of a complex number is unique.  Proposition 10-1.3 of
       [Gleason] p. 130. $)
    creur $p |- ( A e. CC -> E! x e. RR E. y e. RR A = ( x + ( _i x. y ) ) ) $=
      ( vz vw wcel cv ci cmul co caddc wceq cr wrex wa wral wex df-rex syl5bi
      wi wreu ax-cnre anbi12i eeanv bitr4i an4 eqtr2 cru simpl syl6bi syl5 an4s
      expimpd exlimdvv rgen2a jctir opreq1 eqeq2d rexbidv opreq2 opreq2d syl6bb
      cc cbvrexv reu4 sylibr ) CVCFZCAGZHBGZIJZKJZLZBMNZAMNZVMCDGZHEGZIJZKJZLZE
      MNZOZVHVOLZTZDMPAMPZOVMAMUAVGVNWDABCUBWCADMWAVIMFZVLOZVPMFZVSOZOZEQBQZVHM
      FZVOMFZOZWBWAWFBQZWHEQZOWJVMWNVTWOVLBMRVSEMRUCWFWHBEUDUEWMWIWBBEWIWEWGOZV
      LVSOZOWMWBWEVLWGVSUFWMWPWQWBWKWEWLWGWQWBTWQVKVRLZWKWEOWLWGOOZWBCVKVRUGWSW
      RWBVIVPLZOWBVHVIVOVPUHWBWTUIUJUKULUMSUNSUOUPVMVTADMWBVMCVOVJKJZLZBMNVTWBV
      LXBBMWBVKXACVHVOVJKUQURUSXBVSBEMWTXAVRCWTVJVQVOKVIVPHIUTVAURVDVBVEVF $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)

    $( The imaginary part of a complex number is unique.  Proposition 10-1.3 of
       [Gleason] p. 130. $)
    creui $p |- ( A e. CC -> E! y e. RR E. x e. RR A = ( x + ( _i x. y ) ) ) $=
      ( vz vw wcel cv ci cmul co caddc wceq cr wrex wa wral wex df-rex syl5bi
      wi cc wreu ax-cnre rexcom sylib anbi12i eeanv bitr4i an4 eqtr2 cru syl6bi
      simpr syl5 an4s expcom imp3a exlimdvv rgen2a jctir opreq2 opreq2d rexbidv
      eqeq2d opreq1 cbvrexv syl6bb reu4 sylibr ) CUAFZCAGZHBGZIJZKJZLZAMNZBMNZV
      PCDGZHEGZIJZKJZLZDMNZOZVLVSLZTZEMPBMPZOVPBMUBVJVQWGVJVOBMNAMNVQABCUCVOABM
      MUDUEWFBEMWDVKMFZVOOZVRMFZWBOZOZDQAQZVLMFZVSMFZOZWEWDWIAQZWKDQZOWMVPWQWCW
      RVOAMRWBDMRUFWIWKADUGUHWPWLWEADWLWHWJOZVOWBOZOWPWEWHVOWJWBUIWPWSWTWEWSWPW
      TWETZWHWNWJWOXAWTVNWALZWHWNOWJWOOOZWECVNWAUJXCXBVKVRLZWEOWEVKVLVRVSUKXDWE
      UMULUNUOUPUQSURSUSUTVPWCBEMWEVPCVKVTKJZLZAMNWCWEVOXFAMWEVNXECWEVMVTVKKVLV
      SHIVAVBVDVCXFWBADMXDXEWACVKVRVTKVEVDVFVGVHVI $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)
  $}

  $( A real number times the imaginary unit is real only if the number is 0. $)
  rimul $p |- ( ( A e. RR /\ ( _i x. A ) e. RR ) -> A = 0 ) $=
    ( cr wcel ci cmul co wa cc0 wceq caddc cc recn ax-icn mul01i opreq1i mulcli
    0cn addcom mpan 0re syl5eqr syl adantl wb cru mpanr2 mpanl1 mpbid simprd )
    ABCZDAEFZBCZGZHUKIZAHIZUMHUKJFZUKDHEFZJFZIZUNUOGZULUSUJULUKKCZUSUKLVAUPUQUK
    JFZURUQHUKJDMNOUQKCVAVBURIDHMQPUQUKRSUAUBUCHBCZUJULUSUTUDZTVCUJGULVCVDTHAUK
    HUEUFUGUHUI $.
    $( [11-Mar-2008] $) $( [28-May-1999] $)

  ${
    $d x y z A $.
    $( The complex conjugate of a complex number is unique. $)
    cju $p |- ( A e. CC -> E! x e. CC
               ( ( A + x ) e. RR /\ ( _i x. ( A - x ) ) e. RR ) ) $=
      ( vy vz cc wcel caddc co cr ci cmin cmul wa wrex ax-icn syl111anc opreq2d
      wceq opreq2 eleq1d cv wi wral wreu ax-cnre recn mulcl subcl syl2an adantr
      sylancr adantl ppncan ax-addrcl anidms eqeltrd pnncan a1i ax-distr eqtr4d
      ax-addcl syl11anc ax-mulass mp3an12 syl c1 ixi 1re renegcli eqeltri simpr
      cneg ax-mulrcl anbi12d rcla4ev syl12anc opreq1 rexbidv syl5ibrcom an4 cc0
      rexlimivv resubcl pnpcan 3expb syl5ib ancoms adantrl adantrr subdi 3com23
      nnncan1 eqtr3d anim12d rimul subeq0 biimpd 3syld ralrimivva reu4 sylanbrc
      syl5bi ) BEFZBAUAZGHZIFZJBXDKHZLHZIFZMZAENZXJBCUAZGHZIFZJBXLKHZLHZIFZMZMZ
      XDXLRZUBZCEUCAEUCXJAEUDXCBXLJDUAZLHZGHZRZDINCINXKCDBUEYEXKCDIIXLIFZYBIFZM
      ZXKYEYDXDGHZIFZJYDXDKHZLHZIFZMZAENZYHXLYCKHZEFZYDYPGHZIFZJYDYPKHZLHZIFZYO
      YFXLEFZYCEFZYQYGXLUFZYGJEFZYBEFZUUDOYBUFZJYBUGUKZXLYCUHUIYHYRXLXLGHZIYHUU
      CUUDUUCYRUUJRYFUUCYGUUEUJZYGUUDYFUUIULZUUKXLYCXLUMPYFUUJIFZYGYFUUMXLXLUNU
      OUJUPYHUUAJJLHZYBYBGHZLHZIYHUUAJJUUOLHZLHZUUPYHYTUUQJLYHYTYCYCGHZUUQYHUUC
      UUDUUDYTUUSRUUKUULUULXLYCYCUQPYHUUFUUGUUGUUQUUSRUUFYHOURYGUUGYFUUHULZUUTJ
      YBYBUSPUTQYHUUOEFZUUPUURRZYHUUGUUGUVAUUTUUTYBYBVAVBUUFUUFUVAUVBOOJJUUOVCV
      DVEUTYHUUNIFUUOIFZUUPIFUUNVFVLIVGVFVHVIVJYHYGYGUVCYFYGVKZUVDYBYBUNVBUUNUU
      OVMUKUPYNYSUUBMAYPEXDYPRZYJYSYMUUBUVEYIYRIXDYPYDGSTUVEYLUUAIUVEYKYTJLXDYP
      YDKSQTVNVOVPYEXJYNAEYEXFYJXIYMYEXEYIIBYDXDGVQTYEXHYLIYEXGYKJLBYDXDKVQQTVN
      VRVSWBVEXCYAACEEXSXFXNMZXIXQMZMZXCXDEFZUUCMZMZXTXFXIXNXQVTUVKUVHXDXLKHZIF
      ZJUVLLHZIFZMZUVLWARZXTUVKUVFUVMUVGUVOUVFXEXMKHZIFUVKUVMXEXMWCUVKUVRUVLIXC
      UVIUUCUVRUVLRBXDXLWDWETWFUVGXPXHKHZIFZUVKUVOXQXIUVTXPXHWCWGUVKUVSUVNIUVKJ
      XOXGKHZLHZUVSUVNUVKUUFXOEFZXGEFZUWBUVSRUUFUVKOURXCUUCUWCUVIBXLUHWHXCUVIUW
      DUUCBXDUHWIJXOXGWJPUVKUWAUVLJLXCUVIUUCUWAUVLRZXCUUCUVIUWEBXLXDWLWKWEQWMTW
      FWNUVPUVQUBUVKUVLWOURUVJUVQXTUBXCUVJUVQXTXDXLWPWQULWRXBWSXJXRACEXTXFXNXIX
      QXTXEXMIXDXLBGSTXTXHXPIXTXGXOJLXDXLBKSQTVNWTXA $.
      $( [6-Nov-2013] $)
  $}

