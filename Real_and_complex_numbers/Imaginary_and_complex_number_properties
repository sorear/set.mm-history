$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Imaginary and complex number properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The reciprocal of ` i ` . $)
  irec $p |- ( 1 / i ) = -u i $=
    ( c1 ci cdiv co cneg wceq cmul axicn mulneg2 ixi ax1cn mulcl negcon2 mpbir
    eqtr4 negcl ine0 divmul ) ABCDBEZFBSGDZAFTBBGDZEZABBHHIAUBFUAAEFJAUAKBBHHLM
    NOABSKHBHPQRN $.
    $( [11-Oct-1999] $)

  $( ` i ` squared. $)
  i2 $p |- ( i ^ 2 ) = -u 1 $=
    ( ci c2 cexp co cmul c1 cneg axicn sqval ixi eqtr ) ABCDAAEDFGAHIJK $.
    $( [29-May-2005] $) $( [6-May-1999] $)

  $( ` i `  cubed. $)
  i3 $p |- ( i ^ 3 ) = -u i $=
    ( ci c3 cexp co c2 c1 caddc cmul cneg df-3 opreq2i cc wcel cn0 wceq axicn
    2nn0 expp1t mp2an i2 opreq1i mulm1 eqtr 3eqtr ) ABCDAEFGDZCDZAECDZAHDZAIZBU
    EACJKALMENMUFUHOPQAERSUHFIZAHDUIUGUJAHTUAAPUBUCUD $.
    $( [31-Jan-2007] $) $( [31-Jan-2007] $)

  $( ` i ` to the fourth power. $)
  i4 $p |- ( i ^ 4 ) = 1 $=
    ( ci c2 caddc co cexp cmul c4 c1 cc wcel cn0 wceq axicn 2nn0 expaddt mp3an
    2p2e4 opreq2i cneg i2 opreq12i ax1cn mul2neg mulid1 3eqtr 3eqtr3 ) ABBCDZED
    ZABEDZUIFDZAGEDHAIJBKJZUKUHUJLMNNABBOPUGGAEQRUJHSZULFDHHFDHUIULUIULFTTUAHHU
    BUBUCHUBUDUEUF $.
    $( [1-Feb-2007] $) $( [31-Jan-2007] $)

  $( The imaginary unit ` i ` is not a real number. $)
  inelr $p |- -. i e. RR $=
    ( ci cr wcel cc0 wceq wne wn ine0 df-ne mpbi cmul co clt wbr c1 lt01 0re
    1re ltnsym ax-mp caddc cneg ixi renegcl eqeltr ltadd1 ax1cn addid2 axi2m1
    breq12i bitr mtbir msqgt0t ex necon1bd mpi mto ) ABCZADEZADFZUSGHADIJURDAAK
    LZMNZGUSVBODMNZDOMNVCGPDOQRSTVBDOUALZVAOUALZMNVCDVAOQVAOUBBUCORUDUERUFVDOVE
    DMOUGUHUIUJUKULURVBADURUTVBAUMUNUOUPUQ $.
    $( [6-May-1999] $)

  ${
    cru.1 $e |- A e. RR $.
    cru.2 $e |- B e. RR $.
    cru.3 $e |- C e. RR $.
    cru.4 $e |- D e. RR $.
    $( Lemma for ~ cru . $)
    crulem $p |- ( ( A + ( i x. B ) ) = ( C + ( i x. D ) ) -> B = D ) $=
      ( ci cmul co caddc wceq cmin cc0 cr wcel inelr wne wn wi wa cneg cdiv
      renegcl readdcl recn resubcl axicn divmulz mulcom eqeq1i syl6bb opreq1
      mulcl negcl add4 negid opreq1i addcl addid2 3eqtr opreq2i addid1 3eqtr3g
      subdi negsub eqtr4 syl5eq syl5bir imp eleq1d redivclz syl5bi ex pm2.43b
      df-ne syl5ibr mt3i subeq0 sylib ) AIBJKZLKZCIDJKZLKZMZBDNKZOMZBDMWFWHIPQZ
      RWFWGOSZWIWHTWFWJWIWJWFWJWIUAWJWFUBZCAUCZLKZWGUDKZPQWIWJWKWNIPWJWFWNIMZWJ
      WOIWGJKZWMMZWFWJWOWGIJKZWMMWQWMWGIWMCWLGAEUEZUFZUGZWGBDFHUHZUGZUIUJWRWPWM
      WGIXCUIUKULUMWFWBWDUCZLKZWMWPWFWCWLXDLKZLKZWEXFLKZXEWMWCWEXFLUNXGAWLLKZXE
      LKOXELKXEAWBWLXDAEUGZIBUIBFUGZUOZWLWSUGZWDIDUIDHUGZUOZUPZUQXIOXELAXJURUSX
      EWBXDXLXPUTVAVBXHWMWDXDLKZLKWMOLKWMCWDWLXDCGUGXOXMXPUQXQOWMLWDXOURVCWMXAV
      DVBVEWPWBWDNKXEIBDUIXKXNVFWBWDXLXOVGVHVIVJVKVLWMWGWTXBVMVNVOVPWGOVQVRVSBD
      XKXNVTWA $.
      $( [11-Mar-2008] $) $( [8-May-1999] $)

    $( The representation of complex numbers in terms of real and imaginary
       parts is unique.  Proposition 10-1.3 of [Gleason] p. 130. $)
    cru $p |- ( ( A + ( i x. B ) ) = ( C + ( i x. D ) ) <->
                   ( A = C /\ B = D ) ) $=
      ( ci cmul co caddc wceq wa crulem opreq2 opreq2d eqeq2d recn axicn mulcl
      addcom eqeq12i addcan biimp sylbi syl6bir mpcom jca id opreqan12d impbi
      ) AIBJKZLKZCIDJKZLKZMZACMZBDMZNUQURUSUSUQURABCDEFGHOZUSUQUNCUMLKZMZURUSVA
      UPUNUSUMUOCLBDIJPZQRVBUMALKZUMCLKZMZURUNVDVAVEAUMAESZIBTBFSUAZUBCUMCGSZVH
      UBUCVFURUMACVHVGVIUDUEUFUGUHUTUIURUSACUMUOLURUJVCUKUL $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)
  $}

  $( The representation of complex numbers in terms of real and imaginary
     parts is unique.  Proposition 10-1.3 of [Gleason] p. 130. $)
  crut $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
      ( ( A + ( i x. B ) ) = ( C + ( i x. D ) ) <-> ( A = C /\ B = D ) ) ) $=
    ( cr wcel ci cmul co caddc wceq wa wb cc0 cif opreq1 eqeq1d eqeq1 anbi1d
    bibi12d opreq2 opreq2d anbi2d eqeq2d eqeq2 0re elimel cru dedth4h ) AEFZBEF
    ZCEFZDEFZAGBHIZJIZCGDHIZJIZKZACKZBDKZLZMABCDNNUJANOZUNJIZUQKZVBCKZUTLZMVBGU
    KBNOZHIZJIZUQKZVEVGDKZLZMVIULCNOZUPJIZKZVBVMKZVKLZMVIVMGUMDNOZHIZJIZKZVPVGV
    RKZLZMNNAVBKZURVDVAVFWDUOVCUQAVBUNJPQWDUSVEUTAVBCRSTBVGKZVDVJVFVLWEVCVIUQWE
    UNVHVBJBVGGHUAUBQWEUTVKVEBVGDRUCTCVMKZVJVOVLVQWFUQVNVICVMUPJPUDWFVEVPVKCVMV
    BUESTDVRKZVOWAVQWCWGVNVTVIWGUPVSVMJDVRGHUAUBUDWGVKWBVPDVRVGUEUCTVBVGVMVRANE
    UFUGBNEUFUGCNEUFUGDNEUFUGUHUI $.
    $( [11-Mar-2008] $) $( [9-May-1999] $)

  $( The representation of complex numbers in terms of real and imaginary
     parts is unique.  Proposition 10-1.3 of [Gleason] p. 130. $)
  crutOLD $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
             ( ( A + ( B x. i ) ) = ( C + ( D x. i ) ) <->
             ( A = C /\ B = D ) ) ) $=
    ( cr wcel wa ci cmul co caddc wceq cc recnt axicn axmulcom mpan2 syl
    adantl opreq2d eqeqan12d crut bitrd ) AEFZBEFZGZCEFZDEFZGZGABHIJZKJZCDHIJZK
    JZLAHBIJZKJZCHDIJZKJZLACLBDLGUFUIUKUOUMUQUFUJUNAKUEUJUNLZUDUEBMFZURBNUSHMFZ
    UROBHPQRSTUIULUPCKUHULUPLZUGUHDMFZVADNVBUTVAODHPQRSTUAABCDUBUC $.
    $( [12-May-2005] $) $( [9-May-1999] $)

  ${
    crne0.1 $e |- A e. RR $.
    crne0.2 $e |- B e. RR $.
    $( The real representation of complex numbers is nonzero iff one of its
       terms is nonzero. $)
    crne0 $p |- ( ( A =/= 0 \/ B =/= 0 ) <-> ( A + ( i x. B ) ) =/= 0 ) $=
      ( ci cmul co caddc cc0 wceq wn wa wne wo id axicn 0cn mulcl addid2 mul01
      eqtr2 syl6eq 0re cru sylib opreq2 opreqan12d eqtr impbi negbii df-ne
      neorian 3bitr4r ) AEBFGZHGZIJZKAIJZBIJZLZKUOIMAIMBIMNUPUSUPUSUPUOIEIFGZHG
      ZJUSUPUOIVAUPOVAUTIUTEIPQRSZEPTZUAUBABIICDUCUCUDUEUSUOVAIUQURAIUNUTHUQOBI
      EFUFUGVAUTIVBVCUHUBUIUJUOIUKAIBIULUM $.
      $( [11-Mar-2008] $) $( [29-Apr-2005] $)
  $}

  ${
    crmul.1 $e |- A e. CC $.
    crmul.2 $e |- B e. CC $.
    crmul.3 $e |- C e. CC $.
    crmul.4 $e |- D e. CC $.
    $( Multiplication rule for complex number representation.  Remark in
       [Apostol] p. 361.  In normal use, the arguments are the real components
       of two complex numbers, but the theorem works for complex components as
       well. $)
    crmul $p |- ( ( A + ( i x. B ) ) x. ( C + ( i x. D ) ) ) =
      ( ( ( A x. C ) - ( B x. D ) ) + ( i x. ( ( A x. D ) + ( B x. C ) ) ) ) $=
      ( ci cmul co caddc cmin cneg axicn mulcl addcl adddir negcl add4 adddi
      mulass opreq2i eqtr4 mul23 c1 mul4 ixi ax1cn mulcom mulneg1 mulid2
      negeqi 3eqtr opreq12i addcom 3eqtr4 negsub opreq1i 3eqtr3 ) ABIJKZLKZCDIJ
      KZLKZJKZACJKZBDJKZMKZADJKZBCJKZLKZIJKZLKZAIBJKZLKZCIDJKZLKZJKVHIVKJKZLKVE
      AVDJKZVAVDJKZLKZVFVGNZLKZVLLKZVMAVAVDEBIFOPZCVCGDIHOPZQRVFVIIJKZLKZWBVJIJ
      KZLKZLKWCWGWILKZLKWAWDVFWGWBWIACEGPZVIIADEHPZOPVGBDFHPZSZVJIBCFGPZOPZTVSW
      HVTWJLVSVFAVCJKZLKWHACVCEGWFUAWGWRVFLADIEHOUBUCUDVTVACJKZVAVCJKZLKWIWBLKW
      JVACVCWEGWFUAWSWIWTWBLBICFOGUEWTVGIIJKZJKVGUFNZJKZWBBIDIFOHOUGXAXBVGJUHUC
      XCXBVGJKUFVGJKZNWBVGXBWNUFUISUJUFVGUIWNUKXDVGVGWNULUMUNUNUOWIWBWQWOUPUNUO
      VLWKWCLVIVJIWMWPORUCUQWCVHVLLVFVGWLWNURUSUNVBVOVDVQJVAVNALBIFOUJUCVCVPCLD
      IHOUJUCUOVLVRVHLVKIVIVJWMWPQOUJUCUT $.
      $( [11-Mar-2008] $) $( [29-Apr-2005] $)
  $}

  ${
    crrecz.1 $e |- A e. RR $.
    crrecz.2 $e |- B e. RR $.
    $( Reciprocal of a complex number in terms of real and imaginary
       components.  Remark in [Apostol] p. 361. $)
    crrecz $p |- ( ( A =/= 0 \/ B =/= 0 ) -> ( 1 / ( A + ( i x. B ) ) ) =
                    ( ( A - ( i x. B ) ) / ( ( A ^ 2 ) + ( B ^ 2 ) ) ) ) $=
      ( cc0 wne wo c1 ci cmul co caddc cdiv cmin c2 cexp wceq cneg renegcl
      crne0 recn negne0 orbi2i axicn mulneg2 opreq2i mulcl negsub eqtr2 neeq1i
      3bitr4 cc wcel subcl dividt mpan sylbi opreq2d wa ax1cn addcl pm3.2i
      divmuldivt mpanl12 sylancb recclz ax1id syl 3eqtr3rd mulid2 binom2aOLD
      sqcl sqmul i2 opreq1i mulneg1 3eqtr negeqi negneg 3eqtr2 opreq12i syl6eq
      ) AEFZBEFZGZHAIBJKZLKZMKZHAWFNKZJKZWGWIJKZMKZWIAOPKZBOPKZLKZMKWEWHWIWIMKZ
      JKZWHHJKZWLWHWEWPHWHJWEWIEFZWPHQZWCBRZEFZGAIXAJKZLKZEFWEWSAXACBDSTWDXBWCB
      BDUAZUBUCWIXDEXDAWFRZLKWIXCXFALIBUDXEUEUFAWFACUAZIBUDXEUGZUHUIUJUKZWIULUM
      ZWSWTAWFXGXHUNZWIUOUPUQURWGEFZWSWQWLQZWEHULUMZWGULUMZUSXJXJUSXLWSUSXMXNXO
      UTAWFXGXHVAZVBXJXJXKXKVBHWGWIWIVCVDABCDTZXIVEWEWHULUMZWRWHQWEXLXRXQWGXPVF
      UQWHVGVHVIWJWIWKWOMWIXKVJWKWMWFOPKZNKWMXSRZLKWOAWFXGXHVKWMXSAXGVLWFXHVLUH
      XTWNWMLXTHWNJKZRZRYAWNXSYBXSIOPKZWNJKHRZWNJKYBIBUDXEVMYCYDWNJVNVOHWNUTBXE
      VLZVPVQVRYAHWNUTYEUGVSWNYEVJVQUFVTWAWB $.
      $( [11-Mar-2008] $) $( [29-Apr-2005] $)
  $}

  ${
    $d x y z w A $.
    $( The real part of a complex number is unique.  Proposition
       10-1.3 of [Gleason] p. 130. $)
    creur $p |- ( A e. CC -> E! x e. RR E. y e. RR A = ( x + ( i x. y ) ) ) $=
      ( vz vw cc wcel cv ci cmul co caddc wceq cr wrex wa wi wral wreu axcnre
      wex crut pm3.26 syl6bi eqtr2t syl5 an4s ex imp3a an4 syl5ib 19.23advv
      df-rex anbi12i eeanv bitr4 rgen2a jctir opreq1 eqeq2d rexbidv opreq2
      opreq2d cbvrexv syl6bb reu4 sylibr ) CFGZCAHZIBHZJKZLKZMZBNOZANOZVNCDHZIE
      HZJKZLKZMZENOZPZVIVPMZQZDNRANRZPVNANSVHVOWEABCTWDADNVINGZVPNGZPZVJNGZVMPZ
      VQNGZVTPZPZEUABUAZWCWBWHWMWCBEWHWIWKPZVMVTPZPWCWMWHWOWPWCWHWOWPWCQZWFWIWG
      WKWQWFWIPWGWKPPZVLVSMZWCWPWRWSWCVJVQMZPWCVIVJVPVQUBWCWTUCUDCVLVSUEUFUGUHU
      IWIVMWKVTUJUKULWBWJBUAZWLEUAZPWNVNXAWAXBVMBNUMVTENUMUNWJWLBEUOUPUKUQURVNW
      AADNWCVNCVPVKLKZMZBNOWAWCVMXDBNWCVLXCCVIVPVKLUSUTVAXDVTBENWTXCVSCWTVKVRVP
      LVJVQIJVBVCUTVDVEVFVG $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)

    $( The imaginary part of a complex number is unique.  Proposition
       10-1.3 of [Gleason] p. 130. $)
    creui $p |- ( A e. CC -> E! y e. RR E. x e. RR A = ( x + ( i x. y ) ) ) $=
      ( vz vw cc wcel cv ci cmul co caddc wceq cr wrex wa wi wral wreu axcnre
      rexcom sylib wex crut pm3.27 syl6bi eqtr2t syl5 an4s expcom imp3a an4
      syl5ib 19.23advv df-rex anbi12i eeanv bitr4 rgen2a jctir opreq2 opreq2d
      eqeq2d rexbidv opreq1 cbvrexv syl6bb reu4 sylibr ) CFGZCAHZIBHZJKZLKZMZAN
      OZBNOZVPCDHZIEHZJKZLKZMZDNOZPZVLVSMZQZENRBNRZPVPBNSVJVQWGVJVOBNOANOVQABCT
      VOABNNUAUBWFBENVLNGZVSNGZPZVKNGZVOPZVRNGZWBPZPZDUCAUCZWEWDWJWOWEADWJWKWMP
      ZVOWBPZPWEWOWJWQWRWEWQWJWRWEQZWKWHWMWIWSWKWHPWMWIPPZVNWAMZWEWRWTXAVKVRMZW
      EPWEVKVLVRVSUDXBWEUEUFCVNWAUGUHUIUJUKWKVOWMWBULUMUNWDWLAUCZWNDUCZPWPVPXCW
      CXDVOANUOWBDNUOUPWLWNADUQURUMUSUTVPWCBENWEVPCVKVTLKZMZANOWCWEVOXFANWEVNXE
      CWEVMVTVKLVLVSIJVAVBVCVDXFWBADNXBXEWACVKVRVTLVEVCVFVGVHVI $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)
  $}

  $( A real number times the imaginary unit is real only if the number is 0. $)
  rimul $p |- ( ( A e. RR /\ ( i x. A ) e. RR ) -> A = 0 ) $=
    ( cr wcel ci cmul co wa cc0 wceq caddc cc recnt axicn 0cn mulcl axaddcom
    mpan mul01 opreq1i syl5eqr syl adantl wb 0re crut mpanr2 mpanl1 mpbid
    pm3.27d ) ABCZDAEFZBCZGZHUKIZAHIZUMHUKJFZUKDHEFZJFZIZUNUOGZULUSUJULUKKCZUSU
    KLVAUQUKJFZURUPUQKCVAVBURIDHMNOUQUKPQUQHUKJDMRSTUAUBHBCZUJULUSUTUCZUDVCUJGU
    LVCVDUDHAUKHUEUFUGUHUI $.
    $( [11-Mar-2008] $) $( [28-May-1999] $)

  ${
    $( The sequence ` NN ` , ` ZZ ` , ` QQ ` , ` RR ` , and ` CC ` forms
       a chain of proper subsets.  In each case the proper subset
       relationship is shown by demonstrating a number that belongs to
       one set but not the other.  We show that zero belongs to ` ZZ `
       but not ` NN ` , one-half belongs to ` QQ ` but not ` ZZ ` ,
       the square root of 2 belongs to ` RR ` but not ` QQ ` , and finally
       that the imaginary number ` i ` belongs to ` CC ` but not ` RR ` .
       See ~ nthruz for a further refinement. $)
    nthruc $p |- ( ( NN (. ZZ /\ ZZ (. QQ ) /\ ( QQ (. RR /\ RR (. CC ) ) $=
      ( cn cz wpss cq wa cr cc wss cc0 wcel wn nnssz 0z 0nnn pm3.2i ssnelpss
      mp2 c1 c2 cdiv co zssq 1z 2nn znq mp2an halfnz csqr cfv qssre sqr2re
      wnel sqr2irr df-nel mpbi ci axresscn axicn inelr ) ABCZBDCZEDFCZFGCZEUTVA
      ABHIBJZIAJKZEUTLVDVEMNOABIPQBDHRSTUAZDJZVFBJKZEVAUBVGVHRBJSAJVGUCUDRSUEUF
      UGOBDVFPQOVBVCDFHSUHUIZFJZVIDJKZEVBUJVJVKUKVIDULVKUMVIDUNUOODFVIPQFGHUPGJ
      ZUPFJKZEVCUQVLVMURUSOFGUPPQOO $.
      $( [12-Jan-2002] $)
  $}

  ${
    $( The sequence ` NN ` , ` NN0 ` , and ` ZZ ` forms a chain of proper
       subsets.  In each case the proper subset relationship is shown by
       demonstrating a number that belongs to one set but not the other.  We
       show that zero belongs to ` NN0 ` but not ` NN ` and minus one belongs
       to ` ZZ ` but not ` NN0 ` .  This theorem refines the chain of proper
       subsets ~ nthruc . $)
    nthruz $p |- ( NN (. NN0 /\ NN0 (. ZZ ) $=
      ( cn cn0 wpss cz wss cc0 wcel wn wa nnssnn0 0nn0 0nnn pm3.2i ssnelpss
      mp2 c1 cneg nn0ssz 1nn nnnegz ax-mp cr clt wbr 1re renegcl neg0 lt01
      eqbrtr 0re ltnegcon1 mpbir lt0nnn0 mp2an ) ABCZBDCZABEFBGZFAGHZIUOJUQURKL
      MABFNOBDEPQZDGZUSBGHZIUPRUTVAPAGUTSPTUAUSUBGUSFUCUDZVAPUEUFVBFQZPUCUDVCFP
      UCUGUHUIPFUEUJUKULUSUMUNMBDUSNOM $.
      $( [25-Jul-2004] $) $( [9-May-2004] $)
  $}

