$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Imaginary and complex number properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The imaginary unit ` i ` is not zero. $)
  ine0 $p |- -. i = 0 $=
    ( ci cc0 wceq c1 wne wn ax1ne0 df-ne mpbi caddc co cmulc opreq2 axicn
    mulzer1 syl6req opreq1d 1cn addid2 axi2m1 3eqtr3g mto ) ABCZDBCZDBEUDFGDBHI
    UCBDJKAALKZDJKDBUCBUEDJUCUEABLKBABALMANOPQDRSTUAUB $.
    $( [6-May-99] $)

  $( ` i ` -squared is minus 1. $)
  isqm1 $p |- ( i x. i ) = -u 1 $=
    ( ci cmulc co cc0 caddc c1 cneg axicn mulcl 1cn negcl addass axi2m1
    opreq1i negid opreq2i 3eqtr3r addid1 addid2 3eqtr3 ) AABCZDECZDFGZECZUAUCUA
    FECZUCECUAFUCECZECUDUBUAFUCAAHHIZJFJKZLUEDUCEMNUFDUAEFJOPQUAUGRUCUHST $.
    $( [6-May-99] $)

  $( The reciprocal of ` i ` . $)
  irec $p |- ( 1 / i ) = -u i $=
    ( c1 ci cdiv co cneg wceq cmulc axicn mulneg2 isqm1 1cn mulcl negcon2
    mpbir eqtr4 negcl cc0 wne wn ine0 df-ne divmul ) ABCDBEZFBUCGDZAFUDBBGDZEZA
    BBHHIAUFFUEAEFJAUEKBBHHLMNOABUCKHBHPBQRBQFSTBQUANUBN $.
    $( [11-Oct-99] $)

  $( The imaginary unit ` i ` is not a real number. $)
  inelr $p |- -. i e. RR $=
    ( ci cr wcel cc0 wceq ine0 cmulc co clt wbr c1 wn lt01 ax0re ax1re ltnsym
    ax-mp caddc cneg isqm1 negrcl eqeltr ltadd1 1cn addid2 axi2m1 breq12i bitr
    mtbir ltsqt mt3i mto ) ABCZADEZFUMUNDAAGHZIJZUPKDIJZDKIJUQLMDKNOPQUPDKRHZUO
    KRHZIJUQDUOKNUOKSBTKOUAUBOUCURKUSDIKUDUEUFUGUHUIAUJUKUL $.
    $( [6-May-99] $)

  ${
    cru.1 $e |- A e. RR $.
    cru.2 $e |- B e. RR $.
    cru.3 $e |- C e. RR $.
    cru.4 $e |- D e. RR $.
    $( The real representation of complex numbers is unique.  Lemma for
       Proposition 10-1.3 of [Gleason] p. 130. $)
    crulem $p |- ( ( A + ( B x. i ) ) = ( C + ( D x. i ) ) -> B = D ) $=
      ( ci cmulc co caddc wceq cmin cc0 cr wcel inelr wne wn wi wa cneg cdiv
      negrcl addrcl recn subcl axicn divmulz opreq1 mulcl negcl add4 negid
      opreq1i addcl addid2 3eqtr opreq2i addid1 3eqtr3g subdistr subneg eqtr
      syl5eq syl5bir imp eleq1d subrcl divrclz syl5bi exp pm2.43b df-ne
      syl5ibr mt3i subeq0 sylib ) ABIJKZLKZCDIJKZLKZMZBDNKZOMZBDMWDWFIPQZRWDWEO
      SZWGWFTWDWHWGWHWDWHWGUAWHWDUBZCAUCZLKZWEUDKZPQWGWHWIWLIPWHWDWLIMZWHWMWEIJ
      KZWKMWDWKWEIWKCWJGAEUEZUFZUGZBDBFUGZDHUGZUHUIUJWDVTWBUCZLKZWKWNWDWAWJWTLK
      ZLKZWCXBLKZXAWKWAWCXBLUKXCAWJLKZXALKOXALKXAAVTWJWTAEUGZBIWRUIULZWJWOUGZWB
      DIWSUIULZUMZUNXEOXALAXFUOUPXAVTWTXGXJUQURUSXDWKWBWTLKZLKWKOLKWKCWBWJWTCGU
      GXIXHXJUNXKOWKLWBXIUOUTWKWQVAUSVBWNVTWBNKXABDIWRWSUIVCVTWBXGXIVDVEVFVGVHV
      IWKWEWPBDFHVJVKVLVMVNWEOVOVPVQBDWRWSVRVS $.
      $( [8-May-99] $)

    $( The real representation of complex numbers is unique.  Proposition
       10-1.3 of [Gleason] p. 130. $)
    cru $p |- ( ( A + ( B x. i ) ) = ( C + ( D x. i ) ) ->
              ( A = C /\ B = D ) ) $=
      ( ci cmulc co caddc wceq crulem opreq1 opreq2d cleq2d recn axicn mulcl
      addcom cleq12i addcan biimp sylbi syl6bir mpcom jca ) ABIJKZLKZCDIJKZLKZM
      ZACMZBDMZUOUMUNABCDEFGHNZUOUMUJCUILKZMZUNUOUQULUJUOUIUKCLBDIJOPQURUIALKZU
      ICLKZMZUNUJUSUQUTAUIAERZBIBFRSTZUACUICGRZVCUAUBVAUNUIACVCVBVDUCUDUEUFUGUP
      UH $.
      $( [9-May-99] $)

    $( Multiplication rule for complex number representation.  Remark of
       [Apostol] p. 361. $)
    crmult $p |- ( ( A + ( B x. i ) ) x. ( C + ( D x. i ) ) ) =
     ( ( ( A x. C ) - ( B x. D ) ) + ( ( ( A x. D ) + ( B x. C ) ) x. i ) ) $=
      ( ci cmulc co caddc cneg cmin mulrcl recn axicn mulcl negrcl add4 distr1
      mulass opreq2i eqtr4 mul23 c1 mul4 isqm1 1cn negcl mulcom mulneg1 mulid2
      negeqi 3eqtr opreq12i addcom distr2 3eqtr4 addcl subneg opreq1i ) ACDIJKZ
      LKZJKZBIJKZVDJKZLKZACJKZBDJKZMZLKZADJKZBCJKZLKIJKZLKZAVFLKVDJKVIVJNKZVOLK
      VIVMIJKZLKZVKVNIJKZLKZLKVLVRVTLKZLKVHVPVIVRVKVTVIACEGOPZVMIVMADEHOPZQRVKV
      JBDFHOZSPZVNIVNBCFGOPZQRZTVEVSVGWALVEVIAVCJKZLKVSACVCAEPZCGPZDIDHPZQRZUAV
      RWIVILADIWJWLQUBUCUDVGVFCJKZVFVCJKZLKVTVKLKWAVFCVCBIBFPZQRZWKWMUAWNVTWOVK
      LBICWPQWKUEWOVJIIJKZJKVJUFMZJKZVKBIDIWPQWLQUGWRWSVJJUHUCWTWSVJJKUFVJJKZMV
      KVJWSVJWEPZUFUIUJUKUFVJUIXBULXAVJVJXBUMUNUOUOUPVTVKWHWFUQUOUPVOWBVLLVMVNI
      WDWGQURUCUSAVFVDWJWQCVCWKWMUTURVQVLVOLVIVJWCXBVAVBUS $.
      $( [11-May-99] $)
  $}

  $( The real representation of complex numbers is unique.  Proposition
     10-1.3 of [Gleason] p. 130. $)
  crut $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
             ( ( A + ( B x. i ) ) = ( C + ( D x. i ) ) ->
             ( A = C /\ B = D ) ) ) $=
    ( cr wcel ci cmulc co caddc wceq wa wi cc0 cded opreq1 cleq1d cleq1 birand
    bi2imd opreq2d biland cleq2d cleq2 ax0re elimel cru dedth4h ) AEFZBEFZCEFZD
    EFZABGHIZJIZCDGHIZJIZKZACKZBDKZLZMABCDNNUIANOZUMJIZUPKZVACKZUSLZMVAUJBNOZGH
    IZJIZUPKZVDVFDKZLZMVHUKCNOZUOJIZKZVAVLKZVJLZMVHVLULDNOZGHIZJIZKZVOVFVQKZLZM
    NNAVAKZUQVCUTVEWCUNVBUPAVAUMJPQWCURVDUSAVACRSTBVFKZVCVIVEVKWDVBVHUPWDUMVGVA
    JBVFGHPUAQWDUSVJVDBVFDRUBTCVLKZVIVNVKVPWEUPVMVHCVLUOJPUCWEVDVOVJCVLVAUDSTDV
    QKZVNVTVPWBWFVMVSVHWFUOVRVLJDVQGHPUAUCWFVJWAVODVQVFUDUBTVAVFVLVQANEUEUFBNEU
    EUFCNEUEUFDNEUEUFUGUH $.
    $( [9-May-99] $)

  ${
    $d x y z w A $.  $( ` z w ` are dummy. $)
    $( The real part of a complex number is unique.  Proposition
       10-1.3 of [Gleason] p. 130. $)
    creur $p |- ( A e. CC -> E! x e. RR E. y e. RR A = ( x + ( y x. i ) ) ) $=
      ( vz vw cc wcel cv ci cmulc co caddc wceq cr wrex wa weq wi wral wreu
      axcnre wex crut pm3.26 syl6 cleqid cleq12 mpbii syl5 an4s exp imp3a an4
      syl5ib 19.23advv df-rex bi2an eeanv bitr4 rgen2 jctir opreq1 cleq2d
      birexdv opreq2d cbvrexv syl6bb reu4 sylibr ) CFGZCAHZBHZIJKZLKZMZBNOZANOZ
      VPCDHZEHZIJKZLKZMZENOZPZADQZRZDNSANSZPVPANTVJVQWGABCUAWFADNVKNGZVRNGZPZVL
      NGZVOPZVSNGZWBPZPZEUBBUBZWEWDWJWOWEBEWJWKWMPZVOWBPZPWEWOWJWQWRWEWJWQWRWER
      ZWHWKWIWMWSWHWKPWIWMPPZVNWAMZWEWRWTXAWEBEQZPWEVKVLVRVSUCWEXBUDUEWRCCMXACU
      FCVNCWAUGUHUIUJUKULWKVOWMWBUMUNUOWDWLBUBZWNEUBZPWPVPXCWCXDVOBNUPWBENUPUQW
      LWNBEURUSUNUTVAVPWCADNWEVPCVRVMLKZMZBNOWCWEVOXFBNWEVNXECVKVRVMLVBVCVDXFWB
      BENXBXEWACXBVMVTVRLVLVSIJVBVEVCVFVGVHVI $.
      $( [9-May-99] $)

    $( The imaginary part of a complex number is unique.  Proposition
       10-1.3 of [Gleason] p. 130. $)
    creui $p |- ( A e. CC -> E! y e. RR E. x e. RR A = ( x + ( y x. i ) ) ) $=
      ( vz vw cc wcel cv ci cmulc co caddc wceq cr wrex wa weq wi wral wreu
      axcnre rexcom sylib wex crut pm3.27 syl6 cleqid cleq12 mpbii syl5 an4s
      exp com12 imp3a an4 syl5ib 19.23advv df-rex bi2an eeanv bitr4 rgen2
      jctir opreq1 opreq2d cleq2d birexdv cbvrexv syl6bb reu4 sylibr ) CFGZCAHZ
      BHZIJKZLKZMZANOZBNOZVSCDHZEHZIJKZLKZMZDNOZPZBEQZRZENSBNSZPVSBNTVMVTWJVMVR
      BNOANOVTABCUAVRABNNUBUCWIBENVONGZWBNGZPZVNNGZVRPZWANGZWEPZPZDUDAUDZWHWGWM
      WRWHADWMWNWPPZVRWEPZPWHWRWMWTXAWHWTWMXAWHRZWTWMXBWNWKWPWLXBWNWKPWPWLPPZVQ
      WDMZWHXAXCXDADQZWHPWHVNVOWAWBUEXEWHUFUGXACCMXDCUHCVQCWDUIUJUKULUMUNUOWNVR
      WPWEUPUQURWGWOAUDZWQDUDZPWSVSXFWFXGVRANUSWEDNUSUTWOWQADVAVBUQVCVDVSWFBENW
      HVSCVNWCLKZMZANOWFWHVRXIANWHVQXHCWHVPWCVNLVOWBIJVEVFVGVHXIWEADNXEXHWDCVNW
      AWCLVEVGVIVJVKVL $.
      $( [9-May-99] $)
  $}

  $( A real number times the imaginary unit is real only if the number is 0. $)
  rimul $p |- ( ( A e. RR /\ ( A x. i ) e. RR ) -> A = 0 ) $=
    ( cr wcel ci cmulc co wa cc0 wceq caddc cc recnt 0cn axicn mulcl axaddcom
    mpan mulzer2 opreq1i syl5eqr syl adantl wi ax0re crut mpan22 mpan11 mpd
    pm3.27d ) ABCZADEFZBCZGZHUKIZAHIZUMHUKJFZUKHDEFZJFZIZUNUOGZULUSUJULUKKCZUSU
    KLVAUQUKJFZURUPUQKCVAVBURIHDMNOUQUKPQUQHUKJDNRSTUAUBHBCZUJULUSUTUCZUDVCUJGU
    LVCVDUDHAUKHUEUFUGUHUI $.
    $( [28-May-99] $)

