$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Subtraction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c d x y z A $.  $d x B $.  $d x C $.
    $( Lemma for ~ cnegex . $)
    cnegexlem1 $p |- ( ( A e. RR /\ B e. CC /\ C e. CC ) ->
                  ( ( A + B ) = ( A + C ) <-> B = C ) ) $=
      ( vx cr wcel cc w3a caddc co wceq cv cc0 wrex wi axrnegex 3ad2ant1 wa
      addass pm3.27 simpll simplrl syl3anc simplrr eqeq12d opreq2 syl5bir
      adantr wb addcom eqeq1d adantlr opreq1 adantl addid2 eqeqan12d ad2antrr
      bitrd ex sylbid imp sylibd recn sylan2 r19.23adva 3impb syl3an1 mpd
      impbid1 ) AEFZBGFZCGFZHZABIJZACIJZKZBCKZVMADLZIJZMKZDENZVPVQOZVJVKWAVLDAP
      QAGFZVKVLWAWBOZVJWCVKVLWDWCVKVLRZRZVTWBDEWFVRGFZVTWBOVREFWFWGRZVTWBWHVTRV
      PVRAIJZBIJZWICIJZKZVQWHVPWLOVTWHWLVRVNIJZVRVOIJZKVPWHWJWMWKWNWGWCVKWJWMKW
      HVRABSWFWGTZWCWEWGUAZWCVKVLWGUBUCWGWCVLWKWNKWHVRACSWOWPWCVKVLWGUDUCUEVNVO
      VRIUFUGUHWHVTWLVQUIZWHVTWIMKZWQWCWGVTWRUIWEWCWGRVSWIMAVRUJUKULWHWRWQWHWRR
      WLMBIJZMCIJZKZVQWRWLXAUIWHWRWJWSWKWTWIMBIUMWIMCIUMUEUNWFXAVQUIZWGWRWEXBWC
      VKVLWSBWTCBUOCUOUPUNUQURUSUTVAVBUSVRVCVDVEVFAVCVGVHBCAIUFVI $.
      $( [22-May-2007] $)

    $( Lemma for ~ cnegex . $)
    cnegexlem2 $p |- E. y e. RR ( _i x. y ) e. RR $=
      ( vx vz cc0 cv ci cmul co caddc wceq cr wrex wcel cc 0cn axcnre ax-mp wa
      wi axrnegex adantr w3a addid2 3ad2ant3 opreq1 ad2antrl add23 3com23
      eqcomd sylan9eq adantrl 3ad2ant2 3eqtr3d eqtr3d ex recn mulcl axicn
      sylancr syl3an 3expa imp simplr eqeltrrd exp32 r19.23adva mpd reximdva
      r19.23aiv ) DBEZFAEZGHZIHZJZAKLZBKLZVLKMZAKLZDNMVPOBADPQVOVRBKVJKMZVNVQAK
      VSVKKMZRZVJCEZIHZDJZCKLZVNVQSZVSWEVTCVJTUAWAWDWFCKWAWBKMZRZWDVNVQWHWDVNRZ
      RWBVLKWHWIWBVLJZVSVTWGWIWJSZVJNMZVLNMZWBNMZWKVSVTWGWLWMWNUBZWIWJWOWIRZDWB
      IHZWBVLWOWQWBJZWIWNWLWRWMWBUCUDUAWPWCVLIHZDVLIHZWQVLWDWSWTJWOVNWCDVLIUEUF
      WOVNWSWQJWDWOVNWSVMWBIHZWQWLWNWMWSXAJVJWBVLUGUHVNWQXADVMWBIUEUIUJUKWOWTVL
      JZWIWMWLXBWNVLUCULUAUMUNUOVJUPFNMVKNMWMVTFVKUQURVKUPUSWBUPUTVAVBWAWGWIVCV
      DVEVFVGVHVIQ $.
      $( [11-Mar-2008] $) $( [22-May-2007] $)

    $( Lemma for ~ cnegex . $)
    cnegexlem3 $p |- ( ( b e. RR /\ y e. RR ) -> E. c e. RR ( b + c ) = y ) $=
      ( vx cv cr wcel wa caddc co cc0 wceq wrex axrnegex adantl readdcl syl
      adantlr adantr cc wb add23 3expa addcom addcl sylan an1rs eqtr2d recn
      sylanl2 adantllr ancoms sylan2 id sylan9eq adantlll eqeq12d cnegexlem1
      simplr simpllr syl3anc bitr3d anim12i anim1i syl2an rexbidva mpbid ex
      r19.23adva mpd ) BEZFGZAEZFGZHZVMDEZIJZKLZDFMZVKCEZIJZVMLZCFMZVNVSVLDVMNO
      VOVRWCDFVOVPFGZHZVRWCWEVRHZVKVPIJZVTIJZKLZCFMZWCWEWJVRVLWDWJVNVLWDHWGFGWJ
      VKVPPCWGNQRSWFWIWBCFVKTGZVMTGZHZWDHZVRHZVTTGZWIWBUAWFVTFGWOWPHZVPWAIJZVPV
      MIJZLZWIWBWQWRWHWSKWNWPWRWHLZVRWKWDWPXAWLWKVPTGZWPXAWDWKXBHWPHWHWAVPIJZWR
      WKXBWPWHXCLVKVPVTUBUCWKWPXBXCWRLZWATGZXBXDWKWPHWAVPUDVKVTUEZUFUGUHVPUIZUJ
      UKRWOWSKLZWPWLWDVRXHWKWLWDHVRWSVQKWLXBWSVQLZWDXBWLXIVPVMUDULXGUMVRUNUOUPS
      UQWNWPWTWBUAZVRWDXEWLXJWNWPHVPWAVMURWMWDWPUSWMWPXEWDWKWPXEWLXFRRWKWLWDWPU
      TVARVBWEWNVRVOWMWDVLWKVNWLVKUIVMUIVCVDVDVTUIVEVFVGVHVIVJ $.
      $( [22-May-2007] $)

    $( Existence of the negative of a complex number.  (Contributed by Eric
       Schmidt, 21-May-2007.) $)
    cnegex $p |- ( A e. CC -> E. x e. CC ( A + x ) = 0 ) $=
      ( va vb vy vc vd cc wcel cv ci cmul co caddc wceq cr wrex cc0 axcnre wa
      opreq1 eqeq1d rexbidv cnegexlem2 cnegexlem3 ad2ant2lr readdcl axrnegex
      syl ad2ant2rl adantr opreq2 rcla4ev addcl mulcl axicn recn sylancr
      syl2an adantlr adantll addass mpan sylan2 ad2antrr ad2antlr pm3.27
      syl3anc simpll adantl adddi mp3an1 opreq2d eqtr4d opreq1d eqtr3d anim12i
      adantlrr eqtr2d adantllr biimpa sylanc exp31 r19.23adv mpd exp32 mpi
      syl5cbir r19.23aivv ) BHIBCJZKDJZLMZNMZOZDPQCPQBAJZNMZROZAHQZCDBSWNWRCDPP
      WNWRWMWONMZROZAHQZWJPIZWKPIZTZWNWQWTAHWNWPWSRBWMWONUAUBUCXDKEJZLMZPIZEPQX
      AEUDXDXGXAEPXDXEPIZXGXAXDXHXGTZTZWKFJZNMZXEOZFPQZXAXCXHXNXBXGEDFUEUFXJXMX
      AFPXJXKPIZXMXAXJXOXMTZTZWJXFNMZGJZNMZROZGPQZXAXJYBXPXBXGYBXCXHXBXGTXRPIYB
      WJXFUGGXRUHUIUJUKXQYAXAGPXQXSPIZYAXAKXKLMZXSNMZHIZWMYENMZROZXAXQYCTZYATWT
      YHAYEHWOYEOWSYGRWOYEWMNULUBUMYIYFYAXPYCYFXJXOYCYFXMYDHIZXSHIZYFXOYCYDXSUN
      KHIZXKHIZYJXOKXKUOZUPXKUQZURXSUQZUSUTVAUKYIYAYHYIXTYGRXDXPYCXTYGOXIXDXPTY
      CTYGWJKXLLMZNMZXSNMZXTXDXOYCYGYSOZXMWJHIZWKHIZTZYMTZYKYTXDXOTYCUUDYKTZWMY
      DNMZXSNMZYGYSWMHIZYJYKUUGYGOUUEWMYDXSVBUUCUUHYMYKUUAWLHIZUUHUUBWJWLUNYLUU
      BUUIUPKWKUOVCZVDVEYMYJUUCYKYLYMYJUPYNVCZVFUUDYKVGVHUUEUUFYRXSNUUDUUFYROYK
      UUDUUFWJWLYDNMZNMZYRUUAUUIYJUUFUUMOUUDWJWLYDVBUUAUUBYMVIUUBUUIUUAYMUUJVFY
      MYJUUCUUKVJVHUUDYQUULWJNUUBYMYQUULOZUUAYLUUBYMUUNUPKWKXKVKVLVAVMVNUKVOVPX
      DUUCXOYMXBUUAXCUUBWJUQWKUQVQYOVQYPUSVRXPYSXTOZXDYCXMUUOXOXMYRXRXSNXMYQXFW
      JNXLXEKLULVMVOVJVFVSVTUBWAWBWCWDWEWFWDWEWFWDWGWHWIUI $.
      $( [11-Mar-2008] $) $( [22-May-2007] $)
  $}

  ${
    $d x A $.
    negex.1 $e |- A e. CC $.
    $( Existence of negatives. $)
    cnegexi $p |- E. x e. CC ( A + x ) = 0 $=
      ( cc wcel cv caddc co cc0 wceq wrex cnegex ax-mp ) BDEBAFGHIJADKCABLM $.
      $( [23-Nov-1994] $)
  $}

  $( 0 is a complex number.  (Proved without referencing ~ ax1cn by Eric
     Schmidt, 11-Apr-2007.  Compare ~ 0cn .) $)
  0cnALT $p |- 0 e. CC $=
    ( vx ci cv caddc co cc0 wceq cc wrex wcel axicn cnegex ax-mp wa addcl mpan
    adantr wb eleq1 adantl mpbid r19.23aiva ) BACZDEZFGZAHIZFHJZBHJZUFKABLMUEUG
    AHUCHJZUENUDHJZUGUIUJUEUHUIUJKBUCOPQUEUJUGRUIUDFHSTUAUBM $.
    $( [12-Apr-2007] $) $( [19-Feb-2005] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    addcan.1 $e |- A e. CC $.
    addcan.2 $e |- B e. CC $.
    addcan.3 $e |- C e. CC $.
    $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18.  (The
       proof was shortened by Andrew Salmon, 22-Oct-2011.) $)
    addcani $p |- ( ( A + B ) = ( A + C ) <-> B = C ) $=
      ( vx caddc co wceq cv cc0 cc wrex wi cnegexi wcel wa add23 mp3an12
      eqeq12d opreq1 sylan9bb addid2i eqeq12i biimpi syl6bi syl5 r19.23aiva
      ax-mp opreq2 impbii ) ABHIZACHIZJZBCJZAGKZHIZLJZGMNUOUPOZGADPUSUTGMUQMQZU
      SRZUMUQHIZUNUQHIZJZUPUOVBVELBHIZLCHIZJZUPVAVEURBHIZURCHIZJUSVHVAVCVIVDVJA
      MQZBMQVAVCVIJDEABUQSTVKCMQVAVDVJJDFACUQSTUAUSVIVFVJVGURLBHUBURLCHUBUAUCVH
      UPVFBVGCBEUDCFUDUEUFUGUMUNUQHUBUHUIUJBCAHUKUL $.
      $( [22-Oct-2011] $) $( [27-Oct-1999] $)

    $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18. $)
    addcaniOLD $p |- ( ( A + B ) = ( A + C ) <-> B = C ) $=
      ( vx caddc co wceq cv cc0 cc wrex wi cnegexi wcel wa wb addass mp3an3
      eqeq12d mpan2 opreq2 syl5bir adantr addcom mpan eqeq1d opreq1 addid2i
      syl6eq syl6bi imp sylibd r19.23aiva ax-mp impbii ) ABHIZACHIZJZBCJZAGKZHI
      ZLJZGMNVAVBOZGADPVEVFGMVCMQZVERVAVCAHIZBHIZVHCHIZJZVBVGVAVKOVEVGVKVCUSHIZ
      VCUTHIZJZVAVGAMQZVKVNSDVGVORVIVLVJVMVGVOBMQVIVLJEVCABTUAVGVOCMQVJVMJFVCAC
      TUAUBUCUSUTVCHUDUEUFVGVEVKVBSZVGVEVHLJZVPVGVDVHLVOVGVDVHJDAVCUGUHUIVQVIBV
      JCVQVILBHIBVHLBHUJBEUKULVQVJLCHICVHLCHUJCFUKULUBUMUNUOUPUQBCAHUDUR $.
      $( [27-Oct-1999] $)
  $}

  $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18.  This
     proof illustrates how ~ dedth3h can be used to convert the assumptions of
     ~ addcani into antecedents.  This general method can be used to convert
     deductions into theorems as needed. $)
  addcan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) = ( A + C ) <-> B = C ) ) $=
    ( cc wcel caddc co wceq wb cc0 cif opreq1 eqeq12d bibi1d opreq2 eqeq1d
    eqeq1 bibi12d eqeq2d eqeq2 0cn elimel addcani dedth3h ) ADEZBDEZCDEZABFGZAC
    FGZHZBCHZIUEAJKZBFGZULCFGZHZUKIULUFBJKZFGZUNHZUPCHZIUQULUGCJKZFGZHZUPUTHZIA
    BCJJJAULHZUJUOUKVDUHUMUIUNAULBFLAULCFLMNBUPHZUOURUKUSVEUMUQUNBUPULFOPBUPCQR
    CUTHZURVBUSVCVFUNVAUQCUTULFOSCUTUPTRULUPUTAJDUAUBBJDUAUBCJDUAUBUCUD $.
    $( [22-Nov-1994] $)

  $( Cancellation law for addition. $)
  addcan2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + C ) = ( B + C ) <-> A = B ) ) $=
    ( cc wcel caddc co wceq wb w3a addcom 3adant3 3adant2 eqeq12d addcan
    bitr3d 3coml ) CDEZADEZBDEZACFGZBCFGZHZABHZIRSTJZCAFGZCBFGZHUCUDUEUFUAUGUBR
    SUFUAHTCAKLRTUGUBHSCBKMNCABOPQ $.
    $( [23-Aug-2004] $) $( [30-Jul-2004] $)

  ${
    addcan2.1 $e |- A e. CC $.
    addcan2.2 $e |- B e. CC $.
    addcan2.3 $e |- C e. CC $.
    $( Cancellation law for addition. $)
    addcan2i $p |- ( ( A + C ) = ( B + C ) <-> A = B ) $=
      ( cc wcel caddc co wceq wb addcan2 mp3an ) AGHBGHCGHACIJBCIJKABKLDEFABCMN
      $.
      $( [14-May-2003] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    negeu.1 $e |- A e. CC $.
    negeu.2 $e |- B e. CC $.
    $( Existential uniqueness of negatives.  Theorem I.2 of [Apostol] p. 18. $)
    negeui $p |- E! x e. CC ( A + x ) = B $=
      ( vy cv caddc co wceq cc wreu wrex wa wi wral opreq2 eqeq1d reu4 cc0
      cnegexi wcel addcl mpan2 risset sylib addass mp3an13 eqcomd sylan9eqr
      opreq1 addid2i syl6eq exp32 impcom a1d r19.21aiv ex rexim syl6 mpid
      r19.23aiv ax-mp w3a addcan eqtr3 syl5bi mp3an1 rgen2a mpbir2an ) BAGZHIZC
      JZAKLVMAKMZVMBFGZHIZCJZNZVKVOJZOZFKPAKPVMVQAFKVSVLVPCVKVOBHQRSVPTJZFKMVNF
      BDUAWAVNFKVOKUBZWAVKVOCHIZJZAKMZVNWBWCKUBZWEWBCKUBZWFEVOCUCUDAWCKUEUFWBWA
      WDVMOZAKPZWEVNOWBWAWIWBWANZWHAKWJWHVKKUBZWAWBWHWAWBWDVMWBWDNWAVLVPCHIZCWD
      WBVLBWCHIZWLVKWCBHQWBWLWMBKUBZWBWGWLWMJDEBVOCUGUHUIUJWAWLTCHICVPTCHUKCEUL
      UMUJUNUOUPUQURWDVMAKUSUTVAVBVCVTAFKWNWKWBVTDWNWKWBVDVLVPJVSVRBVKVOVEVLVPC
      VFVGVHVIVJ $.
      $( [22-Nov-1994] $)
  $}

  ${
    $d x y z w $.
    $( Define subtraction.  Theorem ~ subval shows it value (and describes how
       this definition works), theorem ~ subaddi relates it to addition, and
       theorems ~ subcli and ~ resubcli prove its closure laws. $)
    df-sub $a |- - = { <. <. x , y >. , z >. |
                   ( ( x e. CC /\ y e. CC ) /\
                   z = U. { w e. CC | ( y + w ) = x } ) } $.
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Value of subtraction, which is the (unique) element ` x ` such that
       ` B + x = A ` .  The notation ` U. { x e. CC | ( B + x ) = A } ` may at
       first seem cryptic but is actually a way of saying "the element ` x `
       such that ` B + x = A ` " (see Theorem 8.17 of [Quine] p. 56); this
       works because there is only one such ` x ` as shown by ~ negeui ,
       allowing us to exploit ~ euabsn and ~ unisn (which you will find if you
       trace back the proof of ~ subcli ). $)
    subval $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( A - B ) = U. { x e. CC | ( B + x ) = A } ) $=
      ( vy vz vw cc cv caddc co wceq crab cuni cmin axcnex rabex uniex eqeq2
      rabbidv unieqd opreq1 eqeq1d df-sub oprabval2 ) DEFBCGGEHZAHZIJZDHZKZAGLZ
      MCUFIJZBKZAGLZMNUGBKZAGLZMUMULAGOPQUHBKZUJUOUPUIUNAGUHBUGRSTUECKZUOUMUQUN
      ULAGUQUGUKBUECUFIUAUBSTDEFAUCUD $.
      $( [4-Aug-2007] $)
  $}

  $( Define the negative of a number (unary minus).  We use different symbols
     for unary minus ( ` -u ` ) and subtraction ( ` - ` ) to prevent syntax
     ambiguity.  See ~ cneg for a discussion of this. $)
  df-neg $a |- -u A = ( 0 - A ) $.

  $( Equality theorem for negatives. $)
  negeq $p |- ( A = B -> -u A = -u B ) $=
    ( wceq cc0 cmin co cneg opreq2 df-neg 3eqtr4g ) ABCDAEFDBEFAGBGABDEHAIBIJ
    $.
    $( [10-Feb-1995] $)

  ${
    negeqi.1 $e |- A = B $.
    $( Equality inference for negatives. $)
    negeqi $p |- -u A = -u B $=
      ( wceq cneg negeq ax-mp ) ABDAEBEDCABFG $.
      $( [14-Feb-1995] $)
  $}

  ${
    negeqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for negatives. $)
    negeqd $p |- ( ph -> -u A = -u B ) $=
      ( wceq cneg negeq syl ) ABCEBFCFEDBCGH $.
      $( [14-May-1999] $)
  $}

  ${
    $d y A $.  $d x y $.
    hbneg.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for the negative of a complex
       number. $)
    hbneg $p |- ( y e. -u A -> A. x y e. -u A ) $=
      ( cneg cc0 cmin co df-neg cv wcel ax-17 hbopr hbxfr ) ABCEFCGHCIABFCGBJZF
      KALOGKALDMN $.
      $( [12-Jun-2005] $)
  $}

  ${
    $d y z A $.  $d y ph $.  $d x y z $.
    hbnegd.1 $e |- ( ph -> A. x ph ) $.
    hbnegd.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    $( Deduction version of ~ hbneg .  (The proof was shortened by Andrew
       Salmon, 22-Oct-2011.) $)
    hbnegd $p |- ( ph -> ( y e. -u A -> A. x y e. -u A ) ) $=
      ( cv cc0 cmin co wcel wal cneg wi ax-17 a1i hboprd df-neg eleq2i albii
      3imtr4g ) ACGZHDIJZKZUDBLUBDMZKZUFBLABCHDIEUBHKZUGBLNAUGBOPUBIKZUHBLNAUHB
      OPFQUEUCUBDRSZUFUDBUITUA $.
      $( [22-Oct-2011] $) $( [29-Feb-2008] $)

    $( Deduction version of ~ hbneg . $)
    hbnegdOLD $p |- ( ph -> ( y e. -u A -> A. x y e. -u A ) ) $=
      ( vz cv wcel wal cab cneg wi hba1 hbab hbneg a1i wceq 19.21aiv abidhb
      syl negeqd eleq2d albid 3imtr3d ) ACHZGHDIZBJZGKZLZIZUKBJZUFDLZIZUNBJUKUL
      MABCUIUHBGCUGBNOPQAUJUMUFAUIDAUFDIZUOBJMZCJUIDRAUPCFSBCGDTUAUBUCZAUKUNBEU
      QUDUE $.
      $( [2-Mar-2008] $) $( [29-Feb-2008] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d y z C $.  $d x y z $.
    $( Move class substitution in and out of the negative of a number.  (The
       proof was shortened by Andrew Salmon, 22-Oct-2011.) $)
    csbnegg $p |- ( A e. C -> [_ A / x ]_ -u B = -u [_ A / x ]_ B ) $=
      ( wcel cc0 cmin co csb cneg csbopr2g df-neg csbeq2i wceq a1i 3eqtr4d ) BD
      EZABFCGHZIFABCIZGHZABCJZISJZABFCDGKABUARDCLMUBTNQSLOP $.
      $( [22-Oct-2011] $) $( [1-Mar-2008] $)

    $( Move class substitution in and out of the negative of a number. $)
    csbneggOLD $p |- ( A e. C -> [_ A / x ]_ -u B = -u [_ A / x ]_ B ) $=
      ( vy vz wcel cv cneg csb ax-17 hbcsb1g hbnegd wceq csbeq1a negeqd wex
      a9e visset hbcsb1 hbneg hbeq eqtr3d 19.23ai ax-mp syl5eq csbiegf csbcog
      3eqtr3d ) BDGZEBAEHZCIZJZJEBAUKCJZJZIZABULJABCJZIEFBUMUPDUJEFUOUJEKEFBUND
      FHZBGEKLMUKBNZUNIZUPUMUSUNUOEBUNOPAHUKNZAQUMUTNZAERVAVBAAFFUMUTAFUKULESZU
      RUKGAKZTAFUNAFUKCVCVDTUAUBVAULUMUTAUKULOVACUNAUKCOPUCUDUEUFUGAEBULDUHUJUO
      UQAEBCDUHPUI $.
      $( [23-Mar-2008] $) $( [1-Mar-2008] $)
  $}

  $( A negative is a set. $)
  negex $p |- -u A e. _V $=
    ( cneg cc0 cmin co cvv df-neg oprex eqeltri ) ABCADEFAGCADHI $.
    $( [27-May-2005] $) $( [4-Apr-2005] $)

  ${
    $d x A $.  $d x B $.
    subcl.1 $e |- A e. CC $.
    subcl.2 $e |- B e. CC $.
    $( Closure law for subtraction. $)
    subcli $p |- ( A - B ) e. CC $=
      ( vx cmin co cv caddc wceq cc crab cuni wcel subval mp2an wreu negeui
      reucl ax-mp eqeltri ) ABFGZBEHIGAJZEKLMZKAKNBKNUBUDJCDEABOPUCEKQUDKNEBADC
      RUCEKSTUA $.
      $( [26-Nov-1994] $)
  $}

  $( Closure law for subtraction. $)
  subcl $p |- ( ( A e. CC /\ B e. CC ) -> ( A - B ) e. CC ) $=
    ( cc wcel cmin co cc0 cif wceq opreq1 eleq1d opreq2 0cn elimel subcli
    dedth2h ) ACDZBCDZABEFZCDQAGHZBEFZCDTRBGHZEFZCDABGGATISUACATBEJKBUBIUAUCCBU
    BTELKTUBAGCMNBGCMNOP $.
    $( [10-May-1999] $)

  $( Closure law for negative. $)
  negcl $p |- ( A e. CC -> -u A e. CC ) $=
    ( cc wcel cc0 cmin co cneg 0cn subcl mpan df-neg syl5eqel ) ABCZDAEFZBAGDBC
    MNBCHDAIJAKL $.
    $( [6-Aug-2003] $)

  ${
    negcl.1 $e |- A e. CC $.
    $( Closure law for negative. $)
    negcli $p |- -u A e. CC $=
      ( cc wcel cneg negcl ax-mp ) ACDAECDBAFG $.
      $( [26-Nov-1994] $)
  $}

  ${
    $d w x y z $.
    $( Subtraction is an operation on the complex numbers. $)
    subopr $p |- - : ( CC X. CC ) --> CC $=
      ( vx vy vz vw cc cv caddc co wceq crab cuni cmin df-sub wcel wa subval
      subcl eqeltrrd foprab ) ABCEEBFZDFGHAFZIDEJKZELABCDMUAENTENOUATLHUBEDUATP
      UATQRS $.
      $( [9-Aug-2007] $) $( [4-Aug-2007] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    subadd.1 $e |- A e. CC $.
    subadd.2 $e |- B e. CC $.
    subadd.3 $e |- C e. CC $.
    $( Relationship between subtraction and addition. $)
    subaddi $p |- ( ( A - B ) = C <-> ( B + C ) = A ) $=
      ( vx cc wcel cmin co wceq caddc wb cv eqeq2 opreq2 eqeq1d bibi12d crab
      cuni wreu negeui reuuni1 mpan2 subval mp2an eqeq1i syl6rbbr vtoclga
      ax-mp ) CHIABJKZCLZBCMKZALZNZFULGOZLZBUQMKZALZNUPGCHUQCLZURUMUTUOUQCULPVA
      USUNAUQCBMQRSUQHIZUTUTGHTUAZUQLZURVBUTGHUBUTVDNGBAEDUCUTGHUDUEULVCUQAHIBH
      IULVCLDEGABUFUGUHUIUJUK $.
      $( [26-Nov-1994] $)

    ${
      subaddri.4 $e |- ( B + C ) = A $.
      $( Relationship between subtraction and addition. $)
      subaddrii $p |- ( A - B ) = C $=
        ( cmin co wceq caddc subaddi mpbir ) ABHICJBCKIAJGABCDEFLM $.
        $( [19-Dec-2006] $) $( [16-Dec-2006] $)
    $}

    $( Relationship between subtraction and addition. $)
    subadd2i $p |- ( ( A - B ) = C <-> ( C + B ) = A ) $=
      ( cmin co wceq caddc subaddi addcomi eqeq1i bitri ) ABGHCIBCJHZAICBJHZAIA
      BCDEFKOPABCEFLMN $.
      $( [15-Dec-2006] $)

    $( Swap subtrahend and result of subtraction. $)
    subsub23i $p |- ( ( A - B ) = C <-> ( A - C ) = B ) $=
      ( caddc co wceq cmin addcomi eqeq1i subaddi 3bitr4i ) BCGHZAICBGHZAIABJHC
      IACJHBIOPABCEFKLABCDEFMACBDFEMN $.
      $( [7-Oct-1999] $)
  $}

  $( Relationship between subtraction and addition. $)
  subadd $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) = C <-> ( B + C ) = A ) ) $=
    ( cc wcel cmin co wceq caddc wb cc0 cif opreq1 eqeq1d eqeq2 bibi12d opreq2
    0cn elimel subaddi dedth3h ) ADEZBDEZCDEZABFGZCHZBCIGZAHZJUBAKLZBFGZCHZUGUI
    HZJUIUCBKLZFGZCHZUMCIGZUIHZJUNUDCKLZHZUMURIGZUIHZJABCKKKAUIHZUFUKUHULVBUEUJ
    CAUIBFMNAUIUGOPBUMHZUKUOULUQVCUJUNCBUMUIFQNVCUGUPUIBUMCIMNPCURHZUOUSUQVACUR
    UNOVDUPUTUICURUMIQNPUIUMURAKDRSBKDRSCKDRSTUA $.
    $( [20-Jan-1997] $)

  $( Swap subtrahend and result of subtraction. $)
  subsub23 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) = C <-> ( A - C ) = B ) ) $=
    ( cc wcel w3a caddc co wceq cmin addcom 3adant1 eqeq1d subadd wb 3com23
    3bitr4d ) ADEZBDEZCDEZFZBCGHZAICBGHZAIZABJHCIACJHBIZUAUBUCASTUBUCIRBCKLMABC
    NRTSUEUDOACBNPQ $.
    $( [14-Dec-2007] $)

  $( Subtraction and addition of equals. $)
  pncan3 $p |- ( ( A e. CC /\ B e. CC ) -> ( A + ( B - A ) ) = B ) $=
    ( cc wcel wa cmin co wceq caddc eqid wb subadd pm3.27 pm3.26 subcl ancoms
    syl3anc mpbii ) ACDZBCDZEZBAFGZUBHZAUBIGBHZUBJTSUBCDZUCUDKUABAUBLSTMSTNTSUE
    BAOPQR $.
    $( [15-Mar-2005] $) $( [14-Mar-2005] $)

  ${
    pncan3.1 $e |- A e. CC $.
    pncan3.2 $e |- B e. CC $.
    $( Subtraction and addition of equals. $)
    pncan3i $p |- ( A + ( B - A ) ) = B $=
      ( cc wcel cmin co caddc wceq pncan3 mp2an ) AEFBEFABAGHIHBJCDABKL $.
      $( [26-Nov-1994] $)
  $}

  $( Addition of a number and its negative. $)
  negid $p |- ( A e. CC -> ( A + -u A ) = 0 ) $=
    ( cc wcel cc0 cmin co caddc cneg wceq 0cn pncan3 mpan2 df-neg opreq2i
    syl5eq ) ABCZADAEFZGFZDAAHZGFPDBCRDIJADKLSQAGAMNO $.
    $( [15-Mar-2005] $) $( [14-Mar-2005] $)

  ${
    negid.1 $e |- A e. CC $.
    $( Addition of a number and its negative. $)
    negidi $p |- ( A + -u A ) = 0 $=
      ( cc wcel cneg caddc co cc0 wceq negid ax-mp ) ACDAAEFGHIBAJK $.
      $( [26-Nov-1994] $)
  $}

  ${
    negsub.1 $e |- A e. CC $.
    negsub.2 $e |- B e. CC $.
    $( Relationship between subtraction and negative.  Theorem I.3 of [Apostol]
       p. 18.  (The proof was shortened by Andrew Salmon, 22-Oct-2011.) $)
    negsubi $p |- ( A + -u B ) = ( A - B ) $=
      ( cmin co cneg caddc negcli addcli cc0 add12i negidi opreq2i addid1i
      3eqtri subaddrii eqcomi ) ABEFABGZHFZABTCDASCBDIZJBTHFABSHFZHFAKHFABASDCU
      ALUBKAHBDMNACOPQR $.
      $( [22-Oct-2011] $) $( [26-Nov-1994] $)

    $( Relationship between subtraction and negative.  Theorem I.3 of [Apostol]
       p. 18. $)
    negsubiOLD $p |- ( A + -u B ) = ( A - B ) $=
      ( cmin co cneg caddc negcli addcli addcomi cc0 addassi negidi eqtri
      opreq2i addid1i 3eqtri subaddrii eqcomi ) ABEFABGZHFZABUBCDAUACBDIZJZBUBH
      FUBBHFZABUBDUDKUEAUABHFZHFALHFAAUABCUCDMUFLAHUFBUAHFLUABUCDKBDNOPACQROST
      $.
      $( [3-Dec-2005] $) $( [26-Nov-1994] $)
  $}

  $( Relationship between subtraction and negative.  Theorem I.3 of [Apostol]
     p. 18. $)
  negsub $p |- ( ( A e. CC /\ B e. CC ) -> ( A + -u B ) = ( A - B ) ) $=
    ( cc wcel cneg caddc co cmin wceq cc0 cif opreq1 eqeq12d negeq opreq2d
    opreq2 0cn elimel negsubi dedth2h ) ACDZBCDZABEZFGZABHGZIUAAJKZUCFGZUFBHGZI
    UFUBBJKZEZFGZUFUIHGZIABJJAUFIUDUGUEUHAUFUCFLAUFBHLMBUIIZUGUKUHULUMUCUJUFFBU
    INOBUIUFHPMUFUIAJCQRBJCQRST $.
    $( [3-Dec-2005] $) $( [21-Jan-1997] $)

  $( Associative-type law for addition and subtraction. $)
  addsubass $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( A + B ) - C ) = ( A + ( B - C ) ) ) $=
    ( cc wcel w3a caddc co cneg cmin wceq addass negcl syl3an3 wa negsub addcl
    sylan 3impa 3adant1 opreq2d 3eqtr3d ) ADEZBDEZCDEZFZABGHZCIZGHZABUHGHZGHZUG
    CJHZABCJHZGHUCUDUHDEUIUKKUEABUHLCMNUCUDUEUIULKZUGDEUEUNUCUDOUGCPABQRSUFUJUM
    AGUDUEUJUMKUCBCPTUAUB $.
    $( [1-Aug-2006] $) $( [6-Aug-2003] $)

  $( Law for addition and subtraction.  (The proof was shortened by Andrew
     Salmon, 22-Oct-2011.) $)
  addsub $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A + B ) - C ) = ( ( A - C ) + B ) ) $=
    ( cc wcel w3a caddc co cmin wceq wa addcom opreq1d 3adant3 addsubass
    3com12 subcl sylan2 3impb 3eqtrd ) ADEZBDEZCDEZFABGHZCIHZBAGHZCIHZBACIHZGHZ
    UHBGHZUAUBUEUGJUCUAUBKUDUFCIABLMNUBUAUCUGUIJBACOPUBUAUCUIUJJZUBUAUCUKUBUHDE
    UKUAUCKBUHLACQRSPT $.
    $( [22-Oct-2011] $) $( [19-Aug-2001] $)

  $( Law for addition and subtraction. $)
  addsubOLD $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A + B ) - C ) = ( ( A - C ) + B ) ) $=
    ( cc wcel w3a caddc co cmin wceq wa addcom opreq1d 3adant3 addsubass
    3com12 wi ex subcl syl5 exp3a com12 3imp 3eqtrd ) ADEZBDEZCDEZFABGHZCIHZBAG
    HZCIHZBACIHZGHZULBGHZUEUFUIUKJUGUEUFKUHUJCIABLMNUFUEUGUKUMJBACOPUEUFUGUMUNJ
    ZUFUEUGUOQUFUEUGUOUFULDEZUOUEUGKUFUPUOBULLRACSTUAUBUCUD $.
    $( [19-Aug-2001] $)

  $( Commutative/associative law for addition and subtraction. $)
  subadd23 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A - B ) + C ) = ( A + ( C - B ) ) ) $=
    ( cc wcel cmin co caddc wceq w3a addsub addsubass eqtr3d 3com23 ) ADEZCDEZB
    DEZABFGCHGZACBFGHGZIOPQJACHGBFGRSACBKACBLMN $.
    $( [1-Feb-2007] $)

  $( Commutative/associative law for addition and subtraction. $)
  addsub12 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( A + ( B - C ) ) = ( B + ( A - C ) ) ) $=
    ( cc wcel cmin co caddc wceq w3a subadd23 wa addcom subcl sylan 3impa
    eqtr3d 3com23 ) ADEZCDEZBDEZABCFGHGZBACFGZHGZISTUAJUCBHGZUBUDACBKSTUAUEUDIZ
    UCDEUAUFSTLUCBMACNOPQR $.
    $( [1-Mar-2005] $) $( [8-Feb-2005] $)

  ${
    addsubass.1 $e |- A e. CC $.
    addsubass.2 $e |- B e. CC $.
    addsubass.3 $e |- C e. CC $.
    $( Associative-type law for subtraction and addition. $)
    addsubassi $p |- ( ( A + B ) - C ) = ( A + ( B - C ) ) $=
      ( cc wcel caddc co cmin wceq addsubass mp3an ) AGHBGHCGHABIJCKJABCKJIJLDE
      FABCMN $.
      $( [16-Sep-1999] $)

    $( Law for subtraction and addition. $)
    addsubi $p |- ( ( A + B ) - C ) = ( ( A - C ) + B ) $=
      ( cc wcel caddc co cmin wceq addsub mp3an ) AGHBGHCGHABIJCKJACKJBIJLDEFAB
      CMN $.
      $( [6-Aug-2003] $)
  $}

  $( Law for subtraction and addition. $)
  2addsub $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
                 ( ( ( A + B ) + C ) - D ) = ( ( ( A + C ) - D ) + B ) ) $=
    ( cc wcel wa caddc co cmin wceq add23 3expa adantrr opreq1d addsub 3expb
    addcl sylan an4s eqtrd ) AEFZBEFZGZCEFZDEFZGGZABHICHIZDJIACHIZBHIZDJIZUIDJI
    BHIZUGUHUJDJUDUEUHUJKZUFUBUCUEUMABCLMNOUBUEUCUFUKULKZUIEFZUCUFGUNUBUEGUOUCU
    FUNUIBDPQACRSTUA $.
    $( [8-Dec-2005] $) $( [20-Nov-2005] $)

  ${
    negneg.1 $e |- A e. CC $.
    $( A number is equal to the negative of its negative.  Theorem I.4 of
       [Apostol] p. 18.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    negnegi $p |- -u -u A = A $=
      ( cneg caddc co wceq cc0 negidi negcli addcomi 3eqtr4ri addcani mpbi ) AC
      ZNCZDEZNADEZFOAFANDEGQPABHNAABIZBJNRHKNOARNRIBLM $.
      $( [22-Oct-2011] $) $( [8-Feb-1995] $)

    $( A number is equal to the negative of its negative.  Theorem I.4 of
       [Apostol] p. 18. $)
    negnegiOLD $p |- -u -u A = A $=
      ( cneg caddc co cc0 negcli negidi opreq2i opreq1i addassi addid2i
      3eqtr3i addid1i ) AACZOCZDEZDEZAFDEPAQFADOABGZHIAODEZPDEFPDERPTFPDABHJAOP
      BSOSGZKPUALMABNM $.
      $( [8-Feb-1995] $)

    $( Subtraction of a number from itself. $)
    subidi $p |- ( A - A ) = 0 $=
      ( cneg caddc co cmin cc0 negsubi negidi eqtr3i ) AACDEAAFEGAABBHABIJ $.
      $( [26-Nov-1994] $)

    $( Identity law for subtraction. $)
    subid1i $p |- ( A - 0 ) = A $=
      ( cc0 0cn addid2i subaddrii ) ACABDBABEF $.
      $( [29-May-1999] $)

  $}

  $( A number is equal to the negative of its negative.  Theorem I.4 of
     [Apostol] p. 18. $)
  negneg $p |- ( A e. CC -> -u -u A = A ) $=
    ( cc wcel cneg wceq cc0 cif negeq negeqd id eqeq12d 0cn elimel negnegi
    dedth ) ABCZADZDZAEPAFGZDZDZSEAFASEZRUAASUBQTASHIUBJKSAFBLMNO $.
    $( [12-Jan-2002] $)

  $( Relationship between subtraction and negative. $)
  subneg $p |- ( ( A e. CC /\ B e. CC ) -> ( A - -u B ) = ( A + B ) ) $=
    ( cc wcel wa cneg caddc co cmin wceq negsub negcl sylan2 negneg opreq2d
    adantl eqtr3d ) ACDZBCDZEABFZFZGHZATIHZABGHZRTCDUBUCJSATKBLMSUBUDJRSUABAGBN
    OPQ $.
    $( [4-Jun-2004] $) $( [10-May-2004] $)

  $( Subtraction of a number from itself. $)
  subid $p |- ( A e. CC -> ( A - A ) = 0 ) $=
    ( cc wcel cmin co cc0 wceq cif opreq12 anidms eqeq1d 0cn elimel subidi
    dedth ) ABCZAADEZFGPAFHZRDEZFGAFARGZQSFTQSGARARDIJKRAFBLMNO $.
    $( [8-Oct-1999] $)

  $( Identity law for subtraction. $)
  subid1 $p |- ( A e. CC -> ( A - 0 ) = A ) $=
    ( cc wcel cc0 cmin co wceq cif opreq1 id eqeq12d 0cn elimel subid1i dedth
    ) ABCZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRADBLMNO $.
    $( [5-Jun-2004] $) $( [9-May-2004] $)

  $( Cancellation law for subtraction. $)
  pncan $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + B ) - B ) = A ) $=
    ( cc wcel wa caddc co cmin wceq addsubass 3anidm23 cc0 subid opreq2d
    addid1 sylan9eqr eqtrd ) ACDZBCDZEABFGBHGZABBHGZFGZARSTUBIABBJKSRUBALFGASUA
    LAFBMNAOPQ $.
    $( [23-May-2004] $) $( [10-May-2004] $)

  $( Cancellation law for subtraction. $)
  pncan2 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + B ) - A ) = B ) $=
    ( cc wcel caddc co cmin wceq wa addcom opreq1d pncan eqtr3d ancoms ) BCDZAC
    DZABEFZAGFZBHOPIZBAEFZAGFRBSTQAGBAJKBALMN $.
    $( [20-Apr-2005] $) $( [17-Apr-2005] $)

  $( Cancellation law for subtraction. $)
  npcan $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A - B ) + B ) = A ) $=
    ( cc wcel wa caddc co cmin wceq addsub 3anidm23 pncan eqtr3d ) ACDZBCDZEABF
    GBHGZABHGBFGZANOPQIABBJKABLM $.
    $( [23-May-2004] $) $( [10-May-2004] $)

  $( Cancellation law for subtraction. $)
  npncan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) + ( B - C ) ) = ( A - C ) ) $=
    ( cc wcel w3a cmin co caddc wceq addsubass subcl 3adant3 syld3an1 wa npcan
    opreq1d eqtr3d ) ADEZBDEZCDEZFABGHZBIHZCGHZUBBCGHIHZACGHZUBDEZTUAUDUEJSUBBC
    KSTUGUAABLMNSTUDUFJUASTOUCACGABPQMR $.
    $( [19-Feb-2005] $) $( [8-Feb-2005] $)

  $( Cancellation law for subtraction. $)
  nppcan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( ( A - B ) + C ) + B ) = ( A + C ) ) $=
    ( cc wcel w3a cmin co caddc wceq add23 subcl 3adant3 3simp3 3simp2 syl3anc
    wa npcan opreq1d eqtrd ) ADEZBDEZCDEZFZABGHZCIHBIHZUEBIHZCIHZACIHZUEDEZUCUB
    UFUHJUDUECBKUAUBUJUCABLMUAUBUCNUAUBUCOPUAUBUHUIJUCUAUBQUGACIABRSMT $.
    $( [8-Sep-2005] $) $( [1-Sep-2005] $)

  $( Cancellation law for subtraction. $)
  subcan2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - C ) = ( B - C ) <-> A = B ) ) $=
    ( cc wcel w3a cneg caddc co wceq cmin negsub 3adant2 3adant1 eqeq12d wb
    addcan2 negcl syl3an3 bitr3d ) ADEZBDEZCDEZFZACGZHIZBUEHIZJZACKIZBCKIZJABJZ
    UDUFUIUGUJUAUCUFUIJUBACLMUBUCUGUJJUABCLNOUAUBUEDEUHUKPUCABUEQCRST $.
    $( [2-Mar-2005] $) $( [8-Feb-2005] $)

  $( If the difference between two numbers is zero, they are equal. $)
  subeq0 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A - B ) = 0 <-> A = B ) ) $=
    ( cc wcel wa cmin co wceq cc0 subid adantl eqeq2d wb subcan2 3anidm23
    bitr3d ) ACDZBCDZEZABFGZBBFGZHZTIHABHZSUAITRUAIHQBJKLQRUBUCMABBNOP $.
    $( [16-Nov-1999] $)

  ${
    subneg.1 $e |- A e. CC $.
    subneg.2 $e |- B e. CC $.
    $( Relationship between subtraction and negative. $)
    subnegi $p |- ( A - -u B ) = ( A + B ) $=
      ( cc wcel cneg cmin co caddc wceq subneg mp2an ) AEFBEFABGHIABJIKCDABLM
      $.
      $( [3-Dec-2005] $) $( [1-Dec-2005] $)

    $( If the difference between two numbers is zero, they are equal. $)
    subeq0i $p |- ( ( A - B ) = 0 <-> A = B ) $=
      ( cc wcel cmin co cc0 wceq wb subeq0 mp2an ) AEFBEFABGHIJABJKCDABLM $.
      $( [8-May-1999] $)

    $( Negative is one-to-one. $)
    neg11i $p |- ( -u A = -u B <-> A = B ) $=
      ( cneg wceq cc0 cmin co caddc df-neg eqeq12i 0cn subcli subaddi opreq2i
      negsubi eqtr3i eqeq1i subeq0i bitri 3bitri ) AEZBEZFGAHIZGBHIZFAUFJIZGFZA
      BFZUCUEUDUFAKBKZLGAUFMCGBMDNOUHABHIZGFUIUGUKGAUDJIUGUKUDUFAJUJPABCDQRSABC
      DTUAUB $.
      $( [1-Aug-1999] $)

    $( Negative contraposition law. $)
    negcon1i $p |- ( -u A = B <-> -u B = A ) $=
      ( cneg wceq negnegi eqeq1i negcli neg11i eqcom 3bitr3i ) AEZEZBEZFAOFMBFO
      AFNAOACGHMBACIDJAOKL $.
      $( [25-Aug-1999] $)

    $( Negative contraposition law. $)
    negcon2i $p |- ( A = -u B <-> B = -u A ) $=
      ( cneg wceq negcon1i eqcom 3bitr4i ) BEZAFAEZBFAJFBKFBADCGAJHBKHI $.
      $( [25-Aug-1999] $)
  $}

  $( Negative is one-to-one. $)
  neg11 $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A = -u B <-> A = B ) ) $=
    ( cc wcel cneg wceq wb cc0 cif negeq eqeq1d eqeq1 bibi12d eqeq2d eqeq2 0cn
    elimel neg11i dedth2h ) ACDZBCDZAEZBEZFZABFZGTAHIZEZUCFZUFBFZGUGUABHIZEZFZU
    FUJFZGABHHAUFFZUDUHUEUIUNUBUGUCAUFJKAUFBLMBUJFZUHULUIUMUOUCUKUGBUJJNBUJUFOM
    UFUJAHCPQBHCPQRS $.
    $( [10-Feb-2005] $) $( [8-Feb-2005] $)

  $( Negative contraposition law. $)
  negcon1 $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A = B <-> -u B = A ) ) $=
    ( cc wcel wa cneg wceq wb neg11 negcl sylan negneg adantr eqeq1d bitr3d
    eqcom syl6bb ) ACDZBCDZEZAFZBGZABFZGZUCAGTUAFZUCGZUBUDUACDSUFUBHRUABIAJKTUE
    AUCRUEAGSALMNOAUCPQ $.
    $( [5-Jun-2004] $) $( [9-May-2004] $)

  $( Negative contraposition law. $)
  negcon2 $p |- ( ( A e. CC /\ B e. CC ) -> ( A = -u B <-> B = -u A ) ) $=
    ( cc wcel wa cneg wceq negcon1 eqcom syl6rbbr syl6bb ) ACDBCDEZABFZGZAFZBGZ
    BOGLPMAGNABHAMIJOBIK $.
    $( [17-Nov-2004] $) $( [14-Nov-2004] $)

  $( Cancellation law for subtraction. $)
  subcan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) = ( A - C ) <-> B = C ) ) $=
    ( cc wcel w3a cneg caddc co wceq cmin wb addcan negcl syl3an3 syl3an2
    negsub 3adant3 3adant2 eqeq12d neg11 3adant1 3bitr3d ) ADEZBDEZCDEZFZABGZHI
    ZACGZHIZJZUHUJJZABKIZACKIZJBCJZUDUHDEZUFULUMLZUEUDUQUJDEURUFAUHUJMCNOBNPUGU
    IUNUKUOUDUEUIUNJUFABQRUDUFUKUOJUEACQSTUEUFUMUPLUDBCUAUBUC $.
    $( [2-Aug-2006] $) $( [8-Feb-2005] $)

  ${
    subcan.1 $e |- A e. CC $.
    subcan.2 $e |- B e. CC $.
    subcan.3 $e |- C e. CC $.
    $( Cancellation law for subtraction. $)
    subcani $p |- ( ( A - B ) = ( A - C ) <-> B = C ) $=
      ( cc wcel cmin co wceq wb subcan mp3an ) AGHBGHCGHABIJACIJKBCKLDEFABCMN
      $.
      $( [11-Apr-2005] $) $( [8-Feb-2005] $)

    $( Cancellation law for subtraction. $)
    subcan2i $p |- ( ( A - C ) = ( B - C ) <-> A = B ) $=
      ( cc wcel cmin co wceq wb subcan2 mp3an ) AGHBGHCGHACIJBCIJKABKLDEFABCMN
      $.
      $( [11-Mar-2005] $) $( [8-Feb-2005] $)
  $}

  $( Minus 0 equals 0. $)
  neg0 $p |- -u 0 = 0 $=
    ( cc0 cneg cmin co df-neg 0cn subidi eqtri ) ABAACDAAEAFGH $.
    $( [17-Jan-1997] $)

  ${
    $d x A $.
    renegcl.1 $e |- A e. RR $.
    $( Closure law for negative of reals.  (The proof was shortened by Andrew
       Salmon, 22-Oct-2011.) $)
    renegcli $p |- -u A e. RR $=
      ( vx cv caddc co cc0 wceq cr wrex cneg wcel axrnegex ax-mp cc wb recn
      cmin 0cn recni subadd mp3an12 df-neg eqeq1i syl5bb syl eleq1a sylbird
      r19.23aiv ) ACDZEFGHZCIJZAKZILZAILULBCAMNUKUNCIUJILZUKUMUJHZUNUOUJOLZUPUK
      PUJQUQGARFZUJHZUKUPGOLAOLUQUSUKPSABTGAUJUAUBUMURUJAUCUDUEUFUJIUMUGUHUIN
      $.
      $( [22-Oct-2011] $) $( [17-Jan-1997] $)

    $( Closure law for negative of reals. $)
    renegcliOLD $p |- -u A e. RR $=
      ( vx cv cr wcel caddc co cc0 wceq wa wex cneg wrex axrnegex ax-mp df-rex
      mpbi cmin cc wb recn 0cn recni subadd mp3an12 syl df-neg eqeq1i syl5bb
      eleq1a sylbird imp 19.23aiv ) CDZEFZAUOGHIJZKZCLZAMZEFZUQCENZUSAEFVBBCAOP
      UQCEQRURVACUPUQVAUPUQUTUOJZVAUPIASHZUOJZUQVCUPUOTFZVEUQUAZUOUBITFATFVFVGU
      CABUDIAUOUEUFUGUTVDUOAUHUIUJUOEUTUKULUMUNP $.
      $( [17-Jan-1997] $)
  $}

