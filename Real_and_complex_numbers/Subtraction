$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Subtraction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Define subtraction.  Theorem ~ subval shows it value (and describes how
       this definition works), theorem ~ subadd relates it to addition, and
       theorems ~ subcl and ~ subrcl prove its closure laws. $)
    df-sub $a |- - = { <. <. x , y >. , z >. |
                   ( ( x e. CC /\ y e. CC ) /\
                   z = U. { w e. CC | ( y + w ) = x } ) } $.
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    subval.1 $e |- A e. CC $.
    subval.2 $e |- B e. CC $.
    $( Value of subtraction, which is the (unique) element ` x ` such that
       ` B + x = A ` .  The notation ` U. { x e. CC | ( B + x ) = A } `
       may at first seem cryptic but is actually a way of saying
       "the element ` x ` such that ` B + x = A `" (see Theorem 8.17 of
       [Quine] p. 56);  this works because there is only one such ` x ` as
       shown by ~ negeu , allowing us to exploit ~ eusn and ~ unisn (which you
       will find if you trace back the proof of ~ subcl ). $)
    subval $p |- ( A - B ) = U. { x e. CC | ( B + x ) = A } $=
      ( vy vz vw cc wcel cmin co cv caddc wceq crab cuni axcnex rabex uniex
      cleq2 birabsdv unieqd opreq1 cleq1d df-sub oprabval2 mp2an ) BIJCIJBCKLCA
      MZNLZBOZAIPZQZODEFGHBCIIGMZUINLZFMZOZAIPZQUMKUOBOZAIPZQULUKAIRSTUPBOZURUT
      VAUQUSAIUPBUOUAUBUCUNCOZUTULVBUSUKAIVBUOUJBUNCUINUDUEUBUCFGHAUFUGUH $.
      $( [8-May-99] $)
  $}

  $( Define the negative of a number (unary minus).  We use different symbols
     for unary minus ( ` -u ` ) and subtraction ( ` - ` ) to prevent syntax
     ambiguity.  See ~ cneg for a discussion of this. $)
  df-neg $a |- -u A = ( 0 - A ) $.

  $( Equality theorem for negatives. $)
  negeq $p |- ( A = B -> -u A = -u B ) $=
    ( wceq cc0 cmin co cneg opreq2 df-neg 3eqtr4g ) ABCDAEFDBEFAGBGABDEHAIBIJ
    $.
    $( [10-Feb-95] $)

  ${
    negeqi.1 $e |- A = B $.
    $( Equality inference for negatives. $)
    negeqi $p |- -u A = -u B $=
      ( wceq cneg negeq ax-mp ) ABDAEBEDCABFG $.
      $( [14-Feb-95] $)
  $}

  ${
    negeqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for negatives. $)
    negeqd $p |- ( ph -> -u A = -u B ) $=
      ( wceq cneg negeq syl ) ABCEBFCFEDBCGH $.
      $( [14-May-99] $)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    subcl.1 $e |- A e. CC $.
    subcl.2 $e |- B e. CC $.
    $( Closure law for subtraction. $)
    subcl $p |- ( A - B ) e. CC $=
      ( vx cmin co cv caddc wceq cc crab cuni subval wreu wcel negeu reucl
      ax-mp eqeltr ) ABFGBEHIGAJZEKLMZKEABCDNUAEKOUBKPEBADCQUAEKRST $.
      $( [26-Nov-94] $)
  $}

  $( Closure law for subtraction. $)
  subclt $p |- ( ( A e. CC /\ B e. CC ) -> ( A - B ) e. CC ) $=
    ( cc wcel cmin co cc0 cded wceq opreq1 eleq1d opreq2 0cn elimel subcl
    dedth2h ) ACDZBCDZABEFZCDQAGHZBEFZCDTRBGHZEFZCDABGGATISUACATBEJKBUBIUAUCCBU
    BTELKTUBAGCMNBGCMNOP $.
    $( [10-May-99] $)

  $( Closure law for negative. $)
  negclt $p |- ( A e. CC -> -u A e. CC ) $=
    ( cc wcel cc0 cmin co cneg 0cn subclt mpan df-neg eleq1i sylibr ) ABCZDAEFZ
    BCZAGZBCDBCNPHDAIJQOBAKLM $.
    $( [6-Aug-03] $)

  ${
    negcl.1 $e |- A e. CC $.
    $( Closure law for negative. $)
    negcl $p |- -u A e. CC $=
      ( cc wcel cneg negclt ax-mp ) ACDAECDBAFG $.
      $( [26-Nov-94] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    subadd.1 $e |- A e. CC $.
    subadd.2 $e |- B e. CC $.
    subadd.3 $e |- C e. CC $.
    $( Relationship between subtraction and addition. $)
    subadd $p |- ( ( A - B ) = C <-> ( B + C ) = A ) $=
      ( vx cc wcel cmin co wceq caddc wb cv cleq2 opreq2 cleq1d bi2bid crab
      cuni wreu negeu reuuni mpan2 subval cleq1i syl6rbbr vtoclga ax-mp ) CHIAB
      JKZCLZBCMKZALZNZFUKGOZLZBUPMKZALZNUOGCHUPCLZUQULUSUNUPCUKPUTURUMAUPCBMQRS
      UPHIZUSUSGHTUAZUPLZUQVAUSGHUBUSVCNGBAEDUCUSGHUDUEUKVBUPGABDEUFUGUHUIUJ $.
      $( [26-Nov-94] $)

    $( Relationship between subtraction and subtraction. $)
    subsub $p |- ( ( A - B ) = C <-> ( A - C ) = B ) $=
      ( caddc co wceq cmin addcom cleq1i subadd 3bitr4 ) BCGHZAICBGHZAIABJHCIAC
      JHBIOPABCEFKLABCDEFMACBDFEMN $.
      $( [7-Oct-99] $)
  $}

  $( Relationship between subtraction and addition. $)
  subaddt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) = C <-> ( B + C ) = A ) ) $=
    ( cc wcel cmin co wceq caddc wb cc0 cded opreq1 cleq1d cleq2 bi2bid opreq2
    0cn elimel subadd dedth3h ) ADEZBDEZCDEZABFGZCHZBCIGZAHZJUBAKLZBFGZCHZUGUIH
    ZJABCKKKUIUCBKLZFGZCHZUMCIGZUIHZJUNUDCKLZHZUMURIGZUIHZJAUIHZUFUKUHULVBUEUJC
    AUIBFMNAUIUGOPBUMHZUKUOULUQVCUJUNCBUMUIFQNVCUGUPUIBUMCIMNPCURHZUOUSUQVACURU
    NOVDUPUTUICURUMIQNPUIUMURAKDRSBKDRSCKDRSTUA $.
    $( [20-Jan-97] $)

  ${
    subaddeq.1 $e |- A e. CC $.
    subaddeq.2 $e |- B e. CC $.
    $( Subtraction and addition of equals. $)
    subaddeq $p |- ( A + ( B - A ) ) = B $=
      ( cmin co wceq caddc cleqid subcl subadd mpbi ) BAEFZMGAMHFBGMIBAMDCBADCJ
      KL $.
      $( [26-Nov-94] $)
  $}

  ${
    negid.1 $e |- A e. CC $.
    $( Addition of a number and its negative. $)
    negid $p |- ( A + -u A ) = 0 $=
      ( cneg caddc co cc0 cmin df-neg opreq2i 0cn subaddeq eqtr ) AACZDEAFAGEZD
      EFMNADAHIAFBJKL $.
      $( [26-Nov-94] $)
  $}

  ${
    subneg.1 $e |- A e. CC $.
    subneg.2 $e |- B e. CC $.
    $( Relationship between subtraction and negative.  Theorem I.3 of
       [Apostol] p. 18. $)
    subneg $p |- ( A - B ) = ( A + -u B ) $=
      ( cmin co cneg caddc wceq negcl addcl addcom cc0 addass negid eqtr
      opreq2i addid1 3eqtr subadd mpbir ) ABEFABGZHFZIBUCHFZAIUDUCBHFZABUCDAUBC
      BDJZKZLUEAUBBHFZHFAMHFAAUBBCUFDNUHMAHUHBUBHFMUBBUFDLBDOPQACRSPABUCCDUGTUA
      $.
      $( [26-Nov-94] $)
  $}

  $( Relationship between subtraction and negative.  Theorem I.3 of
     [Apostol] p. 18. $)
  subnegt $p |- ( ( A e. CC /\ B e. CC ) -> ( A - B ) = ( A + -u B ) ) $=
    ( cc wcel cmin co cneg caddc wceq cc0 cded opreq1 cleq12d opreq2 negeq
    opreq2d 0cn elimel subneg dedth2h ) ACDZBCDZABEFZABGZHFZIUAAJKZBEFZUFUDHFZI
    UFUBBJKZEFZUFUIGZHFZIABJJAUFIUCUGUEUHAUFBELAUFUDHLMBUIIZUGUJUHULBUIUFENUMUD
    UKUFHBUIOPMUFUIAJCQRBJCQRST $.
    $( [21-Jan-97] $)

  $( Associative-type law for subtraction and addition. $)
  addsubasst $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( A + B ) - C ) = ( A + ( B - C ) ) ) $=
    ( cc wcel w3a caddc co cneg cmin wceq axaddass negclt syl3an3 subnegt
    axaddcl 3adant3 3simp3 sylan13 3adant1 opreq2d 3eqtr4d ) ADEZBDEZCDEZFZABGH
    ZCIZGHZABUHGHZGHZUGCJHZABCJHZGHUCUDUHDEUIUKKUEABUHLCMNUGDEZUEULUIKUFUGCOUCU
    DUNUEABPQUCUDUERSUFUMUJAGUDUEUMUJKUCBCOTUAUB $.
    $( [6-Aug-03] $)

  $( Law for subtraction and addition. $)
  addsubt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A + B ) - C ) = ( ( A - C ) + B ) ) $=
    ( cc wcel w3a caddc co cmin wceq wa axaddcom opreq1d 3adant3 addsubasst
    3com12 wi exp subclt syl5 exp3a com12 3imp 3eqtrd ) ADEZBDEZCDEZFABGHZCIHZB
    AGHZCIHZBACIHZGHZULBGHZUEUFUIUKJUGUEUFKUHUJCIABLMNUFUEUGUKUMJBACOPUEUFUGUMU
    NJZUFUEUGUOQUFUEUGUOUFULDEZUOUEUGKUFUPUOBULLRACSTUAUBUCUD $.
    $( [19-Aug-01] $)

  ${
    addsubass.1 $e |- A e. CC $.
    addsubass.2 $e |- B e. CC $.
    addsubass.3 $e |- C e. CC $.
    $( Associative-type law for subtraction and addition. $)
    addsubass $p |- ( ( A + B ) - C ) = ( A + ( B - C ) ) $=
      ( cc wcel caddc co cmin wceq addsubasst mp3an ) AGHBGHCGHABIJCKJABCKJIJLD
      EFABCMN $.
      $( [16-Sep-99] $)

    $( Law for subtraction and addition. $)
    addsub $p |- ( ( A + B ) - C ) = ( ( A - C ) + B ) $=
      ( cc wcel caddc co cmin wceq addsubt mp3an ) AGHBGHCGHABIJCKJACKJBIJLDEFA
      BCMN $.
      $( [6-Aug-03] $)
  $}

  ${
    negneg.1 $e |- A e. CC $.
    $( A number is equal to the negative of its negative.  Theorem I.4 of
       [Apostol] p. 18. $)
    negneg $p |- -u -u A = A $=
      ( cneg caddc co cc0 negcl negid opreq2i opreq1i addass addid2 3eqtr3
      addid1 ) AACZOCZDEZDEZAFDEPAQFADOABGZHIAODEZPDEFPDERPTFPDABHJAOPBSOSGZKPU
      ALMABNM $.
      $( [8-Feb-95] $)

    $( Subtraction of a number from itself. $)
    subid $p |- ( A - A ) = 0 $=
      ( cmin co cneg caddc cc0 subneg negid eqtr ) AACDAAEFDGAABBHABIJ $.
      $( [26-Nov-94] $)

    $( Identity law for subtraction. $)
    subid1 $p |- ( A - 0 ) = A $=
      ( cc0 cmin co wceq caddc addid2 0cn subadd mpbir ) ACDEAFCAGEAFABHACABIBJ
      K $.
      $( [29-May-99] $)

  $}

  $( A number is equal to the negative of its negative.  Theorem I.4 of
     [Apostol] p. 18. $)
  negnegt $p |- ( A e. CC -> -u -u A = A ) $=
    ( cc wcel cneg wceq cc0 cded negeq negeqd id cleq12d 0cn elimel negneg
    dedth ) ABCZADZDZAEPAFGZDZDZSEAFASEZRUAASUBQTASHIUBJKSAFBLMNO $.
    $( [12-Jan-02] $)

  $( Relationship between subtraction and negative. $)
  subneg2t $p |- ( ( A e. CC /\ B e. CC ) -> ( A - -u B ) = ( A + B ) ) $=
    ( cc wcel wa cneg cmin co caddc wceq subnegt negclt sylan5 negnegt opreq2d
    adantl eqtrd ) ACDZBCDZEABFZGHZATFZIHZABIHZRTCDUAUCJSATKBLMSUCUDJRSUBBAIBNO
    PQ $.
    $( [4-Jun-04] $) $( [10-May-04] $)

  $( Subtraction of a number from itself. $)
  subidt $p |- ( A e. CC -> ( A - A ) = 0 ) $=
    ( cc wcel cmin co cc0 wceq cded opreq12 anidms cleq1d 0cn elimel subid
    dedth ) ABCZAADEZFGPAFHZRDEZFGAFARGZQSFTQSGARARDIJKRAFBLMNO $.
    $( [8-Oct-99] $)

  $( Identity law for subtraction. $)
  subid1t $p |- ( A e. CC -> ( A - 0 ) = A ) $=
    ( cc wcel cc0 cmin co wceq cded opreq1 id cleq12d 0cn elimel subid1 dedth
    ) ABCZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRADBLMNO $.
    $( [5-Jun-04] $) $( [9-May-04] $)

  $( Cancellation law for subtraction. $)
  pncant $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + B ) - B ) = A ) $=
    ( cc wcel wa caddc co cmin wceq addsubasst 3expb anabsan2 cc0 subidt
    opreq2d ax0id sylan9eqr eqtrd ) ACDZBCDZEABFGBHGZABBHGZFGZASTUAUCIZSTTUDABB
    JKLTSUCAMFGATUBMAFBNOAPQR $.
    $( [23-May-04] $) $( [10-May-04] $)

  $( Cancellation law for subtraction. $)
  npcant $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A - B ) + B ) = A ) $=
    ( cc wcel wa caddc co cmin wceq addsubt 3expb anabsan2 pncant eqtr3d ) ACDZ
    BCDZEABFGBHGZABHGBFGZAOPQRIZOPPSABBJKLABMN $.
    $( [23-May-04] $) $( [10-May-04] $)

  ${
    subeq0.1 $e |- A e. CC $.
    subeq0.2 $e |- B e. CC $.
    $( If the difference between two numbers is zero, they are equal. $)
    subeq0 $p |- ( ( A - B ) = 0 <-> A = B ) $=
      ( cmin co cc0 wceq cneg caddc subneg cleq1i opreq1 sylbi negcl add23
      addass negid opreq2i addid1 eqtr 3eqtr addid2 3eqtr3g subid syl6eq impbi
      ) ABEFZGHZABHZUIABIZJFZBJFZGBJFZABUIULGHUMUNHUHULGABCDKLULGBJMNUMABJFUKJF
      ABUKJFZJFZAAUKBCBDOZDPABUKCDUQQUPAGJFAUOGAJBDRSACTUAUBBDUCUDUJUHBBEFGABBE
      MBDUEUFUG $.
      $( [8-May-99] $)

    $( Negative is one-to-one. $)
    neg11 $p |- ( -u A = -u B <-> A = B ) $=
      ( cneg wceq cc0 cmin co caddc df-neg cleq12i 0cn subcl subadd subneg
      opreq2i eqtr2 cleq1i subeq0 bitr 3bitr ) AEZBEZFGAHIZGBHIZFAUFJIZGFZABFZU
      CUEUDUFAKBKZLGAUFMCGBMDNOUHABHIZGFUIUGUKGUKAUDJIUGABCDPUDUFAJUJQRSABCDTUA
      UB $.
      $( [1-Aug-99] $)

    $( Negative contraposition law. $)
    negcon1 $p |- ( -u A = B <-> -u B = A ) $=
      ( cneg wceq negneg cleq1i negcl neg11 cleqcom 3bitr3 ) AEZEZBEZFAOFMBFOAF
      NAOACGHMBACIDJAOKL $.
      $( [25-Aug-99] $)

    $( Negative contraposition law. $)
    negcon2 $p |- ( A = -u B <-> B = -u A ) $=
      ( cneg wceq negcon1 cleqcom 3bitr4 ) BEZAFAEZBFAJFBKFBADCGAJHBKHI $.
      $( [25-Aug-99] $)
  $}

  $( Negative contraposition law. $)
  negcon1t $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A = B <-> -u B = A ) ) $=
    ( cc wcel cneg wceq wb cc0 cded negeq cleq1d cleq2 bi2bid 0cn elimel
    negcon1 dedth2h ) ACDZBCDZAEZBFZBEZAFZGRAHIZEZBFZUBUDFZGUESBHIZFZUHEZUDFZGA
    BHHAUDFZUAUFUCUGULTUEBAUDJKAUDUBLMBUHFZUFUIUGUKBUHUELUMUBUJUDBUHJKMUDUHAHCN
    OBHCNOPQ $.
    $( [5-Jun-04] $) $( [9-May-04] $)

  $( If the difference between two numbers is zero, they are equal. $)
  subeq0t $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A - B ) = 0 <-> A = B ) ) $=
    ( cc wcel cmin co cc0 wceq wb cded opreq1 cleq1d cleq1 bi2bid opreq2 cleq2
    0cn elimel subeq0 dedth2h ) ACDZBCDZABEFZGHZABHZIUAAGJZBEFZGHZUFBHZIUFUBBGJ
    ZEFZGHZUFUJHZIABGGAUFHZUDUHUEUIUNUCUGGAUFBEKLAUFBMNBUJHZUHULUIUMUOUGUKGBUJU
    FEOLBUJUFPNUFUJAGCQRBGCQRST $.
    $( [16-Nov-99] $)

  $( Minus 0 equals 0. $)
  neg0 $p |- -u 0 = 0 $=
    ( cc0 cneg cmin co df-neg 0cn subid eqtr ) ABAACDAAEAFGH $.
    $( [17-Jan-97] $)

  ${
    $d x A $.  $( ` x ` is dummy. $)
    negrcl.1 $e |- A e. RR $.
    $( Closure law for negative of reals. $)
    negrcl $p |- -u A e. RR $=
      ( vx cv cr wcel caddc co cc0 wceq wa wex cneg wrex axrnegex ax-mp df-rex
      mpbi cmin cc wb recnt recn 0cn subaddt mp3an1 mpan syl df-neg cleq1i
      syl5bb eleq1a sylbird imp 19.23aiv ) CDZEFZAUPGHIJZKZCLZAMZEFZURCENZUTAEF
      VCBCAOPURCEQRUSVBCUQURVBUQURVAUPJZVBUQIASHZUPJZURVDUQUPTFZVFURUAZUPUBATFZ
      VGVHABUCITFVIVGVHUDIAUPUEUFUGUHVAVEUPAUIUJUKUPEVAULUMUNUOP $.
      $( [17-Jan-97] $)
  $}

  $( Closure law for negative of reals. $)
  negrclt $p |- ( A e. RR -> -u A e. RR ) $=
    ( cr wcel cneg cc0 cded wceq negeq eleq1d ax0re elimel negrcl dedth ) ABCZA
    DZBCNAEFZDZBCAEAPGOQBAPHIPAEBJKLM $.
    $( [20-Jan-97] $)

  $( Closure law for subtraction of reals. $)
  subrclt $p |- ( ( A e. RR /\ B e. RR ) -> ( A - B ) e. RR ) $=
    ( cr wcel wa cmin co cneg caddc cc wceq subnegt recnt syl2an axaddrcl
    negrclt sylan5 eqeltrd ) ACDZBCDZEABFGZABHZIGZCAJDBJDUAUCKSTABLAMBMNSUBCDUC
    CDTAUBOBPQR $.
    $( [20-Jan-97] $)

  ${
    subrcl.1 $e |- A e. RR $.
    subrcl.2 $e |- B e. RR $.
    $( Closure law for subtraction of reals. $)
    subrcl $p |- ( A - B ) e. RR $=
      ( cr wcel cmin co subrclt mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [17-Jan-97] $)
  $}

