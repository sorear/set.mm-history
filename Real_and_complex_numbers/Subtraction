$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Subtraction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c d x y z A $.  $d x B $.  $d x C $.
    $( Lemma for ~ cnegext . $)
    cnegextlem1 $p |- ( ( A e. RR /\ B e. CC /\ C e. CC ) ->
                  ( ( A + B ) = ( A + C ) <-> B = C ) ) $=
      ( vx cr wcel cc w3a caddc co wceq cv cc0 wrex wi axrnegex 3ad2ant1 wa
      axaddass pm3.27 simpll simprl adantr syl3anc simprr eqeq12d opreq2
      syl5bir wb axaddcom eqeq1d adantlr opreq1 adantl addid2t eqeqan12d
      ad2antrr bitrd ex sylbid imp sylibd recnt sylan2 r19.23adva 3impb
      syl3an1 mpd impbid1 ) AEFZBGFZCGFZHZABIJZACIJZKZBCKZVMADLZIJZMKZDENZVPVQO
      ZVJVKWAVLDAPQAGFZVKVLWAWBOZVJWCVKVLWDWCVKVLRZRZVTWBDEWFVRGFZVTWBOVREFWFWG
      RZVTWBWHVTRVPVRAIJZBIJZWICIJZKZVQWHVPWLOVTWHWLVRVNIJZVRVOIJZKVPWHWJWMWKWN
      WGWCVKWJWMKWHVRABSWFWGTZWCWEWGUAZWFVKWGWCVKVLUBUCUDWGWCVLWKWNKWHVRACSWOWP
      WFVLWGWCVKVLUEUCUDUFVNVOVRIUGUHUCWHVTWLVQUIZWHVTWIMKZWQWCWGVTWRUIWEWCWGRV
      SWIMAVRUJUKULWHWRWQWHWRRWLMBIJZMCIJZKZVQWRWLXAUIWHWRWJWSWKWTWIMBIUMWIMCIU
      MUFUNWFXAVQUIZWGWRWEXBWCVKVLWSBWTCBUOCUOUPUNUQURUSUTVAVBUSVRVCVDVEVFAVCVG
      VHBCAIUGVI $.
      $( [22-May-2007] $) $( [22-May-2007] $)

    $( Lemma for ~ cnegext . $)
    cnegextlem2 $p |- E. y e. RR ( i x. y ) e. RR $=
      ( vx vz cc0 cv ci cmul co caddc wceq cr wrex wcel cc 0cn axcnre ax-mp wa
      wi axrnegex adantr w3a addid2t 3ad2ant3 opreq1 ad2antrl add23t 3com23
      eqcomd sylan9eq adantrl 3ad2ant2 3eqtr3d eqtr3d ex recnt axicn axmulcl
      mpan syl syl3an 3expa imp simplr eqeltrrd exp32 r19.23adva mpd r19.22dva
      r19.23aiv ) DBEZFAEZGHZIHZJZAKLZBKLZVMKMZAKLZDNMVQOBADPQVPVSBKVKKMZVOVRAK
      VTVLKMZRZVKCEZIHZDJZCKLZVOVRSZVTWFWACVKTUAWBWEWGCKWBWCKMZRZWEVOVRWIWEVORZ
      RWCVMKWIWJWCVMJZVTWAWHWJWKSZVKNMZVMNMZWCNMZWLVTWAWHWMWNWOUBZWJWKWPWJRZDWC
      IHZWCVMWPWRWCJZWJWOWMWSWNWCUCUDUAWQWDVMIHZDVMIHZWRVMWEWTXAJWPVOWDDVMIUEUF
      WPVOWTWRJWEWPVOWTVNWCIHZWRWMWOWNWTXBJVKWCVMUGUHVOWRXBDVNWCIUEUIUJUKWPXAVM
      JZWJWNWMXCWOVMUCULUAUMUNUOVKUPWAVLNMZWNVLUPFNMXDWNUQFVLURUSUTWCUPVAVBVCWB
      WHWJVDVEVFVGVHVIVJQ $.
      $( [11-Mar-2008] $) $( [22-May-2007] $)

    $( Lemma for ~ cnegext . $)
    cnegextlem3 $p |- ( ( b e. RR /\ y e. RR ) -> E. c e. RR ( b + c ) = y ) $=
      ( vx cv cr wcel wa caddc co cc0 wceq wrex axrnegex adantl axaddrcl syl
      adantlr adantr cc wb add23t 3expa axaddcom axaddcl sylan an1rs eqtr2d
      recnt sylanl2 adantllr ancoms sylan2 id sylan9eq adantlll eqeq12d
      cnegextlem1 simplr syl3anc bitr3d anim12i anim1i syl2an rexbidva mpbid
      ex r19.23adva mpd ) BEZFGZAEZFGZHZVLDEZIJZKLZDFMZVJCEZIJZVLLZCFMZVMVRVKDV
      LNOVNVQWBDFVNVOFGZHZVQWBWDVQHZVJVOIJZVSIJZKLZCFMZWBWDWIVQVKWCWIVMVKWCHWFF
      GWIVJVOPCWFNQRSWEWHWACFVJTGZVLTGZHZWCHZVQHZVSTGZWHWAUAWEVSFGWNWOHZVOVTIJZ
      VOVLIJZLZWHWAWPWQWGWRKWMWOWQWGLZVQWJWCWOWTWKWJVOTGZWOWTWCWJXAHWOHWGVTVOIJ
      ZWQWJXAWOWGXBLVJVOVSUBUCWJWOXAXBWQLZVTTGZXAXCWJWOHVTVOUDVJVSUEZUFUGUHVOUI
      ZUJUKRWNWRKLZWOWKWCVQXGWJWKWCHVQWRVPKWKXAWRVPLZWCXAWKXHVOVLUDULXFUMVQUNUO
      UPSUQWMWOWSWAUAZVQWCXDWKXIWMWOHVOVTVLURWLWCWOUSWLWOXDWCWJWOXDWKXERRWMWKWO
      WJWKWCUSSUTRVAWDWMVQVNWLWCVKWJVMWKVJUIVLUIVBVCVCVSUIVDVEVFVGVHVI $.
      $( [22-May-2007] $) $( [22-May-2007] $)

    $( Existence of the negative of a complex number.  (Contributed by Eric
       Schmidt, 21-May-2007.) $)
    cnegext $p |- ( A e. CC -> E. x e. CC ( A + x ) = 0 ) $=
      ( va vb vy vc vd cc wcel cv ci cmul co caddc wceq cr wrex cc0 axcnre wa
      opreq1 eqeq1d rexbidv cnegextlem2 cnegextlem3 ad2ant2lr axaddrcl
      axrnegex syl ad2ant2rl adantr opreq2 rcla4ev axaddcl recnt axicn axmulcl
      mpan syl2an adantlr adantll axaddass sylan2 ad2antrr ad2antlr pm3.27
      syl3anc simpll adantl axdistr mp3an1 opreq2d eqtr4d opreq1d eqtr3d
      anim12i adantlrr eqtr2d adantllr biimpa sylanc exp31 r19.23adv mpd exp32
      mpi syl5cbir r19.23aivv ) BHIBCJZKDJZLMZNMZOZDPQCPQBAJZNMZROZAHQZCDBSWMWQ
      CDPPWMWQWLWNNMZROZAHQZWIPIZWJPIZTZWMWPWSAHWMWOWRRBWLWNNUAUBUCXCKEJZLMZPIZ
      EPQWTEUDXCXFWTEPXCXDPIZXFWTXCXGXFTZTZWJFJZNMZXDOZFPQZWTXBXGXMXAXFEDFUEUFX
      IXLWTFPXIXJPIZXLWTXIXNXLTZTZWIXENMZGJZNMZROZGPQZWTXIYAXOXAXFYAXBXGXAXFTXQ
      PIYAWIXEUGGXQUHUIUJUKXPXTWTGPXPXRPIZXTWTKXJLMZXRNMZHIZWLYDNMZROZWTXPYBTZX
      TTWSYGAYDHWNYDOWRYFRWNYDWLNULUBUMYHYEXTXOYBYEXIXNYBYEXLYCHIZXRHIZYEXNYBYC
      XRUNXNXJHIZYIXJUOZKHIZYKYIUPKXJUQURZUIXRUOZUSUTVAUKYHXTYGYHXSYFRXCXOYBXSY
      FOXHXCXOTYBTYFWIKXKLMZNMZXRNMZXSXCXNYBYFYROZXLWIHIZWJHIZTZYKTZYJYSXCXNTYB
      UUCYJTZWLYCNMZXRNMZYFYRWLHIZYIYJUUFYFOUUDWLYCXRVBUUBUUGYKYJYTWKHIZUUGUUAW
      IWKUNYMUUAUUHUPKWJUQURZVCVDYKYIUUBYJYNVEUUCYJVFVGUUDUUEYQXRNUUCUUEYQOYJUU
      CUUEWIWKYCNMZNMZYQYTUUHYIUUEUUKOUUCWIWKYCVBYTUUAYKVHUUAUUHYTYKUUIVEYKYIUU
      BYNVIVGUUCYPUUJWINUUAYKYPUUJOZYTYMUUAYKUULUPKWJXJVJVKVAVLVMUKVNVOXCUUBXNY
      KXAYTXBUUAWIUOWJUOVPYLVPYOUSVQXOYRXSOZXCYBXLUUMXNXLYQXQXRNXLYPXEWINXKXDKL
      ULVLVNVIVEVRVSUBVTWAWBWCWDWEWCWDWEWCWFWGWHUI $.
      $( [11-Mar-2008] $) $( [22-May-2007] $)
  $}

  ${
    $d x A $.
    negex.1 $e |- A e. CC $.
    $( Existence of negatives. $)
    cnegex $p |- E. x e. CC ( A + x ) = 0 $=
      ( cc wcel cv caddc co cc0 wceq wrex cnegext ax-mp ) BDEBAFGHIJADKCABLM $.
      $( [23-Nov-1994] $)
  $}

  $( 0 is a complex number.  (Proved without referencing ~ ax1cn by Eric
     Schmidt, 11-Apr-2007.  Compare ~ 0cn .) $)
  0cnALT $p |- 0 e. CC $=
    ( vx ci cv caddc co cc0 wceq cc wrex wcel axicn cnegext ax-mp wa axaddcl
    mpan adantr wb eleq1 adantl mpbid r19.23aiva ) BACZDEZFGZAHIZFHJZBHJZUFKABL
    MUEUGAHUCHJZUENUDHJZUGUIUJUEUHUIUJKBUCOPQUEUJUGRUIUDFHSTUAUBM $.
    $( [12-Apr-2007] $) $( [19-Feb-2005] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    addcan.1 $e |- A e. CC $.
    addcan.2 $e |- B e. CC $.
    addcan.3 $e |- C e. CC $.
    $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18. $)
    addcan $p |- ( ( A + B ) = ( A + C ) <-> B = C ) $=
      ( vx caddc co wceq cv cc0 cc wrex wi cnegex wcel wa wb axaddass mp3an3
      eqeq12d mpan2 opreq2 syl5bir adantr axaddcom mpan eqeq1d opreq1 addid2
      syl6eq syl6bi imp sylibd r19.23aiva ax-mp impbi ) ABHIZACHIZJZBCJZAGKZHIZ
      LJZGMNVAVBOZGADPVEVFGMVCMQZVERVAVCAHIZBHIZVHCHIZJZVBVGVAVKOVEVGVKVCUSHIZV
      CUTHIZJZVAVGAMQZVKVNSDVGVORVIVLVJVMVGVOBMQVIVLJEVCABTUAVGVOCMQVJVMJFVCACT
      UAUBUCUSUTVCHUDUEUFVGVEVKVBSZVGVEVHLJZVPVGVDVHLVOVGVDVHJDAVCUGUHUIVQVIBVJ
      CVQVILBHIBVHLBHUJBEUKULVQVJLCHICVHLCHUJCFUKULUBUMUNUOUPUQBCAHUDUR $.
      $( [27-Oct-1999] $)
  $}

  $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18.  This
     proof illustrates how ~ dedth3h can be used to convert
     the assumptions of ~ addcan into antecedents.  This general
     method can be used to convert deductions into theorems as needed. $)
  addcant $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) = ( A + C ) <-> B = C ) ) $=
    ( cc wcel caddc co wceq wb cc0 cif opreq1 eqeq12d bibi1d opreq2 eqeq1d
    eqeq1 bibi12d eqeq2d eqeq2 0cn elimel addcan dedth3h ) ADEZBDEZCDEZABFGZACF
    GZHZBCHZIUEAJKZBFGZULCFGZHZUKIABCJJJULUFBJKZFGZUNHZUPCHZIUQULUGCJKZFGZHZUPU
    THZIAULHZUJUOUKVDUHUMUIUNAULBFLAULCFLMNBUPHZUOURUKUSVEUMUQUNBUPULFOPBUPCQRC
    UTHZURVBUSVCVFUNVAUQCUTULFOSCUTUPTRULUPUTAJDUAUBBJDUAUBCJDUAUBUCUD $.
    $( [22-Nov-1994] $)

  $( Cancellation law for addition. $)
  addcan2t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + C ) = ( B + C ) <-> A = B ) ) $=
    ( cc wcel caddc co wceq wb w3a axaddcom 3adant3 3adant2 eqeq12d addcant
    bitr3d 3coml ) CDEZADEZBDEZACFGZBCFGZHZABHZIRSTJZCAFGZCBFGZHUCUDUEUFUAUGUBR
    SUFUAHTCAKLRTUGUBHSCBKMNCABOPQ $.
    $( [23-Aug-2004] $) $( [30-Jul-2004] $)

  ${
    addcan2.1 $e |- A e. CC $.
    addcan2.2 $e |- B e. CC $.
    addcan2.3 $e |- C e. CC $.
    $( Cancellation law for addition. $)
    addcan2 $p |- ( ( A + C ) = ( B + C ) <-> A = B ) $=
      ( cc wcel caddc co wceq wb addcan2t mp3an ) AGHBGHCGHACIJBCIJKABKLDEFABCM
      N $.
      $( [14-May-2003] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    negeu.1 $e |- A e. CC $.
    negeu.2 $e |- B e. CC $.
    $( Existential uniqueness of negatives.  Theorem I.2 of [Apostol]
       p. 18. $)
    negeu $p |- E! x e. CC ( A + x ) = B $=
      ( vy cv caddc co wceq cc wreu wrex wa wi wral opreq2 eqeq1d reu4 cc0
      cnegex wcel axaddcl mpan2 risset sylib axaddass mp3an13 eqcomd sylan9eqr
      opreq1 addid2 syl6eq exp32 impcom a1d r19.21aiv ex r19.22 syl6 mpid
      r19.23aiv ax-mp w3a addcant eqtr3t syl5bi mp3an1 rgen2a mpbir2an ) BAGZHI
      ZCJZAKLVMAKMZVMBFGZHIZCJZNZVKVOJZOZFKPAKPVMVQAFKVSVLVPCVKVOBHQRSVPTJZFKMV
      NFBDUAWAVNFKVOKUBZWAVKVOCHIZJZAKMZVNWBWCKUBZWEWBCKUBZWFEVOCUCUDAWCKUEUFWB
      WAWDVMOZAKPZWEVNOWBWAWIWBWANZWHAKWJWHVKKUBZWAWBWHWAWBWDVMWBWDNWAVLVPCHIZC
      WDWBVLBWCHIZWLVKWCBHQWBWLWMBKUBZWBWGWLWMJDEBVOCUGUHUIUJWAWLTCHICVPTCHUKCE
      ULUMUJUNUOUPUQURWDVMAKUSUTVAVBVCVTAFKWNWKWBVTDWNWKWBVDVLVPJVSVRBVKVOVEVLV
      PCVFVGVHVIVJ $.
      $( [22-Nov-1994] $)
  $}

  ${
    $d x y z w $.
    $( Define subtraction.  Theorem ~ subvalt shows it value (and describes how
       this definition works), theorem ~ subadd relates it to addition, and
       theorems ~ subcl and ~ resubcl prove its closure laws. $)
    df-sub $a |- - = { <. <. x , y >. , z >. |
                   ( ( x e. CC /\ y e. CC ) /\
                   z = U. { w e. CC | ( y + w ) = x } ) } $.
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Value of subtraction, which is the (unique) element ` x ` such that
       ` B + x = A ` .  The notation ` U. { x e. CC | ( B + x ) = A } `
       may at first seem cryptic but is actually a way of saying
       "the element ` x ` such that ` B + x = A `" (see Theorem 8.17 of
       [Quine] p. 56);  this works because there is only one such ` x ` as
       shown by ~ negeu , allowing us to exploit ~ eusn and ~ unisn (which you
       will find if you trace back the proof of ~ subcl ). $)
    subvalt $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( A - B ) = U. { x e. CC | ( B + x ) = A } ) $=
      ( vy vz vw cc cv caddc co wceq crab cuni cmin axcnex rabex uniex eqeq2
      rabbisdv unieqd opreq1 eqeq1d df-sub oprabval2 ) DEFBCGGEHZAHZIJZDHZKZAGL
      ZMCUFIJZBKZAGLZMNUGBKZAGLZMUMULAGOPQUHBKZUJUOUPUIUNAGUHBUGRSTUECKZUOUMUQU
      NULAGUQUGUKBUECUFIUAUBSTDEFAUCUD $.
      $( [4-Aug-2007] $) $( [4-Aug-2007] $)
  $}

  $( Define the negative of a number (unary minus).  We use different symbols
     for unary minus ( ` -u ` ) and subtraction ( ` - ` ) to prevent syntax
     ambiguity.  See ~ cneg for a discussion of this. $)
  df-neg $a |- -u A = ( 0 - A ) $.

  $( Equality theorem for negatives. $)
  negeq $p |- ( A = B -> -u A = -u B ) $=
    ( wceq cc0 cmin co cneg opreq2 df-neg 3eqtr4g ) ABCDAEFDBEFAGBGABDEHAIBIJ
    $.
    $( [10-Feb-1995] $)

  ${
    negeqi.1 $e |- A = B $.
    $( Equality inference for negatives. $)
    negeqi $p |- -u A = -u B $=
      ( wceq cneg negeq ax-mp ) ABDAEBEDCABFG $.
      $( [14-Feb-1995] $)
  $}

  ${
    negeqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for negatives. $)
    negeqd $p |- ( ph -> -u A = -u B ) $=
      ( wceq cneg negeq syl ) ABCEBFCFEDBCGH $.
      $( [14-May-1999] $)
  $}

  ${
    $d y A $.  $d x y $.
    hbneg.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for the negative of a complex
       number. $)
    hbneg $p |- ( y e. -u A -> A. x y e. -u A ) $=
      ( cneg cc0 cmin co df-neg cv wcel ax-17 hbopr hbxfr ) ABCEFCGHCIABFCGBJZF
      KALOGKALDMN $.
      $( [12-Jun-2005] $) $( [12-Jun-2005] $)
  $}

  ${
    $d y z A $.  $d y ph $.  $d x y z $.
    hbnegd.1 $e |- ( ph -> A. x ph ) $.
    hbnegd.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    $( Deduction version of ~ hbneg . $)
    hbnegd $p |- ( ph -> ( y e. -u A -> A. x y e. -u A ) ) $=
      ( vz cv wcel wal cab cneg wi hba1 hbab hbneg a1i wceq 19.21aiv abidhb
      syl negeqd eleq2d albid 3imtr3d ) ACHZGHDIZBJZGKZLZIZUKBJZUFDLZIZUNBJUKUL
      MABCUIUHBGCUGBNOPQAUJUMUFAUIDAUFDIZUOBJMZCJUIDRAUPCFSBCGDTUAUBUCZAUKUNBEU
      QUDUE $.
      $( [2-Mar-2008] $) $( [29-Feb-2008] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d y z C $.  $d x y z $.
    $( Move class substitution in and out of the negative of a number. $)
    csbnegg $p |- ( A e. C -> [_ A / x ]_ -u B = -u [_ A / x ]_ B ) $=
      ( vy vz wcel cv cneg csb ax-17 hbcsb1g hbnegd wceq csbeq1a negeqd wex
      a9e visset hbcsb1 hbneg hbeq eqtr3d 19.23ai ax-mp syl5eq csbiegf csbcog
      3eqtr3d ) BDGZEBAEHZCIZJZJEBAUKCJZJZIZABULJABCJZIEFBUMUPDUJEFUOUJEKEFBUND
      FHZBGEKLMUKBNZUNIZUPUMUSUNUOEBUNOPAHUKNZAQUMUTNZAERVAVBAAFFUMUTAFUKULESZU
      RUKGAKZTAFUNAFUKCVCVDTUAUBVAULUMUTAUKULOVACUNAUKCOPUCUDUEUFUGAEBULDUHUJUO
      UQAEBCDUHPUI $.
      $( [23-Mar-2008] $) $( [1-Mar-2008] $)
  $}

  $( A negative is a set. $)
  negex $p |- -u A e. V $=
    ( cneg cc0 cmin co cvv df-neg oprex eqeltr ) ABCADEFAGCADHI $.
    $( [27-May-2005] $) $( [4-Apr-2005] $)

  ${
    $d x A $.  $d x B $.
    subcl.1 $e |- A e. CC $.
    subcl.2 $e |- B e. CC $.
    $( Closure law for subtraction. $)
    subcl $p |- ( A - B ) e. CC $=
      ( vx cmin co cv caddc wceq cc crab cuni wcel subvalt mp2an wreu negeu
      reucl ax-mp eqeltr ) ABFGZBEHIGAJZEKLMZKAKNBKNUBUDJCDEABOPUCEKQUDKNEBADCR
      UCEKSTUA $.
      $( [26-Nov-1994] $)
  $}

  $( Closure law for subtraction. $)
  subclt $p |- ( ( A e. CC /\ B e. CC ) -> ( A - B ) e. CC ) $=
    ( cc wcel cmin co cc0 cif wceq opreq1 eleq1d opreq2 0cn elimel subcl
    dedth2h ) ACDZBCDZABEFZCDQAGHZBEFZCDTRBGHZEFZCDABGGATISUACATBEJKBUBIUAUCCBU
    BTELKTUBAGCMNBGCMNOP $.
    $( [10-May-1999] $)

  $( Closure law for negative. $)
  negclt $p |- ( A e. CC -> -u A e. CC ) $=
    ( cc wcel cc0 cmin co cneg 0cn subclt mpan df-neg syl5eqel ) ABCZDAEFZBAGDB
    CMNBCHDAIJAKL $.
    $( [6-Aug-2003] $)

  ${
    negcl.1 $e |- A e. CC $.
    $( Closure law for negative. $)
    negcl $p |- -u A e. CC $=
      ( cc wcel cneg negclt ax-mp ) ACDAECDBAFG $.
      $( [26-Nov-1994] $)
  $}

  ${
    $d w x y z $.
    $( Subtraction is an operation on the complex numbers. $)
    subopr $p |- - : ( CC X. CC ) --> CC $=
      ( vx vy vz vw cc cv caddc co wceq crab cuni cmin df-sub wcel wa subvalt
      subclt eqeltrrd foprab ) ABCEEBFZDFGHAFZIDEJKZELABCDMUAENTENOUATLHUBEDUAT
      PUATQRS $.
      $( [9-Aug-2007] $) $( [4-Aug-2007] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    subadd.1 $e |- A e. CC $.
    subadd.2 $e |- B e. CC $.
    subadd.3 $e |- C e. CC $.
    $( Relationship between subtraction and addition. $)
    subadd $p |- ( ( A - B ) = C <-> ( B + C ) = A ) $=
      ( vx cc wcel cmin co wceq caddc wb cv eqeq2 opreq2 eqeq1d bibi12d crab
      cuni wreu negeu reuuni1 mpan2 subvalt mp2an eqeq1i syl6rbbr vtoclga
      ax-mp ) CHIABJKZCLZBCMKZALZNZFULGOZLZBUQMKZALZNUPGCHUQCLZURUMUTUOUQCULPVA
      USUNAUQCBMQRSUQHIZUTUTGHTUAZUQLZURVBUTGHUBUTVDNGBAEDUCUTGHUDUEULVCUQAHIBH
      IULVCLDEGABUFUGUHUIUJUK $.
      $( [26-Nov-1994] $)

    ${
      subaddri.4 $e |- ( B + C ) = A $.
      $( Relationship between subtraction and addition. $)
      subaddri $p |- ( A - B ) = C $=
        ( cmin co wceq caddc subadd mpbir ) ABHICJBCKIAJGABCDEFLM $.
        $( [19-Dec-2006] $) $( [16-Dec-2006] $)
    $}

    $( Relationship between subtraction and addition. $)
    subadd2 $p |- ( ( A - B ) = C <-> ( C + B ) = A ) $=
      ( cmin co wceq caddc subadd addcom eqeq1i bitr ) ABGHCIBCJHZAICBJHZAIABCD
      EFKOPABCEFLMN $.
      $( [15-Dec-2006] $) $( [15-Dec-2006] $)

    $( Swap subtrahend and result of subtraction. $)
    subsub23 $p |- ( ( A - B ) = C <-> ( A - C ) = B ) $=
      ( caddc co wceq cmin addcom eqeq1i subadd 3bitr4 ) BCGHZAICBGHZAIABJHCIAC
      JHBIOPABCEFKLABCDEFMACBDFEMN $.
      $( [7-Oct-1999] $)
  $}

  $( Relationship between subtraction and addition. $)
  subaddt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) = C <-> ( B + C ) = A ) ) $=
    ( cc wcel cmin co wceq caddc wb cc0 cif opreq1 eqeq1d eqeq2 bibi12d opreq2
    0cn elimel subadd dedth3h ) ADEZBDEZCDEZABFGZCHZBCIGZAHZJUBAKLZBFGZCHZUGUIH
    ZJABCKKKUIUCBKLZFGZCHZUMCIGZUIHZJUNUDCKLZHZUMURIGZUIHZJAUIHZUFUKUHULVBUEUJC
    AUIBFMNAUIUGOPBUMHZUKUOULUQVCUJUNCBUMUIFQNVCUGUPUIBUMCIMNPCURHZUOUSUQVACURU
    NOVDUPUTUICURUMIQNPUIUMURAKDRSBKDRSCKDRSTUA $.
    $( [20-Jan-1997] $)

  $( Swap subtrahend and result of subtraction. $)
  subsub23t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) = C <-> ( A - C ) = B ) ) $=
    ( cc wcel w3a caddc co wceq cmin axaddcom 3adant1 eqeq1d subaddt wb 3com23
    3bitr4d ) ADEZBDEZCDEZFZBCGHZAICBGHZAIZABJHCIACJHBIZUAUBUCASTUBUCIRBCKLMABC
    NRTSUEUDOACBNPQ $.
    $( [14-Dec-2007] $) $( [14-Dec-2007] $)

  $( Subtraction and addition of equals. $)
  pncan3t $p |- ( ( A e. CC /\ B e. CC ) -> ( A + ( B - A ) ) = B ) $=
    ( cc wcel wa cmin co wceq caddc eqid wb subaddt pm3.27 pm3.26 subclt
    ancoms syl3anc mpbii ) ACDZBCDZEZBAFGZUBHZAUBIGBHZUBJTSUBCDZUCUDKUABAUBLSTM
    STNTSUEBAOPQR $.
    $( [15-Mar-2005] $) $( [14-Mar-2005] $)

  ${
    pncan3.1 $e |- A e. CC $.
    pncan3.2 $e |- B e. CC $.
    $( Subtraction and addition of equals. $)
    pncan3 $p |- ( A + ( B - A ) ) = B $=
      ( cc wcel cmin co caddc wceq pncan3t mp2an ) AEFBEFABAGHIHBJCDABKL $.
      $( [26-Nov-1994] $)
  $}

  $( Addition of a number and its negative. $)
  negidt $p |- ( A e. CC -> ( A + -u A ) = 0 ) $=
    ( cc wcel cc0 cmin co caddc cneg wceq 0cn pncan3t mpan2 df-neg opreq2i
    syl5eq ) ABCZADAEFZGFZDAAHZGFPDBCRDIJADKLSQAGAMNO $.
    $( [15-Mar-2005] $) $( [14-Mar-2005] $)

  ${
    negid.1 $e |- A e. CC $.
    $( Addition of a number and its negative. $)
    negid $p |- ( A + -u A ) = 0 $=
      ( cc wcel cneg caddc co cc0 wceq negidt ax-mp ) ACDAAEFGHIBAJK $.
      $( [26-Nov-1994] $)
  $}

  ${
    negsub.1 $e |- A e. CC $.
    negsub.2 $e |- B e. CC $.
    $( Relationship between subtraction and negative.  Theorem I.3 of
       [Apostol] p. 18. $)
    negsub $p |- ( A + -u B ) = ( A - B ) $=
      ( cmin co cneg caddc negcl addcl addcom cc0 addass negid eqtr opreq2i
      addid1 3eqtr subaddri eqcomi ) ABEFABGZHFZABUBCDAUACBDIZJZBUBHFUBBHFZABUB
      DUDKUEAUABHFZHFALHFAAUABCUCDMUFLAHUFBUAHFLUABUCDKBDNOPACQROST $.
      $( [3-Dec-2005] $) $( [26-Nov-1994] $)
  $}

  $( Relationship between subtraction and negative.  Theorem I.3 of
     [Apostol] p. 18. $)
  negsubt $p |- ( ( A e. CC /\ B e. CC ) -> ( A + -u B ) = ( A - B ) ) $=
    ( cc wcel cneg caddc co cmin wceq cc0 cif opreq1 eqeq12d negeq opreq2d
    opreq2 0cn elimel negsub dedth2h ) ACDZBCDZABEZFGZABHGZIUAAJKZUCFGZUFBHGZIU
    FUBBJKZEZFGZUFUIHGZIABJJAUFIUDUGUEUHAUFUCFLAUFBHLMBUIIZUGUKUHULUMUCUJUFFBUI
    NOBUIUFHPMUFUIAJCQRBJCQRST $.
    $( [3-Dec-2005] $) $( [21-Jan-1997] $)

  $( Associative-type law for addition and subtraction. $)
  addsubasst $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( A + B ) - C ) = ( A + ( B - C ) ) ) $=
    ( cc wcel w3a caddc co cneg cmin wceq axaddass negclt syl3an3 wa negsubt
    axaddcl sylan 3impa 3adant1 opreq2d 3eqtr3d ) ADEZBDEZCDEZFZABGHZCIZGHZABUH
    GHZGHZUGCJHZABCJHZGHUCUDUHDEUIUKKUEABUHLCMNUCUDUEUIULKZUGDEUEUNUCUDOUGCPABQ
    RSUFUJUMAGUDUEUJUMKUCBCPTUAUB $.
    $( [1-Aug-2006] $) $( [6-Aug-2003] $)

  $( Law for addition and subtraction. $)
  addsubt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A + B ) - C ) = ( ( A - C ) + B ) ) $=
    ( cc wcel w3a caddc co cmin wceq wa axaddcom opreq1d 3adant3 addsubasst
    3com12 wi ex subclt syl5 exp3a com12 3imp 3eqtrd ) ADEZBDEZCDEZFABGHZCIHZBA
    GHZCIHZBACIHZGHZULBGHZUEUFUIUKJUGUEUFKUHUJCIABLMNUFUEUGUKUMJBACOPUEUFUGUMUN
    JZUFUEUGUOQUFUEUGUOUFULDEZUOUEUGKUFUPUOBULLRACSTUAUBUCUD $.
    $( [19-Aug-2001] $)

  $( Commutative/associative law for addition and subtraction. $)
  subadd23t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A - B ) + C ) = ( A + ( C - B ) ) ) $=
    ( cc wcel cmin co caddc wceq w3a addsubt addsubasst eqtr3d 3com23 ) ADEZCDE
    ZBDEZABFGCHGZACBFGHGZIOPQJACHGBFGRSACBKACBLMN $.
    $( [1-Feb-2007] $) $( [1-Feb-2007] $)

  $( Commutative/associative law for addition and subtraction. $)
  addsub12t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( A + ( B - C ) ) = ( B + ( A - C ) ) ) $=
    ( cc wcel cmin co caddc wceq w3a subadd23t wa axaddcom subclt sylan 3impa
    eqtr3d 3com23 ) ADEZCDEZBDEZABCFGHGZBACFGZHGZISTUAJUCBHGZUBUDACBKSTUAUEUDIZ
    UCDEUAUFSTLUCBMACNOPQR $.
    $( [1-Mar-2005] $) $( [8-Feb-2005] $)

  ${
    addsubass.1 $e |- A e. CC $.
    addsubass.2 $e |- B e. CC $.
    addsubass.3 $e |- C e. CC $.
    $( Associative-type law for subtraction and addition. $)
    addsubass $p |- ( ( A + B ) - C ) = ( A + ( B - C ) ) $=
      ( cc wcel caddc co cmin wceq addsubasst mp3an ) AGHBGHCGHABIJCKJABCKJIJLD
      EFABCMN $.
      $( [16-Sep-1999] $)

    $( Law for subtraction and addition. $)
    addsub $p |- ( ( A + B ) - C ) = ( ( A - C ) + B ) $=
      ( cc wcel caddc co cmin wceq addsubt mp3an ) AGHBGHCGHABIJCKJACKJBIJLDEFA
      BCMN $.
      $( [6-Aug-2003] $)
  $}

  $( Law for subtraction and addition. $)
  2addsubt $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
                 ( ( ( A + B ) + C ) - D ) = ( ( ( A + C ) - D ) + B ) ) $=
    ( cc wcel wa caddc co cmin wceq add23t 3expa adantrr opreq1d addsubt 3expb
    axaddcl sylan an4s eqtrd ) AEFZBEFZGZCEFZDEFZGGZABHICHIZDJIACHIZBHIZDJIZUID
    JIBHIZUGUHUJDJUDUEUHUJKZUFUBUCUEUMABCLMNOUBUEUCUFUKULKZUIEFZUCUFGUNUBUEGUOU
    CUFUNUIBDPQACRSTUA $.
    $( [8-Dec-2005] $) $( [20-Nov-2005] $)

  ${
    negneg.1 $e |- A e. CC $.
    $( A number is equal to the negative of its negative.  Theorem I.4 of
       [Apostol] p. 18. $)
    negneg $p |- -u -u A = A $=
      ( cneg caddc co cc0 negcl negid opreq2i opreq1i addass addid2 3eqtr3
      addid1 ) AACZOCZDEZDEZAFDEPAQFADOABGZHIAODEZPDEFPDERPTFPDABHJAOPBSOSGZKPU
      ALMABNM $.
      $( [8-Feb-1995] $)

    $( Subtraction of a number from itself. $)
    subid $p |- ( A - A ) = 0 $=
      ( cneg caddc co cmin cc0 negsub negid eqtr3 ) AACDEAAFEGAABBHABIJ $.
      $( [26-Nov-1994] $)

    $( Identity law for subtraction. $)
    subid1 $p |- ( A - 0 ) = A $=
      ( cc0 0cn addid2 subaddri ) ACABDBABEF $.
      $( [29-May-1999] $)

  $}

  $( A number is equal to the negative of its negative.  Theorem I.4 of
     [Apostol] p. 18. $)
  negnegt $p |- ( A e. CC -> -u -u A = A ) $=
    ( cc wcel cneg wceq cc0 cif negeq negeqd id eqeq12d 0cn elimel negneg
    dedth ) ABCZADZDZAEPAFGZDZDZSEAFASEZRUAASUBQTASHIUBJKSAFBLMNO $.
    $( [12-Jan-2002] $)

  $( Relationship between subtraction and negative. $)
  subnegt $p |- ( ( A e. CC /\ B e. CC ) -> ( A - -u B ) = ( A + B ) ) $=
    ( cc wcel wa cneg caddc co cmin wceq negsubt negclt sylan2 negnegt opreq2d
    adantl eqtr3d ) ACDZBCDZEABFZFZGHZATIHZABGHZRTCDUBUCJSATKBLMSUBUDJRSUABAGBN
    OPQ $.
    $( [4-Jun-2004] $) $( [10-May-2004] $)

  $( Subtraction of a number from itself. $)
  subidt $p |- ( A e. CC -> ( A - A ) = 0 ) $=
    ( cc wcel cmin co cc0 wceq cif opreq12 anidms eqeq1d 0cn elimel subid
    dedth ) ABCZAADEZFGPAFHZRDEZFGAFARGZQSFTQSGARARDIJKRAFBLMNO $.
    $( [8-Oct-1999] $)

  $( Identity law for subtraction. $)
  subid1t $p |- ( A e. CC -> ( A - 0 ) = A ) $=
    ( cc wcel cc0 cmin co wceq cif opreq1 id eqeq12d 0cn elimel subid1 dedth )
    ABCZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRADBLMNO $.
    $( [5-Jun-2004] $) $( [9-May-2004] $)

  $( Cancellation law for subtraction. $)
  pncant $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + B ) - B ) = A ) $=
    ( cc wcel wa caddc co cmin wceq addsubasst 3anidm23 cc0 subidt opreq2d
    ax0id sylan9eqr eqtrd ) ACDZBCDZEABFGBHGZABBHGZFGZARSTUBIABBJKSRUBALFGASUAL
    AFBMNAOPQ $.
    $( [23-May-2004] $) $( [10-May-2004] $)

  $( Cancellation law for subtraction. $)
  pncan2t $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + B ) - A ) = B ) $=
    ( cc wcel caddc co cmin wceq wa axaddcom opreq1d pncant eqtr3d ancoms ) BCD
    ZACDZABEFZAGFZBHOPIZBAEFZAGFRBSTQAGBAJKBALMN $.
    $( [20-Apr-2005] $) $( [17-Apr-2005] $)

  $( Cancellation law for subtraction. $)
  npcant $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A - B ) + B ) = A ) $=
    ( cc wcel wa caddc co cmin wceq addsubt 3anidm23 pncant eqtr3d ) ACDZBCDZEA
    BFGBHGZABHGBFGZANOPQIABBJKABLM $.
    $( [23-May-2004] $) $( [10-May-2004] $)

  $( Cancellation law for subtraction. $)
  npncant $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) + ( B - C ) ) = ( A - C ) ) $=
    ( cc wcel w3a cmin co caddc wceq addsubasst subclt 3adant3 3simp2 3simp3
    syl3anc wa npcant opreq1d eqtr3d ) ADEZBDEZCDEZFZABGHZBIHZCGHZUEBCGHIHZACGH
    ZUEDEZUBUCUGUHJUDUEBCKUAUBUJUCABLMUAUBUCNUAUBUCOPUAUBUGUIJUCUAUBQUFACGABRSM
    T $.
    $( [19-Feb-2005] $) $( [8-Feb-2005] $)

  $( Cancellation law for subtraction. $)
  nppcant $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( ( A - B ) + C ) + B ) = ( A + C ) ) $=
    ( cc wcel w3a cmin co caddc wceq add23t subclt 3adant3 3simp3 3simp2
    syl3anc wa npcant opreq1d eqtrd ) ADEZBDEZCDEZFZABGHZCIHBIHZUEBIHZCIHZACIHZ
    UEDEZUCUBUFUHJUDUECBKUAUBUJUCABLMUAUBUCNUAUBUCOPUAUBUHUIJUCUAUBQUGACIABRSMT
    $.
    $( [8-Sep-2005] $) $( [1-Sep-2005] $)

  $( Cancellation law for subtraction. $)
  subcan2t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - C ) = ( B - C ) <-> A = B ) ) $=
    ( cc wcel w3a cneg caddc co wceq cmin negsubt 3adant2 3adant1 eqeq12d wb
    addcan2t negclt syl3an3 bitr3d ) ADEZBDEZCDEZFZACGZHIZBUEHIZJZACKIZBCKIZJAB
    JZUDUFUIUGUJUAUCUFUIJUBACLMUBUCUGUJJUABCLNOUAUBUEDEUHUKPUCABUEQCRST $.
    $( [2-Mar-2005] $) $( [8-Feb-2005] $)

  $( If the difference between two numbers is zero, they are equal. $)
  subeq0t $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A - B ) = 0 <-> A = B ) ) $=
    ( cc wcel wa cmin co wceq cc0 subidt adantl eqeq2d wb subcan2t 3anidm23
    bitr3d ) ACDZBCDZEZABFGZBBFGZHZTIHABHZSUAITRUAIHQBJKLQRUBUCMABBNOP $.
    $( [16-Nov-1999] $)

  ${
    subneg.1 $e |- A e. CC $.
    subneg.2 $e |- B e. CC $.
    $( Relationship between subtraction and negative. $)
    subneg $p |- ( A - -u B ) = ( A + B ) $=
      ( cc wcel cneg cmin co caddc wceq subnegt mp2an ) AEFBEFABGHIABJIKCDABLM
      $.
      $( [3-Dec-2005] $) $( [1-Dec-2005] $)

    $( If the difference between two numbers is zero, they are equal. $)
    subeq0 $p |- ( ( A - B ) = 0 <-> A = B ) $=
      ( cc wcel cmin co cc0 wceq wb subeq0t mp2an ) AEFBEFABGHIJABJKCDABLM $.
      $( [8-May-1999] $)

    $( Negative is one-to-one. $)
    neg11 $p |- ( -u A = -u B <-> A = B ) $=
      ( cneg wceq cc0 cmin co caddc df-neg eqeq12i 0cn subcl subadd opreq2i
      negsub eqtr3 eqeq1i subeq0 bitr 3bitr ) AEZBEZFGAHIZGBHIZFAUFJIZGFZABFZUC
      UEUDUFAKBKZLGAUFMCGBMDNOUHABHIZGFUIUGUKGAUDJIUGUKUDUFAJUJPABCDQRSABCDTUAU
      B $.
      $( [1-Aug-1999] $)

    $( Negative contraposition law. $)
    negcon1 $p |- ( -u A = B <-> -u B = A ) $=
      ( cneg wceq negneg eqeq1i negcl neg11 eqcom 3bitr3 ) AEZEZBEZFAOFMBFOAFNA
      OACGHMBACIDJAOKL $.
      $( [25-Aug-1999] $)

    $( Negative contraposition law. $)
    negcon2 $p |- ( A = -u B <-> B = -u A ) $=
      ( cneg wceq negcon1 eqcom 3bitr4 ) BEZAFAEZBFAJFBKFBADCGAJHBKHI $.
      $( [25-Aug-1999] $)
  $}

  $( Negative is one-to-one. $)
  neg11t $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A = -u B <-> A = B ) ) $=
    ( cc wcel cneg wceq wb cc0 cif negeq eqeq1d eqeq1 bibi12d eqeq2d eqeq2 0cn
    elimel neg11 dedth2h ) ACDZBCDZAEZBEZFZABFZGTAHIZEZUCFZUFBFZGUGUABHIZEZFZUF
    UJFZGABHHAUFFZUDUHUEUIUNUBUGUCAUFJKAUFBLMBUJFZUHULUIUMUOUCUKUGBUJJNBUJUFOMU
    FUJAHCPQBHCPQRS $.
    $( [10-Feb-2005] $) $( [8-Feb-2005] $)

  $( Negative contraposition law. $)
  negcon1t $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A = B <-> -u B = A ) ) $=
    ( cc wcel wa cneg wceq wb neg11t negclt sylan negnegt adantr eqeq1d bitr3d
    eqcom syl6bb ) ACDZBCDZEZAFZBGZABFZGZUCAGTUAFZUCGZUBUDUACDSUFUBHRUABIAJKTUE
    AUCRUEAGSALMNOAUCPQ $.
    $( [5-Jun-2004] $) $( [9-May-2004] $)

  $( Negative contraposition law. $)
  negcon2t $p |- ( ( A e. CC /\ B e. CC ) -> ( A = -u B <-> B = -u A ) ) $=
    ( cc wcel wa cneg wceq negcon1t eqcom syl6rbbr syl6bb ) ACDBCDEZABFZGZAFZBG
    ZBOGLPMAGNABHAMIJOBIK $.
    $( [17-Nov-2004] $) $( [14-Nov-2004] $)

  $( Cancellation law for subtraction. $)
  subcant $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) = ( A - C ) <-> B = C ) ) $=
    ( cc wcel w3a cneg caddc co wceq cmin wb addcant negclt syl3an3 syl3an2
    negsubt 3adant3 3adant2 eqeq12d neg11t 3adant1 3bitr3d ) ADEZBDEZCDEZFZABGZ
    HIZACGZHIZJZUHUJJZABKIZACKIZJBCJZUDUHDEZUFULUMLZUEUDUQUJDEURUFAUHUJMCNOBNPU
    GUIUNUKUOUDUEUIUNJUFABQRUDUFUKUOJUEACQSTUEUFUMUPLUDBCUAUBUC $.
    $( [2-Aug-2006] $) $( [8-Feb-2005] $)

  ${
    subcan.1 $e |- A e. CC $.
    subcan.2 $e |- B e. CC $.
    subcan.3 $e |- C e. CC $.
    $( Cancellation law for subtraction. $)
    subcan $p |- ( ( A - B ) = ( A - C ) <-> B = C ) $=
      ( cc wcel cmin co wceq wb subcant mp3an ) AGHBGHCGHABIJACIJKBCKLDEFABCMN
      $.
      $( [11-Apr-2005] $) $( [8-Feb-2005] $)

    $( Cancellation law for subtraction. $)
    subcan2 $p |- ( ( A - C ) = ( B - C ) <-> A = B ) $=
      ( cc wcel cmin co wceq wb subcan2t mp3an ) AGHBGHCGHACIJBCIJKABKLDEFABCMN
      $.
      $( [11-Mar-2005] $) $( [8-Feb-2005] $)
  $}

  $( Minus 0 equals 0. $)
  neg0 $p |- -u 0 = 0 $=
    ( cc0 cneg cmin co df-neg 0cn subid eqtr ) ABAACDAAEAFGH $.
    $( [17-Jan-1997] $)

  ${
    $d x A $.
    renegcl.1 $e |- A e. RR $.
    $( Closure law for negative of reals. $)
    renegcl $p |- -u A e. RR $=
      ( vx cv cr wcel caddc co cc0 wceq wa wex cneg wrex axrnegex ax-mp df-rex
      mpbi cmin cc wb recnt 0cn recn subaddt mp3an12 syl df-neg eqeq1i syl5bb
      eleq1a sylbird imp 19.23aiv ) CDZEFZAUOGHIJZKZCLZAMZEFZUQCENZUSAEFVBBCAOP
      UQCEQRURVACUPUQVAUPUQUTUOJZVAUPIASHZUOJZUQVCUPUOTFZVEUQUAZUOUBITFATFVFVGU
      CABUDIAUOUEUFUGUTVDUOAUHUIUJUOEUTUKULUMUNP $.
      $( [17-Jan-1997] $)
  $}

