$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Subtraction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.
    negex.1 $e |- A e. CC $.
    $( Existence of negatives. $)
    cnegexi $p |- E. x e. CC ( A + x ) = 0 $=
      ( cc wcel cv caddc co cc0 wceq wrex cnegex ax-mp ) BDEBAFGHIJADKCABLM $.
      $( [23-Nov-1994] $)
  $}

  $( 0 is a complex number.  (Proved without referencing ~ ax-1cn by Eric
     Schmidt, 11-Apr-2007.  Compare ~ 0cn .) $)
  0cnALT $p |- 0 e. CC $=
    ( vx ci cv caddc co cc0 wceq cc wrex wcel ax-icn cnegex ax-mp wa addcl
    mpan adantr wb eleq1 adantl mpbid r19.23aiva ) BACZDEZFGZAHIZFHJZBHJZUFKABL
    MUEUGAHUCHJZUENUDHJZUGUIUJUEUHUIUJKBUCOPQUEUJUGRUIUDFHSTUAUBM $.
    $( [12-Apr-2007] $) $( [19-Feb-2005] $)


  ${
    $d x y A $.  $d x y B $.
    negeu.1 $e |- A e. CC $.
    negeu.2 $e |- B e. CC $.
    $( Existential uniqueness of negatives.  Theorem I.2 of [Apostol] p. 18. $)
    negeui $p |- E! x e. CC ( A + x ) = B $=
      ( vy cv caddc co wceq cc wreu wrex wa wi wral opreq2 eqeq1d reu4 cc0
      cnegexi wcel addcl mpan2 risset sylib addass mp3an13 eqcomd sylan9eqr
      opreq1 addid2i syl6eq exp32 impcom a1d r19.21aiv ex rexim syl6 mpid
      r19.23aiv ax-mp w3a eqtr3 addcan syl5ib mp3an1 rgen2a mpbir2an ) BAGZHIZC
      JZAKLVMAKMZVMBFGZHIZCJZNZVKVOJZOZFKPAKPVMVQAFKVSVLVPCVKVOBHQRSVPTJZFKMVNF
      BDUAWAVNFKVOKUBZWAVKVOCHIZJZAKMZVNWBWCKUBZWEWBCKUBZWFEVOCUCUDAWCKUEUFWBWA
      WDVMOZAKPZWEVNOWBWAWIWBWANZWHAKWJWHVKKUBZWAWBWHWAWBWDVMWBWDNWAVLVPCHIZCWD
      WBVLBWCHIZWLVKWCBHQWBWLWMBKUBZWBWGWLWMJDEBVOCUGUHUIUJWAWLTCHICVPTCHUKCEUL
      UMUJUNUOUPUQURWDVMAKUSUTVAVBVCVTAFKWNWKWBVTDVRVLVPJWNWKWBVDVSVLVPCVEBVKVO
      VFVGVHVIVJ $.
      $( [22-Nov-1994] $)
  $}

  ${
    $d x y z $.
    $( Define subtraction.  Theorem ~ subval shows it value (and describes how
       this definition works), theorem ~ subaddi relates it to addition, and
       theorems ~ subcli and ~ resubcli prove its closure laws. $)
    df-sub $a |- - = ( x e. CC , y e. CC |->
                   ( iota_ z e. CC ( y + z ) = x ) ) $.
  $}

  ${
    $d x y z A $.  $d x z B $.
    $( Value of subtraction, which is the (unique) element ` x ` such that
       ` B + x = A ` . $)
    subval $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( A - B ) = ( iota_ x e. CC ( B + x ) = A ) ) $=
      ( vy vz cc cv caddc co wceq crio cmin eqeq2 riotabidv opreq1 eqeq1d
      df-sub riotaex ovmpt2 ) DEBCFFEGZAGZHIZDGZJZAFKCUAHIZBJZAFKLUBBJZAFKUCBJU
      DUGAFUCBUBMNTCJZUGUFAFUHUBUEBTCUAHOPNDEAQUFAFRS $.
      $( [2-Nov-2013] $) $( [4-Aug-2007] $)
  $}

  $( Define the negative of a number (unary minus).  We use different symbols
     for unary minus ( ` -u ` ) and subtraction ( ` - ` ) to prevent syntax
     ambiguity.  See ~ cneg for a discussion of this. $)
  df-neg $a |- -u A = ( 0 - A ) $.

  $( Equality theorem for negatives. $)
  negeq $p |- ( A = B -> -u A = -u B ) $=
    ( wceq cc0 cmin co cneg opreq2 df-neg 3eqtr4g ) ABCDAEFDBEFAGBGABDEHAIBIJ
    $.
    $( [10-Feb-1995] $)

  ${
    negeqi.1 $e |- A = B $.
    $( Equality inference for negatives. $)
    negeqi $p |- -u A = -u B $=
      ( wceq cneg negeq ax-mp ) ABDAEBEDCABFG $.
      $( [14-Feb-1995] $)
  $}

  ${
    negeqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for negatives. $)
    negeqd $p |- ( ph -> -u A = -u B ) $=
      ( wceq cneg negeq syl ) ABCEBFCFEDBCGH $.
      $( [14-May-1999] $)
  $}

  ${
    $d y A $.  $d x y $.
    hbneg.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for the negative of a complex
       number. $)
    hbneg $p |- ( y e. -u A -> A. x y e. -u A ) $=
      ( cneg cc0 cmin co df-neg cv wcel ax-17 hbopr hbxfreq ) ABCEFCGHCIABFCGBJ
      ZFKALOGKALDMN $.
      $( [12-Jun-2005] $)
  $}

  ${
    $d y z A $.  $d y ph $.  $d x y z $.
    hbnegd.1 $e |- ( ph -> A. x ph ) $.
    hbnegd.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    $( Deduction version of ~ hbneg .  (The proof was shortened by Andrew
       Salmon, 22-Oct-2011.) $)
    hbnegd $p |- ( ph -> ( y e. -u A -> A. x y e. -u A ) ) $=
      ( cv cc0 cmin co wcel wal cneg a17d hboprd df-neg eleq2i albii 3imtr4g )
      ACGZHDIJZKZUBBLTDMZKZUDBLABCHDIEATHKBNATIKBNFOUCUATDPQZUDUBBUERS $.
      $( [22-Oct-2011] $) $( [29-Feb-2008] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d y z C $.  $d x y z $.
    $( Move class substitution in and out of the negative of a number.  (The
       proof was shortened by Andrew Salmon, 22-Oct-2011.) $)
    csbnegg $p |- ( A e. V -> [_ A / x ]_ -u B = -u [_ A / x ]_ B ) $=
      ( wcel cc0 cmin co csb cneg csbopr2g df-neg csbeq2i wceq a1i 3eqtr4d ) BD
      EZABFCGHZIFABCIZGHZABCJZISJZABFCDGKABUARDCLMUBTNQSLOP $.
      $( [22-Oct-2011] $) $( [1-Mar-2008] $)
  $}

  $( A negative is a set. $)
  negex $p |- -u A e. _V $=
    ( cneg cc0 cmin co cvv df-neg oprex eqeltri ) ABCADEFAGCADHI $.
    $( [4-Apr-2005] $)

  ${
    $d x A $.  $d x B $.
    subcl.1 $e |- A e. CC $.
    subcl.2 $e |- B e. CC $.
    $( Closure law for subtraction. $)
    subcli $p |- ( A - B ) e. CC $=
      ( vx cmin co cv caddc wceq cc crio wcel subval mp2an wreu negeui ax-cnex
      riotaclb mpbi eqeltri ) ABFGZBEHIGAJZEKLZKAKMBKMUBUDJCDEABNOUCEKPUDKMEBAD
      CQUCEKRSTUA $.
      $( [24-Nov-2011] $) $( [26-Nov-1994] $)
  $}

  $( Closure law for subtraction. $)
  subcl $p |- ( ( A e. CC /\ B e. CC ) -> ( A - B ) e. CC ) $=
    ( cc wcel cmin co cc0 cif wceq opreq1 eleq1d opreq2 0cn elimel subcli
    dedth2h ) ACDZBCDZABEFZCDQAGHZBEFZCDTRBGHZEFZCDABGGATISUACATBEJKBUBIUAUCCBU
    BTELKTUBAGCMNBGCMNOP $.
    $( [10-May-1999] $)

  $( Closure law for negative. $)
  negcl $p |- ( A e. CC -> -u A e. CC ) $=
    ( cc wcel cneg cc0 cmin co df-neg 0cn subcl mpan syl5eqel ) ABCZADEAFGZBAHE
    BCMNBCIEAJKL $.
    $( [6-Aug-2003] $)

  ${
    negcl.1 $e |- A e. CC $.
    $( Closure law for negative. $)
    negcli $p |- -u A e. CC $=
      ( cc wcel cneg negcl ax-mp ) ACDAECDBAFG $.
      $( [26-Nov-1994] $)
  $}

  ${
    $d x y z $.
    $( Subtraction is an operation on the complex numbers. $)
    subopr $p |- - : ( CC X. CC ) --> CC $=
      ( vy vz vx cv caddc co wceq cc crio wcel wral cxp cmin wf wa subval
      subcl eqeltrrd rgen2a df-sub foprab2a mpbi ) ADZBDEFCDZGBHIZHJZAHKCHKHHLH
      MNUFCAHUDHJUCHJOUDUCMFUEHBUDUCPUDUCQRSCAHHUEHMCABTUAUB $.
      $( [16-Nov-2013] $) $( [4-Aug-2007] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    subadd.1 $e |- A e. CC $.
    subadd.2 $e |- B e. CC $.
    subadd.3 $e |- C e. CC $.
    $( Relationship between subtraction and addition. $)
    subaddi $p |- ( ( A - B ) = C <-> ( B + C ) = A ) $=
      ( vx vy cmin co wceq cv caddc cc crio wcel subval mp2an eqeq1i wreu wb
      negeui ax-17 wal wi a1i opreq2 eqeq1d riota2f bitr4i ) ABIJZCKBGLZMJZAKZG
      NOZCKZBCMJZAKZUKUOCANPBNPUKUOKDEGABQRSCNPZUNGNTURUPUAFGBAEDUBUNURGHNCHLCP
      GUCURURGUDUEUSURGUCUFULCKUMUQAULCBMUGUHUIRUJ $.
      $( [24-Nov-2011] $) $( [26-Nov-1994] $)

    ${
      subaddri.4 $e |- ( B + C ) = A $.
      $( Relationship between subtraction and addition. $)
      subaddrii $p |- ( A - B ) = C $=
        ( cmin co wceq caddc subaddi mpbir ) ABHICJBCKIAJGABCDEFLM $.
        $( [16-Dec-2006] $)
    $}

    $( Relationship between subtraction and addition. $)
    subadd2i $p |- ( ( A - B ) = C <-> ( C + B ) = A ) $=
      ( cmin co wceq caddc subaddi addcomi eqeq1i bitri ) ABGHCIBCJHZAICBJHZAIA
      BCDEFKOPABCEFLMN $.
      $( [15-Dec-2006] $)

    $( Swap subtrahend and result of subtraction. $)
    subsub23i $p |- ( ( A - B ) = C <-> ( A - C ) = B ) $=
      ( caddc co wceq cmin addcomi eqeq1i subaddi 3bitr4i ) BCGHZAICBGHZAIABJHC
      IACJHBIOPABCEFKLABCDEFMACBDFEMN $.
      $( [7-Oct-1999] $)
  $}

  $( Relationship between subtraction and addition. $)
  subadd $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) = C <-> ( B + C ) = A ) ) $=
    ( cc wcel cmin co wceq caddc wb cc0 cif opreq1 eqeq1d eqeq2 bibi12d opreq2
    0cn elimel subaddi dedth3h ) ADEZBDEZCDEZABFGZCHZBCIGZAHZJUBAKLZBFGZCHZUGUI
    HZJUIUCBKLZFGZCHZUMCIGZUIHZJUNUDCKLZHZUMURIGZUIHZJABCKKKAUIHZUFUKUHULVBUEUJ
    CAUIBFMNAUIUGOPBUMHZUKUOULUQVCUJUNCBUMUIFQNVCUGUPUIBUMCIMNPCURHZUOUSUQVACUR
    UNOVDUPUTUICURUMIQNPUIUMURAKDRSBKDRSCKDRSTUA $.
    $( [20-Jan-1997] $)

  $( Swap subtrahend and result of subtraction. $)
  subsub23 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) = C <-> ( A - C ) = B ) ) $=
    ( cc wcel w3a caddc co wceq cmin addcom 3adant1 eqeq1d subadd wb 3com23
    3bitr4d ) ADEZBDEZCDEZFZBCGHZAICBGHZAIZABJHCIACJHBIZUAUBUCASTUBUCIRBCKLMABC
    NRTSUEUDOACBNPQ $.
    $( [14-Dec-2007] $)

  $( Subtraction and addition of equals. $)
  pncan3 $p |- ( ( A e. CC /\ B e. CC ) -> ( A + ( B - A ) ) = B ) $=
    ( cc wcel wa cmin co wceq caddc eqid wb simpr simpl subcl ancoms subadd
    syl111anc mpbii ) ACDZBCDZEZBAFGZUBHZAUBIGBHZUBJUATSUBCDZUCUDKSTLSTMTSUEBAN
    OBAUBPQR $.
    $( [14-Mar-2005] $)

  ${
    pncan3.1 $e |- A e. CC $.
    pncan3.2 $e |- B e. CC $.
    $( Subtraction and addition of equals. $)
    pncan3i $p |- ( A + ( B - A ) ) = B $=
      ( cc wcel cmin co caddc wceq pncan3 mp2an ) AEFBEFABAGHIHBJCDABKL $.
      $( [26-Nov-1994] $)
  $}

  $( Addition of a number and its negative. $)
  negid $p |- ( A e. CC -> ( A + -u A ) = 0 ) $=
    ( cc wcel cneg caddc co cc0 cmin df-neg opreq2i wceq 0cn pncan3 mpan2
    syl5eq ) ABCZAADZEFAGAHFZEFZGQRAEAIJPGBCSGKLAGMNO $.
    $( [14-Mar-2005] $)

  ${
    negid.1 $e |- A e. CC $.
    $( Addition of a number and its negative. $)
    negidi $p |- ( A + -u A ) = 0 $=
      ( cc wcel cneg caddc co cc0 wceq negid ax-mp ) ACDAAEFGHIBAJK $.
      $( [26-Nov-1994] $)
  $}

  ${
    negsub.1 $e |- A e. CC $.
    negsub.2 $e |- B e. CC $.
    $( Relationship between subtraction and negative.  Theorem I.3 of [Apostol]
       p. 18.  (The proof was shortened by Andrew Salmon, 22-Oct-2011.) $)
    negsubi $p |- ( A + -u B ) = ( A - B ) $=
      ( cmin co cneg caddc negcli addcli cc0 add12i negidi opreq2i addid1i
      3eqtri subaddrii eqcomi ) ABEFABGZHFZABTCDASCBDIZJBTHFABSHFZHFAKHFABASDCU
      ALUBKAHBDMNACOPQR $.
      $( [22-Oct-2011] $) $( [26-Nov-1994] $)
  $}

  $( Relationship between subtraction and negative.  Theorem I.3 of [Apostol]
     p. 18. $)
  negsub $p |- ( ( A e. CC /\ B e. CC ) -> ( A + -u B ) = ( A - B ) ) $=
    ( cc wcel cneg caddc co cmin wceq cc0 cif opreq1 eqeq12d negeq opreq2d
    opreq2 0cn elimel negsubi dedth2h ) ACDZBCDZABEZFGZABHGZIUAAJKZUCFGZUFBHGZI
    UFUBBJKZEZFGZUFUIHGZIABJJAUFIUDUGUEUHAUFUCFLAUFBHLMBUIIZUGUKUHULUMUCUJUFFBU
    INOBUIUFHPMUFUIAJCQRBJCQRST $.
    $( [3-Dec-2005] $) $( [21-Jan-1997] $)

  $( Associative-type law for addition and subtraction. $)
  addsubass $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( A + B ) - C ) = ( A + ( B - C ) ) ) $=
    ( cc wcel w3a caddc co cneg cmin wceq negcl addass syl3an3 wa addcl negsub
    sylan 3impa 3adant1 opreq2d 3eqtr3d ) ADEZBDEZCDEZFZABGHZCIZGHZABUHGHZGHZUG
    CJHZABCJHZGHUEUCUDUHDEUIUKKCLABUHMNUCUDUEUIULKZUCUDOUGDEUEUNABPUGCQRSUFUJUM
    AGUDUEUJUMKUCBCQTUAUB $.
    $( [1-Aug-2006] $) $( [6-Aug-2003] $)

  $( Law for addition and subtraction.  (The proof was shortened by Andrew
     Salmon, 22-Oct-2011.) $)
  addsub $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A + B ) - C ) = ( ( A - C ) + B ) ) $=
    ( cc wcel w3a caddc co cmin wceq wa addcom opreq1d 3adant3 addsubass
    3com12 subcl sylan2 3impb 3eqtrd ) ADEZBDEZCDEZFABGHZCIHZBAGHZCIHZBACIHZGHZ
    UHBGHZUAUBUEUGJUCUAUBKUDUFCIABLMNUBUAUCUGUIJBACOPUBUAUCUIUJJZUBUAUCUKUAUCKU
    BUHDEUKACQBUHLRSPT $.
    $( [22-Oct-2011] $) $( [19-Aug-2001] $)

  $( Commutative/associative law for addition and subtraction. $)
  subadd23 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A - B ) + C ) = ( A + ( C - B ) ) ) $=
    ( cc wcel cmin co caddc wceq w3a addsub addsubass eqtr3d 3com23 ) ADEZCDEZB
    DEZABFGCHGZACBFGHGZIOPQJACHGBFGRSACBKACBLMN $.
    $( [1-Feb-2007] $)

  $( Commutative/associative law for addition and subtraction. $)
  addsub12 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( A + ( B - C ) ) = ( B + ( A - C ) ) ) $=
    ( cc wcel cmin co caddc wceq w3a subadd23 wa subcl addcom sylan 3impa
    eqtr3d 3com23 ) ADEZCDEZBDEZABCFGHGZBACFGZHGZISTUAJUCBHGZUBUDACBKSTUAUEUDIZ
    STLUCDEUAUFACMUCBNOPQR $.
    $( [8-Feb-2005] $)

  ${
    addsubass.1 $e |- A e. CC $.
    addsubass.2 $e |- B e. CC $.
    addsubass.3 $e |- C e. CC $.
    $( Associative-type law for subtraction and addition. $)
    addsubassi $p |- ( ( A + B ) - C ) = ( A + ( B - C ) ) $=
      ( cc wcel caddc co cmin wceq addsubass mp3an ) AGHBGHCGHABIJCKJABCKJIJLDE
      FABCMN $.
      $( [16-Sep-1999] $)

    $( Law for subtraction and addition. $)
    addsubi $p |- ( ( A + B ) - C ) = ( ( A - C ) + B ) $=
      ( cc wcel caddc co cmin wceq addsub mp3an ) AGHBGHCGHABIJCKJACKJBIJLDEFAB
      CMN $.
      $( [6-Aug-2003] $)
  $}

  $( Law for subtraction and addition. $)
  2addsub $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
                 ( ( ( A + B ) + C ) - D ) = ( ( ( A + C ) - D ) + B ) ) $=
    ( cc wcel wa caddc co cmin wceq add32 3expa adantrr opreq1d addcl addsub
    3expb sylan an4s eqtrd ) AEFZBEFZGZCEFZDEFZGGZABHICHIZDJIACHIZBHIZDJIZUIDJI
    BHIZUGUHUJDJUDUEUHUJKZUFUBUCUEUMABCLMNOUBUEUCUFUKULKZUBUEGUIEFZUCUFGUNACPUO
    UCUFUNUIBDQRSTUA $.
    $( [20-Nov-2005] $)

  $( Relation between sums and differences..  (Contributed by Jeff Madsen,
     17-Jun-2010.) $)
  addsubeq4 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
                    ( ( A + B ) = ( C + D ) <-> ( C - A ) = ( B - D ) ) ) $=
    ( cc wcel wa cmin co wceq caddc eqcom wb subcl ancoms subadd 3expa sylan
    an4s syl5bb addcom adantl opreq1d addsubass 3com12 eqtrd adantlr eqeq1d
    ax-addcl 3expb sylan2 3bitr2rd ) AEFZBEFZGZCEFZDEFZGZGZCAHIZBDHIZJZDUTKIZBJ
    ZCDKIZAHIZBJZABKIVEJZVBVAUTJZUSVDUTVALUMUPUNUQVIVDMZUMUPGUTEFZUNUQGZVJUPUMV
    KCANOVLVKVJUNUQVKVJBDUTPQORSTUSVFVCBUMURVFVCJUNUMURGZVFDCKIZAHIZVCVMVEVNAHU
    RVEVNJUMCDUAUBUCURUMVOVCJZUPUQUMVPUQUPUMVPDCAUDUEQOUFUGUHURUOVEEFZVGVHMZCDU
    IVQUOVRVQUMUNVRVEABPUJOUKUL $.
    $( [17-Jun-2010] $)

  ${
    negneg.1 $e |- A e. CC $.
    $( A number is equal to the negative of its negative.  Theorem I.4 of
       [Apostol] p. 18.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    negnegi $p |- -u -u A = A $=
      ( cneg caddc co wceq cc0 negidi negcli addcomi 3eqtr4ri addcani mpbi ) AC
      ZNCZDEZNADEZFOAFANDEGQPABHNAABIZBJNRHKNOARNRIBLM $.
      $( [22-Oct-2011] $) $( [8-Feb-1995] $)

    $( Subtraction of a number from itself. $)
    subidi $p |- ( A - A ) = 0 $=
      ( cneg caddc co cmin cc0 negsubi negidi eqtr3i ) AACDEAAFEGAABBHABIJ $.
      $( [26-Nov-1994] $)

    $( Identity law for subtraction. $)
    subid1i $p |- ( A - 0 ) = A $=
      ( cc0 0cn addid2i subaddrii ) ACABDBABEF $.
      $( [29-May-1999] $)

  $}

  $( A number is equal to the negative of its negative.  Theorem I.4 of
     [Apostol] p. 18. $)
  negneg $p |- ( A e. CC -> -u -u A = A ) $=
    ( cc wcel cneg wceq cc0 cif negeq negeqd id eqeq12d 0cn elimel negnegi
    dedth ) ABCZADZDZAEPAFGZDZDZSEAFASEZRUAASUBQTASHIUBJKSAFBLMNO $.
    $( [12-Jan-2002] $)

  $( Relationship between subtraction and negative. $)
  subneg $p |- ( ( A e. CC /\ B e. CC ) -> ( A - -u B ) = ( A + B ) ) $=
    ( cc wcel wa cneg caddc co cmin wceq negcl negsub sylan2 negneg opreq2d
    adantl eqtr3d ) ACDZBCDZEABFZFZGHZATIHZABGHZSRTCDUBUCJBKATLMSUBUDJRSUABAGBN
    OPQ $.
    $( [10-May-2004] $)

  $( Subtraction of a number from itself. $)
  subid $p |- ( A e. CC -> ( A - A ) = 0 ) $=
    ( cc wcel cmin co cc0 wceq cif opreq12 anidms eqeq1d 0cn elimel subidi
    dedth ) ABCZAADEZFGPAFHZRDEZFGAFARGZQSFTQSGARARDIJKRAFBLMNO $.
    $( [8-Oct-1999] $)

  $( Identity law for subtraction. $)
  subid1 $p |- ( A e. CC -> ( A - 0 ) = A ) $=
    ( cc wcel cc0 cmin co wceq cif opreq1 id eqeq12d 0cn elimel subid1i dedth
    ) ABCZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRADBLMNO $.
    $( [9-May-2004] $)

  $( Cancellation law for subtraction. $)
  pncan $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + B ) - B ) = A ) $=
    ( cc wcel wa caddc co cmin wceq addsubass 3anidm23 cc0 subid opreq2d
    addid1 sylan9eqr eqtrd ) ACDZBCDZEABFGBHGZABBHGZFGZARSTUBIABBJKSRUBALFGASUA
    LAFBMNAOPQ $.
    $( [10-May-2004] $)

  $( Cancellation law for subtraction. $)
  pncan2 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + B ) - A ) = B ) $=
    ( cc wcel caddc co cmin wceq wa addcom opreq1d pncan eqtr3d ancoms ) BCDZAC
    DZABEFZAGFZBHOPIZBAEFZAGFRBSTQAGBAJKBALMN $.
    $( [17-Apr-2005] $)

  $( Cancellation law for subtraction. $)
  npcan $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A - B ) + B ) = A ) $=
    ( cc wcel wa caddc co cmin wceq addsub 3anidm23 pncan eqtr3d ) ACDZBCDZEABF
    GBHGZABHGBFGZANOPQIABBJKABLM $.
    $( [10-May-2004] $)

  $( Cancellation law for subtraction. $)
  npncan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) + ( B - C ) ) = ( A - C ) ) $=
    ( cc wcel w3a cmin co caddc wceq subcl 3adant3 addsubass syld3an1 wa npcan
    opreq1d eqtr3d ) ADEZBDEZCDEZFABGHZBIHZCGHZUBBCGHIHZACGHZUBDEZTSUAUDUEJSTUG
    UAABKLUBBCMNSTUDUFJUASTOUCACGABPQLR $.
    $( [8-Feb-2005] $)

  $( Cancellation law for subtraction. $)
  nppcan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( ( A - B ) + C ) + B ) = ( A + C ) ) $=
    ( cc wcel w3a cmin co caddc wceq subcl 3adant3 simp3 simp2 add32 syl111anc
    wa npcan opreq1d eqtrd ) ADEZBDEZCDEZFZABGHZCIHBIHZUEBIHZCIHZACIHZUDUEDEZUC
    UBUFUHJUAUBUJUCABKLUAUBUCMUAUBUCNUECBOPUAUBUHUIJUCUAUBQUGACIABRSLT $.
    $( [1-Sep-2005] $)

  $( Cancellation law for subtraction. $)
  subcan2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - C ) = ( B - C ) <-> A = B ) ) $=
    ( cc wcel w3a cneg caddc co wceq cmin negsub 3adant2 3adant1 eqeq12d wb
    negcl addcan2 syl3an3 bitr3d ) ADEZBDEZCDEZFZACGZHIZBUEHIZJZACKIZBCKIZJABJZ
    UDUFUIUGUJUAUCUFUIJUBACLMUBUCUGUJJUABCLNOUCUAUBUEDEUHUKPCQABUERST $.
    $( [8-Feb-2005] $)

  $( If the difference between two numbers is zero, they are equal. $)
  subeq0 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A - B ) = 0 <-> A = B ) ) $=
    ( cc wcel wa cmin co wceq cc0 subid adantl eqeq2d wb subcan2 3anidm23
    bitr3d ) ACDZBCDZEZABFGZBBFGZHZTIHABHZSUAITRUAIHQBJKLQRUBUCMABBNOP $.
    $( [16-Nov-1999] $)

  ${
    subneg.1 $e |- A e. CC $.
    subneg.2 $e |- B e. CC $.
    $( Relationship between subtraction and negative. $)
    subnegi $p |- ( A - -u B ) = ( A + B ) $=
      ( cc wcel cneg cmin co caddc wceq subneg mp2an ) AEFBEFABGHIABJIKCDABLM
      $.
      $( [1-Dec-2005] $)

    $( If the difference between two numbers is zero, they are equal. $)
    subeq0i $p |- ( ( A - B ) = 0 <-> A = B ) $=
      ( cc wcel cmin co cc0 wceq wb subeq0 mp2an ) AEFBEFABGHIJABJKCDABLM $.
      $( [8-May-1999] $)

    $( Negative is one-to-one. $)
    neg11i $p |- ( -u A = -u B <-> A = B ) $=
      ( cneg wceq cc0 cmin co caddc df-neg eqeq12i 0cn subcli subaddi opreq2i
      negsubi eqtr3i eqeq1i subeq0i bitri 3bitri ) AEZBEZFGAHIZGBHIZFAUFJIZGFZA
      BFZUCUEUDUFAKBKZLGAUFMCGBMDNOUHABHIZGFUIUGUKGAUDJIUGUKUDUFAJUJPABCDQRSABC
      DTUAUB $.
      $( [1-Aug-1999] $)

    $( Negative contraposition law. $)
    negcon1i $p |- ( -u A = B <-> -u B = A ) $=
      ( cneg wceq negnegi eqeq1i negcli neg11i eqcom 3bitr3i ) AEZEZBEZFAOFMBFO
      AFNAOACGHMBACIDJAOKL $.
      $( [25-Aug-1999] $)

    $( Negative contraposition law. $)
    negcon2i $p |- ( A = -u B <-> B = -u A ) $=
      ( cneg wceq negcon1i eqcom 3bitr4i ) BEZAFAEZBFAJFBKFBADCGAJHBKHI $.
      $( [25-Aug-1999] $)
  $}

  $( Negative is one-to-one. $)
  neg11 $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A = -u B <-> A = B ) ) $=
    ( cc wcel cneg wceq wb cc0 cif negeq eqeq1d eqeq1 bibi12d eqeq2d eqeq2 0cn
    elimel neg11i dedth2h ) ACDZBCDZAEZBEZFZABFZGTAHIZEZUCFZUFBFZGUGUABHIZEZFZU
    FUJFZGABHHAUFFZUDUHUEUIUNUBUGUCAUFJKAUFBLMBUJFZUHULUIUMUOUCUKUGBUJJNBUJUFOM
    UFUJAHCPQBHCPQRS $.
    $( [8-Feb-2005] $)

  $( Negative contraposition law. $)
  negcon1 $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A = B <-> -u B = A ) ) $=
    ( cc wcel wa cneg wceq wb negcl neg11 sylan negneg adantr eqeq1d bitr3d
    eqcom syl6bb ) ACDZBCDZEZAFZBGZABFZGZUCAGTUAFZUCGZUBUDRUACDSUFUBHAIUABJKTUE
    AUCRUEAGSALMNOAUCPQ $.
    $( [9-May-2004] $)

  $( Negative contraposition law. $)
  negcon2 $p |- ( ( A e. CC /\ B e. CC ) -> ( A = -u B <-> B = -u A ) ) $=
    ( cc wcel wa cneg wceq negcon1 eqcom syl6rbbr syl6bb ) ACDBCDEZABFZGZAFZBGZ
    BOGLPMAGNABHAMIJOBIK $.
    $( [14-Nov-2004] $)

  $( Cancellation law for subtraction. $)
  subcan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) = ( A - C ) <-> B = C ) ) $=
    ( cc wcel w3a cneg caddc co wceq cmin wb negcl addcan syl3an3 syl3an2
    negsub 3adant3 3adant2 eqeq12d neg11 3adant1 3bitr3d ) ADEZBDEZCDEZFZABGZHI
    ZACGZHIZJZUHUJJZABKIZACKIZJBCJZUEUDUHDEZUFULUMLZBMUFUDUQUJDEURCMAUHUJNOPUGU
    IUNUKUOUDUEUIUNJUFABQRUDUFUKUOJUEACQSTUEUFUMUPLUDBCUAUBUC $.
    $( [2-Aug-2006] $) $( [8-Feb-2005] $)

  ${
    subcan.1 $e |- A e. CC $.
    subcan.2 $e |- B e. CC $.
    subcan.3 $e |- C e. CC $.
    $( Cancellation law for subtraction. $)
    subcani $p |- ( ( A - B ) = ( A - C ) <-> B = C ) $=
      ( cc wcel cmin co wceq wb subcan mp3an ) AGHBGHCGHABIJACIJKBCKLDEFABCMN
      $.
      $( [8-Feb-2005] $)

    $( Cancellation law for subtraction. $)
    subcan2i $p |- ( ( A - C ) = ( B - C ) <-> A = B ) $=
      ( cc wcel cmin co wceq wb subcan2 mp3an ) AGHBGHCGHACIJBCIJKABKLDEFABCMN
      $.
      $( [8-Feb-2005] $)
  $}

  $( Minus 0 equals 0. $)
  neg0 $p |- -u 0 = 0 $=
    ( cc0 cneg cmin co df-neg 0cn subidi eqtri ) ABAACDAAEAFGH $.
    $( [17-Jan-1997] $)

  ${
    $d x A $.
    renegcl.1 $e |- A e. RR $.
    $( Closure law for negative of reals.  (The proof was shortened by Andrew
       Salmon, 22-Oct-2011.) $)
    renegcli $p |- -u A e. RR $=
      ( vx cv caddc co cc0 wceq cr wrex cneg wcel ax-rnegex ax-mp cc wb recn
      cmin df-neg eqeq1i 0cn recni subadd mp3an12 syl5bb syl eleq1a sylbird
      r19.23aiv ) ACDZEFGHZCIJZAKZILZAILULBCAMNUKUNCIUJILZUKUMUJHZUNUOUJOLZUPUK
      PUJQUPGARFZUJHZUQUKUMURUJASTGOLAOLUQUSUKPUAABUBGAUJUCUDUEUFUJIUMUGUHUIN
      $.
      $( [22-Oct-2011] $) $( [17-Jan-1997] $)
  $}

