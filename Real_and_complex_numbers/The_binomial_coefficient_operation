$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The binomial coefficient operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c _C $. $( Binomial coefficient operation. $)

  $( Extend class notation to include the binomial coefficient operation
     (combinatorial choose operation). $)
  cbc $a class _C $.

  ${
    $d n k m $.
    $( Define the binomial coefficient operation.  In the literature, this
       function is often written as a column vector of the two arguments, or
       with the arguments as subscripts before and after the letter "C".
       ` ( N _C K ) ` is read " ` N ` choose ` K ` ."  Definition of binomial
       coefficient in [Gleason] p. 295.  As suggested by Gleason, we define it
       to be 0 when ` 0 <_ k <_ n ` does not hold. $)
    df-bc $a |- _C = ( n e. NN0 , k e. ZZ |-> if ( k e. ( 0 ... n ) ,
                ( ( ! ` n ) / ( ( ! ` ( n - k ) ) x. ( ! ` k ) ) ) , 0 ) ) $.
  $}

  ${
    $d n k m N $.  $d n k m K $.
    $( Value of the binomial coefficient, ` N ` choose ` K ` .  Definition of
       binomial coefficient in [Gleason] p. 295.  As suggested by Gleason, we
       define it to be 0 when ` 0 <_ K <_ N ` does not hold.  See ~ bcval2 for
       the value in the standard domain. $)
    bcval $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) = if ( K e. ( 0 ... N )
      , ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) , 0 ) ) $=
      ( vn vk cv cc0 cfz co wcel cfa cfv cmin cmul cdiv cif opreq2 fveq2 fveq2d
      wceq opreq12d cn0 cbc eleq2d opreq1 opreq1d eqidd ifbieq12d eleq1 opreq2d
      cz df-bc oprex cc 0cn elisseti ifex ovmpt2 ) CDBAUAUJDEZFCEZGHZIZUSJKZUSU
      RLHZJKZURJKZMHZNHZFOAFBGHZIZBJKZBALHZJKZAJKZMHZNHZFOUBURVHIZVJBURLHZJKZVE
      MHZNHZFOUSBSZVAVPVGFVTFWAUTVHURUSBFGPUCWAVBVJVFVSNUSBJQWAVDVRVEMWAVCVQJUS
      BURLUDRUETWAFUFUGURASZVPVIVTFVOFURAVHUHWBVSVNVJNWBVRVLVEVMMWBVQVKJURABLPR
      URAJQTUIWBFUFUGDCUKVIVOFVJVNNULFUMUNUOUPUQ $.
      $( [7-Nov-2013] $) $( [10-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , in its standard
       domain. $)
    bcval2 $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) =
                    ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) ) $=
      ( wcel cc0 cfz co cbc cfa cfv cmin cmul cdiv cif cn0 wceq elfz3nn0 adantl
      wa cz elfzelz bcval syl11anc iftrue eqtrd ) BCDZAEBFGDZSZBAHGZUGBIJBAKGIJ
      AIJLGMGZENZUJUHBODATDZUIUKPCABQUGULUFAEBUARABUBUCUGUKUJPUFUGUJEUDRUE $.
      $( [7-Nov-2013] $) $( [9-Jun-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval3 $p |- ( ( N e. NN0 /\ K e. ZZ /\ -. K e. ( 0 ... N ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 cfz co wn w3a cbc cfa cfv cmin cmul cdiv cif wceq bcval
      3adant3 iffalse 3ad2ant3 eqtrd ) BCDZAEDZAFBGHDZIZJBAKHZUFBLMBANHLMALMOHP
      HZFQZFUDUEUHUJRUGABSTUGUDUJFRUEUFUIFUAUBUC $.
      $( [8-Nov-2013] $) $( [14-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval4 $p |- ( ( N e. NN0 /\ K e. ZZ /\ ( K < 0 \/ N < K ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 clt wbr wo w3a cfz co wn wa cle cr lenlt mpbid adantl
      wb cbc wceq simp1 simp2 elfzle1 0re elfzelz zre syl sylancr elfzle2 nn0re
      wi syl2anc jca ioran sylibr ex adantr con2d 3impia bcval3 syl111anc ) BCD
      ZAEDZAFGHZBAGHZIZJVDVEAFBKLDZMZBAUALFUBVDVEVHUCVDVEVHUDVDVEVHVJVDVENVIVHV
      DVIVHMZUMVEVDVIVKVDVINZVFMZVGMZNVKVLVMVNVIVMVDVIFAOHZVMAFBUEVIFPDAPDZVOVM
      TUFVIVEVPAFBUGAUHUIZFAQUJRSVLABOHZVNVIVRVDAFBUKSVIVPBPDVRVNTVDVQBULABQUNR
      UOVFVGUPUQURUSUTVAABVBVC $.
      $( [7-Nov-2013] $) $( [14-Jul-2005] $)
  $}

  $( "Complementing" its second argument doesn't change a binary
     coefficient. $)
  bccmpl $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) =
                    ( N _C ( N - K ) ) ) $=
    ( wcel co cfa cfv cmin cmul cdiv cbc cc wceq cn0 cn elfznn0 faccl nncn 3syl
    syl11anc cc0 cfz wa fznn0sub2 syl adantl mulcom elfz3nn0 nn0cn nncan syl2an
    fveq2d opreq1d eqtr4d opreq2d bcval2 syldan 3eqtr4d ) BCDZAUABUBEZDZUCZBFGZ
    BAHEZFGZAFGZIEZJEVCBVDHEZFGZVEIEZJEZBAKEBVDKEZVBVGVJVCJVBVGVFVEIEZVJVBVELDZ
    VFLDZVGVMMVBVDUTDZVNCABUDZVPVDNDVEODVNVDBPVDQVERSUEVBANDZVFODVOVAVRUSABPUFZ
    AQVFRSVEVFUGTVBVIVFVEIVBVHAFVBBNDZVRVHAMZCABUHVSVTBLDALDWAVRBUIAUIBAUJUKTUL
    UMUNUOABCUPUSVAVPVLVKMVQVDBCUPUQUR $.
    $( [8-Nov-2013] $) $( [21-Jun-2005] $)

  $( ` N ` choose 0 is 1.  Remark in [Gleason] p. 296. $)
  bcn0 $p |- ( N e. NN0 -> ( N _C 0 ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cfa cfv cmin cmul cdiv c1 cfz wceq cuz eluzfz1 eleq2s
    nn0uz cc syl bcval2 mpdan nn0cn subid1 fveq2d fac0 opreq12 sylancl cn faccl
    nncn mulid1 eqtrd opreq2d wne facne0 divid syl11anc 3eqtrd ) ABCZADEFZAGHZA
    DIFZGHZDGHZJFZKFZVBVBKFZLUTDDAMFCZVAVGNVIADOHBDAPRQDABUAUBUTVFVBVBKUTVFVBLJ
    FZVBUTVDVBNVELNVFVJNUTVCAGUTASCVCANAUCAUDTUEUFVDVBVELJUGUHUTVBSCZVJVBNUTVBU
    ICVKAUJVBUKTZVBULTUMUNUTVKVBDUOVHLNVLAUPVBUQURUS $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( ` N ` choose ` N ` is 1.  Remark in [Gleason] p. 296. $)
  bcnn $p |- ( N e. NN0 -> ( N _C N ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cmin c1 cfz cuz cfv eluzfz1 nn0uz eleq2s bccmpl mpdan
    wceq bcn0 cc nn0cn subid1 syl opreq2d 3eqtr3rd ) ABCZADEFZAADGFZEFZHAAEFUED
    DAIFCZUFUHQUIADJKBDALMNDABOPARUEUGAAEUEASCUGAQATAUAUBUCUD $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( Binomial coefficient: ` N ` choose ` 1 ` . $)
  bcn1 $p |- ( N e. NN0 -> ( N _C 1 ) = N ) $=
    ( cn0 wcel cn cc0 wceq wo c1 cbc co cfa cfv cmul cdiv cuz cc syl nncn eqtrd
    clt elnn0 cmin cfz wa elnnuz biimpi 1nn0 nn0uz eleqtri jctil elfzuzb mpbird
    bcval2 mpdan facnn2 fac1 opreq2i nnm1nn0 faccl mulid1 syl5eq opreq12d nnne0
    3syl wne divcan3 syl111anc cz wbr 0nn0 lt01 olci bcval4 mp3an opreq1 eqeq12
    1z wb mpancom mpbiri jaoi sylbi ) ABCADCZAEFZGAHIJZAFZAUAWCWFWDWCWEAKLZAHUB
    JZKLZHKLZMJZNJZAWCHEAUCJCZWEWLFWCWMHEOLZCZAHOLCZUDWCWPWOWCWPAUEUFHBWNUGUHUI
    UJDHEAUKULHADUMUNWCWLWIAMJZWINJZAWCWGWQWKWINAUOWCWKWIHMJZWIWJHWIMUPUQWCWIDC
    ZWIPCZWSWIFWCWHBCWTAURWHUSQZWIRZWIUTVDVAVBWCAPCXAWIEVEZWRAFARWCWTXAXBXCQWCW
    TXDXBWIVCQAWIVFVGSSWDWFEHIJZEFZEBCHVHCHETVIZEHTVIZGXFVJVQXHXGVKVLHEVMVNWEXE
    FWDWFXFVRAEHIVOWEXEAEVPVSVTWAWB $.
    $( [8-Nov-2013] $) $( [21-Jun-2005] $)

  $( Binomial coefficient: ` N + 1 ` choose ` N ` . $)
  bcnp1n $p |- ( N e. NN0 -> ( ( N + 1 ) _C N ) = ( N + 1 ) ) $=
    ( cn0 wcel c1 caddc co cbc cmin cc0 cfz wceq peano2nn0 cle wa cr nn0re lep1
    wbr syl cc ancli fznn0 mpbird bccmpl syl11anc ax-1cn pncan2 sylancl opreq2d
    wb nn0cn bcn1 3eqtrd ) ABCZADEFZAGFZUOUOAHFZGFZUODGFZUOUNUOBCZAIUOJFCZUPURK
    ALZUNVAUNAUOMRZNZUNVCUNAOCVCAPAQSUAUNUTVAVDUJVBAUOUBSUCAUOBUDUEUNUQDUOGUNAT
    CDTCUQDKAUKUFADUGUHUIUNUTUSUOKVBUOULSUM $.
    $( [8-Nov-2013] $) $( [20-Jun-2005] $)

  $( Pascal's rule for the binomial coefficient, generalized to all integers
     ` K ` .  Equation 2 of [Gleason] p. 295. $)
  bcpasc $p |- ( ( N e. NN0 /\ K e. ZZ ) ->
                ( ( N _C K ) + ( N _C ( K - 1 ) ) ) = ( ( N + 1 ) _C K ) ) $=
    ( cn0 wcel cc0 c1 caddc co cfz cbc wceq cfv syl opreq12d cfa cmul cdiv nncn
    cc cn cz wa cmin wo wb peano2nn0 cuz elfzp12 nn0uz eleq2s ax-1cn ax-mp bcn0
    addid1 clt wbr 0z 1z zsubcl mp2an cr 0re ltm1 bcval4 mp3an23 3eqtr4a opreq2
    orci opreq1 opreq2d eqeq12d syl5ibrcom addid2 opreq1i syl6eleq nn0p1nn nnuz
    simpr biimpa sylan nn0cn pncan sylancl eleq2d wne a1i elfzelz adantl elfznn
    fzm1 zcn nnne0 jca cle elfzle2 nn0z adantr znn0sub syl11anc mpbid divadddiv
    syl22anc mulid2 mulid1 add32 syl111anc npcan opreq1d 3eqtrd eqtrd faccl nnz
    fzelp1 elfzm1b syl2anc eleqtrd elfznn0 fznn0sub nnmulcl nnrecre recnd facp1
    divmuldiv fveq2d 3syl ax-mulcom 3eqtr2d fzp1ss sseld bcval2 syldan 3eqtr4rd
    wss imp cvv sylan9eqr bcnn wn con3d bcval3 divcl adddi addsub facnn2 eqtr4d
    nnnn0 mul4 3eqtr3d mul32 syl5eqssr ax-mulass subsub eqeltrd sylancr 3eqtr4d
    mpan oprex ltp1 olcd mpd3an23 jaodan jaod sylbid adantlr 00id simpll simplr
    nn0re ex fzssp1 peano2zm id eqsstr3i sseli syl6bir sylbird pm2.61dan ) BCDZ
    AUADZUBZAEBFGHZIHZDZBAJHZBAFUCHZJHZGHZUWAAJHZKZUVRUWCUWIUVSUVRUWCUWIUVRUWCA
    EKZAEFGHZUWAIHZDZUDZUWIUVRUWACDZUWCUWNUEZBUFZUWPUWAEUGLCAEUWAUHUIUJMUVRUWJU
    WIUWMUVRUWIUWJBEJHZBEFUCHZJHZGHZUWAEJHZKUVRFEGHZFUXAUXBFSDZUXCFKUKFUNULUVRU
    WRFUWTEGBUMUVRUWSUADZUWSEUOUPZBUWSUOUPZUDUWTEKEUADZFUADUXEUQUREFUSUTUXFUXGE
    VADUXFVBEVCULVHUWSBVDVENUVRUWOUXBFKUWQUWAUMMVFUWJUWGUXAUWHUXBUWJUWDUWRUWFUW
    TGAEBJVGUWJUWEUWSBJAEFUCVIVJNAEUWAJVGVKVLUVRUWMUWIUVRUWMAFUWAIHZDZUWIUVRUWM
    UBAUWLUXIUVRUWMVRUWKFUWAIUXDUWKFKUKFVMULZVNZVOUVRUXJAFUWAFUCHZIHZDZAUWAKZUD
    ZUWIUVRUWAFUGLZDZUXJUXQUVRUWATUXRBVPZVQVOUXSUXJUXQAFUWAWJVSVTUVRUXOUWIUXPUV
    RUXOAFBIHZDZUWIUVRUXOUYBUVRUXNUYAAUVRUXMBFIUVRBSDZUXDUXMBKZBWAZUKBFWBZWCZVJ
    WDVSUVRUYBUBZBOLZUWEOLZBAUCHZOLZPHZQHZFAQHZPHZUYNFUYKFGHZQHZPHZGHZUWAOLZUWA
    AUCHZOLZAOLZPHZQHZUWGUWHUYHUYNUYOUYRGHZPHZUYNUWAAUYQPHZQHZPHZUYTVUFUYHVUGVU
    JUYNPUYHVUGFUYQPHZAFPHZGHZVUIQHZVUJUYHUXDUXDASDZAEWEZUBZUYQSDZUYQEWEZUBZVUG
    VUOKUXDUYHUKWFZVVBUYHVUPVUQUYHUVSVUPUYBUVSUVRAFBWGZWHZAWKMZUYHATDZVUQUYBVVF
    UVRABWIWHZAWLMWMZUYHUYQTDZVVAUYHUYKCDZVVIUYHABWNUPZVVJUYBVVKUVRAFBWOWHUYHUV
    SBUADZVVKVVJUEVVDUVRVVLUYBBWPWQABWRWSWTZUYKVPMZVVIVUSVUTUYQRUYQWLWMMZFFAUYQ
    XAXBUYHVUNUWAVUIQUYHVUNUYQAGHZUYKAGHZFGHZUWAUYHVULUYQVUMAGUYHVUSVULUYQKUYHU
    YQCDZVUSUYHVVJVVSVVMUYKUFMZUYQWAMZUYQXCMUYHVUPVUMAKVVEAXDMNUYHUYKSDZUXDVUPV
    VPVVRKUYHVVJVWBVVMUYKWAMVVBVVEUYKFAXEXFUYHVVQBFGUYHUYCVUPVVQBKUVRUYCUYBUYEW
    QZVVEBAXGWSXHXIXHXJVJUYHUYNSDZUYOSDZUYRSDZVUHUYTKUYHUYISDZUYMSDZUYMEWEZVWDU
    YHUYITDZVWGUVRVWJUYBBXKWQUYIRMZUYHUYMTDZVWHUYHUYJTDZUYLTDZVWLUYHUWECDZVWMUY
    HUWEEBIHZDZVWOUYHUWEEUXMIHZVWPUYHUXJUWEVWRDZCAFBXMUYBUVSUWAUADZUXJVWSUEZUVR
    VVCUVRUWATDZVWTUXTUWAXLMZAUWAXNZXOWTUVRVWRVWPKUYBUVRUXMBEIUYGVJWQXPZUWEBXQM
    ZUWEXKZMUYHVVJVWNCAFBXRUYKXKZMUYJUYLXSWSZUYMRZMUYHVWLVWIVXIUYMWLZMUYIUYMUUA
    XFUYHVVFVWEVVGVVFUYOAXTYAMUYHVVIVWFVVNVVIUYRUYQXTYAMUYNUYOUYRUUBXFUYHVUKUYI
    UWAPHZUYMVUIPHZQHZVUFUYHVWGUWASDZVWHVWIUBZVUISDZVUIEWEZUBZVUKVXNKVWKUYHVXBV
    XOUVRVXBUYBUXTWQUWARMUYHVWLVXPVXIVWLVWHVWIVXJVXKWMMZUYHVUITDZVXSUYHVVFVVIVY
    AVVGVVNAUYQXSWSVYAVXQVXRVUIRVUIWLWMMUYIUWAUYMVUIYCXBUYHVUAVXLVUEVXMQUVRVUAV
    XLKUYBBYBWQUYHVUEUYJAPHZUYLUYQPHZPHZVXMUYHVUEUYQOLZVUDPHZVUDVYEPHZVYDUYHVUC
    VYEVUDPUYHVUBUYQOUYHUYCUXDVUPVUBUYQKVWCVVBVVEBFAUUCXFYDXHUYHVYESDZVUDSDZVYF
    VYGKUYHVVSVYETDVYHVVTUYQXKVYERYEUYHACDZVUDTDVYIUYHVVFVYJVVGAUUFMAXKVUDRYEZV
    YEVUDYFWSUYHVUDVYBVYEVYCPUYHVVFVUDVYBKVVGAUUDMZUYHVVJVYEVYCKVVMUYKYBMZNXIUY
    HUYJSDZVUPUYLSDZVUSVYDVXMKUYHVWOVWMVYNVXFVXGUYJRYEZVVEUYHVVJVWNVYOVVMVXHUYL
    RYEZVWAUYJAUYLUYQUUGXBXJNUUEUUHUYHUWDUYPUWFUYSGUYHUYIFPHZUYMAPHZQHZUYIUYLVU
    DPHZQHZUYPUWDUYHVYRUYIVYSWUAQUYHVWGVYRUYIKVWKUYIXDMZUYHVYSVYBUYLPHZVUDUYLPH
    ZWUAUYHVYNVYOVUPVYSWUDKVYPVYQVVEUYJUYLAUUIXFUYHVUDVYBUYLPVYLXHUYHVYIVYOWUEW
    UAKVYKVYQVUDUYLYFWSYGNUYHVWGUXDVXPVURUYPVYTKVWKVVBVXTVVHUYIFUYMAYCXBUVRUYBA
    VWPDZUWDWUBKUVRUYBWUFUVRUYAVWPAUVRUYAUWKBIHZVWPUWKFBIUXKVNUXHUVRWUGVWPYMUQE
    BCYHUUPUUJYIYNZABCYJYKYLUYHVYRUYMUYQPHZQHZUYIBUWEUCHZOLZUYJPHZQHZUYSUWFUYHV
    YRUYIWUIWUMQWUCUYHWUIUYJVYCPHZUYJWULPHZWUMUYHVYNVYOVUSWUIWUOKVYPVYQVWAUYJUY
    LUYQUUKXFUYHWULVYCUYJPUYHWULVYEVYCUYHWUKUYQOUYHUYCVUPUXDWUKUYQKVWCVVEVVBBAF
    UULXFZYDVYMXJVJUYHVYNWULSDZWUPWUMKVYPUYHWUKCDWULTDWURUYHWUKUYQCWUQVVTUUMWUK
    XKWULRYEUYJWULYFWSYGNUYHVWGUXDVXPVVAUYSWUJKVWKVVBVXTVVOUYIFUYMUYQYCXBUVRUYB
    VWQUWFWUNKVXEUWEBCYJYKYLNUYHUWAYODZUWCUWHVUFKBFGUUQZUVRUYBWUFUWCWUHCAEBXMYK
    AUWAYOYJUUNUUOYKUVRUXPUBZUWKFUWGUWHUXKWVAUWDEUWFFGUXPUVRUWDBUWAJHZEAUWABJVG
    UVRVWTUWAEUOUPZBUWAUOUPZUDWVBEKVXCUVRWVDWVCUVRBVADWVDBUVHBUURMUUSUWABVDUUTY
    PWVAUWFBBJHZFWVAUWEBBJUXPUVRUWEUXMBAUWAFUCVIUYGYPVJUVRWVEFKUXPBYQWQXJNUXPUV
    RUWHUWAUWAJHZFAUWAUWAJVGUVRUWOWVFFKUWQUWAYQMYPVFUVAYKYKUVIUVBUVCYNUVDUVTUWC
    YRZUBZEEGHEUWGUWHUVEWVHUWDEUWFEGWVHUVRUVSWUFYRZUWDEKUVRUVSWVGUVFZUVRUVSWVGU
    VGZUVTWVGWVIUVTWUFUWCUVTVWPUWBAUVRVWPUWBYMUVSEBCUVJWQYIYSYNABYTXFWVHUVRUWEU
    ADZVWQYRZUWFEKWVJWVHUVSWVLWVKAUVKMUVTWVGWVMUVTVWQUWCUVTVWQVWSUWCUVTVWRVWPUW
    EUVTUXMBEIUVTUYCUXDUYDUVRUYCUVSUYEWQUKUYFWCVJWDUVTVWSUXJUWCUVSUVSVWTVXAUVRU
    VSUVLUVRUWOVWTUWQUWAWPMVXDXOUXIUWBAUXIUWLUWBUXLUXHWUSUWLUWBYMUQWUTEUWAYOYHU
    TUVMUVNUVOUVPYSYNUWEBYTXFNWVHUWOUVSWVGUWHEKWVHUVRUWOWVJUWQMWVKUVTWVGVRAUWAY
    TXFVFUVQ $.
    $( [9-Nov-2013] $) $( [13-Jul-2005] $)

  ${
    $d k m n N $.  $d k m n K $.
    $( A binomial coefficient, in its extended domain, is a nonnegative
       integer. $)
    bccl $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) e. NN0 ) $=
      ( vk vm cn0 wcel cv cbc co cz wral cc0 c1 wceq opreq1 eleq1d ralbidv 0nn0
      wa opreq2 vn caddc cfz elfz1eq adantl bcn0 1nn0 eqeltri mpbiri syl bcval3
      ax-mp mp3an1 syl6eqel pm2.61dan rgen cbvralv cmin bcpasc adantlr rcla4cva
      wn peano2zm sylan2 nn0addcl syl11anc adantll eqeltrrd ralrimiva ex syl5bi
      nn0ind sylan ) BEFBCGZHIZEFZCJKZAJFBAHIZEFZDGZVNHIZEFZCJKLVNHIZEFZCJKUAGZ
      VNHIZEFZCJKZWEMUBIZVNHIZEFZCJKZVQDUABVTLNZWBWDCJWMWAWCEVTLVNHOPQVTWENZWBW
      GCJWNWAWFEVTWEVNHOPQVTWINZWBWKCJWOWAWJEVTWIVNHOPQVTBNZWBVPCJWPWAVOEVTBVNH
      OPQWDCJVNJFZVNLLUCIFZWDWQWRSVNLNZWDWRWSWQVNLUDUEWSWDLLHIZEFWTMELEFZWTMNRL
      UFULUGUHWSWCWTEVNLLHTPUIUJWQWRVBZSWCLEXAWQXBWCLNRVNLUKUMRUNUOUPWHWEVTHIZE
      FZDJKZWEEFZWLWGXDCDJVNVTNWFXCEVNVTWEHTPUQXFXEWLXFXESZWKCJXGWQSWFWEVNMURIZ
      HIZUBIZWJEXFWQXJWJNXEVNWEUSUTXEWQXJEFZXFXEWQSWGXIEFZXKXDWGDVNJVTVNNXCWFEV
      TVNWEHTPVAWQXEXHJFXLVNVCXDXLDXHJVTXHNXCXIEVTXHWEHTPVAVDWFXIVEVFVGVHVIVJVK
      VLVPVSCAJVNANVOVREVNABHTPVAVM $.
      $( [9-Nov-2013] $) $( [10-Jul-2005] $)

    $( A binomial coefficient, in its standard domain, is a natural number. $)
    bccl2 $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) e. NN ) $=
      ( vm vn wcel cc0 co wa cuz cfv cn0 cbc cn wi c1 wceq opreq1 eleq1d imbi2d
      cfz elfzuz3 elfznn0 adantl cv caddc cz bcnn 1nn syl6eqel a1i nn0uz eleq2i
      cmin uztrn sylan2b syl6eleqr adantr nn0z bcpasc syl11anc id peano2zm bccl
      syl nnnn0addcl syl2anc eqeltrrd exp31 a2d uzind4 sylc ) BCFZAGBUAHFZIBAJK
      ZFALFZBAMHZNFZCAGBUBVNVPVMABUCUDVPDUEZAMHZNFZOVPAAMHZNFZOZVPEUEZAMHZNFZOV
      PWEPUFHZAMHZNFZOVPVRODEABVSAQZWAWCVPWKVTWBNVSAAMRSTVSWEQZWAWGVPWLVTWFNVSW
      EAMRSTVSWHQZWAWJVPWMVTWINVSWHAMRSTVSBQZWAVRVPWNVTVQNVSBAMRSTWDAUGFZVPWBPN
      AUHUIUJUKWEVOFZVPWGWJWPVPWGWJWPVPIZWGIZWFWEAPUNHZMHZUFHZWINWRWELFZWOXAWIQ
      WQXBWGWQWEGJKZLVPWPAXCFWEXCFLXCAULUMAWEGUOUPULUQZURWQWOWGVPWOWPAUSUDZURAW
      EUTVAWGWGWTLFZXANFWQWGVBWQXBWSUGFZXFXDWQWOXGXEAVCVEWSWEVDVAWFWTVFVGVHVIVJ
      VKVL $.
      $( [9-Nov-2013] $) $( [3-Jan-2006] $)
  $}

  $( The number of permutations of ` N - R ` objects from a collection of ` N `
     objects is a natural number.  (Contributed by Jason Orendorff,
     24-Jan-2007.) $)
  permnn $p |- ( ( N e. NN0 /\ R e. ( 0 ... N ) ) ->
               ( ( ! ` N ) / ( ! ` R ) ) e. NN ) $=
    ( cn0 wcel cc0 cfz co wa cfa cfv cn cmin cmul cdiv elfznn0 faccl syl adantl
    cc nncn fznn0sub nnmulcl syl11anc adantr wne wceq divcan4 syl111anc eqeltrd
    facne0 cbc bcval2 bccl2 eqeltrrd nndivtr syl32anc ) BCDZAEBFGDZHZAIJZKDZBAL
    GZIJZUTMGZKDZBIJZSDZVDUTNGZKDVFVDNGZKDVFUTNGKDURVAUQURACDZVAABOZAPQZRZUSVCK
    DZVAVEUSVBCDVNCAEBUAVBPQZVMVCUTUBUCUQVGURUQVFKDVGBPVFTQUDUSVHVCKUSVCSDZUTSD
    ZUTEUEZVHVCUFUSVNVPVOVCTQURVQUQURVAVQVLUTTQRURVRUQURVJVRVKAUJQRVCUTUGUHVOUI
    USBAUKGVIKABCULABCUMUNUTVDVFUOUP $.
    $( [24-Jan-2007] $)

