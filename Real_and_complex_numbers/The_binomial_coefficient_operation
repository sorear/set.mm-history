$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The binomial coefficient operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c C. $. $( Binomial coefficient operation. $)

  $( Extend class notation to include the binomial coefficient operation
     (combinatorial choose operation). $)
  cbc $a class C. $.

  ${
    $d n k m $.
    $( Define the binomial coefficient operation.  In the literature, this
       function is often written as a column vector of the two arguments, or
       with the arguments as subscripts before and after the letter "C".
       ` ( N C. K ) ` is read " ` N ` choose ` K ` ."  Definition of
       binomial coefficient in [Gleason] p. 295.  As suggested by Gleason,
       we define it to be 0 when ` 0 <_ k <_ n ` does not hold. $)
    df-bc $a |- C. = { <. <. n , k >. , m >. | ( ( n e. NN0 /\ k e. ZZ ) /\
                m = if ( ( 0 <_ k /\ k <_ n ) ,
                ( ( ! ` n ) / ( ( ! ` ( n - k ) ) x. ( ! ` k ) ) ) , 0 ) ) } $.
  $}

  ${
    $d n k m N $.  $d n k m K $.
    $( Value of the binomial coefficient, ` N ` choose ` K ` .  Definition
       of binomial coefficient in [Gleason] p. 295.  As suggested by Gleason,
       we define it to be 0 when ` 0 <_ K <_ N ` does not hold.  See ~ bcval2t
       for the value in the standard domain. $)
    bcvalt  $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N C. K ) = if ( ( 0 <_ K /\
    K <_ N ) , ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) , 0 ) ) $=
      ( vn vk vm cn0 cz cc0 cv cle wbr wa cfa cfv cmin co cmul cdiv cif cbc
      oprex 0nn0 elisseti ifex wceq breq2 anbi2d ifbid fveq2 opreq1 fveq2d
      opreq1d opreq12d ifeq1d eqtrd breq1 anbi12d opreq2 opreq2d df-bc
      oprabval2 ) CDEBAFGHDIZJKZVBCIZJKZLZVDMNZVDVBOPZMNZVBMNZQPZRPZHSZHAJKZABJ
      KZLZBMNZBAOPZMNZAMNZQPZRPZHSZTVCVBBJKZLZVQBVBOPZMNZVJQPZRPZHSZVPWBHVQWARU
      AHFUBUCUDVDBUEZVMWEVLHSWJWKVFWEVLHWKVEWDVCVDBVBJUFUGUHWKWEVLWIHWKVGVQVKWH
      RVDBMUIWKVIWGVJQWKVHWFMVDBVBOUJUKULUMUNUOVBAUEZWJVPWIHSWCWLWEVPWIHWLVCVNW
      DVOVBAHJUFVBABJUPUQUHWLVPWIWBHWLWHWAVQRWLWGVSVJVTQWLWFVRMVBABOURUKVBAMUIU
      MUSUNUODECUTVA $.
      $( [11-Jul-2005] $) $( [10-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , in its standard
       domain. $)
    bcval3tOLD $p |- ( ( ( N e. NN0 /\ K e. ZZ ) /\ ( 0 <_ K /\ K <_ N ) ) ->
          ( N C. K ) = ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) )  $=
      ( cn0 wcel cz wa cc0 cle wbr cbc co cfa cfv cmin cmul cdiv cif bcvalt
      iftrue sylan9eq ) BCDAEDFGAHIABHIFZBAJKUABLMBANKLMALMOKPKZGQUBABRUAUBGST
      $.
      $( [11-Jul-2005] $) $( [10-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , in its standard
       domain. $)
    bcval2t $p |- ( ( N e. NN0 /\ K e. NN0 /\ K <_ N ) -> ( N C. K ) =
                    ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) )  $=
      ( cn0 wcel cz wa cc0 cle wbr cbc co cfa cfv cmin cmul cdiv wceq w3a
      bcval3tOLD nn0zt anim2i 3adant3 nn0ge0t anim1i 3adant1 sylanc ) BCDZAEDZF
      ZGAHIZABHIZFZBAJKBLMBANKLMALMOKPKQUGACDZUKRABSUGUMUIUKUMUHUGATUAUBUMUKULU
      GUMUJUKAUCUDUEUF $.
      $( [18-Jun-2005] $) $( [9-Jun-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , in its standard
       domain. $)
    bcval3t $p |- ( ( N e. NN0 /\ K e. ( 0 ... N ) ) -> ( N C. K ) =
                    ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) )  $=
      ( cn0 wcel cle wbr cbc co cfa cfv cmin cmul cdiv wceq cc0 cfz wa bcval2t
      elfz3nn0t elfznn0t adantl elfzle2 syl3anc ) BCDZACDZABEFZBAGHBIJBAKHIJAIJ
      LHMHNUDAOBPHDZQABRCABSUGUEUDABTUAUGUFUDAOBUBUAUC $.
      $( [3-Feb-2006] $) $( [3-Jan-2006] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval4t $p |- ( ( N e. NN0 /\ K e. ZZ /\ ( K < 0 \/ N < K ) ) ->
          ( N C. K ) = 0 )  $=
      ( cn0 wcel cz cc0 clt wbr wo cbc co wceq wa cle wn cr wb 0re ltnlet
      mpan2 adantl orbi12d nn0ret zret syl2an biimpa cfa cfv cmin cmul cdiv
      cif bcvalt iffalse sylan9eq ianor sylan2br syldan 3impa ) BCDZAEDZAFGHZBA
      GHZIZBAJKZFLZUTVAMZVDFANHZOZABNHZOZIZVFVGVDVLBPDZAPDZVDVLQUTVAVMVNMVBVIVC
      VKVNVBVIQZVMVNFPDVORAFSTUABASUBBUCAUDUEUFVGVHVJMZOZVFVLVGVQVEVPBUGUHBAUIK
      UGUHAUGUHUJKUKKZFULFABUMVPVRFUNUOVHVJUPUQURUS $.
      $( [9-Sep-2005] $) $( [14-Jul-2005] $)
  $}

  $( "Complementing" its second argument doesn't change a binary
     coefficient. $)
  bccmplt $p |- ( ( N e. NN0 /\ K e. NN0 /\ K <_ N ) -> ( N C. K ) =
                    ( N C. ( N - K ) ) )  $=
    ( cn0 wcel cle wbr w3a cfa cfv cmin co cmul cdiv cbc cc wceq axmulcom cn
    wb nn0subt ancoms biimp3a facclt nncnt 3syl syl 3ad2ant2 sylanc wa nncant
    nn0cnt syl2an fveq2d 3adant3 opreq1d eqtr4d opreq2d bcval2t 3simp1 cr
    caddc nn0addge1t lesubaddt 3anidm13 nn0ret sylan2 mpbird sylan syl3anc
    3eqtr4d ) BCDZACDZABEFZGZBHIZBAJKZHIZAHIZLKZMKVOBVPJKZHIZVQLKZMKZBANKBVPNKZ
    VNVSWBVOMVNVSVRVQLKZWBVQODZVRODZVSWEPVNVQVRQVNVPCDZVQRDWFVKVLVMWHVLVKVMWHSA
    BTUAUBZVPUCVQUDUEVLVKWGVMVLVRRDWGAUCVRUDUFUGUHVNWAVRVQLVKVLWAVRPVMVKVLUIVTA
    HBODAODVTAPVKVLBAUJBUKAUKULUMUNUOUPUQABURVKWHVPBEFZWDWCPVNVPBURVKVLVMUSWIVK
    VLWJVMBUTDZVLWJVKWKVLUIWJBBAVAKEFZBAVBWKAUTDZWJWLSZVLWKWMWNBABVCVDAVEVFVGBV
    EVHUNVIVJ $.
    $( [9-Jul-2005] $) $( [21-Jun-2005] $)

  $( ` N ` choose 0 is 1.  Remark in [Gleason] p. 296. $)
  bcn0t $p |- ( N e. NN0 -> ( N C. 0 ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cfa cfv cmin cmul cdiv c1 cle wbr wceq nn0ge0t 0nn0
    bcval2t mp3an2 mpdan cc nn0cnt subid1t syl fveq2d fac0 a1i opreq12d cn
    facclt nncnt ax1id eqtrd opreq2d wne dividt facne0t sylanc 3eqtrd ) ABCZADE
    FZAGHZADIFZGHZDGHZJFZKFZVBVBKFZLUTDAMNZVAVGOZAPUTDBCVIVJQDARSTUTVFVBVBKUTVF
    VBLJFZVBUTVDVBVELJUTVCAGUTAUACVCAOAUBAUCUDUEVELOUTUFUGUHUTVBUACZVKVBOUTVBUI
    CVLAUJVBUKUDZVBULUDUMUNVLVBDUOVHLOUTVBUPVMAUQURUS $.
    $( [18-Jun-2005] $) $( [17-Jun-2005] $)

  $( ` N ` choose ` N ` is 1.  Remark in [Gleason] p. 296. $)
  bcnnt $p |- ( N e. NN0 -> ( N C. N ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cmin c1 cle wbr wceq nn0ge0t 0nn0 bccmplt mp3an2
    mpdan bcn0t cc nn0cnt subid1t syl opreq2d 3eqtr3rd ) ABCZADEFZAADGFZEFZHAAE
    FUDDAIJZUEUGKZALUDDBCUHUIMDANOPAQUDUFAAEUDARCUFAKASATUAUBUC $.
    $( [2-Aug-2005] $) $( [17-Jun-2005] $)

  $( Binomial coefficient: ` N + 1 ` choose ` 1 ` . $)
  bcnp11t $p |- ( N e. NN0 -> ( ( N + 1 ) C. 1 ) = ( N + 1 ) ) $=
    ( cn0 wcel c1 caddc co cbc cfa cfv cmin cmul cdiv cle wbr wceq 1nn0
    bcval2t mp3an2 peano2nn0 cr 1re nn0addge2t mpan sylanc cc nn0cnt ax1cn
    pncant mpan2 syl fveq2d fac1 a1i opreq12d cn facclt nncnt ax1id eqtrd
    opreq2d facp1t eqcomd w3a cc0 wne wb divmult 3jca facne0t mpbird 3eqtrd )
    ABCZADEFZDGFZVMHIZVMDJFZHIZDHIZKFZLFZVOAHIZLFZVMVMBCZDVMMNZVNVTOZVLWCDBCWDW
    EPDVMQRASZDTCVLWDUADAUBUCUDVLVSWAVOLVLVSWADKFZWAVLVQWAVRDKVLVPAHVLAUECZVPAO
    ZAUFWHDUECWIUGADUHUIUJUKVRDOVLULUMUNVLWAUECZWGWAOVLWAUOCWJAUPWAUQUJZWAURUJU
    SUTVLWBVMOZWAVMKFZVOOZVLVOWMAVAVBVOUECZWJVMUECZVCWAVDVEWLWNVFVLVOWAVMVGVLWO
    WJWPVLWCWOWFWCVOUOCWOVMUPVOUQUJUJWKVLWCWPWFVMUFUJVHAVIUDVJVK $.
    $( [8-Aug-2005] $) $( [21-Jun-2005] $)

  $( Binomial coefficient: ` N + 1 ` choose ` N ` . $)
  bcnp1nt $p |- ( N e. NN0 -> ( ( N + 1 ) C. N ) = ( N + 1 ) ) $=
    ( cn0 wcel c1 caddc co cbc cmin cle wbr wceq 1nn0 bccmplt mp3an2 peano2nn0
    cr 1re nn0addge2t mpan sylanc bcnp11t cc nn0cnt ax1cn pncant mpan2 syl
    opreq2d 3eqtr3rd ) ABCZADEFZDGFZUKUKDHFZGFZUKUKAGFUKBCZDUKIJZULUNKZUJUODBCU
    PUQLDUKMNAODPCUJUPQDARSTAUAUJUMAUKGUJAUBCZUMAKZAUCURDUBCUSUDADUEUFUGUHUI $.
    $( [25-Nov-2005] $) $( [20-Jun-2005] $)

  ${
    bcpasc2.1 $e |- N e. NN $.
    bcpasc2.2 $e |- K e. NN $.
    bcpasc2.3 $e |- K <_ N $.
    $( Pascal's rule for the binomial coefficient.  Equation 2 of [Gleason]
       p. 295. $)
    bcpasc2 $p |- ( ( N C. K ) + ( N C. ( K - 1 ) ) ) = ( ( N + 1 ) C. K ) $=
      ( cfa cfv c1 cmin co cmul cdiv caddc cbc ax1cn nncn subcl addcl nnne0
      cn0 wcel cn cle wbr wb nnnn0 nn0subt mp2an mpbi nn0p1nnt ax-mp divadddiv
      mulid2 mulid1 opreq12i add23 cc wceq npcant opreq1i 3eqtr eqtr opreq2i
      facclt nnge1t 1nn0 nnmulclt divcl reccl adddi nn0addcl nn0cn mulcl muln0
      divmuldiv facp1t nnre nn0re clt nnleltp1t ltlei mulcom facnn2t addsub
      fveq2i mul4 eqtr4 3eqtr3 mul23 3eqtr2 bcval2t mp3an 3eqtr4r mulass
      subsubt eqtr2 1re lesubadd mpbir 3eqtr4 ) BFGZAHIJZFGZBAIJZFGZKJZLJZHALJZ
      KJZXGHXDHMJZLJZKJZMJZBHMJZFGZXNAIJZFGZAFGZKJZLJZBANJZBXBNJZMJXNANJZXGXHXK
      MJZKJXGXNAXJKJZLJZKJZXMXTYDYFXGKYDHXJKJZAHKJZMJZYELJYFHAHXJOADPZOXDHBABCP
      ZYKQZORZADSZXJXDTUAZXJUBUAABUCUDZYPEATUAZBTUAZYQYPUEADUFZBCUFZABUGUHUIZXD
      UJUKSZULYJXNYELYJXJAMJXDAMJZHMJXNYHXJYIAMXJYNUMAYKUNUOXDHAYMOYKUPUUDBHMBU
      QUAZAUQUAZUUDBURYLYKBAUSUHUTVAUTVBVCXGXHXKXAXFXAYSXAUBUAUUABVDUKPZXFXCUBU
      AZXEUBUAZXFUBUAXBTUAZUUHHAUCUDZUUJAUBUAZUUKDAVEUKHTUAYRUUKUUJUEVFYTHAUGUH
      UIZXBVDUKZYPUUIUUBXDVDUKZXCXEVGUHZPZXFUUPSZVHAYKYOVIXJYNUUCVIVJYGXAXNKJZX
      FYEKJZLJXTXAXFXNYEUUGUUQXNBHUUAVFVKZVLAXJYKYNVMUURAXJYKYNYOUUCVNVOXOUUSXS
      UUTLYSXOUUSURUUABVPUKXSXRXQKJXCAKJZXEXJKJZKJUUTXQXRXQXPTUAZXQUBUAAXNUCUDZ
      UVDAXNADVQZXNUVAVRYQAXNVSUDZEUULBUBUAYQUVGUEDCABVTUHUIWAZYRXNTUAZUVEUVDUE
      YTUVAAXNUGUHUIXPVDUKPXRYRXRUBUAYTAVDUKPZWBXRUVBXQUVCKUULXRUVBURDAWCUKZXQX
      JFGZUVCXPXJFBHAYLOYKWDWEYPUVLUVCURUUBXDVPUKZVBUOXCAXEXJXCUUNPZYKXEUUOPZYN
      WFVAUOWGWHYAXIYBXLMXAHKJZXFAKJZLJXAXEXRKJZLJZXIYAUVPXAUVQUVRLXAUUGUNZUVQU
      VBXEKJXRXEKJUVRXCXEAUVNUVOYKWIXRUVBXEKUVKUTXRXEUVJUVOWBWJUOXAXFHAUUGUUQOY
      KUURYOVOYSYRYQYAUVSURUUAYTEABWKWLWMUVPXFXJKJZLJXABXBIJZFGZXCKJZLJZXLYBUVP
      XAUWAUWDLUVTUWAXCUVCKJXCUWCKJUWDXCXEXJUVNUVOYNWNUVCUWCXCKUWCUVLUVCUWBXJFU
      UEUUFHUQUAUWBXJURYLYKOBAHWOWLWEUVMWPVCXCUWCUVNUWCUWBTUAZUWCUBUAXBBUCUDZUW
      FUWGUVEUVHAHBUVFWQBCVQWRWSZUUJYSUWGUWFUEUUMUUAXBBUGUHUIUWBVDUKPWBVAUOXAXF
      HXJUUGUUQOYNUURUUCVOYSUUJUWGYBUWEURUUAUUMUWHXBBWKWLWMUOUVIYRUVEYCXTURUVAY
      TUVHAXNWKWLWT $.
      $( [23-Jun-2005] $) $( [19-Jun-2005] $)
  $}

  $( Pascal's rule for the binomial coefficient.  Equation 2 of [Gleason]
     p. 295. $)
  bcpasc2t $p |- ( ( N e. NN /\ K e. NN /\ K <_ N ) ->
                ( ( N C. K ) + ( N C. ( K - 1 ) ) ) = ( ( N + 1 ) C. K ) ) $=
    ( cn wcel cle wbr w3a cbc co c1 cmin caddc wceq cif opreq1 opreq12d
    opreq1d eqeq12d opreq2 opreq2d eleq1 breq2 3anbi13d breq1 3anbi23d 1nn 1re
    leid 3pm3.2i elimhyp2v 3simp1i 3simp2i 3simp3i bcpasc2 dedth2v ) BCDZACDZAB
    EFZGZBAHIZBAJKIZHIZLIZBJLIZAHIZMUSBJNZAHIZVFVAHIZLIZVFJLIZAHIZMVFUSAJNZHIZV
    FVLJKIZHIZLIZVJVLHIZMBAJJBVFMZVCVIVEVKVRUTVGVBVHLBVFAHOBVFVAHOPVRVDVJAHBVFJ
    LOQRAVLMZVIVPVKVQVSVGVMVHVOLAVLVFHSVSVAVNVFHAVLJKOTPAVLVJHSRVLVFVFCDZVLCDZV
    LVFEFZUSVTUQAVFEFZGVTWAWBGJCDZWDJJEFZGBAJJVTWDJVFEFZGVRUPVTURWCUQBVFCUABVFA
    EUBUCVSUQWAWCWBVTAVLCUAAVLVFEUDUEJVFMWDVTWEWFWDJVFCUAJVFJEUBUCJVLMWDWAWFWBV
    TJVLCUAJVLVFEUDUEWDWDWEUFUFJUGUHUIUJZUKVTWAWBWGULVTWAWBWGUMUNUO $.
    $( [9-Sep-2005] $) $( [20-Jun-2005] $)

  ${
    bcpasc.1 $e |- N e. NN0 $.
    bcpasc.2 $e |- K e. ZZ $.
    $( Pascal's rule for the binomial coefficient, generalized to all integers
       ` K ` .  Equation 2 of [Gleason] p. 295. $)
    bcpasc $p |- ( ( N C. K ) + ( N C. ( K - 1 ) ) ) = ( ( N + 1 ) C. K ) $=
      ( cn wcel cle wbr cbc co c1 cmin caddc wceq wn cc0 cn0 wo elnn0 mpbi ori
      ax1cn addid1 opreq2 0nn0 bcn0t ax-mp syl6eq cneg opreq1 df-neg syl6eqr
      opreq2d cz clt 1z znegclt lt01 cr wb 1re lt0neg2t orci bcval4t mp3an
      opreq12d 1nn0 3eqtr4a wne zre 0re lttri2 0cn mp3an12 orcs leid zlem1ltt
      mp2an resubcl lttr mpan orc zsubclt 3syl olcs opreq1d 0z zltp1let addid2
      breq1i leloe 3bitr posdif olc sylbi syl eqcomd syl5eqr eqtr2d subid
      bcnnt eqtr3d jaoi eqtrd pm2.61ine znnnlt1t zleltp1t breq2i 3bitr3r bitr
      nn0addcl eqbrtrr syl6eqbr syl6req nn0re ltnle nn0zt readdcl ltaddsub cc
      nn0cn pncant sylbir bcpasc2t 3ecase ) BEFZAEFZABGHZBAIJZBAKLJZIJZMJZBKMJZ
      AIJZNZYFOBPNZYOYFYPBQFZYFYPRCBSTUAYPPAIJZPYJIJZMJZKAIJZYLYNYTUUANZAPAPNZK
      PMJZKYTUUAKUBUCZUUCYRKYSPMUUCYRPPIJZKAPPIUDPQFZUUFKNUEPUFUGZUHUUCYSPKUIZI
      JZPUUCYJUUIPIUUCYJPKLJZUUIAPKLUJZKUKZULUMUUGUUIUNFZUUIPUOHZPUUIUOHZRUUJPN
      UEKUNFZUUNUPKUQUGUUOUUPPKUOHZUUOURKUSFUURUUOUTVAKVBUGTZVCUUIPVDVEUHVFUUCU
      UAKPIJZKAPKIUDKQFZUUTKNVGKUFUGUHVHAPVIAPUOHZPAUOHZRZUUBAPADVJZVKVLUVBUUBU
      VCUVBPPMJZPYTUUAPVMUCZUVBYRPYSPMUVBUVCYRPNZUUGAUNFZUVDUVHUEDAPVDVNZVOUVBY
      JPUOHZUVKPYJUOHZRZYSPNZYJAUOHZUVBUVKAAGHZUVOAUVEVPUVIUVIUVPUVOUTDDAAVQVRT
      YJAPAKUVEVAVSUVEVKVTWAZUVKUVLWBUUGYJUNFZUVMUVNUEUVIUUQUVRDUPAKWCVRZYJPVDV
      NZWDVFUVBKAUOHZUUAPNZUVAUVIUVBUWARUWBVGDAKVDVNZVOVHUVCYTPYSMJZUUAUVCYRPYS
      MUVBUVCUVHUVJWEWFUVCUWAKANZRZUWDUUANZUVCPKMJZAGHZKAGHUWFPUNFZUVIUVCUWIUTW
      GDPAWHVRUWHKAGKUBWIZWJKAVAUVEWKWLUWAUWGUWEUWAUUAPUWDUVBUWAUWBUWCWEUWAUVFU
      WDPUWAPYSPMUWAYSPUWAUVMUVNUWAUVLUVMKAVAUVEWMUVLUVKWNWOUVTWPWQUMUVGWRWSUWE
      KUWDUUAUWEUWHUWDKUWEKYSPMUWEUUFYSKUWEPYJPIUWEKKLJYJPKAKLUJKUBWTWRUMUUHWRU
      MUWKWRUWEKKIJZUUAKKAKIUDUVAUWLKNVGKXAUGWRXBXCWOXDXCWOXEYPYIYRYKYSMBPAIUJB
      PYJIUJVFYPYMKAIYPYMUWHKBPKMUJUWKUHWFVHWPYGOZUVBUUCRZYOUWMAKUOHZUWNUVIUWMU
      WOUTDAXFUGAPGHZAUWHUOHZUWNUWOUVIUWJUWPUWQUTDWGAPXGVRAPUVEVKWKUWHKAUOUWKXH
      XIXJUVBYOUUCUVBUVFPYLYNUVGUVBYIPYKPMUVBBAUOHZYIPNZYQUVIUVBUWRRUWSCDABVDVN
      ZVOUVBUVKUVKBYJUOHZRZYKPNZUVQUVKUXAWBZYQUVRUXBUXCCUVSYJBVDVNZWDVFUVBYMAUO
      HZYNPNZYMQFZUVIUVBUXFRUXGBKCVGXKZDAYMVDVNZVOVHUUCYLKYNUUCYLUUDKUUCYIKYKPM
      UUCYIBPIJZKAPBIUDYQUXKKNCBUFUGUHUUCUVKUXBUXCUUCYJUUKPUOUULUUIUUKPUOUUMUUS
      XLXMUXDUXEWDVFUUEUHUUCYNYMPIJZKAPYMIUDUXHUXLKNUXIYMUFUGXNXDXCWOYHOUWRYOBA
      BCXOZUVEXPUWRYLPYKMJZYNUWRYIPYKMUVBUWRUWSUWTWEWFUWRUXFYMANZRZUXNYNNZUWRYM
      AGHZUXPBUNFZUVIUWRUXRUTYQUXSCBXQUGDBAWHVRYMABKUXMVAXRUVEWKXJUXFUXQUXOUXFU
      VFPUXNYNUVGUXFYKPPMUXFUXBUXCUXFUXAUXBBKAUXMVAUVEXSUXAUVKWNWOUXEWPUMUVBUXF
      UXGUXJWEVHUXOKUXNYNUXOUWHUXNKUXOKYKPMUXOBBIJZYKKUXOBYJBIUXOYMKLJZYJBYMAKL
      UJBXTFKXTFUYABNBCYAUBBKYBVRWRUMYQUXTKNCBXAUGWRUMUWKWRUXOYMYMIJZYNKYMAYMIU
      DUXHUYBKNUXIYMXAUGWRXBXCWOXDYCABYDYE $.
      $( [21-Sep-2005] $) $( [13-Jul-2005] $)
  $}

  $( Pascal's rule for the binomial coefficient, generalized to all integers
     ` K ` .  Equation 2 of [Gleason] p. 295. $)
  bcpasct $p |- ( ( N e. NN0 /\ K e. ZZ ) ->
                ( ( N C. K ) + ( N C. ( K - 1 ) ) ) = ( ( N + 1 ) C. K ) ) $=
    ( cn0 wcel cz cbc co c1 cmin caddc wceq cif opreq1 opreq12d opreq1d
    eqeq12d opreq2 opreq2d 1nn0 elimel 1z bcpasc dedth2h ) BCDZAEDZBAFGZBAHIGZF
    GZJGZBHJGZAFGZKUDBHLZAFGZULUGFGZJGZULHJGZAFGZKULUEAHLZFGZULURHIGZFGZJGZUPUR
    FGZKBAHHBULKZUIUOUKUQVDUFUMUHUNJBULAFMBULUGFMNVDUJUPAFBULHJMOPAURKZUOVBUQVC
    VEUMUSUNVAJAURULFQVEUGUTULFAURHIMRNAURUPFQPURULBHCSTAHEUATUBUC $.
    $( [31-Jan-2006] $) $( [13-Jul-2005] $)

  ${
    $d k n N $.  $d n j k $.  $d k K $.
    $( A binomial coefficient, in its standard domain, is a natural number. $)
    bccl2t $p |- ( ( N e. NN0 /\ K e. ( 0 ... N ) ) -> ( N C. K ) e. NN ) $=
      ( vk vn vj cn0 wcel cle wbr cbc co cn cc0 cfz wa wceq wo wi cv wral
      breq1 opreq2 eleq1d imbi12d rcla4v c1 caddc breq2 opreq1 ralbidv
      nnle1eq1t 1nn0 bcnnt ax-mp 1nn eqeltr syl6eqel syl6bi rgen ax-17 hbra1
      clt cr wb leloet nnret peano2nn syl syl2an nnleltp1t orbi1d bitr4d
      nnge1t 1re mpan mpbid adantr cmin nnaddclt ra4 imp32 ancoms adantllr
      adantlrl cz nnzt peano2zm nnltlem1t biimpd anim2d elnnz1 syl6ibr mpand
      cbvralv syl5ib com23 syl6 lep1t lesubaddt mp3an2 anidms mpbird letrt
      3expa peano2rem ancri sylan syl5d imp42 sylanc bcpasc2t 3com12 adantrl
      exp31 com12 nnnn0t bcnp11t eqeltrd syl5bi adantld a1dd adantrd ccase2
      sylbid ex com4t r19.21ad nnind syl5com imp wn negbid nngt0t 0re ltnlet
      syl5bir pm2.21d bcn0t adantl a1d opreq12 0nn0 ccase elnn0 syl2anb 3impia
      elfz3nn0t elfznn0t elfzle2 syl3anc ) BFGZAFGZABHIZBAJKZLGZUUKAMBNKGZOUUKU
      ULUUMUUOBLGZBMPZQALGZAMPZQUUMUUORZUUKUULUUQUUSUURUUTUVAUUQUUSUVAUUSCSZBHI
      ZBUVBJKZLGZRZCLTZUVAUUQUVFUVACALUVBAPZUVCUUMUVEUUOUVBABHUAUVHUVDUUNLUVBAB
      JUBUCUDUEUVBDSZHIZUVIUVBJKZLGZRZCLTUVBUFHIZUFUVBJKZLGZRZCLTUVBESZHIZUVRUV
      BJKZLGZRZCLTZUVBUVRUFUGKZHIZUWDUVBJKZLGZRZCLTUVGDEBUVIUFPZUVMUVQCLUWIUVJU
      VNUVLUVPUVIUFUVBHUHUWIUVKUVOLUVIUFUVBJUIUCUDUJUVIUVRPZUVMUWBCLUWJUVJUVSUV
      LUWAUVIUVRUVBHUHUWJUVKUVTLUVIUVRUVBJUIUCUDUJUVIUWDPZUVMUWHCLUWKUVJUWEUVLU
      WGUVIUWDUVBHUHUWKUVKUWFLUVIUWDUVBJUIUCUDUJUVIBPZUVMUVFCLUWLUVJUVCUVLUVEUV
      IBUVBHUHUWLUVKUVDLUVIBUVBJUIUCUDUJUVQCLUVBLGZUVNUVBUFPZUVPUVBUKUWNUVOUFUF
      JKZLUVBUFUFJUBUWOUFLUFFGUWOUFPULUFUMUNUOUPUQURUSUVRLGZUWCUWHCLUWPCUTUWBCL
      VAUWMUWEUWPUWCUWGUWMUWPUWEUWCUWGRZUWMUWPUWEUWQRUWMUWPOZUWEUVSUVBUWDPZQZUW
      QUWRUWEUVBUWDVBIZUWSQZUWTUVBVCGZUWDVCGZUWEUXBVDUWMUWPUVBUWDVEUVBVFZUWPUWD
      LGUXDUVRVGZUWDVFVHVIUWRUVSUXAUWSUVBUVRVJVKVLUWRUFUVBVBIZUFUVBPZQZUWTUWQUW
      MUXIUWPUWMUFUVBHIZUXIUVBVMUWMUXCUXJUXIVDZUXEUFVCGZUXCUXKVNUFUVBVEVOVHVPVQ
      UXIUWTOUWRUWQUXGUVSUXHUWSUWRUWQRUWRUXGUVSOZUWQUWRUXMUWCUWGUWRUXMOZUWCOZUV
      TUVRUVBUFVRKZJKZUGKZLGZUWGUWAUXQLGZUXSUXOUVTUXQVSUWRUVSUWCUWAUXGUWMUVSUWC
      UWAUWPUWCUWMUVSOUWAUWCUWMUVSUWAUWBCLVTWAWBWCWDUWRUXGUVSUWCUXTUWRUXGUXPUVR
      HIZUWCUXTRZUVSUWMUXGUYAUYBRZRUWPUWMUXGUXPLGZUYCUWMUXPWEGZUXGUYDUWMUVBWEGU
      YEUVBWFUVBWGVHUWMUYEUXGOUYEUFUXPHIZOUYDUWMUXGUYFUYEUWMUXGUYFUFLGUWMUXGUYF
      VDUOUFUVBWHVOWIWJUXPWKWLWMUYDUWCUYAUXTUYDUVIUVRHIZUVRUVIJKZLGZRZDLTUYAUXT
      RZUWCUYJUYKDUXPLUVIUXPPZUYGUYAUYIUXTUVIUXPUVRHUAUYLUYHUXQLUVIUXPUVRJUBUCU
      DUEUWBUYJCDLUVBUVIPZUVSUYGUWAUYIUVBUVIUVRHUAUYMUVTUYHLUVBUVIUVRJUBUCUDWNW
      OWPWQVQUXCUVRVCGZUVSUYARUWMUWPUXCUYNOUXPUVBHIZUVSUYAUXCUYOUYNUXCUYOUVBUVB
      UFUGKHIZUVBWRUXCUYOUYPVDZUXCUXLUXCUYQVNUVBUFUVBWSWTXAXBVQUXPVCGZUXCOUYNUY
      OUVSOUYARZUXCUYRUXCUYNUYSUXPUVBUVRXCXDUXCUYRUVBXEXFXGWMUXEUVRVFVIXHXIXJUX
      NUXSUWGVDZUWCUWRUVSUYTUXGUWRUVSOUXRUWFLUWMUWPUVSUXRUWFPZUWPUWMUVSVUAUVBUV
      RXKXLXDUCXMVQVPXNXOUXHUWRUWGUWCUXHUWPUWGUWMUXHUWDUFJKZLGUWGUWPUXHVUBUWFLU
      FUVBUWDJUBUCUWPVUBUWDLUWPUVRFGVUBUWDPUVRXPUVRXQVHUXFXRXSXTYAUWSUWRUWGUWCU
      WSUWMUWGUWPUWSUVBUVBJKZLGUWGUWMUWSVUCUWFLUVBUWDUVBJUIUCUWMVUCUFLUWMUVBFGV
      UCUFPUVBXPUVBUMVHUOUQXSYBYAYCXOWMYDYEWPYFYGYHYIYJUURUUSOUUMUUOUURUUSUUMYK
      ZUURVUDAMHIZYKZUUSUURUUMVUEBMAHUHYLUUSMAVBIZVUFAYMUUSAVCGZVUGVUFVDZAVFMVC
      GVUHVUIYNMAYOVOVHVPYPYJYQUUQUUTOZUUOUUMVUJUUOBMJKZLGZUUQVULUUTUUQVUKUFLUU
      QUUKVUKUFPBXPBYRVHUOUQVQUUTUUOVULVDUUQUUTUUNVUKLAMBJUBUCYSXBYTUURUUTOZUUO
      UUMVUMUUNMMJKZLBMAMJUUAVUNUFLMFGVUNUFPUUBMYRUNUOUPUQYTUUCBUUDAUUDUUEUUFFA
      BUUGUUPUULUUKABUUHYSUUPUUMUUKAMBUUIYSUUJ $.
      $( [11-Apr-2006] $) $( [3-Jan-2006] $)
  $}

  $( A binomial coefficient, in its extended domain, is a nonnegative
     integer. $)
  bcclt $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N C. K ) e. NN0 ) $=
    ( cn0 wcel cz wa cc0 cle wbr cbc co cn cfz bccl2t simpll wi fznn0t biimprd
    exp3a elnn0z syl5ibr imp43 sylanc nnnn0t syl wn cfa cfv cmin cmul cdiv cif
    bcvalt iffalse sylan9eq 0nn0 syl6eqel pm2.61dan ) BCDZAEDZFZGAHIZABHIZFZBAJ
    KZCDZVAVDFZVELDZVFUSAGBMKDZVHVGABNUSUTVDOUSUTVBVCVIUSUTVBVCVIPZUSACDZVJUTVB
    FUSVKVCVIUSVIVKVCFABQRSATUASUBUCVEUDUEVAVDUFZFVEGCVAVLVEVDBUGUHBAUIKUGUHAUG
    UHUJKUKKZGULGABUMVDVMGUNUOUPUQUR $.
    $( [22-Sep-2005] $) $( [10-Jul-2005] $)

  $( The number of permutations of ` N - R ` objects from a collection of ` N `
     objects is a natural number.  (Contributed by Jason Orendorff,
     24-Jan-2007.) $)
  permnnt $p |- ( ( N e. NN0 /\ R e. ( 0 ... N ) ) ->
               ( ( ! ` N ) / ( ! ` R ) ) e. NN ) $=
    ( cfa cfv cn wcel cmin co cmul cc w3a cdiv wa cn0 cc0 cfz nndivtrt
    elfznn0t facclt syl adantl nnmulclt fznn0subt sylanc nncnt adantr 3jca wne
    wceq divcan4t facne0t syl3anc eqeltrd cbc bcval3t bccl2t eqeltrrd jca ) ACD
    ZEFZBAGHZCDZUSIHZEFZBCDZJFZKVCUSLHZEFZVEVCLHZEFZMVEUSLHEFBNFZAOBPHFZMZUSVCV
    EQVMUTVDVFVLUTVKVLANFZUTABRZASTZUAZVBEFZUTVDVMVBUSUBVMVANFVRNAOBUCVASTZVQUD
    VKVFVLVKVEEFVFBSVEUETUFUGVMVHVJVMVGVBEUSJFZVBJFZUSOUHZVGVBUIVMUSVBUJVLVTVKV
    LUTVTVPUSUETUAVMVRWAVSVBUETVLWBVKVLVNWBVOAUKTUAULVSUMVMBAUNHVIEABUOABUPUQUR
    UD $.
    $( [24-Jan-2007] $) $( [24-Jan-2007] $)

