$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The binomial coefficient operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c _C $. $( Binomial coefficient operation. $)

  $( Extend class notation to include the binomial coefficient operation
     (combinatorial choose operation). $)
  cbc $a class _C $.

  ${
    $d n k m $.
    $( Define the binomial coefficient operation.  In the literature, this
       function is often written as a column vector of the two arguments, or
       with the arguments as subscripts before and after the letter "C".
       ` ( N _C K ) ` is read " ` N ` choose ` K ` ."  Definition of binomial
       coefficient in [Gleason] p. 295.  As suggested by Gleason, we define it
       to be 0 when ` 0 <_ k <_ n ` does not hold. $)
    df-bc $a |- _C = ( n e. NN0 , k e. ZZ |-> if ( k e. ( 0 ... n ) ,
                ( ( ! ` n ) / ( ( ! ` ( n - k ) ) x. ( ! ` k ) ) ) , 0 ) ) $.
  $}

  ${
    $d n k m N $.  $d n k m K $.
    $( Value of the binomial coefficient, ` N ` choose ` K ` .  Definition of
       binomial coefficient in [Gleason] p. 295.  As suggested by Gleason, we
       define it to be 0 when ` 0 <_ K <_ N ` does not hold.  See ~ bcval2 for
       the value in the standard domain. $)
    bcval $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) = if ( K e. ( 0 ... N )
      , ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) , 0 ) ) $=
      ( vn vk cv cc0 cfz co wcel cfa cfv cmin cmul cdiv cif opreq2 fveq2 fveq2d
      wceq opreq12d cn0 cbc eleq2d opreq1 opreq1d eqidd ifbieq12d eleq1 opreq2d
      cz df-bc oprex cc 0cn elisseti ifex ovmpt2 ) CDBAUAUJDEZFCEZGHZIZUSJKZUSU
      RLHZJKZURJKZMHZNHZFOAFBGHZIZBJKZBALHZJKZAJKZMHZNHZFOUBURVHIZVJBURLHZJKZVE
      MHZNHZFOUSBSZVAVPVGFVTFWAUTVHURUSBFGPUCWAVBVJVFVSNUSBJQWAVDVRVEMWAVCVQJUS
      BURLUDRUETWAFUFUGURASZVPVIVTFVOFURAVHUHWBVSVNVJNWBVRVLVEVMMWBVQVKJURABLPR
      URAJQTUIWBFUFUGDCUKVIVOFVJVNNULFUMUNUOUPUQ $.
      $( [7-Nov-2013] $) $( [10-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , in its standard
       domain. $)
    bcval2 $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) =
                    ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) ) $=
      ( wcel cc0 cfz co cbc cfa cfv cmin cmul cdiv cif cn0 wceq elfz3nn0 adantl
      wa cz elfzelz bcval syl11anc iftrue eqtrd ) BCDZAEBFGDZSZBAHGZUGBIJBAKGIJ
      AIJLGMGZENZUJUHBODATDZUIUKPCABQUGULUFAEBUARABUBUCUGUKUJPUFUGUJEUDRUE $.
      $( [7-Nov-2013] $) $( [9-Jun-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval3 $p |- ( ( N e. NN0 /\ K e. ZZ /\ -. K e. ( 0 ... N ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 cfz co wn w3a cbc cfa cfv cmin cmul cdiv cif wceq bcval
      3adant3 iffalse 3ad2ant3 eqtrd ) BCDZAEDZAFBGHDZIZJBAKHZUFBLMBANHLMALMOHP
      HZFQZFUDUEUHUJRUGABSTUGUDUJFRUEUFUIFUAUBUC $.
      $( [8-Nov-2013] $) $( [14-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval4 $p |- ( ( N e. NN0 /\ K e. ZZ /\ ( K < 0 \/ N < K ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 clt wbr wo w3a cfz co wn wa cle cr lenlt mpbid adantl
      wb cbc wceq simp1 simp2 elfzle1 0re elfzelz zre syl sylancr elfzle2 nn0re
      wi syl2anr jca ioran sylibr ex adantr con2d 3impia bcval3 syl111anc ) BCD
      ZAEDZAFGHZBAGHZIZJVDVEAFBKLDZMZBAUALFUBVDVEVHUCVDVEVHUDVDVEVHVJVDVENVIVHV
      DVIVHMZUMVEVDVIVKVDVINZVFMZVGMZNVKVLVMVNVIVMVDVIFAOHZVMAFBUEVIFPDAPDZVOVM
      TUFVIVEVPAFBUGAUHUIZFAQUJRSVLABOHZVNVIVRVDAFBUKSVIVPBPDVRVNTVDVQBULABQUNR
      UOVFVGUPUQURUSUTVAABVBVC $.
      $( [7-Nov-2013] $) $( [14-Jul-2005] $)
  $}

  $( Closure of the binomial coefficient in the positive reals.  (This is
     mostly a lemma before we have ~ bccl2 .)  (Contributed by Mario Carneiro,
     10-Mar-2014.) $)
  bcrpcl $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) e. RR+ ) $=
    ( wcel cc0 cfz co wa cbc cfa cfv cmin cmul crp cn cn0 faccl syl2an syl11anc
    nnrp cdiv bcval2 elfz3nn0 fznn0sub elfznn0 adantl nnmulcl rpdivcl eqeltrd
    syl ) BCDZAEBFGDZHZBAIGBJKZBALGZJKZAJKZMGZUAGZNABCUBUMUNODZURODZUSNDZUMBPDU
    TCABUCBQUJUMUOPDZAPDZVACAEBUDULVDUKABUEUFVCUPODUQODVAVDUOQAQUPUQUGRSUTUNNDU
    RNDVBVAUNTURTUNURUHRSUI $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( "Complementing" its second argument doesn't change a binary coefficient.
     (Revised by Mario Carneiro, 5-Mar-2014.) $)
  bccmpl $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) =
                    ( N _C ( N - K ) ) ) $=
    ( cn0 wcel cz wa cc0 co cbc cmin wceq cfa cmul cdiv cc fznn0sub2 cn elfznn0
    cfv faccl cfz nncn 3syl syl adantl mulcom syl11anc elfzelz nn0cn zcn syl2an
    nncan sylan2 fveq2d opreq1d eqtr4d opreq2d bcval2 syldan 3eqtr4d adantlr wn
    w3a bcval3 simp1 nn0z zsubcl sylan 3adant3 wi ex adantr eleq1d sylibd con3d
    3impia syl111anc 3expa pm2.61dan ) BCDZAEDZFZAGBUAHZDZBAIHZBBAJHZIHZKZVTWDW
    HWAVTWDFZBLSZWFLSZALSZMHZNHWJBWFJHZLSZWKMHZNHZWEWGWIWMWPWJNWIWMWLWKMHZWPWIW
    KODZWLODZWMWRKWIWFWCDZWSCABPZXAWFCDWKQDWSWFBRWFTWKUBUCUDWIACDZWLQDWTWDXCVTA
    BRUEATWLUBUCWKWLUFUGWIWOWLWKMWIWNALWDVTWAWNAKZAGBUHVTBODAODXDWABUIAUJBAULUK
    ZUMUNUOUPUQABCURVTWDXAWGWQKXBWFBCURUSUTVAVTWAWDVBZWHVTWAXFVCZWEGWGABVDXGVTW
    FEDZXAVBZWGGKVTWAXFVEVTWAXHXFVTBEDWAXHBVFBAVGVHVIVTWAXFXIWBXAWDWBXAWNWCDZWD
    VTXAXJVJWAVTXAXJCWFBPVKVLWBWNAWCXEVMVNVOVPWFBVDVQUPVRVS $.
    $( [5-Mar-2014] $) $( [21-Jun-2005] $)

  $( ` N ` choose 0 is 1.  Remark in [Gleason] p. 296. $)
  bcn0 $p |- ( N e. NN0 -> ( N _C 0 ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cfa cfv cmin cmul cdiv c1 cfz wceq cuz eluzfz1 eleq2s
    nn0uz cc syl bcval2 mpdan nn0cn subid1 fveq2d fac0 opreq12 sylancl cn faccl
    nncn mulid1 eqtrd opreq2d wne facne0 divid syl11anc 3eqtrd ) ABCZADEFZAGHZA
    DIFZGHZDGHZJFZKFZVBVBKFZLUTDDAMFCZVAVGNVIADOHBDAPRQDABUAUBUTVFVBVBKUTVFVBLJ
    FZVBUTVDVBNVELNVFVJNUTVCAGUTASCVCANAUCAUDTUEUFVDVBVELJUGUHUTVBSCZVJVBNUTVBU
    ICVKAUJVBUKTZVBULTUMUNUTVKVBDUOVHLNVLAUPVBUQURUS $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( ` N ` choose ` N ` is 1.  Remark in [Gleason] p. 296. $)
  bcnn $p |- ( N e. NN0 -> ( N _C N ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cmin c1 cz wceq 0z bccmpl mpan2 bcn0 nn0cn subid1 syl
    cc opreq2d 3eqtr3rd ) ABCZADEFZAADGFZEFZHAAEFUADICUBUDJKDALMANUAUCAAEUAARCU
    CAJAOAPQST $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( Binomial coefficient: ` N ` choose ` 1 ` . $)
  bcn1 $p |- ( N e. NN0 -> ( N _C 1 ) = N ) $=
    ( cn0 wcel cn cc0 wceq wo c1 cbc co cfa cfv cmul cdiv cuz cc syl nncn eqtrd
    clt elnn0 cmin cfz wa elnnuz biimpi 1nn0 nn0uz eleqtri jctil elfzuzb mpbird
    bcval2 mpdan facnn2 fac1 opreq2i nnm1nn0 faccl mulid1 syl5eq opreq12d nnne0
    3syl wne divcan3 syl111anc cz wbr 0nn0 lt01 olci bcval4 mp3an opreq1 eqeq12
    1z wb mpancom mpbiri jaoi sylbi ) ABCADCZAEFZGAHIJZAFZAUAWCWFWDWCWEAKLZAHUB
    JZKLZHKLZMJZNJZAWCHEAUCJCZWEWLFWCWMHEOLZCZAHOLCZUDWCWPWOWCWPAUEUFHBWNUGUHUI
    UJDHEAUKULHADUMUNWCWLWIAMJZWINJZAWCWGWQWKWINAUOWCWKWIHMJZWIWJHWIMUPUQWCWIDC
    ZWIPCZWSWIFWCWHBCWTAURWHUSQZWIRZWIUTVDVAVBWCAPCXAWIEVEZWRAFARWCWTXAXBXCQWCW
    TXDXBWIVCQAWIVFVGSSWDWFEHIJZEFZEBCHVHCHETVIZEHTVIZGXFVJVQXHXGVKVLHEVMVNWEXE
    FWDWFXFVRAEHIVOWEXEAEVPVSVTWAWB $.
    $( [8-Nov-2013] $) $( [21-Jun-2005] $)

  $( Binomial coefficient: ` N + 1 ` choose ` N ` . $)
  bcnp1n $p |- ( N e. NN0 -> ( ( N + 1 ) _C N ) = ( N + 1 ) ) $=
    ( cn0 wcel c1 caddc co cmin cz wceq peano2nn0 nn0z bccmpl syl11anc cc nn0cn
    cbc ax-1cn pncan2 sylancl opreq2d bcn1 syl 3eqtrd ) ABCZADEFZAPFZUEUEAGFZPF
    ZUEDPFZUEUDUEBCZAHCUFUHIAJZAKAUELMUDUGDUEPUDANCDNCUGDIAOQADRSTUDUJUIUEIUKUE
    UAUBUC $.
    $( [8-Nov-2013] $) $( [20-Jun-2005] $)

  $( The proportion of one binomial coefficient to another with ` K ` decreased
     by 1.  (Contributed by Mario Carneiro, 10-Mar-2014.) $)
  bcm1k $p |- ( ( N e. V /\ K e. ( 1 ... N ) ) -> ( N _C K ) =
                ( ( N _C ( K - 1 ) ) x. ( ( N - ( K - 1 ) ) / K ) ) ) $=
    ( wcel c1 cfz co wa cfa cfv cmul cdiv cc wceq syl cn nncn cc0 cn0 faccl cbc
    cmin caddc cz elfzel2g zcn elfznn adantl ax-1cn a1i subsub syl111anc fveq2d
    opreq1d opreq2d opreq12d wne cuz elfzuz2 nnuz syl6eleqr nnnn0 nn0p1nn nn0cn
    fznn0sub nnm1nn0 nnmulcl syl11anc nnne0 jca divmuldiv syl22anc mul32 eqcomd
    facp1 facnn2 mulass 3eqtrd eqtr4d divcan5 eqtrd 3eqtrrd wss addid2i opreq1i
    mulcom 0z fzp1ss syl5eqssr ssel2 sylan bcval2 syldan npcan sylancl peano2zm
    mpan uzid peano2uz 3syl eqeltrrd fzss2 simpr elfzelz elfzm1b sseldd 3eqtr4d
    wb mpbid ) BCDZAEBFGZDZHZBIJZBAUBGZIJZAIJZKGZLGZXNBAEUBGZUBGZIJZXTIJZKGZLGZ
    YAALGZKGZBAUAGZBXTUAGZYFKGXMYGXNXOEUCGZIJZYCKGZLGZYJALGZKGZXNYJKGZXRYJKGZLG
    ZXSXMYEYMYFYNKXMYDYLXNLXMYBYKYCKXMYAYJIXMBMDZAMDZEMDZYAYJNXMBUDDZYSCAEBUEZB
    UFOZXMAPDZYTXLUUEXJABUGUHZAQOZUUAXMUIUJBAEUKULZUMUNUOXMYAYJALUUHUNUPXMYOYPY
    LAKGZLGZYRXMXNMDZYJMDZYLMDZYLRUQZHZYTARUQZHYOUUJNXMXNPDZUUKXMBSDZUUQXMBPDUU
    RXMBEURJPCAEBUSUTVABVBOBTOXNQOZXMYJSDZUULXMYJPDZUUTXMXOSDZUVACAEBVEZXOVCOZY
    JVBOZYJVDOZXMYLPDZUUOXMYKPDZYCPDZUVGXMUUTUVHUVEYJTOZXMXTSDZUVIXMUUEUVKUUFAV
    FOXTTOZYKYCVGVHUVGUUMUUNYLQYLVIVJOXMYTUUPUUGXMUUEUUPUUFAVIOVJXNYJYLAVKVLXMY
    QUUIYPLXMYQXPYJKGZXQKGZYKYCAKGZKGZUUIXMXPMDZXQMDZUULYQUVNNXMXPPDZUVQXMUVBUV
    SUVCXOTOZXPQOXMXQPDZUVRXMASDZUWAXMUUEUWBUUFAVBOATOZXQQOUVFXPXQYJVMULXMUVMYK
    XQUVOKXMYKUVMXMUVBYKUVMNUVCXOVOOVNXMUUEXQUVONUUFAVPOUPXMUUIUVPXMYKMDZYCMDZY
    TUUIUVPNXMUVHUWDUVJYKQOXMUVIUWEUVLYCQOUUGYKYCAVQULVNVRUOVSXMYRYJXNKGZYJXRKG
    ZLGZXSXMYPUWFYQUWGLXMUUKUULYPUWFNUUSUVFXNYJWFVHXMXRMDZUULYQUWGNXMXRPDZUWIXM
    UVSUWAUWJUVTUWCXPXQVGVHZXRQOZUVFXRYJWFVHUPXMUUKUWIXRRUQZHUULYJRUQZHUWHXSNUU
    SXMUWIUWMUWLXMUWJUWMUWKXRVIOVJXMUULUWNUVFXMUVAUWNUVDYJVIOVJXNXRYJVTULWAWBXJ
    XLARBFGZDZYHXSNXJXKUWOWCXLUWPXJXKREUCGZBFGZUWOUWQEBFEUIWDWERUDDXJUWRUWOWCWG
    RBCWHWQWIXKUWOAWJWKABCWLWMXMYIYEYFKXJXLXTUWODYIYENXMRBEUBGZFGZUWOXTXMBUWSUR
    JZDUWTUWOWCXMUWSEUCGZBUXAXMYSUUAUXBBNUUDUIBEWNWOXMUWSUDDZUWSUXADUXBUXADXMUU
    BUXCUUCBWPOUWSWRUWSUWSWSWTXAUWSRBXBOXMXLXTUWTDZXJXLXCXMAUDDZUUBXLUXDXHXLUXE
    XJAEBXDUHUUCABXEVHXIXFXTBCWLWMUNXG $.
    $( [10-Mar-2014] $)

  $( The proportion of one binomial coefficient to another with ` N ` increased
     by 1.  (Contributed by Mario Carneiro, 10-Mar-2014.) $)
  bcp1n $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( ( N + 1 ) _C K ) =
                ( ( N _C K ) x. ( ( N + 1 ) / ( ( N + 1 ) - K ) ) ) ) $=
    ( wcel cc0 cfz co wa c1 cfa cfv cmul cdiv cn0 wceq syl cc cn faccl nncn cbc
    caddc cmin elfz3nn0 facp1 fznn0sub nn0cn ax-1cn a1i adantl addsub syl111anc
    elfznn0 fveq2d opreq2d 3eqtr4d opreq1d nn0p1nn eqeltrd mul32 eqtrd opreq12d
    wne nnmulcl syl11anc nnne0 jca divmuldiv syl22anc eqtr4d fzelp1 bcval2 ) BC
    DZAEBFGDZHZBIUBGZJKZVPAUCGZJKZAJKZLGZMGZBJKZBAUCGZJKZVTLGZMGZVPVRMGZLGZVPAU
    AGZBAUAGZWHLGVOWBWCVPLGZWFVRLGZMGZWIVOVQWLWAWMMVOBNDZVQWLOCABUDZBUEPVOWAWEV
    RLGZVTLGZWMVOVSWQVTLVOWDIUBGZJKZWEWSLGZVSWQVOWDNDZWTXAOCAEBUFZWDUEPVOVRWSJV
    OBQDZIQDZAQDZVRWSOVOWOXDWPBUGPXEVOUHUIVOANDZXFVNXGVMABUMUJZAUGPBIAUKULZUNVO
    VRWSWELXIUOUPUQVOWEQDZVRQDZVTQDZWRWMOVOWERDZXJVOXBXMXCWDSPZWETPVOVRRDZXKVOV
    RWSRXIVOXBWSRDXCWDURPUSZVRTPZVOVTRDZXLVOXGXRXHASPZVTTPWEVRVTUTULVAVBVOWCQDZ
    VPQDZWFQDZWFEVCZHZXKVREVCZHWIWNOVOWCRDZXTVOWOYFWPBSPWCTPVOVPRDZYAVOWOYGWPBU
    RPVPTPZVOWFRDZYDVOXMXRYIXNXSWEVTVDVEYIYBYCWFTWFVFVGPVOXKYEXQVOXOYEXPVRVFPVG
    WCVPWFVRVHVIVJVOYAAEVPFGDWJWBOYHCAEBVKAVPQVLVEVOWKWGWHLABCVLUQUP $.
    $( [10-Mar-2014] $)

  $( Pascal's rule for the binomial coefficient, generalized to all integers
     ` K ` .  Equation 2 of [Gleason] p. 295.  (Revised by Mario Carneiro,
     10-Mar-2014.) $)
  bcpasc $p |- ( ( N e. NN0 /\ K e. ZZ ) ->
                ( ( N _C K ) + ( N _C ( K - 1 ) ) ) = ( ( N + 1 ) _C K ) ) $=
    ( cn0 wcel cz cc0 c1 caddc co cfz cbc cmin wceq cc opreq2d cdiv cmul adantr
    syl syl111anc wa wo wb peano2nn0 cuz cfv elfzp12 nn0uz eleq2s ax-1cn addid1
    ax-mp bcn0 clt wbr 0z 1z zsubcl mp2an 0re ltm1 orci bcval4 mp3an23 opreq12d
    cr 3eqtr4a opreq2 opreq1 eqeq12d syl5ibrcom addid2 opreq1i syl6eleq nn0p1nn
    simpr cn nnuz fzm1 biimpa sylan nn0cn pncan sylancl eleq2d wss 1nn0 eleqtri
    fzss1 mpan ssel2 bcp1n syldan wne crp bcrpcl rpcn a1i elfzelz adantl addsub
    nncn zcn cle elfzle2 nn0z znn0sub syl11anc mpbid nnne0 div12 syl112anc nnrp
    eqeltrd rpdivcl mulcom eqtrd npcan 3eqtr2d divcan1 elfznn divdiv2 syl122anc
    adddi bcm1k subsub eqtr4d opreq1d fzelp1 nnz elfzm1b syl2anr sylan9eqr bcnn
    eleqtrd imp wn con3d bcval3 cvv rpne0 divmul3 mpbird div23 3eqtr3rd 3eqtrrd
    nn0re ltp1 olcd mpd3an23 jaodan ex jaod sylbid adantlr simpll simplr fzssp1
    00id sseld peano2zm oprex fzp1ss eqsstr3i sseli syl6bir sylbird pm2.61dan
    id ) BCDZAEDZUAZAFBGHIZJIZDZBAKIZBAGLIZKIZHIZUVMAKIZMZUVJUVOUWAUVKUVJUVOUWA
    UVJUVOAFMZAFGHIZUVMJIZDZUBZUWAUVJUVMCDZUVOUWFUCZBUDZUWHUVMFUEUFZCAFUVMUGUHU
    ISUVJUWBUWAUWEUVJUWAUWBBFKIZBFGLIZKIZHIZUVMFKIZMUVJGFHIZGUWNUWOGNDZUWPGMUJG
    UKULUVJUWKGUWMFHBUMUVJUWLEDZUWLFUNUOZBUWLUNUOZUBUWMFMFEDZGEDUWRUPUQFGURUSUW
    SUWTFVFDUWSUTFVAULVBUWLBVCVDVEUVJUWGUWOGMUWIUVMUMSVGUWBUVSUWNUVTUWOUWBUVPUW
    KUVRUWMHAFBKVHUWBUVQUWLBKAFGLVIOVEAFUVMKVHVJVKUVJUWEUWAUVJUWEAGUVMJIZDZUWAU
    VJUWEUAAUWDUXBUVJUWEVPUWCGUVMJUWQUWCGMUJGVLULZVMZVNUVJUXCAGUVMGLIZJIZDZAUVM
    MZUBZUWAUVJUVMGUEUFZDZUXCUXJUVJUVMVQUXKBVOZVRVNUXLUXCUXJAGUVMVSVTWAUVJUXHUW
    AUXIUVJUXHAGBJIZDZUWAUVJUXHUXOUVJUXGUXNAUVJUXFBGJUVJBNDZUWQUXFBMZBWBZUJBGWC
    ZWDZOWEVTUVJUXOUAZUVTUVPUVMUVMALIZPIQIZUVPUYBPIZUYBQIZUYDAQIZHIZUVSUVJUXOAF
    BJIZDZUVTUYCMUVJUXNUYHWFZUXOUYIGUWJDUVJUYJGCUWJWGUHWHGFBCWIWJUXNUYHAWKWAZAB
    CWLWMUYAUYCUYDUVMQIZUYDUYBAHIZQIZUYGUYAUYCUVMUYDQIZUYLUYAUVPNDZUVMNDZUYBNDZ
    UYBFWNZUYCUYOMUYAUVPWODZUYPUVJUXOUYIUYTUYKABCWPWMZUVPWQSZUYAUVMVQDZUYQUVJVU
    CUXOUXMRUVMXBSZUYAUYBVQDZUYRUYAUYBBALIZGHIZVQUYAUXPUWQANDZUYBVUGMUVJUXPUXOU
    XRRZUWQUYAUJWRZUYAUVKVUHUXOUVKUVJAGBWSZWTZAXCSZBGAXATZUYAVUFCDZVUGVQDUYAABX
    DUOZVUOUXOVUPUVJAGBXEWTUYAUVKBEDZVUPVUOUCVULUVJVUQUXOBXFRABXGXHXIVUFVOSXNZU
    YBXBSZUYAVUEUYSVURUYBXJSZUVPUVMUYBXKXLUYAUYQUYDNDZUYOUYLMVUDUYAUYDWODZVVAUY
    AUYTUYBWODZVVBVUAUYAVUEVVCVURUYBXMSZUVPUYBXOXHUYDWQSZUVMUYDXPXHXQUYAUYMUVMU
    YDQUYAUYQVUHUYMUVMMVUDVUMUVMAXRXHOUYAVVAUYRVUHUYNUYGMVVEVUSVUMUYDUYBAYDTXSU
    YAUYEUVPUYFUVRHUYAUYPUYRUYSUYEUVPMVUBVUSVUTUVPUYBXTTUYAUVPUYBAPIZPIZUVPAQIU
    YBPIZUVRUYFUYAUYPUYRUYSVUHAFWNZVVGVVHMVUBVUSVUTVUMUYAAVQDZVVIUXOVVJUVJABYAW
    TZAXJSUVPUYBAYBYCUYAVVGUVRMZUVPUVRVVFQIZMZUYAUVPUVRBUVQLIZAPIZQIVVMABCYEUYA
    VVPVVFUVRQUYAVVOUYBAPUYAVVOVUGUYBUYAUXPVUHUWQVVOVUGMVUIVUMVUJBAGYFTVUNYGYHO
    XQUYAUYPUVRNDZVVFNDZVVFFWNZVVLVVNUCVUBUYAUVRWODZVVQUVJUXOUVQUYHDZVVTUYAUVQF
    UXFJIZUYHUYAUXCUVQVWBDZCAGBYIUXOUVKUVMEDZUXCVWCUCZUVJVUKUVJVUCVWDUXMUVMYJSZ
    AUVMYKZYLXIUVJVWBUYHMUXOUVJUXFBFJUXTORYOUVQBCWPWMUVRWQSUYAVVFWODZVVRUYAVVCA
    WODZVWHVVDUYAVVJVWIVVKAXMSUYBAXOXHZVVFWQSUYAVWHVVSVWJVVFUUASUVPUVRVVFUUBXLU
    UCUYAUYPVUHUYRUYSVVHUYFMVUBVUMVUSVUTUVPAUYBUUDXLUUEVEUUFWMUVJUXIUAZUWCGUVSU
    VTUXDVWKUVPFUVRGHUXIUVJUVPBUVMKIZFAUVMBKVHUVJVWDUVMFUNUOZBUVMUNUOZUBVWLFMVW
    FUVJVWNVWMUVJBVFDVWNBUUGBUUHSUUIUVMBVCUUJYMVWKUVRBBKIZGVWKUVQBBKUXIUVJUVQUX
    FBAUVMGLVIUXTYMOUVJVWOGMUXIBYNRXQVEUXIUVJUVTUVMUVMKIZGAUVMUVMKVHUVJUWGVWPGM
    UWIUVMYNSYMVGUUKWMWMUULUUMUUNYPUUOUVLUVOYQZUAZFFHIFUVSUVTUUSVWRUVPFUVRFHVWR
    UVJUVKUYIYQZUVPFMUVJUVKVWQUUPZUVJUVKVWQUUQZUVLVWQVWSUVLUYIUVOUVLUYHUVNAUVJU
    YHUVNWFUVKFBCUURRUUTYRYPABYSTVWRUVJUVQEDZVWAYQZUVRFMVWTVWRUVKVXBVXAAUVASUVL
    VWQVXCUVLVWAUVOUVLVWAVWCUVOUVLVWBUYHUVQUVLUXFBFJUVLUXPUWQUXQUVJUXPUVKUXRRUJ
    UXSWDOWEUVLVWCUXCUVOUVKUVKVWDVWEUVJUVKUVIUVJUWGVWDUWIUVMXFSVWGYLUXBUVNAUXBU
    WDUVNUXEUXAUVMYTDUWDUVNWFUPBGHUVBFUVMYTUVCUSUVDUVEUVFUVGYRYPUVQBYSTVEVWRUWG
    UVKVWQUVTFMVWRUVJUWGVWTUWISVXAUVLVWQVPAUVMYSTVGUVH $.
    $( [10-Mar-2014] $) $( [13-Jul-2005] $)

  ${
    $d k m n N $.  $d k m n K $.
    $( A binomial coefficient, in its extended domain, is a nonnegative
       integer. $)
    bccl $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) e. NN0 ) $=
      ( vk vm cn0 wcel cv cbc co cz wral cc0 c1 wceq opreq1 eleq1d ralbidv 0nn0
      wa opreq2 vn caddc cfz elfz1eq adantl bcn0 1nn0 eqeltri mpbiri syl bcval3
      ax-mp mp3an1 syl6eqel pm2.61dan rgen cbvralv cmin bcpasc adantlr rcla4cva
      wn peano2zm sylan2 nn0addcl syl11anc adantll eqeltrrd ralrimiva ex syl5bi
      nn0ind sylan ) BEFBCGZHIZEFZCJKZAJFBAHIZEFZDGZVNHIZEFZCJKLVNHIZEFZCJKUAGZ
      VNHIZEFZCJKZWEMUBIZVNHIZEFZCJKZVQDUABVTLNZWBWDCJWMWAWCEVTLVNHOPQVTWENZWBW
      GCJWNWAWFEVTWEVNHOPQVTWINZWBWKCJWOWAWJEVTWIVNHOPQVTBNZWBVPCJWPWAVOEVTBVNH
      OPQWDCJVNJFZVNLLUCIFZWDWQWRSVNLNZWDWRWSWQVNLUDUEWSWDLLHIZEFWTMELEFZWTMNRL
      UFULUGUHWSWCWTEVNLLHTPUIUJWQWRVBZSWCLEXAWQXBWCLNRVNLUKUMRUNUOUPWHWEVTHIZE
      FZDJKZWEEFZWLWGXDCDJVNVTNWFXCEVNVTWEHTPUQXFXEWLXFXESZWKCJXGWQSWFWEVNMURIZ
      HIZUBIZWJEXFWQXJWJNXEVNWEUSUTXEWQXJEFZXFXEWQSWGXIEFZXKXDWGDVNJVTVNNXCWFEV
      TVNWEHTPVAWQXEXHJFXLVNVCXDXLDXHJVTXHNXCXIEVTXHWEHTPVAVDWFXIVEVFVGVHVIVJVK
      VLVPVSCAJVNANVOVREVNABHTPVAVM $.
      $( [9-Nov-2013] $) $( [10-Jul-2005] $)

    $( A binomial coefficient, in its standard domain, is a natural number.
       (Revised by Mario Carneiro, 10-Mar-2014.) $)
    bccl2 $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) e. NN ) $=
      ( wcel cc0 cfz co wa cbc cn0 clt wbr cn cz elfz3nn0 elfzelz bccl syl11anc
      adantl crp bcrpcl rpgt0 syl elnnnn0b sylanbrc ) BCDZAEBFGDZHZBAIGZJDZEUIK
      LZUIMDUHBJDANDZUJCABOUGULUFAEBPSABQRUHUITDUKABCUAUIUBUCUIUDUE $.
      $( [10-Mar-2014] $) $( [3-Jan-2006] $)
  $}

  $( The number of permutations of ` N - R ` objects from a collection of ` N `
     objects is a natural number.  (Contributed by Jason Orendorff,
     24-Jan-2007.) $)
  permnn $p |- ( ( N e. NN0 /\ R e. ( 0 ... N ) ) ->
               ( ( ! ` N ) / ( ! ` R ) ) e. NN ) $=
    ( cn0 wcel cc0 cfz co wa cfa cfv cn cmin cmul cdiv elfznn0 faccl syl adantl
    cc nncn fznn0sub nnmulcl syl11anc adantr wne wceq divcan4 syl111anc eqeltrd
    facne0 cbc bcval2 bccl2 eqeltrrd nndivtr syl32anc ) BCDZAEBFGDZHZAIJZKDZBAL
    GZIJZUTMGZKDZBIJZSDZVDUTNGZKDVFVDNGZKDVFUTNGKDURVAUQURACDZVAABOZAPQZRZUSVCK
    DZVAVEUSVBCDVNCAEBUAVBPQZVMVCUTUBUCUQVGURUQVFKDVGBPVFTQUDUSVHVCKUSVCSDZUTSD
    ZUTEUEZVHVCUFUSVNVPVOVCTQURVQUQURVAVQVLUTTQRURVRUQURVJVRVKAUJQRVCUTUGUHVOUI
    USBAUKGVIKABCULABCUMUNUTVDVFUOUP $.
    $( [24-Jan-2007] $)

