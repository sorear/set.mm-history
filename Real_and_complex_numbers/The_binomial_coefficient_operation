$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The binomial coefficient operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c _C $. $( Binomial coefficient operation. $)

  $( Extend class notation to include the binomial coefficient operation
     (combinatorial choose operation). $)
  cbc $a class _C $.

  ${
    $d n k m $.
    $( Define the binomial coefficient operation.  In the literature, this
       function is often written as a column vector of the two arguments, or
       with the arguments as subscripts before and after the letter "C".
       ` ( N _C K ) ` is read " ` N ` choose ` K ` ."  Definition of binomial
       coefficient in [Gleason] p. 295.  As suggested by Gleason, we define it
       to be 0 when ` 0 <_ k <_ n ` does not hold. $)
    df-bc $a |- _C = ( n e. NN0 , k e. ZZ |-> if ( k e. ( 0 ... n ) ,
                ( ( ! ` n ) / ( ( ! ` ( n - k ) ) x. ( ! ` k ) ) ) , 0 ) ) $.
  $}

  ${
    $d n k m N $.  $d n k m K $.
    $( Value of the binomial coefficient, ` N ` choose ` K ` .  Definition of
       binomial coefficient in [Gleason] p. 295.  As suggested by Gleason, we
       define it to be 0 when ` 0 <_ K <_ N ` does not hold.  See ~ bcval2 for
       the value in the standard domain. $)
    bcval $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) = if ( K e. ( 0 ... N )
      , ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) , 0 ) ) $=
      ( vn vk cv cc0 cfz co wcel cfa cfv cmin cmul cdiv cif opreq2 fveq2 fveq2d
      wceq opreq12d cn0 cbc eleq2d opreq1 opreq1d eqidd ifbieq12d eleq1 opreq2d
      cz df-bc oprex cc 0cn elexi ifex ovmpt2 ) CDBAUAUJDEZFCEZGHZIZUSJKZUSURLH
      ZJKZURJKZMHZNHZFOAFBGHZIZBJKZBALHZJKZAJKZMHZNHZFOUBURVHIZVJBURLHZJKZVEMHZ
      NHZFOUSBSZVAVPVGFVTFWAUTVHURUSBFGPUCWAVBVJVFVSNUSBJQWAVDVRVEMWAVCVQJUSBUR
      LUDRUETWAFUFUGURASZVPVIVTFVOFURAVHUHWBVSVNVJNWBVRVLVEVMMWBVQVKJURABLPRURA
      JQTUIWBFUFUGDCUKVIVOFVJVNNULFUMUNUOUPUQ $.
      $( [7-Nov-2013] $) $( [10-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , in its standard
       domain. $)
    bcval2 $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) =
                    ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) ) $=
      ( wcel cc0 cfz co cbc cfa cfv cmin cmul cdiv cif cn0 wceq elfz3nn0 adantl
      wa cz elfzelz bcval syl2anc iftrue eqtrd ) BCDZAEBFGDZSZBAHGZUGBIJBAKGIJA
      IJLGMGZENZUJUHBODATDZUIUKPCABQUGULUFAEBUARABUBUCUGUKUJPUFUGUJEUDRUE $.
      $( [7-Nov-2013] $) $( [9-Jun-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval3 $p |- ( ( N e. NN0 /\ K e. ZZ /\ -. K e. ( 0 ... N ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 cfz co wn w3a cbc cfa cfv cmin cmul cdiv cif wceq bcval
      3adant3 iffalse 3ad2ant3 eqtrd ) BCDZAEDZAFBGHDZIZJBAKHZUFBLMBANHLMALMOHP
      HZFQZFUDUEUHUJRUGABSTUGUDUJFRUEUFUIFUAUBUC $.
      $( [8-Nov-2013] $) $( [14-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval4 $p |- ( ( N e. NN0 /\ K e. ZZ /\ ( K < 0 \/ N < K ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 clt wbr wo w3a cfz co wn wa cle cr lenlt mpbid adantl
      wb cbc wceq simp1 simp2 elfzle1 0re elfzelz zre syl sylancr elfzle2 nn0re
      wi syl2anr jca ioran sylibr ex adantr con2d 3impia bcval3 syl3anc ) BCDZA
      EDZAFGHZBAGHZIZJVDVEAFBKLDZMZBAUALFUBVDVEVHUCVDVEVHUDVDVEVHVJVDVENVIVHVDV
      IVHMZUMVEVDVIVKVDVINZVFMZVGMZNVKVLVMVNVIVMVDVIFAOHZVMAFBUEVIFPDAPDZVOVMTU
      FVIVEVPAFBUGAUHUIZFAQUJRSVLABOHZVNVIVRVDAFBUKSVIVPBPDVRVNTVDVQBULABQUNRUO
      VFVGUPUQURUSUTVAABVBVC $.
      $( [7-Nov-2013] $) $( [14-Jul-2005] $)
  $}

  $( Closure of the binomial coefficient in the positive reals.  (This is
     mostly a lemma before we have ~ bccl2 .)  (Contributed by Mario Carneiro,
     10-Mar-2014.) $)
  bcrpcl $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) e. RR+ ) $=
    ( wcel cc0 cfz co wa cbc cfa cfv cmin cmul crp cn faccl syl2an syl2anc nnrp
    cn0 cdiv bcval2 elfz3nn0 fznn0sub elfznn0 adantl nnmulcl rpdivcl eqeltrd
    syl ) BCDZAEBFGDZHZBAIGBJKZBALGZJKZAJKZMGZUAGZNABCUBUMUNODZURODZUSNDZUMBTDU
    TCABUCBPUJUMUOTDZATDZVACAEBUDULVDUKABUEUFVCUPODUQODVAVDUOPAPUPUQUGQRUTUNNDU
    RNDVBVAUNSURSUNURUHQRUI $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( "Complementing" its second argument doesn't change a binary coefficient.
     (Revised by Mario Carneiro, 5-Mar-2014.) $)
  bccmpl $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) =
                    ( N _C ( N - K ) ) ) $=
    ( cn0 wcel cz wa cc0 co cbc cmin wceq cfa cmul cdiv cc fznn0sub2 cn elfznn0
    cfv faccl cfz nncn syl adantl mulcom syl2anc elfzelz nn0cn zcn nncan syl2an
    3syl sylan2 fveq2d opreq1d eqtr4d opreq2d bcval2 syldan 3eqtr4d adantlr w3a
    wn bcval3 simp1 nn0z zsubcl sylan 3adant3 wi ex adantr eleq1d sylibd 3impia
    con3d syl3anc 3expa pm2.61dan ) BCDZAEDZFZAGBUAHZDZBAIHZBBAJHZIHZKZVTWDWHWA
    VTWDFZBLSZWFLSZALSZMHZNHWJBWFJHZLSZWKMHZNHZWEWGWIWMWPWJNWIWMWLWKMHZWPWIWKOD
    ZWLODZWMWRKWIWFWCDZWSCABPZXAWFCDWKQDWSWFBRWFTWKUBULUCWIACDZWLQDWTWDXCVTABRU
    DATWLUBULWKWLUEUFWIWOWLWKMWIWNALWDVTWAWNAKZAGBUGVTBODAODXDWABUHAUIBAUJUKZUM
    UNUOUPUQABCURVTWDXAWGWQKXBWFBCURUSUTVAVTWAWDVCZWHVTWAXFVBZWEGWGABVDXGVTWFED
    ZXAVCZWGGKVTWAXFVEVTWAXHXFVTBEDWAXHBVFBAVGVHVIVTWAXFXIWBXAWDWBXAWNWCDZWDVTX
    AXJVJWAVTXAXJCWFBPVKVLWBWNAWCXEVMVNVPVOWFBVDVQUPVRVS $.
    $( [5-Mar-2014] $) $( [21-Jun-2005] $)

  $( ` N ` choose 0 is 1.  Remark in [Gleason] p. 296. $)
  bcn0 $p |- ( N e. NN0 -> ( N _C 0 ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cfa cfv cmin cmul cdiv c1 cfz wceq cuz eluzfz1 eleq2s
    nn0uz cc syl bcval2 mpdan nn0cn subid1 fveq2d fac0 opreq12 sylancl cn faccl
    nncn mulid1 eqtrd opreq2d wne facne0 divid syl2anc 3eqtrd ) ABCZADEFZAGHZAD
    IFZGHZDGHZJFZKFZVBVBKFZLUTDDAMFCZVAVGNVIADOHBDAPRQDABUAUBUTVFVBVBKUTVFVBLJF
    ZVBUTVDVBNVELNVFVJNUTVCAGUTASCVCANAUCAUDTUEUFVDVBVELJUGUHUTVBSCZVJVBNUTVBUI
    CVKAUJVBUKTZVBULTUMUNUTVKVBDUOVHLNVLAUPVBUQURUS $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( ` N ` choose ` N ` is 1.  Remark in [Gleason] p. 296. $)
  bcnn $p |- ( N e. NN0 -> ( N _C N ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cmin c1 cz wceq 0z bccmpl mpan2 bcn0 nn0cn subid1 syl
    cc opreq2d 3eqtr3rd ) ABCZADEFZAADGFZEFZHAAEFUADICUBUDJKDALMANUAUCAAEUAARCU
    CAJAOAPQST $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( Binomial coefficient: ` N ` choose ` 1 ` . $)
  bcn1 $p |- ( N e. NN0 -> ( N _C 1 ) = N ) $=
    ( cn0 wcel cn cc0 wceq wo c1 cbc co cfa cfv cmul cdiv cuz cc syl nncn eqtrd
    clt elnn0 cmin cfz wa elnnuz biimpi 1nn0 nn0uz eleqtri jctil elfzuzb mpbird
    bcval2 mpdan facnn2 fac1 opreq2i nnm1nn0 faccl mulid1 syl5eq opreq12d nnne0
    3syl wne divcan3 syl3anc cz 0nn0 1z lt01 olci bcval4 mp3an wb opreq1 eqeq12
    wbr mpancom mpbiri jaoi sylbi ) ABCADCZAEFZGAHIJZAFZAUAWCWFWDWCWEAKLZAHUBJZ
    KLZHKLZMJZNJZAWCHEAUCJCZWEWLFWCWMHEOLZCZAHOLCZUDWCWPWOWCWPAUEUFHBWNUGUHUIUJ
    DHEAUKULHADUMUNWCWLWIAMJZWINJZAWCWGWQWKWINAUOWCWKWIHMJZWIWJHWIMUPUQWCWIDCZW
    IPCZWSWIFWCWHBCWTAURWHUSQZWIRZWIUTVDVAVBWCAPCXAWIEVEZWRAFARWCWTXAXBXCQWCWTX
    DXBWIVCQAWIVFVGSSWDWFEHIJZEFZEBCHVHCHETVRZEHTVRZGXFVIVJXHXGVKVLHEVMVNWEXEFW
    DWFXFVOAEHIVPWEXEAEVQVSVTWAWB $.
    $( [8-Nov-2013] $) $( [21-Jun-2005] $)

  $( Binomial coefficient: ` N + 1 ` choose ` N ` . $)
  bcnp1n $p |- ( N e. NN0 -> ( ( N + 1 ) _C N ) = ( N + 1 ) ) $=
    ( cn0 wcel c1 caddc co cbc cmin cz wceq peano2nn0 nn0z bccmpl syl2anc nn0cn
    cc ax-1cn pncan2 sylancl opreq2d bcn1 syl 3eqtrd ) ABCZADEFZAGFZUEUEAHFZGFZ
    UEDGFZUEUDUEBCZAICUFUHJAKZALAUEMNUDUGDUEGUDAPCDPCUGDJAOQADRSTUDUJUIUEJUKUEU
    AUBUC $.
    $( [8-Nov-2013] $) $( [20-Jun-2005] $)

  $( The proportion of one binomial coefficient to another with ` K ` decreased
     by 1.  (Contributed by Mario Carneiro, 10-Mar-2014.) $)
  bcm1k $p |- ( ( N e. V /\ K e. ( 1 ... N ) ) -> ( N _C K ) =
                ( ( N _C ( K - 1 ) ) x. ( ( N - ( K - 1 ) ) / K ) ) ) $=
    ( wcel c1 cfz co wa cfa cfv cmul cdiv cc wceq syl cn nncn cc0 cn0 faccl cbc
    cmin cz elfzel2g zcn elfznn adantl ax-1cn a1i subsub syl3anc fveq2d opreq1d
    caddc opreq2d opreq12d wne cuz elfzuz2 nnuz syl6eleqr nnnn0 nn0p1nn nnm1nn0
    fznn0sub nn0cn nnmulcl syl2anc nnne0 divmuldiv syl22anc mul32 eqcomd facnn2
    jca facp1 mulass 3eqtrd eqtr4d mulcom divcan5 eqtrd 3eqtrrd addid2i opreq1i
    wss 0z fzp1ss mpan syl5eqssr ssel2 sylan bcval2 npcan sylancl peano2zm uzid
    syldan peano2uz 3syl eqeltrrd fzss2 simpr wb elfzelz elfzm1b sseldd 3eqtr4d
    mpbid ) BCDZAEBFGZDZHZBIJZBAUBGZIJZAIJZKGZLGZXNBAEUBGZUBGZIJZXTIJZKGZLGZYAA
    LGZKGZBAUAGZBXTUAGZYFKGXMYGXNXOEUNGZIJZYCKGZLGZYJALGZKGZXNYJKGZXRYJKGZLGZXS
    XMYEYMYFYNKXMYDYLXNLXMYBYKYCKXMYAYJIXMBMDZAMDZEMDZYAYJNXMBUCDZYSCAEBUDZBUEO
    ZXMAPDZYTXLUUEXJABUFUGZAQOZUUAXMUHUIBAEUJUKZULUMUOXMYAYJALUUHUMUPXMYOYPYLAK
    GZLGZYRXMXNMDZYJMDZYLMDZYLRUQZHZYTARUQZHYOUUJNXMXNPDZUUKXMBSDZUUQXMBPDUURXM
    BEURJPCAEBUSUTVABVBOBTOXNQOZXMYJSDZUULXMYJPDZUUTXMXOSDZUVACAEBVEZXOVCOZYJVB
    OZYJVFOZXMYLPDZUUOXMYKPDZYCPDZUVGXMUUTUVHUVEYJTOZXMXTSDZUVIXMUUEUVKUUFAVDOX
    TTOZYKYCVGVHUVGUUMUUNYLQYLVIVOOXMYTUUPUUGXMUUEUUPUUFAVIOVOXNYJYLAVJVKXMYQUU
    IYPLXMYQXPYJKGZXQKGZYKYCAKGZKGZUUIXMXPMDZXQMDZUULYQUVNNXMXPPDZUVQXMUVBUVSUV
    CXOTOZXPQOXMXQPDZUVRXMASDZUWAXMUUEUWBUUFAVBOATOZXQQOUVFXPXQYJVLUKXMUVMYKXQU
    VOKXMYKUVMXMUVBYKUVMNUVCXOVPOVMXMUUEXQUVONUUFAVNOUPXMUUIUVPXMYKMDZYCMDZYTUU
    IUVPNXMUVHUWDUVJYKQOXMUVIUWEUVLYCQOUUGYKYCAVQUKVMVRUOVSXMYRYJXNKGZYJXRKGZLG
    ZXSXMYPUWFYQUWGLXMUUKUULYPUWFNUUSUVFXNYJVTVHXMXRMDZUULYQUWGNXMXRPDZUWIXMUVS
    UWAUWJUVTUWCXPXQVGVHZXRQOZUVFXRYJVTVHUPXMUUKUWIXRRUQZHUULYJRUQZHUWHXSNUUSXM
    UWIUWMUWLXMUWJUWMUWKXRVIOVOXMUULUWNUVFXMUVAUWNUVDYJVIOVOXNXRYJWAUKWBWCXJXLA
    RBFGZDZYHXSNXJXKUWOWFXLUWPXJXKREUNGZBFGZUWOUWQEBFEUHWDWERUCDXJUWRUWOWFWGRBC
    WHWIWJXKUWOAWKWLABCWMWRXMYIYEYFKXJXLXTUWODYIYENXMRBEUBGZFGZUWOXTXMBUWSURJZD
    UWTUWOWFXMUWSEUNGZBUXAXMYSUUAUXBBNUUDUHBEWNWOXMUWSUCDZUWSUXADUXBUXADXMUUBUX
    CUUCBWPOUWSWQUWSUWSWSWTXAUWSRBXBOXMXLXTUWTDZXJXLXCXMAUCDZUUBXLUXDXDXLUXEXJA
    EBXEUGUUCABXFVHXIXGXTBCWMWRUMXH $.
    $( [10-Mar-2014] $)

  $( The proportion of one binomial coefficient to another with ` N ` increased
     by 1.  (Contributed by Mario Carneiro, 10-Mar-2014.) $)
  bcp1n $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( ( N + 1 ) _C K ) =
                ( ( N _C K ) x. ( ( N + 1 ) / ( ( N + 1 ) - K ) ) ) ) $=
    ( wcel cc0 cfz co wa c1 cfa cfv cmul cdiv cn0 wceq syl cc cn faccl nncn cbc
    caddc cmin elfz3nn0 facp1 fznn0sub ax-1cn a1i elfznn0 adantl addsub syl3anc
    nn0cn fveq2d opreq2d 3eqtr4d opreq1d nn0p1nn eqeltrd mul32 opreq12d nnmulcl
    eqtrd wne syl2anc nnne0 jca divmuldiv syl22anc eqtr4d fzelp1 bcval2 ) BCDZA
    EBFGDZHZBIUBGZJKZVPAUCGZJKZAJKZLGZMGZBJKZBAUCGZJKZVTLGZMGZVPVRMGZLGZVPAUAGZ
    BAUAGZWHLGVOWBWCVPLGZWFVRLGZMGZWIVOVQWLWAWMMVOBNDZVQWLOCABUDZBUEPVOWAWEVRLG
    ZVTLGZWMVOVSWQVTLVOWDIUBGZJKZWEWSLGZVSWQVOWDNDZWTXAOCAEBUFZWDUEPVOVRWSJVOBQ
    DZIQDZAQDZVRWSOVOWOXDWPBUMPXEVOUGUHVOANDZXFVNXGVMABUIUJZAUMPBIAUKULZUNVOVRW
    SWELXIUOUPUQVOWEQDZVRQDZVTQDZWRWMOVOWERDZXJVOXBXMXCWDSPZWETPVOVRRDZXKVOVRWS
    RXIVOXBWSRDXCWDURPUSZVRTPZVOVTRDZXLVOXGXRXHASPZVTTPWEVRVTUTULVCVAVOWCQDZVPQ
    DZWFQDZWFEVDZHZXKVREVDZHWIWNOVOWCRDZXTVOWOYFWPBSPWCTPVOVPRDZYAVOWOYGWPBURPV
    PTPZVOWFRDZYDVOXMXRYIXNXSWEVTVBVEYIYBYCWFTWFVFVGPVOXKYEXQVOXOYEXPVRVFPVGWCV
    PWFVRVHVIVJVOYAAEVPFGDWJWBOYHCAEBVKAVPQVLVEVOWKWGWHLABCVLUQUP $.
    $( [10-Mar-2014] $)
  ${
    $d k x y K $.  $d k x y N $.
    $( Write out the top and bottom parts of the binomial coefficient
       ` ( N _C K ) = ( N x. ( N - 1 ) x. ... x. ( ( N - K ) + 1 ) ) / K ! `
       explicitly.  In this form, it is valid even for ` N < K ` , although it
       is no longer valid for non-positive ` K ` .  (Contributed by Mario
       Carneiro, 22-May-2014.) $)
    bcval5 $p |- ( ( N e. NN0 /\ K e. NN ) -> ( N _C K ) =
                 ( ( seq ( ( N - K ) + 1 ) ( x. , _I ) ` N ) / ( ! ` K ) ) ) $=
      ( cn0 wcel cn wa cc0 co cmul cid c1 cfv cdiv cc adantl wbr syl2anc wb syl
      wceq vk vx vy cfz cbc cmin caddc cseq bcval2 adantlr fznn0sub elnn0 sylib
      cfa wo cv mulcl w3a mulass cuz cle clt simplr elfzuz3 nnuz uztrn2 adantrr
      cr crp nnre nnrp ltsubrp syl2an cz nnz zsubcl zltp1le peano2z eluz mpbird
      mpbid simprr syl6eleq fvi elfzelz zcn eqeltrd seqsplit facnn opreq1d expr
      3eqtr4d simpll faccl nncn 3syl mulid2 opreq2d eqtr3d syl6eq opreq1 ax-1cn
      fveq2 fac0 addid2i seqeq1d fveq1d opreq12d eqeq2d syl5ibrcom jaod mpd wne
      wf eqid nn0z adantr eluzelz seqf ffvelrn elfznn0 divcan5 syl122anc 3eqtrd
      nnne0 wn nnnn0 div0 mul02 mul01 0z a1i ad2antrr elfz simpr ad2antlr nn0uz
      syl3anc elfz5 nn0re subge0 bitr4d mtbid zre ltnle sylancl mpbir2and ax-mp
      0re nn0ge0 0cn seqz bcval3 syl3an2 3expa 3eqtr4rd pm2.61dan ) BCDZAEDZFZA
      GBUDHDZBAUEHZBIJBAUFHZKUGHZUHZLZAUNLZMHZTUUTUVAFZUVBBUNLZUVCUNLZUVGIHZMHZ
      UVKUVFIHZUVLMHZUVHUURUVAUVBUVMTUUSABCUIUJUVIUVJUVNUVLMUVIUVCEDZUVCGTZUOZU
      VJUVNTZUVIUVCCDZUVRUURUVAUVTUUSCAGBUKUJZUVCULUMUVIUVPUVSUVQUUTUVAUVPUVSUU
      TUVAUVPFZFZBIJKUHZLZUVCUWDLZUVFIHUVJUVNUWCUAUBUCINJKUVCBUAUPZNDZUBUPZNDZF
      ZUWGUWIIHZNDZUWCUWGUWIUQZOUWHUWJUCUPZNDURUWLUWOIHUWGUWIUWOIHIHTUWCUWGUWIU
      WOUSOUWCBUVDUTLZDZUVDBVAPZUWCUVCBVBPZUWRUWCBEDZUUSUWSUUTUVAUWTUVPUVIUUSBA
      UTLDZUWTUURUUSUVAVCZUURUVAUXAUUSCAGBVDUJKBAEVEVFQZVGZUURUUSUWBVCZUWTBVHDZ
      AVIDUWSUUSBVJAVKBAVLVMZQUWCUVCVNDZBVNDZUWSUWRRZUWCUXIAVNDZUXHUWCUWTUXIUXD
      BVOZSZUWCUUSUXKUXEAVOZSBAVPZQZUXMUVCBVQZQWAUWCUVDVNDZUXIUWQUWRRZUWCUXHUXR
      UXPUVCVRZSUXMUVDBVSZQVTUWCUVCEKUTLUUTUVAUVPWBZVEWCUWGKBUDHZDZUWGJLZNDZUWC
      UYDUYEUWGNUWGUYCWDUYDUWGVNDZUWHUWGKBWEUWGWFZSWGOWHUWCUWTUVJUWETZUXDBWIZSU
      WCUVKUWFUVFIUWCUVPUVKUWFTUYBUVCWISWJWLWKUVIUVSUVQUVJKUWEIHZTUVIKUVJIHZUVJ
      UYKUVIUVJNDZUYLUVJTUVIUURUVJEDUYMUURUUSUVAWMBWNUVJWOWPUVJWQSUVIUVJUWEKIUV
      IUWTUYIUXCUYJSWRWSUVQUVNUYKUVJUVQUVKKUVFUWEIUVQUVKGUNLKUVCGUNXCXDWTUVQBUV
      EUWDUVQUVDKIJUVQUVDGKUGHKUVCGKUGXAKXBXEWTXFXGXHXIXJXKXLWJUVIUVFNDZUVGNDZU
      VGGXMZUVKNDZUVKGXMZUVOUVHTUVIUWPNUVEXNUWQUYNUVIUAUBINJUVDUWPUWPXOUUTUXRUV
      AUUTUXHUXRUURUXIUXKUXHUUSBXPZUXNUXOVMZUXTSZXQZUWGUWPDZUYFUVIVUCUYEUWGNUWG
      UWPWDVUCUYGUWHUVDUWGXRUYHSWGOUWKUWMUVIUWNOXSUVIUWQUWRUVIUWSUWRUVIUWTUUSUW
      SUXCUXBUXGQUVIUXHUXIUXJUUTUXHUVAUYTXQUVIUWTUXIUXCUXLSZUXQQWAUVIUXRUXIUXSV
      UBVUDUYAQVTUWPNBUVEXTQUVIUVGEDZUYOUVIACDZVUEUVAVUFUUTABYAOAWNZSZUVGWOZSUV
      IVUEUYPVUHUVGYEZSUVIUVKEDZUYQUVIUVTVUKUWAUVCWNSZUVKWOSUVIVUKUYRVULUVKYESU
      VFUVGUVKYBYCYDUUTUVAYFZFZGUVGMHZGUVHUVBVUNVUEVUOGTZVUNUUSVUFVUEUURUUSVUMV
      CAYGZVUGWPVUEUYOUYPVUPVUIVUJUVGYHQSVUNUVFGUVGMVUNUAUBINJGUVDBCGUWKUWMVUNU
      WNOUWGUVDBUDHZDZUYFVUNVUSUYEUWGNUWGVURWDVUSUYGUWHUWGUVDBWEUYHSWGOUWHGUWGI
      HGTVUNUWGYIOUWHUWGGIHGTVUNUWGYJOVUNGVURDZUVDGVAPZGBVAPZVUNGVNDZUXRUXIVUTV
      VAVVBFRVVCVUNYKYLZUUTUXRVUMVUAXQUURUXIUUSVUMUYSYMZGUVDBYNYRVUNUVCGVBPZVVA
      VUNVVFGUVCVAPZYFZVUNUVAVVGUUTVUMYOVUNUVAABVAPZVVGVUNAGUTLZDUXIUVAVVIRVUNA
      CVVJUUSVUFUURVUMVUQYPYQWCVVEAGBYSQVUNUXFAVHDZVVGVVIRUURUXFUUSVUMBYTYMUUSV
      VKUURVUMAVJYPBAUUAQUUBUUCVUNUVCVHDZGVHDVVFVVHRVUNUXHVVLUUTUXHVUMUYTXQZUVC
      UUDSUUIUVCGUUEUUFVTVUNUXHVVCVVFVVARVVMVVDUVCGVQQWAUURVVBUUSVUMBUUJYMUUGUU
      RUUSVUMWMGJLGTZVUNGNDVVNUUKGNWDUUHYLUULWJUURUUSVUMUVBGTZUUSUURUXKVUMVVOUX
      NABUUMUUNUUOUUPUUQ $.
      $( [22-May-2014] $)
  $}

  $( Binomial coefficient: ` N ` choose ` 2 ` .  (Contributed by Mario
     Carneiro, 22-May-2014.) $)
  bcn2 $p |- ( N e. NN0 -> ( N _C 2 ) = ( ( N x. ( N - 1 ) ) / 2 ) ) $=
    ( cn0 wcel c2 co cmul cid cmin c1 caddc cseq cfv cdiv wceq ax-1cn cc syl cz
    cuz eqtrd cbc cfa cn 2nn bcval5 mpan2 df-2 eqcomi nncni subaddi mpbir nn0cn
    opreq2i npncan mp3an23 syl5eqr seqeq1d fveq1d peano2zm npcan sylancl fveq2d
    nn0z uzid eleqtrrd seqm1 syl2anc seq1 fvi opreq12d subcl mulcom 3eqtrd fac2
    a1i ) ABCZADUAEZAFGADHEZIJEZKZLZDUBLZMEZAAIHEZFEZDMEVPDUCCVQWCNUDDAUEUFVPWA
    WEWBDMVPWAAFGWDKZLZWDAFEZWEVPAVTWFVPVSWDFGVPVSVRDIHEZJEZWDWIIVRJWIINIIJEZDN
    DWKUGUHDIIDUDUIZOOUJUKUMVPAPCZWJWDNZAULZWMDPCIPCZWNWLOADIUNUOQUPUQURVPWGWDW
    FLZAGLZFEZWHVPWDRCZAWDIJEZSLZCWGWSNVPARCZWTAVCZAUSQZVPAASLZXBVPXCAXFCXDAVDQ
    VPXAASVPWMWPXAANWOOAIUTVAVBVEFGWDAVFVGVPWQWDWRAFVPWQWDGLZWDVPWTWQXGNXEFGWDV
    HQVPWTXGWDNXEWDRVIQTABVIVJTVPWDPCZWMWHWENVPWMWPXHWOOAIVKVAWOWDAVLVGVMWBDNVP
    VNVOVJT $.
    $( [22-May-2014] $)

  $( Compute the binomial coefficent of ` ( N + 1 ) ` over ` ( N - 1 ) `
     (Contributed by Scott Fenton, 11-May-2014.)  (Revised by Mario Carneiro,
     22-May-2014.) $)
  bcp1m1 $p |- ( N e. NN0 -> ( ( N + 1 ) _C ( N - 1 ) ) =
                 ( ( ( N + 1 ) x. N ) / 2 ) ) $=
    ( cn0 wcel c1 caddc co cmin cbc c2 cmul cdiv cz wceq peano2nn0 peano2zm syl
    nn0z cc ax-1cn opreq2d bccmpl syl2anc nn0cn a1i pnncan syl3anc df-2 syl6eqr
    bcn2 pncan sylancl opreq1d eqtrd 3eqtrd ) ABCZADEFZADGFZHFZUPUPUQGFZHFZUPIH
    FZUPAJFZIKFZUOUPBCZUQLCZURUTMANZUOALCVEAQAOPUQUPUAUBUOUSIUPHUOUSDDEFZIUOARC
    ZDRCZVIUSVGMAUCZVIUOSUDZVKADDUEUFUGUHTUOVAUPUPDGFZJFZIKFZVCUOVDVAVNMVFUPUIP
    UOVMVBIKUOVLAUPJUOVHVIVLAMVJSADUJUKTULUMUN $.
    $( [22-May-2014] $) $( [11-May-2014] $)

  $( Pascal's rule for the binomial coefficient, generalized to all integers
     ` K ` .  Equation 2 of [Gleason] p. 295.  (Revised by Mario Carneiro,
     10-Mar-2014.) $)
  bcpasc $p |- ( ( N e. NN0 /\ K e. ZZ ) ->
                ( ( N _C K ) + ( N _C ( K - 1 ) ) ) = ( ( N + 1 ) _C K ) ) $=
    ( cn0 wcel cz cc0 c1 caddc co cfz cbc cmin wceq cc opreq2d cdiv cmul adantr
    syl syl3anc wa wo wb peano2nn0 cuz elfzp12 nn0uz eleq2s ax-1cn addid1 ax-mp
    cfv bcn0 clt wbr 0z 1z zsubcl mp2an cr 0re ltm1 orci bcval4 mp3an23 3eqtr4a
    opreq12d opreq2 opreq1 eqeq12d syl5ibrcom simpr opreq1i syl6eleq cn nn0p1nn
    addid2 nnuz fzm1 biimpa sylan nn0cn pncan sylancl eleq2d 1nn0 eleqtri fzss1
    wss mpan ssel2 bcp1n syldan wne crp bcrpcl rpcn nncn a1i elfzelz adantl zcn
    addsub cle elfzle2 nn0z znn0sub syl2anc mpbid eqeltrd nnne0 div12 syl112anc
    nnrp rpdivcl mulcom eqtrd npcan adddi 3eqtr2d divcan1 elfznn divdiv2 subsub
    syl122anc bcm1k eqtr4d opreq1d fzelp1 nnz elfzm1b syl2anr eleqtrd sylan9eqr
    bcnn imp wn con3d bcval3 cvv rpne0 divmul3 mpbird div23 3eqtr3rd nn0re ltp1
    3eqtrrd olcd mpd3an23 jaodan jaod sylbid adantlr simpll simplr fzssp1 sseld
    ex 00id peano2zm id oprex fzp1ss eqsstr3i sseli syl6bir sylbird pm2.61dan )
    BCDZAEDZUAZAFBGHIZJIZDZBAKIZBAGLIZKIZHIZUVMAKIZMZUVJUVOUWAUVKUVJUVOUWAUVJUV
    OAFMZAFGHIZUVMJIZDZUBZUWAUVJUVMCDZUVOUWFUCZBUDZUWHUVMFUEULZCAFUVMUFUGUHSUVJ
    UWBUWAUWEUVJUWAUWBBFKIZBFGLIZKIZHIZUVMFKIZMUVJGFHIZGUWNUWOGNDZUWPGMUIGUJUKU
    VJUWKGUWMFHBUMUVJUWLEDZUWLFUNUOZBUWLUNUOZUBUWMFMFEDZGEDUWRUPUQFGURUSUWSUWTF
    UTDUWSVAFVBUKVCUWLBVDVEVGUVJUWGUWOGMUWIUVMUMSVFUWBUVSUWNUVTUWOUWBUVPUWKUVRU
    WMHAFBKVHUWBUVQUWLBKAFGLVIOVGAFUVMKVHVJVKUVJUWEUWAUVJUWEAGUVMJIZDZUWAUVJUWE
    UAAUWDUXBUVJUWEVLUWCGUVMJUWQUWCGMUIGVQUKZVMZVNUVJUXCAGUVMGLIZJIZDZAUVMMZUBZ
    UWAUVJUVMGUEULZDZUXCUXJUVJUVMVOUXKBVPZVRVNUXLUXCUXJAGUVMVSVTWAUVJUXHUWAUXIU
    VJUXHAGBJIZDZUWAUVJUXHUXOUVJUXGUXNAUVJUXFBGJUVJBNDZUWQUXFBMZBWBZUIBGWCZWDZO
    WEVTUVJUXOUAZUVTUVPUVMUVMALIZPIQIZUVPUYBPIZUYBQIZUYDAQIZHIZUVSUVJUXOAFBJIZD
    ZUVTUYCMUVJUXNUYHWIZUXOUYIGUWJDUVJUYJGCUWJWFUGWGGFBCWHWJUXNUYHAWKWAZABCWLWM
    UYAUYCUYDUVMQIZUYDUYBAHIZQIZUYGUYAUYCUVMUYDQIZUYLUYAUVPNDZUVMNDZUYBNDZUYBFW
    NZUYCUYOMUYAUVPWODZUYPUVJUXOUYIUYTUYKABCWPWMZUVPWQSZUYAUVMVODZUYQUVJVUCUXOU
    XMRUVMWRSZUYAUYBVODZUYRUYAUYBBALIZGHIZVOUYAUXPUWQANDZUYBVUGMUVJUXPUXOUXRRZU
    WQUYAUIWSZUYAUVKVUHUXOUVKUVJAGBWTZXAZAXBSZBGAXCTZUYAVUFCDZVUGVODUYAABXDUOZV
    UOUXOVUPUVJAGBXEXAUYAUVKBEDZVUPVUOUCVULUVJVUQUXOBXFRABXGXHXIVUFVPSXJZUYBWRS
    ZUYAVUEUYSVURUYBXKSZUVPUVMUYBXLXMUYAUYQUYDNDZUYOUYLMVUDUYAUYDWODZVVAUYAUYTU
    YBWODZVVBVUAUYAVUEVVCVURUYBXNSZUVPUYBXOXHUYDWQSZUVMUYDXPXHXQUYAUYMUVMUYDQUY
    AUYQVUHUYMUVMMVUDVUMUVMAXRXHOUYAVVAUYRVUHUYNUYGMVVEVUSVUMUYDUYBAXSTXTUYAUYE
    UVPUYFUVRHUYAUYPUYRUYSUYEUVPMVUBVUSVUTUVPUYBYATUYAUVPUYBAPIZPIZUVPAQIUYBPIZ
    UVRUYFUYAUYPUYRUYSVUHAFWNZVVGVVHMVUBVUSVUTVUMUYAAVODZVVIUXOVVJUVJABYBXAZAXK
    SUVPUYBAYCYEUYAVVGUVRMZUVPUVRVVFQIZMZUYAUVPUVRBUVQLIZAPIZQIVVMABCYFUYAVVPVV
    FUVRQUYAVVOUYBAPUYAVVOVUGUYBUYAUXPVUHUWQVVOVUGMVUIVUMVUJBAGYDTVUNYGYHOXQUYA
    UYPUVRNDZVVFNDZVVFFWNZVVLVVNUCVUBUYAUVRWODZVVQUVJUXOUVQUYHDZVVTUYAUVQFUXFJI
    ZUYHUYAUXCUVQVWBDZCAGBYIUXOUVKUVMEDZUXCVWCUCZUVJVUKUVJVUCVWDUXMUVMYJSZAUVMY
    KZYLXIUVJVWBUYHMUXOUVJUXFBFJUXTORYMUVQBCWPWMUVRWQSUYAVVFWODZVVRUYAVVCAWODZV
    WHVVDUYAVVJVWIVVKAXNSUYBAXOXHZVVFWQSUYAVWHVVSVWJVVFUUASUVPUVRVVFUUBXMUUCUYA
    UYPVUHUYRUYSVVHUYFMVUBVUMVUSVUTUVPAUYBUUDXMUUEVGUUHWMUVJUXIUAZUWCGUVSUVTUXD
    VWKUVPFUVRGHUXIUVJUVPBUVMKIZFAUVMBKVHUVJVWDUVMFUNUOZBUVMUNUOZUBVWLFMVWFUVJV
    WNVWMUVJBUTDVWNBUUFBUUGSUUIUVMBVDUUJYNVWKUVRBBKIZGVWKUVQBBKUXIUVJUVQUXFBAUV
    MGLVIUXTYNOUVJVWOGMUXIBYORXQVGUXIUVJUVTUVMUVMKIZGAUVMUVMKVHUVJUWGVWPGMUWIUV
    MYOSYNVFUUKWMWMUUSUULUUMYPUUNUVLUVOYQZUAZFFHIFUVSUVTUUTVWRUVPFUVRFHVWRUVJUV
    KUYIYQZUVPFMUVJUVKVWQUUOZUVJUVKVWQUUPZUVLVWQVWSUVLUYIUVOUVLUYHUVNAUVJUYHUVN
    WIUVKFBCUUQRUURYRYPABYSTVWRUVJUVQEDZVWAYQZUVRFMVWTVWRUVKVXBVXAAUVASUVLVWQVX
    CUVLVWAUVOUVLVWAVWCUVOUVLVWBUYHUVQUVLUXFBFJUVLUXPUWQUXQUVJUXPUVKUXRRUIUXSWD
    OWEUVLVWCUXCUVOUVKUVKVWDVWEUVJUVKUVBUVJUWGVWDUWIUVMXFSVWGYLUXBUVNAUXBUWDUVN
    UXEUXAUVMYTDUWDUVNWIUPBGHUVCFUVMYTUVDUSUVEUVFUVGUVHYRYPUVQBYSTVGVWRUWGUVKVW
    QUVTFMVWRUVJUWGVWTUWISVXAUVLVWQVLAUVMYSTVFUVI $.
    $( [10-Mar-2014] $) $( [13-Jul-2005] $)

  ${
    $d k m n N $.  $d k m n K $.
    $( A binomial coefficient, in its extended domain, is a nonnegative
       integer. $)
    bccl $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) e. NN0 ) $=
      ( vk vm cn0 wcel cv cbc co cz wral cc0 c1 wceq opreq1 eleq1d ralbidv 0nn0
      wa opreq2 vn caddc cfz elfz1eq adantl bcn0 1nn0 eqeltri mpbiri syl bcval3
      ax-mp mp3an1 syl6eqel pm2.61dan rgen cbvralv cmin bcpasc adantlr rcla4cva
      peano2zm sylan2 nn0addcl syl2anc adantll eqeltrrd ralrimiva syl5bi nn0ind
      wn ex sylan ) BEFBCGZHIZEFZCJKZAJFBAHIZEFZDGZVNHIZEFZCJKLVNHIZEFZCJKUAGZV
      NHIZEFZCJKZWEMUBIZVNHIZEFZCJKZVQDUABVTLNZWBWDCJWMWAWCEVTLVNHOPQVTWENZWBWG
      CJWNWAWFEVTWEVNHOPQVTWINZWBWKCJWOWAWJEVTWIVNHOPQVTBNZWBVPCJWPWAVOEVTBVNHO
      PQWDCJVNJFZVNLLUCIFZWDWQWRSVNLNZWDWRWSWQVNLUDUEWSWDLLHIZEFWTMELEFZWTMNRLU
      FULUGUHWSWCWTEVNLLHTPUIUJWQWRVKZSWCLEXAWQXBWCLNRVNLUKUMRUNUOUPWHWEVTHIZEF
      ZDJKZWEEFZWLWGXDCDJVNVTNWFXCEVNVTWEHTPUQXFXEWLXFXESZWKCJXGWQSWFWEVNMURIZH
      IZUBIZWJEXFWQXJWJNXEVNWEUSUTXEWQXJEFZXFXEWQSWGXIEFZXKXDWGDVNJVTVNNXCWFEVT
      VNWEHTPVAWQXEXHJFXLVNVBXDXLDXHJVTXHNXCXIEVTXHWEHTPVAVCWFXIVDVEVFVGVHVLVIV
      JVPVSCAJVNANVOVREVNABHTPVAVM $.
      $( [9-Nov-2013] $) $( [10-Jul-2005] $)

    $( A binomial coefficient, in its standard domain, is a natural number.
       (Revised by Mario Carneiro, 10-Mar-2014.) $)
    bccl2 $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) e. NN ) $=
      ( wcel cc0 cfz co wa cbc cn0 clt wbr elfz3nn0 elfzelz adantl bccl syl2anc
      cn cz crp bcrpcl rpgt0 syl elnnnn0b sylanbrc ) BCDZAEBFGDZHZBAIGZJDZEUIKL
      ZUIRDUHBJDASDZUJCABMUGULUFAEBNOABPQUHUITDUKABCUAUIUBUCUIUDUE $.
      $( [10-Mar-2014] $) $( [3-Jan-2006] $)
  $}

  $( The number of permutations of ` N - R ` objects from a collection of ` N `
     objects is a natural number.  (Contributed by Jason Orendorff,
     24-Jan-2007.) $)
  permnn $p |- ( ( N e. NN0 /\ R e. ( 0 ... N ) ) ->
               ( ( ! ` N ) / ( ! ` R ) ) e. NN ) $=
    ( cn0 wcel cc0 cfz co wa cfa cfv cn cmin cmul cdiv elfznn0 faccl syl adantl
    cc nncn fznn0sub nnmulcl syl2anc adantr wceq facne0 divcan4 syl3anc eqeltrd
    wne cbc bcval2 bccl2 eqeltrrd nndivtr syl32anc ) BCDZAEBFGDZHZAIJZKDZBALGZI
    JZUTMGZKDZBIJZSDZVDUTNGZKDVFVDNGZKDVFUTNGKDURVAUQURACDZVAABOZAPQZRZUSVCKDZV
    AVEUSVBCDVNCAEBUAVBPQZVMVCUTUBUCUQVGURUQVFKDVGBPVFTQUDUSVHVCKUSVCSDZUTSDZUT
    EUJZVHVCUEUSVNVPVOVCTQURVQUQURVAVQVLUTTQRURVRUQURVJVRVKAUFQRVCUTUGUHVOUIUSB
    AUKGVIKABCULABCUMUNUTVDVFUOUP $.
    $( [24-Jan-2007] $)

