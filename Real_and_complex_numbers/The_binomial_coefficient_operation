$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The binomial coefficient operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c _C $. $( Binomial coefficient operation. $)

  $( Extend class notation to include the binomial coefficient operation
     (combinatorial choose operation). $)
  cbc $a class _C $.

  ${
    $d n k m $.
    $( Define the binomial coefficient operation.  In the literature, this
       function is often written as a column vector of the two arguments, or
       with the arguments as subscripts before and after the letter "C".
       ` ( N _C K ) ` is read " ` N ` choose ` K ` ."  Definition of binomial
       coefficient in [Gleason] p. 295.  As suggested by Gleason, we define it
       to be 0 when ` 0 <_ k <_ n ` does not hold. $)
    df-bc $a |- _C = ( n e. NN0 , k e. ZZ |-> if ( k e. ( 0 ... n ) ,
                ( ( ! ` n ) / ( ( ! ` ( n - k ) ) x. ( ! ` k ) ) ) , 0 ) ) $.
  $}

  ${
    $d n k m N $.  $d n k m K $.
    $( Value of the binomial coefficient, ` N ` choose ` K ` .  Definition of
       binomial coefficient in [Gleason] p. 295.  As suggested by Gleason, we
       define it to be 0 when ` 0 <_ K <_ N ` does not hold.  See ~ bcval2 for
       the value in the standard domain. $)
    bcval $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) = if ( K e. ( 0 ... N )
      , ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) , 0 ) ) $=
      ( vn vk cn0 cz cv cc0 cfz co wcel cfa cfv cmin cmul cdiv cif cbc wceq
      opreq2 eleq2d fveq2 opreq1 fveq2d opreq1d opreq12d eqidd ifbieq12d eleq1
      opreq2d df-bc oprex cc 0cn elisseti ifex ovmpt2 ) CDBAEFDGZHCGZIJZKZUSLMZ
      USURNJZLMZURLMZOJZPJZHQAHBIJZKZBLMZBANJZLMZALMZOJZPJZHQRURVHKZVJBURNJZLMZ
      VEOJZPJZHQUSBSZVAVPVGHVTHWAUTVHURUSBHITUAWAVBVJVFVSPUSBLUBWAVDVRVEOWAVCVQ
      LUSBURNUCUDUEUFWAHUGUHURASZVPVIVTHVOHURAVHUIWBVSVNVJPWBVRVLVEVMOWBVQVKLUR
      ABNTUDURALUBUFUJWBHUGUHDCUKVIVOHVJVNPULHUMUNUOUPUQ $.
      $( [7-Nov-2013] $) $( [10-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , in its standard
       domain. $)
    bcval2 $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) =
                    ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) ) $=
      ( wcel cc0 cfz co wa cbc cfa cfv cmin cmul cdiv cif cn0 cz wceq elfz3nn0
      elfzelz adantl bcval syl11anc iftrue eqtrd ) BCDZAEBFGDZHZBAIGZUGBJKBALGJ
      KAJKMGNGZEOZUJUHBPDAQDZUIUKRCABSUGULUFAEBTUAABUBUCUGUKUJRUFUGUJEUDUAUE $.
      $( [7-Nov-2013] $) $( [9-Jun-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval3 $p |- ( ( N e. NN0 /\ K e. ZZ /\ -. K e. ( 0 ... N ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 cfz co wn w3a cbc cfa cfv cmin cmul cdiv cif wceq
      bcval 3adant3 iffalse 3ad2ant3 eqtrd ) BCDZAEDZAFBGHDZIZJBAKHZUFBLMBANHLM
      ALMOHPHZFQZFUDUEUHUJRUGABSTUGUDUJFRUEUFUIFUAUBUC $.
      $( [8-Nov-2013] $) $( [14-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval4 $p |- ( ( N e. NN0 /\ K e. ZZ /\ ( K < 0 \/ N < K ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 clt wbr wo w3a cfz co wn cbc wceq simp1 simp2 wa wi
      cle elfzle1 cr wb 0re elfzelz zre syl lenlt sylancr mpbid adantl elfzle2
      nn0re syl2anc jca ioran sylibr ex adantr con2d 3impia bcval3 syl111anc )
      BCDZAEDZAFGHZBAGHZIZJVDVEAFBKLDZMZBANLFOVDVEVHPVDVEVHQVDVEVHVJVDVERVIVHVD
      VIVHMZSVEVDVIVKVDVIRZVFMZVGMZRVKVLVMVNVIVMVDVIFATHZVMAFBUAVIFUBDAUBDZVOVM
      UCUDVIVEVPAFBUEAUFUGZFAUHUIUJUKVLABTHZVNVIVRVDAFBULUKVIVPBUBDVRVNUCVDVQBU
      MABUHUNUJUOVFVGUPUQURUSUTVAABVBVC $.
      $( [7-Nov-2013] $) $( [14-Jul-2005] $)
  $}

  $( "Complementing" its second argument doesn't change a binary
     coefficient. $)
  bccmpl $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) =
                    ( N _C ( N - K ) ) ) $=
    ( wcel cc0 cfz co wa cfa cfv cmin cmul cdiv cbc cc wceq fznn0sub2 cn0 cn
    elfznn0 faccl nncn 3syl syl adantl mulcom syl11anc elfz3nn0 nn0cn nncan
    syl2an fveq2d opreq1d eqtr4d opreq2d bcval2 syldan 3eqtr4d ) BCDZAEBFGZDZHZ
    BIJZBAKGZIJZAIJZLGZMGVCBVDKGZIJZVELGZMGZBANGBVDNGZVBVGVJVCMVBVGVFVELGZVJVBV
    EODZVFODZVGVMPVBVDUTDZVNCABQZVPVDRDVESDVNVDBTVDUAVEUBUCUDVBARDZVFSDVOVAVRUS
    ABTUEZAUAVFUBUCVEVFUFUGVBVIVFVELVBVHAIVBBRDZVRVHAPZCABUHVSVTBODAODWAVRBUIAU
    IBAUJUKUGULUMUNUOABCUPUSVAVPVLVKPVQVDBCUPUQUR $.
    $( [8-Nov-2013] $) $( [21-Jun-2005] $)

  $( ` N ` choose 0 is 1.  Remark in [Gleason] p. 296. $)
  bcn0 $p |- ( N e. NN0 -> ( N _C 0 ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cfa cfv cmin cmul cdiv c1 cfz wceq cuz eluzfz1 nn0uz
    eleq2s bcval2 mpdan cc nn0cn subid1 syl fveq2d fac0 opreq12 sylancl cn
    faccl nncn mulid1 eqtrd opreq2d wne facne0 divid syl11anc 3eqtrd ) ABCZADEF
    ZAGHZADIFZGHZDGHZJFZKFZVBVBKFZLUTDDAMFCZVAVGNVIADOHBDAPQRDABSTUTVFVBVBKUTVF
    VBLJFZVBUTVDVBNVELNVFVJNUTVCAGUTAUACVCANAUBAUCUDUEUFVDVBVELJUGUHUTVBUACZVJV
    BNUTVBUICVKAUJVBUKUDZVBULUDUMUNUTVKVBDUOVHLNVLAUPVBUQURUS $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( ` N ` choose ` N ` is 1.  Remark in [Gleason] p. 296. $)
  bcnn $p |- ( N e. NN0 -> ( N _C N ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cmin c1 cfz wceq cuz cfv eluzfz1 nn0uz eleq2s bccmpl
    mpdan bcn0 cc nn0cn subid1 syl opreq2d 3eqtr3rd ) ABCZADEFZAADGFZEFZHAAEFUE
    DDAIFCZUFUHJUIADKLBDAMNODABPQARUEUGAAEUEASCUGAJATAUAUBUCUD $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( Binomial coefficient: ` N ` choose ` 1 ` . $)
  bcn1 $p |- ( N e. NN0 -> ( N _C 1 ) = N ) $=
    ( cn0 wcel cn cc0 wceq wo c1 cbc co elnn0 cfa cfv cmin cmul cdiv cfz cuz
    wa elnnuz biimpi 1nn0 nn0uz eleqtri jctil elfzuzb mpbird bcval2 mpdan
    facnn2 fac1 opreq2i cc nnm1nn0 faccl syl nncn mulid1 3syl syl5eq opreq12d
    wne nnne0 divcan3 syl111anc eqtrd cz clt wbr 0nn0 1z lt01 olci bcval4
    mp3an wb opreq1 eqeq12 mpancom mpbiri jaoi sylbi ) ABCADCZAEFZGAHIJZAFZAKWC
    WFWDWCWEALMZAHNJZLMZHLMZOJZPJZAWCHEAQJCZWEWLFWCWMHERMZCZAHRMCZSWCWPWOWCWPAT
    UAHBWNUBUCUDUEDHEAUFUGHADUHUIWCWLWIAOJZWIPJZAWCWGWQWKWIPAUJWCWKWIHOJZWIWJHW
    IOUKULWCWIDCZWIUMCZWSWIFWCWHBCWTAUNWHUOUPZWIUQZWIURUSUTVAWCAUMCXAWIEVBZWRAF
    AUQWCWTXAXBXCUPWCWTXDXBWIVCUPAWIVDVEVFVFWDWFEHIJZEFZEBCHVGCHEVHVIZEHVHVIZGX
    FVJVKXHXGVLVMHEVNVOWEXEFWDWFXFVPAEHIVQWEXEAEVRVSVTWAWB $.
    $( [8-Nov-2013] $) $( [21-Jun-2005] $)

  $( Binomial coefficient: ` N + 1 ` choose ` N ` . $)
  bcnp1n $p |- ( N e. NN0 -> ( ( N + 1 ) _C N ) = ( N + 1 ) ) $=
    ( cn0 wcel c1 caddc co cbc cmin cc0 cfz wceq peano2nn0 cle wbr wa cr nn0re
    lep1 syl ancli wb fznn0 mpbird bccmpl syl11anc cc nn0cn ax-1cn pncan2
    sylancl opreq2d bcn1 3eqtrd ) ABCZADEFZAGFZUOUOAHFZGFZUODGFZUOUNUOBCZAIUOJF
    CZUPURKALZUNVAUNAUOMNZOZUNVCUNAPCVCAQARSTUNUTVAVDUAVBAUOUBSUCAUOBUDUEUNUQDU
    OGUNAUFCDUFCUQDKAUGUHADUIUJUKUNUTUSUOKVBUOULSUM $.
    $( [8-Nov-2013] $) $( [20-Jun-2005] $)

  $( Pascal's rule for the binomial coefficient, generalized to all integers
     ` K ` .  Equation 2 of [Gleason] p. 295. $)
  bcpasc $p |- ( ( N e. NN0 /\ K e. ZZ ) ->
                ( ( N _C K ) + ( N _C ( K - 1 ) ) ) = ( ( N + 1 ) _C K ) ) $=
    ( cn0 wcel cz wa cc0 c1 caddc co cfz cbc cmin wceq wo wb peano2nn0 cuz cfv
    elfzp12 nn0uz eleq2s syl cc ax-1cn addid1 ax-mp bcn0 clt wbr 0z 1z zsubcl
    mp2an cr 0re ltm1 orci bcval4 mp3an23 opreq12d 3eqtr4a opreq2 opreq1
    opreq2d eqeq12d syl5ibrcom simpr addid2 opreq1i syl6eleq cn nn0p1nn nnuz
    fzm1 biimpa sylan nn0cn pncan sylancl eleq2d cfa cmul cdiv wne a1i elfzelz
    adantl zcn elfznn nnne0 jca cle elfzle2 nn0z adantr znn0sub syl11anc mpbid
    nncn divadddiv syl22anc mulid2 mulid1 add32 syl111anc npcan opreq1d 3eqtrd
    eqtrd faccl fzelp1 nnz elfzm1b syl2anc eleqtrd elfznn0 fznn0sub nnmulcl
    divcl nnrecre recnd adddi divmuldiv facp1 addsub fveq2d 3syl nnnn0
    ax-mulcom facnn2 mul4 eqtr4d 3eqtr3d mul32 3eqtr2d wss fzp1ss mpan
    syl5eqssr sseld imp bcval2 syldan 3eqtr4rd ax-mulass subsub eqeltrd cvv
    oprex sylancr 3eqtr4d nn0re ltp1 olcd mpd3an23 sylan9eqr bcnn jaodan ex
    jaod sylbid adantlr wn 00id simpll simplr fzssp1 con3d bcval3 peano2zm id
    eqsstr3i sseli syl6bir sylbird pm2.61dan ) BCDZAEDZFZAGBHIJZKJZDZBALJZBAHMJ
    ZLJZIJZUWAALJZNZUVRUWCUWIUVSUVRUWCUWIUVRUWCAGNZAGHIJZUWAKJZDZOZUWIUVRUWACDZ
    UWCUWNPZBQZUWPUWAGRSCAGUWATUAUBUCUVRUWJUWIUWMUVRUWIUWJBGLJZBGHMJZLJZIJZUWAG
    LJZNUVRHGIJZHUXAUXBHUDDZUXCHNUEHUFUGUVRUWRHUWTGIBUHUVRUWSEDZUWSGUIUJZBUWSUI
    UJZOUWTGNGEDZHEDUXEUKULGHUMUNUXFUXGGUODUXFUPGUQUGURUWSBUSUTVAUVRUWOUXBHNUWQ
    UWAUHUCVBUWJUWGUXAUWHUXBUWJUWDUWRUWFUWTIAGBLVCUWJUWEUWSBLAGHMVDVEVAAGUWALVC
    VFVGUVRUWMUWIUVRUWMAHUWAKJZDZUWIUVRUWMFAUWLUXIUVRUWMVHUWKHUWAKUXDUWKHNUEHVI
    UGZVJZVKUVRUXJAHUWAHMJZKJZDZAUWANZOZUWIUVRUWAHRSZDZUXJUXQUVRUWAVLUXRBVMZVNV
    KUXSUXJUXQAHUWAVOVPVQUVRUXOUWIUXPUVRUXOAHBKJZDZUWIUVRUXOUYBUVRUXNUYAAUVRUXM
    BHKUVRBUDDZUXDUXMBNZBVRZUEBHVSZVTZVEWAVPUVRUYBFZBWBSZUWEWBSZBAMJZWBSZWCJZWD
    JZHAWDJZWCJZUYNHUYKHIJZWDJZWCJZIJZUWAWBSZUWAAMJZWBSZAWBSZWCJZWDJZUWGUWHUYHU
    YNUYOUYRIJZWCJZUYNUWAAUYQWCJZWDJZWCJZUYTVUFUYHVUGVUJUYNWCUYHVUGHUYQWCJZAHWC
    JZIJZVUIWDJZVUJUYHUXDUXDAUDDZAGWEZFZUYQUDDZUYQGWEZFZVUGVUONUXDUYHUEWFZVVBUY
    HVUPVUQUYHUVSVUPUYBUVSUVRAHBWGZWHZAWIUCZUYHAVLDZVUQUYBVVFUVRABWJWHZAWKUCWLZ
    UYHUYQVLDZVVAUYHUYKCDZVVIUYHABWMUJZVVJUYBVVKUVRAHBWNWHUYHUVSBEDZVVKVVJPVVDU
    VRVVLUYBBWOWPABWQWRWSZUYKVMUCZVVIVUSVUTUYQWTUYQWKWLUCZHHAUYQXAXBUYHVUNUWAVU
    IWDUYHVUNUYQAIJZUYKAIJZHIJZUWAUYHVULUYQVUMAIUYHVUSVULUYQNUYHUYQCDZVUSUYHVVJ
    VVSVVMUYKQUCZUYQVRUCZUYQXCUCUYHVUPVUMANVVEAXDUCVAUYHUYKUDDZUXDVUPVVPVVRNUYH
    VVJVWBVVMUYKVRUCVVBVVEUYKHAXEXFUYHVVQBHIUYHUYCVUPVVQBNUVRUYCUYBUYEWPZVVEBAX
    GWRXHXIXHXJVEUYHUYNUDDZUYOUDDZUYRUDDZVUHUYTNUYHUYIUDDZUYMUDDZUYMGWEZVWDUYHU
    YIVLDZVWGUVRVWJUYBBXKWPUYIWTUCZUYHUYMVLDZVWHUYHUYJVLDZUYLVLDZVWLUYHUWECDZVW
    MUYHUWEGBKJZDZVWOUYHUWEGUXMKJZVWPUYHUXJUWEVWRDZCAHBXLUYBUVSUWAEDZUXJVWSPZUV
    RVVCUVRUWAVLDZVWTUXTUWAXMUCZAUWAXNZXOWSUVRVWRVWPNUYBUVRUXMBGKUYGVEWPXPZUWEB
    XQUCZUWEXKZUCUYHVVJVWNCAHBXRUYKXKZUCUYJUYLXSWRZUYMWTZUCUYHVWLVWIVXIUYMWKZUC
    UYIUYMXTXFUYHVVFVWEVVGVVFUYOAYAYBUCUYHVVIVWFVVNVVIUYRUYQYAYBUCUYNUYOUYRYCXF
    UYHVUKUYIUWAWCJZUYMVUIWCJZWDJZVUFUYHVWGUWAUDDZVWHVWIFZVUIUDDZVUIGWEZFZVUKVX
    NNVWKUYHVXBVXOUVRVXBUYBUXTWPUWAWTUCUYHVWLVXPVXIVWLVWHVWIVXJVXKWLUCZUYHVUIVL
    DZVXSUYHVVFVVIVYAVVGVVNAUYQXSWRVYAVXQVXRVUIWTVUIWKWLUCUYIUWAUYMVUIYDXBUYHVU
    AVXLVUEVXMWDUVRVUAVXLNUYBBYEWPUYHVUEUYJAWCJZUYLUYQWCJZWCJZVXMUYHVUEUYQWBSZV
    UDWCJZVUDVYEWCJZVYDUYHVUCVYEVUDWCUYHVUBUYQWBUYHUYCUXDVUPVUBUYQNVWCVVBVVEBHA
    YFXFYGXHUYHVYEUDDZVUDUDDZVYFVYGNUYHVVSVYEVLDVYHVVTUYQXKVYEWTYHUYHACDZVUDVLD
    VYIUYHVVFVYJVVGAYIUCAXKVUDWTYHZVYEVUDYJWRUYHVUDVYBVYEVYCWCUYHVVFVUDVYBNVVGA
    YKUCZUYHVVJVYEVYCNVVMUYKYEUCZVAXIUYHUYJUDDZVUPUYLUDDZVUSVYDVXMNUYHVWOVWMVYN
    VXFVXGUYJWTYHZVVEUYHVVJVWNVYOVVMVXHUYLWTYHZVWAUYJAUYLUYQYLXBXJVAYMYNUYHUWDU
    YPUWFUYSIUYHUYIHWCJZUYMAWCJZWDJZUYIUYLVUDWCJZWDJZUYPUWDUYHVYRUYIVYSWUAWDUYH
    VWGVYRUYINVWKUYIXDUCZUYHVYSVYBUYLWCJZVUDUYLWCJZWUAUYHVYNVYOVUPVYSWUDNVYPVYQ
    VVEUYJUYLAYOXFUYHVUDVYBUYLWCVYLXHUYHVYIVYOWUEWUANVYKVYQVUDUYLYJWRYPVAUYHVWG
    UXDVXPVURUYPVYTNVWKVVBVXTVVHUYIHUYMAYDXBUVRUYBAVWPDZUWDWUBNUVRUYBWUFUVRUYAV
    WPAUVRUYAUWKBKJZVWPUWKHBKUXKVJUXHUVRWUGVWPYQUKGBCYRYSYTUUAUUBZABCUUCUUDUUEU
    YHVYRUYMUYQWCJZWDJZUYIBUWEMJZWBSZUYJWCJZWDJZUYSUWFUYHVYRUYIWUIWUMWDWUCUYHWU
    IUYJVYCWCJZUYJWULWCJZWUMUYHVYNVYOVUSWUIWUONVYPVYQVWAUYJUYLUYQUUFXFUYHWULVYC
    UYJWCUYHWULVYEVYCUYHWUKUYQWBUYHUYCVUPUXDWUKUYQNVWCVVEVVBBAHUUGXFZYGVYMXJVEU
    YHVYNWULUDDZWUPWUMNVYPUYHWUKCDWULVLDWURUYHWUKUYQCWUQVVTUUHWUKXKWULWTYHUYJWU
    LYJWRYPVAUYHVWGUXDVXPVVAUYSWUJNVWKVVBVXTVVOUYIHUYMUYQYDXBUVRUYBVWQUWFWUNNVX
    EUWEBCUUCUUDUUEVAUYHUWAUUIDZUWCUWHVUFNBHIUUJZUVRUYBWUFUWCWUHCAGBXLUUDAUWAUU
    IUUCUUKUULUUDUVRUXPFZUWKHUWGUWHUXKWVAUWDGUWFHIUXPUVRUWDBUWALJZGAUWABLVCUVRV
    WTUWAGUIUJZBUWAUIUJZOWVBGNVXCUVRWVDWVCUVRBUODWVDBUUMBUUNUCUUOUWABUSUUPUUQWV
    AUWFBBLJZHWVAUWEBBLUXPUVRUWEUXMBAUWAHMVDUYGUUQVEUVRWVEHNUXPBUURWPXJVAUXPUVR
    UWHUWAUWALJZHAUWAUWALVCUVRUWOWVFHNUWQUWAUURUCUUQVBUUSUUDUUDUUTUVAUVBUUBUVCU
    VTUWCUVDZFZGGIJGUWGUWHUVEWVHUWDGUWFGIWVHUVRUVSWUFUVDZUWDGNUVRUVSWVGUVFZUVRU
    VSWVGUVGZUVTWVGWVIUVTWUFUWCUVTVWPUWBAUVRVWPUWBYQUVSGBCUVHWPUUAUVIUUBABUVJXF
    WVHUVRUWEEDZVWQUVDZUWFGNWVJWVHUVSWVLWVKAUVKUCUVTWVGWVMUVTVWQUWCUVTVWQVWSUWC
    UVTVWRVWPUWEUVTUXMBGKUVTUYCUXDUYDUVRUYCUVSUYEWPUEUYFVTVEWAUVTVWSUXJUWCUVSUV
    SVWTVXAUVRUVSUVLUVRUWOVWTUWQUWAWOUCVXDXOUXIUWBAUXIUWLUWBUXLUXHWUSUWLUWBYQUK
    WUTGUWAUUIYRUNUVMUVNUVOUVPUVIUUBUWEBUVJXFVAWVHUWOUVSWVGUWHGNWVHUVRUWOWVJUWQ
    UCWVKUVTWVGVHAUWAUVJXFVBUVQ $.
    $( [9-Nov-2013] $) $( [13-Jul-2005] $)

  ${
    $d k m n N $.  $d k m n K $.
    $( A binomial coefficient, in its extended domain, is a nonnegative
       integer. $)
    bccl $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) e. NN0 ) $=
      ( vk vm vn cn0 wcel cv cbc co cz wral cc0 c1 caddc wceq opreq1 eleq1d
      ralbidv cfz wa elfz1eq adantl 0nn0 bcn0 ax-mp 1nn0 eqeltri opreq2 mpbiri
      syl wn bcval3 mp3an1 syl6eqel pm2.61dan rgen cbvralv cmin bcpasc adantlr
      rcla4cva peano2zm sylan2 nn0addcl syl11anc adantll eqeltrrd r19.21aiva
      ex syl5bi nn0ind sylan ) BFGBCHZIJZFGZCKLZAKGBAIJZFGZDHZVNIJZFGZCKLMVNIJZ
      FGZCKLEHZVNIJZFGZCKLZWENOJZVNIJZFGZCKLZVQDEBVTMPZWBWDCKWMWAWCFVTMVNIQRSVT
      WEPZWBWGCKWNWAWFFVTWEVNIQRSVTWIPZWBWKCKWOWAWJFVTWIVNIQRSVTBPZWBVPCKWPWAVO
      FVTBVNIQRSWDCKVNKGZVNMMTJGZWDWQWRUAVNMPZWDWRWSWQVNMUBUCWSWDMMIJZFGWTNFMFG
      ZWTNPUDMUEUFUGUHWSWCWTFVNMMIUIRUJUKWQWRULZUAWCMFXAWQXBWCMPUDVNMUMUNUDUOUP
      UQWHWEVTIJZFGZDKLZWEFGZWLWGXDCDKVNVTPWFXCFVNVTWEIUIRURXFXEWLXFXEUAZWKCKXG
      WQUAWFWEVNNUSJZIJZOJZWJFXFWQXJWJPXEVNWEUTVAXEWQXJFGZXFXEWQUAWGXIFGZXKXDWG
      DVNKVTVNPXCWFFVTVNWEIUIRVBWQXEXHKGXLVNVCXDXLDXHKVTXHPXCXIFVTXHWEIUIRVBVDW
      FXIVEVFVGVHVIVJVKVLVPVSCAKVNAPVOVRFVNABIUIRVBVM $.
      $( [9-Nov-2013] $) $( [10-Jul-2005] $)

    $( A binomial coefficient, in its standard domain, is a natural number. $)
    bccl2 $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) e. NN ) $=
      ( vm vn wcel cc0 cfz co wa cuz cfv cn0 cbc cn elfzuz3 elfznn0 adantl cv
      wi c1 caddc wceq opreq1 eleq1d imbi2d cz bcnn 1nn syl6eqel a1i cmin
      nn0uz eleq2i uztrn sylan2b syl6eleqr adantr nn0z bcpasc syl11anc id
      peano2zm syl bccl nnnn0addcl syl2anc eqeltrrd exp31 a2d uzind4 sylc ) BCF
      ZAGBHIFZJBAKLZFAMFZBANIZOFZCAGBPVNVPVMABQRVPDSZANIZOFZTVPAANIZOFZTZVPESZA
      NIZOFZTVPWEUAUBIZANIZOFZTVPVRTDEABVSAUCZWAWCVPWKVTWBOVSAANUDUEUFVSWEUCZWA
      WGVPWLVTWFOVSWEANUDUEUFVSWHUCZWAWJVPWMVTWIOVSWHANUDUEUFVSBUCZWAVRVPWNVTVQ
      OVSBANUDUEUFWDAUGFZVPWBUAOAUHUIUJUKWEVOFZVPWGWJWPVPWGWJWPVPJZWGJZWFWEAUAU
      LIZNIZUBIZWIOWRWEMFZWOXAWIUCWQXBWGWQWEGKLZMVPWPAXCFWEXCFMXCAUMUNAWEGUOUPU
      MUQZURWQWOWGVPWOWPAUSRZURAWEUTVAWGWGWTMFZXAOFWQWGVBWQXBWSUGFZXFXDWQWOXGXE
      AVCVDWSWEVEVAWFWTVFVGVHVIVJVKVL $.
      $( [9-Nov-2013] $) $( [3-Jan-2006] $)
  $}

  $( The number of permutations of ` N - R ` objects from a collection of ` N `
     objects is a natural number.  (Contributed by Jason Orendorff,
     24-Jan-2007.) $)
  permnn $p |- ( ( N e. NN0 /\ R e. ( 0 ... N ) ) ->
               ( ( ! ` N ) / ( ! ` R ) ) e. NN ) $=
    ( cn0 wcel cc0 cfz co wa cfa cfv cn cmin cmul cc cdiv elfznn0 faccl syl
    adantl fznn0sub nnmulcl syl11anc nncn adantr wne wceq facne0 divcan4
    syl111anc eqeltrd cbc bcval2 bccl2 eqeltrrd nndivtr syl32anc ) BCDZAEBFGDZH
    ZAIJZKDZBALGZIJZUTMGZKDZBIJZNDZVDUTOGZKDVFVDOGZKDVFUTOGKDURVAUQURACDZVAABPZ
    AQRZSZUSVCKDZVAVEUSVBCDVNCAEBTVBQRZVMVCUTUAUBUQVGURUQVFKDVGBQVFUCRUDUSVHVCK
    USVCNDZUTNDZUTEUEZVHVCUFUSVNVPVOVCUCRURVQUQURVAVQVLUTUCRSURVRUQURVJVRVKAUGR
    SVCUTUHUIVOUJUSBAUKGVIKABCULABCUMUNUTVDVFUOUP $.
    $( [24-Jan-2007] $)

