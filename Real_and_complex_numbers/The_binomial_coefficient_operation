$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The binomial coefficient operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c _C $. $( Binomial coefficient operation. $)

  $( Extend class notation to include the binomial coefficient operation
     (combinatorial choose operation). $)
  cbc $a class _C $.

  ${
    $d n k m $.
    $( Define the binomial coefficient operation.  In the literature, this
       function is often written as a column vector of the two arguments, or
       with the arguments as subscripts before and after the letter "C".
       ` ( N _C K ) ` is read " ` N ` choose ` K ` ."  Definition of binomial
       coefficient in [Gleason] p. 295.  As suggested by Gleason, we define it
       to be 0 when ` 0 <_ k <_ n ` does not hold. $)
    df-bc $a |- _C = ( n e. NN0 , k e. ZZ |-> if ( k e. ( 0 ... n ) ,
                ( ( ! ` n ) / ( ( ! ` ( n - k ) ) x. ( ! ` k ) ) ) , 0 ) ) $.
  $}

  ${
    $d n k m N $.  $d n k m K $.
    $( Value of the binomial coefficient, ` N ` choose ` K ` .  Definition of
       binomial coefficient in [Gleason] p. 295.  As suggested by Gleason, we
       define it to be 0 when ` 0 <_ K <_ N ` does not hold.  See ~ bcval2 for
       the value in the standard domain. $)
    bcval $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) = if ( K e. ( 0 ... N )
      , ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) , 0 ) ) $=
      ( vn vk cv cc0 cfz co wcel cfa cfv cmin cmul cdiv wceq oveq2 fveq2 fveq2d
      cif oveq12d cn0 cz eleq2d oveq1 oveq1d eqidd ifbieq12d eleq1 oveq2d df-bc
      cbc ovex cc 0cn elexi ifex ovmpt2 ) CDBAUAUBDEZFCEZGHZIZUSJKZUSURLHZJKZUR
      JKZMHZNHZFSAFBGHZIZBJKZBALHZJKZAJKZMHZNHZFSUKURVHIZVJBURLHZJKZVEMHZNHZFSU
      SBOZVAVPVGFVTFWAUTVHURUSBFGPUCWAVBVJVFVSNUSBJQWAVDVRVEMWAVCVQJUSBURLUDRUE
      TWAFUFUGURAOZVPVIVTFVOFURAVHUHWBVSVNVJNWBVRVLVEVMMWBVQVKJURABLPRURAJQTUIW
      BFUFUGDCUJVIVOFVJVNNULFUMUNUOUPUQ $.
      $( [7-Nov-2013] $) $( [10-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , in its standard
       domain. $)
    bcval2 $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) =
                    ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) ) $=
      ( wcel cc0 cfz co cbc cfa cfv cmin cmul cdiv cif cn0 wceq elfz3nn0 adantl
      wa cz elfzelz bcval syl2anc iftrue eqtrd ) BCDZAEBFGDZSZBAHGZUGBIJBAKGIJA
      IJLGMGZENZUJUHBODATDZUIUKPCABQUGULUFAEBUARABUBUCUGUKUJPUFUGUJEUDRUE $.
      $( [7-Nov-2013] $) $( [9-Jun-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval3 $p |- ( ( N e. NN0 /\ K e. ZZ /\ -. K e. ( 0 ... N ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 cfz co wn w3a cbc cfa cfv cmin cmul cdiv cif wceq bcval
      3adant3 iffalse 3ad2ant3 eqtrd ) BCDZAEDZAFBGHDZIZJBAKHZUFBLMBANHLMALMOHP
      HZFQZFUDUEUHUJRUGABSTUGUDUJFRUEUFUIFUAUBUC $.
      $( [8-Nov-2013] $) $( [14-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval4 $p |- ( ( N e. NN0 /\ K e. ZZ /\ ( K < 0 \/ N < K ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 clt wbr wo w3a cfz co wn wa cle cr lenlt mpbid adantl
      wb cbc wceq simp1 simp2 elfzle1 0re elfzelz zre syl sylancr elfzle2 nn0re
      wi syl2anr jca ioran sylibr ex adantr con2d 3impia bcval3 syl3anc ) BCDZA
      EDZAFGHZBAGHZIZJVDVEAFBKLDZMZBAUALFUBVDVEVHUCVDVEVHUDVDVEVHVJVDVENVIVHVDV
      IVHMZUMVEVDVIVKVDVINZVFMZVGMZNVKVLVMVNVIVMVDVIFAOHZVMAFBUEVIFPDAPDZVOVMTU
      FVIVEVPAFBUGAUHUIZFAQUJRSVLABOHZVNVIVRVDAFBUKSVIVPBPDVRVNTVDVQBULABQUNRUO
      VFVGUPUQURUSUTVAABVBVC $.
      $( [7-Nov-2013] $) $( [14-Jul-2005] $)
  $}

  $( Closure of the binomial coefficient in the positive reals.  (This is
     mostly a lemma before we have ~ bccl2 .)  (Contributed by Mario Carneiro,
     10-Mar-2014.) $)
  bcrpcl $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) e. RR+ ) $=
    ( wcel cc0 cfz co wa cbc cfa cfv cmin cmul crp cn faccl syl2an syl2anc nnrp
    cn0 cdiv bcval2 elfz3nn0 fznn0sub elfznn0 adantl nnmulcl rpdivcl eqeltrd
    syl ) BCDZAEBFGDZHZBAIGBJKZBALGZJKZAJKZMGZUAGZNABCUBUMUNODZURODZUSNDZUMBTDU
    TCABUCBPUJUMUOTDZATDZVACAEBUDULVDUKABUEUFVCUPODUQODVAVDUOPAPUPUQUGQRUTUNNDU
    RNDVBVAUNSURSUNURUHQRUI $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( "Complementing" its second argument doesn't change a binary coefficient.
     (Revised by Mario Carneiro, 5-Mar-2014.) $)
  bccmpl $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) =
                    ( N _C ( N - K ) ) ) $=
    ( cn0 wcel cz wa cc0 co cbc cmin wceq cfa cmul cdiv cc fznn0sub2 cn elfznn0
    cfv faccl cfz nncn syl adantl mulcom syl2anc elfzelz nn0cn zcn nncan syl2an
    sylan2 fveq2d oveq1d eqtr4d oveq2d bcval2 syldan 3eqtr4d adantlr w3a bcval3
    3syl wn simp1 zsubcl sylan 3adant3 wi ex adantr eleq1d sylibd con3d syl3anc
    nn0z 3impia 3expa pm2.61dan ) BCDZAEDZFZAGBUAHZDZBAIHZBBAJHZIHZKZVTWDWHWAVT
    WDFZBLSZWFLSZALSZMHZNHWJBWFJHZLSZWKMHZNHZWEWGWIWMWPWJNWIWMWLWKMHZWPWIWKODZW
    LODZWMWRKWIWFWCDZWSCABPZXAWFCDWKQDWSWFBRWFTWKUBVCUCWIACDZWLQDWTWDXCVTABRUDA
    TWLUBVCWKWLUEUFWIWOWLWKMWIWNALWDVTWAWNAKZAGBUGVTBODAODXDWABUHAUIBAUJUKZULUM
    UNUOUPABCUQVTWDXAWGWQKXBWFBCUQURUSUTVTWAWDVDZWHVTWAXFVAZWEGWGABVBXGVTWFEDZX
    AVDZWGGKVTWAXFVEVTWAXHXFVTBEDWAXHBVPBAVFVGVHVTWAXFXIWBXAWDWBXAWNWCDZWDVTXAX
    JVIWAVTXAXJCWFBPVJVKWBWNAWCXEVLVMVNVQWFBVBVOUOVRVS $.
    $( [5-Mar-2014] $) $( [21-Jun-2005] $)

  $( ` N ` choose 0 is 1.  Remark in [Gleason] p. 296. $)
  bcn0 $p |- ( N e. NN0 -> ( N _C 0 ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cfa cfv cmin cmul cdiv c1 cfz wceq cuz eluzfz1 eleq2s
    nn0uz cc syl bcval2 mpdan nn0cn subid1 fveq2d fac0 oveq12 sylancl cn mulid1
    faccl nncn eqtrd oveq2d wne facne0 divid syl2anc 3eqtrd ) ABCZADEFZAGHZADIF
    ZGHZDGHZJFZKFZVBVBKFZLUTDDAMFCZVAVGNVIADOHBDAPRQDABUAUBUTVFVBVBKUTVFVBLJFZV
    BUTVDVBNVELNVFVJNUTVCAGUTASCVCANAUCAUDTUEUFVDVBVELJUGUHUTVBSCZVJVBNUTVBUICV
    KAUKVBULTZVBUJTUMUNUTVKVBDUOVHLNVLAUPVBUQURUS $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( ` N ` choose ` N ` is 1.  Remark in [Gleason] p. 296. $)
  bcnn $p |- ( N e. NN0 -> ( N _C N ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cmin c1 cz wceq 0z bccmpl mpan2 bcn0 nn0cn subid1 syl
    cc oveq2d 3eqtr3rd ) ABCZADEFZAADGFZEFZHAAEFUADICUBUDJKDALMANUAUCAAEUAARCUC
    AJAOAPQST $.
    $( [8-Nov-2013] $) $( [17-Jun-2005] $)

  $( Binomial coefficient: ` N ` choose ` 1 ` . $)
  bcn1 $p |- ( N e. NN0 -> ( N _C 1 ) = N ) $=
    ( cn0 wcel cn cc0 wceq wo c1 cbc co cfa cfv cmul cdiv cuz cc syl nncn eqtrd
    clt elnn0 cmin cfz wa elnnuz biimpi 1nn0 nn0uz eleqtri jctil elfzuzb mpbird
    bcval2 mpdan facnn2 fac1 oveq2i nnm1nn0 faccl mulid1 3syl oveq12d wne nnne0
    syl5eq divcan3 syl3anc cz wbr 0nn0 1z lt01 olci bcval4 mp3an eqeq12 mpancom
    wb oveq1 mpbiri jaoi sylbi ) ABCADCZAEFZGAHIJZAFZAUAWCWFWDWCWEAKLZAHUBJZKLZ
    HKLZMJZNJZAWCHEAUCJCZWEWLFWCWMHEOLZCZAHOLCZUDWCWPWOWCWPAUEUFHBWNUGUHUIUJDHE
    AUKULHADUMUNWCWLWIAMJZWINJZAWCWGWQWKWINAUOWCWKWIHMJZWIWJHWIMUPUQWCWIDCZWIPC
    ZWSWIFWCWHBCWTAURWHUSQZWIRZWIUTVAVEVBWCAPCXAWIEVCZWRAFARWCWTXAXBXCQWCWTXDXB
    WIVDQAWIVFVGSSWDWFEHIJZEFZEBCHVHCHETVIZEHTVIZGXFVJVKXHXGVLVMHEVNVOWEXEFWDWF
    XFVRAEHIVSWEXEAEVPVQVTWAWB $.
    $( [8-Nov-2013] $) $( [21-Jun-2005] $)

  $( Binomial coefficient: ` N + 1 ` choose ` N ` . $)
  bcnp1n $p |- ( N e. NN0 -> ( ( N + 1 ) _C N ) = ( N + 1 ) ) $=
    ( cn0 wcel c1 caddc co cbc cmin cz wceq peano2nn0 nn0z bccmpl syl2anc nn0cn
    cc ax-1cn pncan2 sylancl oveq2d bcn1 syl 3eqtrd ) ABCZADEFZAGFZUEUEAHFZGFZU
    EDGFZUEUDUEBCZAICUFUHJAKZALAUEMNUDUGDUEGUDAPCDPCUGDJAOQADRSTUDUJUIUEJUKUEUA
    UBUC $.
    $( [8-Nov-2013] $) $( [20-Jun-2005] $)

  $( The proportion of one binomial coefficient to another with ` K ` decreased
     by 1.  (Contributed by Mario Carneiro, 10-Mar-2014.) $)
  bcm1k $p |- ( ( N e. V /\ K e. ( 1 ... N ) ) -> ( N _C K ) =
                ( ( N _C ( K - 1 ) ) x. ( ( N - ( K - 1 ) ) / K ) ) ) $=
    ( wcel c1 cfz co wa cfa cfv cmul cdiv cc wceq syl cn nncn cc0 cn0 faccl cbc
    caddc cz elfzel2g zcn elfznn adantl ax-1cn a1i subsub syl3anc fveq2d oveq1d
    cmin oveq2d oveq12d wne elfzuz2 nnuz syl6eleqr nnnn0 fznn0sub nn0p1nn nn0cn
    cuz nnm1nn0 nnmulcl syl2anc nnne0 jca divmuldiv syl22anc mul32 facp1 eqcomd
    facnn2 mulass 3eqtrd eqtr4d mulcom divcan5 eqtrd 3eqtrrd wss addid2i oveq1i
    fzp1ss mpan syl5eqssr ssel2 sylan bcval2 syldan npcan sylancl peano2zm uzid
    0z peano2uz 3syl eqeltrrd fzss2 simpr elfzelz elfzm1b mpbid sseldd 3eqtr4d
    wb ) BCDZAEBFGZDZHZBIJZBAUNGZIJZAIJZKGZLGZXNBAEUNGZUNGZIJZXTIJZKGZLGZYAALGZ
    KGZBAUAGZBXTUAGZYFKGXMYGXNXOEUBGZIJZYCKGZLGZYJALGZKGZXNYJKGZXRYJKGZLGZXSXMY
    EYMYFYNKXMYDYLXNLXMYBYKYCKXMYAYJIXMBMDZAMDZEMDZYAYJNXMBUCDZYSCAEBUDZBUEOZXM
    APDZYTXLUUEXJABUFUGZAQOZUUAXMUHUIBAEUJUKZULUMUOXMYAYJALUUHUMUPXMYOYPYLAKGZL
    GZYRXMXNMDZYJMDZYLMDZYLRUQZHZYTARUQZHYOUUJNXMXNPDZUUKXMBSDZUUQXMBPDUURXMBEV
    EJPCAEBURUSUTBVAOBTOXNQOZXMYJSDZUULXMYJPDZUUTXMXOSDZUVACAEBVBZXOVCOZYJVAOZY
    JVDOZXMYLPDZUUOXMYKPDZYCPDZUVGXMUUTUVHUVEYJTOZXMXTSDZUVIXMUUEUVKUUFAVFOXTTO
    ZYKYCVGVHUVGUUMUUNYLQYLVIVJOXMYTUUPUUGXMUUEUUPUUFAVIOVJXNYJYLAVKVLXMYQUUIYP
    LXMYQXPYJKGZXQKGZYKYCAKGZKGZUUIXMXPMDZXQMDZUULYQUVNNXMXPPDZUVQXMUVBUVSUVCXO
    TOZXPQOXMXQPDZUVRXMASDZUWAXMUUEUWBUUFAVAOATOZXQQOUVFXPXQYJVMUKXMUVMYKXQUVOK
    XMYKUVMXMUVBYKUVMNUVCXOVNOVOXMUUEXQUVONUUFAVPOUPXMUUIUVPXMYKMDZYCMDZYTUUIUV
    PNXMUVHUWDUVJYKQOXMUVIUWEUVLYCQOUUGYKYCAVQUKVOVRUOVSXMYRYJXNKGZYJXRKGZLGZXS
    XMYPUWFYQUWGLXMUUKUULYPUWFNUUSUVFXNYJVTVHXMXRMDZUULYQUWGNXMXRPDZUWIXMUVSUWA
    UWJUVTUWCXPXQVGVHZXRQOZUVFXRYJVTVHUPXMUUKUWIXRRUQZHUULYJRUQZHUWHXSNUUSXMUWI
    UWMUWLXMUWJUWMUWKXRVIOVJXMUULUWNUVFXMUVAUWNUVDYJVIOVJXNXRYJWAUKWBWCXJXLARBF
    GZDZYHXSNXJXKUWOWDXLUWPXJXKREUBGZBFGZUWOUWQEBFEUHWEWFRUCDXJUWRUWOWDWRRBCWGW
    HWIXKUWOAWJWKABCWLWMXMYIYEYFKXJXLXTUWODYIYENXMRBEUNGZFGZUWOXTXMBUWSVEJZDUWT
    UWOWDXMUWSEUBGZBUXAXMYSUUAUXBBNUUDUHBEWNWOXMUWSUCDZUWSUXADUXBUXADXMUUBUXCUU
    CBWPOUWSWQUWSUWSWSWTXAUWSRBXBOXMXLXTUWTDZXJXLXCXMAUCDZUUBXLUXDXIXLUXEXJAEBX
    DUGUUCABXEVHXFXGXTBCWLWMUMXH $.
    $( [10-Mar-2014] $)

  $( The proportion of one binomial coefficient to another with ` N ` increased
     by 1.  (Contributed by Mario Carneiro, 10-Mar-2014.) $)
  bcp1n $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( ( N + 1 ) _C K ) =
                ( ( N _C K ) x. ( ( N + 1 ) / ( ( N + 1 ) - K ) ) ) ) $=
    ( wcel cc0 cfz co wa c1 cfa cfv cmul cdiv cn0 wceq syl cc cn faccl nncn cbc
    caddc cmin elfz3nn0 facp1 fznn0sub ax-1cn a1i elfznn0 adantl addsub syl3anc
    nn0cn fveq2d oveq2d 3eqtr4d nn0p1nn eqeltrd mul32 eqtrd oveq12d wne nnmulcl
    oveq1d syl2anc nnne0 jca divmuldiv syl22anc eqtr4d fzelp1 bcval2 ) BCDZAEBF
    GDZHZBIUBGZJKZVPAUCGZJKZAJKZLGZMGZBJKZBAUCGZJKZVTLGZMGZVPVRMGZLGZVPAUAGZBAU
    AGZWHLGVOWBWCVPLGZWFVRLGZMGZWIVOVQWLWAWMMVOBNDZVQWLOCABUDZBUEPVOWAWEVRLGZVT
    LGZWMVOVSWQVTLVOWDIUBGZJKZWEWSLGZVSWQVOWDNDZWTXAOCAEBUFZWDUEPVOVRWSJVOBQDZI
    QDZAQDZVRWSOVOWOXDWPBUMPXEVOUGUHVOANDZXFVNXGVMABUIUJZAUMPBIAUKULZUNVOVRWSWE
    LXIUOUPVDVOWEQDZVRQDZVTQDZWRWMOVOWERDZXJVOXBXMXCWDSPZWETPVOVRRDZXKVOVRWSRXI
    VOXBWSRDXCWDUQPURZVRTPZVOVTRDZXLVOXGXRXHASPZVTTPWEVRVTUSULUTVAVOWCQDZVPQDZW
    FQDZWFEVBZHZXKVREVBZHWIWNOVOWCRDZXTVOWOYFWPBSPWCTPVOVPRDZYAVOWOYGWPBUQPVPTP
    ZVOWFRDZYDVOXMXRYIXNXSWEVTVCVEYIYBYCWFTWFVFVGPVOXKYEXQVOXOYEXPVRVFPVGWCVPWF
    VRVHVIVJVOYAAEVPFGDWJWBOYHCAEBVKAVPQVLVEVOWKWGWHLABCVLVDUP $.
    $( [10-Mar-2014] $)
  ${
    $d k x y K $.  $d k x y N $.
    $( Write out the top and bottom parts of the binomial coefficient
       ` ( N _C K ) = ( N x. ( N - 1 ) x. ... x. ( ( N - K ) + 1 ) ) / K ! `
       explicitly.  In this form, it is valid even for ` N < K ` , although it
       is no longer valid for non-positive ` K ` .  (Contributed by Mario
       Carneiro, 22-May-2014.) $)
    bcval5 $p |- ( ( N e. NN0 /\ K e. NN ) -> ( N _C K ) =
                 ( ( seq ( ( N - K ) + 1 ) ( x. , _I ) ` N ) / ( ! ` K ) ) ) $=
      ( cn0 wcel cn wa cc0 co cmul cid c1 cfv cdiv cc adantl wbr syl2anc wb syl
      wceq vk vx vy cfz cbc cmin caddc cseq bcval2 adantlr fznn0sub elnn0 sylib
      cfa wo cv mulcl w3a mulass cuz cle clt simplr elfzuz3 nnuz uztrn2 adantrr
      cr crp nnre nnrp ltsubrp syl2an cz nnz zsubcl zltp1le peano2z eluz mpbird
      mpbid simprr syl6eleq fvi elfzelz zcn eqeltrd seqsplit facnn 3eqtr4d expr
      oveq1d simpll faccl nncn mulid2 oveq2d eqtr3d fveq2 syl6eq ax-1cn addid2i
      3syl fac0 oveq1 seqeq1d fveq1d oveq12d eqeq2d syl5ibrcom jaod mpd wf eqid
      wne nn0z adantr eluzelz ffvelrn elfznn0 nnne0 divcan5 syl122anc 3eqtrd wn
      seqf nnnn0 div0 mul02 mul01 0z ad2antrr elfz syl3anc simpr ad2antlr nn0uz
      a1i elfz5 nn0re subge0 bitr4d mtbid zre 0re ltnle sylancl mpbir2and ax-mp
      nn0ge0 0cn seqz bcval3 syl3an2 3expa 3eqtr4rd pm2.61dan ) BCDZAEDZFZAGBUD
      HDZBAUEHZBIJBAUFHZKUGHZUHZLZAUNLZMHZTUUTUVAFZUVBBUNLZUVCUNLZUVGIHZMHZUVKU
      VFIHZUVLMHZUVHUURUVAUVBUVMTUUSABCUIUJUVIUVJUVNUVLMUVIUVCEDZUVCGTZUOZUVJUV
      NTZUVIUVCCDZUVRUURUVAUVTUUSCAGBUKUJZUVCULUMUVIUVPUVSUVQUUTUVAUVPUVSUUTUVA
      UVPFZFZBIJKUHZLZUVCUWDLZUVFIHUVJUVNUWCUAUBUCINJKUVCBUAUPZNDZUBUPZNDZFZUWG
      UWIIHZNDZUWCUWGUWIUQZOUWHUWJUCUPZNDURUWLUWOIHUWGUWIUWOIHIHTUWCUWGUWIUWOUS
      OUWCBUVDUTLZDZUVDBVAPZUWCUVCBVBPZUWRUWCBEDZUUSUWSUUTUVAUWTUVPUVIUUSBAUTLD
      ZUWTUURUUSUVAVCZUURUVAUXAUUSCAGBVDUJKBAEVEVFQZVGZUURUUSUWBVCZUWTBVHDZAVID
      UWSUUSBVJAVKBAVLVMZQUWCUVCVNDZBVNDZUWSUWRRZUWCUXIAVNDZUXHUWCUWTUXIUXDBVOZ
      SZUWCUUSUXKUXEAVOZSBAVPZQZUXMUVCBVQZQWAUWCUVDVNDZUXIUWQUWRRZUWCUXHUXRUXPU
      VCVRZSUXMUVDBVSZQVTUWCUVCEKUTLUUTUVAUVPWBZVEWCUWGKBUDHZDZUWGJLZNDZUWCUYDU
      YEUWGNUWGUYCWDUYDUWGVNDZUWHUWGKBWEUWGWFZSWGOWHUWCUWTUVJUWETZUXDBWIZSUWCUV
      KUWFUVFIUWCUVPUVKUWFTUYBUVCWISWLWJWKUVIUVSUVQUVJKUWEIHZTUVIKUVJIHZUVJUYKU
      VIUVJNDZUYLUVJTUVIUURUVJEDUYMUURUUSUVAWMBWNUVJWOXCUVJWPSUVIUVJUWEKIUVIUWT
      UYIUXCUYJSWQWRUVQUVNUYKUVJUVQUVKKUVFUWEIUVQUVKGUNLKUVCGUNWSXDWTUVQBUVEUWD
      UVQUVDKIJUVQUVDGKUGHKUVCGKUGXEKXAXBWTXFXGXHXIXJXKXLWLUVIUVFNDZUVGNDZUVGGX
      OZUVKNDZUVKGXOZUVOUVHTUVIUWPNUVEXMUWQUYNUVIUAUBINJUVDUWPUWPXNUUTUXRUVAUUT
      UXHUXRUURUXIUXKUXHUUSBXPZUXNUXOVMZUXTSZXQZUWGUWPDZUYFUVIVUCUYEUWGNUWGUWPW
      DVUCUYGUWHUVDUWGXRUYHSWGOUWKUWMUVIUWNOYFUVIUWQUWRUVIUWSUWRUVIUWTUUSUWSUXC
      UXBUXGQUVIUXHUXIUXJUUTUXHUVAUYTXQUVIUWTUXIUXCUXLSZUXQQWAUVIUXRUXIUXSVUBVU
      DUYAQVTUWPNBUVEXSQUVIUVGEDZUYOUVIACDZVUEUVAVUFUUTABXTOAWNZSZUVGWOZSUVIVUE
      UYPVUHUVGYAZSUVIUVKEDZUYQUVIUVTVUKUWAUVCWNSZUVKWOSUVIVUKUYRVULUVKYASUVFUV
      GUVKYBYCYDUUTUVAYEZFZGUVGMHZGUVHUVBVUNVUEVUOGTZVUNUUSVUFVUEUURUUSVUMVCAYG
      ZVUGXCVUEUYOUYPVUPVUIVUJUVGYHQSVUNUVFGUVGMVUNUAUBINJGUVDBCGUWKUWMVUNUWNOU
      WGUVDBUDHZDZUYFVUNVUSUYEUWGNUWGVURWDVUSUYGUWHUWGUVDBWEUYHSWGOUWHGUWGIHGTV
      UNUWGYIOUWHUWGGIHGTVUNUWGYJOVUNGVURDZUVDGVAPZGBVAPZVUNGVNDZUXRUXIVUTVVAVV
      BFRVVCVUNYKYRZUUTUXRVUMVUAXQUURUXIUUSVUMUYSYLZGUVDBYMYNVUNUVCGVBPZVVAVUNV
      VFGUVCVAPZYEZVUNUVAVVGUUTVUMYOVUNUVAABVAPZVVGVUNAGUTLZDUXIUVAVVIRVUNACVVJ
      UUSVUFUURVUMVUQYPYQWCVVEAGBYSQVUNUXFAVHDZVVGVVIRUURUXFUUSVUMBYTYLUUSVVKUU
      RVUMAVJYPBAUUAQUUBUUCVUNUVCVHDZGVHDVVFVVHRVUNUXHVVLUUTUXHVUMUYTXQZUVCUUDS
      UUEUVCGUUFUUGVTVUNUXHVVCVVFVVARVVMVVDUVCGVQQWAUURVVBUUSVUMBUUJYLUUHUURUUS
      VUMWMGJLGTZVUNGNDVVNUUKGNWDUUIYRUULWLUURUUSVUMUVBGTZUUSUURUXKVUMVVOUXNABU
      UMUUNUUOUUPUUQ $.
      $( [22-May-2014] $)
  $}

  $( Binomial coefficient: ` N ` choose ` 2 ` .  (Contributed by Mario
     Carneiro, 22-May-2014.) $)
  bcn2 $p |- ( N e. NN0 -> ( N _C 2 ) = ( ( N x. ( N - 1 ) ) / 2 ) ) $=
    ( cn0 wcel c2 co cmul cid cmin c1 caddc cseq cfv cdiv wceq ax-1cn cc syl cz
    cuz eqtrd cbc cfa cn 2nn bcval5 mpan2 df-2 eqcomi nncni subaddi mpbir nn0cn
    oveq2i npncan mp3an23 syl5eqr seqeq1d fveq1d nn0z uzid npcan sylancl fveq2d
    peano2zm eleqtrrd seqm1 syl2anc seq1 fvi oveq12d subcl mulcom 3eqtrd fac2
    a1i ) ABCZADUAEZAFGADHEZIJEZKZLZDUBLZMEZAAIHEZFEZDMEVPDUCCVQWCNUDDAUEUFVPWA
    WEWBDMVPWAAFGWDKZLZWDAFEZWEVPAVTWFVPVSWDFGVPVSVRDIHEZJEZWDWIIVRJWIINIIJEZDN
    DWKUGUHDIIDUDUIZOOUJUKUMVPAPCZWJWDNZAULZWMDPCIPCZWNWLOADIUNUOQUPUQURVPWGWDW
    FLZAGLZFEZWHVPWDRCZAWDIJEZSLZCWGWSNVPARCZWTAUSZAVDQZVPAASLZXBVPXCAXFCXDAUTQ
    VPXAASVPWMWPXAANWOOAIVAVBVCVEFGWDAVFVGVPWQWDWRAFVPWQWDGLZWDVPWTWQXGNXEFGWDV
    HQVPWTXGWDNXEWDRVIQTABVIVJTVPWDPCZWMWHWENVPWMWPXHWOOAIVKVBWOWDAVLVGVMWBDNVP
    VNVOVJT $.
    $( [22-May-2014] $)

  $( Compute the binomial coefficent of ` ( N + 1 ) ` over ` ( N - 1 ) `
     (Contributed by Scott Fenton, 11-May-2014.)  (Revised by Mario Carneiro,
     22-May-2014.) $)
  bcp1m1 $p |- ( N e. NN0 -> ( ( N + 1 ) _C ( N - 1 ) ) =
                 ( ( ( N + 1 ) x. N ) / 2 ) ) $=
    ( cn0 wcel c1 caddc co cmin cbc c2 cmul cdiv cz wceq peano2nn0 peano2zm syl
    nn0z cc ax-1cn oveq2d bccmpl syl2anc nn0cn pnncan syl3anc df-2 syl6eqr bcn2
    a1i pncan sylancl oveq1d eqtrd 3eqtrd ) ABCZADEFZADGFZHFZUPUPUQGFZHFZUPIHFZ
    UPAJFZIKFZUOUPBCZUQLCZURUTMANZUOALCVEAQAOPUQUPUAUBUOUSIUPHUOUSDDEFZIUOARCZD
    RCZVIUSVGMAUCZVIUOSUIZVKADDUDUEUFUGTUOVAUPUPDGFZJFZIKFZVCUOVDVAVNMVFUPUHPUO
    VMVBIKUOVLAUPJUOVHVIVLAMVJSADUJUKTULUMUN $.
    $( [22-May-2014] $) $( [11-May-2014] $)

  $( Pascal's rule for the binomial coefficient, generalized to all integers
     ` K ` .  Equation 2 of [Gleason] p. 295.  (Revised by Mario Carneiro,
     10-Mar-2014.) $)
  bcpasc $p |- ( ( N e. NN0 /\ K e. ZZ ) ->
                ( ( N _C K ) + ( N _C ( K - 1 ) ) ) = ( ( N + 1 ) _C K ) ) $=
    ( cn0 wcel cz cc0 c1 caddc co cfz cbc cmin wceq syl oveq2d cdiv cmul adantr
    cc syl3anc wa wo peano2nn0 cuz cfv elfzp12 nn0uz eleq2s ax-1cn addid1 ax-mp
    wb bcn0 clt wbr 0z 1z zsubcl mp2an cr 0re ltm1 orci mp3an23 oveq12d 3eqtr4a
    bcval4 oveq2 oveq1 eqeq12d syl5ibrcom simpr addid2 syl6eleq cn nn0p1nn nnuz
    oveq1i fzm1 biimpa sylan nn0cn pncan sylancl eleq2d 1nn0 eleqtri fzss1 mpan
    wss ssel2 syldan wne crp bcrpcl rpcn nncn a1i elfzelz adantl zcn addsub cle
    bcp1n elfzle2 nn0z znn0sub syl2anc mpbid eqeltrd nnne0 div12 syl112anc nnrp
    rpdivcl mulcom eqtrd 3eqtr2d divcan1 elfznn divdiv2 syl122anc subsub eqtr4d
    npcan adddi oveq1d fzelp1 nnz elfzm1b syl2anr eleqtrd sylan9eqr bcnn imp wn
    bcm1k con3d bcval3 cvv rpne0 divmul3 div23 3eqtr3rd 3eqtrrd nn0re ltp1 olcd
    mpbird mpd3an23 jaodan ex jaod sylbid adantlr simpll simplr fzssp1 peano2zm
    00id sseld id ovex fzp1ss eqsstr3i sseli syl6bir sylbird pm2.61dan ) BCDZAE
    DZUAZAFBGHIZJIZDZBAKIZBAGLIZKIZHIZUVMAKIZMZUVJUVOUWAUVKUVJUVOUWAUVJUVOAFMZA
    FGHIZUVMJIZDZUBZUWAUVJUVMCDZUVOUWFULZBUCZUWHUVMFUDUEZCAFUVMUFUGUHNUVJUWBUWA
    UWEUVJUWAUWBBFKIZBFGLIZKIZHIZUVMFKIZMUVJGFHIZGUWNUWOGSDZUWPGMUIGUJUKUVJUWKG
    UWMFHBUMUVJUWLEDZUWLFUNUOZBUWLUNUOZUBUWMFMFEDZGEDUWRUPUQFGURUSUWSUWTFUTDUWS
    VAFVBUKVCUWLBVGVDVEUVJUWGUWOGMUWIUVMUMNVFUWBUVSUWNUVTUWOUWBUVPUWKUVRUWMHAFB
    KVHUWBUVQUWLBKAFGLVIOVEAFUVMKVHVJVKUVJUWEUWAUVJUWEAGUVMJIZDZUWAUVJUWEUAAUWD
    UXBUVJUWEVLUWCGUVMJUWQUWCGMUIGVMUKZVRZVNUVJUXCAGUVMGLIZJIZDZAUVMMZUBZUWAUVJ
    UVMGUDUEZDZUXCUXJUVJUVMVOUXKBVPZVQVNUXLUXCUXJAGUVMVSVTWAUVJUXHUWAUXIUVJUXHA
    GBJIZDZUWAUVJUXHUXOUVJUXGUXNAUVJUXFBGJUVJBSDZUWQUXFBMZBWBZUIBGWCZWDZOWEVTUV
    JUXOUAZUVTUVPUVMUVMALIZPIQIZUVPUYBPIZUYBQIZUYDAQIZHIZUVSUVJUXOAFBJIZDZUVTUY
    CMUVJUXNUYHWJZUXOUYIGUWJDUVJUYJGCUWJWFUGWGGFBCWHWIUXNUYHAWKWAZABCXDWLUYAUYC
    UYDUVMQIZUYDUYBAHIZQIZUYGUYAUYCUVMUYDQIZUYLUYAUVPSDZUVMSDZUYBSDZUYBFWMZUYCU
    YOMUYAUVPWNDZUYPUVJUXOUYIUYTUYKABCWOWLZUVPWPNZUYAUVMVODZUYQUVJVUCUXOUXMRUVM
    WQNZUYAUYBVODZUYRUYAUYBBALIZGHIZVOUYAUXPUWQASDZUYBVUGMUVJUXPUXOUXRRZUWQUYAU
    IWRZUYAUVKVUHUXOUVKUVJAGBWSZWTZAXANZBGAXBTZUYAVUFCDZVUGVODUYAABXCUOZVUOUXOV
    UPUVJAGBXEWTUYAUVKBEDZVUPVUOULVULUVJVUQUXOBXFRABXGXHXIVUFVPNXJZUYBWQNZUYAVU
    EUYSVURUYBXKNZUVPUVMUYBXLXMUYAUYQUYDSDZUYOUYLMVUDUYAUYDWNDZVVAUYAUYTUYBWNDZ
    VVBVUAUYAVUEVVCVURUYBXNNZUVPUYBXOXHUYDWPNZUVMUYDXPXHXQUYAUYMUVMUYDQUYAUYQVU
    HUYMUVMMVUDVUMUVMAYEXHOUYAVVAUYRVUHUYNUYGMVVEVUSVUMUYDUYBAYFTXRUYAUYEUVPUYF
    UVRHUYAUYPUYRUYSUYEUVPMVUBVUSVUTUVPUYBXSTUYAUVPUYBAPIZPIZUVPAQIUYBPIZUVRUYF
    UYAUYPUYRUYSVUHAFWMZVVGVVHMVUBVUSVUTVUMUYAAVODZVVIUXOVVJUVJABXTWTZAXKNUVPUY
    BAYAYBUYAVVGUVRMZUVPUVRVVFQIZMZUYAUVPUVRBUVQLIZAPIZQIVVMABCYQUYAVVPVVFUVRQU
    YAVVOUYBAPUYAVVOVUGUYBUYAUXPVUHUWQVVOVUGMVUIVUMVUJBAGYCTVUNYDYGOXQUYAUYPUVR
    SDZVVFSDZVVFFWMZVVLVVNULVUBUYAUVRWNDZVVQUVJUXOUVQUYHDZVVTUYAUVQFUXFJIZUYHUY
    AUXCUVQVWBDZCAGBYHUXOUVKUVMEDZUXCVWCULZUVJVUKUVJVUCVWDUXMUVMYINZAUVMYJZYKXI
    UVJVWBUYHMUXOUVJUXFBFJUXTORYLUVQBCWOWLUVRWPNUYAVVFWNDZVVRUYAVVCAWNDZVWHVVDU
    YAVVJVWIVVKAXNNUYBAXOXHZVVFWPNUYAVWHVVSVWJVVFUUANUVPUVRVVFUUBXMUUIUYAUYPVUH
    UYRUYSVVHUYFMVUBVUMVUSVUTUVPAUYBUUCXMUUDVEUUEWLUVJUXIUAZUWCGUVSUVTUXDVWKUVP
    FUVRGHUXIUVJUVPBUVMKIZFAUVMBKVHUVJVWDUVMFUNUOZBUVMUNUOZUBVWLFMVWFUVJVWNVWMU
    VJBUTDVWNBUUFBUUGNUUHUVMBVGUUJYMVWKUVRBBKIZGVWKUVQBBKUXIUVJUVQUXFBAUVMGLVIU
    XTYMOUVJVWOGMUXIBYNRXQVEUXIUVJUVTUVMUVMKIZGAUVMUVMKVHUVJUWGVWPGMUWIUVMYNNYM
    VFUUKWLWLUULUUMUUNYOUUOUVLUVOYPZUAZFFHIFUVSUVTUUTVWRUVPFUVRFHVWRUVJUVKUYIYP
    ZUVPFMUVJUVKVWQUUPZUVJUVKVWQUUQZUVLVWQVWSUVLUYIUVOUVLUYHUVNAUVJUYHUVNWJUVKF
    BCUURRUVAYRYOABYSTVWRUVJUVQEDZVWAYPZUVRFMVWTVWRUVKVXBVXAAUUSNUVLVWQVXCUVLVW
    AUVOUVLVWAVWCUVOUVLVWBUYHUVQUVLUXFBFJUVLUXPUWQUXQUVJUXPUVKUXRRUIUXSWDOWEUVL
    VWCUXCUVOUVKUVKVWDVWEUVJUVKUVBUVJUWGVWDUWIUVMXFNVWGYKUXBUVNAUXBUWDUVNUXEUXA
    UVMYTDUWDUVNWJUPBGHUVCFUVMYTUVDUSUVEUVFUVGUVHYRYOUVQBYSTVEVWRUWGUVKVWQUVTFM
    VWRUVJUWGVWTUWINVXAUVLVWQVLAUVMYSTVFUVI $.
    $( [10-Mar-2014] $) $( [13-Jul-2005] $)

  ${
    $d k m n N $.  $d k m n K $.
    $( A binomial coefficient, in its extended domain, is a nonnegative
       integer. $)
    bccl $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) e. NN0 ) $=
      ( vk vm cn0 wcel cv cbc co cz wral cc0 c1 wceq oveq1 eleq1d ralbidv oveq2
      wa 0nn0 vn caddc cfz elfz1eq adantl bcn0 ax-mp 1nn0 eqeltri mpbiri syl wn
      bcval3 mp3an1 syl6eqel pm2.61dan cbvralv bcpasc adantlr rcla4cva peano2zm
      rgen cmin sylan2 nn0addcl syl2anc adantll eqeltrrd ralrimiva syl5bi sylan
      ex nn0ind ) BEFBCGZHIZEFZCJKZAJFBAHIZEFZDGZVNHIZEFZCJKLVNHIZEFZCJKUAGZVNH
      IZEFZCJKZWEMUBIZVNHIZEFZCJKZVQDUABVTLNZWBWDCJWMWAWCEVTLVNHOPQVTWENZWBWGCJ
      WNWAWFEVTWEVNHOPQVTWINZWBWKCJWOWAWJEVTWIVNHOPQVTBNZWBVPCJWPWAVOEVTBVNHOPQ
      WDCJVNJFZVNLLUCIFZWDWQWRSVNLNZWDWRWSWQVNLUDUEWSWDLLHIZEFWTMELEFZWTMNTLUFU
      GUHUIWSWCWTEVNLLHRPUJUKWQWRULZSWCLEXAWQXBWCLNTVNLUMUNTUOUPVBWHWEVTHIZEFZD
      JKZWEEFZWLWGXDCDJVNVTNWFXCEVNVTWEHRPUQXFXEWLXFXESZWKCJXGWQSWFWEVNMVCIZHIZ
      UBIZWJEXFWQXJWJNXEVNWEURUSXEWQXJEFZXFXEWQSWGXIEFZXKXDWGDVNJVTVNNXCWFEVTVN
      WEHRPUTWQXEXHJFXLVNVAXDXLDXHJVTXHNXCXIEVTXHWEHRPUTVDWFXIVEVFVGVHVIVLVJVMV
      PVSCAJVNANVOVREVNABHRPUTVK $.
      $( [9-Nov-2013] $) $( [10-Jul-2005] $)

    $( A binomial coefficient, in its standard domain, is a natural number.
       (Revised by Mario Carneiro, 10-Mar-2014.) $)
    bccl2 $p |- ( ( N e. V /\ K e. ( 0 ... N ) ) -> ( N _C K ) e. NN ) $=
      ( wcel cc0 cfz co wa cbc cn0 clt wbr elfz3nn0 elfzelz adantl bccl syl2anc
      cn cz crp bcrpcl rpgt0 syl elnnnn0b sylanbrc ) BCDZAEBFGDZHZBAIGZJDZEUIKL
      ZUIRDUHBJDASDZUJCABMUGULUFAEBNOABPQUHUITDUKABCUAUIUBUCUIUDUE $.
      $( [10-Mar-2014] $) $( [3-Jan-2006] $)
  $}

  $( The number of permutations of ` N - R ` objects from a collection of ` N `
     objects is a natural number.  (Contributed by Jason Orendorff,
     24-Jan-2007.) $)
  permnn $p |- ( ( N e. NN0 /\ R e. ( 0 ... N ) ) ->
               ( ( ! ` N ) / ( ! ` R ) ) e. NN ) $=
    ( cn0 wcel cc0 cfz co wa cfa cfv cn cmin cmul cdiv elfznn0 faccl syl adantl
    cc nncn fznn0sub nnmulcl syl2anc adantr wceq facne0 divcan4 syl3anc eqeltrd
    wne cbc bcval2 bccl2 eqeltrrd nndivtr syl32anc ) BCDZAEBFGDZHZAIJZKDZBALGZI
    JZUTMGZKDZBIJZSDZVDUTNGZKDVFVDNGZKDVFUTNGKDURVAUQURACDZVAABOZAPQZRZUSVCKDZV
    AVEUSVBCDVNCAEBUAVBPQZVMVCUTUBUCUQVGURUQVFKDVGBPVFTQUDUSVHVCKUSVCSDZUTSDZUT
    EUJZVHVCUEUSVNVPVOVCTQURVQUQURVAVQVLUTTQRURVRUQURVJVRVKAUFQRVCUTUGUHVOUIUSB
    AUKGVIKABCULABCUMUNUTVDVFUOUP $.
    $( [24-Jan-2007] $)

