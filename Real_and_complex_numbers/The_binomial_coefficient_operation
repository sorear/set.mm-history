$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The binomial coefficient operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c _C $. $( Binomial coefficient operation. $)

  $( Extend class notation to include the binomial coefficient operation
     (combinatorial choose operation). $)
  cbc $a class _C $.

  ${
    $d n k m $.
    $( Define the binomial coefficient operation.  In the literature, this
       function is often written as a column vector of the two arguments, or
       with the arguments as subscripts before and after the letter "C".
       ` ( N _C K ) ` is read " ` N ` choose ` K ` ."  Definition of binomial
       coefficient in [Gleason] p. 295.  As suggested by Gleason, we define it
       to be 0 when ` 0 <_ k <_ n ` does not hold. $)
    df-bc $a |- _C = { <. <. n , k >. , m >. | ( ( n e. NN0 /\ k e. ZZ ) /\
                m = if ( ( 0 <_ k /\ k <_ n ) ,
                ( ( ! ` n ) / ( ( ! ` ( n - k ) ) x. ( ! ` k ) ) ) , 0 ) ) } $.
  $}

  ${
    $d n k m N $.  $d n k m K $.
    $( Value of the binomial coefficient, ` N ` choose ` K ` .  Definition of
       binomial coefficient in [Gleason] p. 295.  As suggested by Gleason, we
       define it to be 0 when ` 0 <_ K <_ N ` does not hold.  See ~ bcval2 for
       the value in the standard domain. $)
    bcval $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) = if ( ( 0 <_ K /\
    K <_ N ) , ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) , 0 ) ) $=
      ( vn vk vm cn0 cz cc0 cv cle wbr wa cfa cfv cmin co cmul cdiv cif cbc
      oprex 0nn0 elisseti ifex wceq breq2 anbi2d ifbid fveq2 opreq1 fveq2d
      opreq1d opreq12d ifeq1d eqtrd breq1 anbi12d opreq2 opreq2d df-bc
      oprabval2 ) CDEBAFGHDIZJKZVBCIZJKZLZVDMNZVDVBOPZMNZVBMNZQPZRPZHSZHAJKZABJ
      KZLZBMNZBAOPZMNZAMNZQPZRPZHSZTVCVBBJKZLZVQBVBOPZMNZVJQPZRPZHSZVPWBHVQWARU
      AHFUBUCUDVDBUEZVMWEVLHSWJWKVFWEVLHWKVEWDVCVDBVBJUFUGUHWKWEVLWIHWKVGVQVKWH
      RVDBMUIWKVIWGVJQWKVHWFMVDBVBOUJUKULUMUNUOVBAUEZWJVPWIHSWCWLWEVPWIHWLVCVNW
      DVOVBAHJUFVBABJUPUQUHWLVPWIWBHWLWHWAVQRWLWGVSVJVTQWLWFVRMVBABOURUKVBAMUIU
      MUSUNUODECUTVA $.
      $( [11-Jul-2005] $) $( [10-Jul-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , in its standard
       domain. $)
    bcval2 $p |- ( ( N e. NN0 /\ K e. NN0 /\ K <_ N ) -> ( N _C K ) =
                    ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) ) $=
      ( cn0 wcel cz wa cc0 cle wbr cbc co cfa cfv cmin cmul cdiv wceq w3a cif
      bcval iftrue sylan9eq nn0z anim2i 3adant3 nn0ge0 anim1i 3adant1 sylanc )
      BCDZAEDZFZGAHIZABHIZFZBAJKZBLMBANKLMALMOKPKZQUJACDZUNRULUOUPUOUQGSUQABTUO
      UQGUAUBUJURULUNURUKUJAUCUDUEURUNUOUJURUMUNAUFUGUHUI $.
      $( [28-Jul-2008] $) $( [9-Jun-2005] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , in its standard
       domain. $)
    bcval3 $p |- ( ( N e. NN0 /\ K e. ( 0 ... N ) ) -> ( N _C K ) =
                    ( ( ! ` N ) / ( ( ! ` ( N - K ) ) x. ( ! ` K ) ) ) ) $=
      ( cn0 wcel cle wbr cbc co cfa cfv cmin cmul cdiv wceq cc0 cfz wa bcval2
      elfz3nn0 elfznn0 adantl elfzle2 syl3anc ) BCDZACDZABEFZBAGHBIJBAKHIJAIJLH
      MHNUDAOBPHDZQABRCABSUGUEUDABTUAUGUFUDAOBUBUAUC $.
      $( [3-Feb-2006] $) $( [3-Jan-2006] $)

    $( Value of the binomial coefficient, ` N ` choose ` K ` , outside of its
       standard domain.  Remark in [Gleason] p. 295. $)
    bcval4 $p |- ( ( N e. NN0 /\ K e. ZZ /\ ( K < 0 \/ N < K ) ) ->
          ( N _C K ) = 0 ) $=
      ( cn0 wcel cz cc0 clt wbr wo cbc co wceq wa cle wn cr wb 0re ltnle mpan2
      adantl orbi12d nn0re zre syl2an biimpa cfa cfv cmin cmul cdiv cif bcval
      iffalse sylan9eq ianor sylan2br syldan 3impa ) BCDZAEDZAFGHZBAGHZIZBAJKZF
      LZUTVAMZVDFANHZOZABNHZOZIZVFVGVDVLBPDZAPDZVDVLQUTVAVMVNMVBVIVCVKVNVBVIQZV
      MVNFPDVORAFSTUABASUBBUCAUDUEUFVGVHVJMZOZVFVLVGVQVEVPBUGUHBAUIKUGUHAUGUHUJ
      KUKKZFULFABUMVPVRFUNUOVHVJUPUQURUS $.
      $( [9-Sep-2005] $) $( [14-Jul-2005] $)
  $}

  $( "Complementing" its second argument doesn't change a binary
     coefficient. $)
  bccmpl $p |- ( ( N e. NN0 /\ K e. NN0 /\ K <_ N ) -> ( N _C K ) =
                    ( N _C ( N - K ) ) ) $=
    ( cn0 wcel cle wbr w3a cfa cfv cmin co cmul cdiv cbc cc wceq mulcom cn wb
    nn0sub ancoms biimp3a faccl nncn 3syl syl 3ad2ant2 sylanc wa nncan nn0cn
    syl2an fveq2d 3adant3 opreq1d eqtr4d opreq2d bcval2 3simp1 cr caddc
    nn0addge1 lesubadd 3anidm13 nn0re sylan2 mpbird sylan syl3anc 3eqtr4d ) BCD
    ZACDZABEFZGZBHIZBAJKZHIZAHIZLKZMKVOBVPJKZHIZVQLKZMKZBANKBVPNKZVNVSWBVOMVNVS
    VRVQLKZWBVQODZVRODZVSWEPVNVQVRQVNVPCDZVQRDWFVKVLVMWHVLVKVMWHSABTUAUBZVPUCVQ
    UDUEVLVKWGVMVLVRRDWGAUCVRUDUFUGUHVNWAVRVQLVKVLWAVRPVMVKVLUIVTAHBODAODVTAPVK
    VLBAUJBUKAUKULUMUNUOUPUQABURVKWHVPBEFZWDWCPVNVPBURVKVLVMUSWIVKVLWJVMBUTDZVL
    WJVKWKVLUIWJBBAVAKEFZBAVBWKAUTDZWJWLSZVLWKWMWNBABVCVDAVEVFVGBVEVHUNVIVJ $.
    $( [9-Jul-2005] $) $( [21-Jun-2005] $)

  $( ` N ` choose 0 is 1.  Remark in [Gleason] p. 296. $)
  bcn0 $p |- ( N e. NN0 -> ( N _C 0 ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cfa cfv cmin cmul cdiv c1 cle wbr wceq nn0ge0 0nn0
    bcval2 mp3an2 mpdan cc nn0cn subid1 syl fveq2d fac0 a1i opreq12d cn faccl
    nncn ax1id eqtrd opreq2d wne divid facne0 sylanc 3eqtrd ) ABCZADEFZAGHZADIF
    ZGHZDGHZJFZKFZVBVBKFZLUTDAMNZVAVGOZAPUTDBCVIVJQDARSTUTVFVBVBKUTVFVBLJFZVBUT
    VDVBVELJUTVCAGUTAUACVCAOAUBAUCUDUEVELOUTUFUGUHUTVBUACZVKVBOUTVBUICVLAUJVBUK
    UDZVBULUDUMUNVLVBDUOVHLOUTVBUPVMAUQURUS $.
    $( [18-Jun-2005] $) $( [17-Jun-2005] $)

  $( ` N ` choose ` N ` is 1.  Remark in [Gleason] p. 296. $)
  bcnn $p |- ( N e. NN0 -> ( N _C N ) = 1 ) $=
    ( cn0 wcel cc0 cbc co cmin c1 cle wbr wceq nn0ge0 0nn0 bccmpl mp3an2 mpdan
    bcn0 cc nn0cn subid1 syl opreq2d 3eqtr3rd ) ABCZADEFZAADGFZEFZHAAEFUDDAIJZU
    EUGKZALUDDBCUHUIMDANOPAQUDUFAAEUDARCUFAKASATUAUBUC $.
    $( [2-Aug-2005] $) $( [17-Jun-2005] $)

  $( Binomial coefficient: ` N + 1 ` choose ` 1 ` . $)
  bcnp11 $p |- ( N e. NN0 -> ( ( N + 1 ) _C 1 ) = ( N + 1 ) ) $=
    ( cn0 wcel c1 caddc co cbc cfa cfv cmin cmul cdiv cle wbr wceq 1nn0 bcval2
    mp3an2 peano2nn0 cr 1re nn0addge2 mpan sylanc cc pncan nn0cn ax1cn sylancl
    fveq2d fac1 a1i opreq12d cn faccl nncn syl ax1id eqtrd opreq2d facp1
    eqcomd cc0 wne wa wb divmul facne0 jca syl3anc mpbird 3eqtrd ) ABCZADEFZDGF
    ZVNHIZVNDJFZHIZDHIZKFZLFZVPAHIZLFZVNVNBCZDVNMNZVOWAOZVMWDDBCWEWFPDVNQRASZDT
    CVMWEUADAUBUCUDVMVTWBVPLVMVTWBDKFZWBVMVRWBVSDKVMVQAHAUECDUECVQAOVMADUFAUGUH
    UIUJVSDOVMUKULUMVMWBUECZWHWBOVMWBUNCWIAUOWBUPUQZWBURUQUSUTVMWCVNOZWBVNKFZVP
    OZVMVPWLAVAVBVPUECZVNUECZWIWBVCVDZVEWKWMVFVMVPVNWBVGVMVPUNCZWNVMWDWQWGVNUOU
    QVPUPUQVMWDWOWGVNUGUQVMWIWPWJAVHVIVJVKVL $.
    $( [8-Aug-2005] $) $( [21-Jun-2005] $)

  $( Binomial coefficient: ` N + 1 ` choose ` N ` . $)
  bcnp1n $p |- ( N e. NN0 -> ( ( N + 1 ) _C N ) = ( N + 1 ) ) $=
    ( cn0 wcel c1 caddc co cbc cmin cle wbr wceq 1nn0 bccmpl mp3an2 peano2nn0
    cr 1re nn0addge2 mpan sylanc bcnp11 cc pncan nn0cn ax1cn sylancl opreq2d
    3eqtr3rd ) ABCZADEFZDGFZUJUJDHFZGFZUJUJAGFUJBCZDUJIJZUKUMKZUIUNDBCUOUPLDUJM
    NAODPCUIUOQDARSTAUAUIULAUJGAUBCDUBCULAKUIADUCAUDUEUFUGUH $.
    $( [25-Nov-2005] $) $( [20-Jun-2005] $)

  ${
    bcpasc2.1 $e |- N e. NN $.
    bcpasc2.2 $e |- K e. NN $.
    bcpasc2.3 $e |- K <_ N $.
    $( Pascal's rule for the binomial coefficient.  Equation 2 of [Gleason]
       p. 295. $)
    bcpasc2i $p |- ( ( N _C K ) + ( N _C ( K - 1 ) ) ) = ( ( N + 1 ) _C K ) $=
      ( cfa cfv c1 cmin co cmul cdiv caddc cbc ax1cn nncni subcli addcli
      nnne0i cn0 wcel cn cle wbr wb nnnn0i nn0sub mp2an mpbi nn0p1nn ax-mp
      divadddivi mulid2i mulid1i opreq12i add23i cc wceq npcan opreq1i 3eqtri
      eqtri opreq2i faccl nnge1 1nn0 nnmulcl divcli reccli adddii nn0addcli
      nn0cni mulcli mulne0i divmuldivi facp1 nnrei nn0rei clt nnleltp1 ltleii
      mulcomi facnn2 addsubi fveq2i mul4i eqtr4i 3eqtr3i mul23i 3eqtr2i bcval2
      mp3an 3eqtr4ri mulassi subsub eqtr2i 1re lesubaddi mpbir 3eqtr4i ) BFGZAH
      IJZFGZBAIJZFGZKJZLJZHALJZKJZXGHXDHMJZLJZKJZMJZBHMJZFGZXNAIJZFGZAFGZKJZLJZ
      BANJZBXBNJZMJXNANJZXGXHXKMJZKJXGXNAXJKJZLJZKJZXMXTYDYFXGKYDHXJKJZAHKJZMJZ
      YELJYFHAHXJOADPZOXDHBABCPZYKQZORZADSZXJXDTUAZXJUBUAABUCUDZYPEATUAZBTUAZYQ
      YPUEADUFZBCUFZABUGUHUIZXDUJUKSZULYJXNYELYJXJAMJXDAMJZHMJXNYHXJYIAMXJYNUMA
      YKUNUOXDHAYMOYKUPUUDBHMBUQUAZAUQUAZUUDBURYLYKBAUSUHUTVAUTVBVCXGXHXKXAXFXA
      YSXAUBUAUUABVDUKPZXFXCUBUAZXEUBUAZXFUBUAXBTUAZUUHHAUCUDZUUJAUBUAZUUKDAVEU
      KHTUAYRUUKUUJUEVFYTHAUGUHUIZXBVDUKZYPUUIUUBXDVDUKZXCXEVGUHZPZXFUUPSZVHAYK
      YOVIXJYNUUCVIVJYGXAXNKJZXFYEKJZLJXTXAXFXNYEUUGUUQXNBHUUAVFVKZVLAXJYKYNVMU
      URAXJYKYNYOUUCVNVOXOUUSXSUUTLYSXOUUSURUUABVPUKXSXRXQKJXCAKJZXEXJKJZKJUUTX
      QXRXQXPTUAZXQUBUAAXNUCUDZUVDAXNADVQZXNUVAVRYQAXNVSUDZEUULBUBUAYQUVGUEDCAB
      VTUHUIWAZYRXNTUAZUVEUVDUEYTUVAAXNUGUHUIXPVDUKPXRYRXRUBUAYTAVDUKPZWBXRUVBX
      QUVCKUULXRUVBURDAWCUKZXQXJFGZUVCXPXJFBHAYLOYKWDWEYPUVLUVCURUUBXDVPUKZVBUO
      XCAXEXJXCUUNPZYKXEUUOPZYNWFVAUOWGWHYAXIYBXLMXAHKJZXFAKJZLJXAXEXRKJZLJZXIY
      AUVPXAUVQUVRLXAUUGUNZUVQUVBXEKJXRXEKJUVRXCXEAUVNUVOYKWIXRUVBXEKUVKUTXRXEU
      VJUVOWBWJUOXAXFHAUUGUUQOYKUURYOVOYSYRYQYAUVSURUUAYTEABWKWLWMUVPXFXJKJZLJX
      ABXBIJZFGZXCKJZLJZXLYBUVPXAUWAUWDLUVTUWAXCUVCKJXCUWCKJUWDXCXEXJUVNUVOYNWN
      UVCUWCXCKUWCUVLUVCUWBXJFUUEUUFHUQUAUWBXJURYLYKOBAHWOWLWEUVMWPVCXCUWCUVNUW
      CUWBTUAZUWCUBUAXBBUCUDZUWFUWGUVEUVHAHBUVFWQBCVQWRWSZUUJYSUWGUWFUEUUMUUAXB
      BUGUHUIUWBVDUKPWBVAUOXAXFHXJUUGUUQOYNUURUUCVOYSUUJUWGYBUWEURUUAUUMUWHXBBW
      KWLWMUOUVIYRUVEYCXTURUVAYTUVHAXNWKWLWT $.
      $( [23-Jun-2005] $) $( [19-Jun-2005] $)
  $}

  $( Pascal's rule for the binomial coefficient.  Equation 2 of [Gleason]
     p. 295. $)
  bcpasc2 $p |- ( ( N e. NN /\ K e. NN /\ K <_ N ) ->
                ( ( N _C K ) + ( N _C ( K - 1 ) ) ) = ( ( N + 1 ) _C K ) ) $=
    ( cn wcel cle wbr w3a cbc co c1 cmin caddc wceq cif opreq1 opreq12d
    opreq1d eqeq12d opreq2 opreq2d eleq1 breq2 3anbi13d breq1 3anbi23d 1nn 1re
    leidi 3pm3.2i elimhyp2v 3simp1i 3simp2i 3simp3i bcpasc2i dedth2v ) BCDZACDZ
    ABEFZGZBAHIZBAJKIZHIZLIZBJLIZAHIZMUSBJNZAHIZVFVAHIZLIZVFJLIZAHIZMVFUSAJNZHI
    ZVFVLJKIZHIZLIZVJVLHIZMBAJJBVFMZVCVIVEVKVRUTVGVBVHLBVFAHOBVFVAHOPVRVDVJAHBV
    FJLOQRAVLMZVIVPVKVQVSVGVMVHVOLAVLVFHSVSVAVNVFHAVLJKOTPAVLVJHSRVLVFVFCDZVLCD
    ZVLVFEFZUSVTUQAVFEFZGVTWAWBGJCDZWDJJEFZGVTWDJVFEFZGBAJJVRUPVTURWCUQBVFCUABV
    FAEUBUCVSUQWAWCWBVTAVLCUAAVLVFEUDUEJVFMWDVTWEWFWDJVFCUAJVFJEUBUCJVLMWDWAWFW
    BVTJVLCUAJVLVFEUDUEWDWDWEUFUFJUGUHUIUJZUKVTWAWBWGULVTWAWBWGUMUNUO $.
    $( [9-Sep-2005] $) $( [20-Jun-2005] $)

  ${
    bcpasc.1 $e |- N e. NN0 $.
    bcpasc.2 $e |- K e. ZZ $.
    $( Pascal's rule for the binomial coefficient, generalized to all integers
       ` K ` .  Equation 2 of [Gleason] p. 295. $)
    bcpasci $p |- ( ( N _C K ) + ( N _C ( K - 1 ) ) ) = ( ( N + 1 ) _C K ) $=
      ( cn wcel cle wbr cbc co c1 cmin caddc wceq wn cc0 cn0 wo elnn0 mpbi ori
      ax1cn addid1i opreq2 0nn0 bcn0 ax-mp syl6eq cneg opreq1 df-neg syl6eqr
      opreq2d cz clt 1z znegcl lt01 cr wb 1re lt0neg2 orci bcval4 mp3an
      opreq12d 1nn0 3eqtr4a wne zrei 0re lttri2i 0cn mp3an12 orcs leidi
      zlem1lt mp2an resubcli lttri mpan orc zsubcl 3syl olcs opreq1d 0z
      zltp1le addid2i breq1i leloei 3bitri posdifi olc sylbi syl eqcomd
      syl5eqr eqtr2d subidi bcnn eqtr3d jaoi eqtrd pm2.61ine znnnlt1 zleltp1
      breq2i 3bitr3ri bitri nn0addcli eqbrtrri syl6eqbr syl6req nn0rei ltnlei
      nn0z readdcli ltaddsubi cc nn0cni pncan sylbir bcpasc2 3ecase ) BEFZAEFZA
      BGHZBAIJZBAKLJZIJZMJZBKMJZAIJZNZYFOBPNZYOYFYPBQFZYFYPRCBSTUAYPPAIJZPYJIJZ
      MJZKAIJZYLYNYTUUANZAPAPNZKPMJZKYTUUAKUBUCZUUCYRKYSPMUUCYRPPIJZKAPPIUDPQFZ
      UUFKNUEPUFUGZUHUUCYSPKUIZIJZPUUCYJUUIPIUUCYJPKLJZUUIAPKLUJZKUKZULUMUUGUUI
      UNFZUUIPUOHZPUUIUOHZRUUJPNUEKUNFZUUNUPKUQUGUUOUUPPKUOHZUUOURKUSFUURUUOUTV
      AKVBUGTZVCUUIPVDVEUHVFUUCUUAKPIJZKAPKIUDKQFZUUTKNVGKUFUGUHVHAPVIAPUOHZPAU
      OHZRZUUBAPADVJZVKVLUVBUUBUVCUVBPPMJZPYTUUAPVMUCZUVBYRPYSPMUVBUVCYRPNZUUGA
      UNFZUVDUVHUEDAPVDVNZVOUVBYJPUOHZUVKPYJUOHZRZYSPNZYJAUOHZUVBUVKAAGHZUVOAUV
      EVPUVIUVIUVPUVOUTDDAAVQVRTYJAPAKUVEVAVSUVEVKVTWAZUVKUVLWBUUGYJUNFZUVMUVNU
      EUVIUUQUVRDUPAKWCVRZYJPVDVNZWDVFUVBKAUOHZUUAPNZUVAUVIUVBUWARUWBVGDAKVDVNZ
      VOVHUVCYTPYSMJZUUAUVCYRPYSMUVBUVCUVHUVJWEWFUVCUWAKANZRZUWDUUANZUVCPKMJZAG
      HZKAGHUWFPUNFZUVIUVCUWIUTWGDPAWHVRUWHKAGKUBWIZWJKAVAUVEWKWLUWAUWGUWEUWAUU
      APUWDUVBUWAUWBUWCWEUWAUVFUWDPUWAPYSPMUWAYSPUWAUVMUVNUWAUVLUVMKAVAUVEWMUVL
      UVKWNWOUVTWPWQUMUVGWRWSUWEKUWDUUAUWEUWHUWDKUWEKYSPMUWEUUFYSKUWEPYJPIUWEKK
      LJYJPKAKLUJKUBWTWRUMUUHWRUMUWKWRUWEKKIJZUUAKKAKIUDUVAUWLKNVGKXAUGWRXBXCWO
      XDXCWOXEYPYIYRYKYSMBPAIUJBPYJIUJVFYPYMKAIYPYMUWHKBPKMUJUWKUHWFVHWPYGOZUVB
      UUCRZYOUWMAKUOHZUWNUVIUWMUWOUTDAXFUGAPGHZAUWHUOHZUWNUWOUVIUWJUWPUWQUTDWGA
      PXGVRAPUVEVKWKUWHKAUOUWKXHXIXJUVBYOUUCUVBUVFPYLYNUVGUVBYIPYKPMUVBBAUOHZYI
      PNZYQUVIUVBUWRRUWSCDABVDVNZVOUVBUVKUVKBYJUOHZRZYKPNZUVQUVKUXAWBZYQUVRUXBU
      XCCUVSYJBVDVNZWDVFUVBYMAUOHZYNPNZYMQFZUVIUVBUXFRUXGBKCVGXKZDAYMVDVNZVOVHU
      UCYLKYNUUCYLUUDKUUCYIKYKPMUUCYIBPIJZKAPBIUDYQUXKKNCBUFUGUHUUCUVKUXBUXCUUC
      YJUUKPUOUULUUIUUKPUOUUMUUSXLXMUXDUXEWDVFUUEUHUUCYNYMPIJZKAPYMIUDUXHUXLKNU
      XIYMUFUGXNXDXCWOYHOUWRYOBABCXOZUVEXPUWRYLPYKMJZYNUWRYIPYKMUVBUWRUWSUWTWEW
      FUWRUXFYMANZRZUXNYNNZUWRYMAGHZUXPBUNFZUVIUWRUXRUTYQUXSCBXQUGDBAWHVRYMABKU
      XMVAXRUVEWKXJUXFUXQUXOUXFUVFPUXNYNUVGUXFYKPPMUXFUXBUXCUXFUXAUXBBKAUXMVAUV
      EXSUXAUVKWNWOUXEWPUMUVBUXFUXGUXJWEVHUXOKUXNYNUXOUWHUXNKUXOKYKPMUXOBBIJZYK
      KUXOBYJBIUXOYMKLJZYJBYMAKLUJBXTFKXTFUYABNBCYAUBBKYBVRWRUMYQUXTKNCBXAUGWRU
      MUWKWRUXOYMYMIJZYNKYMAYMIUDUXHUYBKNUXIYMXAUGWRXBXCWOXDYCABYDYE $.
      $( [21-Sep-2005] $) $( [13-Jul-2005] $)
  $}

  $( Pascal's rule for the binomial coefficient, generalized to all integers
     ` K ` .  Equation 2 of [Gleason] p. 295. $)
  bcpasc $p |- ( ( N e. NN0 /\ K e. ZZ ) ->
                ( ( N _C K ) + ( N _C ( K - 1 ) ) ) = ( ( N + 1 ) _C K ) ) $=
    ( cn0 wcel cz cbc co c1 cmin caddc wceq cif opreq1 opreq12d opreq1d
    eqeq12d opreq2 opreq2d 1nn0 elimel 1z bcpasci dedth2h ) BCDZAEDZBAFGZBAHIGZ
    FGZJGZBHJGZAFGZKUDBHLZAFGZULUGFGZJGZULHJGZAFGZKULUEAHLZFGZULURHIGZFGZJGZUPU
    RFGZKBAHHBULKZUIUOUKUQVDUFUMUHUNJBULAFMBULUGFMNVDUJUPAFBULHJMOPAURKZUOVBUQV
    CVEUMUSUNVAJAURULFQVEUGUTULFAURHIMRNAURUPFQPURULBHCSTAHEUATUBUC $.
    $( [31-Jan-2006] $) $( [13-Jul-2005] $)

  ${
    $d k n N $.  $d n j k $.  $d k K $.
    $( A binomial coefficient, in its standard domain, is a natural number. $)
    bccl2 $p |- ( ( N e. NN0 /\ K e. ( 0 ... N ) ) -> ( N _C K ) e. NN ) $=
      ( vk vn vj cn0 wcel cle wbr cbc co cn cc0 cfz wa wceq wo wi cv wral
      breq1 opreq2 eleq1d imbi12d rcla4v c1 caddc breq2 opreq1 ralbidv
      nnle1eq1 1nn0 bcnn ax-mp 1nn eqeltri syl6eqel syl6bi rgen ax-17 hbra1
      clt cr wb leloe nnre peano2nn syl syl2an nnleltp1 orbi1d bitr4d nnge1
      1re sylancr mpbid adantr cmin nnaddcl ra4 imp32 ancoms adantllr adantlrl
      cz nnz peano2zm nnltlem1 mpan biimpd anim2d elnnz1 syl6ibr mpand cbvralv
      syl5ib com23 syl6 lep1 lesubadd mp3an2 anidms mpbird letr 3expa
      peano2rem ancri sylan syl5d imp42 sylanc bcpasc2 3com12 adantrl exp31
      com12 nnnn0 bcnp11 eqeltrd syl5bi adantld a1dd adantrd ccase2 sylbid ex
      com4t r19.21ad nnind syl5com imp wn notbid nngt0 ltnle 0re syl5bir
      pm2.21d bcn0 adantl a1d opreq12 0nn0 ccase elnn0 syl2anb 3impia elfz3nn0
      elfznn0 elfzle2 syl3anc ) BFGZAFGZABHIZBAJKZLGZUULAMBNKGZOUULUUMUUNUUPBLG
      ZBMPZQALGZAMPZQUUNUUPRZUULUUMUURUUTUUSUVAUVBUURUUTUVBUUTCSZBHIZBUVCJKZLGZ
      RZCLTZUVBUURUVGUVBCALUVCAPZUVDUUNUVFUUPUVCABHUAUVIUVEUUOLUVCABJUBUCUDUEUV
      CDSZHIZUVJUVCJKZLGZRZCLTUVCUFHIZUFUVCJKZLGZRZCLTUVCESZHIZUVSUVCJKZLGZRZCL
      TZUVCUVSUFUGKZHIZUWEUVCJKZLGZRZCLTUVHDEBUVJUFPZUVNUVRCLUWJUVKUVOUVMUVQUVJ
      UFUVCHUHUWJUVLUVPLUVJUFUVCJUIUCUDUJUVJUVSPZUVNUWCCLUWKUVKUVTUVMUWBUVJUVSU
      VCHUHUWKUVLUWALUVJUVSUVCJUIUCUDUJUVJUWEPZUVNUWICLUWLUVKUWFUVMUWHUVJUWEUVC
      HUHUWLUVLUWGLUVJUWEUVCJUIUCUDUJUVJBPZUVNUVGCLUWMUVKUVDUVMUVFUVJBUVCHUHUWM
      UVLUVELUVJBUVCJUIUCUDUJUVRCLUVCLGZUVOUVCUFPZUVQUVCUKUWOUVPUFUFJKZLUVCUFUF
      JUBUWPUFLUFFGUWPUFPULUFUMUNUOUPUQURUSUVSLGZUWDUWICLUWQCUTUWCCLVAUWNUWFUWQ
      UWDUWHUWNUWQUWFUWDUWHRZUWNUWQUWFUWRRUWNUWQOZUWFUVTUVCUWEPZQZUWRUWSUWFUVCU
      WEVBIZUWTQZUXAUVCVCGZUWEVCGZUWFUXCVDUWNUWQUVCUWEVEUVCVFZUWQUWELGUXEUVSVGZ
      UWEVFVHVIUWSUVTUXBUWTUVCUVSVJVKVLUWSUFUVCVBIZUFUVCPZQZUXAUWRUWNUXJUWQUWNU
      FUVCHIZUXJUVCVMUFVCGZUXDUXKUXJVDUWNUFUVCVEVNUXFVOVPVQUXJUXAOUWSUWRUXHUVTU
      XIUWTUWSUWRRUWSUXHUVTOZUWRUWSUXMUWDUWHUWSUXMOZUWDOZUWAUVSUVCUFVRKZJKZUGKZ
      LGZUWHUWBUXQLGZUXSUXOUWAUXQVSUWSUVTUWDUWBUXHUWNUVTUWDUWBUWQUWDUWNUVTOUWBU
      WDUWNUVTUWBUWCCLVTWAWBWCWDUWSUXHUVTUWDUXTUWSUXHUXPUVSHIZUWDUXTRZUVTUWNUXH
      UYAUYBRZRUWQUWNUXHUXPLGZUYCUWNUXPWEGZUXHUYDUWNUVCWEGUYEUVCWFUVCWGVHUWNUYE
      UXHOUYEUFUXPHIZOUYDUWNUXHUYFUYEUWNUXHUYFUFLGUWNUXHUYFVDUOUFUVCWHWIWJWKUXP
      WLWMWNUYDUWDUYAUXTUYDUVJUVSHIZUVSUVJJKZLGZRZDLTUYAUXTRZUWDUYJUYKDUXPLUVJU
      XPPZUYGUYAUYIUXTUVJUXPUVSHUAUYLUYHUXQLUVJUXPUVSJUBUCUDUEUWCUYJCDLUVCUVJPZ
      UVTUYGUWBUYIUVCUVJUVSHUAUYMUWAUYHLUVCUVJUVSJUBUCUDWOWPWQWRVQUXDUVSVCGZUVT
      UYARUWNUWQUXDUYNOUXPUVCHIZUVTUYAUXDUYOUYNUXDUYOUVCUVCUFUGKHIZUVCWSUXDUYOU
      YPVDZUXDUXLUXDUYQVNUVCUFUVCWTXAXBXCVQUXPVCGZUXDOUYNUYOUVTOUYARZUXDUYRUXDU
      YNUYSUXPUVCUVSXDXEUXDUYRUVCXFXGXHWNUXFUVSVFVIXIXJXKUXNUXSUWHVDZUWDUWSUVTU
      YTUXHUWSUVTOUXRUWGLUWNUWQUVTUXRUWGPZUWQUWNUVTVUAUVCUVSXLXMXEUCXNVQVPXOXPU
      XIUWSUWHUWDUXIUWQUWHUWNUXIUWEUFJKZLGUWHUWQUXIVUBUWGLUFUVCUWEJUBUCUWQVUBUW
      ELUWQUVSFGVUBUWEPUVSXQUVSXRVHUXGXSXTYAYBUWTUWSUWHUWDUWTUWNUWHUWQUWTUVCUVC
      JKZLGUWHUWNUWTVUCUWGLUVCUWEUVCJUIUCUWNVUCUFLUWNUVCFGVUCUFPUVCXQUVCUMVHUOU
      QXTYCYBYDXPWNYEYFWQYGYHYIYJYKUUSUUTOUUNUUPUUSUUTUUNYLZUUSVUDAMHIZYLZUUTUU
      SUUNVUEBMAHUHYMUUTMAVBIZVUFAYNMVCGAVCGVUGVUFVDUUTMAYOYPAVFVOVPYQYKYRUURUV
      AOZUUPUUNVUHUUPBMJKZLGZUURVUJUVAUURVUIUFLUURUULVUIUFPBXQBYSVHUOUQVQUVAUUP
      VUJVDUURUVAUUOVUILAMBJUBUCYTXCUUAUUSUVAOZUUPUUNVUKUUOMMJKZLBMAMJUUBVULUFL
      MFGVULUFPUUCMYSUNUOUPUQUUAUUDBUUEAUUEUUFUUGFABUUHUUQUUMUULABUUIYTUUQUUNUU
      LAMBUUJYTUUK $.
      $( [11-Apr-2006] $) $( [3-Jan-2006] $)
  $}

  $( A binomial coefficient, in its extended domain, is a nonnegative
     integer. $)
  bccl $p |- ( ( N e. NN0 /\ K e. ZZ ) -> ( N _C K ) e. NN0 ) $=
    ( cn0 wcel cz wa cc0 cle wbr cbc co cn cfz bccl2 simpll wi fznn0 biimprd
    exp3a elnn0z syl5ibr imp43 sylanc nnnn0 syl wn cfa cfv cmin cmul cdiv cif
    bcval iffalse sylan9eq 0nn0 syl6eqel pm2.61dan ) BCDZAEDZFZGAHIZABHIZFZBAJK
    ZCDZVAVDFZVELDZVFUSAGBMKDZVHVGABNUSUTVDOUSUTVBVCVIUSUTVBVCVIPZUSACDZVJUTVBF
    USVKVCVIUSVIVKVCFABQRSATUASUBUCVEUDUEVAVDUFZFVEGCVAVLVEVDBUGUHBAUIKUGUHAUGU
    HUJKUKKZGULGABUMVDVMGUNUOUPUQUR $.
    $( [22-Sep-2005] $) $( [10-Jul-2005] $)

  $( The number of permutations of ` N - R ` objects from a collection of ` N `
     objects is a natural number.  (Contributed by Jason Orendorff,
     24-Jan-2007.) $)
  permnn $p |- ( ( N e. NN0 /\ R e. ( 0 ... N ) ) ->
               ( ( ! ` N ) / ( ! ` R ) ) e. NN ) $=
    ( cfa cfv cn wcel cmin co cmul cc w3a cdiv wa cn0 cc0 cfz nndivtr elfznn0
    faccl syl adantl nnmulcl fznn0sub sylanc nncn adantr 3jca wne wceq divcan4
    facne0 syl3anc eqeltrd cbc bcval3 bccl2 eqeltrrd jca ) ACDZEFZBAGHZCDZUSIHZ
    EFZBCDZJFZKVCUSLHZEFZVEVCLHZEFZMVEUSLHEFBNFZAOBPHFZMZUSVCVEQVMUTVDVFVLUTVKV
    LANFZUTABRZASTZUAZVBEFZUTVDVMVBUSUBVMVANFVRNAOBUCVASTZVQUDVKVFVLVKVEEFVFBSV
    EUETUFUGVMVHVJVMVGVBEVBJFZUSJFZUSOUHZVGVBUIVMVBUSUJVMVRVTVSVBUETVLWAVKVLUTW
    AVPUSUETUAVLWBVKVLVNWBVOAUKTUAULVSUMVMBAUNHVIEABUOABUPUQURUD $.
    $( [24-Jan-2007] $) $( [24-Jan-2007] $)

