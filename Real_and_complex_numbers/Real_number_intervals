$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Real number intervals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c (,) $.
  $c (,] $.
  $c [,) $.
  $c [,] $.

  $( Extend class notation with the set of open intervals of extended reals. $)
  cioo $a class (,) $.

  $( Extend class notation with the set of open-below, closed-above intervals
     of extended reals. $)
  cioc $a class (,] $.

  $( Extend class notation with the set of closed-below, open-above intervals
     of extended reals. $)
  cico $a class [,) $.

  $( Extend class notation with the set of closed intervals of extended
     reals. $)
  cicc $a class [,] $.

  ${
    $d x y z $.
    $( Define the set of open intervals of extended reals. $)
    df-ioo $a |- (,) = ( x e. RR* , y e. RR* |->
                   { z e. RR* | ( x < z /\ z < y ) } ) $.

    $( Define the set of open-below, closed-above intervals of extended
       reals. $)
    df-ioc $a |- (,] = ( x e. RR* , y e. RR* |->
                   { z e. RR* | ( x < z /\ z <_ y ) } ) $.

    $( Define the set of closed-below, open-above intervals of extended
       reals. $)
    df-ico $a |- [,) = ( x e. RR* , y e. RR* |->
                   { z e. RR* | ( x <_ z /\ z < y ) } ) $.

    $( Define the set of closed intervals of extended reals. $)
    df-icc $a |- [,] = ( x e. RR* , y e. RR* |->
                   { z e. RR* | ( x <_ z /\ z <_ y ) } ) $.
  $}

  ${
    $d w x y z A $.  $d x y z R $.  $d x y z S $.  $d x y z B $.  $d x y z C $.
    ixx.1 $e |- O = ( x e. RR* , y e. RR* |->
                   { z e. RR* | ( x R z /\ z S y ) } ) $.
    $( The set of intervals of extended reals exists.  (Revised by Mario
       Carneiro, 3-Nov-2013.) $)
    ixxex $p |- O e. _V $=
      ( cxr cv wbr wa crab cmpt2 cvv wcel xrex mpt2exg mp2an eqeltri ) FABHHAIC
      IZDJTBIEJKCHLZMZNGHNOZUCUBNOPPABHHUANNQRS $.
      $( [3-Nov-2013] $)

    $( Value of the interval function.  (Revised by Mario Carneiro,
       3-Nov-2013.) $)
    ixxval $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A O B ) =
                 { z e. RR* | ( A R z /\ z S B ) } ) $=
      ( cxr cv wbr wa crab wceq breq1 anbi1d rabbidv breq2 anbi2d xrex rabex
      ovmpt2 ) ABDEJJAKZCKZFLZUEBKZGLZMZCJNDUEFLZUEEGLZMZCJNHUJUHMZCJNUDDOZUIUM
      CJUNUFUJUHUDDUEFPQRUGEOZUMULCJUOUHUKUJUGEUEGSTRIULCJUAUBUC $.
      $( [3-Nov-2013] $)

    $( Membership in an interval of extended reals.  (Revised by Mario
       Carneiro, 3-Nov-2013.) $)
    elixx1 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A O B ) <->
                 ( C e. RR* /\ A R C /\ C S B ) ) ) $=
      ( cxr wcel wa co cv wbr crab w3a ixxval eleq2d wceq breq2 breq1 anbi12d
      elrab 3anass bitr4i syl6bb ) DKLEKLMZFDEINZLFDCOZGPZUKEHPZMZCKQZLZFKLZDFG
      PZFEHPZRZUIUJUOFABCDEGHIJSTUPUQURUSMZMUTUNVACFKUKFUAULURUMUSUKFDGUBUKFEHU
      CUDUEUQURUSUFUGUH $.
      $( [3-Nov-2013] $)

    $( The set of intervals of extended reals maps to subsets of extended
       reals.  (Contributed by FL, 14-Jun-2007.)  (Revised by Mario Carneiro,
       3-Nov-2013.) $)
    ixxf $p |- O : ( RR* X. RR* ) --> ~P RR* $=
      ( cv wbr wa cxr crab cpw wcel wral cxp wf wss ssrab2 xrex rabex elpw
      mpbir a1i rgen2a foprab2a mpbi ) AHZCHZDIUIBHZEIJZCKLZKMZNZBKOAKOKKPUMFQU
      NABKUNUHKNUJKNJUNULKRUKCKSULKUKCKTUAUBUCUDUEABKKULUMFGUFUG $.
      $( [16-Nov-2013] $) $( [3-Nov-2013] $)

    $( Membership in a set of open intervals of extended reals.  We use the
       fact that an operation's value is empty outside of its domain to show
       ` A e. RR* ` and ` B e. RR* ` .  (Revised by Mario Carneiro,
       3-Nov-2013.) $)
    elixx3g $p |- ( B e. V -> ( C e. ( A O B ) <->
          ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\ ( A R C /\ C S B ) ) ) ) $=
      ( wcel co cxr wa wbr w3a wb elixx1 3anass ibar syl5bb bitrd adantl wn c0
      cdm cxp wceq cpw ixxf fdmi ndmoprg mp3an1 eleq2d noel pm2.21i simpl
      pm5.21ni pm2.61dan df-3an anbi1i anass bitr2i syl6bb ) EJLZFDEIMZLZDNLZEN
      LZOZFNLZDFGPZFEHPZOZOZOZVIVJVLQZVOOZVFVKVHVQRZVKVTVFVKVHVLVMVNQZVQABCDEFG
      HIKSWAVPVKVQVLVMVNTVKVPUAUBUCUDVFVKUEZOZVHFUFLZVQWCVGUFFIUGNNUHZUIVFWBVGU
      FUIWENUJIABCGHIKUKULDEJNNIUMUNUOWBWDVQRVFWDVKVQWDVKFUPUQVKVPURUSUDUCUTVSV
      KVLOZVOOVQVRWFVOVIVJVLVAVBVKVLVOVCVDVE $.
      $( [3-Nov-2013] $)

    ${
      $d b w x y z A $.  $d b B $.  $d b w O $.
      ixx.2 $e |- ( ( A e. RR* /\ w e. RR* ) -> ( A R w -> A <_ w ) ) $.
      ixx.3 $e |- ( ( w e. RR* /\ b e. RR* ) -> ( w S b -> w <_ b ) ) $.
      $( An interval is a subset of its closure.  (Contributed by Paul Chapman,
         18-Oct-2007.)  (Revised by Mario Carneiro, 3-Nov-2013.) $)
      ixxssicc $p |- ( A O B ) C_ ( A [,] B ) $=
        ( cvv wcel co cicc wss cv wceq opreq2 sseq12d cxr wa c0 wne ne0i
        visset wbr crab xrex rabex dmoprab2a ndmopr necon1ai syl w3a cle wi
        simp1 a1i simpl 3simpa expimpd syl2im simpr 3simpb ancoms 3jcad elixx1
        df-icc 3imtr4d mpcom ssriv vtoclg wn cdm wrel cxp relxp releqi mpbir
        oprprc1 0ss eqsstrd pm2.61i oprprc2 3sstr4d ) FNOZEFIPZEFQPZRZEJSZIPZEW
        MQPZRZWLJFNWMFTWNWJWOWKWMFEIUAWMFEQUAUBDWNWOEUCOZWMUCOZUDZDSZWNOZWTWOOZ
        XAWNUEUFWSWNWTUGWSWNUEEWMUCIJUHABUCUCASCSZGUIXCBSHUIUDZCUCUJIKXDCUCUKUL
        UMZUNUOUPWSWTUCOZEWTGUIZWTWMHUIZUQZXFEWTURUIZWTWMURUIZUQXAXBWSXIXFXJXKX
        IXFUSWSXFXGXHUTVAWSWQXIXFXGUDXJWQWRVBXFXGXHVCWQXFXGXJLVDVEWSWRXIXFXHUDX
        KWQWRVFXFXGXHVGWRXFXHXKXFWRXHXKUSMVHVDVEVIABCEWMWTGHIKVJABCEWMWTURURQAB
        CVKVJVLVMVNZVOWIVPZEEIPZEEQPZWJWKXNXORZXMENOZXPWPXPJENWMETWNXNWOXOWMEEI
        UAWMEEQUAUBXLVOXQVPZXNUEXOEEIIVQZVRUCUCVSZVRUCUCVTXSXTXEWAWBWCUEXORXRXO
        WDVAWEWFVAEFIWGEFQWGWHWF $.
        $( [3-Nov-2013] $)

      $( Membership in an interval of extended reals.  (Revised by Mario
         Carneiro, 3-Nov-2013.) $)
      elixx2 $p |- ( ( A e. RR /\ B e. RR ) -> ( C e. ( A O B ) <->
                   ( C e. RR /\ A R C /\ C S B ) ) ) $=
        ( cr wcel wa co wbr w3a cicc ixxssicc sseli cxr cle wb rexr df-icc
        elixx1 syl2an cmnf clt cpnf simpr1 simpll mnflt syl simpr2 wi mnfxr
        a1i xrltletr syl111anc mp2and simpr3 simplr ltpnf pnfxr xrlelttr
        xrrebnd biimpar syl12anc ex sylbid syl5 3simpc syl6bi jcad 3anass
        syl6ibr 3anim1i syl5ibr impbid ) EOPZFOPZQZGEFJRZPZGOPZEGHSZGFISZTZWFWH
        WIWJWKQZQWLWFWHWIWMWHGEFUARZPZWFWIWGWNGABCDEFHIJKLMNUBUCWFWOGUDPZEGUESZ
        GFUESZTZWIWDEUDPZFUDPZWOWSUFWEEUGZFUGZABCEFGUEUEUAABCUHUIUJWFWSWIWFWSQZ
        WPUKGULSZGUMULSZWIWFWPWQWRUNZXDUKEULSZWQXEXDWDXHWDWEWSUOZEUPUQWFWPWQWRU
        RXDUKUDPZWTWPXHWQQXEUSXJXDUTVAXDWDWTXIXBUQXGUKEGVBVCVDXDWRFUMULSZXFWFWP
        WQWRVEXDWEXKWDWEWSVFZFVGUQXDWPXAUMUDPZWRXKQXFUSXGXDWEXAXLXCUQXMXDVHVAGF
        UMVIVCVDWPWIXEXFQGVJVKVLVMVNVOWFWHWPWJWKTZWMWDWTXAWHXNUFWEXBXCABCEFGHIJ
        LUIUJZWPWJWKVPVQVRWIWJWKVSVTWLWHWFXNWIWPWJWKGUGWAXOWBWC $.
        $( [3-Nov-2013] $)
    $}

    ${
      $d x y z D $.
      ixxin.2 $e |- ( ( A e. RR* /\ C e. RR* /\ z e. RR* ) ->
        ( if ( A <_ C , C , A ) R z <-> ( A R z /\ C R z ) ) ) $.
      ixxin.3 $e |- ( ( z e. RR* /\ B e. RR* /\ D e. RR* ) ->
        ( z S if ( B <_ D , B , D ) <-> ( z S B /\ z S D ) ) ) $.
      $( Intersection of two intervals of extended reals.  (Revised by Mario
         Carneiro, 3-Nov-2013.) $)
      ixxin $p |- ( ( ( A e. RR* /\ B e. RR* ) /\ ( C e. RR* /\ D e. RR* ) ) ->
          ( ( A O B ) i^i ( C O D ) ) =
            ( if ( A <_ C , C , A ) O if ( B <_ D , B , D ) ) ) $=
        ( cxr wcel wa cle wbr cif cv crab co cin wceq wb 3expa adantlr 3expb
        ancoms adantll anbi12d an4 syl6bbr rabbidva an4s ifcl ixxval syl2an
        adantr adantl ineq12d inrab syl6eq 3eqtr4rd ) DNOZENOZPZFNOZGNOZPZPZDFQ
        RZFDSZCTZHRZVNEGQRZEGSZIRZPZCNUAZDVNHRZVNEIRZPZFVNHRZVNGIRZPZPZCNUAZVMV
        QJUBZDEJUBZFGJUBZUCZVEVHVFVIVTWHUDVEVHPZVFVIPZPZVSWGCNWOVNNOZPZVSWAWDPZ
        WBWEPZPWGWQVOWRVRWSWMWPVOWRUEZWNVEVHWPWTLUFUGWNWPVRWSUEZWMWPWNXAWPVFVIX
        AMUHUIUJUKWAWBWDWEULUMUNUOVEVHVFVIWIVTUDZWMVMNOZVQNOXBWNVHVEXCVLFDNUPUI
        VPEGNUPABCVMVQHIJKUQURUOVKWLWCCNUAZWFCNUAZUCWHVKWJXDWKXEVGWJXDUDVJABCDE
        HIJKUQUSVJWKXEUDVGABCFGHIJKUQUTVAWCWFCNVBVCVD $.
        $( [3-Nov-2013] $)
    $}

    ${
      ixxss1.2 $e |- ( ( A e. RR* /\ B e. RR* /\ z e. RR* ) ->
        ( if ( A <_ B , B , A ) R z <-> ( A R z /\ B R z ) ) ) $.
      $( Subset relationship for intervals of extended reals.  (Revised by
         Mario Carneiro, 3-Nov-2013.) $)
      ixxss1 $p |- ( ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\ A <_ B ) ->
                   ( B O C ) C_ ( A O C ) ) $=
        ( cxr wcel w3a cle wbr wa co cin cif wceq pm4.24 cv wb ifid breq2i
        bitri a1i ixxin an4s sylan2b 3impa iftrue opreq12d sylan9eq wss inss1
        eqsstr3d ) DLMZELMZFLMZNZDEOPZQZEFIRZDFIRZVESZVFVBVCVGVCEDTZFFOPZFFTZIR
        ZVEUSUTVAVGVKUAZVAUSUTQVAVAQVLVAUBUSVAUTVAVLABCDFEFGHIJKCUCZVJHPZVMFHPZ
        VOQZUDVMLMVAVANVNVOVPVJFVMHVIFUEZUFVOUBUGUHUIUJUKULVCVHEVJFIVCEDUMVJFUA
        VCVQUHUNUOVGVFUPVDVFVEUQUHUR $.
        $( [3-Nov-2013] $)
    $}

    ${
      ixxss2.2 $e |- ( ( z e. RR* /\ B e. RR* /\ C e. RR* ) ->
        ( z S if ( B <_ C , B , C ) <-> ( z S B /\ z S C ) ) ) $.
      $( Subset relationship for intervals of extended reals.  (Revised by
         Mario Carneiro, 3-Nov-2013.) $)
      ixxss2 $p |- ( ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\ B <_ C ) ->
                   ( A O B ) C_ ( A O C ) ) $=
        ( cxr wcel w3a cle wbr wa co cin cif wceq pm4.24 cv wb ifid breq1i
        bitri a1i ixxin an4s sylanb 3impb iftrue opreq12d sylan9eq wss inss2
        eqsstr3d ) DLMZELMZFLMZNZEFOPZQZDEIRZVEDFIRZSZVFVBVCVGDDOPZDDTZVCEFTZIR
        ZVEUSUTVAVGVKUAZUSUSUSQUTVAQVLUSUBUSUTUSVAVLABCDEDFGHIJVICUCZGPZDVMGPZV
        OQZUDUSUSVMLMNVNVOVPVIDVMGVHDUEZUFVOUBUGUHKUIUJUKULVCVIDVJEIVIDUAVCVQUH
        VCEFUMUNUOVGVFUPVDVEVFUQUHUR $.
        $( [3-Nov-2013] $)
    $}
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d x y z C $.  $d x y z D $.
    $( The set of open intervals of extended reals exists. $)
    iooex $p |- (,) e. _V $=
      ( vx vy vz clt cioo df-ioo ixxex ) ABCDDEABCFG $.
      $( [3-Nov-2013] $) $( [6-Feb-2007] $)

    $( Value of the open interval function. $)
    iooval $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A (,) B ) =
                 { x e. RR* | ( A < x /\ x < B ) } ) $=
      ( vy vz clt cioo df-ioo ixxval ) DEABCFFGDEAHI $.
      $( [3-Nov-2013] $) $( [24-Dec-2006] $)

    $( An empty open interval of extended reals. $)
    ioo0 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( ( A (,) B ) = (/) <->
                 B <_ A ) ) $=
      ( vx cxr wcel wa cioo co c0 wceq cv clt wbr crab cle iooval eqeq1d wn
      wrex wne df-ne rabn0 bitr3i wi xrlttr 3com23 3expa r19.23adva w3a cq
      qbtwnxr cr qre rexr syl anim1i reximi2 3expia impbid syl5bb xrltnle
      bitrd con4bid ) ADEZBDEZFZABGHZIJACKZLMVHBLMFZCDNZIJZBAOMZVFVGVJICABPQVFV
      KVLVFVKRZABLMZVLRVMVICDSZVFVNVMVJITVOVJIUAVICDUBUCVFVOVNVFVIVNCDVDVEVHDEZ
      VIVNUDZVDVPVEVQAVHBUEUFUGUHVDVEVNVOVDVEVNUIVICUJSVOCABUKVIVICUJDVHUJEZVPV
      IVRVHULEVPVHUMVHUNUOUPUQUOURUSUTABVAVBVCVB $.
      $( [6-Feb-2007] $)

    $( An open interval of extended reals is nonempty iff the lower argument is
       less than the upper argument. $)
    ioon0 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( ( A (,) B ) =/= (/) <->
                 A < B ) ) $=
      ( cxr wcel wa clt wbr cioo co c0 wceq cle wn ioo0 wb xrlenlt ancoms
      bitr2d necon1abid ) ACDZBCDZEZABFGZABHIZJUBUDJKBALGZUCMZABNUATUEUFOBAPQRS
      $.
      $( [2-Mar-2007] $)

    $( The open interval function's value is empty outside of its domain.
       (Revised by Mario Carneiro, 3-Nov-2013.) $)
    ndmioo $p |- ( -. ( A e. RR* /\ B e. RR* ) -> ( A (,) B ) = (/) ) $=
      ( vx vy vz cxr wcel wa wn cvv cioo co c0 wceq cdm cxp cpw clt df-ioo
      ixxf fdmi ndmoprg mp3an1 expcom oprprc2 cle wbr xrleid adantr ioo0
      mpbird wrel relxp releqi mpbir oprprc1 pm2.61d1 pm2.61i syl6eq ) AFGZBFGH
      IZBJGZABKLZMNZVBVAVDKOZFFPZNZVBVAVDVFFQKCDERRKCDESTUAZABJFFKUBUCUDVBIVCAA
      KLZMABKUEUTUTHZVIMNZVJVKAAUFUGZUTVLUTAUHUIAAUJUKVJIZAJGZVKVNVMVKVGVNVMVKV
      HAAJFFKUBUCUDAAKVEULVFULFFUMVEVFVHUNUOUPUQURUSUQ $.
      $( [3-Nov-2013] $) $( [21-Jun-2007] $)

    $( An open interval with identical lower and upper bounds is empty. $)
    iooid $p |- ( A (,) A ) = (/) $=
      ( cxr wcel wa cioo co c0 wceq cle wbr xrleid adantr ioo0 mpbird ndmioo
      pm2.61i ) ABCZQDZAAEFGHZRSAAIJZQTQAKLAAMNAAOP $.
      $( [3-Nov-2013] $) $( [21-Jun-2007] $)

    $( A member of an open interval of reals is a real.  (Revised by Mario
       Carneiro, 3-Nov-2013.) $)
    elioore $p |- ( A e. ( B (,) C ) -> A e. RR ) $=
      ( vx vy vz cvv wcel cioo co cr wi cxr w3a clt wbr wa df-ioo elixx3g
      3ancomb xrre2 sylanb syl6bi wn oprprc2 eleq2d c0 noel pm2.21i iooid
      eleq2s pm2.61i ) CGHZABCIJZHZAKHZLUMUOBMHZCMHZAMHZNZBAOPACOPQZQUPDEFBCAOO
      IGDEFRSUTUQUSURNVAUPUQURUSTBACUAUBUCUMUDZUOABBIJZHUPVBUNVCABCIUEUFUPAUGVC
      AUGHUPAUHUIBUJUKUCUL $.
      $( [3-Nov-2013] $) $( [17-Aug-2008] $)

    $( Value of the open interval function. $)
    iooval2 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A (,) B ) =
                 { x e. RR | ( A < x /\ x < B ) } ) $=
      ( cxr wcel wa cioo co cv clt wbr crab cr iooval cin inrab2 wceq wss
      ressxr sseqin2 mpbi rabeq ax-mp eqtri elioore ssriv a1i eqsstr3d df-ss
      sylib syl5reqr eqtrd ) BDECDEFZBCGHZBAIZJKUOCJKFZADLZUPAMLZABCNZUMURUQMOZ
      UQUTUPADMOZLZURUPADMPVAMQZVBURQMDRVCSMDTUAUPAVAMUBUCUDUMUQMRUTUQQUMUQUNMU
      SUNMRUMAUNMUOBCUEUFUGUHUQMUIUJUKUL $.
      $( [3-Nov-2013] $) $( [6-Feb-2007] $)

    $( Intersection of two open intervals of extended reals. $)
    iooin $p |- ( ( ( A e. RR* /\ B e. RR* ) /\ ( C e. RR* /\ D e. RR* ) ) ->
         ( ( A (,) B ) i^i ( C (,) D ) ) =
         ( if ( A <_ C , C , A ) (,) if ( B <_ D , B , D ) ) ) $=
      ( vx vy vz clt cioo df-ioo cv xrmaxlt xrltmin ixxin ) EFGABCDHHIEFGJACGKZ
      LOBDMN $.
      $( [3-Nov-2013] $) $( [7-Feb-2007] $)

    $( Subset relationship for open intervals of extended reals. $)
    iooss1 $p |- ( ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\ A <_ B ) ->
                 ( B (,) C ) C_ ( A (,) C ) ) $=
      ( vx vy vz clt cioo df-ioo cv xrmaxlt ixxss1 ) DEFABCGGHDEFIABFJKL $.
      $( [3-Nov-2013] $) $( [7-Feb-2007] $)

    $( Subset relationship for open intervals of extended reals. $)
    iooss2 $p |- ( ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\ B <_ C ) ->
                 ( A (,) B ) C_ ( A (,) C ) ) $=
      ( vx vy vz clt cioo df-ioo cv xrltmin ixxss2 ) DEFABCGGHDEFIFJBCKL $.
      $( [3-Nov-2013] $) $( [7-Feb-2007] $)

    $( Value of the open-below, closed-above interval function. $)
    iocval $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A (,] B ) =
                 { x e. RR* | ( A < x /\ x <_ B ) } ) $=
      ( vy vz clt cle cioc df-ioc ixxval ) DEABCFGHDEAIJ $.
      $( [3-Nov-2013] $) $( [24-Dec-2006] $)

    $( Value of the closed-below, open-above interval function. $)
    icoval $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A [,) B ) =
                 { x e. RR* | ( A <_ x /\ x < B ) } ) $=
      ( vy vz cle clt cico df-ico ixxval ) DEABCFGHDEAIJ $.
      $( [3-Nov-2013] $) $( [24-Dec-2006] $)

    $( Value of the closed interval function. $)
    iccval $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A [,] B ) =
                 { x e. RR* | ( A <_ x /\ x <_ B ) } ) $=
      ( vy vz cle cicc df-icc ixxval ) DEABCFFGDEAHI $.
      $( [3-Nov-2013] $) $( [24-Dec-2006] $)

    $( Membership in an open interval of extended reals. $)
    elioo1 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A (,) B ) <->
                 ( C e. RR* /\ A < C /\ C < B ) ) ) $=
      ( vx vy vz clt cioo df-ioo elixx1 ) DEFABCGGHDEFIJ $.
      $( [3-Nov-2013] $) $( [24-Dec-2006] $)

    $( Membership in an open interval of extended reals. $)
    elioo2 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A (,) B ) <->
                 ( C e. RR /\ A < C /\ C < B ) ) ) $=
      ( vx cxr wcel wa cioo co cv clt wbr cr crab w3a iooval2 eleq2d wceq
      breq2 breq1 anbi12d elrab 3anass bitr4i syl6bb ) AEFBEFGZCABHIZFCADJZKLZU
      HBKLZGZDMNZFZCMFZACKLZCBKLZOZUFUGULCDABPQUMUNUOUPGZGUQUKURDCMUHCRUIUOUJUP
      UHCAKSUHCBKTUAUBUNUOUPUCUDUE $.
      $( [6-Feb-2007] $)

    $( Membership in a set of open intervals of extended reals.  We use the
       fact that an operation's value is empty outside of its domain to show
       ` A e. RR* ` and ` B e. RR* ` . $)
    elioo3g $p |- ( B e. V -> ( C e. ( A (,) B ) <->
          ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\ ( A < C /\ C < B ) ) ) ) $=
      ( vx vy vz clt cioo df-ioo elixx3g ) EFGABCHHIDEFGJK $.
      $( [3-Nov-2013] $) $( [24-Dec-2006] $)

    $( Membership in an open-below, closed-above interval of extended reals. $)
    elioc1 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A (,] B ) <->
                 ( C e. RR* /\ A < C /\ C <_ B ) ) ) $=
      ( vx vy vz clt cle cioc df-ioc elixx1 ) DEFABCGHIDEFJK $.
      $( [3-Nov-2013] $) $( [24-Dec-2006] $)

    $( Membership in a closed-below, open-above interval of extended reals. $)
    elico1 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A [,) B ) <->
                 ( C e. RR* /\ A <_ C /\ C < B ) ) ) $=
      ( vx vy vz cle clt cico df-ico elixx1 ) DEFABCGHIDEFJK $.
      $( [3-Nov-2013] $) $( [24-Dec-2006] $)

    $( Membership in a closed interval of extended reals. $)
    elicc1 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A [,] B ) <->
                 ( C e. RR* /\ A <_ C /\ C <_ B ) ) ) $=
      ( vx vy vz cle cicc df-icc elixx1 ) DEFABCGGHDEFIJ $.
      $( [3-Nov-2013] $) $( [24-Dec-2006] $)
  $}

  $( An element of a closed interval is less than or equal to its upper bound.
     (Contributed by Jeffrey Hankins, 14-Jul-2009.) $)
  iccleub $p |- ( ( A e. RR* /\ B e. RR* /\ C e. ( A [,] B ) ) -> C <_ B ) $=
    ( cxr wcel cicc co cle wbr wa w3a elicc1 simp3 syl6bi 3impia ) ADEZBDEZCABF
    GEZCBHIZPQJRCDEZACHIZSKSABCLTUASMNO $.
    $( [14-Jul-2009] $)

  $( MAY BE REVISED - see if this shortens the uses of any other
     elioo*'s $)
  $( Membership in an open interval of extended reals. $)
  elioo5 $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* )
           -> ( C e. ( A (,) B ) <-> ( A < C /\ C < B ) ) ) $=
    ( cxr wcel w3a cioo co clt wbr wa wb elioo1 3adant3 3anass baibr 3ad2ant3
    bitr4d ) ADEZBDEZCDEZFCABGHEZUAACIJZCBIJZFZUCUDKZSTUBUELUAABCMNUASUFUELTUEU
    AUFUAUCUDOPQR $.
    $( [17-Aug-2008] $)

  $( Membership in an open interval of extended reals. $)
  elioo4g $p |- ( B e. V -> ( C e. ( A (,) B ) <->
        ( ( A e. RR* /\ B e. RR* /\ C e. RR ) /\ ( A < C /\ C < B ) ) ) ) $=
    ( wcel cioo co cxr w3a clt wbr wa cr elioo3g simpl1 simpl2 wi xrre2 ex
    3com23 imp 3jca simpr jca rexr 3anim3i anim1i impbii syl6bb ) BDECABFGEAHEZ
    BHEZCHEZIZACJKCBJKLZLZUJUKCMEZIZUNLZABCDNUOURUOUQUNUOUJUKUPUJUKULUNOUJUKULU
    NPUMUNUPUJULUKUNUPQUJULUKIUNUPACBRSTUAUBUMUNUCUDUQUMUNUPULUJUKCUEUFUGUHUI
    $.
    $( [8-Jun-2007] $)

  $( A non-empty open interval spans an interval of extended reals. $)
  eliooxr $p |- ( A e. ( B (,) C ) -> ( B e. RR* /\ C e. RR* ) ) $=
    ( cioo co wcel c0 wne cxr wa ne0i ndmioo necon1ai syl ) ABCDEZFOGHBIFCIFJZO
    AKPOGBCLMN $.
    $( [17-Aug-2008] $)

  $( Ordering implied by a member of an open interval of reals. $)
  eliooord $p |- ( A e. ( B (,) C ) -> ( B <_ A /\ A <_ C ) ) $=
    ( cioo co wcel cxr w3a clt wbr wa cle eliooxr cr elioore rexr syl df-3an
    sylanbrc wb elioo2 ibi 3simpc wi xrltle 3adant2 ancoms 3adant1 anim12d
    sylc ) ABCDEFZBGFZCGFZAGFZHZBAIJZACIJZKZBALJZACLJZKUKULUMKZUNUOABCMZUKANFZU
    NABCOAPQULUMUNRSUKVCUPUQHZURUKVDUKVAUKVDTVBBCAUAQUBVCUPUQUCQUOUPUSUQUTULUNU
    PUSUDUMBAUEUFUMUNUQUTUDZULUNUMVEACUEUGUHUIUJ $.
    $( [17-Aug-2008] $)

  ${
    $d x A $.  $d x B $.
    $( An open interval is a set of reals. $)
    ioossre $p |- ( A (,) B ) C_ RR $=
      ( vx cioo co cr cv elioore ssriv ) CABDEFCGABHI $.
      $( [31-May-2007] $)
  $}

  ${
    $d v w x y z A $.  $d v x y z B $.  $d x y z C $.
    $( Membership in an open-below, closed-above real interval.  (Contributed
       by Paul Chapman, 30-Dec-2007.) $)
    elioc2 $p |- ( ( A e. RR /\ B e. RR ) ->
        ( C e. ( A (,] B ) <-> ( C e. RR /\ A < C /\ C <_ B ) ) ) $=
      ( vx vy vz vw vv clt cle cioc df-ioc cv xrltle cxr wcel wa wbr idd
      elixx2 ) DEFGABCIJKHDEFLAGMZNUAOPHMZOPQUAUBJRST $.
      $( [30-Dec-2007] $)

    $( Membership in a closed-below, open-above real interval.  (Contributed by
       Paul Chapman, 21-Jan-2008.) $)
    elico2 $p |- ( ( A e. RR /\ B e. RR ) ->
        ( C e. ( A [,) B ) <-> ( C e. RR /\ A <_ C /\ C < B ) ) ) $=
      ( vx vy vz vw vv cle clt cico df-ico cxr wcel cv wa wbr idd xrltle
      elixx2 ) DEFGABCIJKHDEFLAMNGOZMNPAUAIQRUAHOST $.
      $( [21-Jan-2008] $)

    $( Membership in a closed real interval.  (Contributed by Paul Chapman,
       21-Sep-2007.) $)
    elicc2 $p |- ( ( A e. RR /\ B e. RR ) -> ( C e. ( A [,] B ) <->
                      ( C e. RR /\ A <_ C /\ C <_ B ) ) ) $=
      ( vx vy vz vw vv cle cicc df-icc cxr wcel cv wa wbr idd elixx2 ) DEFGABCI
      IJHDEFKALMGNZLMZOASIPQTHNZLMOSUAIPQR $.
      $( [21-Sep-2007] $)
  $}

  $( The open interval from minus to plus infinity. $)
  ioomax $p |- ( -oo (,) +oo ) = RR $=
    ( vx cmnf cpnf cioo co cv clt wbr wa cr crab cxr wcel wceq mnfxr pnfxr
    iooval2 mp2an rabid2 mnflt ltpnf jca mprgbir eqtr4i ) BCDEZBAFZGHZUFCGHZIZA
    JKZJBLMCLMUEUJNOPABCQRJUJNUIAJUIAJSUFJMUGUHUFTUFUAUBUCUD $.
    $( [6-Feb-2007] $)

  $( The set of positive reals expressed as an open interval. $)
  ioopos $p |- ( 0 (,) +oo ) = { x e. RR | 0 < x } $=
    ( cc0 cpnf cioo co cv clt wbr wa cr crab cxr wcel wceq 0re rexr ax-mp
    pnfxr iooval2 mp2an ltpnf biantrud rabbiia eqtr4i ) BCDEZBAFZGHZUFCGHZIZAJK
    ZUGAJKBLMZCLMUEUJNBJMUKOBPQRABCSTUGUIAJUFJMUHUGUFUAUBUCUD $.
    $( [7-May-2007] $)

  $( The set of positive reals expressed as an open interval.  (Contributed by
     Steve Rodriguez, 25-Nov-2007.) $)
  ioorp $p |- ( 0 (,) +oo ) = RR+ $=
    ( vx cc0 cpnf cioo co cv clt wbr cr crab crp ioopos df-rp eqtr4i ) BCDEBAFG
    HAIJKALAMN $.
    $( [25-Nov-2007] $)

  $( Shift the arguments of the open interval function. $)
  iooshf $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) )
     -> ( ( A - B ) e. ( C (,) D ) <-> A e. ( ( C + B ) (,) ( D + B ) ) ) ) $=
    ( cr wcel wa caddc co clt wbr cmin cioo wb ltaddsub 3com13 3expa adantrr
    w3a ltsubadd bicomd adantrl anbi12d cxr readdcl rexr syl ad2ant2rl
    ad2ant2l ad2antrl elioo5 syl111anc ancoms ad2antll resubcl adantr 3bitr4rd
    ) AEFZBEFZGZCEFZDEFZGZGZCBHIZAJKZADBHIZJKZGZCABLIZJKZVJDJKZGZAVEVGMIFZVJCDM
    IFZVDVFVKVHVLUTVAVFVKNZVBURUSVAVPVAUSURVPCBAOPQRUTVBVHVLNZVAURUSVBVQURUSVBS
    VLVHABDTUAQUBUCVCUTVNVINZVCUTGVEUDFZVGUDFZAUDFZVRVAUSVSVBURVAUSGVEEFVSCBUEV
    EUFUGUHVBUSVTVAURVBUSGVGEFVTDBUEVGUFUGUIURWAVCUSAUFUJVEVGAUKULUMVDCUDFZDUDF
    ZVJUDFZVOVMNVAWBUTVBCUFUJVBWCUTVADUFUNUTWDVCUTVJEFWDABUOVJUFUGUPCDVJUKULUQ
    $.
    $( [17-Aug-2008] $)

  ${
    $d v w x y z A $.  $d v x y z B $.
    $( A closed real interval is a set of reals.  (Contributed by FL,
       6-Jun-2007.  Proof shortened by Paul Chapman, 21-Jan-2008.) $)
    iccssre $p |- ( ( A e. RR /\ B e. RR ) -> ( A [,] B ) C_ RR ) $=
      ( vx cr wcel wa cicc co cv w3a cle wbr elicc2 biimp3a simp1d 3expia
      ssrdv ) ADEZBDEZFCABGHZDRSCIZTEZUADEZRSUBJUCAUAKLZUABKLZRSUBUCUDUEJABUAMN
      OPQ $.
      $( [6-Jun-2007] $)

    $( An open interval is a subset of its closure.  (Contributed by Paul
       Chapman, 18-Oct-2007.) $)
    ioossicc $p |- ( A (,) B ) C_ ( A [,] B ) $=
      ( vx vy vz vw vv clt cioo df-ioo cv xrltle ixxssicc ) CDEFABHHIGCDEJAFKZL
      NGKLM $.
      $( [18-Oct-2007] $)
  $}

  ${
    $d A y $.  $d B x y $.  $d S x y $.
    $( A nonempty subset of a closed real interval satisfies the conditions for
       the existence of its supremum (see ~ suprcl ).  (Contributed by Paul
       Chapman, 21-Jan-2008.) $)
    iccsupr $p |- ( ( ( A e. RR /\ B e. RR ) /\ S C_ ( A [,] B ) /\ C e. S ) ->
                   ( S C_ RR /\ S =/= (/) /\ E. x e. RR A. y e. S y <_ x ) ) $=
      ( cr wcel wa cicc co wss w3a c0 wne cv cle wbr wral wrex iccssre sstr
      ancoms sylan 3adant3 ne0i 3ad2ant3 simplr ssel elicc2 biimpd sylan9r imp
      simp3d r19.21aiva wceq breq2 ralbidv rcla4ev syl11anc 3jca ) CGHZDGHZIZFC
      DJKZLZEFHZMFGLZFNOZBPZAPZQRZBFSZAGTZVDVFVHVGVDVEGLZVFVHCDUAVFVOVHFVEGUBUC
      UDUEVGVDVIVFFEUFUGVDVFVNVGVDVFIZVCVJDQRZBFSZVNVBVCVFUHVPVQBFVPVJFHZIVJGHZ
      CVJQRZVQVPVSVTWAVQMZVFVSVJVEHZVDWBFVEVJUIVDWCWBCDVJUJUKULUMUNUOVMVRADGVKD
      UPVLVQBFVKDVJQUQURUSUTUEVA $.
      $( [21-Jan-2008] $)
  $}

  ${
    $d w x y z A $.
    $( Two ways of saying that a real number is positive. $)
    repos $p |- ( A e. ( 0 (,) +oo ) <-> ( A e. RR /\ 0 < A ) ) $=
      ( vx cc0 cv clt wbr cr cpnf cioo co breq2 ioopos elrab2 ) CBDZEFCAEFBAGCH
      IJNACEKBLM $.
      $( [7-May-2007] $)

    $( The set of open intervals of extended reals maps to subsets of reals.
       (Revised by Mario Carneiro, 3-Nov-2013.) $)
    ioof $p |- (,) : ( RR* X. RR* ) --> ~P RR $=
      ( vx vz vy cv clt wbr wa cxr crab cr cpw wcel wral cxp cioo wf co iooval
      wss ioossre oprex elpw mpbir syl6eqelr rgen2a df-ioo foprab2a mpbi ) ADZB
      DZEFUJCDZEFGBHIZJKZLZCHMAHMHHNUMOPUNACHUIHLUKHLGULUIUKOQZUMBUIUKRUOUMLUOJ
      SUIUKTUOJUIUKOUAUBUCUDUEACHHULUMOACBUFUGUH $.
      $( [16-Nov-2013] $) $( [7-Feb-2007] $)

    $( The set of closed intervals of extended reals maps to subsets of
       extended reals.  (Contributed by FL, 14-Jun-2007.)  (Revised by Mario
       Carneiro, 3-Nov-2013.) $)
    iccf $p |- [,] : ( RR* X. RR* ) --> ~P RR* $=
      ( vx vy vz cle cicc df-icc ixxf ) ABCDDEABCFG $.
      $( [3-Nov-2013] $) $( [14-Jun-2007] $)

    $( The union of the range of the open interval function. $)
    unirnioo $p |- U. ran (,) = RR $=
      ( cioo crn cuni cr cpw wss cxr cxp wf ioof frn ax-mp uniss unipw sseqtri
      wcel cmnf cpnf co ioomax wfn ffn mnfxr pnfxr fnoprvrn mp3an eqeltrri
      elssuni eqssi ) ABZCZDUKDEZCZDUJULFZUKUMFGGHZULAIZUNJUOULAKLUJULMLDNODUJP
      DUKFQRASZDUJTAUOUAZQGPRGPUQUJPUPURJUOULAUBLUCUDGGQRAUEUFUGDUJUHLUI $.
      $( [7-May-2007] $)

    $( Alternate definition of the set of open intervals of extended reals. $)
    dfioo2 $p |- (,) = { <. <. x , y >. , z >. | ( ( x e. RR* /\ y e. RR* )
                   /\ z = { w e. RR | ( x < w /\ w < y ) } ) } $=
      ( cioo cv cxr wcel wa co wceq copab2 clt wbr cr crab cxp wfn cpw wf ioof
      ffn ax-mp fnoprv mpbi iooval2 eqeq2d pm5.32i oprabbii eqtri ) EAFZGHBFZGH
      IZCFZUKULEJZKZIZABCLZUMUNUKDFZMNUSULMNIDOPZKZIZABCLEGGQZRZEURKVCOSZETVDUA
      VCVEEUBUCABCGGEUDUEUQVBABCUMUPVAUMUOUTUNDUKULUFUGUHUIUJ $.
      $( [1-Mar-2007] $)
  $}

  $( The lower bound of a closed interval is a member of it.  (Contributed by
     Paul Chapman, 26-Nov-2007.) $)
  lbicc2 $p |- ( ( A e. RR /\ B e. RR /\ A <_ B ) -> A e. ( A [,] B ) ) $=
    ( cr wcel cle wbr w3a cicc co simp1 leid 3ad2ant1 simp3 3jca wb elicc2
    3adant3 mpbird ) ACDZBCDZABEFZGZAABHIDZSAAEFZUAGZUBSUDUASTUAJSTUDUAAKLSTUAM
    NSTUCUEOUAABAPQR $.
    $( [26-Nov-2007] $)

  $( The upper bound of a closed interval is a member of it.  (Contributed by
     Paul Chapman, 26-Nov-2007.) $)
  ubicc2 $p |- ( ( A e. RR /\ B e. RR /\ A <_ B ) -> B e. ( A [,] B ) ) $=
    ( cr wcel cle wbr w3a cicc co simp2 simp3 leid 3ad2ant2 3jca wb elicc2
    3adant3 mpbird ) ACDZBCDZABEFZGZBABHIDZTUABBEFZGZUBTUAUDSTUAJSTUAKTSUDUABLM
    NSTUCUEOUAABBPQR $.
    $( [26-Nov-2007] $)

  $( Membership in a negated open real interval.  (Contributed by Paul Chapman,
     26-Nov-2007.) $)
  iooneg $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( C e. ( A (,) B ) <-> -u C e. ( -u B (,) -u A ) ) ) $=
    ( cr wcel w3a clt wbr wa cneg cioo co wb ltneg 3adant2 ancoms 3adant1
    anbi12d ancom syl6bb cxr rexr elioo5 syl3an renegcl 3com12 3bitr4d ) ADEZBD
    EZCDEZFZACGHZCBGHZIZBJZCJZGHZUPAJZGHZIZCABKLEZUPUOURKLEZUKUNUSUQIUTUKULUSUM
    UQUHUJULUSMUIACNOUIUJUMUQMZUHUJUIVCCBNPQRUSUQSTUHAUAEUIBUAEUJCUAEVAUNMAUBBU
    BCUBABCUCUDUIUHUJVBUTMZUIUODEZUHURDEZUJUPDEZVDBUEAUECUEVEUOUAEVFURUAEVGUPUA
    EVDUOUBURUBUPUBUOURUPUCUDUDUFUG $.
    $( [26-Nov-2007] $)

  $( Membership in a negated closed real interval.  (Contributed by Paul
     Chapman, 26-Nov-2007.) $)
  iccneg $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( C e. ( A [,] B ) <-> -u C e. ( -u B [,] -u A ) ) ) $=
    ( cr wcel w3a cle wbr wa cneg cicc co wb renegcl ax-1 impbid2 3ad2ant3
    ancom leneg ancoms 3adant1 3adant2 anbi12d syl5bbr elicc2 3adant3 3anass
    syl6bb anim12i syl 3bitr4d ) ADEZBDEZCDEZFZUNACGHZCBGHZIZIZCJZDEZBJZUTGHZUT
    AJZGHZIZIZCABKLEZUTVBVDKLEZUOUNVAURVFUNULUNVAMUMUNUNVACNUNVAOPQURUQUPIUOVFU
    QUPRUOUQVCUPVEUMUNUQVCMZULUNUMVJCBSTUAULUNUPVEMUMACSUBUCUDUCUOVHUNUPUQFZUSU
    LUMVHVKMUNABCUEUFUNUPUQUGUHUOVIVAVCVEFZVGULUMVIVLMZUNULUMIVBDEZVDDEZIZVMUMU
    LVPUMVNULVOBNANUITVBVDUTUEUJUFVAVCVEUGUHUK $.
    $( [26-Nov-2007] $)

  $( A shifted real is a member of a shifted, closed-below, open-above real
     interval.  (Contributed by Paul Chapman, 25-Mar-2008.) $)
  icoshft $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) -> ( X e. ( A [,) B ) ->
                  ( X + C ) e. ( ( A + C ) [,) ( B + C ) ) ) ) $=
    ( cr wcel w3a cico co cle wbr clt wa caddc wi elico2 biimpd 3adant3 3anass
    syl6ib wb leadd1 3com12 3expib com12 3adant2 imp ltadd1 3adant1 anbi12d
    pm5.32da readdcl expcom anim1d syl6ibr 3ad2ant3 biimprd syl11anc syld
    sylbid ) AEFZBEFZCEFZGZDABHIFZDEFZADJKZDBLKZMZMZDCNIZACNIZBCNIZHIFZVDVEVFVG
    VHGZVJVAVBVEVOOVCVAVBMVEVOABDPQRVFVGVHSTVDVJVFVLVKJKZVKVMLKZMZMZVNVDVFVIVRV
    DVFMVGVPVHVQVDVFVGVPUAZVAVCVFVTOVBVFVAVCMVTVFVAVCVTVAVFVCVTADCUBUCUDUEUFUGV
    DVFVHVQUAZVBVCVFWAOVAVFVBVCMWAVFVBVCWADBCUHUDUEUIUGUJUKVDVSVKEFZVPVQGZVNVCV
    AVSWCOVBVCVSWBVRMWCVCVFWBVRVFVCWBDCULUMUNWBVPVQSUOUPVDVLEFZVMEFZWCVNOVAVCWD
    VBACULUFVBVCWEVABCULUIWDWEMVNWCVLVMVKPUQURUSUTUS $.
    $( [25-Mar-2008] $)

  ${
    $d A x y $.  $d B x y $.  $d C x y $.
    icoshftf1o.1 $e |- F = { <. x , y >. | ( x e. ( A [,) B ) /\
                              y = ( x + C ) ) } $.

    ${
      $d A w z $.  $d B w z $.  $d C w z $.  $d F w z $.  $d w x y z $.
      icoshftf1o.2 $e |- A e. RR $.
      icoshftf1o.3 $e |- B e. RR $.
      icoshftf1o.4 $e |- C e. RR $.
      $( Shifting a closed-below, open-above interval is one-to-one onto.
         (Contributed by Paul Chapman, 25-Mar-2008.) $)
      icoshftf1oii $p |- F : ( A [,) B ) -1-1-onto->
                          ( ( A + C ) [,) ( B + C ) ) $=
        ( vz vw cico co caddc wf1o wf1 wfo df-f1o wf cv cfv wceq wi wral dff13
        cr wcel icoshft mp3an fopab wa opreq1 oprex fvopab4 eqeqan12d cc cle
        wbr clt w3a wb elico2 mp2an simp1bi recnd recni addcan2 mp3an3 biimpd
        syl2an sylbid rgen2a mpbir2an wrex dffo3 cmin cneg readdcli recn
        negsub mpan2 3syl renegcli negsubi pncan eqtri opreq12i syl6eleq
        eqeltrrd syl npcan eqtr2d fveq2 eqeq2d rcla4ev syl11anc rgen ) CDMNZCEO
        NZDEONZMNZFPWSXBFQZWSXBFRZWSXBFSXCWSXBFTZKUAZFUBZLUAZFUBZUCZXFXHUCZUDZL
        WSUEKWSUEKLWSXBFUFABWSXBAUAZEONZFGCUGUHZDUGUHZEUGUHXMWSUHXNXBUHUDHIJCDE
        XMUIUJUKZXLKLWSXFWSUHZXHWSUHZULXJXFEONZXHEONZUCZXKXRXSXGXTXIYAABXFXNXTW
        SFXMXFEOUMGXFEOUNUOABXHXNYAWSFXMXHEOUMGXHEOUNUOUPXRXFUQUHZXHUQUHZYBXKUD
        XSXRXFXRXFUGUHZCXFURUSZXFDUTUSZXOXPXRYEYFYGVAVBHICDXFVCVDVEVFXSXHXSXHUG
        UHZCXHURUSZXHDUTUSZXOXPXSYHYIYJVAVBHICDXHVCVDVEVFYCYDULYBXKYCYDEUQUHZYB
        XKVBEJVGZXFXHEVHVIVJVKVLVMVNXDXEXFXIUCZLWSVOZKXBUELKWSXBFVPXQYNKXBXFXBU
        HZXFEVQNZWSUHZXFYPFUBZUCZYNYOXFEVRZONZYPWSYOYEYCUUAYPUCZYOYEWTXFURUSZXF
        XAUTUSZWTUGUHZXAUGUHZYOYEUUCUUDVAVBCEHJVSZDEIJVSZWTXAXFVCVDVEZXFVTZYCYK
        UUBYLXFEWAWBWCYOUUAWTYTONZXAYTONZMNZWSUUEUUFYTUGUHYOUUAUUMUHUDUUGUUHEJW
        DWTXAYTXFUIUJUUKCUULDMUUKWTEVQNZCWTEWTUUGVGYLWECUQUHYKUUNCUCCHVGYLCEWFV
        DWGUULXAEVQNZDXAEXAUUHVGYLWEDUQUHYKUUODUCDIVGYLDEWFVDWGWHWIWJZYOYRYPEON
        ZXFYOYQYRUUQUCUUPABYPXNUUQWSFXMYPEOUMGYPEOUNUOWKYOYEYCUUQXFUCZUUIUUJYCY
        KUURYLXFEWLWBWCWMYMYSLYPWSXHYPUCXIYRXFXHYPFWNWOWPWQWRVNVN $.
        $( [25-Mar-2008] $)
    $}

    ${
      icoshftf1olem.1 $e |- G = { <. x , y >. |
          ( x e. ( if ( A e. RR , A , 0 ) [,) if ( B e. RR , B , 0 ) ) /\
            y = ( x + if ( C e. RR , C , 0 ) ) ) } $.
      $( Lemma for ~ icoshftf1o . $)
      icoshftf1olem $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  F : ( A [,) B ) -1-1-onto-> ( ( A + C ) [,) ( B + C ) ) ) $=
        ( cr wcel w3a cico co caddc wf1o cc0 cif wceq wb opreq1 f1oeq2 syl
        opreq1d f1oeq3 bitrd opreq2 opreq2d opreq12d 0re elimel icoshftf1oii
        dedth3h cv wa copab iftrue 3ad2ant1 3ad2ant2 eleq2d 3ad2ant3 eqeq2d
        anbi12d opabbidv 3eqtr4g f1oeq1 mpbid ) CJKZDJKZEJKZLZCDMNZCEONZDEONZMN
        ZGPZVLVOFPZVHVIVJVPVHCQRZDMNZVREONZVNMNZGPZVRVIDQRZMNZVTWCEONZMNZGPZWDV
        RVJEQRZONZWCWHONZMNZGPZCDEQQQCVRSZVPVSVOGPZWBWMVLVSSVPWNTCVRDMUAVLVSVOG
        UBUCWMVOWASWNWBTWMVMVTVNMCVREOUAUDVOWAVSGUEUCUFDWCSZWBWDWAGPZWGWOVSWDSW
        BWPTDWCVRMUGVSWDWAGUBUCWOWAWFSWPWGTWOVNWEVTMDWCEOUAUHWAWFWDGUEUCUFEWHSZ
        WFWKSWGWLTWQVTWIWEWJMEWHVROUGEWHWCOUGUIWFWKWDGUEUCABVRWCWHGICQJUJUKDQJU
        JUKEQJUJUKULUMVKGFSVPVQTVKAUNZWDKZBUNZWRWHONZSZUOZABUPWRVLKZWTWREONZSZU
        OZABUPGFVKXCXGABVKWSXDXBXFVKWDVLWRVKVRCWCDMVHVIVRCSVJVHCQUQURVIVHWCDSVJ
        VIDQUQUSUIUTVKXAXEWTVKWHEWROVJVHWHESVIVJEQUQVAUHVBVCVDIHVEVLVOGFVFUCVG
        $.
        $( [25-Mar-2008] $)
    $}

    $( Shifting a closed-below, open-above interval is one-to-one onto.
       (Contributed by Paul Chapman, 25-Mar-2008.) $)
    icoshftf1o $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
        F : ( A [,) B ) -1-1-onto-> ( ( A + C ) [,) ( B + C ) ) ) $=
      ( cv cr wcel cc0 cif cico co caddc wceq wa copab eqid icoshftf1olem ) ABC
      DEFAHZCIJCKLDIJDKLMNJBHUAEIJEKLONPQABRZGUBST $.
      $( [25-Mar-2008] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.
    icounlem.1 $e |- A e. RR $.
    icounlem.2 $e |- B e. RR $.
    icounlem.3 $e |- C e. RR $.
    $( Lemma for ~ icoun . $)
    icounlem $p |- ( ( A <_ B /\ B <_ C ) ->
                     ( ( A [,) B ) u. ( B [,) C ) ) = ( A [,) C ) ) $=
      ( vx cle wbr wa cico co cun cv wcel wo cr clt wb wi letr mp3an12 expdimp
      pm4.72 sylib adantrr orcom syl6bb letri wn ltnle mpan2 adantr ltletr
      mp3an23 exp3a com23 imp sylbird orrd sylan2 biantrurd bitrd ancom
      adantrl lelttric mpan orcomd anbi12d orddi syl6bbr expcom pm5.32d andi
      w3a elico2 mp2an orbi12i 3anass bitri syl6rbbr elun 3bitr4g eqrdv ) ABHIZ
      BCHIZJZGABKLZBCKLZMZACKLZWGGNZWHOZWLWIOZPZWLQOZAWLHIZWLCRIZJZJZWLWJOWLWKO
      ZWGWTWPWQWLBRIZJZJZWPBWLHIZWRJZJZPZWOWGWTWPXCXFPZJXHWGWPWSXIWPWGWSXISWPWG
      JZWSWQXEPZWQWRPZJZXBXEPZXBWRPZJZJXIXJWQXMWRXPXJWQXLXKJZXMXJWQXKXQXJWQXEWQ
      PZXKWPWEWQXRSZWFWPWEJXEWQTXSWPWEXEWQAQOZBQOZWPWEXEJWQTDEABWLUAUBUCXEWQUDU
      EUFXEWQUGUHXJXLXKWGWPACHIZXLABCDEFUIWPYBJZWQWRYCWQUJZWLARIZWRWPYEYDSZYBWP
      XTYFDWLAUKULUMWPYBYEWRTWPYEYBWRWPYEYBWRWPXTCQOZYEYBJWRTDFWLACUNUOUPUQURUS
      UTVAVBVCXLXKVDUHXJWRXOXPWPWFWRXOSZWEWPWFJXBWRTZYHWPWFYIWPXBWFWRWPXBWFWRWP
      YAYGXBWFJWRTEFWLBCUNUOUPUQURXBWRUDUEVEXJXNXOWPXNWGWPXEXBYAWPXEXBPEBWLVFVG
      VHUMVBVCVIWQXBXEWRVJVKVLVMWPXCXFVNUHWOWPWQXBVOZWPXEWRVOZPXHWMYJWNYKXTYAWM
      YJSDEABWLVPVQYAYGWNYKSEFBCWLVPVQVRYJXDYKXGWPWQXBVSWPXEWRVSVRVTWAWLWHWIWBX
      AWPWQWRVOZWTXTYGXAYLSDFACWLVPVQWPWQWRVSVTWCWD $.
      $( [15-Mar-2008] $)
  $}

  $( The union of end-to-end closed-below, open-above real intervals.
     (Contributed by Paul Chapman, 15-Mar-2008.) $)
  icoun $p |- ( ( ( A e. RR /\ B e. RR /\ C e. RR ) /\ ( A <_ B /\ B <_ C ) )
                -> ( ( A [,) B ) u. ( B [,) C ) ) = ( A [,) C ) ) $=
    ( cr wcel w3a cle wbr wa cico co cun wceq wi cc0 cif breq1 anbi1d opreq1
    uneq1d eqeq12d imbi12d breq2 anbi12d opreq2 uneq12d eqeq1d anbi2d uneq2d
    0re elimel icounlem dedth3h imp ) ADEZBDEZCDEZFABGHZBCGHZIZABJKZBCJKZLZACJK
    ZMZUOUPUQUTVENUOAOPZBGHZUSIZVFBJKZVBLZVFCJKZMZNVFUPBOPZGHZVMCGHZIZVFVMJKZVM
    CJKZLZVKMZNVNVMUQCOPZGHZIZVQVMWAJKZLZVFWAJKZMZNABCOOOAVFMZUTVHVEVLWHURVGUSA
    VFBGQRWHVCVJVDVKWHVAVIVBAVFBJSTAVFCJSUAUBBVMMZVHVPVLVTWIVGVNUSVOBVMVFGUCBVM
    CGQUDWIVJVSVKWIVIVQVBVRBVMVFJUEBVMCJSUFUGUBCWAMZVPWCVTWGWJVOWBVNCWAVMGUCUHW
    JVSWEVKWFWJVRWDVQCWAVMJUEUICWAVFJUEUAUBVFVMWAAODUJUKBODUJUKCODUJUKULUMUN $.
    $( [15-Mar-2008] $)

  ${
    $d A x $.  $d B x $.
    snunioolem.1 $e |- A e. RR $.
    snunioolem.2 $e |- B e. RR $.
    $( Lemma for ~ snunioo . $)
    snunioolem $p |- ( A < B -> ( { A } u. ( A (,) B ) ) = ( A [,) B ) ) $=
      ( vx clt wbr csn cioo co cun cico cv wcel wceq wo cr cle wa w3a wb
      elico2 mp2an 3anass bitri leloe mpan anbi1d wi breq1 biimpcd pm4.71
      sylib orbi2d andir syl6rbbr sylan9bb expcom pm5.32d andi cxr rexr elioo2
      syl2an eleq1 mpbii pm4.71ri orbi12i bitr4i syl6bb syl5bb orcom elun elsn
      eqcom orbi1i eqrdv ) ABFGZEAHZABIJZKZABLJZVREMZWBNZAWCOZWCVTNZPZWCWANZVRW
      DWFWEPZWGWDWCQNZAWCRGZWCBFGZSZSZVRWIWDWJWKWLTZWNAQNZBQNZWDWOUACDABWCUBUCW
      JWKWLUDUEVRWNWJAWCFGZWLSZWEPZSZWIVRWJWMWTWJVRWMWTUAWJWMWRWEPZWLSZVRWTWJWK
      XBWLWPWJWKXBUACAWCUFUGUHVRWTWSWEWLSZPXCVRWEXDWSVRWEWLUIWEXDUAWEVRWLAWCBFU
      JUKWEWLULUMUNWRWEWLUOUPUQURUSXAWJWSSZWJWESZPWIWJWSWEUTWFXEWEXFWFWJWRWLTZX
      EWPWQWFXGUAZCDWPAVANBVANXHWQAVBBVBABWCVCVDUCWJWRWLUDUEWEWJWEWPWJCAWCQVEVF
      VGVHVIVJVKWFWEVLVJWHWCVSNZWFPWGWCVSVTVMXIWEWFXIWCAOWEEAVNWCAVOUEVPUEUPVQ
      $.
      $( [15-Mar-2008] $)
  $}

  $( The closure of one end of an open real interval.  (Contributed by Paul
     Chapman, 15-Mar-2008.) $)
  snunioo $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
                  ( { A } u. ( A (,) B ) ) = ( A [,) B ) ) $=
    ( cr wcel clt wbr csn cioo co cun cico wceq wi cc0 cif breq1 sneq opreq1
    uneq12d eqeq12d imbi12d breq2 opreq2 uneq2d 0re elimel snunioolem dedth2h
    3impia ) ACDZBCDZABEFZAGZABHIZJZABKIZLZUJUKULUQMUJANOZBEFZURGZURBHIZJZURBKI
    ZLZMURUKBNOZEFZUTURVEHIZJZURVEKIZLZMABNNAURLZULUSUQVDAURBEPVKUOVBUPVCVKUMUT
    UNVAAURQAURBHRSAURBKRTUABVELZUSVFVDVJBVEUREUBVLVBVHVCVIVLVAVGUTBVEURHUCUDBV
    EURKUCTUAURVEANCUEUFBNCUEUFUGUHUI $.
    $( [15-Mar-2008] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Join two open intervals to create a third. $)
    ioojoin $p |- ( ( ( A e. RR /\ B e. RR /\ C e. RR ) /\ ( A < B /\ B < C )
           ) -> ( ( ( A (,) B ) u. { B } ) u. ( B (,) C ) ) = ( A (,) C ) ) $=
      ( vx cr wcel w3a clt wbr wa cioo co csn cun unass cico wceq snunioo
      3expa 3adantl1 adantrl uneq2d cv wo elun cle wb cxr rexr elioo2 syl2an
      3adant3 3anass syl6bb elico2 3adant1 orbi12d adantr wn ianor wi pm2.24
      ad2antrl lenlt biimprd 3ad2antl2 adantlr jaod syl5bi simpr a1d adantl
      jcad orrd ex axlttrn exp3a com23 3exp com4l imp31 imp anim2d ltletr
      exp5o com34 3adantl3 adantrr anim1d impbid pm5.32da syl5bb andi bitr4d
      3adant2 eqrdv eqtrd syl5eq ) AEFZBEFZCEFZGZABHIZBCHIZJZJZABKLZBMZNBCKLZNX
      GXHXINZNZACKLZXGXHXIOXFXKXGBCPLZNZXLXFXJXMXGXBXDXJXMQZXCWTXAXDXOWSWTXAXDX
      OBCRSTUAUBXFDXNXLXFDUCZXNFZXPEFZAXPHIZXPCHIZGZXPXLFZXQXPXGFZXPXMFZUDZXFYA
      XPXGXMUEXFYEXRXSXPBHIZJZJZXRBXPUFIZXTJZJZUDZYAXBYEYLUGXEXBYCYHYDYKXBYCXRX
      SYFGZYHWSWTYCYMUGZXAWSAUHFZBUHFYNWTAUIZBUIABXPUJUKULXRXSYFUMUNXBYDXRYIXTG
      ZYKWTXAYDYQUGWSBCXPUOUPXRYIXTUMUNUQURXFYAXRYGYJUDZJZYLYAXRXSXTJZJXFYSXRXS
      XTUMXFXRYTYRXFXRJZYTYRUUAYTYRUUAYTJZYGYJUUBYGUSZYIXTUUCXSUSZYFUSZUDUUBYIX
      SYFUTUUBUUDYIUUEXSUUDYIVAUUAXTXSYIVBVCUUAUUEYIVAZYTXBXRUUFXEWTWSXRUUFXAWT
      XRJYIUUEBXPVDVEVFVGURVHVIYTUUCXTVAUUAYTXTUUCXSXTVJVKVLVMVNVOUUAYGYTYJUUAY
      FXTXSXFXRYFXTVAZXBXDXRUUGVAZXCWTXAXDUUHWSWTXAXDUUHXRWTXAXDUUGXRWTXAXDUUGV
      AXRWTXAGZYFXDXTUUIYFXDXTXPBCVPVQVRVSVTWATUAWBWCUUAYIXSXTXFXRYIXSVAZXBXCXR
      UUJVAZXDWSWTXCUUKXAWSWTXCUUKWSWTXRXCUUJWSWTXRXCYIXSABXPWDWEWFWAWGWHWBWIVH
      WJWKWLXRYGYJWMUNWNWLXBYBYAUGZXEWSXAUULWTWSYOCUHFUULXAYPCUIACXPUJUKWOURWNW
      PWQWR $.
      $( [11-Aug-2008] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( The class difference of ` RR ` and a closed interval.  (Contributed by
       FL, 18-Jun-2007.) $)
    difreicc $p |- ( ( A e. RR /\ B e. RR ) -> ( RR \ ( A [,] B ) ) =
      U. { ( -oo (,) A ) , ( B (,) +oo ) } ) $=
      ( vx cr wcel wa cicc co cdif cmnf cioo cpnf cpr cuni cv wn eldif cun wo
      cxr cle wbr w3a wb rexr elicc1 syl2an adantr notbid 3anass notbii ianor
      wi pm2.24d adantl clt ad2antlr mnflt simpr simpll ltnle bicomd syl11anc
      biimpa 3jca mnfxr ad3antrrr elioo1 sylancr mpbird ex adantll ltpnf
      ad2antrr pnfxr sylancl sylbird orim12d syl5bi jaod sylbid expimpd elun
      syl6ibr ioossre unssi sseli elioo2 biimpd a1i com13 3impd xrltnle com3l
      com14 syl imp sylibr intnand sylnibr anim12i jca impbid cvv wceq oprex
      pm3.2i uniprg eqcomd eleq2d bitrd syl5bb eqrdv ) ADEZBDEZFZCDABGHZIZJAKHZ
      BLKHZMNZCOZXREYBDEZYBXQEZPZFZXPYBYAEZYBDXQQXPYFYBXSXTRZEZYGXPYFYIXPYFYBXS
      EZYBXTEZSZYIXPYCYEYLXPYCFZYEYBTEZAYBUAUBZYBBUAUBZUCZPZYLYMYDYQXPYDYQUDZYC
      XNATEZBTEZYSXOAUEZBUEZABYBUFZUGUHUIYRYNYOYPFZFZPZYMYLYQUUFYNYOYPUJZUKUUGY
      NPZUUEPZSYMYLYNUUEULYMUUIYLUUJYCUUIYLUMXPYCYNYLYBUEZUNUOUUJYOPZYPPZSZYMYL
      YOYPULZYMUULYJUUMYKYMUULYJYMUULFZYJYNJYBUPUBZYBAUPUBZUCZUUPYNUUQUURYCYNXP
      UULUUKUQYCUUQXPUULYBURUQYMUULUURYMYCXNUULUURUDXPYCUSXNXOYCUTYCXNFZUURUULY
      BAVAZVBVCVDVEUUPJTEZYTYJUUSUDVFXNYTXOYCUULUUBVGJAYBVHVIVJVKYMUUMBYBUPUBZY
      KXOYCUVCUUMUDXNBYBVAVLYMUVCYKYMUVCFZYKYNUVCYBLUPUBZUCZUVDYNUVCUVEYCYNXPUV
      CUUKUQYMUVCUSYCUVEXPUVCYBVMUQVEUVDUUALTEZYKUVFUDZXPUUAYCUVCXOUUAXNUUCUOZV
      NVOBLYBVHZVPVJVKVQVRVSVTVSVSWAWBYBXSXTWCZWDXPYIYFXPYIFZYCYEYIYCXPYHDYBXSX
      TDJAWEBLWEWFWGUOUVLYEYRUVLUUFYQUVLUUEYNUVLUUNUUJXPYIUUNYIYLXPUUNUVKXPYJUU
      LYKUUMXPYJYCUUQUURUCZUULXNYJUVMUDZXOXNUVBYTUVNVFUUBJAYBWHVIUHXPYCUUQUURUU
      LXNYCUUQUURUULUMZUMUMXOUUQYCXNUVOYCXNUVOUMUMUUQYCXNUVOUUTUURUULUVAWIVKWJW
      KUHWLWAXPYKUVFUUMXPUUAUVGUVHUVIVOUVJVPXPYNUVCUVEUUMXOYNUVCUVEUUMUMUMUMZXN
      XOUUAUVPUUCUVEYNUVCUUAUUMYNUVCUUAUUMUMUMUMUVEUUAYNUVCUUMUUAYNUVCUUMUMUUAY
      NFUVCUUMBYBWMWIVKWNWJWOWPUOWLWAVRVSWQUUOWRWSUUHWTUVLYTUUAFZYEYRUDXPUVQYIX
      NYTXOUUAUUBUUCXAUHUVQYDYQUUDUIWPVJXBVKXCXPYHYAYBXPXSXDEZXTXDEZFZYHYAXEUVT
      XPUVRUVSJAKXFBLKXFXGWJUVTYAYHXSXTXDXDXHXIWPXJXKXLXM $.
      $( [1-Dec-2010] $) $( [7-Jun-2007] $)
  $}

