$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Real number intervals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c (,) $.
  $c (,] $.
  $c [,) $.
  $c [,] $.

  $( Extend class notation with the set of open intervals of extended reals. $)
  cioo $a class (,) $.

  $( Extend class notation with the set of open-below, closed-above intervals
     of extended reals. $)
  cioc $a class (,] $.

  $( Extend class notation with the set of closed-below, open-above
     intervals of extended reals. $)
  cico $a class [,) $.

  $( Extend class notation with the set of closed intervals of extended
     reals. $)
  cicc $a class [,] $.

  ${
    $d x y z w $.
    $( Define the set of open intervals of extended reals. $)
    df-ioo $a |- (,) = { <. <. x , y >. , z >. | ( ( x e. RR* /\ y e. RR* )
                   /\ z = { w e. RR* | ( x < w /\ w < y ) } ) } $.

    $( Define the set of open-below, closed-above intervals of extended
       reals. $)
    df-ioc $a |- (,] = { <. <. x , y >. , z >. | ( ( x e. RR* /\ y e. RR* )
                   /\ z = { w e. RR* | ( x < w /\ w <_ y ) } ) } $.

    $( Define the set of closed-below, open-above intervals of extended
       reals. $)
    df-ico $a |- [,) = { <. <. x , y >. , z >. | ( ( x e. RR* /\ y e. RR* )
                   /\ z = { w e. RR* | ( x <_ w /\ w < y ) } ) } $.

    $( Define the set of closed intervals of extended reals. $)
    df-icc $a |- [,] = { <. <. x , y >. , z >. | ( ( x e. RR* /\ y e. RR* )
                   /\ z = { w e. RR* | ( x <_ w /\ w <_ y ) } ) } $.
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d x C $.  $d x D $.
    $( The set of open intervals of extended reals is a set. $)
    iooex $p |- (,) e. V $=
      ( vx vy vz vw cxr cv clt wbr wa crab cioo xrex df-ioo oprabex2 ) ABCEEAFD
      FZGHOBFGHIDEJKLLABCDMN $.
      $( [11-Feb-2007] $) $( [6-Feb-2007] $)

    $( The value of a set of open intervals of extended reals. $)
    ioovalt $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A (,) B ) =
                 { x e. RR* | ( A < x /\ x < B ) } ) $=
      ( vy vz vw cxr cv clt wbr wa crab cioo xrex rabex wceq breq1 anbi1d
      rabbisdv breq2 anbi2d df-ioo oprabval2 ) DEFBCGGDHZAHZIJZUEEHZIJZKZAGLBUE
      IJZUECIJZKZAGLMUJUHKZAGLULAGNOUDBPZUIUMAGUNUFUJUHUDBUEIQRSUGCPZUMULAGUOUH
      UKUJUGCUEITUASDEFAUBUC $.
      $( [27-Dec-2006] $) $( [24-Dec-2006] $)

    $( The value of a set of open intervals of extended reals. $)
    iooval2t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A (,) B ) =
                 { x e. RR | ( A < x /\ x < B ) } ) $=
      ( cxr wcel wa cioo co cv clt wbr crab cr ioovalt cab wi w3a xrre2t ex
      3com23 3exp imp4b pm3.27 a1i jcad rexrt anim1i impbid1 abbidv df-rab
      3eqtr4g eqtrd ) BDEZCDEZFZBCGHBAIZJKUPCJKFZADLZUQAMLZABCNUOUPDEZUQFZAOUPM
      EZUQFZAOURUSUOVAVCAUOVAVCUOVAVBUQUMUNUTUQVBUMUNUTUQVBPZUMUTUNVDUMUTUNQUQV
      BBUPCRSTUAUBVAUQPUOUTUQUCUDUEVBUTUQUPUFUGUHUIUQADUJUQAMUJUKUL $.
      $( [7-Feb-2007] $) $( [6-Feb-2007] $)

    $( An empty open intervals of extended reals. $)
    ioo0t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( ( A (,) B ) = (/) <->
                 B <_ A ) ) $=
      ( vx cxr wcel wa cioo co c0 wceq cv clt wbr crab cle ioovalt eqeq1d wn
      wrex wi xrlttrt 3com23 3expa r19.23adva w3a cq qbtwnxr cr qret rexrt syl
      anim1i r19.22i2 ex impbid wne df-ne rabn0 bitr3 syl5bb xrltnlet bitrd
      con4bid ) ADEZBDEZFZABGHZIJACKZLMVHBLMFZCDNZIJZBAOMZVFVGVJICABPQVFVKVLVFV
      KRZABLMZVLRVFVICDSZVNVMVFVOVNVFVIVNCDVDVEVHDEZVIVNTZVDVPVEVQAVHBUAUBUCUDV
      FVNVOVDVEVNVOVDVEVNUEVICUFSVOCABUGVIVICUFDVHUFEZVPVIVRVHUHEVPVHUIVHUJUKUL
      UMUKUCUNUOVMVJIUPVOVJIUQVICDURUSUTABVAVBVCVB $.
      $( [24-Feb-2007] $) $( [6-Feb-2007] $)

    $( An open interval of extended reals is nonempty iff the lower argument is
       less than the upper argument. $)
    ioon0t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( ( A (,) B ) =/= (/) <->
                 A < B ) ) $=
      ( cxr wcel wa cioo co c0 wceq wn clt wbr wne cle ioo0t wb xrlenltt
      ancoms bitr2d con1bid df-ne syl5bb ) ACDZBCDZEZABFGZHIZJABKLZUFHMUEUHUGUE
      UGBANLZUHJZABOUDUCUIUJPBAQRSTUFHUAUB $.
      $( [3-Mar-2007] $) $( [2-Mar-2007] $)

    $( Intersection of two open intervals of extended reals. $)
    iooint $p |- ( ( ( A e. RR* /\ B e. RR* ) /\ ( C e. RR* /\ D e. RR* ) ) ->
         ( ( A (,) B ) i^i ( C (,) D ) ) =
         ( if ( A <_ C , C , A ) (,) if ( B <_ D , B , D ) ) ) $=
      ( vx cxr wcel wa cv clt wbr crab cin cle cif cioo co wceq wb xrmaxltt
      3expa adantlr xrltmint 3coml adantll anbi12d an4 syl6bb rabbidv an4s
      inrab syl6reqr ioovalt ineqan12d ifcl ancoms ad2ant2r ad2ant2l sylanc
      3eqtr4d ) AFGZBFGZHZCFGZDFGZHZHZAEIZJKZVHBJKZHZEFLZCVHJKZVHDJKZHZEFLZMZAC
      NKZCAOZVHJKZVHBDNKZBDOZJKZHZEFLZABPQZCDPQZMVSWBPQZVGWEVKVOHZEFLZVQVAVDVBV
      EWEWJRVAVDHZVBVEHZHZWDWIEFWMVHFGZHZWDVIVMHZVJVNHZHWIWOVTWPWCWQWKWNVTWPSZW
      LVAVDWNWRACVHTUAUBWLWNWCWQSZWKVBVEWNWSWNVBVEWSVHBDUCUDUAUEUFVIVMVJVNUGUHU
      IUJVKVOEFUKULVCVFWFVLWGVPEABUMECDUMUNVSFGZWBFGZWHWERVGEVSWBUMVAVDWTVBVEVD
      VAWTVRCAFUOUPUQVBVEXAVAVDWABDFUOURUSUT $.
      $( [12-Feb-2007] $) $( [7-Feb-2007] $)

    $( Subset relationship for open intervals of extended reals. $)
    iooss1 $p |- ( ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\ A <_ B ) ->
                 ( B (,) C ) (_ ( A (,) C ) ) $=
      ( vx cxr wcel w3a cle wbr wa cv clt crab cioo co wss wi xrlelttrt exp3a
      3expa imp an1rs anim1d ss2rabdv 3adantl3 wceq ioovalt 3adant1 adantr
      3adant2 3sstr4d ) AEFZBEFZCEFZGZABHIZJBDKZLIZUQCLIZJZDEMZAUQLIZUSJZDEMZBC
      NOZACNOZULUMUPVAVDPUNULUMJZUPJZUTVCDEVHUQEFZJURVBUSVGVIUPURVBQZVGVIJUPVJU
      LUMVIUPVJQULUMVIGUPURVBABUQRSTUAUBUCUDUEUOVEVAUFZUPUMUNVKULDBCUGUHUIUOVFV
      DUFZUPULUNVLUMDACUGUJUIUK $.
      $( [12-Feb-2007] $) $( [7-Feb-2007] $)

    $( Subset relationship for open intervals of extended reals. $)
    iooss2 $p |- ( ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\ B <_ C ) ->
                 ( A (,) B ) (_ ( A (,) C ) ) $=
      ( vx cxr wcel w3a cle wbr wa cv clt crab cioo co wss wi xrltletrt 3coml
      ancomsd exp3a 3expa imp an1rs anim2d ss2rabdv 3adantl1 wceq ioovalt
      3adant3 adantr 3adant2 3sstr4d ) AEFZBEFZCEFZGZBCHIZJADKZLIZUSBLIZJZDEMZU
      TUSCLIZJZDEMZABNOZACNOZUOUPURVCVFPUNUOUPJZURJZVBVEDEVJUSEFZJVAVDUTVIVKURV
      AVDQZVIVKJURVLUOUPVKURVLQUOUPVKGZURVAVDVMVAURVDVKUOUPVAURJVDQUSBCRSTUAUBU
      CUDUEUFUGUQVGVCUHZURUNUOVNUPDABUIUJUKUQVHVFUHZURUNUPVOUODACUIULUKUM $.
      $( [12-Feb-2007] $) $( [7-Feb-2007] $)

    $( The value of a set of open-below, closed-above intervals of extended
       reals. $)
    iocvalt $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A (,] B ) =
                 { x e. RR* | ( A < x /\ x <_ B ) } ) $=
      ( vy vz vw cxr cv clt wbr cle wa crab cioc xrex rabex wceq breq1 anbi1d
      rabbisdv breq2 anbi2d df-ioc oprabval2 ) DEFBCGGDHZAHZIJZUFEHZKJZLZAGMBUF
      IJZUFCKJZLZAGMNUKUILZAGMUMAGOPUEBQZUJUNAGUOUGUKUIUEBUFIRSTUHCQZUNUMAGUPUI
      ULUKUHCUFKUAUBTDEFAUCUD $.
      $( [27-Dec-2006] $) $( [24-Dec-2006] $)

    $( The value of a set of closed-below, open-above intervals of extended
       reals. $)
    icovalt $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A [,) B ) =
                 { x e. RR* | ( A <_ x /\ x < B ) } ) $=
      ( vy vz vw cxr cv cle wbr clt wa crab cico xrex rabex wceq breq1 anbi1d
      rabbisdv breq2 anbi2d df-ico oprabval2 ) DEFBCGGDHZAHZIJZUFEHZKJZLZAGMBUF
      IJZUFCKJZLZAGMNUKUILZAGMUMAGOPUEBQZUJUNAGUOUGUKUIUEBUFIRSTUHCQZUNUMAGUPUI
      ULUKUHCUFKUAUBTDEFAUCUD $.
      $( [27-Dec-2006] $) $( [24-Dec-2006] $)

    $( The value of a set of closed intervals of extended reals. $)
    iccvalt $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A [,] B ) =
                 { x e. RR* | ( A <_ x /\ x <_ B ) } ) $=
      ( vy vz vw cxr cv cle wbr wa crab cicc xrex rabex wceq breq1 anbi1d
      rabbisdv breq2 anbi2d df-icc oprabval2 ) DEFBCGGDHZAHZIJZUEEHZIJZKZAGLBUE
      IJZUECIJZKZAGLMUJUHKZAGLULAGNOUDBPZUIUMAGUNUFUJUHUDBUEIQRSUGCPZUMULAGUOUH
      UKUJUGCUEITUASDEFAUBUC $.
      $( [27-Dec-2006] $) $( [24-Dec-2006] $)
  $}

  ${
    $d x C $.  $d x A $.  $d x B $.
    $( Membership in a set of open intervals of extended reals. $)
    elioo1t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A (,) B ) <->
                 ( C e. RR* /\ A < C /\ C < B ) ) ) $=
      ( vx cxr wcel wa cioo co cv clt wbr crab w3a ioovalt eleq2d wceq breq2
      breq1 anbi12d elrab 3anass bitr4 syl6bb ) AEFBEFGZCABHIZFCADJZKLZUGBKLZGZ
      DEMZFZCEFZACKLZCBKLZNZUEUFUKCDABOPULUMUNUOGZGUPUJUQDCEUGCQUHUNUIUOUGCAKRU
      GCBKSTUAUMUNUOUBUCUD $.
      $( [27-Dec-2006] $) $( [24-Dec-2006] $)

    $( Membership in a set of open intervals of extended reals. $)
    elioo2t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A (,) B ) <->
                 ( C e. RR /\ A < C /\ C < B ) ) ) $=
      ( vx cxr wcel wa cioo co cv clt wbr cr crab w3a iooval2t eleq2d wceq
      breq2 breq1 anbi12d elrab 3anass bitr4 syl6bb ) AEFBEFGZCABHIZFCADJZKLZUH
      BKLZGZDMNZFZCMFZACKLZCBKLZOZUFUGULCDABPQUMUNUOUPGZGUQUKURDCMUHCRUIUOUJUPU
      HCAKSUHCBKTUAUBUNUOUPUCUDUE $.
      $( [28-Apr-2007] $) $( [6-Feb-2007] $)

    $( Membership in a set open-below, closed-above intervals of extended
       reals. $)
    elioc1t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A (,] B ) <->
                 ( C e. RR* /\ A < C /\ C <_ B ) ) ) $=
      ( vx cxr wcel wa cioc co cv clt wbr cle crab w3a iocvalt eleq2d wceq
      breq2 breq1 anbi12d elrab 3anass bitr4 syl6bb ) AEFBEFGZCABHIZFCADJZKLZUH
      BMLZGZDENZFZCEFZACKLZCBMLZOZUFUGULCDABPQUMUNUOUPGZGUQUKURDCEUHCRUIUOUJUPU
      HCAKSUHCBMTUAUBUNUOUPUCUDUE $.
      $( [28-Dec-2006] $) $( [24-Dec-2006] $)

    $( Membership in a set of closed-below, open-above intervals of extended
       reals. $)
    elico1t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A [,) B ) <->
                 ( C e. RR* /\ A <_ C /\ C < B ) ) ) $=
      ( vx cxr wcel wa cico co cv cle wbr clt crab w3a icovalt eleq2d wceq
      breq2 breq1 anbi12d elrab 3anass bitr4 syl6bb ) AEFBEFGZCABHIZFCADJZKLZUH
      BMLZGZDENZFZCEFZACKLZCBMLZOZUFUGULCDABPQUMUNUOUPGZGUQUKURDCEUHCRUIUOUJUPU
      HCAKSUHCBMTUAUBUNUOUPUCUDUE $.
      $( [28-Dec-2006] $) $( [24-Dec-2006] $)

    $( Membership in a set of closed intervals of extended reals. $)
    elicc1t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A [,] B ) <->
                 ( C e. RR* /\ A <_ C /\ C <_ B ) ) ) $=
      ( vx cxr wcel wa cicc co cv cle wbr crab w3a iccvalt eleq2d wceq breq2
      breq1 anbi12d elrab 3anass bitr4 syl6bb ) AEFBEFGZCABHIZFCADJZKLZUGBKLZGZ
      DEMZFZCEFZACKLZCBKLZNZUEUFUKCDABOPULUMUNUOGZGUPUJUQDCEUGCQUHUNUIUOUGCAKRU
      GCBKSTUAUMUNUOUBUCUD $.
      $( [28-Dec-2006] $) $( [24-Dec-2006] $)
  $}

  $( The open interval from minus to plus infinity. $)
  ioomax $p |- ( -oo (,) +oo ) = RR $=
    ( vx cmnf cpnf cioo co cv clt wbr wa cr crab cxr wcel wceq mnfxr pnfxr
    iooval2t mp2an rabid2 mnfltt ltpnft jca mprgbir eqtr4 ) BCDEZBAFZGHZUFCGHZI
    ZAJKZJBLMCLMUEUJNOPABCQRJUJNUIAJUIAJSUFJMUGUHUFTUFUAUBUCUD $.
    $( [8-Feb-2007] $) $( [6-Feb-2007] $)

  ${
    $d w x y z A $.  $d w x y z B $.
    $( The set of open intervals of extended reals maps to subsets of reals. $)
    ioof $p |- (,) : ( RR* X. RR* ) --> P~ RR $=
      ( vx vw vy vz cv clt wbr wa cxr crab cr cpw wcel wral cxp cioo wf co
      ioovalt wss cmnf cpnf cle mnflet adantr mnfxr iooss1 mp3anl1 mpdan
      pnfget pnfxr iooss2 mp3anl3 mpanl1 adantl sstrd ioomax syl6ss oprex elpw
      sylibr eqeltrrd rgen2 df-ioo foprab2 mpbi ) AEZBEZFGVHCEZFGHBIJZKLZMZCINA
      INIIOVKPQVLACIVGIMZVIIMZHZVGVIPRZVJVKBVGVISVOVPKTVPVKMVOVPUAUBPRZKVOVPUAV
      IPRZVQVOUAVGUCGZVPVRTZVMVSVNVGUDUEUAIMZVMVNVSVTUFUAVGVIUGUHUIVNVRVQTZVMVN
      VIUBUCGZWBVIUJWAVNWCWBUFWAVNUBIMWCWBUKUAVIUBULUMUNUIUOUPUQURVPKVGVIPUSUTV
      AVBVCACDIIVJVKPACDBVDVEVF $.
      $( [12-Feb-2007] $) $( [7-Feb-2007] $)

    $( Alternate definition of the set of open intervals of extended reals. $)
    dfioo2 $p |- (,) = { <. <. x , y >. , z >. | ( ( x e. RR* /\ y e. RR* )
                   /\ z = { w e. RR | ( x < w /\ w < y ) } ) } $=
      ( cioo cv cxr wcel wa co wceq copab2 clt wbr cr crab cxp wfn cpw wf ioof
      ffn ax-mp fnoprval mpbi iooval2t eqeq2d pm5.32i oprabbii eqtr ) EAFZGHBFZ
      GHIZCFZUKULEJZKZIZABCLZUMUNUKDFZMNUSULMNIDOPZKZIZABCLEGGQZRZEURKVCOSZETVD
      UAVCVEEUBUCABCGGEUDUEUQVBABCUMUPVAUMUOUTUNDUKULUFUGUHUIUJ $.
      $( [6-Mar-2007] $) $( [1-Mar-2007] $)

    $( Membership in a set of open intervals of extended reals.  We use the
       fact that an operation's value is empty outside of its domain to show
       ` A e. RR* ` and ` B e. RR* ` . $)
    elioo3t $p |- ( B e. A -> ( C e. ( A (,) B ) <->
          ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\ ( A < C /\ C < B ) ) ) ) $=
      ( vx vy vz vw wcel cioo co cxr wa clt wbr w3a wb elioo1t ibar 3anass
      syl5bb bitrd adantl wn c0 cv crab xrex rabex df-ioo dmoprab2 ndmoprg
      eleq2d noel pm2.21i pm3.26 pm5.21ni pm2.61dan df-3an anbi1i anass bitr2
      syl6bb ) BAHZCABIJZHZAKHZBKHZLZCKHZACMNZCBMNZLZLZLZVFVGVIOZVLLZVCVHVEVNPZ
      VHVQVCVHVEVIVJVKOZVNABCQVHVMVNVRVHVMRVIVJVKSTUAUBVCVHUCZLZVECUDHZVNVTVDUD
      CABAKIDEFKKDUEGUEZMNWBEUEMNLZGKUFIWCGKUGUHDEFGUIUJUKULVSWAVNPVCWAVHVNWAVH
      CUMUNVHVMUOUPUBUAUQVPVHVILZVLLVNVOWDVLVFVGVIURUSVHVIVLUTVAVB $.
      $( [?] $) $( [24-Dec-2006] $)
  $}


