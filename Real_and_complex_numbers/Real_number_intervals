$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Real number intervals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c (,) $.
  $c (,] $.
  $c [,) $.
  $c [,] $.

  $( Extend class notation with the set of open intervals of extended reals. $)
  cioo $a class (,) $.

  $( Extend class notation with the set of open-below, closed-above intervals
     of extended reals. $)
  cioc $a class (,] $.

  $( Extend class notation with the set of closed-below, open-above
     intervals of extended reals. $)
  cico $a class [,) $.

  $( Extend class notation with the set of closed intervals of extended
     reals. $)
  cicc $a class [,] $.

  ${
    $d x y z w $.
    $( Define the set of open intervals of extended reals. $)
    df-ioo $a |- (,) = { <. <. x , y >. , z >. | ( ( x e. RR* /\ y e. RR* )
                   /\ z = { w e. RR* | ( x < w /\ w < y ) } ) } $.

    $( Define the set of open-below, closed-above intervals of extended
       reals. $)
    df-ioc $a |- (,] = { <. <. x , y >. , z >. | ( ( x e. RR* /\ y e. RR* )
                   /\ z = { w e. RR* | ( x < w /\ w <_ y ) } ) } $.

    $( Define the set of closed-below, open-above intervals of extended
       reals. $)
    df-ico $a |- [,) = { <. <. x , y >. , z >. | ( ( x e. RR* /\ y e. RR* )
                   /\ z = { w e. RR* | ( x <_ w /\ w < y ) } ) } $.

    $( Define the set of closed intervals of extended reals. $)
    df-icc $a |- [,] = { <. <. x , y >. , z >. | ( ( x e. RR* /\ y e. RR* )
                   /\ z = { w e. RR* | ( x <_ w /\ w <_ y ) } ) } $.
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d x C $.  $d x D $.
    $( The set of open intervals of extended reals exists. $)
    iooex $p |- (,) e. V $=
      ( vx vy vz vw cxr cv clt wbr wa crab cioo xrex df-ioo oprabex2 ) ABCEEAFD
      FZGHOBFGHIDEJKLLABCDMN $.
      $( [11-Feb-2007] $) $( [6-Feb-2007] $)

    $( Value of the open interval function. $)
    ioovalt $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A (,) B ) =
                 { x e. RR* | ( A < x /\ x < B ) } ) $=
      ( vy vz vw cxr cv clt wbr wa crab cioo xrex rabex wceq breq1 anbi1d
      rabbisdv breq2 anbi2d df-ioo oprabval2 ) DEFBCGGDHZAHZIJZUEEHZIJZKZAGLBUE
      IJZUECIJZKZAGLMUJUHKZAGLULAGNOUDBPZUIUMAGUNUFUJUHUDBUEIQRSUGCPZUMULAGUOUH
      UKUJUGCUEITUASDEFAUBUC $.
      $( [27-Dec-2006] $) $( [24-Dec-2006] $)

    $( Value of the open interval function. $)
    iooval2t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A (,) B ) =
                 { x e. RR | ( A < x /\ x < B ) } ) $=
      ( cxr wcel wa cioo co cv clt wbr crab cr ioovalt cab wi w3a xrre2t ex
      3com23 3exp imp4b pm3.27 a1i jcad rexrt anim1i impbid1 abbidv df-rab
      3eqtr4g eqtrd ) BDEZCDEZFZBCGHBAIZJKUPCJKFZADLZUQAMLZABCNUOUPDEZUQFZAOUPM
      EZUQFZAOURUSUOVAVCAUOVAVCUOVAVBUQUMUNUTUQVBUMUNUTUQVBPZUMUTUNVDUMUTUNQUQV
      BBUPCRSTUAUBVAUQPUOUTUQUCUDUEVBUTUQUPUFUGUHUIUQADUJUQAMUJUKUL $.
      $( [7-Feb-2007] $) $( [6-Feb-2007] $)

    $( An empty open interval of extended reals. $)
    ioo0t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( ( A (,) B ) = (/) <->
                 B <_ A ) ) $=
      ( vx cxr wcel wa cioo co c0 wceq cv clt wbr crab cle ioovalt eqeq1d wn
      wrex wi xrlttrt 3com23 3expa r19.23adva w3a cq qbtwnxr cr qret rexrt syl
      anim1i r19.22i2 3expia impbid wne df-ne rabn0 bitr3 syl5bb xrltnlet
      bitrd con4bid ) ADEZBDEZFZABGHZIJACKZLMVHBLMFZCDNZIJZBAOMZVFVGVJICABPQVFV
      KVLVFVKRZABLMZVLRVFVICDSZVNVMVFVOVNVFVIVNCDVDVEVHDEZVIVNTZVDVPVEVQAVHBUAU
      BUCUDVDVEVNVOVDVEVNUEVICUFSVOCABUGVIVICUFDVHUFEZVPVIVRVHUHEVPVHUIVHUJUKUL
      UMUKUNUOVMVJIUPVOVJIUQVICDURUSUTABVAVBVCVB $.
      $( [24-Feb-2007] $) $( [6-Feb-2007] $)

    $( An open interval of extended reals is nonempty iff the lower argument is
       less than the upper argument. $)
    ioon0t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( ( A (,) B ) =/= (/) <->
                 A < B ) ) $=
      ( cxr wcel wa clt wbr cioo co c0 wceq cle wn ioo0t wb xrlenltt ancoms
      bitr2d necon1abid ) ACDZBCDZEZABFGZABHIZJUBUDJKBALGZUCMZABNUATUEUFOBAPQRS
      $.
      $( [3-Mar-2007] $) $( [2-Mar-2007] $)

    $( The open interval function's value is empty outside of its domain. $)
    ndmioo $p |- ( -. ( A e. RR* /\ B e. RR* ) -> ( A (,) B ) = (/) ) $=
      ( vx vy vz vw cxr wcel wa wn cvv cioo co c0 wceq cv clt wbr crab xrex
      rabex df-ioo dmoprab2 ndmoprgOLD expcom oprprc2 cle xrleidt adantr ioo0t
      mpbird cdm wrel cxp relxp releqi mpbir oprprc1 pm2.61d1 pm2.61i syl6eq )
      AGHZBGHIJZBKHZABLMZNOZVDVCVFABKGLCDEGGCPFPZQRVGDPQRIZFGSLVHFGTUACDEFUBUCZ
      UDUEVDJVEAALMZNABLUFVBVBIZVJNOZVKVLAAUGRZVBVMVBAUHUIAAUJUKVKJZAKHZVLVOVNV
      LAAKGLVIUDUEAALLULZUMGGUNZUMGGUOVPVQVIUPUQURUSUTVAUS $.
      $( [22-Jun-2007] $) $( [21-Jun-2007] $)

    $( An open interval with identical lower and upper bounds is empty. $)
    iooid $p |- ( A (,) A ) = (/) $=
      ( cxr wcel wa cioo co c0 wceq cle wbr xrleidt adantr ioo0t mpbird ndmioo
      pm2.61i ) ABCZQDZAAEFGHZRSAAIJZQTQAKLAAMNAAOP $.
      $( [21-Jun-2007] $) $( [21-Jun-2007] $)

    $( Intersection of two open intervals of extended reals. $)
    iooint $p |- ( ( ( A e. RR* /\ B e. RR* ) /\ ( C e. RR* /\ D e. RR* ) ) ->
         ( ( A (,) B ) i^i ( C (,) D ) ) =
         ( if ( A <_ C , C , A ) (,) if ( B <_ D , B , D ) ) ) $=
      ( vx cxr wcel wa cv clt wbr crab cin cle cif cioo co wceq wb xrmaxltt
      3expa adantlr xrltmint 3coml adantll anbi12d an4 syl6bb rabbidv an4s
      inrab syl6reqr ioovalt ineqan12d ifcl ancoms ad2ant2r ad2ant2l sylanc
      3eqtr4d ) AFGZBFGZHZCFGZDFGZHZHZAEIZJKZVHBJKZHZEFLZCVHJKZVHDJKZHZEFLZMZAC
      NKZCAOZVHJKZVHBDNKZBDOZJKZHZEFLZABPQZCDPQZMVSWBPQZVGWEVKVOHZEFLZVQVAVDVBV
      EWEWJRVAVDHZVBVEHZHZWDWIEFWMVHFGZHZWDVIVMHZVJVNHZHWIWOVTWPWCWQWKWNVTWPSZW
      LVAVDWNWRACVHTUAUBWLWNWCWQSZWKVBVEWNWSWNVBVEWSVHBDUCUDUAUEUFVIVMVJVNUGUHU
      IUJVKVOEFUKULVCVFWFVLWGVPEABUMECDUMUNVSFGZWBFGZWHWERVGEVSWBUMVAVDWTVBVEVD
      VAWTVRCAFUOUPUQVBVEXAVAVDWABDFUOURUSUT $.
      $( [12-Feb-2007] $) $( [7-Feb-2007] $)

    $( Subset relationship for open intervals of extended reals. $)
    iooss1 $p |- ( ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\ A <_ B ) ->
                 ( B (,) C ) (_ ( A (,) C ) ) $=
      ( vx cxr wcel w3a cle wbr wa cv clt crab cioo co wss wi xrlelttrt exp3a
      3expa imp an1rs anim1d ss2rabdv 3adantl3 wceq ioovalt 3adant1 adantr
      3adant2 3sstr4d ) AEFZBEFZCEFZGZABHIZJBDKZLIZUQCLIZJZDEMZAUQLIZUSJZDEMZBC
      NOZACNOZULUMUPVAVDPUNULUMJZUPJZUTVCDEVHUQEFZJURVBUSVGVIUPURVBQZVGVIJUPVJU
      LUMVIUPVJQULUMVIGUPURVBABUQRSTUAUBUCUDUEUOVEVAUFZUPUMUNVKULDBCUGUHUIUOVFV
      DUFZUPULUNVLUMDACUGUJUIUK $.
      $( [12-Feb-2007] $) $( [7-Feb-2007] $)

    $( Subset relationship for open intervals of extended reals. $)
    iooss2 $p |- ( ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\ B <_ C ) ->
                 ( A (,) B ) (_ ( A (,) C ) ) $=
      ( vx cxr wcel w3a cle wbr wa cv clt crab cioo co wss wi xrltletrt 3coml
      ancomsd exp3a 3expa imp an1rs anim2d ss2rabdv 3adantl1 wceq ioovalt
      3adant3 adantr 3adant2 3sstr4d ) AEFZBEFZCEFZGZBCHIZJADKZLIZUSBLIZJZDEMZU
      TUSCLIZJZDEMZABNOZACNOZUOUPURVCVFPUNUOUPJZURJZVBVEDEVJUSEFZJVAVDUTVIVKURV
      AVDQZVIVKJURVLUOUPVKURVLQUOUPVKGZURVAVDVMVAURVDVKUOUPVAURJVDQUSBCRSTUAUBU
      CUDUEUFUGUQVGVCUHZURUNUOVNUPDABUIUJUKUQVHVFUHZURUNUPVOUODACUIULUKUM $.
      $( [12-Feb-2007] $) $( [7-Feb-2007] $)

    $( Value of the open-below, closed-above interval function. $)
    iocvalt $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A (,] B ) =
                 { x e. RR* | ( A < x /\ x <_ B ) } ) $=
      ( vy vz vw cxr cv clt wbr cle wa crab cioc xrex rabex wceq breq1 anbi1d
      rabbisdv breq2 anbi2d df-ioc oprabval2 ) DEFBCGGDHZAHZIJZUFEHZKJZLZAGMBUF
      IJZUFCKJZLZAGMNUKUILZAGMUMAGOPUEBQZUJUNAGUOUGUKUIUEBUFIRSTUHCQZUNUMAGUPUI
      ULUKUHCUFKUAUBTDEFAUCUD $.
      $( [27-Dec-2006] $) $( [24-Dec-2006] $)

    $( Value of the closed-below, open-above interval function. $)
    icovalt $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A [,) B ) =
                 { x e. RR* | ( A <_ x /\ x < B ) } ) $=
      ( vy vz vw cxr cv cle wbr clt wa crab cico xrex rabex wceq breq1 anbi1d
      rabbisdv breq2 anbi2d df-ico oprabval2 ) DEFBCGGDHZAHZIJZUFEHZKJZLZAGMBUF
      IJZUFCKJZLZAGMNUKUILZAGMUMAGOPUEBQZUJUNAGUOUGUKUIUEBUFIRSTUHCQZUNUMAGUPUI
      ULUKUHCUFKUAUBTDEFAUCUD $.
      $( [27-Dec-2006] $) $( [24-Dec-2006] $)

    $( Value of the closed interval function. $)
    iccvalt $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A [,] B ) =
                 { x e. RR* | ( A <_ x /\ x <_ B ) } ) $=
      ( vy vz vw cxr cv cle wbr wa crab cicc xrex rabex wceq breq1 anbi1d
      rabbisdv breq2 anbi2d df-icc oprabval2 ) DEFBCGGDHZAHZIJZUEEHZIJZKZAGLBUE
      IJZUECIJZKZAGLMUJUHKZAGLULAGNOUDBPZUIUMAGUNUFUJUHUDBUEIQRSUGCPZUMULAGUOUH
      UKUJUGCUEITUASDEFAUBUC $.
      $( [27-Dec-2006] $) $( [24-Dec-2006] $)

    $( Membership in an open interval of extended reals. $)
    elioo1t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A (,) B ) <->
                 ( C e. RR* /\ A < C /\ C < B ) ) ) $=
      ( vx cxr wcel wa cioo co cv clt wbr crab w3a ioovalt eleq2d wceq breq2
      breq1 anbi12d elrab 3anass bitr4 syl6bb ) AEFBEFGZCABHIZFCADJZKLZUGBKLZGZ
      DEMZFZCEFZACKLZCBKLZNZUEUFUKCDABOPULUMUNUOGZGUPUJUQDCEUGCQUHUNUIUOUGCAKRU
      GCBKSTUAUMUNUOUBUCUD $.
      $( [27-Dec-2006] $) $( [24-Dec-2006] $)

    $( Membership in an open interval of extended reals. $)
    elioo2t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A (,) B ) <->
                 ( C e. RR /\ A < C /\ C < B ) ) ) $=
      ( vx cxr wcel wa cioo co cv clt wbr cr crab w3a iooval2t eleq2d wceq
      breq2 breq1 anbi12d elrab 3anass bitr4 syl6bb ) AEFBEFGZCABHIZFCADJZKLZUH
      BKLZGZDMNZFZCMFZACKLZCBKLZOZUFUGULCDABPQUMUNUOUPGZGUQUKURDCMUHCRUIUOUJUPU
      HCAKSUHCBKTUAUBUNUOUPUCUDUE $.
      $( [28-Apr-2007] $) $( [6-Feb-2007] $)

    $( Membership in a set of open intervals of extended reals.  We use the
       fact that an operation's value is empty outside of its domain to show
       ` A e. RR* ` and ` B e. RR* ` . $)
    elioo3g $p |- ( B e. D -> ( C e. ( A (,) B ) <->
          ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\ ( A < C /\ C < B ) ) ) ) $=
      ( vx vy vz vw wcel cioo co cxr wa clt wbr w3a wb elioo1t ibar 3anass
      syl5bb bitrd adantl wn c0 cv crab xrex rabex df-ioo dmoprab2 ndmoprgOLD
      eleq2d noel pm2.21i pm3.26 pm5.21ni pm2.61dan df-3an anbi1i anass bitr2
      syl6bb ) BDIZCABJKZIZALIZBLIZMZCLIZACNOZCBNOZMZMZMZVGVHVJPZVMMZVDVIVFVOQZ
      VIVRVDVIVFVJVKVLPZVOABCRVIVNVOVSVIVNSVJVKVLTUAUBUCVDVIUDZMZVFCUEIZVOWAVEU
      ECABDLJEFGLLEUFHUFZNOWCFUFNOMZHLUGJWDHLUHUIEFGHUJUKULUMVTWBVOQVDWBVIVOWBV
      ICUNUOVIVNUPUQUCUBURVQVIVJMZVMMVOVPWEVMVGVHVJUSUTVIVJVMVAVBVC $.
      $( [7-Jun-2007] $) $( [24-Dec-2006] $)

    $( Membership in an open interval of extended reals. $)
    elioo4g $p |- ( B e. D -> ( C e. ( A (,) B ) <->
          ( ( A e. RR* /\ B e. RR* /\ C e. RR ) /\ ( A < C /\ C < B ) ) ) ) $=
      ( wcel cioo co cxr w3a clt wbr wa cr elioo3g 3simp1 adantr 3simp2 wi
      xrre2t ex 3com23 imp 3jca pm3.27 jca id rexrt 3anim123i anim1i impbi
      syl6bb ) BDECABFGEAHEZBHEZCHEZIZACJKCBJKLZLZULUMCMEZIZUPLZABCDNUQUTUQUSUP
      UQULUMURUOULUPULUMUNOPUOUMUPULUMUNQPUOUPURULUNUMUPURRULUNUMIUPURACBSTUAUB
      UCUOUPUDUEUSUOUPULULUMUMURUNULUFUMUFCUGUHUIUJUK $.
      $( [2-Jul-2007] $) $( [8-Jun-2007] $)

    $( Membership in an open-below, closed-above interval of extended reals. $)
    elioc1t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A (,] B ) <->
                 ( C e. RR* /\ A < C /\ C <_ B ) ) ) $=
      ( vx cxr wcel wa cioc co cv clt wbr cle crab w3a iocvalt eleq2d wceq
      breq2 breq1 anbi12d elrab 3anass bitr4 syl6bb ) AEFBEFGZCABHIZFCADJZKLZUH
      BMLZGZDENZFZCEFZACKLZCBMLZOZUFUGULCDABPQUMUNUOUPGZGUQUKURDCEUHCRUIUOUJUPU
      HCAKSUHCBMTUAUBUNUOUPUCUDUE $.
      $( [28-Dec-2006] $) $( [24-Dec-2006] $)

    $( Membership in a closed-below, open-above interval of extended reals. $)
    elico1t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A [,) B ) <->
                 ( C e. RR* /\ A <_ C /\ C < B ) ) ) $=
      ( vx cxr wcel wa cico co cv cle wbr clt crab w3a icovalt eleq2d wceq
      breq2 breq1 anbi12d elrab 3anass bitr4 syl6bb ) AEFBEFGZCABHIZFCADJZKLZUH
      BMLZGZDENZFZCEFZACKLZCBMLZOZUFUGULCDABPQUMUNUOUPGZGUQUKURDCEUHCRUIUOUJUPU
      HCAKSUHCBMTUAUBUNUOUPUCUDUE $.
      $( [28-Dec-2006] $) $( [24-Dec-2006] $)

    $( Membership in a closed interval of extended reals. $)
    elicc1t $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A [,] B ) <->
                 ( C e. RR* /\ A <_ C /\ C <_ B ) ) ) $=
      ( vx cxr wcel wa cicc co cv cle wbr crab w3a iccvalt eleq2d wceq breq2
      breq1 anbi12d elrab 3anass bitr4 syl6bb ) AEFBEFGZCABHIZFCADJZKLZUGBKLZGZ
      DEMZFZCEFZACKLZCBKLZNZUEUFUKCDABOPULUMUNUOGZGUPUJUQDCEUGCQUHUNUIUOUGCAKRU
      GCBKSTUAUMUNUOUBUCUD $.
      $( [28-Dec-2006] $) $( [24-Dec-2006] $)
  $}

  $( Membership in an open-below, closed-above real interval.  (Contributed by
     Paul Chapman, 30-Dec-2007.) $)
  elioc2t $p |- ( ( A e. RR /\ B e. RR ) ->
      ( C e. ( A (,] B ) <-> ( C e. RR /\ A < C /\ C <_ B ) ) ) $=
    ( cr wcel wa cioc co clt wbr cle w3a cxr wb elioc1t rexrt syl2an wi cmnf
    cpnf mnfltt ad2antrr mnfxr xrlttrt mp3an1 sylan adantlr mpand ltpnft
    ad2antlr pnfxr xrlelttrt mp3an3 sylan2 ancoms adantll mpan2d anim12d
    xrrebndt adantl sylibrd ex imp3a pm3.27 a1i jcad 3anass 3imtr4g sylbid
    anim1i 3imtr4 syl5bir impbid ) ADEZBDEZFZCABGHEZCDEZACIJZCBKJZLZVPVQCMEZVSV
    TLZWAAMEZBMEZVQWCNVNVOABCOAPZBPZQZVPWBVSVTFZFZVRWIFZWCWAVPWJVRWIVPWBWIVRVPW
    BWIVRRVPWBFZWISCIJZCTIJZFZVRWLVSWMVTWNWLSAIJZVSWMVNWPVOWBAUAUBVNWBWPVSFWMRZ
    VOWDWBWQVNSMEWDWBWQUCSACUDUEWFUFUGUHWLVTBTIJZWNVOWRVNWBBUIUJVOWBVTWRFWNRZVN
    WBVOWSWBWEWSVOWBWETMEWSUKCBTULUMWGUNUOUPUQURWBVRWONVPCUSUTVAVBVCWJWIRVPWBWI
    VDVEVFWBVSVTVGZVRVSVTVGZVHVIVPVQWCWAWHWKWJWAWCVRWBWICPVJXAWTVKVLVM $.
    $( [30-Dec-2007] $) $( [30-Dec-2007] $)

  $( Membership in a closed-below, open-above real interval.  (Contributed by
     Paul Chapman, 21-Jan-2008.) $)
  elico2t $p |- ( ( A e. RR /\ B e. RR ) ->
      ( C e. ( A [,) B ) <-> ( C e. RR /\ A <_ C /\ C < B ) ) ) $=
    ( cr wcel wa cico co cle wbr clt w3a cxr wb elico1t rexrt syl2an wi cmnf
    cpnf mnfltt ad2antrr mnfxr xrltletrt mp3an1 sylan adantlr mpand ltpnft
    ad2antlr pnfxr xrlttrt mp3an3 sylan2 ancoms adantll mpan2d anim12d
    xrrebndt adantl sylibrd ex imp3a pm3.27 a1i jcad 3anass 3imtr4g sylbid
    anim1i 3imtr4 syl5bir impbid ) ADEZBDEZFZCABGHEZCDEZACIJZCBKJZLZVPVQCMEZVSV
    TLZWAAMEZBMEZVQWCNVNVOABCOAPZBPZQZVPWBVSVTFZFZVRWIFZWCWAVPWJVRWIVPWBWIVRVPW
    BWIVRRVPWBFZWISCKJZCTKJZFZVRWLVSWMVTWNWLSAKJZVSWMVNWPVOWBAUAUBVNWBWPVSFWMRZ
    VOWDWBWQVNSMEWDWBWQUCSACUDUEWFUFUGUHWLVTBTKJZWNVOWRVNWBBUIUJVOWBVTWRFWNRZVN
    WBVOWSWBWEWSVOWBWETMEWSUKCBTULUMWGUNUOUPUQURWBVRWONVPCUSUTVAVBVCWJWIRVPWBWI
    VDVEVFWBVSVTVGZVRVSVTVGZVHVIVPVQWCWAWHWKWJWAWCVRWBWICPVJXAWTVKVLVM $.
    $( [21-Jan-2008] $) $( [21-Jan-2008] $)

  $( Membership in a closed real interval.  (Contributed by Paul Chapman,
     21-Sep-2007.) $)
  elicc2t $p |- ( ( A e. RR /\ B e. RR ) -> ( C e. ( A [,] B ) <->
                    ( C e. RR /\ A <_ C /\ C <_ B ) ) ) $=
    ( cr wcel wa cicc co cle wbr w3a cxr wb elicc1t rexrt syl2an wi cmnf clt
    cpnf mnfltt ad2antrr mnfxr xrltletrt mp3an1 sylan adantlr mpand ltpnft
    ad2antlr pnfxr xrlelttrt mp3an3 sylan2 ancoms adantll mpan2d anim12d
    xrrebndt adantl sylibrd ex imp3a pm3.27 a1i jcad 3anass 3imtr4g sylbid
    anim1i 3imtr4 syl5bir impbid ) ADEZBDEZFZCABGHEZCDEZACIJZCBIJZKZVPVQCLEZVSV
    TKZWAALEZBLEZVQWCMVNVOABCNAOZBOZPZVPWBVSVTFZFZVRWIFZWCWAVPWJVRWIVPWBWIVRVPW
    BWIVRQVPWBFZWIRCSJZCTSJZFZVRWLVSWMVTWNWLRASJZVSWMVNWPVOWBAUAUBVNWBWPVSFWMQZ
    VOWDWBWQVNRLEWDWBWQUCRACUDUEWFUFUGUHWLVTBTSJZWNVOWRVNWBBUIUJVOWBVTWRFWNQZVN
    WBVOWSWBWEWSVOWBWETLEWSUKCBTULUMWGUNUOUPUQURWBVRWOMVPCUSUTVAVBVCWJWIQVPWBWI
    VDVEVFWBVSVTVGZVRVSVTVGZVHVIVPVQWCWAWHWKWJWAWCVRWBWICOVJXAWTVKVLVM $.
    $( [21-Sep-2007] $) $( [21-Sep-2007] $)

  $( The open interval from minus to plus infinity. $)
  ioomax $p |- ( -oo (,) +oo ) = RR $=
    ( vx cmnf cpnf cioo co cv clt wbr wa cr crab cxr wcel wceq mnfxr pnfxr
    iooval2t mp2an rabid2 mnfltt ltpnft jca mprgbir eqtr4 ) BCDEZBAFZGHZUFCGHZI
    ZAJKZJBLMCLMUEUJNOPABCQRJUJNUIAJUIAJSUFJMUGUHUFTUFUAUBUCUD $.
    $( [8-Feb-2007] $) $( [6-Feb-2007] $)

  $( The set of positive reals expressed as an open interval. $)
  ioopos $p |- ( 0 (,) +oo ) = { x e. RR | 0 < x } $=
    ( cc0 cpnf cioo co cv clt wbr wa cr crab cxr wcel wceq 0re rexrt ax-mp
    pnfxr iooval2t mp2an ltpnft biantrud rabbii eqtr4 ) BCDEZBAFZGHZUFCGHZIZAJK
    ZUGAJKBLMZCLMUEUJNBJMUKOBPQRABCSTUGUIAJUFJMUHUGUFUAUBUCUD $.
    $( [8-May-2007] $) $( [7-May-2007] $)

  $( The set of positive reals expressed as an open interval.  (Contributed by
     Steve Rodriguez, 25-Nov-2007.) $)
  ioorp $p |- ( 0 (,) +oo ) = RR+ $=
    ( vx cc0 cpnf cioo co cv clt wbr cr crab crp ioopos df-rp eqtr4 ) BCDEBAFGH
    AIJKALAMN $.
    $( [25-Nov-2007] $) $( [25-Nov-2007] $)

  $( An open interval is a set of reals. $)
  ioossre $p |- ( A (,) B ) (_ RR $=
    ( cxr wcel wa cioo co cr wss cmnf cpnf cle wbr mnflet adantr mnfxr iooss1
    mp3anl1 mpdan pnfget pnfxr iooss2 mp3anl3 mpanl1 adantl sstrd ioomax
    syl6ss wn c0 ndmioo 0ss a1i eqsstrd pm2.61i ) ACDZBCDZEZABFGZHIURUSJKFGZHUR
    USJBFGZUTURJALMZUSVAIZUPVBUQANOJCDZUPUQVBVCPJABQRSUQVAUTIZUPUQBKLMZVEBTVDUQ
    VFVEPVDUQKCDVFVEUAJBKUBUCUDSUEUFUGUHURUIZUSUJHABUKUJHIVGHULUMUNUO $.
    $( [21-Jun-2007] $) $( [31-May-2007] $)

  ${
    $d A x $.  $d B x $.
    $( A closed real interval is a set of reals.  (Contributed by FL,
       6-Jun-2007.  Proof shortened by Paul Chapman, 21-Jan-2008.) $)
    iccssret $p |- ( ( A e. RR /\ B e. RR ) -> ( A [,] B ) (_ RR ) $=
      ( vx cr wcel wa cicc co cv w3a cle wbr elicc2t biimp3a 3simp1d 3expia
      ssrdv ) ADEZBDEZFCABGHZDRSCIZTEZUADEZRSUBJUCAUAKLZUABKLZRSUBUCUDUEJABUAMN
      OPQ $.
      $( [21-Jan-2008] $) $( [6-Jun-2007] $)

    $( An open interval is a subset of its closure.  (Contributed by Paul
       Chapman, 18-Oct-2007.) $)
    ioossicc $p |- ( A (,) B ) (_ ( A [,] B ) $=
      ( vx cioo co cicc cv wcel cxr cle wbr wa cr clt w3a wb c0 wne ne0i
      ndmioo necon1ai syl elioo2t ibi 3simp1d rexrt wi xrltlet 3adant2 ancoms
      3adant1 anim12d jca df-3an sylibr 3simpc sylc elicc1t biimprd 3anass
      syl5ibr mp2and ssriv ) CABDEZABFEZCGZVDHZVFIHZAVFJKZVFBJKZLZVFVEHZVGVFMHZ
      VHVGVMAVFNKZVFBNKZVGVMVNVOOZVGAIHZBIHZLZVGVPPVGVDQRVSVDVFSVSVDQABTUAUBZAB
      VFUCUBUDZUEVFUFUBZVQVRVHOZVNVOLZVKVGWCVNVIVOVJVQVHVNVIUGVRAVFUHUIVRVHVOVJ
      UGZVQVHVRWEVFBUHUJUKULVGVSVHLWCVGVSVHVTWBUMVQVRVHUNUOVGVPWDWAVMVNVOUPUBUQ
      VGVHVIVJOZVLVHVKLVGVLWFVGVSVLWFPVTABVFURUBUSVHVIVJUTVAVBVC $.
      $( [18-Oct-2007] $) $( [18-Oct-2007] $)


  $}

  ${
    $d A y $.  $d B x y $.  $d S x y $.
    $( A nonempty subset of a closed real interval satisfies the conditions for
       the existence of its supremum (see ~ suprcl ).  (Contributed by Paul
       Chapman, 21-Jan-2008.) $)
    iccsupr $p |- ( ( ( A e. RR /\ B e. RR ) /\ S (_ ( A [,] B ) /\ C e. S ) ->
                   ( S (_ RR /\ S =/= (/) /\ E. x e. RR A. y e. S y <_ x ) ) $=
      ( cr wcel wa cicc co wss w3a c0 wne cv cle wbr wral wrex sstr ancoms
      iccssret sylan 3adant3 ne0i 3ad2ant3 wceq breq2 ralbidv rcla4ev simplr
      ssel elicc2t biimpd sylan9r imp 3simp3d r19.21aiva sylanc 3jca ) CGHZDGHZ
      IZFCDJKZLZEFHZMFGLZFNOZBPZAPZQRZBFSZAGTZVDVFVHVGVEGLZVFVHVDVFVOVHFVEGUAUB
      CDUCUDUEVGVDVIVFFEUFUGVDVFVNVGVCVJDQRZBFSZVNVDVFIZVMVQADGVKDUHVLVPBFVKDVJ
      QUIUJUKVBVCVFULVRVPBFVRVJFHZIVJGHZCVJQRZVPVRVSVTWAVPMZVFVSVJVEHZVDWBFVEVJ
      UMVDWCWBCDVJUNUOUPUQURUSUTUEVA $.
      $( [21-Jan-2008] $) $( [21-Jan-2008] $)
  $}

  ${
    $d w x y z A $.
    $( Two ways of saying that a real number is positive. $)
    repos $p |- ( A e. ( 0 (,) +oo ) <-> ( A e. RR /\ 0 < A ) ) $=
      ( vx cc0 cv clt wbr cr cpnf cioo co breq2 ioopos elrab2 ) CBDZEFCAEFBAGCH
      IJNACEKBLM $.
      $( [9-May-2007] $) $( [7-May-2007] $)

    $( The set of open intervals of extended reals maps to subsets of reals. $)
    ioof $p |- (,) : ( RR* X. RR* ) --> P~ RR $=
      ( vx vy vz vw cxr cv clt wbr wa crab cr cpw cioo df-ioo wcel co ioovalt
      wss ioossre oprex elpw mpbir syl6eqelr foprab ) ABCEEAFZDFZGHUFBFZGHIDEJZ
      KLZMABCDNUEEOUGEOIUHUEUGMPZUIDUEUGQUJUIOUJKRUEUGSUJKUEUGMTUAUBUCUD $.
      $( [12-Feb-2007] $) $( [7-Feb-2007] $)

    $( The set of closed intervals of extended reals maps to subsets of
       extended reals.  (Contributed by FL, 14-Jun-2007.) $)
    iccf $p |- [,] : ( RR* X. RR* ) --> P~ RR* $=
      ( vx vw vy vz cv cle wbr wa cxr crab cpw wcel wral cxp cicc wf wss
      ssrab2 a1i xrex rabex elpw sylibr rgen2a df-icc foprab2 mpbi ) AEZBEZFGUI
      CEZFGHZBIJZIKZLZCIMAIMIINUMOPUNACIUHILUJILHZULIQZUNUPUOUKBIRSULIUKBITUAUB
      UCUDACDIIULUMOACDBUEUFUG $.
      $( [21-Sep-2007] $) $( [14-Jun-2007] $)

    $( The union of the range of the open interval function. $)
    unirnioo $p |- U. ran (,) = RR $=
      ( cioo crn cuni cr cpw wss cxr cxp wf ioof frn ax-mp uniss unipw sseqtr
      wcel cmnf cpnf co ioomax wfn ffn mnfxr pnfxr fnoprvalrn mp3an eqeltrr
      elssuni eqssi ) ABZCZDUKDEZCZDUJULFZUKUMFGGHZULAIZUNJUOULAKLUJULMLDNODUJP
      DUKFQRASZDUJTAUOUAZQGPRGPUQUJPUPURJUOULAUBLUCUDGGQRAUEUFUGDUJUHLUI $.
      $( [7-May-2007] $) $( [7-May-2007] $)

    $( Alternate definition of the set of open intervals of extended reals. $)
    dfioo2 $p |- (,) = { <. <. x , y >. , z >. | ( ( x e. RR* /\ y e. RR* )
                   /\ z = { w e. RR | ( x < w /\ w < y ) } ) } $=
      ( cioo cv cxr wcel wa co wceq copab2 clt wbr cr crab cxp wfn cpw wf ioof
      ffn ax-mp fnoprval mpbi iooval2t eqeq2d pm5.32i oprabbii eqtr ) EAFZGHBFZ
      GHIZCFZUKULEJZKZIZABCLZUMUNUKDFZMNUSULMNIDOPZKZIZABCLEGGQZRZEURKVCOSZETVD
      UAVCVEEUBUCABCGGEUDUEUQVBABCUMUPVAUMUOUTUNDUKULUFUGUHUIUJ $.
      $( [6-Mar-2007] $) $( [1-Mar-2007] $)
  $}

  $( The lower bound of a closed interval is a member of it.  (Contributed by
     Paul Chapman, 26-Nov-2007.) $)
  lbicc2t $p |- ( ( A e. RR /\ B e. RR /\ A <_ B ) -> A e. ( A [,] B ) ) $=
    ( cr wcel cle wbr w3a cicc co 3simp1 leidt 3ad2ant1 3simp3 3jca wb elicc2t
    3adant3 mpbird ) ACDZBCDZABEFZGZAABHIDZSAAEFZUAGZUBSUDUASTUAJSTUDUAAKLSTUAM
    NSTUCUEOUAABAPQR $.
    $( [26-Nov-2007] $) $( [26-Nov-2007] $)

  $( The upper bound of a closed interval is a member of it.  (Contributed by
     Paul Chapman, 26-Nov-2007.) $)
  ubicc2t $p |- ( ( A e. RR /\ B e. RR /\ A <_ B ) -> B e. ( A [,] B ) ) $=
    ( cr wcel cle wbr w3a cicc co 3simp2 3simp3 leidt 3ad2ant2 3jca wb elicc2t
    3adant3 mpbird ) ACDZBCDZABEFZGZBABHIDZTUABBEFZGZUBTUAUDSTUAJSTUAKTSUDUABLM
    NSTUCUEOUAABBPQR $.
    $( [26-Nov-2007] $) $( [26-Nov-2007] $)

  $( Membership in a negated open real interval.  (Contributed by Paul
     Chapman, 26-Nov-2007.) $)
  ioonegt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( C e. ( A (,) B ) <-> -u C e. ( -u B (,) -u A ) ) ) $=
    ( cr wcel w3a clt wbr wa cneg cioo co wb renegclt ax-1 impbid2 3ad2ant3
    ltnegt ancoms 3adant1 3adant2 anbi12d ancom syl5bbr cxr elioo2t rexrt
    syl2an 3adant3 3anass syl6bb anim12i syl 3bitr4d ) ADEZBDEZCDEZFZUQACGHZCBG
    HZIZIZCJZDEZBJZVCGHZVCAJZGHZIZIZCABKLEZVCVEVGKLEZURUQVDVAVIUQUOUQVDMUPUQUQV
    DCNUQVDOPQURUTUSIVIVAURUTVFUSVHUPUQUTVFMZUOUQUPVMCBRSTUOUQUSVHMUPACRUAUBUTU
    SUCUDUBURVKUQUSUTFZVBUOUPVKVNMZUQAUEEBUEEVOUOUPABCUFAUGBUGUHUIUQUSUTUJUKURV
    LVDVFVHFZVJUOUPVLVPMZUQUOUPIVEDEZVGDEZIZVQUPUOVTUPVRUOVSBNANULSVEUEEVGUEEVQ
    VRVSVEVGVCUFVEUGVGUGUHUMUIVDVFVHUJUKUN $.
    $( [26-Nov-2007] $) $( [26-Nov-2007] $)

  $( Membership in a negated closed real interval.  (Contributed by Paul
     Chapman, 26-Nov-2007.) $)
  iccnegt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( C e. ( A [,] B ) <-> -u C e. ( -u B [,] -u A ) ) ) $=
    ( cr wcel w3a cle wbr wa cneg cicc co wb renegclt ax-1 impbid2 3ad2ant3
    lenegt ancoms 3adant1 3adant2 anbi12d ancom syl5bbr elicc2t 3adant3 3anass
    syl6bb anim12i syl 3bitr4d ) ADEZBDEZCDEZFZUNACGHZCBGHZIZIZCJZDEZBJZUTGHZUT
    AJZGHZIZIZCABKLEZUTVBVDKLEZUOUNVAURVFUNULUNVAMUMUNUNVACNUNVAOPQUOUQUPIVFURU
    OUQVCUPVEUMUNUQVCMZULUNUMVJCBRSTULUNUPVEMUMACRUAUBUQUPUCUDUBUOVHUNUPUQFZUSU
    LUMVHVKMUNABCUEUFUNUPUQUGUHUOVIVAVCVEFZVGULUMVIVLMZUNULUMIVBDEZVDDEZIZVMUMU
    LVPUMVNULVOBNANUISVBVDUTUEUJUFVAVCVEUGUHUK $.
    $( [26-Nov-2007] $) $( [26-Nov-2007] $)

  $( A shifted real is a member of a shifted, closed-below, open-above real
     interval.  (Contributed by Paul Chapman, 25-Mar-2008.) $)
  icoshft $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) -> ( X e. ( A [,) B ) ->
                  ( X + C ) e. ( ( A + C ) [,) ( B + C ) ) ) ) $=
    ( cr wcel w3a cico co cle wbr clt wa caddc wi elico2t biimpd 3adant3
    3anass syl6ib wb leadd1t 3com12 3expib com12 3adant2 imp ltadd1t 3adant1
    anbi12d pm5.32da axaddrcl expcom anim1d syl6ibr 3ad2ant3 biimprd sylanc
    syld sylbid ) AEFZBEFZCEFZGZDABHIFZDEFZADJKZDBLKZMZMZDCNIZACNIZBCNIZHIFZVDV
    EVFVGVHGZVJVAVBVEVOOVCVAVBMVEVOABDPQRVFVGVHSTVDVJVFVLVKJKZVKVMLKZMZMZVNVDVF
    VIVRVDVFMVGVPVHVQVDVFVGVPUAZVAVCVFVTOVBVFVAVCMVTVFVAVCVTVAVFVCVTADCUBUCUDUE
    UFUGVDVFVHVQUAZVBVCVFWAOVAVFVBVCMWAVFVBVCWADBCUHUDUEUIUGUJUKVDVSVKEFZVPVQGZ
    VNVCVAVSWCOVBVCVSWBVRMWCVCVFWBVRVFVCWBDCULUMUNWBVPVQSUOUPVLEFZVMEFZWCVNOVDW
    DWEMVNWCVLVMVKPUQVAVCWDVBACULUFVBVCWEVABCULUIURUSUTUS $.
    $( [26-Mar-2008] $) $( [25-Mar-2008] $)

  ${
    $d A x y $.  $d B x y $.  $d C x y $.
    icoshftf1o.1 $e |- F = { <. x , y >. | ( x e. ( A [,) B ) /\
                              y = ( x + C ) ) } $.

    ${
      $d A w z $.  $d B w z $.  $d C w z $.  $d F w z $.  $d w x y z $.
      icoshftf1o.2 $e |- A e. RR $.
      icoshftf1o.3 $e |- B e. RR $.
      icoshftf1o.4 $e |- C e. RR $.
      $( Shifting a closed-below, open-above interval is one-to-one onto.
        (Contributed by Paul Chapman, 25-Mar-2008.) $)
      icoshftf1oi $p |- F : ( A [,) B ) -1-1-onto->
                          ( ( A + C ) [,) ( B + C ) ) $=
        ( vz vw cico co caddc wf1o wf1 wfo df-f1o wf cv cfv wceq wi wral f1fv
        cr wcel icoshft mp3an fopab wa opreq1 oprex fvopab4 eqeqan12d cc wb
        recn addcan2t mp3an3 biimpd cle wbr clt w3a elico2t mp2an biimp
        3simp1d recnd syl2an sylbid rgen2a mpbir2an wrex dffo3 cmin fveq2
        eqeq2d rcla4ev cneg readdcl recnt negsubt mpan2 3syl renegcl negsub
        pncant eqtr opreq12i syl6eleq eqeltrrd syl npcant eqtr2d sylanc rgen )
        CDMNZCEONZDEONZMNZFPWTXCFQZWTXCFRZWTXCFSXDWTXCFTZKUAZFUBZLUAZFUBZUCZXGX
        IUCZUDZLWTUEKWTUEKLWTXCFUFABWTXCAUAZEONZFGCUGUHZDUGUHZEUGUHXNWTUHXOXCUH
        UDHIJCDEXNUIUJUKZXMKLWTXGWTUHZXIWTUHZULXKXGEONZXIEONZUCZXLXSXTXHYAXJYBA
        BXGXOYAWTFXNXGEOUMGXGEOUNUOABXIXOYBWTFXNXIEOUMGXIEOUNUOUPXGUQUHZXIUQUHZ
        YCXLUDXSXTYDYEULYCXLYDYEEUQUHZYCXLUREJUSZXGXIEUTVAVBXSXGXSXGUGUHZCXGVCV
        DZXGDVEVDZXSYHYIYJVFZXPXQXSYKURHICDXGVGVHVIVJVKXTXIXTXIUGUHZCXIVCVDZXID
        VEVDZXTYLYMYNVFZXPXQXTYOURHICDXIVGVHVIVJVKVLVMVNVOXEXFXGXJUCZLWTVPZKXCU
        ELKWTXCFVQXRYQKXCXGEVRNZWTUHZXGYRFUBZUCZYQXGXCUHZYPUUALYRWTXIYRUCXJYTXG
        XIYRFVSVTWAUUBXGEWBZONZYRWTUUBYHYDUUDYRUCZUUBYHXAXGVCVDZXGXBVEVDZUUBYHU
        UFUUGVFZXAUGUHZXBUGUHZUUBUUHURCEHJWCZDEIJWCZXAXBXGVGVHVIVJZXGWDZYDYFUUE
        YGXGEWEWFWGUUBUUDXAUUCONZXBUUCONZMNZWTUUIUUJUUCUGUHUUBUUDUUQUHUDUUKUULE
        JWHXAXBUUCXGUIUJUUOCUUPDMUUOXAEVRNZCXAEXAUUKUSYGWICUQUHYFUURCUCCHUSYGCE
        WJVHWKUUPXBEVRNZDXBEXBUULUSYGWIDUQUHYFUUSDUCDIUSYGDEWJVHWKWLWMWNZUUBYTY
        REONZXGUUBYSYTUVAUCUUTABYRXOUVAWTFXNYREOUMGYREOUNUOWOUUBYHYDUVAXGUCZUUM
        UUNYDYFUVBYGXGEWPWFWGWQWRWSVOVO $.
        $( [26-Mar-2008] $) $( [25-Mar-2008] $)
    $}

    ${
      icoshftf1olem.1 $e |- G = { <. x , y >. |
          ( x e. ( if ( A e. RR , A , 0 ) [,) if ( B e. RR , B , 0 ) ) /\
            y = ( x + if ( C e. RR , C , 0 ) ) ) } $.
      $( Lemma for ~ icoshftf1o . $)
      icoshftf1olem $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  F : ( A [,) B ) -1-1-onto-> ( ( A + C ) [,) ( B + C ) ) ) $=
        ( cr wcel w3a cico co caddc wf1o cc0 cif wceq wb opreq1 f1oeq2 syl
        opreq1d f1oeq3 bitrd opreq2 opreq2d opreq12d 0re elimel icoshftf1oi
        dedth3h cv wa copab iftrue 3ad2ant1 3ad2ant2 eleq2d 3ad2ant3 eqeq2d
        anbi12d opabbidv 3eqtr4g f1oeq1 mpbid ) CJKZDJKZEJKZLZCDMNZCEONZDEONZMN
        ZGPZVLVOFPZVHVIVJVPVHCQRZDMNZVREONZVNMNZGPZCDEQQQVRVIDQRZMNZVTWCEONZMNZ
        GPZWDVRVJEQRZONZWCWHONZMNZGPZCVRSZVPVSVOGPZWBWMVLVSSVPWNTCVRDMUAVLVSVOG
        UBUCWMVOWASWNWBTWMVMVTVNMCVREOUAUDVOWAVSGUEUCUFDWCSZWBWDWAGPZWGWOVSWDSW
        BWPTDWCVRMUGVSWDWAGUBUCWOWAWFSWPWGTWOVNWEVTMDWCEOUAUHWAWFWDGUEUCUFEWHSZ
        WFWKSWGWLTWQVTWIWEWJMEWHVROUGEWHWCOUGUIWFWKWDGUEUCABVRWCWHGICQJUJUKDQJU
        JUKEQJUJUKULUMVKGFSVPVQTVKAUNZWDKZBUNZWRWHONZSZUOZABUPWRVLKZWTWREONZSZU
        OZABUPGFVKXCXGABVKWSXDXBXFVKWDVLWRVKVRCWCDMVHVIVRCSVJVHCQUQURVIVHWCDSVJ
        VIDQUQUSUIUTVKXAXEWTVKWHEWROVJVHWHESVIVJEQUQVAUHVBVCVDIHVEVLVOGFVFUCVG
        $.
        $( [26-Mar-2008] $) $( [25-Mar-2008] $)
    $}

    $( Shifting a closed-below, open-above interval is one-to-one onto.
      (Contributed by Paul Chapman, 25-Mar-2008.) $)
    icoshftf1o $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
        F : ( A [,) B ) -1-1-onto-> ( ( A + C ) [,) ( B + C ) ) ) $=
      ( cv cr wcel cc0 cif cico co caddc wceq wa copab eqid icoshftf1olem ) ABC
      DEFAHZCIJCKLDIJDKLMNJBHUAEIJEKLONPQABRZGUBST $.
      $( [26-Mar-2008] $) $( [25-Mar-2008] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.
    iconshftf1o.1 $e |- F = { <. x , y >. | ( x e. ( A [,) B ) /\
                              y = ( x - C ) ) } $.
    $( Shifting a closed-below, open-above interval is one-to-one onto.
      (Contributed by Paul Chapman, 25-Mar-2008.) $)
    iconshftf1oOLD $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
        F : ( A [,) B ) -1-1-onto-> ( ( A - C ) [,) ( B - C ) ) ) $=
      ( cr wcel w3a cico co cneg caddc wf1o cmin cv wceq wa copab eqid
      icoshftf1o renegclt syl3an3 wb wi cle wbr clt elico2t 3simp1 syl6bi
      3adant3 cc negsubt recnt syl2an eqeq2d expcom 3ad2ant3 syld pm5.32d
      opabbidv syl6reqr f1oeq1 syl mpbird 3adant2 3adant1 opreq12d f1oeq3
      mpbid ) CHIZDHIZEHIZJZCDKLZCEMZNLZDVRNLZKLZFOZVQCEPLZDEPLZKLZFOZVPWBVQWAA
      QZVQIZBQZWGVRNLZRZSZABTZOZVMVNVRHIWNVOABCDVRWMWMUAUBEUCUDVPFWMRWBWNUEVPWM
      WHWIWGEPLZRZSZABTFVPWLWQABVPWHWKWPVPWHWGHIZWKWPUEZVMVNWHWRUFVOVMVNSWHWRCW
      GUGUHZWGDUIUHZJWRCDWGUJWRWTXAUKULUMVOVMWRWSUFVNWRVOWSWRVOSWJWOWIWGUNIEUNI
      ZWJWORWRVOWGEUOWGUPEUPZUQURUSUTVAVBVCGVDVQWAFWMVEVFVGVPWAWERWBWFUEVPVSWCV
      TWDKVMVOVSWCRZVNCUNIXBXDVMVOCEUOCUPXCUQVHVNVOVTWDRZVMDUNIXBXEVNVODEUODUPX
      CUQVIVJWAWEVQFVKVFVL $.
      $( [26-Mar-2008] $) $( [25-Mar-2008] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.
    icounlem.1 $e |- A e. RR $.
    icounlem.2 $e |- B e. RR $.
    icounlem.3 $e |- C e. RR $.
    $( Lemma for ~ icoun . $)
    icounlem $p |- ( ( A <_ B /\ B <_ C ) ->
                     ( ( A [,) B ) u. ( B [,) C ) ) = ( A [,) C ) ) $=
      ( vx cle wbr wa cico co cun cv wcel wo cr clt wb wi letrt mp3an12 exp3a
      imp pm4.72 sylib adantrr orcom syl6bb wn ltnlet mpan2 adantr ltletrt
      mp3an23 com23 sylbird orrd letr sylan2 biantrurd bitrd ancom adantrl
      lelttrit mpan anbi12d orddi syl6bbr expcom pm5.32d andi w3a elico2t
      mp2an orbi12i 3anass bitr syl6rbbr elun 3bitr4g eqrdv ) ABHIZBCHIZJZGABKL
      ZBCKLZMZACKLZWEGNZWFOZWJWGOZPZWJQOZAWJHIZWJCRIZJZJZWJWHOWJWIOZWEWRWNWOWJB
      RIZJZJZWNBWJHIZWPJZJZPZWMWEWRWNXAXDPZJXFWEWNWQXGWNWEWQXGSWNWEJZWQWOXCPZWO
      WPPZJZWTXCPZWTWPPZJZJXGXHWOXKWPXNXHWOXJXIJZXKXHWOXIXOXHWOXCWOPZXIWNWCWOXP
      SZWDWNWCJXCWOTZXQWNWCXRWNWCXCWOAQOZBQOZWNWCXCJWOTDEABWJUAUBUCUDXCWOUEUFUG
      XCWOUHUIXHXJXIWNACHIZXJWEWNYAJZWOWPYBWOUJZWJARIZWPWNYDYCSZYAWNXSYEDWJAUKU
      LUMWNYAYDWPTWNYDYAWPWNYDYAWPWNXSCQOZYDYAJWPTDFWJACUNUOUCUPUDUQURABCDEFUSU
      TVAVBXJXIVCUIXHWPXMXNWNWDWPXMSZWCWNWDJWTWPTZYGWNWDYHWNWTWDWPWNWTWDWPWNXTY
      FWTWDJWPTEFWJBCUNUOUCUPUDWTWPUEUFVDXHXLXMWNXLWEWNXCWTPZXLXTWNYIEBWJVEVFXC
      WTUHUFUMVAVBVGWOWTXCWPVHVIVJVKWNXAXDVLUIWMWNWOWTVMZWNXCWPVMZPXFWKYJWLYKXS
      XTWKYJSDEABWJVNVOXTYFWLYKSEFBCWJVNVOVPYJXBYKXEWNWOWTVQWNXCWPVQVPVRVSWJWFW
      GVTWSWNWOWPVMZWRXSYFWSYLSDFACWJVNVOWNWOWPVQVRWAWB $.
      $( [15-Mar-2008] $) $( [15-Mar-2008] $)
  $}

  $( The union of end-to-end closed-below, open-above real intervals.
     (Contributed by Paul Chapman, 15-Mar-2008.) $)
  icoun $p |- ( ( ( A e. RR /\ B e. RR /\ C e. RR ) /\ ( A <_ B /\ B <_ C ) )
                -> ( ( A [,) B ) u. ( B [,) C ) ) = ( A [,) C ) ) $=
    ( cr wcel w3a cle wbr wa cico co cun wceq wi cc0 cif breq1 anbi1d opreq1
    uneq1d eqeq12d imbi12d breq2 anbi12d opreq2 uneq12d eqeq1d anbi2d uneq2d
    0re elimel icounlem dedth3h imp ) ADEZBDEZCDEZFABGHZBCGHZIZABJKZBCJKZLZACJK
    ZMZUOUPUQUTVENUOAOPZBGHZUSIZVFBJKZVBLZVFCJKZMZNABCOOOVFUPBOPZGHZVMCGHZIZVFV
    MJKZVMCJKZLZVKMZNVNVMUQCOPZGHZIZVQVMWAJKZLZVFWAJKZMZNAVFMZUTVHVEVLWHURVGUSA
    VFBGQRWHVCVJVDVKWHVAVIVBAVFBJSTAVFCJSUAUBBVMMZVHVPVLVTWIVGVNUSVOBVMVFGUCBVM
    CGQUDWIVJVSVKWIVIVQVBVRBVMVFJUEBVMCJSUFUGUBCWAMZVPWCVTWGWJVOWBVNCWAVMGUCUHW
    JVSWEVKWFWJVRWDVQCWAVMJUEUICWAVFJUEUAUBVFVMWAAODUJUKBODUJUKCODUJUKULUMUN $.
    $( [15-Mar-2008] $) $( [15-Mar-2008] $)

  ${
    $d A x $.  $d B x $.
    snunioolem.1 $e |- A e. RR $.
    snunioolem.2 $e |- B e. RR $.
    $( Lemma for ~ snunioo . $)
    snunioolem $p |- ( A < B -> ( { A } u. ( A (,) B ) ) = ( A [,) B ) ) $=
      ( vx clt wbr csn cioo co cun cico cv wcel wceq wo cr cle wa wb leloet
      mpan anbi1d wi breq1 biimpcd pm4.71 sylib orbi2d andir syl6rbbr sylan9bb
      expcom pm5.32d andi w3a cxr elioo2t rexrt syl2an mp2an 3anass bitr eleq1
      mpbii pm4.71ri orbi12i bitr4 syl6bb elico2t syl5bb orcom elun elsn eqcom
      orbi1i eqrdv ) ABFGZEAHZABIJZKZABLJZVREMZWBNZAWCOZWCVTNZPZWCWANZVRWDWFWEP
      ZWGVRWCQNZAWCRGZWCBFGZSZSZWIWDVRWNWJAWCFGZWLSZWEPZSZWIVRWJWMWQWJVRWMWQTWJ
      WMWOWEPZWLSZVRWQWJWKWSWLAQNZWJWKWSTCAWCUAUBUCVRWQWPWEWLSZPWTVRWEXBWPVRWEW
      LUDWEXBTWEVRWLAWCBFUEUFWEWLUGUHUIWOWEWLUJUKULUMUNWRWJWPSZWJWESZPWIWJWPWEU
      OWFXCWEXDWFWJWOWLUPZXCXABQNZWFXETZCDAUQNBUQNXGXAXFABWCURAUSBUSUTVAWJWOWLV
      BVCWEWJWEXAWJCAWCQVDVEVFVGVHVIWDWJWKWLUPZWNXAXFWDXHTCDABWCVJVAWJWKWLVBVCV
      KWFWEVLVIWHWCVSNZWFPWGWCVSVTVMXIWEWFXIWCAOWEEAVNWCAVOVCVPVCUKVQ $.
      $( [15-Mar-2008] $) $( [15-Mar-2008] $)
  $}

  $( The closure of one end of an open real interval.  (Contributed by Paul
     Chapman, 15-Mar-2008.) $)
  snunioo $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
                  ( { A } u. ( A (,) B ) ) = ( A [,) B ) ) $=
    ( cr wcel clt wbr csn cioo co cun cico wceq wi cc0 cif breq1 sneq opreq1
    uneq12d eqeq12d imbi12d breq2 opreq2 uneq2d 0re elimel snunioolem dedth2h
    3impia ) ACDZBCDZABEFZAGZABHIZJZABKIZLZUJUKULUQMUJANOZBEFZURGZURBHIZJZURBKI
    ZLZMURUKBNOZEFZUTURVEHIZJZURVEKIZLZMABNNAURLZULUSUQVDAURBEPVKUOVBUPVCVKUMUT
    UNVAAURQAURBHRSAURBKRTUABVELZUSVFVDVJBVEUREUBVLVBVHVCVIVLVAVGUTBVEURHUCUDBV
    EURKUCTUAURVEANCUEUFBNCUEUFUGUHUI $.
    $( [15-Mar-2008] $) $( [15-Mar-2008] $)


