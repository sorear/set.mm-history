$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The infinite sequence builder "seq"
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d y z A $.  $d w z B $.  $d v w x y z C $.  $d v w y z G $.
    om2uz.1 $e |- C e. ZZ $.
    om2uz.2 $e |- G = ( rec ( ( x e. _V |-> ( x + 1 ) ) , C ) |` om ) $.
    $( The mapping ` G ` is a one-to-one mapping from ` om ` onto upper
       integers that will be used to construct a recursive definition
       generator.  Ordinal natural number 0 maps to complex number ` C `
       (normally 0 for the upper integers ` NN0 ` or 1 for the upper integers
       ` NN ` ), 1 maps to ` C ` + 1, etc.  This theorem shows the value of
       ` G ` at ordinal natural number zero.  (This series of theorems
       generalizes an earlier series for ` NN0 ` contributed by Raph Levien,
       10-Apr-2004.) $)
    om2uz0i $p |- ( G ` (/) ) = C $=
      ( c0 cfv cvv cv c1 caddc co cmpt crdg com cres fveq1i cz wcel wceq fr0g
      ax-mp eqtri ) FCGFAHAIJKLMZBNOPZGZBFCUEEQBRSUFBTDBRUDUAUBUC $.
      $( [13-Sep-2013] $) $( [3-Oct-2004] $)

    $( The value of ` G ` (see ~ om2uz0i ) at a successor. $)
    om2uzsuci $p |- ( A e. om -> ( G ` suc A ) = ( ( G ` A ) + 1 ) ) $=
      ( vz vy cv csuc cfv c1 caddc co wceq com suceq fveq2d fveq2 opreq1d
      eqeq12d wcel cvv oprex ax-17 cmpt crdg cres hbmpt1 hbrdg hbres hbxfreq
      hbfv hbopr opreq1 frsucmpt mpan2 vtoclga ) GIZJZDKZUSDKZLMNZOZBJZDKZBDKZL
      MNZOGBPUSBOZVAVFVCVHVIUTVEDUSBQRVIVBVGLMUSBDSTUAUSPUBVCUCUBVDVBLMUDAHCUSA
      IZLMNZVCUCDHIZCUBAUEZVLUSUBAUEZAHVBLMAHUSDAHDAUCVKUFZCUGZPUHFAHVPPAHCVOAH
      UCVKUIVMUJVLPUBAUEUKULVNUMVLMUBAUEVLLUBAUEUNFVJVBLMUOUPUQUR $.
      $( [13-Sep-2013] $) $( [3-Oct-2004] $)

    $( The value ` G ` (see ~ om2uz0i ) at an ordinal natural number is in the
       upper integers.  (Revised by Mario Carneiro, 26-Jun-2013.) $)
    om2uzuzi $p |- ( A e. om -> ( G ` A ) e. ( ZZ>= ` C ) ) $=
      ( vy vz cv cfv cuz wcel c0 csuc wceq fveq2 eleq1d om2uz0i cz uzid ax-mp
      eqeltri com c1 caddc co peano2uz om2uzsuci syl5ibr finds ) GIZDJZCKJZLMDJ
      ZUMLHIZDJZUMLZUONZDJZUMLZBDJZUMLGHBUKMOULUNUMUKMDPQUKUOOULUPUMUKUODPQUKUR
      OULUSUMUKURDPQUKBOULVAUMUKBDPQUNCUMACDEFRCSLCUMLECTUAUBUQUTUOUCLZUPUDUEUF
      ZUMLCUPUGVBUSVCUMAUOCDEFUHQUIUJ $.
      $( [13-Sep-2013] $) $( [3-Oct-2004] $)

    $( Less-than relation for ` G ` (see ~ om2uz0i ).  (Revised by Mario
       Carneiro, 26-Jun-2013.) $)
    om2uzlti $p |- ( ( A e. om /\ B e. om ) ->
                   ( A e. B -> ( G ` A ) < ( G ` B ) ) ) $=
      ( vz vy com wcel cfv clt wbr wi cv c0 csuc wceq eleq2 fveq2 breq2d
      imbi12d imbi2d noel pm2.21i a1i wo wa id orim12d wb elsuc2g bicomd
      adantl c1 caddc co cle om2uzsuci cuz om2uzuzi cz eluzelz zleltp1 syl2an
      cr zre 3syl leloe 3bitr2rd syl5ib expcom a2d finds impcom ) CJKBJKZBCKZBE
      LZCELZMNZOZVQBHPZKZVSWCELZMNZOZOVQBQKZVSQELZMNZOZOVQBIPZKZVSWLELZMNZOZOVQ
      BWLRZKZVSWQELZMNZOZOVQWBOHICWCQSZWGWKVQXBWDWHWFWJWCQBTXBWEWIVSMWCQEUAUBUC
      UDWCWLSZWGWPVQXCWDWMWFWOWCWLBTXCWEWNVSMWCWLEUAUBUCUDWCWQSZWGXAVQXDWDWRWFW
      TWCWQBTXDWEWSVSMWCWQEUAUBUCUDWCCSZWGWBVQXEWDVRWFWAWCCBTXEWEVTVSMWCCEUAUBU
      CUDWKVQWHWJBUEUFUGWLJKZVQWPXAVQXFWPXAOWPWMBWLSZUHZWOVSWNSZUHZOVQXFUIZXAWP
      WMWOXGXIWPUJXGXIOWPBWLEUAUGUKXKXHWRXJWTXFXHWRULVQXFWRXHBWLJUMUNUOXKWTVSWN
      UPUQURZMNZVSWNUSNZXJXFWTXMULVQXFWSXLVSMAWLDEFGUTUBUOVQVSDVALZKZWNXOKZXNXM
      ULZXFABDEFGVBZAWLDEFGVBZXPVSVCKZWNVCKZXRXQDVSVDZDWNVDZVSWNVEVFVFVQVSVGKZW
      NVGKZXNXJULXFVQXPYAYEXSYCVSVHVIXFXQYBYFXTYDWNVHVIVSWNVJVFVKUCVLVMVNVOVP
      $.
      $( [13-Sep-2013] $) $( [3-Oct-2004] $)

    $( The mapping ` G ` (see ~ om2uz0i ) preserves order.  (Revised by Mario
       Carneiro, 26-Jun-2013.) $)
    om2uzlt2i $p |- ( ( A e. om /\ B e. om ) ->
                   ( A e. B <-> ( G ` A ) < ( G ` B ) ) ) $=
      ( com wcel wa cfv clt wbr om2uzlti wceq wo wn wi fveq2 a1i orim12d
      ancoms wb con0 nnon wss onsseleq ontri1 bitr3d syl2an cr cuz om2uzuzi cz
      uzssz zssre sstri sseli syl cle leloe lenlt 3imtr3d impcon4bid ) BHIZCHIZ
      JZBCIZBEKZCEKZLMZABCDEFGNVGCBIZCBOZPZVJVILMZVJVIOZPZVHQZVKQZVFVEVNVQRVFVE
      JZVLVOVMVPACBDEFGNVMVPRVTCBESTUAUBVFVEVNVRUCZVFCUDIZBUDIZWAVECUEBUEWBWCJC
      BUFVNVRCBUGCBUHUIUJUBVFVEVQVSUCZVFVJUKIZVIUKIZWDVEVFVJDULKZIWEACDEFGUMWGU
      KVJWGUNUKDUOUPUQZURUSVEVIWGIWFABDEFGUMWGUKVIWHURUSWEWFJVJVIUTMVQVSVJVIVAV
      JVIVBUIUJUBVCVD $.
      $( [13-Sep-2013] $) $( [4-May-2005] $)

    $( Range of ` G ` (see ~ om2uz0i ).  (Revised by Mario Carneiro,
       26-Jun-2013.) $)
    om2uzrani $p |- ran G = ( ZZ>= ` C ) $=
      ( vy vz crn cuz cfv cv wcel wceq com wrex wfn wb cvv c1 caddc co cmpt
      crdg cres frfnom fneq1i mpbir fvelrnb ax-mp om2uzuzi eleq1 syl5ibcom
      r19.23aiv sylbi cz c0 om2uz0i peano1 fnfvelrn mp2an eqeltrri a1i wi wa
      csuc om2uzsuci opreq1 sylan9eq peano2 sylancr adantr eqeltrrd r19.23aiva
      uzind4 impbii eqriv ) FCHZBIJZFKZVQLZVSVRLZVTGKZCJZVSMZGNOZWACNPZVTWEQWFA
      RAKSTUAUBZBUCNUDZNPBWGUENCWHEUFUGZGNVSCUHUIZWDWAGNWBNLZWCVRLWDWAAWBBCDEUJ
      WCVSVRUKULUMUNWBVQLBVQLZVTVSSTUAZVQLZVTGFBVSWBBVQUKWBVSVQUKZWBWMVQUKWOWLB
      UOLUPCJZBVQABCDEUQWFUPNLWPVQLWIURNUPCUSUTVAVBVTWNVCWAVTWEWNWJWDWNGNWKWDVD
      WBVEZCJZWMVQWKWDWRWCSTUAWMAWBBCDEVFWCVSSTVGVHWKWRVQLZWDWKWFWQNLWSWIWBVINW
      QCUSVJVKVLVMUNVBVNVOVP $.
      $( [13-Sep-2013] $) $( [3-Oct-2004] $)

    $( ` G ` (see ~ om2uz0i ) is a one-to-one onto mapping.  (Revised by Mario
       Carneiro, 26-Jun-2013.) $)
    om2uzf1oi $p |- G : om -1-1-onto-> ( ZZ>= ` C ) $=
      ( vy vz com cuz cfv wf1o wf1 crn wceq dff1o5 wf cv wi wral dff13 wfn wss
      df-f cvv c1 caddc co cmpt crdg cres frfnom fneq1i mpbir om2uzrani
      eqimssi mpbir2an wcel wa clt wbr wo wn cr wb cz om2uzuzi eluzelz zre
      3syl lttri3 syl2an ioran syl6bbr word nnord ordtri3 con2bid om2uzlti
      ancoms orim12d sylbird con1d sylbid rgen2a ) HBIJZCKHWECLZCMZWENHWECOWFHW
      ECPZFQZCJZGQZCJZNZWIWKNZRZGHSFHSFGHWECTWHCHUAZWGWEUBHWECUCWPAUDAQUEUFUGUH
      ZBUIHUJZHUABWQUKHCWREULUMWGWEABCDEUNZUOUPWOFGHWIHUQZWKHUQZURZWMWJWLUSUTZW
      LWJUSUTZVAZVBZWNXBWMXCVBXDVBURZXFWTWJVCUQZWLVCUQZWMXGVDXAWTWJWEUQWJVEUQXH
      AWIBCDEVFBWJVGWJVHVIXAWLWEUQWLVEUQXIAWKBCDEVFBWLVGWLVHVIWJWLVJVKXCXDVLVMX
      BWNXEXBWNVBWIWKUQZWKWIUQZVAZXEXBWNXLWTWIVNWKVNWNXLVBVDXAWIVOWKVOWIWKVPVKV
      QXBXJXCXKXDAWIWKBCDEVRXAWTXKXDRAWKWIBCDEVRVSVTWAWBWCWDUPWSUP $.
      $( [13-Sep-2013] $) $( [3-Oct-2004] $)

    $( ` G ` (see ~ om2uz0i ) is an isomorphism from natural ordinals to upper
       integers.  (Revised by Mario Carneiro, 26-Jun-2013.) $)
    om2uzisoi $p |- G Isom _E , < ( om , ( ZZ>= ` C ) ) $=
      ( vy vz com cuz cfv cep clt wiso wf1o cv wbr wb wral df-iso om2uzf1oi
      wcel wa epel om2uzlt2i syl5bb rgen2a mpbir2an ) HBIJZKLCMHUHCNFOZGOZKPZUI
      CJUJCJLPZQZGHRFHRFGHUHKLCSABCDETUMFGHUKUIUJUAUIHUAUJHUAUBULFGUCAUIUJBCDEU
      DUEUFUG $.
      $( [13-Sep-2013] $) $( [9-Oct-2008] $)

    $d w x y z F $.  $d v w z R $.
    uzrdg.1 $e |- A e. _V $.
    uzrdg.2 $e |- R = rec ( ( x e. _V , y e. _V |->
      <. ( x + 1 ) , ( x F y ) >. ) , <. C , A >. ) $.
    $( A helper lemma for the value of a recursive definition generator on
       upper integers (typically either ` NN ` or ` NN0 ` ) with characteristic
       function ` F ( x , y ) ` and initial value ` A ` .  Normally ` F ` is a
       function on the partition, and ` A ` is a member of the partition.  See
       also comment in ~ om2uz0i .  (Contributed by Mario Carneiro,
       26-Jun-2013.) $)
    om2uzrdg $p |- ( B e. om -> ( R ` B ) =
        <. ( G ` B ) , ( 2nd ` ( R ` B ) ) >. ) $=
      ( vz vv vw cv cfv c2nd cop wceq c0 csuc fveq2 fveq2d opeq12d eqeq12d cvv
      c1 caddc co cmpt2 crdg fveq1i opex rdg0 eqtri om2uz0i fveq2i cz elisseti
      op2nd opeq12i eqtr4i com wcel wa con0 nnon rdgsuc 3eqtr4g syl df-opr
      fvex opreq1 opreq2 opeq2d cbvmpt2v ovmpt2 mp2an eqtr3i syl6eq sylan9eq
      om2uzsuci adantr oprex eqtr4d ex finds ) MPZFQZWIHQZWJRQZSZTUAFQZUAHQZWNR
      QZSZTNPZFQZWRHQZWSRQZSZTZWRUBZFQZXDHQZXERQZSZTZDFQZDHQZXJRQZSZTMNDWIUATZW
      JWNWMWQWIUAFUCZXNWKWOWLWPWIUAHUCXNWJWNRXOUDUEUFWIWRTZWJWSWMXBWIWRFUCZXPWK
      WTWLXAWIWRHUCXPWJWSRXQUDUEUFWIXDTZWJXEWMXHWIXDFUCZXRWKXFWLXGWIXDHUCXRWJXE
      RXSUDUEUFWIDTZWJXJWMXMWIDFUCZXTWKXKWLXLWIDHUCXTWJXJRYAUDUEUFWNECSZWQWNUAA
      BUGUGAPZUHUIUJZYCBPZGUJZSZUKZYBULZQYBUAFYILUMYBYHECUNUOUPZWOEWPCAEHIJUQWP
      YBRQCWNYBRYJURECEUSIUTKVAUPVBVCWRVDVEZXCXIYKXCVFZXEWTUHUIUJZWTXAGUJZSZXHY
      KXCXEWSYHQZYOYKWRVGVEZXEYPTWRVHYQXDYIQWRYIQZYHQXEYPYBWRYHVIXDFYILUMWSYRYH
      WRFYILUMURVJVKXCYPXBYHQZYOWSXBYHUCWTXAYHUJZYSYOWTXAYHVLWTUGVEXAUGVEYTYOTW
      RHVMWSRVMOMWTXAUGUGOPZUHUIUJZUUAWIGUJZSZYOYHYMWTWIGUJZSUUAWTTUUBYMUUCUUEU
      UAWTUHUIVNUUAWTWIGVNUEWIXATUUEYNYMWIXAWTGVOVPABOMUGUGYGUUDUUBUUAYEGUJZSYC
      UUATYDUUBYFUUFYCUUAUHUIVNYCUUAYEGVNUEYEWITUUFUUCUUBYEWIUUAGVOVPVQYMYNUNVR
      VSVTWAWBZYLXFYMXGYNYKXFYMTXCAWREHIJWCWDYLXGYORQYNYLXEYORUUGUDYMYNWTUHUIWE
      WTXAGWEVAWAUEWFWGWH $.
      $( [13-Sep-2013] $) $( [26-Jun-2013] $)

    $( A helper lemma for the value of a recursive definition generator on
       upper integers.  (Contributed by Mario Carneiro, 26-Jun-2013.) $)
    uzrdglem $p |- ( B e. ( ZZ>= ` C ) ->
        <. B , ( 2nd ` ( R ` ( `' G ` B ) ) ) >. e. ( R " om ) ) $=
      ( cuz cfv wcel ccnv c2nd cop com cima wf1o wceq om2uzf1oi f1ocnvfv2 mpan
      opeq1d f1ocnvdm om2uzrdg wfun cdm wss wi cvv cv c1 caddc co cmpt2 crdg
      con0 wfn rdgfnon fneq1 mpbiri fnfun mp2b omsson fndm sseqtr4i funfvima2
      mp2an eqeltrrd syl ) DEMNZOZDHPNZHNZVPFNZQNZRZDVSRFSTZVOVQDVSSVNHUAZVOVQD
      UBAEHIJUCZSVNDHUDUEUFVOVPSOZVTWAOWBVOWDWCSVNDHUGUEWDVRVTWAABCVPEFGHIJKLUH
      FUIZSFUJZUKWDVRWAOULFABUMUMAUNZUOUPUQWGBUNGUQRURZECRZUSZUBZFUTVAZWELWKWLW
      JUTVAWIWHVBUTFWJVCVDZUTFVEVFSUTWFVGWKWLWFUTUBLWMUTFVHVFVISVPFVJVKVLVMVL
      $.
      $( [13-Sep-2013] $) $( [26-Jun-2013] $)

    $d v w z S $.
    uzrdg.3 $e |- S = ( R " om ) $.
    $( The recursive definition generator on upper integers is a function.  See
       comment in ~ om2uzrdg .  (Contributed by Mario Carneiro,
       26-Jun-2013.) $)
    uzrdgfni $p |- S Fn ( ZZ>= ` C ) $=
      ( vv vz vw cuz cfv wfn wfun cdm wceq df-fn wrel cv cop wcel wi wal wex
      dffun5 cvv cxp wss com wrex cima c1 caddc co cmpt2 crdg con0 rdgfnon
      fneq1 mpbiri fnfun mp2b fvelima mpan eleq2s c2nd om2uzrdg om2uzuzi fvex
      opelxpi sylancl eqeltrd eleq1 syl5ibcom r19.23aiv syl ssriv xpss sstri
      df-rel mpbir ccnv wa eqeq1d opth1 syl6bi wf1o om2uzf1oi f1ocnvfv syld
      fveq2 fveq2d syl6 imp visset op2nd syl6eq adantl eqtr2d r19.23aiva
      ax-gen eqeq2 imbi2d albidv cla4ev ax-mp mpbir2an dmss dmxpss uzrdglem
      eleq2i sylibr opeldm eqssi ) FDQRZSFTZFUAZYAUBFYAUCYBFUDZNUEZOUEZUFZFUGZY
      FPUEZUBZUHZOUIZPUJZNUINOPFUKYDFULULUMZUNFYAULUMZYNOFYOYFFUGYIERZYFUBZPUOU
      PZYFYOUGZYRYFEUOUQZFETZYFYTUGYREABULULAUEZURUSUTUUBBUEGUTUFVAZDCUFZVBZUBZ
      EVCSZUUALUUFUUGUUEVCSUUDUUCVDVCEUUEVEVFVCEVGVHZPYFUOEVIVJMVKYQYSPUOYIUOUG
      ZYPYOUGYQYSUUIYPYIHRZYPVLRZUFZYOABCYIDEGHIJKLVMZUUIUUJYAUGUUKULUGUULYOUGA
      YIDHIJVNYPVLVOUUJUUKYAULVPVQVRYPYFYOVSVTWAWBWCZYAULWDWEFWFWGYMNYHYFYEHWHR
      ZERZVLRZUBZUHZOUIZYMUUSOYHYPYGUBZPUOUPZUURUVBYGYTFUUAYGYTUGUVBUUHPYGUOEVI
      VJMVKUVAUURPUOUUIUVAWIUUQUUKYFUUIUVAUUQUUKUBZUUIUVAUUOYIUBZUVCUUIUVAUUJYE
      UBZUVDUUIUVAUULYGUBUVEUUIYPUULYGUUMWJUUJUUKYEYFYIHVOWKWLUOYAHWMUUIUVEUVDU
      HADHIJWNUOYAYIYEHWOVJWPUVDUUPYPVLUUOYIEWQWRWSWTUVAUUKYFUBUUIUVAUUKYGVLRYF
      YPYGVLWQYEYFNXAZOXAXBXCXDXEXFWBXGYLUUTPUUQUUPVLVOYIUUQUBZYKUUSOUVGYJUURYH
      YIUUQYFXHXIXJXKXLXGXMYCYAYCYOUAZYAFYOUNYCUVHUNUUNFYOXNXLYAULXOWENYAYCYEYA
      UGZYEUUQUFZFUGZYEYCUGUVIUVJYTUGUVKABCYEDEGHIJKLXPFYTUVJMXQXRYEUUQFUVFXSWB
      WCXTXM $.
      $( [13-Sep-2013] $) $( [26-Jun-2013] $)

    $( Initial value of a recursive definition generator on upper integers.
       See comment in ~ om2uzrdg .  (Contributed by Mario Carneiro,
       26-Jun-2013.) $)
    uzrdg0i $p |- ( S ` C ) = A $=
      ( wfun cop wcel cfv wceq cuz wfn uzrdgfni fnfun ax-mp com cima c0 cvv cv
      c1 caddc co cmpt2 crdg fveq1i opex rdg0 eqtri peano1 cdm wss wi con0
      rdgfnon fneq1 mpbiri mp2b omsson fndm sseqtr4i funfvima2 mp2an eqeltrri
      eleqtrri funopfv mp2 ) FNZDCOZFPDFQCRFDSQZTVPABCDEFGHIJKLMUAVRFUBUCVQEUDU
      EZFUFEQZVQVSVTUFABUGUGAUHZUIUJUKWABUHGUKOULZVQUMZQVQUFEWCLUNVQWBDCUOUPUQU
      FUDPZVTVSPZURENZUDEUSZUTWDWEVAEWCRZEVBTZWFLWHWIWCVBTVQWBVCVBEWCVDVEZVBEUB
      VFUDVBWGVGWHWIWGVBRLWJVBEVHVFVIUDUFEVJVKUCVLMVMDCFKVNVO $.
      $( [13-Sep-2013] $) $( [26-Jun-2013] $)

    $( Successor value of a recursive definition generator on upper integers.
       See comment in ~ om2uzrdg .  (Contributed by Mario Carneiro,
       26-Jun-2013.) $)
    uzrdgsuci $p |- ( B e. ( ZZ>= ` C ) ->
        ( S ` ( B + 1 ) ) = ( B F ( S ` B ) ) ) $=
      ( vz vw cuz cfv wcel c1 caddc co ccnv csuc c2nd wfun cop wceq wfn
      uzrdgfni fnfun ax-mp com cima peano2uz uzrdglem syl eleq2i sylibr fvex
      funopfv mpsyl wf1o om2uzf1oi f1ocnvdm mpan om2uzsuci f1ocnvfv2 opreq1d
      eqtrd wi peano2 f1ocnvfv 3syl mpd fveq2d cvv cv cmpt2 con0 nnon crdg
      rdgsuc fveq1i fveq2i 3eqtr4g om2uzrdg df-opr syl6eqr opreq1 opeq12d
      opreq2 opeq2d cbvmpt2v opex ovmpt2 mp2an syl6eq oprex op2nd eqcomd
      opreq12d 3eqtrd ) DEQRZSZDTUAUBZGRZDIUCZRZUDZFRZUERZXIIRZXIFRZUERZHUBZDDG
      RZHUBXEXGXFXHRZFRZUERZXLGUFZXEXFXTUGZGSZXGXTUHGXDUIYAABCEFGHIJKLMNUJXDGUK
      ULZXEYBFUMUNZSZYCXEXFXDSYFEDUOABCXFEFHIJKLMUPUQGYEYBNURUSXFXTGXSUEUTVAVBX
      EXSXKUEXEXRXJFXEXJIRZXFUHZXRXJUHZXEYGXMTUAUBZXFXEXIUMSZYGYJUHUMXDIVCZXEYK
      AEIJKVDZUMXDDIVEVFZAXIEIJKVGUQXEXMDTUAYLXEXMDUHYMUMXDDIVHVFZVIVJXEYKXJUMS
      ZYHYIVKZYNXIVLYLYPYQYMUMXDXJXFIVMVFVNVOVPVPVJXEYKXLXPUHYNYKXLYJXPUGZUERXP
      YKXKYRUEYKXKXMXOABVQVQAVRZTUAUBZYSBVRZHUBZUGZVSZUBZYRYKXKXNUUDRZUUEYKXIVT
      SZXKUUFUHXIWAUUGXJUUDECUGZWBZRXIUUIRZUUDRXKUUFUUHXIUUDWCXJFUUIMWDXNUUJUUD
      XIFUUIMWDWEWFUQYKUUFXMXOUGZUUDRUUEYKXNUUKUUDABCXIEFHIJKLMWGVPXMXOUUDWHWIV
      JXMVQSXOVQSUUEYRUHXIIUTXNUEUTZOPXMXOVQVQOVRZTUAUBZUUMPVRZHUBZUGZYRUUDYJXM
      UUOHUBZUGUUMXMUHUUNYJUUPUURUUMXMTUAWJUUMXMUUOHWJWKUUOXOUHUURXPYJUUOXOXMHW
      LWMABOPVQVQUUCUUQUUNUUMUUAHUBZUGYSUUMUHYTUUNUUBUUSYSUUMTUAWJYSUUMUUAHWJWK
      UUAUUOUHUUSUUPUUNUUAUUOUUMHWLWMWNYJXPWOWPWQWRVPYJXPXMTUAWSXMXOHWSWTWRUQXE
      XMDXOXQHYOXEXQXOYAXEDXOUGZGSZXQXOUHYDXEUUTYESUVAABCDEFHIJKLMUPGYEUUTNURUS
      DXOGUULVAVBXAXBXC $.
      $( [13-Sep-2013] $) $( [26-Jun-2013] $)
  $}

  ${
    $d x y z A $.
    $( ` < ` is a well-founded relation on any sequence of upper integers.
       (Contributed by Andrew Salmon, 13-Nov-2011.)  (Revised by Mario
       Carneiro, 3-Nov-2013.) $)
    ltfrn $p |- < We ( ZZ>= ` A ) $=
      ( vx cuz cdm wcel cfv clt wwe cz com cep con0 wss omsson epweon wess mp2
      cvv cv c1 caddc co cmpt crdg cres wiso wb cc0 cif wceq rdgeq2 reseq1
      isoeq1 3syl fveq2 isoeq5 syl bitrd 0z elimel eqid om2uzisoi dedth isowe
      mpbii cpw uzf fdmi eleq2s wn c0 we0 ndmfv weeq2 mpbiri pm2.61i ) ACDZEZAC
      FZGHZVTAIVQAIEZJKHZVTJLMLKHWBNOJLKPQWAJVSKGBRBSTUAUBUCZAUDZJUEZUFZWBVTUGW
      AWFJWAAUHUIZCFZKGWCWGUDZJUEZUFZAUHAWGUJZWFJVSKGWJUFZWKWLWDWIUJWEWJUJWFWMU
      GAWGWCUKWDWIJULJVSKGWJWEUMUNWLVSWHUJWMWKUGAWGCUOJVSWHKGWJUPUQURBWGWJAUHIU
      SUTWJVAVBVCJVSKGWEVDUQVEIIVFCVGVHVIVRVJZVTVKGHZGVLWNVSVKUJVTWOUGACVMVSVKG
      VNUQVOVP $.
      $( [3-Nov-2013] $) $( [13-Nov-2011] $)
  $}

  ${
    $d m n G $.  $d n N $.  $d j k n $.
    fzennn.1 $e |- G = ( rec ( ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) $.
    $( The cardinality of a finite set of sequential integers.  (See ~ om2uz0i
       for a description of the hypothesis.)  (Contributed by Mario Carneiro,
       12-Feb-2013.) $)
    fzennn $p |- ( N e. NN0 -> ( 1 ... N ) ~~ ( `' G ` N ) ) $=
      ( vn vm c1 cv cfz co ccnv cfv cen wbr cc0 caddc wceq opreq2 fveq2
      breq12d c0 0ex enref clt lt01 cz wcel wb 1z 0z fzn mp2an mpbi com cuz
      wf1o wa om2uzf1oi peano1 pm3.2i om2uz0i f1ocnvfv mp2 3brtr4i cn0 csn cun
      cin simpr cvv oprex fvex en2sn a1i wn cle cr nn0re ltp1 peano2re ltnle
      mpdan mpbid syl elfzle2 nsyl adantr disjsn sylibr word f1ocnvdm mpan
      nn0uz eleq2s nnord ordirr 3syl unen syl22anc nn0z ax-1cn addid2i nn0ge0
      0re 1re leadd1 mp3an13 syl5eqbrr fzsuc mp3an1 syl11anc csuc peano2 jctil
      om2uzsuci eleq2i biimpi f1ocnvfv2 sylancr opreq1d eqtrd sylc df-suc
      syl6eq 3brtr4d ex nn0ind ) GEHZIJZYHBKZLZMNGOIJZOYJLZMNGFHZIJZYNYJLZMNZGY
      NGPJZIJZYRYJLZMNZGCIJZCYJLZMNEFCYHOQYIYLYKYMMYHOGIRYHOYJSTYHYNQYIYOYKYPMY
      HYNGIRYHYNYJSTYHYRQYIYSYKYTMYHYRGIRYHYRYJSTYHCQYIUUBYKUUCMYHCGIRYHCYJSTUA
      UAYLYMMUAUBUCOGUDNZYLUAQZUEGUFUGZOUFUGUUDUUEUHUIUJGOUKULUMUNOUOLZBUPZUAUN
      UGZUQUABLOQYMUAQUUHUUIAOBUJDURZUSUTAOBUJDVAUNUUGUAOBVBVCVDYNVEUGZYQUUAUUK
      YQUQZYOYRVFZVGZYPYPVFZVGZYSYTMUULYQUUMUUOMNZYOUUMVHUAQZYPUUOVHUAQZUUNUUPM
      NUUKYQVIUUQUULYRVJUGYPVJUGUUQYNGPVKYNYJVLYRYPVJVJVMULVNUULYRYOUGZVOZUURUU
      KUVAYQUUKYRYNVPNZUUTUUKYNVQUGZUVBVOZYNVRZUVCYNYRUDNZUVDYNVSUVCYRVQUGUVFUV
      DUHYNVTYNYRWAWBWCWDYRGYNWEWFWGYOYRWHWIUULYPYPUGVOZUUSUUKUVGYQUUKYPUNUGZYP
      WJUVGUVHYNUUGVEUUHYNUUGUGZUVHUUJUNUUGYNBWKWLWMWNZYPWOYPWPWQWGYPYPWHWIYOYP
      UUMUUOWRWSUUKYSUUNQZYQUUKYNUFUGZGYRVPNZUVKYNWTUUKGOGPJZYRVPGXAXBUUKOYNVPN
      ZUVNYRVPNZYNXCUUKUVCUVOUVPUHZUVEOVQUGUVCGVQUGUVQXDXEOYNGXFXGWDWCXHUUFUVLU
      VMUVKUIGYNXIXJXKWGUULYTYPXLZUUPUUKYTUVRQZYQUUKUUHUVRUNUGZUQUVRBLZYRQUVSUU
      KUVTUUHUUKUVHUVTUVJYPXMWDUUJXNUUKUWAYPBLZGPJZYRUUKUVHUWAUWCQUVJAYPOBUJDXO
      WDUUKUWBYNGPUUKUUHUVIUWBYNQUUJUUKUVIVEUUGYNWMXPXQUNUUGYNBXRXSXTYAUNUUGUVR
      YRBVBYBWGYPYCYDYEYFYG $.
      $( [13-Sep-2013] $) $( [12-Feb-2013] $)

    $( The cardinality of a finite set of sequential integers.  (See ~ om2uz0i
       for a description of the hypothesis.)  (Revised by Mario Carneiro,
       12-Feb-2013.) $)
    cardfz $p |- ( N e. NN0 -> ( card ` ( 1 ... N ) ) = ( `' G ` N ) ) $=
      ( cn0 wcel c1 cfz co ccrd cfv ccnv cvv cen wbr wceq fvex fzennn carden2b
      mpsyl com cc0 cuz wf1o 0z om2uzf1oi nn0uz eleq2i biimpi f1ocnvdm sylancr
      cardnn syl eqtrd ) CEFZGCHIZJKZCBLZKZJKZUSUSMFUOUPUSNOUQUTPCURQABCDRUPUSM
      STUOUSUAFZUTUSPUOUAUBUCKZBUDCVBFZVAAUBBUEDUFUOVCEVBCUGUHUIUAVBCBUJUKUSULU
      MUN $.
      $( [15-Sep-2013] $) $( [7-Nov-2008] $)
  $}

  ${
    $d x y z $.
    $( The nonnegative integers are equinumerous to the natural numbers. $)
    nn0ennn $p |- NN0 ~~ NN $=
      ( vx vy cn0 cn cv c1 caddc co cmin nn0ex nn0p1nn nnm1nn0 wcel wceq wb cc
      nncn nn0cn wa ax-1cn subadd mp3an2 eqcom 3bitr4g addcom mpan eqeq2d
      adantl bitrd syl2an ancoms en3 ) ABCDAEZFGHZBEZFIHZJUMKUOLUODMZUMCMZUMUPN
      ZUOUNNZOZUQUOPMZUMPMZVAURUOQUMRVBVCSZUSUOFUMGHZNZUTVDUPUMNZVEUONZUSVFVBFP
      MZVCVGVHOTUOFUMUAUBUMUPUCUOVEUCUDVCVFUTOVBVCVEUNUOVIVCVEUNNTFUMUEUFUGUHUI
      UJUKUL $.
      $( [19-Jul-2004] $)

    $( The set of natural numbers (as a subset of complex numbers) is
       equinumerous to omega (the set of finite ordinal numbers). $)
    nnenom $p |- NN ~~ om $=
      ( vx com cn0 cn nnex cvv cv c1 caddc co cmpt cc0 crdg cres wf1o cen wbr
      cuz cfv 0z eqid om2uzf1oi wceq wb nn0uz f1oeq3 ax-mp mpbir omex f1oen
      nn0ennn entr2i ) BCDEBCAFAGHIJKLMBNZOZBCPQUNBLRSZUMOZALUMTUMUAUBCUOUCUNUP
      UDUECUOBUMUFUGUHBCUMUIUJUGUKUL $.
      $( [15-Sep-2013] $) $( [31-Jul-2004] $)
  $}


  $c seq $.
  $( Extend class notation with recursive sequence builder. $)
  cseq $a class seq M ( P , F ) $.

  ${
    $d P m x y $.  $d F m x y $.
    $( Define a recursive sequence builder operation that starts at an
       arbitrary integer index.  See ~ seqz1 and ~ seqzp1 for its initial and
       successor values.  Theorems ~ seq0seqz and ~ seq1seqz derive the 0-based
       ` seq0 ` and the 1-based ` seq1 ` as special cases.

       The ` rec ` function generates as its values a set of ordered pairs
       starting at ` <. M , ( F `` M ) >. ` , with the first member of each
       pair incremented by one in each successive value.  So, the range of
       ` rec ` is exactly the sequence we want, and we just extract the range
       (restricted to omega) and throw away the domain. $)
    df-seq $a |- seq M ( P , F ) = ( rec (
        ( x e. _V , y e. _V |-> <. ( x + 1 ) , ( y P ( F ` ( x + 1 ) ) ) >. ) ,
          <. M , ( F ` M ) >. ) " om ) $.
  $}

  ${
    $d x y F $.  $d x y P $.  $d x y G $.  $d x y Q $.
    $( Existence of the sequence builder operation. $)
    seqex $p |- seq M ( P , F ) e. _V $=
      ( vx vy cseq cvv cv c1 caddc co cfv cop cmpt2 crdg com cima df-seq wfun
      wcel con0 wfn rdgfnon fnfun ax-mp omex funimaexg mp2an eqeltri ) ABCFDEGG
      DHIJKZEHUJBLAKMNZCCBLMZOZPQZGDEABCRUMSZPGTUNGTUMUAUBUOULUKUCUAUMUDUEUFUMP
      GUGUHUI $.
      $( [4-Sep-2013] $)

    $( Equality theorem for the sequence builder operation. $)
    seqeq1 $p |- ( M = N -> seq M ( P , F ) = seq N ( P , F ) ) $=
      ( vx vy wceq cvv cv c1 caddc co cfv cop cmpt2 crdg com cima cseq fveq2
      opeq12 mpdan rdgeq2 syl imaeq1d df-seq 3eqtr4g ) CDGZEFHHEIJKLZFIUIBMALNO
      ZCCBMZNZPZQRUJDDBMZNZPZQRABCSABDSUHUMUPQUHULUOGZUMUPGUHUKUNGUQCDBTCUKDUNU
      AUBULUOUJUCUDUEEFABCUFEFABDUFUG $.
      $( [4-Sep-2013] $)

    $( Equality theorem for the sequence builder operation.  (Contributed by
       Mario Carneiro, 24-Jun-2013.) $)
    seqeq2 $p |- ( P = Q -> seq M ( P , F ) = seq M ( Q , F ) ) $=
      ( vx vy wceq cvv cv c1 caddc co cfv cop cmpt2 crdg com cima cseq eqidd
      opreq opeq2d mpt2eq123dv rdgeq1 syl imaeq1d df-seq 3eqtr4g ) ABGZEFHHEIJK
      LZFIZUJCMZALZNZOZDDCMNZPZQREFHHUJUKULBLZNZOZUPPZQRACDSBCDSUIUQVAQUIUOUTGU
      QVAGUIEFHHUNHHUSUIHTZVBUIUMURUJUKULABUAUBUCUPUOUTUDUEUFEFACDUGEFBCDUGUH
      $.
      $( [4-Sep-2013] $)

    $( Equality theorem for the sequence builder operation.  (Contributed by
       Mario Carneiro, 24-Jun-2013.) $)
    seqeq3 $p |- ( F = G -> seq M ( P , F ) = seq M ( P , G ) ) $=
      ( vx vy wceq cvv cv c1 caddc co cfv cop cmpt2 crdg com cima cseq eqidd
      fveq1 opreq2d opeq2d mpt2eq123dv rdgeq12 syl11anc imaeq1d df-seq 3eqtr4g
      ) BCGZEFHHEIJKLZFIZUKBMZALZNZOZDDBMZNZPZQREFHHUKULUKCMZALZNZOZDDCMZNZPZQR
      ABDSACDSUJUSVFQUJUPVCGURVEGUSVFGUJEFHHUOHHVBUJHTZVGUJUNVAUKUJUMUTULAUKBCU
      AUBUCUDUJUQVDDDBCUAUCURVEUPVCUEUFUGEFABDUHEFACDUHUI $.
      $( [4-Sep-2013] $)
  $}

  ${
    seqeqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for the sequence builder operation. $)
    seqeq1d $p |- ( ph -> seq A ( P , F ) = seq B ( P , F ) ) $=
      ( wceq cseq seqeq1 syl ) ABCGDEBHDECHGFDEBCIJ $.
      $( [7-Sep-2013] $)

    $( Equality deduction for the sequence builder operation. $)
    seqeq2d $p |- ( ph -> seq M ( A , F ) = seq M ( B , F ) ) $=
      ( wceq cseq seqeq2 syl ) ABCGBDEHCDEHGFBCDEIJ $.
      $( [7-Sep-2013] $)

    $( Equality deduction for the sequence builder operation. $)
    seqeq3d $p |- ( ph -> seq M ( P , A ) = seq M ( P , B ) ) $=
      ( wceq cseq seqeq3 syl ) ABCGDBEHDCEHGFDBCEIJ $.
      $( [7-Sep-2013] $)
  $}

  ${
    seqeq123d.1 $e |- ( ph -> M = N ) $.
    seqeq123d.2 $e |- ( ph -> P = Q ) $.
    seqeq123d.3 $e |- ( ph -> F = G ) $.
    $( Equality deduction for the sequence builder operation. $)
    seqeq123d $p |- ( ph -> seq M ( P , F ) = seq N ( Q , G ) ) $=
      ( cseq seqeq1d seqeq2d seqeq3d 3eqtrd ) ABDFKBDGKCDGKCEGKAFGBDHLABCDGIMAD
      ECGJNO $.
      $( [7-Sep-2013] $)
  $}

  ${
    $d w x y z $.  $d w y z F $.  $d w y z P $.  $d y M $.
    hbseq.1 $e |- ( y e. M -> A. x y e. M ) $.
    hbseq.2 $e |- ( y e. P -> A. x y e. P ) $.
    hbseq.3 $e |- ( y e. F -> A. x y e. F ) $.
    $( Hypothesis builder for the sequence builder operation.  (Contributed by
       Mario Carneiro, 24-Jun-2013.) $)
    hbseq $p |- ( y e. seq M ( P , F ) -> A. x y e. seq M ( P , F ) ) $=
      ( vz vw cseq cvv cv c1 caddc co cfv cop cmpt2 crdg com cima df-seq wcel
      ax-17 hbfv hbopr hbop hbmpt2 hbrdg hbima hbxfreq ) ABCDEKIJLLIMNOPZJMZUMD
      QZCPZRZSZEEDQZRZTZUAUBIJCDEUCABVAUAABUTURIJABLLUQBMZLUDAUEZVCABUMUPVBUMUD
      AUEZABUNUOCVBUNUDAUEGABUMDHVDUFUGUHUIABEUSFABEDHFUFUHUJVBUAUDAUEUKUL $.
      $( [24-Jun-2013] $)
  $}

  ${
    $d F m w x y z $.  $d P m w x y z $.  $d M m $.  $d R m $.
    seqval.1 $e |- R = rec (
        ( x e. _V , y e. _V |-> <. ( x + 1 ) ,
          ( x ( z e. _V , w e. _V |-> ( w P ( F ` ( z + 1 ) ) ) ) y ) >. ) ,
          <. M , ( F ` M ) >. ) $.
    $( Value of the sequence builder function.  (Contributed by Mario Carneiro,
       24-Jun-2013.) $)
    seqval $p |- seq M ( P , F ) = ( R " om ) $=
      ( cseq cvv cv c1 caddc co cfv cop cmpt2 crdg com cima df-seq wceq eqid
      wcel visset opreq1 fveq2d opreq2d oprex ovmpt2 mp2an opeq2i mpt2eq123i
      rdgeq1 ax-mp eqtri imaeq1i eqtr4i ) EGHJABKKALZMNOZBLZVAGPZEOZQZRZHHGPQZS
      ZTUAFTUAABEGHUBFVHTFABKKVAUTVBCDKKDLZCLZMNOZGPZEOZRZOZQZRZVGSZVHIVQVFUCVR
      VHUCABKKVPKKVEKUDZVSVOVDVAUTKUEVBKUEVOVDUCAUFBUFCDUTVBKKVMVDVNVIVCEOVJUTU
      CZVLVCVIEVTVKVAGVJUTMNUGUHUIVIVBVCEUGVNUDVBVCEUJUKULUMUNVGVQVFUOUPUQURUS
      $.
      $( [24-Jun-2013] $)
  $}

  ${
    $d w x y z F $.  $d w x y z P $.  $d x y M $.
    $( The sequence builder function is a function.  (Contributed by Mario
       Carneiro, 24-Jun-2013.) $)
    seqfn $p |- ( M e. ZZ -> seq M ( P , F ) Fn ( ZZ>= ` M ) ) $=
      ( vx vy vz vw cz wcel cseq cuz cfv wfn cc0 cif wceq seqeq1 fneq1d fveq2
      fneq2d bitrd cvv cv c1 caddc co cmpt2 cop crdg cmpt com cres 0z elimel
      eqid fvex seqval uzrdgfni dedth ) CHIZABCJZCKLZMZABUTCNOZJZVDKLZMZCNCVDPZ
      VCVEVBMVGVHVBVAVEABCVDQRVHVBVFVECVDKSTUADEVDBLZVDDEUBUBDUCZUDUEUFZVJEUCFG
      UBUBGUCFUCUDUEUFBLAUFUGZUFUHUGVDVIUHUIZVEVLDUBVKUJVDUIUKULZCNHUMUNVNUOVDB
      UPVMUOZDEFGAVMBVDVOUQURUS $.
      $( [15-Sep-2013] $) $( [24-Jun-2013] $)
  $}

  ${
    $d w x y z F $.  $d w x y z P $.  $d x y M $.
    $( Value of the sequence builder function at its initial value.
       (Contributed by Mario Carneiro, 24-Jun-2013.) $)
    seq1NEW $p |- ( M e. ZZ ->
        ( seq M ( P , F ) ` M ) = ( F ` M ) ) $=
      ( vx vy vz vw cz wcel cseq cfv wceq cc0 cif seqeq1 id fveq12d fveq2
      eqeq12d cvv cv c1 caddc co cmpt2 cop crdg cmpt com cres 0z elimel eqid
      fvex seqval uzrdg0i dedth ) CHIZCABCJZKZCBKZLURCMNZABVBJZKZVBBKZLCMCVBLZU
      TVDVAVEVFCVBUSVCABCVBOVFPQCVBBRSDEVEVBDETTDUAZUBUCUDZVGEUAFGTTGUAFUAUBUCU
      DBKAUDUEZUDUFUEVBVEUFUGZVCVIDTVHUHVBUGUIUJZCMHUKULVKUMVBBUNVJUMZDEFGAVJBV
      BVLUOUPUQ $.
      $( [15-Sep-2013] $) $( [24-Jun-2013] $)
  $}

  ${
    $d x y z w P $.  $d x y z w F $.  $d x y w M $.  $d z w N $.
    $( Value of the sequence builder function at a successor.  (Contributed by
       Mario Carneiro, 24-Jun-2013.) $)
    seqp1 $p |- ( N e. ( ZZ>= ` M ) ->
        ( seq M ( P , F ) ` ( N + 1 ) ) =
          ( ( seq M ( P , F ) ` N ) P ( F ` ( N + 1 ) ) ) ) $=
      ( vz vw vx vy cuz cfv wcel c1 caddc co cseq cvv cv cmpt2 cz wceq eluzel2
      wi cc0 cif fveq2 eleq2d seqeq1 fveq1d opreq2d eqeq12d imbi12d cop crdg
      cmpt com cres 0z elimel eqid fvex seqval uzrdgsuci dedth mpcom elisset
      opreq1 fveq2d oprex ovmpt2 sylancl eqtrd ) DCIJZKZDLMNZABCOZJZDDVOJZEFPPF
      QZEQZLMNZBJZANZRZNZVQVNBJZANZCSKZVMVPWDTZCDUAWGVMWHUBDWGCUCUDZIJZKZVNABWI
      OZJZDDWLJZWCNZTZUBCUCCWITZVMWKWHWPWQVLWJDCWIIUEUFWQVPWMWDWOWQVNVOWLABCWIU
      GZUHWQVQWNDWCWQDVOWLWRUHUIUJUKGHWIBJZDWIGHPPGQZLMNZWTHQWCNULRWIWSULUMZWLW
      CGPXAUNWIUMUOUPZCUCSUQURXCUSWIBUTXBUSZGHEFAXBBWIXDVAVBVCVDVMDPKVQPKWDWFTD
      VLVEDVOUTEFDVQPPWBWFWCVRWEANVSDTZWAWEVRAXEVTVNBVSDLMVFVGUIVRVQWEAVFWCUSVQ
      WEAVHVIVJVK $.
      $( [15-Sep-2013] $) $( [24-Jun-2013] $)
  $}

  ${
    $( Value of the sequence builder function at a successor.  (Contributed by
       Mario Carneiro, 24-Jun-2013.) $)
    seqm1 $p |- ( ( M e. ZZ /\ N e. ( ZZ>= ` ( M + 1 ) ) ) ->
        ( seq M ( P , F ) ` N ) =
          ( ( seq M ( P , F ) ` ( N - 1 ) ) P ( F ` N ) ) ) $=
      ( cz wcel c1 caddc co cuz cfv wa cmin cseq wceq eluzp1m1 seqp1 syl cc
      eluzelz zcn ax-1cn npcan mpan2 3syl adantl fveq2d opreq2d 3eqtr3d ) CEFZD
      CGHIZJKFZLZDGMIZGHIZABCNZKZUNUPKZUOBKZAIZDUPKURDBKZAIUMUNCJKFUQUTOCDPABCU
      NQRUMUODUPULUODOZUJULDEFDSFZVBUKDTDUAVCGSFVBUBDGUCUDUEUFZUGUMUSVAURAUMUOD
      BVDUGUHUI $.
      $( [24-Jun-2013] $)
  $}

  ${
    $d C n x y z $.  $d D n x y z $.  $d F n x y z $.  $d M n x y z $.
    $d N x z $.  $d P n x y z $.
    $( Closure properties of the recursive sequence builder.  (Contributed by
       Mario Carneiro, 2-Jul-2013.) $)
    seqcl2 $p |- ( ( N e. ( ZZ>= ` M ) /\ ( ( F ` M ) e. C /\
        A. x e. ( ( M + 1 ) ... N ) ( F ` x ) e. D )
          /\ A. x e. C A. y e. D ( x P y ) e. C ) ->
        ( seq M ( P , F ) ` N ) e. C ) $=
      ( vz vn cuz cfv wcel cv c1 caddc co cfz wral wa cseq an32 wi wceq opreq2
      raleqdv fveq2 eleq1d imbi12d imbi2d cz simpl seq1NEW syl5ibr a1dd wss
      cvv visset fzssp1 ax-mp ssralv imim1i eluzp1p1 eluzfz2 rcla4v 3syl
      adantr opreq1 rcla42v seqp1 syl5ibrcom imp3a ex com13 adantrl syld a2d
      syl5 uzind4 syl5bi 3impib ) HGKLZMZGFLZCMZANZFLZDMZAGOPQZHRQZSZTZWFBNZEQZ
      CMZBDSACSZHEFGUAZLZCMZWLWPTWEWPTZWKTWCWSWEWKWPUBWCWTWKWSWTWHAWIINZRQZSZXA
      WQLZCMZUCZUCWTWHAWIGRQZSZGWQLZCMZUCZUCWTWHAWIJNZRQZSZXLWQLZCMZUCZUCWTWHAW
      IXLOPQZRQZSZXRWQLZCMZUCZUCWTWKWSUCZUCIJGHXAGUDZXFXKWTYEXCXHXEXJYEWHAXBXGX
      AGWIRUEUFYEXDXICXAGWQUGUHUIUJXAXLUDZXFXQWTYFXCXNXEXPYFWHAXBXMXAXLWIRUEUFY
      FXDXOCXAXLWQUGUHUIUJXAXRUDZXFYCWTYGXCXTXEYBYGWHAXBXSXAXRWIRUEUFYGXDYACXAX
      RWQUGUHUIUJXAHUDZXFYDWTYHXCWKXEWSYHWHAXBWJXAHWIRUEUFYHXDWRCXAHWQUGUHUIUJG
      UKMZWTXJXHWTXJYIWEWEWPULYIXIWDCEFGUMUHUNUOXLWBMZWTXQYCYJWTXQYCUCXQXTXPUCY
      JWTTZYCXTXNXPXMXSUPZXTXNUCXLUQMYLJURWIXLUQUSUTWHAXMXSVAUTVBYKXTXPYBYKXTXR
      FLZDMZXPYBUCZYJXTYNUCZWTYJXRWIKLMXRXSMYPGXLVCWIXRVDWHYNAXRXSWFXRUDWGYMDWF
      XRFUGUHVEVFVGYJWPYNYOUCWEXPYNYJWPTZYBXPYNYQYBUCXPYNTZYJWPYBYRWPYBUCYJWPXO
      YMEQZCMZUCWOYTXOWMEQZCMABXOYMCDWFXOUDWNUUACWFXOWMEVHUHWMYMUDUUAYSCWMYMXOE
      UEUHVIYJYBYTWPYJYAYSCEFGXLVJUHUJVKVLVMVNVOVPVQVRVMVQVSVLVTWA $.
      $( [2-Jul-2013] $)
  $}

  ${
    $d x y P $.  $d x y S $.  $d x y F $.  $d x y M $.  $d x N $.
    seqcl.1 $e |- ( ( x e. S /\ y e. S ) -> ( x P y ) e. S ) $.
    $( Closure properties of the recursive sequence builder.  (Contributed by
       Mario Carneiro, 2-Jul-2013.) $)
    seqcl $p |- ( ( N e. ( ZZ>= ` M ) /\
          A. x e. ( M ... N ) ( F ` x ) e. S ) ->
        ( seq M ( P , F ) ` N ) e. S ) $=
      ( cuz cfv wcel cv cfz co wral c1 caddc wa cseq eluzfz1 wceq fveq2 eleq1d
      rcla4va sylan wss wi cz eluzel2 eluzelz fzp1ss syl11anc ssralv syl imp
      jca rgen2 seqcl2 mp3an3 syldan ) GFIJKZALZEJZDKZAFGMNZOZFEJZDKZVDAFPQNGMN
      ZOZRZGCEFSJDKZVAVFRVHVJVAFVEKVFVHFGTVDVHAFVEVBFUAVCVGDVBFEUBUCUDUEVAVFVJV
      AVIVEUFZVFVJUGVAFUHKGUHKVMFGUIFGUJFGUHUKULVDAVIVEUMUNUOUPVAVKVBBLCNDKZBDO
      ADOVLVNABDDHUQABDDCEFGURUSUT $.
      $( [2-Jul-2013] $)
  $}

  ${
    $d C x y z $.  $d D x y z $.  $d F x y z $.  $d M x y z $.  $d P x y z $.
    $( Range of the recursive sequence builder.  (Contributed by Mario
       Carneiro, 24-Jun-2013.) $)
    seqf2 $p |- ( ( M e. ZZ /\ ( ( F ` M ) e. C /\
        A. x e. ( ZZ>= ` ( M + 1 ) ) ( F ` x ) e. D )
          /\ A. x e. C A. y e. D ( x P y ) e. C ) ->
        seq M ( P , F ) : ( ZZ>= ` M ) --> C ) $=
      ( vz cz wcel cfv cv c1 caddc co cuz wral wa w3a cseq wfn wf seqfn
      3ad2ant1 cfz wi wss fzssuz ssralv ax-mp anim2i seqcl2 3expib com12 sylan
      r19.21aiv 3adant1 ffnfv sylanbrc ) GIJZGFKCJZALZFKDJZAGMNOZPKZQZRZVBBLEOC
      JBDQACQZSEFGTZGPKZUAZHLZVIKCJZHVJQZVJCVIUBUTVGVKVHEFGUCUDVGVHVNUTVGVHRVMH
      VJVGVAVCAVDVLUEOZQZRZVHVLVJJZVMUFVFVPVAVOVEUGVFVPUFVDVLUHVCAVOVEUIUJUKVRV
      QVHRVMVRVQVHVMABCDEFGVLULUMUNUOUPUQHVJCVIURUS $.
      $( [24-Jun-2013] $)
  $}

  ${
    $d F x y $.  $d M x y $.  $d P x y $.  $d S x y $.
    $( Range of the recursive sequence builder (special case of ~ seqf2 ).
       (Contributed by Mario Carneiro, 24-Jun-2013.) $)
    seqf $p |- ( ( M e. ZZ /\ A. x e. ( ZZ>= ` M ) ( F ` x ) e. S /\
          A. x e. S A. y e. S ( x P y ) e. S ) ->
        seq M ( P , F ) : ( ZZ>= ` M ) --> S ) $=
      ( cz wcel cv cfv cuz wral co w3a c1 caddc cseq wf simp1 uzid wceq fveq2
      eleq1d rcla4va sylan 3adant3 wss wi peano2uz syl uzss ssralv 3syl imp
      simp3 seqf2 syl121anc ) FGHZAIZEJZDHZAFKJZLZUSBICMDHBDLADLZNURFEJZDHZVAAF
      OPMZKJZLZVDVBDCEFQRURVCVDSURVCVFVDURFVBHZVCVFFTZVAVFAFVBUSFUAUTVEDUSFEUBU
      CUDUEUFURVCVIVDURVCVIURVGVBHZVHVBUGVCVIUHURVJVLVKFFUIUJFVGUKVAAVHVBULUMUN
      UFURVCVDUOABDDCEFUPUQ $.
      $( [24-Jun-2013] $)
  $}

  ${
    $d A x y $.  $d B k x y $.  $d C k y $.  $d F k x y $.  $d G k x y $.
    $d P x y $.
    $( Equality of sequences.  (Contributed by Mario Carneiro, 13-Jul-2013.) $)
    seqfveq2 $p |- ( ( ( B e. ( ZZ>= ` A ) /\ C e. ( ZZ>= ` B ) ) /\
      ( ( seq A ( P , F ) ` B ) = ( G ` B ) /\
        A. k e. ( ( B + 1 ) ... C ) ( F ` k ) = ( G ` k ) ) ) ->
       ( seq A ( P , F ) ` C ) = ( seq B ( P , G ) ` C ) ) $=
      ( vy vx cuz cfv wcel cseq wceq cv c1 caddc co cfz wral wi wa opreq2
      raleqdv fveq2 eqeq12d imbi12d imbi2d cz simpr seq1NEW eqeq2d syl5ibr
      a1dd w3a cvv wss visset fzssp1 ssralv mp2b imim1i eluzp1p1 eluzfz2
      rcla4v 3syl 3ad2ant1 opreq12 uztrn seqp1 syl 3adant3 exp3acom23 syld a2d
      syl5 3expib uzind4 exp3a com12 imp43 ) BAJKZLZCBJKZLZBDFAMZKZBGKZNZEOZFKZ
      WJGKZNZEBPQRZCSRZTZCWFKZCDGBMZKZNZWEWCWIWPWTUAZUAWEWCWIXAWCWIUBZWMEWNHOZS
      RZTZXCWFKZXCWRKZNZUAZUAXBWMEWNBSRZTZWGBWRKZNZUAZUAXBWMEWNIOZSRZTZXOWFKZXO
      WRKZNZUAZUAXBWMEWNXOPQRZSRZTZYBWFKZYBWRKZNZUAZUAXBXAUAHIBCXCBNZXIXNXBYIXE
      XKXHXMYIWMEXDXJXCBWNSUCUDYIXFWGXGXLXCBWFUEXCBWRUEUFUGUHXCXONZXIYAXBYJXEXQ
      XHXTYJWMEXDXPXCXOWNSUCUDYJXFXRXGXSXCXOWFUEXCXOWRUEUFUGUHXCYBNZXIYHXBYKXEY
      DXHYGYKWMEXDYCXCYBWNSUCUDYKXFYEXGYFXCYBWFUEXCYBWRUEUFUGUHXCCNZXIXAXBYLXEW
      PXHWTYLWMEXDWOXCCWNSUCUDYLXFWQXGWSXCCWFUEXCCWRUEUFUGUHBUILZXBXMXKXBXMYMWI
      WCWIUJYMXLWHWGDGBUKULUMUNXOWDLZXBYAYHYNWCWIYAYHUAYAYDXTUAYNWCWIUOZYHYDXQX
      TXOUPLXPYCUQYDXQUAIURWNXOUPUSWMEXPYCUTVAVBYOYDXTYGYOYDYBFKZYBGKZNZXTYGUAY
      NWCYDYRUAZWIYNYBWNJKLYBYCLYSBXOVCWNYBVDWMYREYBYCWJYBNWKYPWLYQWJYBFUEWJYBG
      UEUFVEVFVGYOXTYRYGXTYRUBYGYOXRYPDRZXSYQDRZNXRXSYPYQDVHYOYEYTYFUUAYNWCYEYT
      NZWIYNWCUBXOWBLUUBBXOAVIDFAXOVJVKVLYNWCYFUUANWIDGBXOVJVGUFUMVMVNVOVPVQVOV
      RVSVTWA $.
      $( [13-Jul-2013] $) $( [17-Mar-2005] $)
  $}

  ${
    $d F k $.  $d G k $.  $d M k $.  $d N k $.
    $( Equality of sequences.  (Contributed by Mario Carneiro, 27-Jun-2013.) $)
    seqfveq $p |- ( A. k e. ( M ... N ) ( F ` k ) = ( G ` k ) ->
        ( N e. ( ZZ>= ` M ) -> ( seq M ( P , F ) ` N ) =
                               ( seq M ( P , G ) ` N ) ) ) $=
      ( cuz cfv wcel cv wceq cfz co wral cseq wa c1 caddc cz simpl eluzel2
      uzid 3syl seq1NEW eluzfz1 fveq2 eqeq12d rcla4va sylan eqtrd wss wi
      fzp1ss mpancom ssralv syl imp seqfveq2 syl22anc expcom ) FEGHZIZBJZCHZVCD
      HZKZBEFLMZNZFACEOZHFADEOHKZVBVHPZEVAIZVBEVIHZEDHZKVFBEQRMFLMZNZVJVKVBESIZ
      VLVBVHTZEFUAZEUBUCVRVKVMECHZVNVKVBVQVMVTKVRVSACEUDUCVBEVGIVHVTVNKZEFUEVFW
      ABEVGVCEKVDVTVEVNVCECUFVCEDUFUGUHUIUJVBVHVPVBVOVGUKZVHVPULVQVBWBVSEFVAUMU
      NVFBVOVGUOUPUQEEFABCDURUSUT $.
      $( [27-Jun-2013] $) $( [17-Mar-2005] $)
  $}

  ${
    $d A x $.  $d B k x $.  $d F k x $.  $d G k x $.  $d P x $.
    $( Equality of sequences.  (Contributed by Mario Carneiro, 13-Jul-2013.) $)
    seqfeq2 $p |- ( ( B e. ( ZZ>= ` A ) /\
        ( seq A ( P , F ) ` B ) = ( G ` B ) /\
        A. k e. ( ZZ>= ` ( B + 1 ) ) ( F ` k ) = ( G ` k ) ) ->
        ( seq A ( P , F ) |` ( ZZ>= ` B ) ) =
          seq B ( P , G ) ) $=
      ( vx cuz cfv wcel cseq wceq cv c1 caddc co wral w3a cres wa fvres adantl
      cfz simpl1 simpr simpl2 wss fzssuz simpl3 ssralv mpsyl seqfveq2 syl22anc
      eqtrd r19.21aiva wb wfn cz eluzel2 seqfn uzss fnssres syl2an mpancom
      eluzelz syl eqfnfv2 syl11anc 3ad2ant1 mpbird ) BAHIZJZBCEAKZIBFILZDMZEIVO
      FILZDBNOPZHIZQZRZVMBHIZSZCFBKZLZGMZWBIZWEWCIZLZGWAQZVTWHGWAVTWEWAJZTZWFWE
      VMIZWGWJWFWLLVTWEWAVMUAUBWKVLWJVNVPDVQWEUCPZQZWLWGLVLVNVSWJUDVTWJUEVLVNVS
      WJUFWMVRUGWKVSWNVQWEUHVLVNVSWJUIVPDWMVRUJUKABWECDEFULUMUNUOVLVNWDWIUPZVSV
      LWBWAUQZWCWAUQZWOAURJZVLWPABUSWRVMVKUQWAVKUGWPVLCEAUTABVAVKWAVMVBVCVDVLBU
      RJWQABVECFBUTVFGWAWBWCVGVHVIVJ $.
      $( [13-Jul-2013] $)
  $}

  ${
    $d F k n $.  $d G k n $.  $d M k n $.  $d P n $.
    $( Equality of sequences.  (Contributed by Mario Carneiro, 13-Jul-2013.) $)
    seqfeq $p |- ( ( M e. ZZ /\ A. k e. ( ZZ>= ` M ) ( F ` k ) = ( G ` k ) ) ->
        seq M ( P , F ) = seq M ( P , G ) ) $=
      ( vn cz wcel cv cfv wceq cuz wral cseq cfz co wi wss fzssuz ssralv ax-mp
      seqfveq syl r19.21aiv wfn wb seqfn eqfnfv2 syl11anc syl5ibr imp ) EGHZBIZ
      CJUMDJKZBELJZMZACENZADENZKZUPUSULFIZUQJUTURJKZFUOMZUPVAFUOUPUNBEUTOPZMZUT
      UOHVAQVCUORUPVDQEUTSUNBVCUOTUAABCDEUTUBUCUDULUQUOUEURUOUEUSVBUFACEUGADEUG
      FUOUQURUHUIUJUK $.
      $( [13-Jul-2013] $)
  $}

  ${
    $d F x y $.  $d M x y $.  $d P x $.
    $( Restricting its characteristic function to ` ( ZZ>= `` M ) ` does not
       affect the ` seq ` function.  (Contributed by Mario Carneiro,
       24-Jun-2013.) $)
    seqres $p |- ( M e. ZZ ->
        seq M ( P , ( F |` ( ZZ>= ` M ) ) )
          = seq M ( P , F ) ) $=
      ( vx vy cz wcel cuz cfv cres cseq wceq cv wral wi cfz co seqfveq elfzuz
      fvres syl mprg rgen wfn wb seqfn eqfnfv2 syl11anc mpbiri ) CFGZABCHIZJZCK
      ZABCKZLZDMZUMIUPUNILZDUKNZUQDUKEMZULIUSBILZUPUKGUQOECUPPQZAEULBCUPRUSVAGU
      SUKGUTUSCUPSUSUKBTUAUBUCUJUMUKUDUNUKUDUOURUEAULCUFABCUFDUKUMUNUGUHUI $.
      $( [24-Jun-2013] $)
  $}

  ${
    $d F x y $.  $d M x y $.
    $( An infinite series of complex terms is a function from ` NN ` to
       ` CC ` .  (Contributed by Mario Carneiro, 27-Jun-2013.) $)
    serf $p |- ( ( M e. ZZ /\ A. x e. ( ZZ>= ` M ) ( F ` x ) e. CC ) ->
        seq M ( + , F ) : ( ZZ>= ` M ) --> CC ) $=
      ( vy cz wcel cv cfv cc cuz wral caddc co cseq wf ax-addcl rgen2a seqf
      mp3an3 ) CEFAGZBHIFACJHZKTDGZLMIFZDIKAIKUAILBCNOUCADITUBPQADLIBCRS $.
      $( [27-Jun-2013] $) $( [18-Apr-2005] $)

    $( An infinite series of real numbers is a function from ` NN ` to
       ` RR ` .  (Contributed by Mario Carneiro, 27-Jun-2013.) $)
    serfre $p |- ( ( M e. ZZ /\ A. x e. ( ZZ>= ` M ) ( F ` x ) e. RR ) ->
        seq M ( + , F ) : ( ZZ>= ` M ) --> RR ) $=
      ( vy cz wcel cv cfv cr cuz wral caddc co cseq wf ax-addrcl rgen2a seqf
      mp3an3 ) CEFAGZBHIFACJHZKTDGZLMIFZDIKAIKUAILBCNOUCADITUBPQADLIBCRS $.
      $( [27-Jun-2013] $) $( [18-Apr-2005] $)
  $}

  ${
    $d A x z $.  $d B x y z $.  $d F x y z $.  $d M x y z $.
    $( The partial sums in an infinite series of positive terms form a
       monotonic sequence.  (Contributed by Mario Carneiro, 30-Jun-2013.) $)
    sermono $p |- ( ( ( B e. ( ZZ>= ` M ) /\ A e. ( ZZ>= ` B ) ) /\
        A. x e. ( M ... A ) ( F ` x ) e. RR /\
        A. x e. ( ( B + 1 ) ... A ) 0 <_ ( F ` x ) ) ->
     ( seq M ( + , F ) ` B ) <_ ( seq M ( + , F ) ` A ) ) $=
      ( vz vy cuz cfv wcel wa cv cr cfz co wral cc0 cle wbr c1 caddc cseq wi
      wceq opreq2 raleqdv anbi12d fveq2 breq2d imbi12d imbi2d cz ax-addrcl
      seqcl leid syl ex adantrd a1i cvv wss visset fzssp1 ssralv mp2b anim12i
      imim1i eluzp1p1 adantr eluzfz2 rcla4va sylan adantrl wb uztrn adantl
      syl11anc peano2uz eleq1d addge01 adantrr mpbid seqp1 breqtrrd simplr
      fzss2 3syl imp letr syl111anc mpan2d a2d syl5 uzind4 impcom 3impib ) CEHI
      ZJZBCHIZJZKALZDIZMJZAEBNOZPZQXBRSZACTUAOZBNOZPZCUADEUBZIZBXJIZRSZWTWRXEXI
      KZXMUCZWRXCAEFLZNOZPZXFAXGXPNOZPZKZXKXPXJIZRSZUCZUCWRXCAECNOZPZXFAXGCNOZP
      ZKZXKXKRSZUCZUCZWRXCAEGLZNOZPZXFAXGYMNOZPZKZXKYMXJIZRSZUCZUCWRXCAEYMTUAOZ
      NOZPZXFAXGUUBNOZPZKZXKUUBXJIZRSZUCZUCWRXOUCFGCBXPCUDZYDYKWRUUKYAYIYCYJUUK
      XRYFXTYHUUKXCAXQYEXPCENUEUFUUKXFAXSYGXPCXGNUEUFUGUUKYBXKXKRXPCXJUHUIUJUKX
      PYMUDZYDUUAWRUULYAYRYCYTUULXRYOXTYQUULXCAXQYNXPYMENUEUFUULXFAXSYPXPYMXGNU
      EUFUGUULYBYSXKRXPYMXJUHUIUJUKXPUUBUDZYDUUJWRUUMYAUUGYCUUIUUMXRUUDXTUUFUUM
      XCAXQUUCXPUUBENUEUFUUMXFAXSUUEXPUUBXGNUEUFUGUUMYBUUHXKRXPUUBXJUHUIUJUKXPB
      UDZYDXOWRUUNYAXNYCXMUUNXRXEXTXIUUNXCAXQXDXPBENUEUFUUNXFAXSXHXPBXGNUEUFUGU
      UNYBXLXKRXPBXJUHUIUJUKYLCULJWRYFYJYHWRYFYJWRYFKXKMJZYJAFUAMDECXAXPUMZUNZX
      KUOUPUQURUSYMWSJZWRUUAUUJUURWRUUAUUJUCUUAUUGYTUCUURWRKZUUJUUGYRYTUUDYOUUF
      YQYMUTJZYNUUCVAUUDYOUCGVBZEYMUTVCXCAYNUUCVDVEZUUTYPUUEVAUUFYQUCUVAXGYMUTV
      CXFAYPUUEVDVEVFVGUUSUUGYTUUIUUSUUGYTUUIUCUUSUUGKZYTYSUUHRSZUUIUVCYSYSUUBD
      IZUAOZUUHRUVCQUVERSZYSUVFRSZUUSUUFUVGUUDUUSUUBUUEJZUUFUVGUUSUUBXGHIJZUVIU
      URUVJWRCYMVHVIXGUUBVJUPXFUVGAUUBUUEXAUUBUDZXBUVEQRXAUUBDUHZUIVKVLVMUUSUUD
      UVGUVHVNZUUFUUSUUDKZYSMJZUVEMJZUVMUVNYMWQJZYOUVOUUSUVQUUDCYMEVOZVIUUDYOUU
      SUVBVPAFUAMDEYMUUPUNVQZUUSUUBUUCJZUUDUVPUUSUUBWQJZUVTUUSUVQUWAUVREYMVRUPZ
      EUUBVJUPXCUVPAUUBUUCUVKXBUVEMUVLVSVKVLYSUVEVTVQWAWBUVCUVQUUHUVFUDUUSUVQUU
      GUVRVIUADEYMWCUPWDUUSUUDYTUVDKUUIUCZUUFUVNUUOUVOUUHMJZUWCUVNWRYFUUOUURWRU
      UDWEUUSUUDYFUUSUUBWSJZYEUUCVAUUDYFUCUURUWEWRCYMVRVICEUUBWFXCAYEUUCVDWGWHU
      UQVQUVSUUSUWAUUDUWDUWBAFUAMDEUUBUUPUNVLXKYSUUHWIWJWAWKUQWLWMUQWLWNWOWP $.
      $( [30-Jun-2013] $) $( [17-Mar-2005] $)
  $}

  ${
    $d F k n x y z $.  $d G k n x y z $.  $d H k n z $.  $d M k n x y z $.
    $d N k z $.  $d P k n x y z $.  $d S k n x y z $.
    sercaopr.1 $e |- ( ( x e. S /\ y e. S ) -> ( x P y ) e. S ) $.
    sercaopr.2 $e |- ( ( x e. S /\ y e. S ) -> ( x P y ) = ( y P x ) ) $.
    sercaopr.3 $e |- ( ( x e. S /\ y e. S /\ z e. S ) ->
      ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    $( The sum of two infinite series (generalized to an arbitrary commutative
       and associative operation).  (Contributed by Mario Carneiro,
       30-Jun-2013.) $)
    sercaopr $p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... N )
      ( ( F ` k ) e. S /\ ( G ` k ) e. S /\
        ( H ` k ) = ( ( F ` k ) P ( G ` k ) ) ) ) ->
          ( seq M ( P , H ) ` N ) =
        ( ( seq M ( P , F ) ` N ) P
          ( seq M ( P , G ) ` N ) ) ) $=
      ( vn cuz cfv wcel cv co wceq w3a cfz wral cseq wi c1 caddc opreq2
      raleqdv fveq2 opreq12d eqeq12d imbi12d cz elfz3 simp3 ralimi rcla4v
      syl2im seq1NEW sylibrd cvv wss visset fzssp1 ssralv mp2b imim1i wa
      peano2uz eluzfz2 syl rcla4va syl2an opreq2d simp2 caoprcl seqcl sylan2
      simp1 eleq1d caoprcomg syl11anc opreq1d caoprassg syl111anc 3eqtr3d
      3eqtr4d eqtrd opreq1 eqeq1d syl5ibrcom wb seqp1 adantr ex a2d syl5
      uzind4 imp ) KJPQZRFSZGQZERZXCHQZERZXCIQZXDXFDTZUAZUBZFJKUCTZUDZKDIJUEZQZ
      KDGJUEZQZKDHJUEZQZDTZUAZXKFJCSZUCTZUDZYBXNQZYBXPQZYBXRQZDTZUAZUFXKFJJUCTZ
      UDZJXNQZJXPQZJXRQZDTZUAZUFXKFJOSZUCTZUDZYQXNQZYQXPQZYQXRQZDTZUAZUFZXKFJYQ
      UGUHTZUCTZUDZUUFXNQZUUFXPQZUUFXRQZDTZUAZUFZXMYAUFCOJKYBJUAZYDYKYIYPUUOXKF
      YCYJYBJJUCUIUJUUOYEYLYHYOYBJXNUKUUOYFYMYGYNDYBJXPUKYBJXRUKULUMUNYBYQUAZYD
      YSYIUUDUUPXKFYCYRYBYQJUCUIUJUUPYEYTYHUUCYBYQXNUKUUPYFUUAYGUUBDYBYQXPUKYBY
      QXRUKULUMUNYBUUFUAZYDUUHYIUUMUUQXKFYCUUGYBUUFJUCUIUJUUQYEUUIYHUULYBUUFXNU
      KUUQYFUUJYGUUKDYBUUFXPUKYBUUFXRUKULUMUNYBKUAZYDXMYIYAUURXKFYCXLYBKJUCUIUJ
      UURYEXOYHXTYBKXNUKUURYFXQYGXSDYBKXPUKYBKXRUKULUMUNJUORZYKJIQZJGQZJHQZDTZU
      AZYPUUSJYJRYKXJFYJUDUVDJUPXKXJFYJXEXGXJUQZURXJUVDFJYJXCJUAZXHUUTXIUVCXCJI
      UKUVFXDUVAXFUVBDXCJGUKXCJHUKULUMUSUTUUSYLUUTYOUVCDIJVAUUSYMUVAYNUVBDDGJVA
      DHJVAULUMVBUUEUUHUUDUFYQXBRZUUNUUHYSUUDYQVCRYRUUGVDUUHYSUFOVEJYQVCVFXKFYR
      UUGVGVHZVIUVGUUHUUDUUMUVGUUHUUDUUMUFUVGUUHVJZUUDYTUUFIQZDTZUUAUUFGQZDTZUU
      BUUFHQZDTZDTZUAZUUMUVIUVQUUDUUCUVJDTZUVPUAUVIUVRUUCUVLUVNDTZDTZUVPUVIUVJU
      VSUUCDUVGUUFUUGRZXJFUUGUDUVJUVSUAZUUHUVGUUFXBRUWAJYQVKJUUFVLVMZXKXJFUUGUV
      EURXJUWBFUUFUUGXCUUFUAZXHUVJXIUVSXCUUFIUKUWDXDUVLXFUVNDXCUUFGUKZXCUUFHUKZ
      ULUMVNVOVPUVIUUAUUBUVSDTZDTZUUAUVLUVODTZDTZUVTUVPUVIUWGUWIUUADUVIUUBUVLDT
      ZUVNDTZUVLUUBDTZUVNDTZUWGUWIUVIUWKUWMUVNDUVIUUBERZUVLERZUWKUWMUAUUHUVGYSU
      WOUVHYSUVGXGFYRUDUWOXKXGFYRXEXGXJVQZURFCDEHJYQABXCYBEDLVRZVSVTVTZUVGUWAXE
      FUUGUDUWPUUHUWCXKXEFUUGXEXGXJWAZURXEUWPFUUFUUGUWDXDUVLEUWEWBVNVOZABUUBUVL
      EDMWCWDWEUVIUWOUWPUVNERZUWLUWGUAUWSUXAUVGUWAXGFUUGUDUXBUUHUWCXKXGFUUGUWQU
      RXGUXBFUUFUUGUWDXFUVNEUWFWBVNVOZABCUUBUVLUVNEDNWFWGUVIUWPUWOUXBUWNUWIUAUX
      AUWSUXCABCUVLUUBUVNEDNWFWGWHVPUVIUUAERZUWOUVSERZUVTUWHUAUUHUVGYSUXDUVHYSU
      VGXEFYRUDUXDXKXEFYRUWTURFCDEGJYQUWRVSVTVTZUWSUVIUWPUXBUXEUXAUXCABUVLUVNED
      LVRWDABCUUAUUBUVSEDNWFWGUVIUXDUWPUVOERZUVPUWJUAUXFUXAUVIUWOUXBUXGUWSUXCAB
      UUBUVNEDLVRWDABCUUAUVLUVOEDNWFWGWIWJUUDUVKUVRUVPYTUUCUVJDWKWLWMUVGUUMUVQW
      NUUHUVGUUIUVKUULUVPDIJYQWOUVGUUJUVMUUKUVODDGJYQWODHJYQWOULUMWPVBWQWRWSWTX
      A $.
      $( [30-Jun-2013] $) $( [17-Mar-2005] $)
  $}

  ${
    $d F k x y z $.  $d G k x y z $.  $d H k z $.  $d M k x y z $.  $d N k z $.
    $( The sum of two infinite series.  (Contributed by Mario Carneiro,
       30-Jun-2013.) $)
    seradd $p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... N )
      ( ( F ` k ) e. CC /\ ( G ` k ) e. CC /\
        ( H ` k ) = ( ( F ` k ) + ( G ` k ) ) ) ) ->
          ( seq M ( + , H ) ` N ) =
        ( ( seq M ( + , F ) ` N ) +
          ( seq M ( + , G ) ` N ) ) ) $=
      ( vx vy vz caddc cc cv ax-addcl addcom ax-addass sercaopr ) GHIJKABCDEFGL
      ZHLZMQRNQRILOP $.
      $( [30-Jun-2013] $) $( [17-Mar-2005] $)
  $}

  ${
    $d x y z P n $.  $d x y z S n $.  $d x y z n F $.  $d x y z n M $.
    $d x z N $.
    seq1p.1 $e |- ( ( x e. S /\ y e. S ) -> ( x P y ) e. S ) $.
    seq1p.2 $e |- ( ( x e. S /\ y e. S /\ z e. S ) ->
      ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    $( Removing the first term from a sequence.  (Contributed by Mario
       Carneiro, 2-Jul-2013.) $)
    seq1p $p |- ( ( N e. ( ZZ>= ` ( M + 1 ) ) /\ M e. ZZ /\
          A. x e. ( M ... N ) ( F ` x ) e. S ) ->
        ( seq M ( P , F ) ` N ) =
          ( ( F ` M ) P ( seq ( M + 1 ) ( P , F ) ` N ) ) ) $=
      ( vn c1 caddc co cuz cfv wcel cz cv cfz wral cseq wceq wa wi opreq2
      raleqdv anbi2d fveq2 opreq2d eqeq12d imbi12d uzid seqp1 syl seq1NEW
      peano2z eqcomd opreq12d eqtrd adantr a1i cvv wss visset fzssp1 ssralv
      mp2b anim2i imim1i opreq1 peano2uzr adantlr eqeq1d syl5ibr adantl
      peano2uz eluzfz1 3syl simplr eleq1d rcla4v sylc simpr eluzelz fzp1ss
      ax-mp syl6ss sylan2 impcom adantll syldan seqcl syl11anc eluzfz2
      caoprassg syl111anc eqtr4d eqeq2d sylibrd expcom a2d syl5 uzind4 3impib
      ) HGLMNZOPZQGRQZASZFPZEQZAGHTNZUAZHDFGUBZPZGFPZHDFXFUBZPZDNZUCZXHXKAGCSZT
      NZUAZUDZYAXNPZXPYAXQPZDNZUCZUEXHXKAGXFTNZUAZUDZXFXNPZXPXFXQPZDNZUCZUEZXHX
      KAGKSZTNZUAZUDZYQXNPZXPYQXQPZDNZUCZUEZXHXKAGYQLMNZTNZUAZUDZUUFXNPZXPUUFXQ
      PZDNZUCZUEZXHXMUDZXTUECKXFHYAXFUCZYDYKYHYOUUPYCYJXHUUPXKAYBYIYAXFGTUFUGUH
      UUPYEYLYGYNYAXFXNUIUUPYFYMXPDYAXFXQUIUJUKULYAYQUCZYDYTYHUUDUUQYCYSXHUUQXK
      AYBYRYAYQGTUFUGUHUUQYEUUAYGUUCYAYQXNUIUUQYFUUBXPDYAYQXQUIUJUKULYAUUFUCZYD
      UUIYHUUMUURYCUUHXHUURXKAYBUUGYAUUFGTUFUGUHUURYEUUJYGUULYAUUFXNUIUURYFUUKX
      PDYAUUFXQUIUJUKULYAHUCZYDUUOYHXTUUSYCXMXHUUSXKAYBXLYAHGTUFUGUHUUSYEXOYGXS
      YAHXNUIUUSYFXRXPDYAHXQUIUJUKULYPXFRQZXHYOYJXHYLGXNPZXFFPZDNZYNXHGGOPZQYLU
      VCUCGUMDFGGUNUOXHUVAXPUVBYMDDFGUPXHYMUVBXHUUTYMUVBUCGUQDFXFUPUOURUSUTVAVB
      UUEUUIUUDUEYQXGQZUUNUUIYTUUDUUHYSXHYQVCQZYRUUGVDZUUHYSUEKVEZGYQVCVFZXKAYR
      UUGVGVHVIVJUVEUUIUUDUUMUUIUVEUUDUUMUEUUIUVEUDZUUDUUJUUCUUFFPZDNZUCZUUMUUD
      UVMUVJUUAUVKDNZUVLUCUUAUUCUVKDVKUVJUUJUVNUVLUVJYQUVDQZUUJUVNUCXHUVEUVOUUH
      GYQVLVMZDFGYQUNUOVNVOUVJUULUVLUUJUVJUULXPUUBUVKDNZDNZUVLUVJUUKUVQXPDUVEUU
      KUVQUCUUIDFXFYQUNVPUJUVJXPEQZUUBEQZUVKEQZUVLUVRUCUVJGUUGQZUUHUVSUVJUVOUUF
      UVDQZUWBUVPGYQVQZGUUFVRVSXHUUHUVEVTZXKUVSAGUUGXIGUCXJXPEXIGFUIWAWBWCUVJUV
      EXKAXFYQTNZUAZUVTUUIUVEWDUUIUVEUWFUUGVDZUWGXHUVEUWHUUHUVEXHYQRQZUWHXFYQWE
      XHUWIUDUWFYRUUGGYQRWFUVFUVGUVHUVIWGWHWIVMUUHUWHUWGXHUWHUUHUWGXKAUWFUUGVGW
      JWKWLABDEFXFYQIWMWNUVJUUFUUGQZUUHUWAUVJUVOUWCUWJUVPUWDGUUFWOVSUWEXKUWAAUU
      FUUGXIUUFUCXJUVKEXIUUFFUIWAWBWCABCXPUUBUVKEDJWPWQWRWSWTXAXBXCXDXE $.
      $( [2-Jul-2013] $) $( [17-Mar-2005] $)
  $}

  ${
    $d F k x $.  $d M k x $.  $d N k x $.  $d P k x $.  $d S x $.  $d Z k x $.
    seqid.1 $e |- ( x e. S -> ( Z P x ) = x ) $.
    seqid.2 $e |- Z e. S $.
    $( Discard the first few terms of a sequence that starts with all zeroes
       (or whatever the identity ` Z ` is for operation ` P ` ).  (Contributed
       by Mario Carneiro, 13-Jul-2013.) $)
    seqid $p |- ( ( N e. ( ZZ>= ` M ) /\ ( F ` N ) e. S /\
      A. x e. ( M ... ( N - 1 ) ) ( F ` x ) = Z ) ->
        ( seq M ( P , F ) |` ( ZZ>= ` N ) ) =
          seq N ( P , F ) ) $=
      ( vk cuz cfv wcel cv wceq c1 cmin co cfz wral w3a cseq cres simp1 caddc
      wo uzp1 3ad2ant1 wi seqeq1 fveq1d a1i wa cz eluzel2 seqm1 sylan csn
      eluzp1m1 simpl3 fvex elsnc ralbii sylibr elsni opreq12 wb opreq2 eqeq12
      mpancom vtoclga ax-mp oprex mpbir syl6eqel syl2an seqcl syl11anc syl
      opreq1d simpl2 eluzelz seq1NEW adantr eqtr4d 3eqtrrd ex jaod mpd eqtr3d
      eqidd rgen seqfeq2 mp3an3 ) FEKLZMZFDLZCMZANZDLZGOZAEFPQRZSRZTZUAZWPFBDEU
      BZLZWQOZXFFKLUCBDFUBZOZWPWRXDUDXEFXILZXGWQXEFEOZFEPUERKLMZUFZXKXGOZWPWRXN
      XDEFUGUHXEXLXOXMXLXOUIXEXLFXIXFBDFEUJUKULXEXMXOXEXMUMZXGXBXFLZWQBRZGWQBRZ
      XKXEEUNMZXMXGXROWPWRXTXDEFUOUHZBDEFUPUQXPXQGWQBXPXQGURZMZXQGOXPXBWOMZWTYB
      MZAXCTZYCXEXTXMYDYAEFUSUQXPXDYFWPWRXDXMUTYEXAAXCWTGWSDVAVBVCVDAJBYBDEXBWS
      YBMWSGOZJNZGOZWSYHBRZYBMYHYBMWSGVEYHGVEYGYIUMYJGGBRZYBWSGYHGBVFYKYBMYKGOZ
      GCMYLIGWSBRZWSOZYLAGCYMYKOYGYNYLVGWSGGBVHYMYKWSGVIVJHVKVLYKGGGBVMVBVNVOVP
      VQVRXQGVEVSVTXPXSWQXKXPWRXSWQOZWPWRXDXMWAYNYOAWQCYMXSOWSWQOYNYOVGWSWQGBVH
      YMXSWSWQVIVJHVKVSXEXKWQOZXMWPWRYPXDWPFUNMYPEFWBBDFWCVSUHZWDWEWFWGWHWIYQWJ
      WPXHYHDLZYROZJFPUERKLZTXJYSJYTYHYTMYRWKWLEFBJDDWMWNVR $.
      $( [13-Jul-2013] $)
  $}

  ${
    $d A k x y $.  $d B k x y $.  $d C x y $.  $d F k x y $.  $d P k x y $.
    $d S k x y $.  $d Z k x y $.
    seqid2.1 $e |- ( x e. S -> ( x P Z ) = x ) $.
    $( The last few terms of a sequence that ends with all zeroes (or whatever
       the identity ` Z ` is for operation ` P ` ) are all the same.
       (Contributed by Mario Carneiro, 13-Jul-2013.) $)
    seqid2 $p |- ( ( ( B e. ( ZZ>= ` A ) /\ C e. ( ZZ>= ` B ) ) /\
      ( seq A ( P , F ) ` B ) e. S /\
        A. x e. ( ( B + 1 ) ... C ) ( F ` x ) = Z ) ->
          ( seq A ( P , F ) ` B ) =
          ( seq A ( P , F ) ` C ) ) $=
      ( vy vk cuz cfv wcel wa cseq cv wceq c1 caddc co cfz wral wi opreq2
      raleqdv fveq2 eqeq2d imbi12d imbi2d cz eqidd a1i12 w3a cvv wss visset
      fzssp1 ssralv mp2b imim1i uztrn 3adant3 seqp1 syl simp1 eluzp1p1 eluzfz2
      3syl simp3 eqeq1d rcla4v sylc opreq2d eqtr2d wb opreq1 eqeq12 mpancom
      vtoclga biimpcd eleq1 eqeq1 syl5ibrcom com23 3expia 3impia a2d syl5
      3expib uzind4 expdimp ancoms 3imp ) CBLMZNZDCLMZNZOCEGBPZMZFNZAQZGMZHRZAC
      STUAZDUBUAZUCZWTDWSMZRZWRWPXAXGXIUDZUDWRWPXAXJWPXAOZXDAXEJQZUBUAZUCZWTXLW
      SMZRZUDZUDXKXDAXECUBUAZUCZWTWTRZUDZUDXKXDAXEKQZUBUAZUCZWTYBWSMZRZUDZUDXKX
      DAXEYBSTUAZUBUAZUCZWTYHWSMZRZUDZUDXKXJUDJKCDXLCRZXQYAXKYNXNXSXPXTYNXDAXMX
      RXLCXEUBUEUFYNXOWTWTXLCWSUGUHUIUJXLYBRZXQYGXKYOXNYDXPYFYOXDAXMYCXLYBXEUBU
      EUFYOXOYEWTXLYBWSUGUHUIUJXLYHRZXQYMXKYPXNYJXPYLYPXDAXMYIXLYHXEUBUEUFYPXOY
      KWTXLYHWSUGUHUIUJXLDRZXQXJXKYQXNXGXPXIYQXDAXMXFXLDXEUBUEUFYQXOXHWTXLDWSUG
      UHUIUJCUKNXKYAXSWTULUMYBWQNZXKYGYMYRWPXAYGYMUDYGYJYFUDYRWPXAUNZYMYJYDYFYB
      UONYCYIUPYJYDUDKUQXEYBUOURXDAYCYIUSUTVAYSYJYFYLYRWPXAYJYFYLUDZUDYRWPOYJXA
      YTYRWPYJXAYTUDYRWPYJUNZYFXAYLUUAXAYLUDYFYEFNZYEYKRZUDZUUAYEHEUAZYKRZUUDUU
      AYKYEYHGMZEUAZUUEUUAYBWONZYKUUHRYRWPUUIYJCYBBVBVCEGBYBVDVEUUAUUGHYEEUUAYH
      YINZYJUUGHRZUUAYRYHXELMNUUJYRWPYJVFCYBVGXEYHVHVIYRWPYJVJXDUUKAYHYIXBYHRXC
      UUGHXBYHGUGVKVLVMVNVOUUBUUFUUCUUBUUEYEYKXBHEUAZXBRZUUEYERZAYEFUULUUERXBYE
      RUUMUUNVPXBYEHEVQUULUUEXBYEVRVSIVTVKWAVEYFXAUUBYLUUCWTYEFWBWTYEYKWCUIWDWE
      WFWEWGWHWIWJWHWKWLWMWN $.
      $( [13-Jul-2013] $)
  $}

  ${
    $d F n x y z $.  $d G n x z $.  $d H n x y z $.  $d M n x y z $.
    $d N x z $.  $d P n x y z $.  $d Q n x y z $.  $d S n x y z $.
    seqhomo.1 $e |- ( ( x e. S /\ y e. S ) -> ( x P y ) e. S ) $.
    $( Apply a homomorphism to a sequence.  (Contributed by Mario Carneiro,
       28-Jul-2013.) $)
    seqhomo $p |- ( ( N e. ( ZZ>= ` M ) /\
      A. x e. S A. y e. S ( H ` ( x P y ) ) = ( ( H ` x ) Q ( H ` y ) ) /\
      A. x e. ( M ... N ) ( ( F ` x ) e. S /\ ( H ` ( F ` x ) ) = ( G ` x ) ) )
        -> ( H ` ( seq M ( P , F ) ` N ) ) =
                 ( seq M ( Q , G ) ` N ) ) $=
      ( vz vn cuz cfv wcel cv co wceq wral wa cfz cseq wi c1 caddc opreq2
      raleqdv fveq2 fveq2d eqeq12d imbi12d imbi2d cz elfz3 simpr ralimi rcla4v
      syl2im seq1NEW sylibrd a1d wss fzssp1 adantr ssralv syl imim1d w3a simpl
      imp seqcl syldan 3adant2 simp3 simp1 peano2uz eluzfz2 3syl eleq1d
      anbi12d mpd simpld jca simp2 opreq1 opreq1d opreq2d rcla42v sylc adantl
      simprd opreq12 sylan2 eqtrd simpr1 seqp1 3eqtr4d expcom 3expia a2d syld
      ex uzind4 3imp ) JINOZPAQZBQZCRZHOZXGHOZXHHOZDRZSZBETAETZXGFOZEPZXPHOZXGG
      OZSZUAZAIJUBRZTZJCFIUCZOZHOZJDGIUCZOZSZXOYAAILQZUBRZTZYJYDOZHOZYJYGOZSZUD
      ZUDXOYAAIIUBRZTZIYDOZHOZIYGOZSZUDZUDXOYAAIMQZUBRZTZUUEYDOZHOZUUEYGOZSZUDZ
      UDXOYAAIUUEUEUFRZUBRZTZUUMYDOZHOZUUMYGOZSZUDZUDXOYCYIUDZUDLMIJYJISZYQUUDX
      OUVBYLYSYPUUCUVBYAAYKYRYJIIUBUGUHUVBYNUUAYOUUBUVBYMYTHYJIYDUIUJYJIYGUIUKU
      LUMYJUUESZYQUULXOUVCYLUUGYPUUKUVCYAAYKUUFYJUUEIUBUGUHUVCYNUUIYOUUJUVCYMUU
      HHYJUUEYDUIUJYJUUEYGUIUKULUMYJUUMSZYQUUTXOUVDYLUUOYPUUSUVDYAAYKUUNYJUUMIU
      BUGUHUVDYNUUQYOUURUVDYMUUPHYJUUMYDUIUJYJUUMYGUIUKULUMYJJSZYQUVAXOUVEYLYCY
      PYIUVEYAAYKYBYJJIUBUGUHUVEYNYFYOYHUVEYMYEHYJJYDUIUJYJJYGUIUKULUMIUNPZUUDX
      OUVFYSIFOZHOZIGOZSZUUCUVFIYRPYSXTAYRTUVJIUOYAXTAYRXQXTUPUQXTUVJAIYRXGISZX
      RUVHXSUVIUVKXPUVGHXGIFUIUJXGIGUIUKURUSUVFUUAUVHUUBUVIUVFYTUVGHCFIUTUJDGIU
      TUKVAVBUUEXFPZXOUULUUTUVLXOUULUUTUDUVLXOUAZUULUUOUUKUDUUTUVMUUOUUGUUKUVMU
      UFUUNVCZUUOUUGUDUVLUVNXOIUUEXFVDZVEYAAUUFUUNVFVGVHUVMUUOUUKUUSUVLXOUUOUUK
      UUSUDUUKUVLXOUUOVIZUUSUUKUVPUAZUUHUUMFOZCRZHOZUUJUUMGOZDRZUUQUURUVQUVTUUI
      UVRHOZDRZUWBUVPUVTUWDSZUUKUVPUUHEPZUVREPZUAXOUWEUVPUWFUWGUVLUUOUWFXOUVLUU
      OXQAUUFTZUWFUVLUUOUWHUVLUVNUUOXQAUUNTUWHUVOYAXQAUUNXQXTVJUQXQAUUFUUNVFUSV
      KABCEFIUUEKVLVMVNUVPUWGUWCUWASZUVPUUOUWGUWIUAZUVLXOUUOVOUVPUUMUUNPZUUOUWJ
      UDUVPUVLUUMXFPUWKUVLXOUUOVPIUUEVQIUUMVRVSYAUWJAUUMUUNXGUUMSZXQUWGXTUWIUWL
      XPUVREXGUUMFUIZVTUWLXRUWCXSUWAUWLXPUVRHUWMUJXGUUMGUIUKWAURVGWBZWCWDUVLXOU
      UOWEXNUWEUUHXHCRZHOZUUIXLDRZSABUUHUVREEXGUUHSZXJUWPXMUWQUWRXIUWOHXGUUHXHC
      WFUJUWRXKUUIXLDXGUUHHUIWGUKXHUVRSZUWPUVTUWQUWDUWSUWOUVSHXHUVRUUHCUGUJUWSX
      LUWCUUIDXHUVRHUIWHUKWIWJWKUVPUUKUWIUWDUWBSUVPUWGUWIUWNWLUUIUUJUWCUWADWMWN
      WOUVQUVLUUQUVTSUUKUVLXOUUOWPZUVLUUPUVSHCFIUUEWQUJVGUVQUVLUURUWBSUWTDGIUUE
      WQVGWRWSWTXAXBXCXAXDXE $.
      $( [28-Jul-2013] $)
  $}

  ${
    $d C u v w x y $.  $d F x $.  $d G w x y $.  $d M w x y $.  $d N w x $.
    $d P u v w x y z $.  $d S u v w x y z $.  $d T u v w x y z $.
    serdistr.1 $e |- ( ( x e. S /\ y e. S /\ z e. S ) ->
      ( x T ( y P z ) ) = ( ( x T y ) P ( x T z ) ) ) $.
    serdistr.2 $e |- ( ( x e. S /\ y e. S ) -> ( x P y ) e. S ) $.
    $( The distributive property for series.  (Contributed by Mario Carneiro,
       28-Jul-2013.) $)
    serdistr $p |- ( ( N e. ( ZZ>= ` M ) /\ C e. S /\ A. x e. ( M ... N )
        ( ( G ` x ) e. S /\ ( F ` x ) = ( C T ( G ` x ) ) ) ) ->
      ( seq M ( P , F ) ` N ) =
        ( C T ( seq M ( P , G ) ` N ) ) ) $=
      ( vw vv vu cuz cfv wcel cv co wceq wa cfz wral w3a cseq cmpt eqcom
      opreq2 eqid oprex fvmpt eqeq2d syl5bb pm5.32i ralbii caoprdistrg syl
      3adant1 opreq12 syl2an 3eqtr4d 3expib r19.21aivv seqhomo syl3an2
      syl3an3br simpl ralimi seqcl sylan2 3adant2 eqtr3d ) KJQRSZDFSZATZIRZFSZV
      QHRZDVRGUAZUBZUCZAJKUDUAZUEZUFKEIJUGRZNFDNTZGUAZUHZRZKEHJUGRZDWFGUAZWEVOV
      PVSVRWIRZVTUBZUCZAWDUEZWJWKUBZWOWCAWDVSWNWBWNVTWMUBVSWBWMVTUIVSWMWAVTNVRW
      HWAFWIWGVRDGUJWIUKZDVRGULUMUNUOUPUQVPVOVQBTZEUAZWIRZVQWIRZWSWIRZEUAZUBZBF
      UEAFUEWPWQVPXEABFFVPVQFSZWSFSZXEVPXFXGUFDWTGUAZDVQGUAZDWSGUAZEUAZXAXDNOPD
      VQWSFEGABCWGOTPTFEGLURURXFXGXAXHUBZVPXFXGUCWTFSXLMNWTWHXHFWIWGWTDGUJWRDWT
      GULUMUSUTXFXGXDXKUBZVPXFXBXIUBXCXJUBXMXGNVQWHXIFWIWGVQDGUJWRDVQGULUMNWSWH
      XJFWIWGWSDGUJWRDWSGULUMXBXIXCXJEVAVBUTVCVDVEABEEFIHWIJKMVFVGVHVOWEWJWLUBZ
      VPVOWEUCWFFSZXNWEVOVSAWDUEXOWCVSAWDVSWBVIVJABEFIJKMVKVLNWFWHWLFWIWGWFDGUJ
      WRDWFGULUMUSVMVN $.
      $( [28-Jul-2013] $)
  $}

  ${
    $d k x y G $.  $d k x F $.  $d k H $.  $d k x y M $.  $d k y N $.
    $( The difference of two infinite series.  (Contributed by Mario Carneiro,
       12-Aug-2013.) $)
    sersub $p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... N )
      ( ( F ` k ) e. CC /\ ( G ` k ) e. CC /\
        ( H ` k ) = ( ( F ` k ) - ( G ` k ) ) ) ) ->
          ( seq M ( + , H ) ` N ) =
        ( ( seq M ( + , F ) ` N ) -
          ( seq M ( + , G ) ` N ) ) ) $=
      ( vy vx cuz cfv wcel cv cc cmin co wceq w3a cfz wral wa caddc cseq cneg
      cmpt simpr1 fveq2 negeqd eqid negex fvmpt adantr simpr2 negcl syl
      eqeltrd simpr3 opreq2d negsub syl11anc eqtrd eqeq2d mpbird 3jca ex
      ralimia seradd sylan2 c1 cmul mulm1 eqtr4d jca ax-1cn negcli ax-distr
      ax-addcl serdistr mp3an2 simp2 ralimi seqcl simp1 3eqtrd ) FEIJKZALZBJZMK
      ZWECJZMKZWEDJZWFWHNOZPZQZAEFROZSZTZFUADEUBJZFUABEUBJZFUAGWNGLZCJZUCZUDZEU
      BJZUAOZWRFUACEUBJZUCZUAOZWRXENOZWOWDWGWEXBJZMKZWJWFXIUAOZPZQZAWNSWQXDPWMX
      MAWNWEWNKZWMXMXNWMTZWGXJXLXNWGWIWLUEZXOXIWHUCZMXNXIXQPWMGWEXAXQWNXBWSWEPW
      TWHWSWECUFUGXBUHWHUIUJUKZXOWIXQMKXNWGWIWLULZWHUMUNUOXOXLWLXNWGWIWLUPXOXKW
      KWJXOXKWFXQUAOZWKXOXIXQWFUAXRUQXOWGWIXTWKPXPXSWFWHURUSUTVAVBVCVDVEABXBDEF
      VFVGWPXCXFWRUAWPXCVHUCZXEVIOZXFWOWDWIXIYAWHVIOZPZTZAWNSZXCYBPZWMYEAWNXNWM
      YEXOWIYDXSXOXIXQYCXRXOWIYCXQPXSWHVJUNVKVLVDVEWDYAMKYFYGVHVMVNAHGYAUAMVIXB
      CEFWEHLZWSVOWEYHVPZVQVRVGWPXEMKZYBXFPWOWDWIAWNSYJWMWIAWNWGWIWLVSVTAHUAMCE
      FYIWAVGZXEVJUNUTUQWPWRMKZYJXGXHPWOWDWGAWNSYLWMWGAWNWGWIWLWBVTAHUAMBEFYIWA
      VGYKWRXEURUSWC $.
      $( [12-Aug-2013] $) $( [17-Mar-2005] $)
  $}

  ${
    $d M x $.  $d N x $.
    $( The value of the partial sums in a zero-valued infinite series.
       (Contributed by Mario Carneiro, 31-Aug-2013.) $)
    ser0 $p |- ( N e. ( ZZ>= ` M ) ->
      ( seq M ( + , ( ( ZZ>= ` M ) X. { 0 } ) ) ` N ) = 0 ) $=
      ( vx cuz cfv wcel caddc cc0 csn cxp cseq cc cv wceq c1 co cfz wral cz
      eluzel2 uzid syl id seq1NEW 0cn elisseti fvconst2 eqtrd syl6eqel wa
      elfzuz peano2uzr sylan2 r19.21aiva addid1 seqid2 syl211anc eqtr3d ) BADEZ
      FZAGUSHIJZAKZEZBVBEZHUTAUSFZUTVCLFCMZVAEHNZCAOGPZBQPZRZVCVDNUTASFZVEABTZA
      UAUBZUTUCUTVCHLUTVCAVAEZHUTVKVCVNNVLGVAAUDUBUTVEVNHNVMUSHAHLUEUFZUGUBUHZU
      EUIUTVKVJVLVKVGCVIVKVFVIFZUJVFUSFZVGVQVKVFVHDEFVRVFVHBUKAVFULUMUSHVFVOUGU
      BUNUBCAABGLVAHVFUOUPUQVPUR $.
      $( [31-Aug-2013] $)
  $}

  ${
    $d k x F $.  $d k x G $.  $d k x M $.  $d k x N $.
    $( Comparison of partial sums of two infinite series of reals.
       (Contributed by Mario Carneiro, 12-Aug-2013.) $)
    sercmp $p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... N ) ( ( F ` k ) e.
      RR /\ ( G ` k ) e. RR /\ ( F ` k ) <_ ( G ` k ) ) ) ->
        ( seq M ( + , F ) ` N ) <_
        ( seq M ( + , G ) ` N ) ) $=
      ( vx cuz cfv wcel cv cr cle wbr w3a cfz co wral wa cc0 caddc cseq cmin
      eluzfz1 wceq fveq2 eleq1d breq12d 3anbi123d rcla4va sylan simp3 wb simp2
      simp1 subge0 syl11anc mpbird syl cmpt c1 cz simpl eluzel2 uzid 3syl
      opreq12d eqid oprex fvmpt adantr simpr2 simpr1 resubcl eqeltrd ex
      ralimia adantl simpr3 breqtrrd wss wi eluzelz fzp1ss ssralv imp sylan2
      sermono syl211anc elfzelz seq1NEW eqtrd cc recnd 3jca sersub 3brtr3d
      ralimi ax-addrcl seqcl 0re letr mp3an1 mp2and mpbid ) EDGHZIZAJZBHZKIZXGC
      HZKIZXHXJLMZNZADEOPZQZRZSETCDUAHZETBDUAHZUBPZLMZXRXQLMZXPSDCHZDBHZUBPZLMZ
      YDXSLMZXTXPYCKIZYBKIZYCYBLMZNZYEXFDXNIZXOYJDEUCZXMYJADXNXGDUDZXIYGXKYHXLY
      IYMXHYCKXGDBUEZUFYMXJYBKXGDCUEZUFYMXHYCXJYBLYNYOUGUHUIUJZYJYEYIYGYHYIUKYJ
      YHYGYEYIULYGYHYIUMZYGYHYIUNZYBYCUOUPUQURXPDTFXNFJZCHZYSBHZUBPZUSZDUAZHZEU
      UDHZYDXSLXPDXEIZXFXGUUCHZKIZAXNQZSUUHLMZADUTTPEOPZQZUUEUUFLMXPXFDVAIZUUGX
      FXOVBZDEVCZDVDVEUUOXOUUJXFXMUUIAXNXGXNIZXMUUIUUQXMRZUUHXJXHUBPZKUUQUUHUUS
      UDZXMFXGUUBUUSXNUUCYSXGUDYTXJUUAXHUBYSXGCUEYSXGBUEVFUUCVGZXJXHUBVHVIVJZUU
      RXKXIUUSKIUUQXIXKXLVKZUUQXIXKXLVLZXJXHVMUPVNVOVPVQXOXFUUKAXNQZUUMXMUUKAXN
      UUQXMUUKUURSUUSUUHLUURSUUSLMZXLUUQXIXKXLVRUURXKXIUVFXLULUVCUVDXJXHUOUPUQU
      VBVSVOVPXFUVEUUMXFUULXNVTZUVEUUMWAXFUUNEVAIUVGUUPDEWBDEVAWCUPUUKAUULXNWDU
      RWEWFAEDUUCDWGWHXPYKUUEYDUDXFYKXOYLVJYKUUEDUUCHZYDYKUUNUUEUVHUDDDEWITUUCD
      WJURFDUUBYDXNUUCYSDUDYTYBUUAYCUBYSDCUEYSDBUEVFUVAYBYCUBVHVIWKURXOXFXJWLIZ
      XHWLIZUUTNZAXNQUUFXSUDXMUVKAXNUUQXMUVKUURUVIUVJUUTUURXJUVCWMUURXHUVDWMUVB
      WNVOVPACBUUCDEWOWFWPXPYDKIZXSKIZYEYFRXTWAZXPYJUVLYPYJYHYGUVLYQYRYBYCVMUPU
      RXPXQKIZXRKIZUVMXOXFXKAXNQUVOXMXKAXNXIXKXLUMWQAFTKCDEXGYSWRZWSWFZXOXFXIAX
      NQUVPXMXIAXNXIXKXLUNWQAFTKBDEUVQWSWFZXQXRVMUPSKIUVLUVMUVNWTSYDXSXAXBUPXCX
      PUVOUVPXTYAULUVRUVSXQXRUOUPXD $.
      $( [12-Aug-2013] $) $( [27-Dec-2005] $)
  $}

