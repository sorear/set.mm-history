$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Multiplication
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Identity law for multiplication.  Note: see ~ ax1id for commuted
     version. $)
  mulid2 $p |- ( A e. CC -> ( 1 x. A ) = A ) $=
    ( cc wcel c1 cmul co wceq ax1cn mulcom mpan ax1id eqtrd ) ABCZDAEFZADEFZADB
    CMNOGHDAIJAKL $.
    $( [8-Oct-1999] $)

  $( Commutative/associative law for multiplication. $)
  mul12 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( A x. ( B x. C ) ) = ( B x. ( A x. C ) ) ) $=
    ( cc wcel w3a cmul co wceq wa mulcom opreq1d 3adant3 mulass 3com12 3eqtr3d
    ) ADEZBDEZCDEZFABGHZCGHZBAGHZCGHZABCGHGHBACGHGHZQRUAUCISQRJTUBCGABKLMABCNRQ
    SUCUDIBACNOP $.
    $( [30-Apr-2005] $)

  $( Commutative/associative law. $)
  mul23 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A x. B ) x. C ) = ( ( A x. C ) x. B ) ) $=
    ( cc wcel w3a cmul co wceq wa mulcom opreq2d 3adant1 mulass 3com23 3eqtr4d
    ) ADEZBDEZCDEZFABCGHZGHZACBGHZGHZABGHCGHACGHBGHZRSUAUCIQRSJTUBAGBCKLMABCNQS
    RUDUCIACBNOP $.
    $( [8-Oct-1999] $)

  $( Rearrangement of 4 factors. $)
  mul4 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A x. B ) x. ( C x. D ) ) =
                 ( ( A x. C ) x. ( B x. D ) ) ) $=
    ( cc wcel wa cmul co wceq w3a mul23 opreq1d 3expa adantrr mulass 3expb
    mulcl sylan an4s 3eqtr3d ) AEFZBEFZGZCEFZDEFZGZGABHIZCHIZDHIZACHIZBHIZDHIZU
    HCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOUHEFZUGUJUNJZUDUQU
    EUFURUHCDPQABRSUBUEUCUFUMUOJZUKEFZUCUFGUSUBUEGUTUCUFUSUKBDPQACRSTUA $.
    $( [8-Oct-1999] $)

  $( Product of two sums.  (The proof was shortened by Andrew Salmon,
     19-Nov-2011.) $)
  muladd $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
               ( ( A + B ) x. ( C + D ) ) =
       ( ( ( A x. C ) + ( D x. B ) ) + ( ( A x. D ) + ( C x. B ) ) ) ) $=
    ( cc wcel wa caddc co cmul wceq axdistr 3expb addcl sylan adddir 3expa
    adantrr adantrl opreq12d add23 mulcl ad2ant2r ad2ant2lr syl2an anandirs
    syl3anc mulcom ad2ant2l opreq2d addass ad2ant2rl ancoms 3eqtr3d an4s
    3eqtrd ) AEFZBEFZGZCEFZDEFZGZGZABHIZCDHIJIZVDCJIZVDDJIZHIZACJIZBCJIZHIZADJI
    ZBDJIZHIZHIZVIDBJIZHIZVLCBJIZHIHIZVDEFZVBVEVHKZUSVTUTVAWAVDCDLMABNOVCVFVKVG
    VNHUSUTVFVKKZVAUQURUTWBABCPQRUSVAVGVNKZUTUQURVAWCABDPQSTVCVOVIVNHIZVJHIZVQV
    LHIZVRHIZVSVIEFZVJEFZVNEFZVOWEKVCVIVJVNUAUQUTWHURVAACUBZUCZURUTWIUQVABCUBUD
    USVAWJUTUQURVAWJVLEFZVMEFZWJUQVAGURVAGZVLVMNADUBZBDUBZUEUFSUGVCWDWFVJVRHVCV
    IVLHIZVMHIZWRVPHIZWDWFVCVMVPWRHURVAVMVPKUQUTBDUHUIUJWHWMWNWSWDKVCVIVLVMUKWL
    UQVAWMURUTWPULZURVAWNUQUTWQUIUGWHWMVPEFZWTWFKVCVIVLVPUAWLXAURVAXBUQUTVAURXB
    DBUBUMZUIUGUNURUTVJVRKUQVABCUHUDTVQEFZWMVREFZWGVSKVCVQVLVRUKUQUTURVAXDWHXBX
    DUQUTGWOVIVPNWKXCUEUOXAURUTXEUQVAUTURXECBUBUMUDUGUPUP $.
    $( [19-Nov-2011] $) $( [14-Jan-2006] $)

  $( Product of two sums. $)
  muladdOLD $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
               ( ( A + B ) x. ( C + D ) ) =
       ( ( ( A x. C ) + ( D x. B ) ) + ( ( A x. D ) + ( C x. B ) ) ) ) $=
    ( cc wcel wa caddc co cmul wceq adddi addcl adantr simprl simprr syl3anc
    adddir 3expa adantrr adantrl opreq12d add23 mulcl ad2ant2r ad2ant2lr
    syl2an anandirs mulcom ad2ant2l opreq2d addass ad2ant2rl ancoms 3eqtr3d
    an4s 3eqtrd ) AEFZBEFZGZCEFZDEFZGZGZABHIZCDHIJIZVECJIZVEDJIZHIZACJIZBCJIZHI
    ZADJIZBDJIZHIZHIZVJDBJIZHIZVMCBJIZHIHIZVEEFZVAVBVFVIKVDVECDLUTWAVCABMNUTVAV
    BOUTVAVBPQVDVGVLVHVOHUTVAVGVLKZVBURUSVAWBABCRSTUTVBVHVOKZVAURUSVBWCABDRSUAU
    BVDVPVJVOHIZVKHIZVRVMHIZVSHIZVTVJEFZVKEFZVOEFZVPWEKVDVJVKVOUCURVAWHUSVBACUD
    ZUEZUSVAWIURVBBCUDUFUTVBWJVAURUSVBWJVMEFZVNEFZWJURVBGUSVBGZVMVNMADUDZBDUDZU
    GUHUAQVDWDWFVKVSHVDVJVMHIZVNHIZWRVQHIZWDWFVDVNVQWRHUSVBVNVQKURVABDUIUJUKWHW
    MWNWSWDKVDVJVMVNULWLURVBWMUSVAWPUMZUSVBWNURVAWQUJQWHWMVQEFZWTWFKVDVJVMVQUCW
    LXAUSVBXBURVAVBUSXBDBUDUNZUJQUOUSVAVKVSKURVBBCUIUFUBVREFZWMVSEFZWGVTKVDVRVM
    VSULURVAUSVBXDWHXBXDURVAGWOVJVQMWKXCUGUPXAUSVAXEURVBVAUSXECBUDUNUFQUQUQ $.
    $( [14-Jan-2006] $)

  $( A simple product of sums expansion. $)
  muladd11 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( 1 + A ) x. ( 1 + B ) ) =
                 ( ( 1 + A ) + ( B + ( A x. B ) ) ) ) $=
    ( cc wcel wa c1 caddc co cmul wceq ax1cn adddi mp3an2 addcl mpan sylan
    ax1id syl adantr adddir mp3an1 mulid2 adantl opreq1d eqtrd opreq12d ) ACDZB
    CDZEZFAGHZFBGHIHZUJFIHZUJBIHZGHZUJBABIHZGHZGHUJCDZUHUKUNJZUGUQFCDZUHURKUJFB
    LMUSUGUQKFANOZPUIULUJUMUPGUGULUJJZUHUGUQVAUTUJQRSUIUMFBIHZUOGHZUPUSUGUHUMVC
    JKFABTUAUIVBBUOGUHVBBJUGBUBUCUDUEUFUE $.
    $( [21-Feb-2005] $)

  ${
    mul.1 $e |- A e. CC $.
    mul.2 $e |- B e. CC $.
    mul.3 $e |- C e. CC $.
    $( Commutative/associative law that swaps the first two factors in a triple
       product.  (The proof was shortened by Andrew Salmon, 19-Nov-2011.) $)
    mul12i $p |- ( A x. ( B x. C ) ) = ( B x. ( A x. C ) ) $=
      ( cc wcel cmul co wceq mul12 mp3an ) AGHBGHCGHABCIJIJBACIJIJKDEFABCLM $.
      $( [19-Nov-2011] $) $( [11-May-1999] $)

    $( Commutative/associative law that swaps the first two factors in a triple
       product. $)
    mul12iOLD $p |- ( A x. ( B x. C ) ) = ( B x. ( A x. C ) ) $=
      ( cmul co mulcomi opreq1i mulassi 3eqtr3i ) ABGHZCGHBAGHZCGHABCGHGHBACGHG
      HMNCGABDEIJABCDEFKBACEDFKL $.
      $( [11-May-1999] $)

    $( Commutative/associative law that swaps the last two factors in a triple
       product. $)
    mul23i $p |- ( ( A x. B ) x. C ) = ( ( A x. C ) x. B ) $=
      ( cc wcel cmul co wceq mul23 mp3an ) AGHBGHCGHABIJCIJACIJBIJKDEFABCLM $.
      $( [11-May-1999] $)
  $}

  ${
    mul4.1 $e |- A e. CC $.
    mul4.2 $e |- B e. CC $.
    mul4.3 $e |- C e. CC $.
    mul4.4 $e |- D e. CC $.
    $( Rearrangement of 4 factors. $)
    mul4i $p |- ( ( A x. B ) x. ( C x. D ) ) =
               ( ( A x. C ) x. ( B x. D ) ) $=
      ( cc wcel wa cmul co wceq pm3.2i mul4 mp2an ) AIJZBIJZKCIJZDIJZKABLMCDLML
      MACLMBDLMLMNRSEFOTUAGHOABCDPQ $.
      $( [16-Feb-1995] $)

    $( Product of two sums. $)
    muladdi $p |- ( ( A + B ) x. ( C + D ) ) =
        ( ( ( A x. C ) + ( D x. B ) ) + ( ( A x. D ) + ( C x. B ) ) ) $=
      ( cc wcel wa caddc co cmul wceq pm3.2i muladd mp2an ) AIJZBIJZKCIJZDIJZKA
      BLMCDLMNMACNMDBNMLMADNMCBNMLMLMOSTEFPUAUBGHPABCDQR $.
      $( [17-May-1999] $)
  $}

  $( Distribution of multiplication over subtraction.  Theorem I.5 of [Apostol]
     p. 18. $)
  subdi $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( A x. ( B - C ) ) = ( ( A x. B ) - ( A x. C ) ) ) $=
    ( cc wcel w3a cmul co cmin wceq caddc adddi 3simp1 3simp3 subcl 3adant1
    syl3anc pncan3 ancoms opreq2d eqtr3d wb subadd mulcl 3adant3 3adant2 wa
    sylan2 3impb mpbird eqcomd ) ADEZBDEZCDEZFZABGHZACGHZIHZABCIHZGHZUOURUTJZUQ
    UTKHZUPJZUOACUSKHZGHZVBUPULUNUSDEZVEVBJUOACUSLULUMUNMULUMUNNUMUNVFULBCOZPQU
    OVDBAGUMUNVDBJZULUNUMVHCBRSPTUAUPDEZUQDEZUTDEZVAVCUBUOUPUQUTUCULUMVIUNABUDU
    EULUNVJUMACUDUFULUMUNVKULVFVKUMUNUGAUSUDVGUHUIQUJUK $.
    $( [18-Nov-2004] $)

  $( Distribution of multiplication over subtraction.  Theorem I.5 of [Apostol]
     p. 18. $)
  subdir $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A - B ) x. C ) = ( ( A x. C ) - ( B x. C ) ) ) $=
    ( cc wcel w3a cmin co cmul wceq subdi 3coml wa mulcom subcl sylan 3impa
    3adant2 3adant1 opreq12d 3eqtr4d ) ADEZBDEZCDEZFZCABGHZIHZCAIHZCBIHZGHZUFCI
    HZACIHZBCIHZGHUDUBUCUGUJJCABKLUBUCUDUKUGJZUFDEUDUNUBUCMUFCNABOPQUEULUHUMUIG
    UBUDULUHJUCACNRUCUDUMUIJUBBCNSTUA $.
    $( [30-Dec-2005] $)

  ${
    subdi.1 $e |- A e. CC $.
    subdi.2 $e |- B e. CC $.
    subdi.3 $e |- C e. CC $.
    $( Distribution of multiplication over subtraction.  Theorem I.5 of
       [Apostol] p. 18. $)
    subdii $p |- ( A x. ( B - C ) ) = ( ( A x. B ) - ( A x. C ) ) $=
      ( cc wcel cmin co cmul wceq subdi mp3an ) AGHBGHCGHABCIJKJABKJACKJIJLDEFA
      BCMN $.
      $( [26-Nov-1994] $)

    $( Distribution of multiplication over subtraction.  Theorem I.5 of
       [Apostol] p. 18. $)
    subdiri $p |- ( ( A - B ) x. C ) = ( ( A x. C ) - ( B x. C ) ) $=
      ( cc wcel cmin co cmul wceq subdir mp3an ) AGHBGHCGHABIJCKJACKJBCKJIJLDEF
      ABCMN $.
      $( [8-May-1999] $)
  $}

  ${
    mulzer.1 $e |- A e. CC $.
    $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
    mul01i $p |- ( A x. 0 ) = 0 $=
      ( cc0 cmin co cmul 0cn subdii subidi opreq2i mulcli 3eqtr3i ) ACCDEZFEACF
      EZNDENCACCBGGHMCAFCGIJNACBGKIL $.
      $( [23-Nov-1994] $)

    $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
    mul02i $p |- ( 0 x. A ) = 0 $=
      ( cc0 cmul co 0cn mulcomi mul01i eqtri ) CADEACDECCAFBGABHI $.
      $( [23-Nov-1994] $)

    $( Two times a number. $)
    1p1timesi $p |- ( ( 1 + 1 ) x. A ) = ( A + A ) $=
      ( c1 caddc co cmul ax1cn adddiri mulid2i opreq12i eqtri ) CCDEAFECAFEZLDE
      AADECCAGGBHLALADABIZMJK $.
      $( [18-May-1999] $)
  $}

  $( The imaginary unit ` _i ` is not zero. $)
  ine0 $p |- _i =/= 0 $=
    ( ci cc0 wne wceq wn c1 ax1ne0 df-ne mpbi caddc co cmul opreq2 axicn
    mul01i syl6req opreq1d ax1cn addid2i axi2m1 3eqtr3g mto mpbir ) ABCABDZEUDF
    BDZFBCUEEGFBHIUDBFJKAALKZFJKFBUDBUFFJUDUFABLKBABALMANOPQFRSTUAUBABHUC $.
    $( [29-Apr-2005] $) $( [6-May-1999] $)

  ${
    $d x y z $.
    $( 1 is a real number.  This used to be one of our postulates for complex
       numbers, but Eric Schmidt discovered that it could be derived from a
       weaker postulate, ~ ax1cn , by exploiting properties of the imaginary
       unit ` _i ` .  (Contributed by Eric Schmidt, 11-Apr-2007.) $)
    1re $p |- 1 e. RR $=
      ( vz vx vy cv cc0 wne cr wrex c1 wcel ci cmul co caddc wceq cc axicn
      axcnre ax-mp wa wo neeq1 rcla4ev adantlr adantll jaodan ex ine0 mpbii wn
      ioran nne anbi12i bitri opreq12 opreq2 mul01i syl6eq sylan2 0cn addid1i
      sylbi necon1ai syl syl5 r19.23aivv axrrecex wi eleq1 remulcl syl5cbi
      r19.23adva adantr mpd r19.23aiva ) ADZEFZAGHZIGJZKBDZKCDZLMZNMZOZCGHBGHZV
      RKPJWEQBCKRSWDVRBCGGVTGJZWAGJZTZVTEFZWAEFZUAZVRWDWHWKVRWHWIVRWJWFWIVRWGVQ
      WIAVTGVPVTEUBUCUDWGWJVRWFVQWJAWAGVPWAEUBUCUEUFUGWDWCEFZWKWDKEFWLUHKWCEUBU
      IWKWCEWKUJZVTEOZWAEOZTZWCEOWMWIUJZWJUJZTWPWIWJUKWQWNWRWOVTEULWAEULUMUNWPW
      CEENMZEWNWBEOWCWSOWOVTEWBENUOWOWBKELMEWAEKLUPKQUQURUSEUTVAURVBVCVDVEVFSVQ
      VSAGVPGJZVQTVPVTLMZIOZBGHZVSBVPVGWTXCVSVHVQWTXBVSBGXBXAGJVSWTWFTXAIGVIVPV
      TVJVKVLVMVNVOS $.
      $( [11-Apr-2007] $)
  $}

  $( A theorem for reals analogous the second Peano postulate ~ peano2nn . $)
  peano2re $p |- ( A e. RR -> ( A + 1 ) e. RR ) $=
    ( cr wcel c1 caddc co 1re readdcl mpan2 ) ABCDBCADEFBCGADHI $.
    $( [5-Jul-2005] $)

  $( Closure law for negative of reals.  The weak deduction theorem ~ dedth is
     used to convert hypothesis of the inference (deduction) form of this
     theorem, ~ renegcli , to an antecedent. $)
  renegcl $p |- ( A e. RR -> -u A e. RR ) $=
    ( cr wcel cneg c1 cif wceq negeq eleq1d 1re elimel renegcli dedth ) ABCZADZ
    BCNAEFZDZBCAEAPGOQBAPHIPAEBJKLM $.
    $( [20-Jan-1997] $)

  $( Closure law for subtraction of reals. $)
  resubcl $p |- ( ( A e. RR /\ B e. RR ) -> ( A - B ) e. RR ) $=
    ( cr wcel wa cneg caddc co cmin cc wceq negsub recn syl2an readdcl renegcl
    sylan2 eqeltrrd ) ACDZBCDZEABFZGHZABIHZCAJDBJDUBUCKSTABLAMBMNSUACDUBCDTAUAO
    BPQR $.
    $( [2-Aug-2006] $) $( [20-Jan-1997] $)

  ${
    resubcl.1 $e |- A e. RR $.
    resubcl.2 $e |- B e. RR $.
    $( Closure law for subtraction of reals. $)
    resubcli $p |- ( A - B ) e. RR $=
      ( cr wcel cmin co resubcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [17-Jan-1997] $)
  $}

  ${
    $d x y $.
    $( 0 is a real number.  Proved without referencing ~ 1re .  (Contributed by
       Eric Schmidt, 21-May-2007.) $)
    0re $p |- 0 e. RR $=
      ( vx vy cv cr wcel wex cc0 ci cmul co caddc wceq wrex cc 0cn axcnre
      ax-mp wa df-rex exsimpl sylbi axrnegex pm3.27 readdcl adantr eqeltrrd ex
      r19.23adva mpd 19.23aiv ) ACZDEZAFZGDEZGUKHBCZIJKJLBDMZADMZUMGNEUQOABGPQU
      QULUPRAFUMUPADSULUPATUAQULUNAULUKUOKJZGLZBDMUNBUKUBULUSUNBDULUODERZUSUNUT
      USRURGDUTUSUCUTURDEUSUKUOUDUEUFUGUHUIUJQ $.
      $( [22-May-2007] $)
  $}

  $( 0 is a real number. $)
  0reALT $p |- 0 e. RR $=
    ( c1 cmin co cc0 cr ax1cn subidi 1re resubcli eqeltrri ) AABCDEAFGAAHHIJ $.
    $( [19-Feb-2005] $)

  $( "Reverse" second Peano postulate analog for reals. $)
  peano2rem $p |- ( N e. RR -> ( N - 1 ) e. RR ) $=
    ( cr wcel c1 cmin co 1re resubcl mpan2 ) ABCDBCADEFBCGADHI $.
    $( [6-Feb-2007] $)

  $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
  mul01 $p |- ( A e. CC -> ( A x. 0 ) = 0 ) $=
    ( cc wcel cc0 cmul co wceq cif opreq1 eqeq1d 0cn elimel mul01i dedth ) ABCZ
    ADEFZDGOADHZDEFZDGADAQGPRDAQDEIJQADBKLMN $.
    $( [15-May-1999] $)

  $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
  mul02 $p |- ( A e. CC -> ( 0 x. A ) = 0 ) $=
    ( cc wcel cc0 cmul co wceq 0cn mulcom mpan mul01 eqtrd ) ABCZDAEFZADEFZDDBC
    MNOGHDAIJAKL $.
    $( [10-Aug-1999] $)

  ${
    mulneg.1 $e |- A e. CC $.
    mulneg.2 $e |- B e. CC $.
    $( Product with negative is negative of product.  Theorem I.12 of [Apostol]
       p. 18. $)
    mulneg1i $p |- ( -u A x. B ) = -u ( A x. B ) $=
      ( cc0 cmul co cmin cneg mul01i mulcomi opreq12i df-neg opreq1i 0cn
      subcli subdii 3eqtri 3eqtr4i ) BEFGZBAFGZHGZEABFGZHGAIZBFGZUCITEUAUCHBDJB
      ADCKLUEEAHGZBFGBUFFGUBUDUFBFAMNUFBEAOCPDKBEADOCQRUCMS $.
      $( [10-Feb-1995] $)

    $( Product with negative is negative of product. $)
    mulneg2i $p |- ( A x. -u B ) = -u ( A x. B ) $=
      ( cneg cmul co negcli mulcomi mulneg1i negeqi 3eqtri ) ABEZFGMAFGBAFGZEAB
      FGZEAMCBDHIBADCJNOBADCIKL $.
      $( [31-Jul-1999] $)

    $( Product of two negatives.  Theorem I.12 of [Apostol] p. 18. $)
    mul2negi $p |- ( -u A x. -u B ) = ( A x. B ) $=
      ( cneg cmul co negcli mulneg1i mulcomi eqtri negeqi mulcli negnegi
      3eqtri ) AEBEZFGZBAFGZABFGQAPFGZEREZERAPCBDHZISTSPAFGTAPCUAJBADCIKLRBADCM
      NOBADCJK $.
      $( [14-Feb-1995] $)

    $( Distribution of negative over addition.  (The proof was shortened by
       Andrew Salmon, 19-Nov-2011.) $)
    negdii $p |- -u ( A + B ) = ( -u A + -u B ) $=
      ( caddc co cneg wceq cc0 addcli negidi opreq12i 0cn addid1i eqtri negcli
      add4i 3eqtr2i addcani mpbi ) ABEFZUAGZEFZUAAGZBGZEFZEFZHUBUFHUCIAUDEFZBUE
      EFZEFZUGUAABCDJZKUJIIEFIUHIUIIEACKBDKLIMNOAUDBUECACPZDBDPZQRUAUBUFUKUAUKP
      UDUEULUMJST $.
      $( [19-Nov-2011] $) $( [28-Jul-1999] $)

    $( Distribution of negative over addition. $)
    negdiiOLD $p |- -u ( A + B ) = ( -u A + -u B ) $=
      ( c1 caddc co cmul cneg addcli mulid2i negeqi ax1cn negcli adddii
      mulneg1i eqtri opreq12i 3eqtr3i eqtr3i ) EABFGZHGZIZUAIAIZBIZFGZUBUAUAABC
      DJZKLEIZUAHGUHAHGZUHBHGZFGUCUFUHABEMNCDOEUAMUGPUIUDUJUEFUIEAHGZIUDEAMCPUK
      AACKLQUJEBHGZIUEEBMDPULBBDKLQRST $.
      $( [28-Jul-1999] $)

    $( Distribution of negative over subtraction. $)
    negsubdii $p |- -u ( A - B ) = ( -u A + B ) $=
      ( cneg caddc co cmin negcli negdii negsubi negeqi negnegi opreq2i
      3eqtr3i ) ABEZFGZEAEZPEZFGABHGZERBFGAPCBDIJQTABCDKLSBRFBDMNO $.
      $( [6-Aug-1999] $)

    $( Distribution of negative over subtraction. $)
    negsubdi2i $p |- -u ( A - B ) = ( B - A ) $=
      ( cmin co cneg caddc negsubdii negcli addcomi negsubi 3eqtri ) ABEFGAGZBH
      FBNHFBAEFABCDINBACJDKBADCLM $.
      $( [1-Oct-1999] $)
  $}

  $( Product with negative is negative of product.  Theorem I.12 of [Apostol]
     p. 18. $)
  mulneg1 $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( -u A x. B ) = -u ( A x. B ) ) $=
    ( cc wcel cneg cmul co wceq cc0 cif negeq opreq1d opreq1 negeqd eqeq12d
    opreq2 0cn elimel mulneg1i dedth2h ) ACDZBCDZAEZBFGZABFGZEZHUAAIJZEZBFGZUGB
    FGZEZHUHUBBIJZFGZUGULFGZEZHABIIAUGHZUDUIUFUKUPUCUHBFAUGKLUPUEUJAUGBFMNOBULH
    ZUIUMUKUOBULUHFPUQUJUNBULUGFPNOUGULAICQRBICQRST $.
    $( [14-May-1999] $)

  $( The product with a negative is the negative of the product. $)
  mulneg2 $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( A x. -u B ) = -u ( A x. B ) ) $=
    ( cc wcel wa cneg cmul co wceq mulneg1 ancoms mulcom negcl sylan2 negeqd
    3eqtr4d ) ACDZBCDZEZBFZAGHZBAGHZFZATGHZABGHZFRQUAUCIBAJKQTCDUDUAIRATLBMNSUE
    UBABLOP $.
    $( [30-Jul-2004] $)

  $( Swap the negative sign in a product. $)
  mulneg12 $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( -u A x. B ) = ( A x. -u B ) ) $=
    ( cc wcel wa cneg cmul co mulneg1 mulneg2 eqtr4d ) ACDBCDEAFBGHABGHFABFGHAB
    IABJK $.
    $( [30-Jul-2004] $)

  $( Product of two negatives.  Theorem I.12 of [Apostol] p. 18.  (The proof
     was shortened by Andrew Salmon, 19-Nov-2011.) $)
  mul2neg $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A x. -u B ) = ( A x. B ) ) $=
    ( cc wcel wa cneg cmul co wceq mulneg12 negcl sylan2 negneg adantl opreq2d
    eqtrd ) ACDZBCDZEZAFBFZGHZATFZGHZABGHQTCDUAUCIRATJBKLSUBBAGRUBBIQBMNOP $.
    $( [19-Nov-2011] $) $( [30-Jul-2004] $)

  $( Product of two negatives.  Theorem I.12 of [Apostol] p. 18. $)
  mul2negOLD $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A x. -u B ) = ( A x. B ) )
      $=
    ( cc wcel cneg cmul co wceq cc0 cif negeq opreq1d opreq1 eqeq12d opreq2d
    opreq2 0cn elimel mul2negi dedth2h ) ACDZBCDZAEZBEZFGZABFGZHUAAIJZEZUDFGZUG
    BFGZHUHUBBIJZEZFGZUGUKFGZHABIIAUGHZUEUIUFUJUOUCUHUDFAUGKLAUGBFMNBUKHZUIUMUJ
    UNUPUDULUHFBUKKOBUKUGFPNUGUKAICQRBICQRST $.
    $( [30-Jul-2004] $)

  $( Distribution of negative over addition. $)
  negdi $p |- ( ( A e. CC /\ B e. CC ) ->
                 -u ( A + B ) = ( -u A + -u B ) ) $=
    ( cc wcel caddc co cneg wceq cc0 cif opreq1 negeqd negeq opreq1d eqeq12d
    opreq2 opreq2d 0cn elimel negdii dedth2h ) ACDZBCDZABEFZGZAGZBGZEFZHUBAIJZB
    EFZGZUIGZUGEFZHUIUCBIJZEFZGZULUNGZEFZHABIIAUIHZUEUKUHUMUSUDUJAUIBEKLUSUFULU
    GEAUIMNOBUNHZUKUPUMURUTUJUOBUNUIEPLUTUGUQULEBUNMQOUIUNAICRSBICRSTUA $.
    $( [10-May-2004] $)

  $( Distribution of negative over addition. $)
  negdi2 $p |- ( ( A e. CC /\ B e. CC ) ->
                 -u ( A + B ) = ( -u A - B ) ) $=
    ( cc wcel wa caddc co cneg cmin negdi wceq negsub negcl sylan eqtrd ) ACDZB
    CDZEABFGHAHZBHFGZRBIGZABJRCDQSTKPRBLAMNO $.
    $( [1-Jan-2006] $)

  $( Distribution of negative over subtraction. $)
  negsubdi $p |- ( ( A e. CC /\ B e. CC ) -> -u ( A - B ) = ( -u A + B ) ) $=
    ( cc wcel wa cneg caddc co cmin wceq negdi negcl sylan2 negsub negeqd
    negneg adantl opreq2d 3eqtr3d ) ACDZBCDZEZABFZGHZFZAFZUCFZGHZABIHZFUFBGHTUC
    CDUEUHJUAAUCKBLMUBUDUIABNOUBUGBUFGUAUGBJTBPQRS $.
    $( [1-Aug-2006] $) $( [15-Nov-2004] $)

  $( Distribution of negative over subtraction. $)
  negsubdi2 $p |- ( ( A e. CC /\ B e. CC ) -> -u ( A - B ) = ( B - A ) ) $=
    ( cc wcel wa cmin co cneg caddc negsubdi wceq addcom negcl sylan negsub
    ancoms 3eqtrd ) ACDZBCDZEABFGHAHZBIGZBTIGZBAFGZABJTCDSUAUBKRTBLAMNSRUBUCKBA
    OPQ $.
    $( [1-Aug-2006] $) $( [4-Oct-1999] $)

  $( Relationship between subtraction and negative.  (Contributed by Paul
     Chapman, 8-Oct-2007.) $)
  neg2sub $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A - -u B ) = ( B - A ) ) $=
    ( cc wcel wa cneg cmin co caddc wceq subneg negcl sylan negsubdi negsubdi2
    3eqtr2d ) ACDZBCDZEAFZBFGHZSBIHZABGHFBAGHSCDRTUAJQSBKALMABNABOP $.
    $( [8-Oct-2007] $)

  $( Convert a subtraction to addition using multiplication by a negative. $)
  submul2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( A - ( B x. C ) ) = ( A + ( B x. -u C ) ) ) $=
    ( cc wcel cmul co cmin cneg caddc wceq wa mulneg2 adantl opreq2d negsub
    mulcl sylan2 eqtr2d 3impb ) ADEZBDEZCDEZABCFGZHGZABCIFGZJGZKUAUBUCLZLZUGAUD
    IZJGZUEUIUFUJAJUHUFUJKUABCMNOUAUDDEUKUEKUHAUDPBCQRST $.
    $( [2-Feb-2007] $)

  $( Law for double subtraction. $)
  subsub2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( A - ( B - C ) ) = ( A + ( C - B ) ) ) $=
    ( cc wcel w3a cmin co cneg caddc wceq wa negsub subcl sylan2 3impb
    negsubdi2 opreq2d 3adant1 eqtr3d ) ADEZBDEZCDEZFABCGHZIZJHZAUDGHZACBGHZJHZU
    AUBUCUFUGKZUAUDDEUJUBUCLZAUDMBCNOPUBUCUFUIKUAUKUEUHAJBCQRST $.
    $( [2-Aug-2006] $) $( [30-Jun-2005] $)

  $( Law for double subtraction. $)
  subsub $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( A - ( B - C ) ) = ( ( A - B ) + C ) ) $=
    ( cc wcel w3a cmin co caddc subsub2 wceq addsubass addsub eqtr3d 3com23
    eqtrd ) ADEZBDEZCDEZFABCGHGHACBGHIHZABGHCIHZABCJQSRTUAKQSRFACIHBGHTUAACBLAC
    BMNOP $.
    $( [13-May-2004] $)

  $( Law for double subtraction. $)
  subsub3 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( A - ( B - C ) ) = ( ( A + C ) - B ) ) $=
    ( cc wcel w3a cmin co caddc subsub2 wceq addsubass 3com23 eqtr4d ) ADEZBDEZ
    CDEZFABCGHGHACBGHIHZACIHBGHZABCJOQPSRKACBLMN $.
    $( [27-Jul-2005] $)

  $( Law for double subtraction. $)
  subsub4 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A - B ) - C ) = ( A - ( B + C ) ) ) $=
    ( cc wcel w3a cneg cmin co caddc wceq subsub negcl syl3an3 subneg 3adant1
    opreq2d wa negsub subcl sylan 3impa 3eqtr3rd ) ADEZBDEZCDEZFZABCGZHIZHIZABH
    IZUHJIZABCJIZHIUKCHIZUDUEUHDEUJULKUFABUHLCMNUGUIUMAHUEUFUIUMKUDBCOPQUDUEUFU
    LUNKZUKDEUFUOUDUERUKCSABTUAUBUC $.
    $( [2-Aug-2006] $) $( [19-Aug-2005] $)

  $( Swap the second and third terms in a double subtraction. $)
  sub23 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A - B ) - C ) = ( ( A - C ) - B ) ) $=
    ( cc wcel w3a caddc co cmin wceq addcom 3adant1 opreq2d subsub4 3com23
    3eqtr4d ) ADEZBDEZCDEZFZABCGHZIHACBGHZIHZABIHCIHACIHBIHZTUAUBAIRSUAUBJQBCKL
    MABCNQSRUDUCJACBNOP $.
    $( [19-Aug-2005] $)

  $( Cancellation law for subtraction. $)
  nnncan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - ( B - C ) ) - C ) = ( A - B ) ) $=
    ( cc wcel w3a cmin co caddc wceq subsub4 subcl 3adant1 syld3an2 wa npcan
    opreq2d eqtrd ) ADEZBDEZCDEZFABCGHZGHCGHZAUBCIHZGHZABGHZSUBDEZUAUCUEJTAUBCK
    TUAUGSBCLMNTUAUEUFJSTUAOUDBAGBCPQMR $.
    $( [4-Sep-2005] $)

  $( Cancellation law for subtraction.  (The proof was shortened by Andrew
     Salmon, 19-Nov-2011.) $)
  nnncan1 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) - ( A - C ) ) = ( C - B ) ) $=
    ( cc wcel w3a cmin co wceq sub23 subcl 3adant2 syld3an3 wa caddc subsub2
    3anidm12 pncan3 eqtrd opreq1d ) ADEZBDEZCDEZFZABGHACGHZGHZAUEGHZBGHZCBGHUAU
    BUEDEZUFUHIUCABUEJUAUCUIUBACKLMUDUGCBGUAUCUGCIUBUAUCNUGACAGHOHZCUAUCUGUJIAA
    CPQACRSLTS $.
    $( [19-Nov-2011] $) $( [8-Feb-2005] $)

  $( Cancellation law for subtraction. $)
  nnncan1OLD $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) - ( A - C ) ) = ( C - B ) ) $=
    ( cc wcel w3a cmin co cneg caddc wceq wa negsub addcom negcl sylan2 eqtr3d
    subcl 3adant3 3adant2 sylanc negsubdi2 opreq1d npncan 3coml 3eqtrd ) ADEZBD
    EZCDEZFZABGHZACGHZGHZULIZUKJHZCAGHZUKJHZCBGHZUKDEZULDEZUMUOKUJUSUTLUKUNJHZU
    MUOUKULMUSUNDEVAUOKUTUKUNNULOPQUGUHUSUIABRSUGUIUTUHACRTUAUJUNUPUKJUGUIUNUPK
    UHACUBTUCUIUGUHUQURKCABUDUEUF $.
    $( [1-Aug-2006] $) $( [8-Feb-2005] $)

  $( Cancellation law for subtraction. $)
  nnncan2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - C ) - ( B - C ) ) = ( A - B ) ) $=
    ( cc wcel w3a cmin co wceq sub23 subcl 3adant1 syld3an2 nnncan eqtr3d ) ADE
    ZBDEZCDEZFABCGHZGHCGHZACGHSGHZABGHPSDEZRTUAIQASCJQRUBPBCKLMABCNO $.
    $( [1-Oct-2005] $)

  $( Cancellation law for subtraction.  (The proof was shortened by Andrew
     Salmon, 19-Nov-2011.) $)
  nncan $p |- ( ( A e. CC /\ B e. CC ) -> ( A - ( A - B ) ) = B ) $=
    ( cc wcel wa cmin co caddc wceq subsub2 3anidm12 pncan3 eqtrd ) ACDZBCDZEAA
    BFGFGZABAFGHGZBNOPQIAABJKABLM $.
    $( [19-Nov-2011] $) $( [21-Jun-2005] $)

  $( Cancellation law for subtraction. $)
  nncanOLD $p |- ( ( A e. CC /\ B e. CC ) -> ( A - ( A - B ) ) = B ) $=
    ( cc wcel wa cc0 cmin co wceq 0cn nnncan1 mp3an2 subid1 adantr opreq1d
    adantl 3eqtr3d ) ACDZBCDZEZAFGHZABGHZGHZBFGHZAUBGHBRFCDSUCUDIJAFBKLTUAAUBGR
    UAAISAMNOSUDBIRBMPQ $.
    $( [21-Jun-2005] $)

  $( Cancellation law for subtraction. $)
  nppcan2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - ( B + C ) ) + C ) = ( A - B ) ) $=
    ( cc wcel w3a caddc co cmin wceq subsub addcl 3adant1 syld3an2 pncan
    opreq2d eqtr3d ) ADEZBDEZCDEZFZABCGHZCIHZIHZAUBIHCGHZABIHRUBDEZTUDUEJSAUBCK
    STUFRBCLMNUAUCBAISTUCBJRBCOMPQ $.
    $( [29-Sep-2005] $)

  $( Product with minus one is negative. $)
  mulm1 $p |- ( A e. CC -> ( -u 1 x. A ) = -u A ) $=
    ( cc wcel c1 cneg cmul co wceq ax1cn mulneg1 mpan mulid2 negeqd eqtrd ) ABC
    ZDEAFGZDAFGZEZAEDBCOPRHIDAJKOQAALMN $.
    $( [16-Nov-1999] $)

  ${
    mulm1.1 $e |- A e. CC $.
    $( Product with minus one is negative. $)
    mulm1i $p |- ( -u 1 x. A ) = -u A $=
      ( cc wcel c1 cneg cmul co wceq mulm1 ax-mp ) ACDEFAGHAFIBAJK $.
      $( [31-Jul-1999] $)
  $}

  $( Rearrangement of 4 terms in a mixed addition and subtraction. $)
  addsub4 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A + B ) - ( C + D ) ) = ( ( A - C ) + ( B - D ) ) ) $=
    ( cc wcel wa caddc co cneg cmin wceq negdi adantl opreq2d add4 negcl
    anim12i sylan2 eqtrd negsub addcl syl2an opreqan12d an4s 3eqtr3d ) AEFZBEFZ
    GZCEFZDEFZGZGZABHIZCDHIZJZHIZACJZHIZBDJZHIZHIZUNUOKIZACKIZBDKIZHIZUMUQUNURU
    THIZHIZVBUMUPVGUNHULUPVGLUICDMNOUIUREFZUTEFZGVHVBLULABURUTPUJVIUKVJCQDQRSTU
    NEFUOEFUQVCLUIULUNUOUAABUBCDUBUCUGUJUHUKVBVFLUGUJGUHUKGUSVDVAVEHACUABDUAUDU
    EUF $.
    $( [4-Mar-2005] $)

  ${
    addsub4.1 $e |- A e. CC $.
    addsub4.2 $e |- B e. CC $.
    addsub4.3 $e |- C e. CC $.
    addsub4.4 $e |- D e. CC $.
    $( Rearrangement of 4 terms in a mixed addition and subtraction. $)
    addsub4i $p |- ( ( A + B ) - ( C + D ) ) =
                 ( ( A - C ) + ( B - D ) ) $=
      ( cc wcel wa caddc co cmin wceq pm3.2i addsub4 mp2an ) AIJZBIJZKCIJZDIJZK
      ABLMCDLMNMACNMBDNMLMOSTEFPUAUBGHPABCDQR $.
      $( [17-Oct-1999] $)
  $}

  $( Rearrangement of 4 terms in a mixed addition and subtraction. $)
  subadd4 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A - B ) - ( C - D ) ) = ( ( A + D ) - ( B + C ) ) ) $=
    ( cc wcel wa cmin co caddc wceq subsub2 3expb subcl sylan addsub4 an42s
    eqtr4d ) AEFZBEFZGZCEFZDEFZGZGABHIZCDHIHIZUEDCHIJIZADJIBCJIHIZUEEFZUDUFUGKZ
    UAUIUBUCUJUECDLMABNOSUCTUBUHUGKADBCPQR $.
    $( [24-Aug-2006] $)

  $( Rearrangement of 4 terms in a subtraction. $)
  sub4 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A - B ) - ( C - D ) ) = ( ( A - C ) - ( B - D ) ) ) $=
    ( cc wcel wa caddc co cmin wceq addcom ad2ant2lr opreq2d subadd4 an4s
    3eqtr4d ) AEFZBEFZGCEFZDEFZGGZADHIZBCHIZJIUCCBHIZJIZABJICDJIJIACJIBDJIJIZUB
    UDUEUCJSTUDUEKRUABCLMNABCDORTSUAUGUFKACBDOPQ $.
    $( [23-Nov-2007] $)

  $( Product of two differences. $)
  mulsub $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
               ( ( A - B ) x. ( C - D ) ) =
       ( ( ( A x. C ) + ( D x. B ) ) - ( ( A x. D ) + ( C x. B ) ) ) ) $=
    ( cc wcel wa cneg caddc co cmul cmin negsub opreqan12d wceq muladd negcl
    sylanr2 sylanl2 mul2neg ancoms opreq2d ad2ant2l mulneg2 negdi mulcl syl2an
    eqtr4d ancom2s an42s opreq12d addcl an4s sylanc 3eqtrd eqtr3d ) AEFZBEFZGZC
    EFZDEFZGZGZABHZIJZCDHZIJZKJZABLJZCDLJZKJACKJZDBKJZIJZADKJZCBKJZIJZLJZUSVBVE
    VIVGVJKABMCDMNVCVHVKVFVDKJZIJZAVFKJZCVDKJZIJZIJZVMVPHZIJZVQUQVDEFZVBVHWCOZU
    RUQWFGUTVFEFWGVAAVDCVFPDQRBQSVCVSVMWBWDIURVAVSVMOUQUTURVAGZVRVLVKIVAURVRVLO
    DBTUAUBUCUQVAURUTWBWDOZUQVAGZUTURWIWJUTURGZGWBVNHZVOHZIJZWDWJWKVTWLWAWMIADU
    DCBUDNVNEFZVOEFZWDWNOWJWKVNVOUEADUFZCBUFZUGUHUIUJUKVMEFZVPEFZWEVQOVCVMVPMUQ
    UTURVAWSVKEFVLEFZWSUQUTGWHVKVLULACUFVAURXADBUFUAUGUMUQVAURUTWTWOWPWTWJURUTG
    VNVOULWQUTURWPWRUAUGUJUNUOUP $.
    $( [14-Jan-2006] $)

  $( Cancellation law for mixed addition and subtraction. $)
  pnpcan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) - ( A + C ) ) = ( B - C ) ) $=
    ( cc wcel caddc co cmin wceq wa addsub4 anandis cc0 subid opreq1d subcl
    addid2 syl sylan9eq eqtrd 3impb ) ADEZBDEZCDEZABFGACFGHGZBCHGZIUBUCUDJZJUEA
    AHGZUFFGZUFUBUCUDUEUIIABACKLUBUGUIMUFFGZUFUBUHMUFFANOUGUFDEUJUFIBCPUFQRSTUA
    $.
    $( [4-Mar-2005] $)

  $( Cancellation law for mixed addition and subtraction.  (Contributed by
     Scott Fenton, 9-Jun-2006.) $)
  pnpcan2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A + C ) - ( B + C ) ) = ( A - B ) ) $=
    ( cc wcel w3a caddc co cmin wceq addcom 3adant2 3adant1 opreq12d pnpcan
    3coml eqtrd ) ADEZBDEZCDEZFZACGHZBCGHZIHCAGHZCBGHZIHZABIHZUAUBUDUCUEIRTUBUD
    JSACKLSTUCUEJRBCKMNTRSUFUGJCABOPQ $.
    $( [9-Jun-2006] $)

  $( Cancellation law for mixed addition and subtraction. $)
  pnncan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) - ( A - C ) ) = ( B + C ) ) $=
    ( cc wcel w3a caddc co cneg cmin wceq pnpcan negcl syl3an3 negsub 3adant2
    opreq2d subneg 3adant1 3eqtr3d ) ADEZBDEZCDEZFZABGHZACIZGHZJHZBUFJHZUEACJHZ
    JHBCGHZUAUBUFDEUHUIKUCABUFLCMNUDUGUJUEJUAUCUGUJKUBACOPQUBUCUIUKKUABCRST $.
    $( [2-Aug-2006] $) $( [30-Jun-2005] $)

  $( Cancellation law for mixed addition and subtraction. $)
  ppncan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) + ( C - B ) ) = ( A + C ) ) $=
    ( cc wcel w3a caddc co cmin wceq addcom 3adant3 opreq1d subsub2 addcl
    syld3an1 pnncan 3com12 3eqtr3d ) ADEZBDEZCDEZFZABGHZBCIHZIHZBAGHZUEIHZUDCBI
    HGHZACGHZUCUDUGUEITUAUDUGJUBABKLMUDDEZUAUBUFUIJTUDBCNTUAUKUBABOLPUATUBUHUJJ
    BACQRS $.
    $( [30-Jun-2005] $)

  ${
    pnncan.1 $e |- A e. CC $.
    pnncan.2 $e |- B e. CC $.
    pnncan.3 $e |- C e. CC $.
    $( Cancellation law for mixed addition and subtraction. $)
    pnncani $p |- ( ( A + B ) - ( A - C ) ) = ( B + C ) $=
      ( cc wcel caddc co cmin wceq pnncan mp3an ) AGHBGHCGHABIJACKJKJBCIJLDEFAB
      CMN $.
      $( [14-Jan-2006] $)
  $}

