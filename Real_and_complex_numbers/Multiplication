$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Multiplication
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Identity law for multiplication.  Note:  see ~ ax1id for commuted
     version. $)
  mulid2t $p |- ( A e. CC -> ( 1 x. A ) = A ) $=
    ( cc wcel c1 cmul co wceq ax1cn axmulcom mpan ax1id eqtrd ) ABCZDAEFZADEFZA
    DBCMNOGHDAIJAKL $.
    $( [8-Oct-1999] $)

  $( Commutative/associative law for multiplication. $)
  mul12t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( A x. ( B x. C ) ) = ( B x. ( A x. C ) ) ) $=
    ( cc wcel w3a cmul co wceq wa axmulcom opreq1d 3adant3 axmulass 3com12
    3eqtr3d ) ADEZBDEZCDEZFABGHZCGHZBAGHZCGHZABCGHGHBACGHGHZQRUAUCISQRJTUBCGABK
    LMABCNRQSUCUDIBACNOP $.
    $( [29-May-2005] $) $( [30-Apr-2005] $)

  $( Commutative/associative law. $)
  mul23t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A x. B ) x. C ) = ( ( A x. C ) x. B ) ) $=
    ( cc wcel w3a cmul co wceq wa axmulcom opreq2d 3adant1 axmulass 3com23
    3eqtr4d ) ADEZBDEZCDEZFABCGHZGHZACBGHZGHZABGHCGHACGHBGHZRSUAUCIQRSJTUBAGBCK
    LMABCNQSRUDUCIACBNOP $.
    $( [8-Oct-1999] $)

  $( Rearrangement of 4 factors. $)
  mul4t $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A  x. B )  x. ( C  x. D ) ) =
                 ( ( A  x. C )  x. ( B  x. D ) ) ) $=
    ( cc wcel wa cmul co wceq w3a mul23t opreq1d 3expa adantrr axmulass 3expb
    axmulcl sylan an4s 3eqtr3d ) AEFZBEFZGZCEFZDEFZGZGABHIZCHIZDHIZACHIZBHIZDHI
    ZUHCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOUHEFZUGUJUNJZUDU
    QUEUFURUHCDPQABRSUBUEUCUFUMUOJZUKEFZUCUFGUSUBUEGUTUCUFUSUKBDPQACRSTUA $.
    $( [8-Oct-1999] $)

  $( Product of two sums. $)
  muladdt $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
               ( ( A + B ) x. ( C + D ) ) =
       ( ( ( A x. C ) + ( D x. B ) ) + ( ( A x. D ) + ( C x. B ) ) ) ) $=
    ( cc wcel wa caddc co cmul wceq axdistr axaddcl adantr simprl simprr
    syl3anc adddirt 3expa adantrr adantrl opreq12d add23t axmulcl ad2ant2r
    adantll syl2an anandirs axmulcom ad2ant2l opreq2d axaddass adantlr ancoms
    3eqtr3d adantl an42s an4s 3eqtrd ) AEFZBEFZGZCEFZDEFZGZGZABHIZCDHIJIZVGCJIZ
    VGDJIZHIZACJIZBCJIZHIZADJIZBDJIZHIZHIZVLDBJIZHIZVOCBJIZHIHIZVGEFZVCVDVHVKKV
    FVGCDLVBWCVEABMNVBVCVDOVBVCVDPQVFVIVNVJVQHVBVCVIVNKZVDUTVAVCWDABCRSTVBVDVJV
    QKZVCUTVAVDWEABDRSUAUBVFVRVLVQHIZVMHIZVTVOHIZWAHIZWBVLEFZVMEFZVQEFZVRWGKVFV
    LVMVQUCUTVCWJVAVDACUDZUEZVAVEWKUTVAVCWKVDBCUDTUFVBVDWLVCUTVAVDWLVOEFZVPEFZW
    LUTVDGZVAVDGZVOVPMADUDZBDUDZUGUHUAQVFWFWHVMWAHVFVLVOHIZVPHIZXAVSHIZWFWHVFVP
    VSXAHVAVDVPVSKUTVCBDUIUJUKWJWOWPXBWFKVFVLVOVPULWNUTVEWOVAUTVDWOVCWSUAUMZVAV
    DWPUTVCWTUJQWJWOVSEFZXCWHKVFVLVOVSUCWNXDVAVDXEUTVCVDVAXEDBUDUNZUJQUOUTVDVAV
    CVMWAKZVAVCGZXGWQBCUIUPUQUBVTEFZWOWAEFZWIWBKVFVTVOWAULUTVCVAVDXIWJXEXIUTVCG
    WRVLVSMWMXFUGURXDUTVDVAVCXJXHXJWQVCVAXJCBUDUNUPUQQUSUS $.
    $( [15-Jan-2006] $) $( [14-Jan-2006] $)

  $( A simple product of sums expansion. $)
  muladd11t $p |- ( ( A e. CC /\ B e. CC ) -> ( ( 1 + A ) x. ( 1 + B ) ) =
                 ( ( 1 + A ) + ( B + ( A x. B ) ) ) ) $=
    ( cc wcel wa c1 caddc co cmul wceq ax1cn axdistr mp3an2 axaddcl mpan sylan
    ax1id syl adantr adddirt mp3an1 mulid2t adantl opreq1d eqtrd opreq12d ) ACD
    ZBCDZEZFAGHZFBGHIHZUJFIHZUJBIHZGHZUJBABIHZGHZGHUJCDZUHUKUNJZUGUQFCDZUHURKUJ
    FBLMUSUGUQKFANOZPUIULUJUMUPGUGULUJJZUHUGUQVAUTUJQRSUIUMFBIHZUOGHZUPUSUGUHUM
    VCJKFABTUAUIVBBUOGUHVBBJUGBUBUCUDUEUFUE $.
    $( [9-Mar-2005] $) $( [21-Feb-2005] $)

  ${
    mul.1 $e |- A e. CC $.
    mul.2 $e |- B e. CC $.
    mul.3 $e |- C e. CC $.
    $( Commutative/associative law that swaps the first two factors in a triple
       product. $)
    mul12 $p |- ( A x. ( B x. C ) ) = ( B x. ( A x. C ) ) $=
      ( cmul co mulcom opreq1i mulass 3eqtr3 ) ABGHZCGHBAGHZCGHABCGHGHBACGHGHMN
      CGABDEIJABCDEFKBACEDFKL $.
      $( [11-May-1999] $)

    $( Commutative/associative law that swaps the last two factors in a triple
       product. $)
    mul23 $p |- ( ( A x. B ) x. C ) = ( ( A x. C ) x. B ) $=
      ( cc wcel cmul co wceq mul23t mp3an ) AGHBGHCGHABIJCIJACIJBIJKDEFABCLM $.
      $( [11-May-1999] $)
  $}

  ${
    mul4.1 $e |- A e. CC $.
    mul4.2 $e |- B e. CC $.
    mul4.3 $e |- C e. CC $.
    mul4.4 $e |- D e. CC $.
    $( Rearrangement of 4 factors. $)
    mul4 $p |- ( ( A  x. B )  x. ( C  x. D ) ) =
               ( ( A  x. C )  x. ( B  x. D ) ) $=
      ( cc wcel wa cmul co wceq pm3.2i mul4t mp2an ) AIJZBIJZKCIJZDIJZKABLMCDLM
      LMACLMBDLMLMNRSEFOTUAGHOABCDPQ $.
      $( [16-Feb-1995] $)

    $( Product of two sums. $)
    muladd $p |- ( ( A + B ) x. ( C + D ) ) =
        ( ( ( A x. C ) + ( D x. B ) ) + ( ( A x. D ) + ( C x. B ) ) ) $=
      ( cc wcel wa caddc co cmul wceq pm3.2i muladdt mp2an ) AIJZBIJZKCIJZDIJZK
      ABLMCDLMNMACNMDBNMLMADNMCBNMLMLMOSTEFPUAUBGHPABCDQR $.
      $( [17-May-1999] $)
  $}

  $( Distribution of multiplication over subtraction.  Theorem I.5 of
     [Apostol] p. 18. $)
  subdit $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( A x. ( B - C ) ) = ( ( A x. B ) - ( A x. C ) ) ) $=
    ( cc wcel w3a cmul co cmin wceq caddc axdistr 3simp1 3simp3 subclt 3adant1
    syl3anc pncan3t ancoms opreq2d eqtr3d wb subaddt axmulcl 3adant3 3adant2
    wa sylan2 3impb mpbird eqcomd ) ADEZBDEZCDEZFZABGHZACGHZIHZABCIHZGHZUOURUTJ
    ZUQUTKHZUPJZUOACUSKHZGHZVBUPULUNUSDEZVEVBJUOACUSLULUMUNMULUMUNNUMUNVFULBCOZ
    PQUOVDBAGUMUNVDBJZULUNUMVHCBRSPTUAUPDEZUQDEZUTDEZVAVCUBUOUPUQUTUCULUMVIUNAB
    UDUEULUNVJUMACUDUFULUMUNVKULVFVKUMUNUGAUSUDVGUHUIQUJUK $.
    $( [22-Nov-2004] $) $( [18-Nov-2004] $)

  $( Distribution of multiplication over subtraction.  Theorem I.5 of
     [Apostol] p. 18. $)
  subdirt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A - B ) x. C ) = ( ( A x. C ) - ( B x. C ) ) ) $=
    ( cc wcel w3a cmin co cmul wceq subdit 3coml wa axmulcom subclt sylan
    3impa 3adant2 3adant1 opreq12d 3eqtr4d ) ADEZBDEZCDEZFZCABGHZIHZCAIHZCBIHZG
    HZUFCIHZACIHZBCIHZGHUDUBUCUGUJJCABKLUBUCUDUKUGJZUFDEUDUNUBUCMUFCNABOPQUEULU
    HUMUIGUBUDULUHJUCACNRUCUDUMUIJUBBCNSTUA $.
    $( [31-Dec-2005] $) $( [30-Dec-2005] $)

  ${
    subdi.1 $e |- A e. CC $.
    subdi.2 $e |- B e. CC $.
    subdi.3 $e |- C e. CC $.
    $( Distribution of multiplication over subtraction.  Theorem I.5 of
       [Apostol] p. 18. $)
    subdi $p |- ( A x. ( B - C ) ) = ( ( A x. B ) - ( A x. C ) ) $=
      ( cc wcel cmin co cmul wceq subdit mp3an ) AGHBGHCGHABCIJKJABKJACKJIJLDEF
      ABCMN $.
      $( [26-Nov-1994] $)

    $( Distribution of multiplication over subtraction.  Theorem I.5 of
       [Apostol] p. 18. $)
    subdir $p |- ( ( A - B ) x. C ) = ( ( A x. C ) - ( B x. C ) ) $=
      ( cc wcel cmin co cmul wceq subdirt mp3an ) AGHBGHCGHABIJCKJACKJBCKJIJLDE
      FABCMN $.
      $( [8-May-1999] $)
  $}

  ${
    mulzer.1 $e |- A e. CC $.

    $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
    mul01 $p |- ( A x. 0 ) = 0 $=
      ( cc0 cmin co cmul 0cn subdi subid opreq2i mulcl 3eqtr3 ) ACCDEZFEACFEZND
      ENCACCBGGHMCAFCGIJNACBGKIL $.
      $( [23-Nov-1994] $)

    $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
    mul02 $p |- ( 0 x. A ) = 0 $=
      ( cc0 cmul co 0cn mulcom mul01 eqtr ) CADEACDECCAFBGABHI $.
      $( [23-Nov-1994] $)

    $( Two times a number. $)
    1p1times $p |- ( ( 1 + 1 ) x. A ) = ( A + A ) $=
      ( c1 caddc co cmul ax1cn adddir mulid2 opreq12i eqtr ) CCDEAFECAFEZLDEAAD
      ECCAGGBHLALADABIZMJK $.
      $( [18-May-1999] $)
  $}

  $( The imaginary unit ` i ` is not zero. $)
  ine0 $p |- i =/= 0 $=
    ( ci cc0 wne wceq wn c1 ax1ne0 df-ne mpbi caddc co cmul opreq2 axicn mul01
    syl6req opreq1d ax1cn addid2 axi2m1 3eqtr3g mto mpbir ) ABCABDZEUDFBDZFBCUE
    EGFBHIUDBFJKAALKZFJKFBUDBUFFJUDUFABLKBABALMANOPQFRSTUAUBABHUC $.
    $( [29-Apr-2005] $) $( [6-May-1999] $)

  ${
    $d x y z $.
    $( 1 is a real number.  (Contributed by Eric Schmidt, 11-Apr-2007.) $)
    1re $p |- 1 e. RR $=
      ( vz vx vy cv cc0 wne cr wrex c1 wcel ci cmul co caddc wceq cc axicn
      axcnre ax-mp wa wo neeq1 rcla4ev adantlr adantll jaodan ex ine0 mpbii wn
      ioran nne anbi12i bitr opreq12 opreq1 mul02 syl6eq sylan2 0cn addid1
      sylbi necon1ai syl syl5 r19.23aivv axrrecex wi axmulrcl adantr wb eleq1
      adantl mpbid r19.23adva mpd r19.23aiva ) ADZEFZAGHZIGJZKBDZCDZKLMZNMZOZCG
      HBGHZVTKPJWGQBCKRSWFVTBCGGWBGJZWCGJZTZWBEFZWCEFZUAZVTWFWJWMVTWJWKVTWLWHWK
      VTWIVSWKAWBGVRWBEUBUCUDWIWLVTWHVSWLAWCGVRWCEUBUCUEUFUGWFWEEFZWMWFKEFWNUHK
      WEEUBUIWMWEEWMUJZWBEOZWCEOZTZWEEOWOWKUJZWLUJZTWRWKWLUKWSWPWTWQWBEULWCEULU
      MUNWRWEEENMZEWPWDEOWEXAOWQWBEWDENUOWQWDEKLMEWCEKLUPKQUQURUSEUTVAURVBVCVDV
      EVFSVSWAAGVRGJZVSTVRWBLMZIOZBGHZWABVRVGXBXEWAVHVSXBXDWABGXBWHTZXDWAXFXDTX
      CGJZWAXFXGXDVRWBVIVJXDXGWAVKXFXCIGVLVMVNUGVOVJVPVQS $.
      $( [12-Apr-2007] $) $( [11-Apr-2007] $)
  $}

  $( A theorem for reals analogous the second Peano postulate ~ peano2nn . $)
  peano2re $p |- ( A e. RR -> ( A + 1 ) e. RR ) $=
    ( cr wcel c1 caddc co 1re axaddrcl mpan2 ) ABCDBCADEFBCGADHI $.
    $( [6-Jul-2005] $) $( [5-Jul-2005] $)

  $( Closure law for negative of reals.  The weak deduction theorem ~ dedth is
     used to convert hypothesis of the inference (deduction) form of this
     theorem, ~ renegcl , to an antecedent. $)
  renegclt $p |- ( A e. RR -> -u A e. RR ) $=
    ( cr wcel cneg c1 cif wceq negeq eleq1d 1re elimel renegcl dedth ) ABCZADZB
    CNAEFZDZBCAEAPGOQBAPHIPAEBJKLM $.
    $( [20-Jan-1997] $)

  $( Closure law for subtraction of reals. $)
  resubclt $p |- ( ( A e. RR /\ B e. RR ) -> ( A - B ) e. RR ) $=
    ( cr wcel wa cneg caddc co cmin cc wceq negsubt recnt syl2an axaddrcl
    renegclt sylan2 eqeltrrd ) ACDZBCDZEABFZGHZABIHZCAJDBJDUBUCKSTABLAMBMNSUACD
    UBCDTAUAOBPQR $.
    $( [2-Aug-2006] $) $( [20-Jan-1997] $)

  ${
    resubcl.1 $e |- A e. RR $.
    resubcl.2 $e |- B e. RR $.
    $( Closure law for subtraction of reals. $)
    resubcl $p |- ( A - B ) e. RR $=
      ( cr wcel cmin co resubclt mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [17-Jan-1997] $)
  $}

  $( 0 is a real number. $)
  0re $p |- 0 e. RR $=
    ( c1 cmin co cc0 cr ax1cn subid 1re resubcl eqeltrr ) AABCDEAFGAAHHIJ $.
    $( [20-Feb-2005] $) $( [19-Feb-2005] $)

  $( "Reverse" second Peano postulate analog for reals. $)
  peano2rem $p |- ( N e. RR -> ( N - 1 ) e. RR ) $=
    ( cr wcel c1 cmin co 1re resubclt mpan2 ) ABCDBCADEFBCGADHI $.
    $( [7-Feb-2007] $) $( [6-Feb-2007] $)

  $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
  mul01t $p |- ( A e. CC -> ( A x. 0 ) = 0 ) $=
    ( cc wcel cc0 cmul co wceq cif opreq1 eqeq1d 0cn elimel mul01 dedth ) ABCZA
    DEFZDGOADHZDEFZDGADAQGPRDAQDEIJQADBKLMN $.
    $( [15-May-1999] $)

  $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
  mul02t $p |- ( A e. CC -> ( 0 x. A ) = 0 ) $=
    ( cc wcel cc0 cmul co wceq 0cn axmulcom mpan mul01t eqtrd ) ABCZDAEFZADEFZD
    DBCMNOGHDAIJAKL $.
    $( [10-Aug-1999] $)

  ${
    mulneg.1 $e |- A e. CC $.
    mulneg.2 $e |- B e. CC $.

    $( Product with negative is negative of product.  Theorem I.12 of [Apostol]
       p. 18. $)
    mulneg1 $p |- ( -u A x. B ) = -u ( A x. B ) $=
      ( cc0 cmul co cmin cneg mul01 mulcom opreq12i df-neg opreq1i 0cn subcl
      subdi 3eqtr 3eqtr4 ) BEFGZBAFGZHGZEABFGZHGAIZBFGZUCITEUAUCHBDJBADCKLUEEAH
      GZBFGBUFFGUBUDUFBFAMNUFBEAOCPDKBEADOCQRUCMS $.
      $( [10-Feb-1995] $)

    $( Product with negative is negative of product. $)
    mulneg2 $p |- ( A x. -u B ) = -u ( A x. B ) $=
      ( cneg cmul co negcl mulcom mulneg1 negeqi 3eqtr ) ABEZFGMAFGBAFGZEABFGZE
      AMCBDHIBADCJNOBADCIKL $.
      $( [31-Jul-1999] $)

    $( Product of two negatives.  Theorem I.12 of [Apostol] p. 18. $)
    mul2neg $p |- ( -u A x. -u B ) = ( A x. B ) $=
      ( cneg cmul co negcl mulneg1 mulcom eqtr negeqi mulcl negneg 3eqtr ) AEBE
      ZFGZBAFGZABFGQAPFGZEREZERAPCBDHZISTSPAFGTAPCUAJBADCIKLRBADCMNOBADCJK $.
      $( [14-Feb-1995] $)

    $( Distribution of negative over addition. $)
    negdi $p |- -u ( A + B ) = ( -u A + -u B ) $=
      ( c1 caddc co cmul cneg addcl mulid2 negeqi ax1cn negcl adddi mulneg1
      eqtr opreq12i 3eqtr3 eqtr3 ) EABFGZHGZIZUAIAIZBIZFGZUBUAUAABCDJZKLEIZUAHG
      UHAHGZUHBHGZFGUCUFUHABEMNCDOEUAMUGPUIUDUJUEFUIEAHGZIUDEAMCPUKAACKLQUJEBHG
      ZIUEEBMDPULBBDKLQRST $.
      $( [28-Jul-1999] $)

    $( Distribution of negative over subtraction. $)
    negsubdi $p |- -u ( A - B ) = ( -u A + B ) $=
      ( cneg caddc co cmin negcl negdi negsub negeqi negneg opreq2i 3eqtr3 ) AB
      EZFGZEAEZPEZFGABHGZERBFGAPCBDIJQTABCDKLSBRFBDMNO $.
      $( [6-Aug-1999] $)

    $( Distribution of negative over subtraction. $)
    negsubdi2 $p |- -u ( A - B ) = ( B - A ) $=
      ( cmin co cneg caddc negsubdi negcl addcom negsub 3eqtr ) ABEFGAGZBHFBNHF
      BAEFABCDINBACJDKBADCLM $.
      $( [1-Oct-1999] $)
  $}

  $( Product with negative is negative of product.  Theorem I.12 of [Apostol]
     p. 18. $)
  mulneg1t $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( -u A x. B ) = -u ( A x. B ) ) $=
    ( cc wcel cneg cmul co wceq cc0 cif negeq opreq1d opreq1 negeqd eqeq12d
    opreq2 0cn elimel mulneg1 dedth2h ) ACDZBCDZAEZBFGZABFGZEZHUAAIJZEZBFGZUGBF
    GZEZHUHUBBIJZFGZUGULFGZEZHABIIAUGHZUDUIUFUKUPUCUHBFAUGKLUPUEUJAUGBFMNOBULHZ
    UIUMUKUOBULUHFPUQUJUNBULUGFPNOUGULAICQRBICQRST $.
    $( [14-May-1999] $)

  $( The product with a negative is the negative of the product. $)
  mulneg2t $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( A x. -u B ) = -u ( A x. B ) ) $=
    ( cc wcel wa cneg cmul co wceq mulneg1t ancoms axmulcom negclt sylan2
    negeqd 3eqtr4d ) ACDZBCDZEZBFZAGHZBAGHZFZATGHZABGHZFRQUAUCIBAJKQTCDUDUAIRAT
    LBMNSUEUBABLOP $.
    $( [25-Aug-2004] $) $( [30-Jul-2004] $)

  $( Swap the negative sign in a product. $)
  mulneg12t $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( -u A x. B ) = ( A x. -u B ) ) $=
    ( cc wcel wa cneg cmul co mulneg1t mulneg2t eqtr4d ) ACDBCDEAFBGHABGHFABFGH
    ABIABJK $.
    $( [2-Sep-2004] $) $( [30-Jul-2004] $)

  $( Product of two negatives.  Theorem I.12 of [Apostol] p. 18. $)
  mul2negt $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A x. -u B ) = ( A x. B ) ) $=
    ( cc wcel cneg cmul co wceq cc0 cif negeq opreq1d opreq1 eqeq12d opreq2d
    opreq2 0cn elimel mul2neg dedth2h ) ACDZBCDZAEZBEZFGZABFGZHUAAIJZEZUDFGZUGB
    FGZHUHUBBIJZEZFGZUGUKFGZHABIIAUGHZUEUIUFUJUOUCUHUDFAUGKLAUGBFMNBUKHZUIUMUJU
    NUPUDULUHFBUKKOBUKUGFPNUGUKAICQRBICQRST $.
    $( [30-Aug-2004] $) $( [30-Jul-2004] $)

  $( Distribution of negative over addition. $)
  negdit $p |- ( ( A e. CC /\ B e. CC ) ->
                 -u ( A + B ) = ( -u A + -u B ) ) $=
    ( cc wcel caddc co cneg wceq cc0 cif opreq1 negeqd negeq opreq1d eqeq12d
    opreq2 opreq2d 0cn elimel negdi dedth2h ) ACDZBCDZABEFZGZAGZBGZEFZHUBAIJZBE
    FZGZUIGZUGEFZHUIUCBIJZEFZGZULUNGZEFZHABIIAUIHZUEUKUHUMUSUDUJAUIBEKLUSUFULUG
    EAUIMNOBUNHZUKUPUMURUTUJUOBUNUIEPLUTUGUQULEBUNMQOUIUNAICRSBICRSTUA $.
    $( [5-Jun-2004] $) $( [10-May-2004] $)

  $( Distribution of negative over addition. $)
  negdi2t $p |- ( ( A e. CC /\ B e. CC ) ->
                 -u ( A + B ) = ( -u A - B ) ) $=
    ( cc wcel wa caddc co cneg cmin negdit wceq negsubt negclt sylan eqtrd ) AC
    DZBCDZEABFGHAHZBHFGZRBIGZABJRCDQSTKPRBLAMNO $.
    $( [2-Jan-2006] $) $( [1-Jan-2006] $)

  $( Distribution of negative over subtraction. $)
  negsubdit $p |- ( ( A e. CC /\ B e. CC ) -> -u ( A - B ) = ( -u A + B ) ) $=
    ( cc wcel wa cneg caddc co cmin wceq negdit negclt sylan2 negsubt negeqd
    negnegt adantl opreq2d 3eqtr3d ) ACDZBCDZEZABFZGHZFZAFZUCFZGHZABIHZFUFBGHTU
    CCDUEUHJUAAUCKBLMUBUDUIABNOUBUGBUFGUAUGBJTBPQRS $.
    $( [1-Aug-2006] $) $( [15-Nov-2004] $)

  $( Distribution of negative over subtraction. $)
  negsubdi2t $p |- ( ( A e. CC /\ B e. CC ) -> -u ( A - B ) = ( B - A ) ) $=
    ( cc wcel wa cmin co cneg caddc negsubdit wceq axaddcom negclt sylan
    negsubt ancoms 3eqtrd ) ACDZBCDZEABFGHAHZBIGZBTIGZBAFGZABJTCDSUAUBKRTBLAMNS
    RUBUCKBAOPQ $.
    $( [1-Aug-2006] $) $( [4-Oct-1999] $)

  $( Convert a subtraction to addition using multiplication by a negative. $)
  submul2t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( A - ( B x. C ) ) = ( A + ( B x. -u C ) ) ) $=
    ( cc wcel cmul co cmin cneg caddc wceq wa mulneg2t adantl opreq2d negsubt
    axmulcl sylan2 eqtr2d 3impb ) ADEZBDEZCDEZABCFGZHGZABCIFGZJGZKUAUBUCLZLZUGA
    UDIZJGZUEUIUFUJAJUHUFUJKUABCMNOUAUDDEUKUEKUHAUDPBCQRST $.
    $( [9-Feb-2007] $) $( [2-Feb-2007] $)

  $( Law for double subtraction. $)
  subsub2t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( A - ( B - C ) ) = ( A + ( C - B ) ) ) $=
    ( cc wcel w3a cmin co cneg caddc wceq wa negsubt subclt sylan2 3impb
    negsubdi2t opreq2d 3adant1 eqtr3d ) ADEZBDEZCDEZFABCGHZIZJHZAUDGHZACBGHZJHZ
    UAUBUCUFUGKZUAUDDEUJUBUCLZAUDMBCNOPUBUCUFUIKUAUKUEUHAJBCQRST $.
    $( [2-Aug-2006] $) $( [30-Jun-2005] $)

  $( Law for double subtraction. $)
  subsubt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( A - ( B - C ) ) = ( ( A - B ) + C ) ) $=
    ( cc wcel w3a cmin co caddc subsub2t wceq addsubasst addsubt eqtr3d 3com23
    eqtrd ) ADEZBDEZCDEZFABCGHGHACBGHIHZABGHCIHZABCJQSRTUAKQSRFACIHBGHTUAACBLAC
    BMNOP $.
    $( [6-Jun-2004] $) $( [13-May-2004] $)

  $( Law for double subtraction. $)
  subsub3t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( A - ( B - C ) ) = ( ( A + C ) - B ) ) $=
    ( cc wcel w3a cmin co caddc subsub2t wceq addsubasst 3com23 eqtr4d ) ADEZBD
    EZCDEZFABCGHGHACBGHIHZACIHBGHZABCJOQPSRKACBLMN $.
    $( [29-Jul-2005] $) $( [27-Jul-2005] $)

  $( Law for double subtraction. $)
  subsub4t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A - B ) - C ) = ( A - ( B + C ) ) ) $=
    ( cc wcel w3a cneg cmin co caddc wceq subsubt negclt syl3an3 subnegt
    3adant1 opreq2d wa negsubt subclt sylan 3impa 3eqtr3rd ) ADEZBDEZCDEZFZABCG
    ZHIZHIZABHIZUHJIZABCJIZHIUKCHIZUDUEUHDEUJULKUFABUHLCMNUGUIUMAHUEUFUIUMKUDBC
    OPQUDUEUFULUNKZUKDEUFUOUDUERUKCSABTUAUBUC $.
    $( [2-Aug-2006] $) $( [19-Aug-2005] $)

  $( Swap the second and third terms in a double subtraction. $)
  sub23t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A - B ) - C ) = ( ( A - C ) - B ) ) $=
    ( cc wcel w3a caddc co cmin wceq axaddcom 3adant1 opreq2d subsub4t 3com23
    3eqtr4d ) ADEZBDEZCDEZFZABCGHZIHACBGHZIHZABIHCIHACIHBIHZTUAUBAIRSUAUBJQBCKL
    MABCNQSRUDUCJACBNOP $.
    $( [4-Sep-2005] $) $( [19-Aug-2005] $)

  $( Cancellation law for subtraction. $)
  nnncant $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - ( B - C ) ) - C ) = ( A - B ) ) $=
    ( cc wcel w3a cmin co caddc wceq subsub4t 3simp1 subclt 3adant1 3simp3
    syl3anc wa npcant opreq2d eqtrd ) ADEZBDEZCDEZFZABCGHZGHCGHZAUECIHZGHZABGHZ
    UAUEDEZUCUFUHJUDAUECKUAUBUCLUBUCUJUABCMNUAUBUCOPUBUCUHUIJUAUBUCQUGBAGBCRSNT
    $.
    $( [16-Sep-2005] $) $( [4-Sep-2005] $)

  $( Cancellation law for subtraction. $)
  nnncan1t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) - ( A - C ) ) = ( C - B ) ) $=
    ( cc wcel w3a cmin co cneg caddc wceq wa negsubt axaddcom negclt sylan2
    eqtr3d subclt 3adant3 3adant2 sylanc negsubdi2t opreq1d npncant 3coml
    3eqtrd ) ADEZBDEZCDEZFZABGHZACGHZGHZULIZUKJHZCAGHZUKJHZCBGHZUKDEZULDEZUMUOK
    UJUSUTLUKUNJHZUMUOUKULMUSUNDEVAUOKUTUKUNNULOPQUGUHUSUIABRSUGUIUTUHACRTUAUJU
    NUPUKJUGUIUNUPKUHACUBTUCUIUGUHUQURKCABUDUEUF $.
    $( [1-Aug-2006] $) $( [8-Feb-2005] $)

  $( Cancellation law for subtraction. $)
  nnncan2t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - C ) - ( B - C ) ) = ( A - B ) ) $=
    ( cc wcel w3a cmin co wceq sub23t 3simp1 subclt 3adant1 3simp3 syl3anc
    nnncant eqtr3d ) ADEZBDEZCDEZFZABCGHZGHCGHZACGHUBGHZABGHRUBDEZTUCUDIUAAUBCJ
    RSTKSTUERBCLMRSTNOABCPQ $.
    $( [3-Oct-2005] $) $( [1-Oct-2005] $)

  $( Cancellation law for subtraction. $)
  nncant $p |- ( ( A e. CC /\ B e. CC ) -> ( A - ( A - B ) ) = B ) $=
    ( cc wcel wa cc0 cmin co wceq 0cn nnncan1t mp3an2 subid1t adantr opreq1d
    adantl 3eqtr3d ) ACDZBCDZEZAFGHZABGHZGHZBFGHZAUBGHBRFCDSUCUDIJAFBKLTUAAUBGR
    UAAISAMNOSUDBIRBMPQ $.
    $( [23-Jun-2005] $) $( [21-Jun-2005] $)

  $( Cancellation law for subtraction. $)
  nppcan2t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - ( B + C ) ) + C ) = ( A - B ) ) $=
    ( cc wcel w3a caddc co cmin wceq subsubt 3simp1 axaddcl 3adant1 3simp3
    syl3anc pncant opreq2d eqtr3d ) ADEZBDEZCDEZFZABCGHZCIHZIHZAUDIHCGHZABIHTUD
    DEZUBUFUGJUCAUDCKTUAUBLUAUBUHTBCMNTUAUBOPUCUEBAIUAUBUEBJTBCQNRS $.
    $( [7-Oct-2005] $) $( [29-Sep-2005] $)

  $( Product with minus one is negative. $)
  mulm1t $p |- ( A e. CC -> ( -u 1 x. A ) = -u A ) $=
    ( cc wcel c1 cneg cmul co wceq ax1cn mulneg1t mpan mulid2t negeqd eqtrd )
    ABCZDEAFGZDAFGZEZAEDBCOPRHIDAJKOQAALMN $.
    $( [16-Nov-1999] $)

  ${
    mulm1.1 $e |- A e. CC $.
    $( Product with minus one is negative. $)
    mulm1 $p |- ( -u 1 x. A ) = -u A $=
      ( cc wcel c1 cneg cmul co wceq mulm1t ax-mp ) ACDEFAGHAFIBAJK $.
      $( [31-Jul-1999] $)
  $}

  $( Rearrangement of 4 terms in a mixed addition and subtraction. $)
  sub4t $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A + B ) - ( C + D ) ) = ( ( A - C ) + ( B - D ) ) ) $=
    ( cc wcel wa caddc co cneg cmin wceq negdit adantl opreq2d add4t negclt
    anim12i sylan2 eqtrd negsubt axaddcl syl2an ad2ant2r ad2ant2l opreq12d
    3eqtr3d ) AEFZBEFZGZCEFZDEFZGZGZABHIZCDHIZJZHIZACJZHIZBDJZHIZHIZUOUPKIZACKI
    ZBDKIZHIUNURUOUSVAHIZHIZVCUNUQVGUOHUMUQVGLUJCDMNOUJUSEFZVAEFZGVHVCLUMABUSVA
    PUKVIULVJCQDQRSTUOEFUPEFURVDLUJUMUOUPUAABUBCDUBUCUNUTVEVBVFHUHUKUTVELUIULAC
    UAUDUIULVBVFLUHUKBDUAUEUFUG $.
    $( [6-Mar-2005] $) $( [4-Mar-2005] $)

  ${
    sub4.1 $e |- A e. CC $.
    sub4.2 $e |- B e. CC $.
    sub4.3 $e |- C e. CC $.
    sub4.4 $e |- D e. CC $.
    $( Rearrangement of 4 terms in a mixed addition and subtraction. $)
    sub4 $p |- ( ( A + B ) - ( C + D ) ) =
                 ( ( A - C ) + ( B - D ) ) $=
      ( cc wcel wa caddc co cmin wceq pm3.2i sub4t mp2an ) AIJZBIJZKCIJZDIJZKAB
      LMCDLMNMACNMBDNMLMOSTEFPUAUBGHPABCDQR $.
      $( [17-Oct-1999] $)
  $}

  $( Rearrangement of 4 terms in a mixed addition and subtraction. $)
  subadd4t $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A - B ) - ( C - D ) ) = ( ( A + D ) - ( B + C ) ) ) $=
    ( cc wcel wa cmin co caddc wceq subsub2t 3expb subclt sylan sub4t an42s
    eqtr4d ) AEFZBEFZGZCEFZDEFZGZGABHIZCDHIHIZUEDCHIJIZADJIBCJIHIZUEEFZUDUFUGKZ
    UAUIUBUCUJUECDLMABNOSUCTUBUHUGKADBCPQR $.
    $( [6-Jan-2007] $) $( [24-Aug-2006] $)

  $( Product of two differences. $)
  mulsubt $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
               ( ( A - B ) x. ( C - D ) ) =
       ( ( ( A x. C ) + ( D x. B ) ) - ( ( A x. D ) + ( C x. B ) ) ) ) $=
    ( cc wcel wa cneg caddc co cmul cmin negsubt opreqan12d wceq muladdt
    negclt sylanr2 sylanl2 mul2negt ancoms opreq2d ad2ant2l mulneg2t negdit
    axmulcl syl2an eqtr4d ancom2s an42s opreq12d axaddcl an4s sylanc 3eqtrd
    eqtr3d ) AEFZBEFZGZCEFZDEFZGZGZABHZIJZCDHZIJZKJZABLJZCDLJZKJACKJZDBKJZIJZAD
    KJZCBKJZIJZLJZUSVBVEVIVGVJKABMCDMNVCVHVKVFVDKJZIJZAVFKJZCVDKJZIJZIJZVMVPHZI
    JZVQUQVDEFZVBVHWCOZURUQWFGUTVFEFWGVAAVDCVFPDQRBQSVCVSVMWBWDIURVAVSVMOUQUTUR
    VAGZVRVLVKIVAURVRVLODBTUAUBUCUQVAURUTWBWDOZUQVAGZUTURWIWJUTURGZGWBVNHZVOHZI
    JZWDWJWKVTWLWAWMIADUDCBUDNVNEFZVOEFZWDWNOWJWKVNVOUEADUFZCBUFZUGUHUIUJUKVMEF
    ZVPEFZWEVQOVCVMVPMUQUTURVAWSVKEFVLEFZWSUQUTGWHVKVLULACUFVAURXADBUFUAUGUMUQV
    AURUTWTWOWPWTWJURUTGVNVOULWQUTURWPWRUAUGUJUNUOUP $.
    $( [16-Jan-2006] $) $( [14-Jan-2006] $)

  $( Cancellation law for mixed addition and subtraction. $)
  pnpcant $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) - ( A + C ) ) = ( B - C ) ) $=
    ( cc wcel caddc co cmin wceq wa sub4t anandis cc0 subidt opreq1d subclt
    addid2t syl sylan9eq eqtrd 3impb ) ADEZBDEZCDEZABFGACFGHGZBCHGZIUBUCUDJZJUE
    AAHGZUFFGZUFUBUCUDUEUIIABACKLUBUGUIMUFFGZUFUBUHMUFFANOUGUFDEUJUFIBCPUFQRSTU
    A $.
    $( [13-Mar-2005] $) $( [4-Mar-2005] $)

  $( Cancellation law for mixed addition and subtraction.  (Contributed by
     Scott Fenton, 9-Jun-2006.) $)
  pnpcan2t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A + C ) - ( B + C ) ) = ( A - B ) ) $=
    ( cc wcel w3a caddc co cmin wceq axaddcom 3adant2 3adant1 opreq12d pnpcant
    3coml eqtrd ) ADEZBDEZCDEZFZACGHZBCGHZIHCAGHZCBGHZIHZABIHZUAUBUDUCUEIRTUBUD
    JSACKLSTUCUEJRBCKMNTRSUFUGJCABOPQ $.
    $( [10-Jun-2006] $) $( [9-Jun-2006] $)

  $( Cancellation law for mixed addition and subtraction. $)
  pnncant $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) - ( A - C ) ) = ( B + C ) ) $=
    ( cc wcel w3a caddc co cneg cmin wceq pnpcant negclt syl3an3 negsubt
    3adant2 opreq2d subnegt 3adant1 3eqtr3d ) ADEZBDEZCDEZFZABGHZACIZGHZJHZBUFJ
    HZUEACJHZJHBCGHZUAUBUFDEUHUIKUCABUFLCMNUDUGUJUEJUAUCUGUJKUBACOPQUBUCUIUKKUA
    BCRST $.
    $( [2-Aug-2006] $) $( [30-Jun-2005] $)

  $( Cancellation law for mixed addition and subtraction. $)
  ppncant $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) + ( C - B ) ) = ( A + C ) ) $=
    ( cc wcel w3a caddc co cmin wceq axaddcom 3adant3 opreq1d subsub2t axaddcl
    3simp2 3simp3 syl3anc pnncant 3com12 3eqtr3d ) ADEZBDEZCDEZFZABGHZBCIHZIHZB
    AGHZUGIHZUFCBIHGHZACGHZUEUFUIUGIUBUCUFUIJUDABKLMUFDEZUCUDUHUKJUEUFBCNUBUCUM
    UDABOLUBUCUDPUBUCUDQRUCUBUDUJULJBACSTUA $.
    $( [9-Jul-2005] $) $( [30-Jun-2005] $)

  ${
    pnncan.1 $e |- A e. CC $.
    pnncan.2 $e |- B e. CC $.
    pnncan.3 $e |- C e. CC $.
    $( Cancellation law for mixed addition and subtraction. $)
    pnncan $p |- ( ( A + B ) - ( A - C ) ) = ( B + C ) $=
      ( cc wcel caddc co cmin wceq pnncant mp3an ) AGHBGHCGHABIJACKJKJBCIJLDEFA
      BCMN $.
      $( [15-Jan-2006] $) $( [14-Jan-2006] $)

    ${
      $d x A $.  $d x B $.  $d x C $.
      mulcan.4 $e |- A =/= 0 $.
      $( Cancellation law for multiplication.  Theorem I.7 of [Apostol]
         p. 18. $)
      mulcan $p |- ( ( A x. B ) = ( A x. C ) <-> B = C ) $=
        ( vx cmul co wceq cv c1 cc wrex wi recex wcel wa wb axmulass mp3an3
        eqeq12d mpan2 opreq2 syl5bir adantr axmulcom mpan eqeq1d opreq1 mulid2
        syl6eq syl6bi imp sylibd r19.23aiva ax-mp impbi ) ABIJZACIJZKZBCKZAHLZI
        JZMKZHNOVBVCPZHADGQVFVGHNVDNRZVFSVBVDAIJZBIJZVICIJZKZVCVHVBVLPVFVHVLVDU
        TIJZVDVAIJZKZVBVHANRZVLVOTDVHVPSVJVMVKVNVHVPBNRVJVMKEVDABUAUBVHVPCNRVKV
        NKFVDACUAUBUCUDUTVAVDIUEUFUGVHVFVLVCTZVHVFVIMKZVQVHVEVIMVPVHVEVIKDAVDUH
        UIUJVRVJBVKCVRVJMBIJBVIMBIUKBEULUMVRVKMCIJCVIMCIUKCFULUMUCUNUOUPUQURBCA
        IUEUS $.
        $( [26-Jan-1995] $)
    $}
  $}

  ${
    mulcant2.1 $e |- A =/= 0 $.
    $( Cancellation law for multiplication (theorem form).  Theorem I.7
       of [Apostol] p. 18.  Illustrates use of ~ keephyp . $)
    mulcant2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( A x. B ) = ( A x. C ) <-> B = C ) ) $=
      ( cc wcel cmul co wceq wb c1 cif opreq1 eqeq12d bibi1d opreq2 eqeq1d
      eqeq1 bibi12d eqeq2d eqeq2 ax1cn elimel cc0 wne neeq1 ax1ne0 keephyp
      mulcan dedth3h ) AEFZBEFZCEFZABGHZACGHZIZBCIZJUKAKLZBGHZURCGHZIZUQJABCKKK
      URULBKLZGHZUTIZVBCIZJVCURUMCKLZGHZIZVBVFIZJAURIZUPVAUQVJUNUSUOUTAURBGMAUR
      CGMNOBVBIZVAVDUQVEVKUSVCUTBVBURGPQBVBCRSCVFIZVDVHVEVIVLUTVGVCCVFURGPTCVFV
      BUASURVBVFAKEUBUCBKEUBUCCKEUBUCUKAUDUEKUDUEURUDUEAKAURUDUFKURUDUFDUGUHUIU
      J $.
      $( [29-Jan-1995] $)
  $}

  $( Cancellation law for multiplication (full theorem form).  Theorem I.7
     of [Apostol] p. 18.  Illustrates use of ~ dedth and ~ elimne0 . $)
  mulcant $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ A =/= 0 ) ->
                 ( ( A x. B ) = ( A x. C ) <-> B = C ) ) $=
    ( cc0 wne cc wcel w3a cmul co wceq wb wi c1 cif eleq1 3anbi1d opreq1
    eqeq12d bibi1d imbi12d elimne0 mulcant2 dedth impcom ) ADEZAFGZBFGZCFGZHZAB
    IJZACIJZKZBCKZLZUFUJUOMUFANOZFGZUHUIHZUPBIJZUPCIJZKZUNLZMANAUPKZUJURUOVBVCU
    GUQUHUIAUPFPQVCUMVAUNVCUKUSULUTAUPBIRAUPCIRSTUAUPBCAUBUCUDUE $.
    $( [29-Jan-1995] $)

  $( Cancellation law for multiplication. $)
  mulcan2t $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ C =/= 0 ) ->
                 ( ( A x. C ) = ( B x. C ) <-> A = B ) ) $=
    ( cc wcel w3a cc0 wne wa cmul co wceq wb axmulcom 3adant2 3adant1 eqeq12d
    adantr wi mulcant ex 3coml imp bitrd ) ADEZBDEZCDEZFZCGHZIACJKZBCJKZLZCAJKZ
    CBJKZLZABLZUHULUOMUIUHUJUMUKUNUEUGUJUMLUFACNOUFUGUKUNLUEBCNPQRUHUIUOUPMZUGU
    EUFUIUQSUGUEUFFUIUQCABTUAUBUCUD $.
    $( [23-Jan-2005] $) $( [21-Jan-2005] $)

  ${
    mul0or.1 $e |- A e. CC $.
    ${
      mul0or.2 $e |- B e. CC $.
      $( If a product is zero, one of its factors must be zero.  Theorem I.11
         of [Apostol] p. 18. $)
      mul0or $p |- ( ( A x. B ) = 0 <-> ( A = 0 \/ B = 0 ) ) $=
        ( cmul co cc0 wceq wo wn wne wi df-ne cc wcel w3a wb 0cn 3pm3.2i
        mulcant mpan mul01 eqeq2i syl5bbr biimpd sylbir com12 orrd opreq1
        mul02 syl6eq opreq2 jaoi impbi ) ABEFZGHZAGHZBGHZIUPUQURUQJZUPURUSAGKZU
        PURLAGMUTUPURUTUOAGEFZHZURUPANOZBNOZGNOZPUTVBURQVCVDVECDRSABGTUAVAGUOAC
        UBZUCUDUEUFUGUHUQUPURUQUOGBEFGAGBEUIBDUJUKURUOVAGBGAEULVFUKUMUN $.
        $( [7-Oct-1999] $)
    $}

    $( A number is zero iff its square is zero (where square is represented
       using multiplication). $)
    msq0 $p |- ( ( A x. A ) = 0 <-> A = 0 ) $=
      ( cmul co cc0 wceq wo mul0or oridm bitr ) AACDEFAEFZKGKAABBHKIJ $.
      $( [28-Jul-1999] $)
  $}

  $( If a product is zero, one of its factors must be zero.  Theorem I.11
     of [Apostol] p. 18. $)
  mul0ort $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( ( A x. B ) = 0 <-> ( A = 0 \/ B = 0 ) ) ) $=
    ( cc wcel cmul co cc0 wceq wo wb cif opreq1 eqeq1d eqeq1 orbi1d bibi12d
    opreq2 orbi2d 0cn elimel mul0or dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZIZJUCAGKZ
    BEFZGHZUJGHZUHIZJUJUDBGKZEFZGHZUMUOGHZIZJABGGAUJHZUFULUIUNUTUEUKGAUJBELMUTU
    GUMUHAUJGNOPBUOHZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMBUOGNRPUJUOAGCSTBGCSTUAUB
    $.
    $( [9-Oct-1999] $)

  $( The product of two non-zero numbers is non-zero. $)
  muln0bt $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A =/= 0 /\ B =/= 0 ) <->
               ( A x. B ) =/= 0 ) ) $=
    ( cc wcel wa cc0 wceq wn cmul co wne wo mul0ort negbid ioran syl6rbb df-ne
    anbi12i 3bitr4g ) ACDBCDEZAFGZHZBFGZHZEZABIJZFGZHZAFKZBFKZEUFFKTUHUAUCLZHUE
    TUGUKABMNUAUCOPUIUBUJUDAFQBFQRUFFQS $.
    $( [2-Aug-2004] $) $( [1-Aug-2004] $)

  ${
    muln0.1 $e |- A e. CC $.
    muln0.2 $e |- B e. CC $.
    muln0.3 $e |- A =/= 0 $.
    muln0.4 $e |- B =/= 0 $.
    $( The product of two non-zero numbers is non-zero. $)
    muln0 $p |- ( A x. B ) =/= 0 $=
      ( cc wcel cmul co cc0 wne wa pm3.2i muln0bt mpbii mp2an ) AGHZBGHZABIJKLZ
      CDRSMAKLZBKLZMTUAUBEFNABOPQ $.
      $( [15-Feb-1995] $)
  $}

  $( Property of numbers whose product equals their sum.  Equation 5 of
     [Kreyszig] p. 12. $)
  muleqaddt $p |- ( ( A e. CC /\ B e. CC ) ->
             ( ( A x. B ) = ( A + B ) <-> ( ( A - 1 ) x. ( B - 1 ) ) = 1 ) ) $=
    ( cc wcel wa c1 cmin co cmul wceq caddc cc0 ax1cn mulsubt mpanr2 mpanl2
    mulid1 opreq2i a1i ax1id opreqan12d opreq12d addsubt mp3an2 axmulcl
    axaddcl sylanc 3eqtrd eqeq1d wb subclt 0cn addcan2t mp3an23 syl addid2
    eqeq2i syl5rbbr subeq0t 3bitr2rd ) ACDZBCDZEZAFGHBFGHIHZFJABIHZABKHZGHZFKHZ
    FJZVGLJZVEVFJZVCVDVHFVCVDVEFFIHZKHZAFIHZBFIHZKHZGHZVEFKHZVFGHZVHVAFCDZVBVDV
    QJZMVAVTEVBVTWAMAFBFNOPVCVMVRVPVFGVMVRJVCVLFVEKFMQRSVAVBVNAVOBKATBTUAUBVECD
    ZVFCDZVSVHJZVCWBVTWCWDMVEFVFUCUDABUEZABUFZUGUHUIVCVHLFKHZJZVJVIVCVGCDZWHVJU
    JZWBWCWIVCVEVFUKWEWFUGWILCDVTWJULMVGLFUMUNUOWGFVHFMUPUQURWBWCVJVKUJVCVEVFUS
    WEWFUGUT $.
    $( [13-Nov-2006] $) $( [13-Nov-2006] $)

  ${
    $d x y A $.  $d x y B $.
    receu.1 $e |- A e. CC $.
    receu.2 $e |- B e. CC $.
    receu.3 $e |- A =/= 0 $.
    $( Existential uniqueness of reciprocals.  Theorem I.8 of [Apostol]
       p. 18. $)
    receu $p |- E! x e. CC ( A x. x ) = B $=
      ( vy cv cmul co wceq cc wreu wrex wa wi wral opreq2 eqeq1d reu4 c1 recex
      wcel axmulcl mpan2 risset sylib axmulass mp3an13 eqcomd sylan9eqr opreq1
      mulid2 syl6eq exp32 impcom a1d r19.21aiv ex r19.22 syl6 mpid r19.23aiv
      ax-mp w3a mulcant2 eqtr3t syl5bi mp3an1 rgen2 mpbir2an ) BAHZIJZCKZALMVNA
      LNZVNBGHZIJZCKZOZVLVPKZPZGLQALQVNVRAGLVTVMVQCVLVPBIRSTVQUAKZGLNVOGBDFUBWB
      VOGLVPLUCZWBVLVPCIJZKZALNZVOWCWDLUCZWFWCCLUCZWGEVPCUDUEAWDLUFUGWCWBWEVNPZ
      ALQZWFVOPWCWBWJWCWBOZWIALWKWIVLLUCZWBWCWIWBWCWEVNWCWEOWBVMVQCIJZCWEWCVMBW
      DIJZWMVLWDBIRWCWMWNBLUCZWCWHWMWNKDEBVPCUHUIUJUKWBWMUACIJCVQUACIULCEUMUNUK
      UOUPUQURUSWEVNALUTVAVBVCVDWAAGLWOWLWCWADWOWLWCVEVMVQKVTVSBVLVPFVFVMVQCVGV
      HVIVJVK $.
      $( [29-Jan-1995] $)
  $}

  ${
    $d x y $.
    $( Multiplication maps nonzero complex numbers to nonzero complex numbers.
      (Contributed by Steve Rodriguez, 23-Feb-2007.) $)
    mulnzcnopr $p |- ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) :
                             ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) -->
                             ( CC \ { 0 } ) $=
      ( vx vy cc cc0 csn cdif cxp cmul cres wf wfn cv co wcel wral ffnoprval
      wss wa axmulopr mpbi pm3.26i difss ssxp mp2an fnssres oprvalres wne
      axmulcl ad2ant2r muln0bt biimpa an4s jca wn eldif wceq elsn negbii df-ne
      bitr4 anbi2i bitr syl2anb 0cn elisseti elsnc2 sylibr eqeltrd rgen2
      mpbir2an ) CDEZFZVLGZVLHVMIZJVNVMKZALZBLZVNMZVLNZBVLOAVLOABVLVLVLVNPHCCGZ
      KZVMVTQZVOWAVPVQHMZCNZBCOACOZVTCHJWAWERSABCCCHPTUAVLCQZWFWBCVKUBZWGVLCVLC
      UCUDVTVMHUEUDVSABVLVPVLNZVQVLNZRZVRWCVLVPVQVLVLHUFWJWDWCDUGZRZWCVLNZVPCNZ
      VPDUGZRZVQCNZVQDUGZRZWLWHWIWPWSRWDWKWNWQWDWOWRVPVQUHUIWNWQWOWRWKWNWQRWOWR
      RWKVPVQUJUKULUMWHWNVPVKNZUNZRWPVPCVKUOXAWOWNXAVPDUPZUNWOWTXBADUQURVPDUSUT
      VAVBWIWQVQVKNZUNZRWSVQCVKUOXDWRWQXDVQDUPZUNWRXCXEBDUQURVQDUSUTVAVBVCWMWDW
      CVKNZUNZRWLWCCVKUOXGWKWDXGWCDUPZUNWKXFXHWCDDCVDVEVFURWCDUSUTVAVBVGVHVIVJ
      $.
      $( [24-Feb-2007] $) $( [17-Feb-2007] $)
 $}


