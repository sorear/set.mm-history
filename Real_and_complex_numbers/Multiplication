$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Multiplication
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Identity law for multiplication.  Note:  see ~ ax1id for commuted
     version. $)
  mulid2t $p |- ( A e. CC -> ( 1 x. A ) = A ) $=
    ( cc wcel c1 cmulc co wceq 1cn axmulcom mpan ax1id eqtrd ) ABCZDAEFZADEFZAD
    BCMNOGHDAIJAKL $.
    $( [8-Oct-99] $)

  $( Commutative/associative law. $)
  mul23t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A x. B ) x. C ) = ( ( A x. C ) x. B ) ) $=
    ( cc wcel w3a cmulc co wceq wa axmulcom opreq2d 3adant1 axmulass 3com23
    3eqtr4d ) ADEZBDEZCDEZFABCGHZGHZACBGHZGHZABGHCGHACGHBGHZRSUAUCIQRSJTUBAGBCK
    LMABCNQSRUDUCIACBNOP $.
    $( [8-Oct-99] $)

  $( Rearrangement of 4 factors. $)
  mul4t $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A  x. B )  x. ( C  x. D ) ) =
                 ( ( A  x. C )  x. ( B  x. D ) ) ) $=
    ( cc wcel wa cmulc co wceq w3a mul23t opreq1d 3expa adantrr axmulass 3expb
    axmulcl sylan an4s 3eqtr3d ) AEFZBEFZGZCEFZDEFZGZGABHIZCHIZDHIZACHIZBHIZDHI
    ZUHCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOUHEFZUGUJUNJZUDU
    QUEUFURUHCDPQABRSUBUEUCUFUMUOJZUKEFZUCUFGUSUBUEGUTUCUFUSUKBDPQACRSTUA $.
    $( [8-Oct-99] $)

  ${
    mul.1 $e |- A e. CC $.
    mul.2 $e |- B e. CC $.
    mul.3 $e |- C e. CC $.
    $( Commutative/associative law that swaps the first two factors in a triple
       product. $)
    mul12 $p |- ( A x. ( B x. C ) ) = ( B x. ( A x. C ) ) $=
      ( cmulc co mulcom opreq1i mulass 3eqtr3 ) ABGHZCGHBAGHZCGHABCGHGHBACGHGHM
      NCGABDEIJABCDEFKBACEDFKL $.
      $( [11-May-99] $)

    $( Commutative/associative law that swaps the last two factors in a triple
       product. $)
    mul23 $p |- ( ( A x. B ) x. C ) = ( ( A x. C ) x. B ) $=
      ( cc wcel cmulc co wceq mul23t mp3an ) AGHBGHCGHABIJCIJACIJBIJKDEFABCLM
      $.
      $( [11-May-99] $)
  $}

  ${
    mul4.1 $e |- A e. CC $.
    mul4.2 $e |- B e. CC $.
    mul4.3 $e |- C e. CC $.
    mul4.4 $e |- D e. CC $.
    $( Rearrangement of 4 factors. $)
    mul4 $p |- ( ( A  x. B )  x. ( C  x. D ) ) =
               ( ( A  x. C )  x. ( B  x. D ) ) $=
      ( cc wcel wa cmulc co wceq pm3.2i mul4t mp2an ) AIJZBIJZKCIJZDIJZKABLMCDL
      MLMACLMBDLMLMNRSEFOTUAGHOABCDPQ $.
      $( [16-Feb-95] $)

    $( Product of sums. $)
    muladd $p |- ( ( A + B ) x. ( C + D ) ) =
        ( ( ( A x. C ) + ( D x. B ) ) + ( ( A x. D ) + ( C x. B ) ) ) $=
      ( caddc co cmulc addcl distr1 distr2 opreq12i mulcl add23 mulcom opreq2i
      addass 3eqtr3r 3eqtr ) ABIJZCDIJKJUCCKJZUCDKJZIJACKJZBCKJZIJZADKJZBDKJZIJ
      ZIJZUFDBKJZIJZUICBKJZIJIJZUCCDABEFLGHMUDUHUEUKIABCEFGNABDEFHNOULUFUKIJZUG
      IJUNUIIJZUOIJUPUFUGUKACEGPZBCFGPUIUJADEHPZBDFHPZLQUQURUGUOIUFUIIJZUMIJVBU
      JIJURUQUMUJVBIDBHFRSUFUIUMUSUTDBHFPZQUFUIUJUSUTVATUABCFGROUNUIUOUFUMUSVCL
      UTCBGFPTUBUB $.
      $( [17-May-99] $)
  $}

  ${
    subdist.1 $e |- A e. CC $.
    subdist.2 $e |- B e. CC $.
    subdist.3 $e |- C e. CC $.
    $( Distribution of muliplication over subtraction.  Theorem I.5 of
       [Apostol] p. 18. $)
    subdi $p |- ( A x. ( B - C ) ) = ( ( A x. B ) - ( A x. C ) ) $=
      ( cmulc co cmin wceq caddc subcl distr1 subaddeq opreq2i eqtr3 mulcl
      subadd mpbir cleqcomi ) ABGHZACGHZIHZABCIHZGHZUCUEJUBUEKHZUAJACUDKHZGHUFU
      AACUDDFBCEFLZMUGBAGCBFENOPUAUBUEABDEQACDFQAUDDUHQRST $.
      $( [26-Nov-94] $)

    $( Distribution of muliplication over subtraction.  Theorem I.5 of
       [Apostol] p. 18. $)
    subdistr $p |- ( ( A - B ) x. C ) = ( ( A x. C ) - ( B x. C ) ) $=
      ( cmin co cmulc subdi subcl mulcom opreq12i 3eqtr4 ) CABGHZIHCAIHZCBIHZGH
      OCIHACIHZBCIHZGHCABFDEJOCABDEKFLRPSQGACDFLBCEFLMN $.
      $( [8-May-99] $)
  $}

  ${
    mulzer.1 $e |- A e. CC $.

    $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
    mulzer1 $p |- ( A x. 0 ) = 0 $=
      ( cc0 cmin co cmulc 0cn subdi subid opreq2i mulcl 3eqtr3 ) ACCDEZFEACFEZN
      DENCACCBGGHMCAFCGIJNACBGKIL $.
      $( [23-Nov-94] $)

    $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
    mulzer2 $p |- ( 0 x. A ) = 0 $=
      ( cc0 cmulc co 0cn mulcom mulzer1 eqtr ) CADEACDECCAFBGABHI $.
      $( [23-Nov-94] $)

    $( Two times a number. $)
    1p1times $p |- ( ( 1 + 1 ) x. A ) = ( A + A ) $=
      ( c1 caddc co cmulc 1cn distr2 mulid2 opreq12i eqtr ) CCDEAFECAFEZLDEAADE
      CCAGGBHLALADABIZMJK $.
      $( [18-May-99] $)
  $}

  $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
  mulzer1t $p |- ( A e. CC -> ( A x. 0 ) = 0 ) $=
    ( cc wcel cc0 cmulc co wceq cded opreq1 cleq1d 0cn elimel mulzer1 dedth )
    ABCZADEFZDGOADHZDEFZDGADAQGPRDAQDEIJQADBKLMN $.
    $( [15-May-99] $)

  $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
  mulzer2t $p |- ( A e. CC -> ( 0 x. A ) = 0 ) $=
    ( cc wcel cc0 cmulc co wceq 0cn axmulcom mpan mulzer1t eqtrd ) ABCZDAEFZADE
    FZDDBCMNOGHDAIJAKL $.
    $( [10-Aug-99] $)

  ${
    mulneg.1 $e |- A e. CC $.
    mulneg.2 $e |- B e. CC $.

    $( Product with negative is negative of product.  Theorem I.12 of [Apostol]
       p. 18. $)
    mulneg1 $p |- ( -u A x. B ) = -u ( A x. B ) $=
      ( cc0 cmulc co cmin cneg mulzer1 mulcom opreq12i df-neg opreq1i 0cn
      subcl subdi 3eqtr 3eqtr4 ) BEFGZBAFGZHGZEABFGZHGAIZBFGZUCITEUAUCHBDJBADCK
      LUEEAHGZBFGBUFFGUBUDUFBFAMNUFBEAOCPDKBEADOCQRUCMS $.
      $( [10-Feb-95] $)

    $( Product with negative is negative of product. $)
    mulneg2 $p |- ( A x. -u B ) = -u ( A x. B ) $=
      ( cneg cmulc co negcl mulcom mulneg1 negeqi 3eqtr ) ABEZFGMAFGBAFGZEABFGZ
      EAMCBDHIBADCJNOBADCIKL $.
      $( [31-Jul-99] $)

    $( Product of two negatives.  Theorem I.12 of [Apostol] p. 18. $)
    mul2neg $p |- ( -u A x. -u B ) = ( A x. B ) $=
      ( cneg cmulc co negcl mulneg1 mulcom eqtr negeqi mulcl negneg 3eqtr ) AEB
      EZFGZBAFGZABFGQAPFGZEREZERAPCBDHZISTSPAFGTAPCUAJBADCIKLRBADCMNOBADCJK $.
      $( [14-Feb-95] $)

    $( Distribution of negative over addition. $)
    negdi $p |- -u ( A + B ) = ( -u A + -u B ) $=
      ( c1 caddc co cmulc cneg addcl mulid2 negeqi 1cn negcl distr1 mulneg1
      eqtr opreq12i 3eqtr3 eqtr3 ) EABFGZHGZIZUAIAIZBIZFGZUBUAUAABCDJZKLEIZUAHG
      UHAHGZUHBHGZFGUCUFUHABEMNCDOEUAMUGPUIUDUJUEFUIEAHGZIUDEAMCPUKAACKLQUJEBHG
      ZIUEEBMDPULBBDKLQRST $.
      $( [28-Jul-99] $)

    $( Distribution of negative over subtraction. $)
    negdist2 $p |- -u ( A - B ) = ( -u A + B ) $=
      ( cmin co cneg caddc subneg negeqi negcl negdi negneg opreq2i 3eqtr ) ABE
      FZGABGZHFZGAGZQGZHFSBHFPRABCDIJAQCBDKLTBSHBDMNO $.
      $( [6-Aug-99] $)

    $( Distribution of negative over subtraction. $)
    negdist3 $p |- -u ( A - B ) = ( B - A ) $=
      ( cneg caddc co cmin negcl addcom negdist2 subneg 3eqtr4 ) AEZBFGBNFGABHG
      EBAHGNBACIDJABCDKBADCLM $.
      $( [1-Oct-99] $)
  $}

  $( Product with negative is negative of product.  Theorem I.12 of [Apostol]
     p. 18. $)
  mulneg1t $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( -u A x. B ) = -u ( A x. B ) ) $=
    ( cc wcel cneg cmulc co wceq cc0 cded negeq opreq1d opreq1 negeqd cleq12d
    opreq2 0cn elimel mulneg1 dedth2h ) ACDZBCDZAEZBFGZABFGZEZHUAAIJZEZBFGZUGBF
    GZEZHUHUBBIJZFGZUGULFGZEZHABIIAUGHZUDUIUFUKUPUCUHBFAUGKLUPUEUJAUGBFMNOBULHZ
    UIUMUKUOBULUHFPUQUJUNBULUGFPNOUGULAICQRBICQRST $.
    $( [14-May-99] $)

  $( The product with a negative is the negative of the product. $)
  mulneg2t $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( A x. -u B ) = -u ( A x. B ) ) $=
    ( cc wcel wa cneg cmulc co wceq mulneg1t ancoms axmulcom negclt sylan5
    negeqd 3eqtr4d ) ACDZBCDZEZBFZAGHZBAGHZFZATGHZABGHZFRQUAUCIBAJKQTCDUDUAIRAT
    LBMNSUEUBABLOP $.
    $( [25-Aug-04] $) $( [30-Jul-04] $)

  $( Swap the negative sign in a product. $)
  mulneg12t $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( -u A x. B ) = ( A x. -u B ) ) $=
    ( cc wcel wa cneg cmulc co mulneg1t mulneg2t eqtr4d ) ACDBCDEAFBGHABGHFABFG
    HABIABJK $.
    $( [2-Sep-04] $) $( [30-Jul-04] $)

  $( Product of two negatives.  Theorem I.12 of [Apostol] p. 18. $)
  mul2negt $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A x. -u B ) = ( A x. B ) ) $=
    ( cc wcel cneg cmulc co wceq cc0 cded negeq opreq1d opreq1 cleq12d opreq2d
    opreq2 0cn elimel mul2neg dedth2h ) ACDZBCDZAEZBEZFGZABFGZHUAAIJZEZUDFGZUGB
    FGZHUHUBBIJZEZFGZUGUKFGZHABIIAUGHZUEUIUFUJUOUCUHUDFAUGKLAUGBFMNBUKHZUIUMUJU
    NUPUDULUHFBUKKOBUKUGFPNUGUKAICQRBICQRST $.
    $( [30-Aug-04] $) $( [30-Jul-04] $)

  $( Distribution of negative over addition. $)
  negdistt $p |- ( ( A e. CC /\ B e. CC ) ->
                 -u ( A + B ) = ( -u A + -u B ) ) $=
    ( cc wcel caddc co cneg wceq cc0 cded opreq1 negeqd negeq opreq1d cleq12d
    opreq2 opreq2d 0cn elimel negdi dedth2h ) ACDZBCDZABEFZGZAGZBGZEFZHUBAIJZBE
    FZGZUIGZUGEFZHUIUCBIJZEFZGZULUNGZEFZHABIIAUIHZUEUKUHUMUSUDUJAUIBEKLUSUFULUG
    EAUIMNOBUNHZUKUPUMURUTUJUOBUNUIEPLUTUGUQULEBUNMQOUIUNAICRSBICRSTUA $.
    $( [5-Jun-04] $) $( [10-May-04] $)

  $( Distribution of negative over subtraction. $)
  negdist3t $p |- ( ( A e. CC /\ B e. CC ) -> -u ( A - B ) = ( B - A ) ) $=
    ( cc wcel cmin co cneg wceq cc0 cded opreq1 negeqd opreq2 cleq12d 0cn
    elimel negdist3 dedth2h ) ACDZBCDZABEFZGZBAEFZHSAIJZBEFZGZBUDEFZHUDTBIJZEFZ
    GZUHUDEFZHABIIAUDHZUBUFUCUGULUAUEAUDBEKLAUDBEMNBUHHZUFUJUGUKUMUEUIBUHUDEMLB
    UHUDEKNUDUHAICOPBICOPQR $.
    $( [4-Oct-99] $)

  $( Law for double subtraction. $)
  subsubt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( A - ( B - C ) ) = ( ( A - B ) + C ) ) $=
    ( cc wcel w3a cmin co cneg caddc wceq wa subnegt subclt sylan5 3impb
    negdist3t opreq2d 3adant1 addsubasst addsubt eqtr3d 3com23 3eqtrd ) ADEZBDE
    ZCDEZFABCGHZGHZAUHIZJHZACBGHZJHZABGHCJHZUEUFUGUIUKKZUEUHDEUOUFUGLZAUHMBCNOP
    UFUGUKUMKUEUPUJULAJBCQRSUEUGUFUMUNKUEUGUFFACJHBGHUMUNACBTACBUAUBUCUD $.
    $( [6-Jun-04] $) $( [13-May-04] $)

  $( Product with minus one is negative. $)
  mulm1t $p |- ( A e. CC -> ( -u 1 x. A ) = -u A ) $=
    ( cc wcel c1 cneg cmulc co wceq 1cn mulneg1t mpan mulid2t negeqd eqtrd ) AB
    CZDEAFGZDAFGZEZAEDBCOPRHIDAJKOQAALMN $.
    $( [16-Nov-99] $)

  ${
    mulm1.1 $e |- A e. CC $.
    $( Product with minus one is negative. $)
    mulm1 $p |- ( -u 1 x. A ) = -u A $=
      ( cc wcel c1 cneg cmulc co wceq mulm1t ax-mp ) ACDEFAGHAFIBAJK $.
      $( [31-Jul-99] $)
  $}

  ${
    sub4.1 $e |- A e. CC $.
    sub4.2 $e |- B e. CC $.
    sub4.3 $e |- C e. CC $.
    sub4.4 $e |- D e. CC $.
    $( Rearrangement of 4 terms in a mixed addition and subtraction. $)
    sub4 $p |- ( ( A + B ) - ( C + D ) ) =
                 ( ( A - C ) + ( B - D ) ) $=
      ( caddc co cneg cmin negdi opreq2i negcl add4 eqtr4 addcl subneg
      opreq12i 3eqtr4 ) ABIJZCDIJZKZIJZACKZIJZBDKZIJZIJZUBUCLJACLJZBDLJZIJUEUBU
      FUHIJZIJUJUDUMUBICDGHMNAUFBUHECGOFDHOPQUBUCABEFRCDGHRSUKUGULUIIACEGSBDFHS
      TUA $.
      $( [17-Oct-99] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( x is dummy. $)
    mulcan.1 $e |- A e. CC $.
    mulcan.2 $e |- B e. CC $.
    mulcan.3 $e |- C e. CC $.
    mulcan.4 $e |- A =/= 0 $.
    $( Cancellation law for multiplication.  Theorem I.7 of [Apostol] p. 18. $)
    mulcan $p |- ( ( A x. B ) = ( A x. C ) <-> B = C ) $=
      ( vx cmulc co wceq cv c1 cc wrex wi recex wcel wa wb axmulass mp3an3
      cleq12d mpan2 opreq2 syl5bir adantr axmulcom mpan cleq1d opreq1 mulid2
      syl6eq syl6bi imp sylibd exp r19.23aiv ax-mp impbi ) ABIJZACIJZKZBCKZAHLZ
      IJZMKZHNOVCVDPZHADGQVGVHHNVENRZVGVHVIVGSVCVEAIJZBIJZVJCIJZKZVDVIVCVMPVGVI
      VMVEVAIJZVEVBIJZKZVCVIANRZVMVPTDVIVQSVKVNVLVOVIVQBNRVKVNKEVEABUAUBVIVQCNR
      VLVOKFVEACUAUBUCUDVAVBVEIUEUFUGVIVGVMVDTZVIVGVJMKZVRVIVFVJMVQVIVFVJKDAVEU
      HUIUJVSVKBVLCVSVKMBIJBVJMBIUKBEULUMVSVLMCIJCVJMCIUKCFULUMUCUNUOUPUQURUSBC
      AIUEUT $.
      $( [26-Jan-95] $)
  $}

  ${
    mulcant.1 $e |- A =/= 0 $.
    $( Cancellation law for multiplication (theorem form).  Theorem I.7
       of [Apostol] p. 18.  Illustrates use of ~ keephyp . $)
    mulcant $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( A x. B ) = ( A x. C ) <-> B = C ) ) $=
      ( cc wcel cmulc co wceq wb c1 cded opreq1 cleq12d birbid opreq2 cleq1d
      cleq1 bi2bid cleq2d cleq2 1cn elimel cc0 wne neeq1 ax1ne0 keephyp mulcan
      dedth3h ) AEFZBEFZCEFZABGHZACGHZIZBCIZJUKAKLZBGHZURCGHZIZUQJABCKKKURULBKL
      ZGHZUTIZVBCIZJVCURUMCKLZGHZIZVBVFIZJAURIZUPVAUQVJUNUSUOUTAURBGMAURCGMNOBV
      BIZVAVDUQVEVKUSVCUTBVBURGPQBVBCRSCVFIZVDVHVEVIVLUTVGVCCVFURGPTCVFVBUASURV
      BVFAKEUBUCBKEUBUCCKEUBUCUKAUDUEKUDUEURUDUEAKAURUDUFKURUDUFDUGUHUIUJ $.
      $( [29-Jan-95] $)
  $}

  $( Cancellation law for multiplication (full theorem form).  Theorem I.7
     of [Apostol] p. 18.  Illustrates use of ~ dedth and ~ elimne0 . $)
  mulcant2 $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ A =/= 0 ) ->
                 ( ( A x. B ) = ( A x. C ) <-> B = C ) ) $=
    ( cc0 wne cc wcel w3a cmulc co wceq wb wi c1 cded eleq1 pm4.2i bi3and
    opreq1 cleq12d birbid bi2imd elimne0 mulcant dedth imp ancoms ) ADEZAFGZBFG
    ZCFGZHZABIJZACIJZKZBCKZLZUHULUQUHULUQMUHANOZFGZUJUKHZURBIJZURCIJZKZUPLZMANA
    URKZULUTUQVDVEUIUSUJUJUKUKAURFPVEUJQVEUKQRVEUOVCUPVEUMVAUNVBAURBISAURCISTUA
    UBURBCAUCUDUEUFUG $.
    $( [29-Jan-95] $)

  ${
    mul0or.1 $e |- A e. CC $.
    ${
      mul0or.2 $e |- B e. CC $.
      $( If a product is zero, one of its factors must be zero.  Theorem I.11
         of [Apostol] p. 18. $)
      mul0or $p |- ( ( A x. B ) = 0 <-> ( A = 0 \/ B = 0 ) ) $=
        ( cmulc co cc0 wceq wo wn wne wi df-ne cc wcel w3a wb 0cn 3pm3.2i
        mulcant2 mpan mulzer1 cleq2i syl5bbr biimpd sylbir com12 orrd opreq1
        mulzer2 syl6eq opreq2 jaoi impbi ) ABEFZGHZAGHZBGHZIUPUQURUQJZUPURUSAGK
        ZUPURLAGMUTUPURUTUOAGEFZHZURUPANOZBNOZGNOZPUTVBURQVCVDVECDRSABGTUAVAGUO
        ACUBZUCUDUEUFUGUHUQUPURUQUOGBEFGAGBEUIBDUJUKURUOVAGBGAEULVFUKUMUN $.
        $( [7-Oct-99] $)
    $}

    $( A number is zero iff its square is zero. $)
    sq0 $p |- ( ( A x. A ) = 0 <-> A = 0 ) $=
      ( cmulc co cc0 wceq wo mul0or oridm bitr ) AACDEFAEFZKGKAABBHKIJ $.
      $( [28-Jul-99] $)
  $}

  $( If a product is zero, one of its factors must be zero.  Theorem I.11
     of [Apostol] p. 18. $)
  mul0ort $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( ( A x. B ) = 0 <-> ( A = 0 \/ B = 0 ) ) ) $=
    ( cc wcel cmulc co cc0 wceq wo wb cded opreq1 cleq1d cleq1 birord bi2bid
    opreq2 bilord 0cn elimel mul0or dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZIZJUCAGKZ
    BEFZGHZUJGHZUHIZJUJUDBGKZEFZGHZUMUOGHZIZJABGGAUJHZUFULUIUNUTUEUKGAUJBELMUTU
    GUMUHAUJGNOPBUOHZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMBUOGNRPUJUOAGCSTBGCSTUAUB
    $.
    $( [9-Oct-99] $)

  $( The product of two non-zero numbers is non-zero. $)
  muln0bt $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A =/= 0 /\ B =/= 0 ) <->
               ( A x. B ) =/= 0 ) ) $=
    ( cc wcel wa cc0 wceq wn cmulc co wne wo mul0ort binegd ioran syl6rbb
    df-ne bi2an 3bitr4g ) ACDBCDEZAFGZHZBFGZHZEZABIJZFGZHZAFKZBFKZEUFFKTUHUAUCL
    ZHUETUGUKABMNUAUCOPUIUBUJUDAFQBFQRUFFQS $.
    $( [2-Aug-04] $) $( [1-Aug-04] $)

  ${
    muln0.1 $e |- A e. CC $.
    muln0.2 $e |- B e. CC $.
    muln0.3 $e |- A =/= 0 $.
    muln0.4 $e |- B =/= 0 $.
    $( The product of two non-zero numbers is non-zero. $)
    muln0 $p |- ( A x. B ) =/= 0 $=
      ( cc wcel cmulc co cc0 wne wa pm3.2i muln0bt mpbii mp2an ) AGHZBGHZABIJKL
      ZCDRSMAKLZBKLZMTUAUBEFNABOPQ $.
      $( [15-Feb-95] $)
  $}

  ${
    $d x y A $.  $d x y B $. $( ` y ` is dummy. $)
    receu.1 $e |- A e. CC $.
    receu.2 $e |- B e. CC $.
    receu.3 $e |- A =/= 0 $.
    $( Existential uniqueness of reciprocals.  Theorem I.8 of [Apostol]
       p. 18. $)
    receu $p |- E! x e. CC ( A x. x ) = B $=
      ( vy cv cmulc co wceq cc wreu wrex wa weq wi wral c1 recex wcel axmulcl
      mpan2 risset sylib opreq2 axmulass mp3an1 cleqcomd sylan9eqr opreq1
      mulid2 syl6eq exp32 com12 imp a1d r19.21aiv exp r19.22 syl6 com23 mpd
      r19.23aiv ax-mp w3a mulcant cleq2 biimparc syl5bi rgen2 pm3.2i cleq1d
      reu4 mpbir ) BAHZIJZCKZALMVRALNZVRBGHZIJZCKZOZAGPZQZGLRALRZOVSWFWASKZGLNV
      SGBDFTWGVSGLVTLUAZVPVTCIJZKZALNZWGVSQWHWILUAZWKWHCLUAZWLEVTCUBUCAWILUDUEW
      HWGWKVSWHWGWJVRQZALRZWKVSQWHWGWOWHWGOZWNALWPWNVPLUAZWHWGWNWGWHWNWGWHWJVRW
      HWJOWGVQWACIJZCWJWHVQBWIIJZWRVPWIBIUFWHWRWSWHWMWRWSKZEBLUAZWHWMWTDBVTCUGU
      HUCUIUJWGWRSCIJCWASCIUKCEULUMUJUNUOUPUQURUSWJVRALUTVAVBVCVDVEWEAGLXAWQWHW
      EDXAWQWHVFVQWAKZWDWCBVPVTFVGWBXBVRWACVQVHVIVJUHVKVLVRWBAGLWDVQWACVPVTBIUF
      VMVNVO $.
      $( [29-Jan-95] $)
  $}


