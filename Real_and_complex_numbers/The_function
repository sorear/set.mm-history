$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                               The ` # ` function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c # $.

  $( Extend the definition of a class to include the ` # ` function. $)
  chash $a class # $.

  ${
    $d x y $.
    $( Define the ` # ` function, which gives the cardinality of a finite set
       as a member of ` NN0 ` . $)
    df-hash $a |- # = ( rec ( { <. x , y >. | y = ( x + 1 ) } , 0 ) o. card )
        $.
  $}

  ${
    $d G n $.  $d n x y $.
    hashgf1o.1 $e |-
                  G = ( rec ( { <. x , y >. | y = ( x + 1 ) } , 0 ) |` om ) $.
    $( ` G ` maps ` om ` one-to-one onto ` NN0 ` .  (Contributed by Paul
       Chapman, 22-Jun-2011.) $)
    hashgf1o $p |- G : om -1-1-onto-> NN0 $=
      ( vn com cn0 wf1o cc0 cv cle wbr cz crab 0z om2uzf1oi wceq wb nn0zrab
      f1oeq3 ax-mp mpbir ) FGCHZFIEJKLEMNZCHZABEICODPGUDQUCUERESGUDFCTUAUB $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d x y $.
    $( A 1-based finite sequence of integers is finite.  (Contributed by Paul
       Chapman, 22-Jun-2011.) $)
    fz1fi $p |- ( N e. NN0 -> ( 1 ... N ) e. Fin ) $=
      ( vy vx cn0 wcel c1 cfz co ccrd cfv com cfn cv caddc wceq copab cc0 crdg
      cres ccnv eqid cardfz wf1o hashgf1o f1ocnvdm mpan eqeltrd cvv cen wbr wb
      oprex cardid enfi mp2an con0 cardon onfin ax-mp bitr3i sylibr ) ADEZFAGHZ
      IJZKEZVCLEZVBVDABMCMFNHOCBPQRKSZTJZKCBVGAVGUAZUBKDVGUCVBVHKECBVGVIUDKDAVG
      UEUFUGVFVDLEZVEVCUHEVDVCUIUJVJVFUKFAGULVCUMVDVCUHUNUOVDUPEVJVEUKVCUQVDURU
      SUTVA $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d x y z $.
    hashgval.1 $e |-
                  G = ( rec ( { <. x , y >. | y = ( x + 1 ) } , 0 ) |` om ) $.
    $( The value of the ` # ` function in terms of the mapping ` G ` from
       ` om ` to ` NN0 ` .  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    hashgval $p |- ( A e. Fin -> ( G ` ( card ` A ) ) = ( # ` A ) ) $=
      ( vz cfn wcel ccrd cfv cv c1 caddc co wceq copab cc0 crdg chash com cres
      ficardom fvres syl fveq1i syl5eq cvv elisset wfun wfn con0 rdgfnon fnfun
      ax-mp cen wbr crab cint wrex numth2 intexrab mpbi wa df-card visset
      biantrur opabbii eqtri fnopab2 w3a ccom fvco2 df-hash mp3an12 eqtr4d ) CG
      HZCIJZDJZVQBKZAKZLMNOABPZQRZJZCSJZVPVQWBTUAZJZWCVRVPVQTHWFWCOCUBVQTWBUCUD
      VQDWEEUEUFVPCUGHZWDWCOZCGUHWBUIZIUGUJZWGWHWBUKUJWIQWAULUKWBUMUNABUGFKVTUO
      UPZFUKUQURZIWKFUKUSWLUGHFVTUTWKFUKVAVBIVSWLOZABPVTUGHZWMVCZABPABFVDWMWOAB
      WNWMAVEVFVGVHVIWIWJWGVJCWBIVKZJWCWDUGCWBIVLCSWPABVMUEUFVNUDVO $.
      $( [22-Jun-2011] $)

    $( ` ``' G ` maps the ` # ` function's value to ` card ` .  (Contributed by
       Paul Chapman, 22-Jun-2011.) $)
    hashginv $p |- ( A e. Fin -> ( `' G ` ( # ` A ) ) = ( card ` A ) ) $=
      ( cfn wcel ccrd cfv chash wceq ccnv hashgval com wi ficardom cn0 wf1o
      hashgf1o f1ocnvfv mpan syl mpd ) CFGZCHIZDICJIZKZUFDLIUEKZABCDEMUDUENGZUG
      UHOZCPNQDRUIUJABDESNQUEUFDTUAUBUC $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d x y $.

    $( The set ` ( 1 ... N ) ` has ` N ` elements.  (Contributed by Paul
       Chapman, 22-Jun-2011.) $)
    hashfz1 $p |- ( N e. NN0 -> ( # ` ( 1 ... N ) ) = N ) $=
      ( vy vx cn0 wcel c1 cfz co ccrd cfv cv caddc wceq copab cc0 crdg com
      cres chash cfn fz1fi eqid hashgval syl ccnv cardfz eqcomd fveq2d wf1o
      hashgf1o f1ocnvfv2 mpan eqtr3d ) ADEZFAGHZIJZBKCKFLHMCBNOPQRZJZUOSJZAUNUO
      TEURUSMAUACBUOUQUQUBZUCUDUNAUQUEJZUQJZURAUNVAUPUQUNUPVACBUQAUTUFUGUHQDUQU
      IUNVBAMCBUQUTUJQDAUQUKULUMUM $.
      $( [22-Jun-2011] $)

    $( Two finite sets have the same number of elements iff they are
       equinumerate.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    hashen $p |- ( ( A e. Fin /\ B e. Fin ) ->
                   ( ( # ` A ) = ( # ` B ) <-> A ~~ B ) ) $=
      ( vy vx cfn wcel wa chash cfv wceq ccrd cen wbr cv c1 caddc co copab cc0
      crdg com cres ccnv eqid hashginv eqeqan12d fveq2 syl5bi hashgval impbid
      carden bitrd ) AEFZBEFZGZAHIZBHIZJZAKIZBKIZJZABLMUOURVAUOUPCNDNOPQJDCRSTU
      AUBZUCZIZUQVCIZJVAURUMUNVDUSVEUTDCAVBVBUDZUEDCBVBVFUEUFUPUQVCUGUHUOUSVBIZ
      UTVBIZJURVAUMUNVGUPVHUQDCAVBVFUIDCBVBVFUIUFUSUTVBUGUHUJABEEUKUL $.
      $( [22-Jun-2011] $)
  $}

