$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               The ` # ` (finite set size) function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c # $.

  $( Extend the definition of a class to include the size function. $)
  chash $a class # $.

  ${
    $d x y $.
    $( Define the ` # ` function, which gives the cardinality of a finite set
       as a member of ` NN0 ` . $)
    df-hash $a |- # = ( ( rec (
                   ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) o. card ) $.
  $}

  ${
    $d G n $.  $d n x y $.
    hashgf1o.1 $e |- G = ( rec ( ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) $.
    $( ` G ` maps ` om ` one-to-one onto ` NN0 ` .  (Contributed by Paul
       Chapman, 22-Jun-2011.) $)
    hashgf1o $p |- G : om -1-1-onto-> NN0 $=
      ( com cn0 wf1o cc0 cuz cfv 0z om2uzf1oi wceq wb nn0uz f1oeq3 ax-mp mpbir
      ) DEBFZDGHIZBFZAGBJCKESLRTMNESDBOPQ $.
      $( [13-Sep-2013] $) $( [22-Jun-2011] $)
  $}

  ${
    $d x y $.
    $( A 1-based finite sequence of integers is finite.  (Contributed by Paul
       Chapman, 22-Jun-2011.)  (Proof shortened by Mario Carneiro,
       13-Feb-2013.) $)
    fz1fi $p |- ( N e. NN0 -> ( 1 ... N ) e. Fin ) $=
      ( vx cn0 wcel c1 cfz co cfn cvv cv caddc cmpt cc0 crdg com cres ccnv cfv
      wf1o eqid hashgf1o f1ocnvdm mpan con0 cin onfin2 inss2 eqsstri sseli syl
      cen wbr wb fvex fzennn enfi sylancr mpbird ) ACDZEAFGZHDZABIBJEKGLMNOPZQZ
      RZHDZUSVDODZVEOCVBSUSVFBVBVBTZUAOCAVBUBUCOHVDOUDHUEHUFUDHUGUHUIUJUSVDIDUT
      VDUKULVAVEUMAVCUNBVBAVGUOUTVDIUPUQUR $.
      $( [13-Sep-2013] $) $( [22-Jun-2011] $)
  $}

  ${
    $d A x y $.
    hashgval.1 $e |- G = ( rec ( ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) $.
    $( The value of the ` # ` function in terms of the mapping ` G ` from
       ` om ` to ` NN0 ` .  The proof avoids the use of ~ ax-ac .  (Contributed
       by Paul Chapman, 22-Jun-2011.)  (Revised by Mario Carneiro,
       12-Feb-2013.) $)
    hashgval $p |- ( A e. Fin -> ( G ` ( card ` A ) ) = ( # ` A ) ) $=
      ( vy cfn wcel chash cfv ccrd ccom cvv cv c1 caddc co cmpt cc0 crdg com
      cres df-hash coeq1i eqtr4i fveq1i cdm wceq cen wbr con0 wrex cab finnum
      breq2 rexbidv elabg mpbird cardf2 fdmi syl6eleqr wfun wfn rdgfnon fnfun
      funres mp2b funeqi mpbir wf ffun ax-mp fvco mp3an12 syl syl5req ) BFGZBHI
      BCJKZIZBJICIZBHVQHALAMZNOPQZRSZTUAZJKVQAUBCWCJDUCUDUEVPBJUFZGZVRVSUGZVPBE
      MZVTUHUIZEUJUKZAULZWDVPBWJGWGBUHUIZEUJUKZEBUMWIWLABFVTBUGWHWKEUJVTBWGUHUN
      UOUPUQWJUJJAEURZUSUTCVAZJVAZWEWFWNWCVAZWBUJVBWBVAWPRWAVCUJWBVDTWBVEVFCWCD
      VGVHWJUJJVIWOWMWJUJJVJVKBCJVLVMVNVO $.
      $( [13-Sep-2013] $) $( [22-Jun-2011] $)

    $( ` ``' G ` maps the size function's value to ` card ` .  (Contributed by
       Paul Chapman, 22-Jun-2011.) $)
    hashginv $p |- ( A e. Fin -> ( `' G ` ( # ` A ) ) = ( card ` A ) ) $=
      ( cfn wcel ccrd cfv chash wceq ccnv hashgval com wi ficardom cn0 wf1o
      hashgf1o f1ocnvfv mpan syl mpd ) BEFZBGHZCHBIHZJZUECKHUDJZABCDLUCUDMFZUFU
      GNZBOMPCQUHUIACDRMPUDUECSTUAUB $.
      $( [15-Sep-2013] $) $( [22-Jun-2011] $)
  $}

  ${
    $d x y z $.
    $( The size function maps all finite sets to their cardinality, as members
       of ` NN0 ` . $)
    hashf $p |- # : Fin --> NN0 $=
      ( vx vy vz cfn cn0 chash wf wfn cv cfv wcel wral ffnfv cvv c1 caddc co
      cmpt cc0 crdg com cres ccrd ccom wfun cdm wceq df-fn frfnom fnfun ax-mp
      cen wbr con0 wrex cab cardf2 ffun funco mp2an ccnv cima dmco fndm
      imaeq2i visset brcnv rexbii elima ficardom finnum abid sylibr fdmi
      syl6eleqr wa eqid fvex funbrfvb mpbii sylancr breq2 rcla4ev syl11anc wi
      funbrfv breldm syl6eleq sylib cardid2 syl eqbrtrrd ensym sylan2 isfi
      r19.23aiva impbii 3bitr4i eqriv eqtri mpbir2an df-hash fneq1i mpbir
      hashgval wf1o hashgf1o f1of ffvelrni eqeltrrd rgen ) DEFGFDHZAIZFJZEKZADL
      ADEFMXLANXMOPQRZSTUAUBZUCUDZDHZXSXRUEZXRUFZDUGXRDUHXQUEZUCUEZXTXQUAHZYBSX
      PUIZUAXQUJUKBIZXMULUMBUNUOZAUPZUNUCGYCABUQZYHUNUCURUKZXQUCUSUTYAUCVAZXQUF
      ZVBZDXQUCVCYMYKUAVBZDYLUAYKYDYLUAUGYEUAXQVDUKVEAYNDCIZXMYKUMZCUAUOXMYOUCU
      MZCUAUOZXMYNKXMDKZYPYQCUAYOXMUCCVFZAVFZVGVHCXMYKUAUUAVIYSYRYSXMUCJZUAKZXM
      UUBUCUMZYRXMVJZYSYCXMUCUFZKZUUDYJYSXMYHUUFYSYGXMYHKZBXMVKYGAVLZVMYHUNUCYI
      VNZVOYCUUGVPUUBUUBUGUUDUUBVQXMUUBUCXMUCVRVSVTWAYQUUDCUUBUAYOUUBXMUCWBWCWD
      YQYSCUAYOUAKZYQVPXMYFULUMZBUAUOZYSYQUUKXMYOULUMZUUMYQYOXMULUMUUNYQUUBYOXM
      ULYCYQUUBYOUGWEYJXMYOUCYTWFUKYQYGUUBXMULUMYQUUHYGYQXMUUFYHXMYOUCUUAWGUUJW
      HUUIWIBXMWJWKWLYOXMUUAWMWKUULUUNBYOUAYFYOXMULWBWCWNBXMWOVMWPWQWRWSWTWTXAD
      FXRAXBXCXDXOADYSUUBBNYFOPQRSTUAUBZJZXNEBXMUUOUUOVQZXEYSUUCUUPEKUUEUAEUUBU
      UOUAEUUOXFUAEUUOGBUUOUUQXGUAEUUOXHUKXIWKXJXKXA $.
      $( [13-Sep-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d x N $.
    $( The set ` ( 1 ... N ) ` has ` N ` elements.  (Contributed by Paul
       Chapman, 22-Jun-2011.) $)
    hashfz1 $p |- ( N e. NN0 -> ( # ` ( 1 ... N ) ) = N ) $=
      ( vx cn0 wcel c1 cfz co ccrd cfv cvv cv caddc cmpt cc0 crdg com cres
      chash cfn wceq fz1fi eqid hashgval syl ccnv cardfz eqcomd fveq2d wf1o
      hashgf1o f1ocnvfv2 mpan eqtr3d ) ACDZEAFGZHIZBJBKELGMNOPQZIZUORIZAUNUOSDU
      RUSTAUABUOUQUQUBZUCUDUNAUQUEIZUQIZURAUNVAUPUQUNUPVABUQAUTUFUGUHPCUQUIUNVB
      ATBUQUTUJPCAUQUKULUMUM $.
      $( [15-Sep-2013] $) $( [22-Jun-2011] $)

    $( Two finite sets have the same number of elements iff they are
       equinumerous.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    hashen $p |- ( ( A e. Fin /\ B e. Fin ) ->
                   ( ( # ` A ) = ( # ` B ) <-> A ~~ B ) ) $=
      ( vx cfn wcel wa chash cfv wceq ccrd cen wbr cvv cv c1 caddc co cmpt cc0
      crdg com cres ccnv fveq2 eqid hashginv eqeqan12d syl5ib hashgval impbid
      wi con0 wrex finnum cardid2 syl ensymg mpd breq1 anbi2d biimprcd entr
      syl6 syl2an carden2b adantl bitrd ) ADEZBDEZFZAGHZBGHZIZAJHZBJHZIZABKLZVJ
      VMVPVMVKCMCNZOPQRSTUAUBZUCZHZVLVTHZIVJVPVKVLVTUDVHVIWAVNWBVOCAVSVSUEZUFCB
      VSWCUFUGUHVPVNVSHZVOVSHZIVJVMVNVOVSUDVHVIWDVKWEVLCAVSWCUICBVSWCUIUGUHUJVJ
      VPVQVHAVNKLZVOBKLZVPVQUKVIVHVNAKLZWFVHVRAKLCULUMWHCAUNCAUOUPVNADUQURVIVRB
      KLCULUMWGCBUNCBUOUPWFWGFZVPWFVNBKLZFZVQVPWKWIVPWJWGWFVNVOBKUSUTVAAVNBVBVC
      VDVIVQVPUKVHABDVEVFUJVG $.
      $( [15-Sep-2013] $) $( [22-Jun-2011] $)
  $}

  ${
    $d x A $.
    $( The size function of the cardinality function. $)
    hashcard $p |- ( A e. Fin -> ( # ` ( card ` A ) ) = ( # ` A ) ) $=
      ( vx cfn wcel ccrd cfv cvv cv c1 caddc co cmpt cc0 crdg com cres chash
      wceq cardidm fveq2i a1i wss ficardom ssid ssnnfi sylancl eqid hashgval
      syl 3eqtr3d ) ACDZAEFZEFZBGBHIJKLMNOPZFZULUNFZULQFZAQFUOUPRUKUMULUNASTUAU
      KULCDZUOUQRUKULODULULUBURAUCULUDULULUEUFBULUNUNUGZUHUIBAUNUSUHUJ $.
      $( [4-Nov-2013] $) $( [19-Sep-2013] $)
  $}

  $( Closure of the ` # ` function.  (Contributed by Paul Chapman,
     26-Oct-2012.)  (Revised by Mario Carneiro, 15-Sep-2013.) $)
  hashcl $p |- ( A e. Fin -> ( # ` A ) e. NN0 ) $=
    ( cfn cn0 chash hashf ffvelrni ) BCADEF $.
    $( [15-Sep-2013] $) $( [26-Oct-2012] $)

  $( Two ways of saying a finite set is empty.  (Contributed by Paul Chapman,
     26-Oct-2012.) $)
  hash0 $p |- ( A e. Fin -> ( ( # ` A ) = 0 <-> A = (/) ) ) $=
    ( chash cfv cc0 wceq c0 cfn wcel c1 cfz co eqid cn0 wb 0nn0 fz1n ax-mp
    mpbir fveq2i hashfz1 eqtr3i eqeq2i cen wbr 0fin hashen mpan2 en0 syl6bb
    syl5bbr ) ABCZDEUKFBCZEZAGHZAFEZULDUKIDJKZBCZULDUPFBUPFEZDDEZDLDMHZURUSNODP
    QRSUTUQDEODTQUAUBUNUMAFUCUDZUOUNFGHUMVANUEAFUFUGAUHUIUJ $.
    $( [26-Oct-2012] $)

  $( The size of a singleton.  (Contributed by Paul Chapman, 26-Oct-2012.)
     (The proof was shortened by Mario Carneiro, 13-Feb-2013.) $)
  hashsng $p |- ( A e. V -> ( # ` { A } ) = 1 ) $=
    ( wcel csn chash cfv c1 cen wbr wceq cz 1z en2sn mpan2 cfn wb snfi hashen
    mp2an sylibr cfz co cn0 1nn0 hashfz1 ax-mp fzsn fveq2d syl5reqr syl6eq ) AB
    CZADZEFZGDZEFZGUKULUNHIZUMUOJZUKGKCZUPLAGBKMNULOCUNOCUQUPPAQGQULUNRSTURUOGJ
    LURGGGUAUBZEFZUOGUCCUTGJUDGUEUFURUSUNEGUGUHUIUFUJ $.
    $( [13-Feb-2013] $) $( [26-Oct-2012] $)

  $( The value of the size function on a singleton.  (Contributed by Paul
     Chapman, 26-Oct-2012.) $)
  hashsngOLD $p |- ( A e. V -> ( # ` { A } ) = 1 ) $=
    ( wcel csn c1 cen wbr chash cfv wceq cz 1z en2sn mpan2 cfz co fzsn ax-mp
    fveq2i cn0 1nn0 hashfz1 eqtr3i eqeq2i cfn wb snfi hashen mp2an bitr3i
    sylibr ) ABCZADZEDZFGZUMHIZEJZULEKCZUOLAEBKMNUQUPUNHIZJZUOUSEUPEEOPZHIZUSEV
    AUNHURVAUNJLEQRSETCVBEJUAEUBRUCUDUMUECUNUECUTUOUFAUGEUGUMUNUHUIUJUK $.
    $( [26-Oct-2012] $)

  ${
    $d F f $.  $d N f $.
    $( The value of the size function on a finite 1-based sequence.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    fseq1hash $p |- ( ( N e. NN0 /\ F Fn ( 1 ... N ) ) -> ( # ` F ) = N ) $=
      ( vf cn0 wcel c1 cfz co wfn wa cvv chash cfv wceq oprex fnex mpan2
      adantl cv wi fneq1 anbi2d fveq2 eqeq1d imbi12d cen wbr cdm wfun fnfun
      visset fundmen syl wb fndm breq1 mpbid cfn fz1fi adantr enfi mpan biimpa
      syl11anc hashen mpbird hashfz1 eqtr3d vtoclg com12 mpd ) BDEZAFBGHZIZJZAK
      EZALMZBNZVNVPVLVNVMKEVPFBGOVMKAPQRVPVOVRVLCSZVMIZJZVSLMZBNZTVOVRTCAKVSANZ
      WAVOWCVRWDVTVNVLVMVSAUAUBWDWBVQBVSALUCUDUEWAVMLMZWBBWAWEWBNZVMVSUFUGZVTWG
      VLVTVSUHZVSUFUGZWGVTVSUIWIVMVSUJVSCUKZULUMVTWHVMNWIWGUNVMVSUOWHVMVSUFUPUM
      UQRZWAVMUREZVSUREZWFWGUNVLWLVTBUSUTZWAWGWLWMWKWNWGWLWMVSKEWGWLWMUNWJVMVSK
      VAVBVCVDVMVSVEVDVFVLWEBNVTBVGUTVHVIVJVK $.
      $( [26-Oct-2012] $)
  $}

  ${
    $d A n z $.  $d B n $.  $d G n z $.
    hashgadd.1 $e |- G = ( rec ( ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) $.
    $( ` G ` maps ordinal addition to integer addition.  (Contributed by Paul
       Chapman, 30-Nov-2012.) $)
    hashgadd $p |- ( ( A e. om /\ B e. om ) ->
                     ( G ` ( A +o B ) ) = ( ( G ` A ) + ( G ` B ) ) ) $=
      ( vn vz com wcel coa co cfv caddc wceq cv wi c0 csuc opreq2 fveq2d fveq2
      opreq2d eqeq12d imbi2d cc0 cc cn0 wf1o wf hashgf1o f1of ax-mp ffvelrni
      nn0cn syl addid1 0z om2uz0i opreq2i a1i con0 nnon oa0 3eqtr4rd w3a c1 wa
      oasuc syl2an nnacl om2uzsuci eqtrd 3adant3 ax-1cn addass mp3an3 opreq1
      3ad2ant3 3ad2ant2 3eqtr4d 3expia expcom a2d finds impcom ) CHIBHIZBCJKZDL
      ZBDLZCDLZMKZNZWFBFOZJKZDLZWIWMDLZMKZNZPWFBQJKZDLZWIQDLZMKZNZPWFBGOZJKZDLZ
      WIXDDLZMKZNZPWFBXDRZJKZDLZWIXJDLZMKZNZPWFWLPFGCWMQNZWRXCWFXPWOWTWQXBXPWNW
      SDWMQBJSTXPWPXAWIMWMQDUAUBUCUDWMXDNZWRXIWFXQWOXFWQXHXQWNXEDWMXDBJSTXQWPXG
      WIMWMXDDUAUBUCUDWMXJNZWRXOWFXRWOXLWQXNXRWNXKDWMXJBJSTXRWPXMWIMWMXJDUAUBUC
      UDWMCNZWRWLWFXSWOWHWQWKXSWNWGDWMCBJSTXSWPWJWIMWMCDUAUBUCUDWFWIUEMKZWIXBWT
      WFWIUFIZXTWINWFWIUGIYAHUGBDHUGDUHHUGDUIADEUJHUGDUKULZUMWIUNUOZWIUPUOXBXTN
      WFXAUEWIMAUEDUQEURUSUTWFWSBDWFBVAIZWSBNBVBZBVCUOTVDXDHIZWFXIXOWFYFXIXOPWF
      YFXIXOWFYFXIVEZXLXFVFMKZXNWFYFXLYHNXIWFYFVGZXLXERZDLZYHYIXKYJDWFYDXDVAIXK
      YJNYFYEXDVBBXDVHVITYIXEHIYKYHNBXDVJAXEUEDUQEVKUOVLVMYGXHVFMKZWIXGVFMKZMKZ
      YHXNWFYFYLYNNZXIWFYAXGUFIZYOYFYCYFXGUGIYPHUGXDDYBUMXGUNUOYAYPVFUFIYOVNWIX
      GVFVOVPVIVMXIWFYHYLNYFXFXHVFMVQVRYFWFXNYNNXIYFXMYMWIMAXDUEDUQEVKUBVSVTVLW
      AWBWCWDWE $.
      $( [15-Sep-2013] $) $( [30-Nov-2012] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Dominance relation for the size function. $)
    hashdom $p |- ( ( A e. Fin /\ B e. Fin )
         -> ( ( # ` A ) <_ ( # ` B ) <-> A ~<_ B ) ) $=
      ( vy vx cfn wcel wa ccrd cfv wss cv coa co wceq com wrex cdom wbr chash
      cle wb ficardom nnawordex syl2an ficarddom c1 cmin cfz cn0 hashcl
      ad2antrr ad2antlr simpr nn0sub2 syl111anc fz1fi syl cvv caddc cmpt cc0
      crdg cres ccnv eqid hashgval hashfz1 eqtrd opreq12d cc nn0cn pncan3
      adantr hashgadd syl11anc 3eqtr4d fveq2d wf1o hashgf1o nnacl f1ocnvfv1
      sylancr 3eqtr3d opreq2 eqeq1d rcla4ev ex cardnn adantl opreq2d fveq2 wi
      ssid ssnnfi mpan2 opreqan12d adantlr eqeq12d nn0ge0 cr nn0re addge01
      mpbid breq2 syl5ibcom sylbid sylan2 syl5 sylbird r19.23adva impbid
      3bitr4rd ) AEFZBEFZGZAHIZBHIZJZXPCKZLMZXQNZCOPZABQRASIZBSIZTRZXMXPOFZXQOF
      ZXRYBUAXNAUBZBUBZCXPXQUCUDABUEXOYEYBXOYEYBXOYEGZUFYDYCUGMZUHMZHIZOFZXPYML
      MZXQNZYBYJYLEFZYNYJYKUIFZYQYJYCUIFZYDUIFZYEYRXMYSXNYEAUJZUKXNYTXMYEBUJZUL
      XOYEUMYCYDUNUOZYKUPUQZYLUBUQZYJYODURDKUFUSMUTVAVBOVCZIZUUFVDZIZXQUUFIZUUH
      IZYOXQYJUUGUUJUUHYJXPUUFIZYMUUFIZUSMZYDUUGUUJYJUUNYCYKUSMZYDYJUULYCUUMYKU
      SXMUULYCNXNYEDAUUFUUFVEZVFZUKYJUUMYLSIZYKYJYQUUMUURNUUDDYLUUFUUPVFUQYJYRU
      URYKNUUCYKVGUQVHVIXOUUOYDNZYEXMYCVJFZYDVJFZUUSXNXMYSUUTUUAYCVKUQXNYTUVAUU
      BYDVKUQYCYDVLUDVMVHYJYFYNUUGUUNNXMYFXNYEYHUKZUUEDXPYMUUFUUPVNVOXNUUJYDNZX
      MYEDBUUFUUPVFZULVPVQYJOUIUUFVRZYOOFZUUIYONDUUFUUPVSZYJYFYNUVFUVBUUEXPYMVT
      VOOUIYOUUFWAWBYJUVEYGUUKXQNUVGXNYGXMYEYIULOUIXQUUFWAWBWCYAYPCYMOXSYMNXTYO
      XQXSYMXPLWDWEWFVOWGXOYAYECOXOXSOFZGZYAXPXSHIZLMZXQNZYEUVIUVKXTXQUVIUVJXSX
      PLUVHUVJXSNXOXSWHWIWJWEUVLUVKUUFIZUUJNZUVIYEUVKXQUUFWKUVHXOXSEFZUVNYEWLUV
      HXSXSJUVOXSWMXSXSWNWOXOUVOGZUVNYCXSSIZUSMZYDNZYEUVPUVMUVRUUJYDXMUVOUVMUVR
      NXNXMUVOGZUVMUULUVJUUFIZUSMZUVRXMYFUVJOFUVMUWBNUVOYHXSUBDXPUVJUUFUUPVNUDX
      MUVOUULYCUWAUVQUSUUQDXSUUFUUPVFWPVHWQXNUVCXMUVOUVDULWRUVPYCUVRTRZUVSYEXMU
      VOUWCXNUVTVAUVQTRZUWCUVOUWDXMUVOUVQUIFZUWDXSUJZUVQWSUQWIXMYCWTFZUVQWTFZUW
      DUWCUAUVOXMYSUWGUUAYCXAUQUVOUWEUWHUWFUVQXAUQYCUVQXBUDXCWQUVRYDYCTXDXEXFXG
      XHXIXJXKXL $.
      $( [4-Nov-2013] $) $( [22-Sep-2013] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The size of the union of disjoint finite sets is the sum of their
       sizes.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
    hashun $p |- ( ( A e. Fin /\ B e. Fin /\ ( A i^i B ) = (/) ) ->
                   ( # ` ( A u. B ) ) = ( ( # ` A ) + ( # ` B ) ) ) $=
      ( vx cfn wcel cin c0 wceq w3a cun ccrd cfv cvv cv c1 caddc co cmpt cc0
      crdg com cres coa chash ficardun fveq2d wa unfi eqid hashgval syl
      3adant3 ficardom hashgadd syl2an opreqan12d eqtrd 3eqtr3d ) ADEZBDEZABFGH
      ZIZABJZKLZCMCNOPQRSTUAUBZLZAKLZBKLZUCQZVELZVCUDLZAUDLZBUDLZPQZVBVDVIVEABU
      EUFUSUTVFVKHZVAUSUTUGZVCDEVOABUHCVCVEVEUIZUJUKULUSUTVJVNHVAVPVJVGVELZVHVE
      LZPQZVNUSVGUAEVHUAEVJVTHUTAUMBUMCVGVHVEVQUNUOUSUTVRVLVSVMPCAVEVQUJCBVEVQU
      JUPUQULUR $.
      $( [15-Sep-2013] $) $( [30-Nov-2012] $)

    $( The size of the union of finite sets is less than or equal to the sum of
       their sizes. $)
    hashun2 $p |- ( ( A e. Fin /\ B e. Fin ) ->
                   ( # ` ( A u. B ) ) <_ ( ( # ` A ) + ( # ` B ) ) ) $=
      ( vx cfn wcel wa cun chash cfv ccrd coa co caddc cle wbr cdom wss
      ficardun2 com wceq ficardom nnacl syl2an cardnn syl sseqtr4d wb unfi
      ssid ssnnfi sylancl ficarddom syl11anc mpbird hashdom cvv cv c1 cmpt cc0
      crdg cres fveq2d eqid hashgval hashgadd 3eqtr3d opreqan12d eqtrd breqtrd
      ) ADEZBDEZFZABGZHIZAJIZBJIZKLZHIZAHIZBHIZMLZNVMVOVSNOZVNVRPOZVMWDVNJIZVRJ
      IZQZVMWEVRWFABRVMVRSEZWFVRTVKVPSEZVQSEZWHVLAUAZBUAZVPVQUBUCZVRUDUEZUFVMVN
      DEZVRDEZWDWGUGABUHZVMWHVRVRQWPWMVRUIVRVRUJUKZVNVRULUMUNVMWOWPWCWDUGWQWRVN
      VRUOUMUNVMVSVPCUPCUQURMLUSUTVASVBZIZVQWSIZMLZWBVMWFWSIZVRWSIZVSXBVMWFVRWS
      WNVCVMWPXCVSTWRCVRWSWSVDZVEUEVKWIWJXDXBTVLWKWLCVPVQWSXEVFUCVGVKVLWTVTXAWA
      MCAWSXEVECBWSXEVEVHVIVJ $.
      $( [4-Nov-2013] $) $( [23-Sep-2013] $)
  $}

  ${
    hashpr.1 $e |- A e. _V $.
    hashpr.2 $e |- B e. _V $.
    $( The size of an unordered pair. $)
    hashpr $p |- ( A =/= B <-> ( # ` { A , B } ) = 2 ) $=
      ( wne cpr chash cfv c2 wceq c1 caddc co csn cun df-pr fveq2i cin c0
      disjsn2 cfn wcel snfi hashun mp3an12 syl syl5eq cvv hashsng ax-mp
      opreq12i syl6eq df-2 syl6eqr dfsn2 preq2 syl5req fveq2d 1ne2 eqnetri a1i
      eqnetrd necon2i impbii ) ABEZABFZGHZIJVEVGKKLMZIVEVGANZGHZBNZGHZLMZVHVEVG
      VIVKOZGHZVMVFVNGABPQVEVIVKRSJZVOVMJZABTVIUAUBVKUAUBVPVQAUCBUCVIVKUDUEUFUG
      VJKVLKLAUHUBVJKJCAUHUIUJZBUHUBVLKJDBUHUIUJUKULUMUNABVGIABJZVGVJIVSVFVIGVS
      VIAAFVFAUOABAUPUQURVJIEVSVJKIVRUSUTVAVBVCVD $.
      $( [27-Sep-2013] $)
  $}

  $( The size of the union of a finite set with a disjoint singleton is one
     more than the size of the set.  (Contributed by Paul Chapman,
     30-Nov-2012.) $)
  hashunsng $p |- ( B e. V -> ( ( A e. Fin /\ -. B e. A ) ->
                    ( # ` ( A u. { B } ) ) = ( ( # ` A ) + 1 ) ) ) $=
    ( cfn wcel wn wa csn cun chash cfv c1 caddc co wceq cin c0 disjsn snfi
    hashun mp3an2 sylan2br hashsng opreq2d sylan9eq expcom ) ADEZBAEFZGZBCEZABH
    ZIJKZAJKZLMNZOUIUJULUMUKJKZMNZUNUHUGAUKPQOZULUPOZABRUGUKDEUQURBSAUKTUAUBUJU
    OLUMMBCUCUDUEUF $.
    $( [30-Nov-2012] $)

  ${
    $d A x y z $.  $d B x y z $.
    hashxplem.1 $e |- B e. Fin $.
    $( Lemma for ~ hashxp . $)
    hashxplem $p |- ( A e. Fin ->
                      ( # ` ( A X. B ) ) = ( ( # ` A ) x. ( # ` B ) ) ) $=
      ( vx vy vz cv cxp chash cfv cmul co wceq c0 csn cun xpeq1 fveq2d fveq2
      opreq1d eqeq12d cc0 cfn wcel cn0 cc hashcl nn0cn mul02 3syl ax-mp 0fin
      eqid hash0 biimpar mp2an opreq1i xp0r fveq2i eqtri 3eqtr4ri wn wa caddc
      opreq1 adantl xpundir cin xpfi mpan2 inxp disjsn biimpri syl syl6eq
      syl5eq snfi hashun mp3an2 syl2an cen wbr snex elisseti xpcomen visset
      xpsnen entri wb hashen mpbir opreq2i adantr c1 cvv wi hashunsng ax-1cn
      adddir mp3an23 mulid2 eqtrd 3eqtr4d ex findcard2s ) DGZBHZIJZXFIJZBIJZKLZ
      MNBHZIJZNIJZXJKLZMEGZBHZIJZXPIJZXJKLZMZXPFGZOZPZBHZIJZYDIJZXJKLZMZABHZIJZ
      AIJZXJKLZMDEFAXFNMZXHXMXKXOYNXGXLIXFNBQRYNXIXNXJKXFNISTUAXFXPMZXHXRXKXTYO
      XGXQIXFXPBQRYOXIXSXJKXFXPISTUAXFYDMZXHYFXKYHYPXGYEIXFYDBQRYPXIYGXJKXFYDIS
      TUAXFAMZXHYKXKYMYQXGYJIXFABQRYQXIYLXJKXFAISTUAUBXJKLZUBXOXMBUCUDZYRUBMZCY
      SXJUEUDZXJUFUDZYTBUGZXJUHZXJUIUJUKXNUBXJKNUCUDZNNMZXNUBMZULNUMUUEUUGUUFNU
      NUOUPZUQXMXNUBXLNIBURUSUUHUTVAXPUCUDZYBXPUDVBZVCZYAYIUUKYAVCXRXJVDLZXTXJV
      DLZYFYHYAUULUUMMUUKXRXTXJVDVEVFUUKYFUULMYAUUKYFXQYCBHZPZIJZUULYEUUOIXPYCB
      VGUSUUKUUPXRUUNIJZVDLZUULUUIXQUCUDZXQUUNVHZNMZUUPUURMZUUJUUIYSUUSCXPBVIVJ
      UUJUUTXPYCVHZBBVHZHZNXPBYCBVKUUJUVENUVDHZNUUJUVCNMZUVEUVFMUVGUUJXPYBVLVMU
      VCNUVDQVNUVDURVOVPUUSUUNUCUDZUVAUVBYCUCUDYSUVHYBVQCYCBVIUPZXQUUNVRVSVTUUQ
      XJXRVDUUQXJMZUUNBWAWBZUUNBYCHBYCBYBWCBUCCWDZWEBYBUVLFWFZWGWHUVHYSUVJUVKWI
      UVICUUNBWJUPWKWLVOVPWMUUKYHUUMMYAUUKYHXSWNVDLZXJKLZUUMUUKYGUVNXJKYBWOUDUU
      KYGUVNMWPUVMXPYBWOWQUKTUUIUVOUUMMUUJUUIUVOXTWNXJKLZVDLZUUMUUIXSUEUDXSUFUD
      ZUVOUVQMZXPUGXSUHUVRWNUFUDUUBUVSWRYSUUBCYSUUAUUBUUCUUDVNUKZXSWNXJWSWTUJUV
      PXJXTVDUUBUVPXJMUVTXJXAUKWLVOWMXBWMXCXDXE $.
      $( [30-Nov-2012] $)
  $}

  $( The size of the Cartesian product of two finite sets is the product of
     their sizes.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
  hashxp $p |- ( ( A e. Fin /\ B e. Fin ) ->
                 ( # ` ( A X. B ) ) = ( ( # ` A ) x. ( # ` B ) ) ) $=
    ( cfn wcel cxp chash cfv cmul co wceq wi c0 cif xpeq2 fveq2d fveq2 opreq2d
    eqeq12d imbi2d 0fin elimel hashxplem dedth impcom ) BCDZACDZABEZFGZAFGZBFGZ
    HIZJZUEUFULKUFAUEBLMZEZFGZUIUMFGZHIZJZKBLBUMJZULURUFUSUHUOUKUQUSUGUNFBUMANO
    USUJUPUIHBUMFPQRSAUMBLCTUAUBUCUD $.
    $( [30-Nov-2012] $)

  ${
    $d F a b $.  $d S a b $.  $d Y a b w $.  $d Z a b w $.
    hashpwlem.1 $e |- F = ( w e. ~P Y |-> ( w u. { Z } ) ) $.
    hashpwlem.2 $e |- S = { w | ( w \ Y ) = { Z } } $.
    $( Lemma for ~ hashpw . $)
    hashpwlem $p |- ( -. Z e. Y -> F : ~P Y -1-1-onto-> S ) $=
      ( va vb wcel wn crn wceq cv cfv wi cpw wral wf1o wfo wf wrex wfn csn cun
      visset snex unex cmpt wa copab df-mpt eqtri fnopab2 a1i uneq1 fvmpt
      adantl cdif difundir c0 wss elpw ssdif0 bitri biimpi cin disjsn incom
      eqeq1i bitr3i disj3 eqcomd adantr uneq12d uncom un0 syl6eq syl5eq difeq1
      eqeq1d elab2 sylibr eqeltrd r19.21aiva ffnfv sylanbrc inss2 dfin4 difeq2
      syl5req sseq1d mpbiri cvv difexg ax-mp syl undif1 difss sseq1 mpbii
      ssequn1 syl5eqr eqtr2d jca sylbi fveq2 eqeq2d rcla4ev rgen dffo3 forn wb
      eqeqan12d anbi12i ssel con3rr3 syl6ibr anim12d eqtr3 syl6 imp sylan2b
      unineq sylan2 expcom sylbid r19.21aivva w3a dff1o6 biimpri mp3an1
      syl11anc ) EDJZKZCLBMZHNZCOZINZCOZMZYQYSMZPZIDQZRHUUDRZUUDBCSZYOUUDBCTZYP
      YOUUDBCUAZYQYTMZIUUDUBZHBRZUUGYOCUUDUCZYRBJZHUUDRUUHUULYOAHUUDANZEUDZUEZC
      UUNUUOAUFEUGZUHCAUUDUUPUIUUNUUDJYQUUPMUJAHUKFAHUUDUUPULUMUNZUOYOUUMHUUDYO
      YQUUDJZUJZYRYQUUOUEZBUUSYRUVAMYOAYQUUPUVAUUDCUUNYQUUOUPFYQUUOHUFZUUQUHZUQ
      ZURUUTUVADUSZUUOMZUVABJUUTUVEYQDUSZUUODUSZUEZUUOYQUUODUTUUTUVIVAUUOUEZUUO
      UUTUVGVAUVHUUOUUSUVGVAMZYOUUSUVKUUSYQDVBZUVKYQDUVBVCZYQDVDVEVFURYOUVHUUOM
      UUSYOUUOUVHYOUUOUVHMZYOUUODVGZVAMZUVNYODUUOVGZVAMUVPDEVHUVQUVOVADUUOVIVJV
      KUUODVLVEVFVMVNVOUVJUUOVAUEUUOVAUUOVPUUOVQUMVRVSUUNDUSZUUOMZUVFAUVABUVCUU
      NUVAMUVRUVEUUOUUNUVADVTWAGWBWCWDWEHUUDBCWFWGUUKYOUUJHBYQBJZYQUUOUSZUUDJZY
      QUWACOZMZUJZUUJUVTUVGUUOMZUWEUVSUWFAYQBUVBUUNYQMUVRUVGUUOUUNYQDVTWAGWBUWF
      UWBUWDUWFUWADVBZUWBUWFUWGYQDVGZDVBYQDWHUWFUWAUWHDUWFUWHYQUVGUSUWAYQDWIUVG
      UUOYQWJWKWLWMUWADYQWNJUWAWNJUVBYQUUOWNWOWPZVCWCZUWFUWCUWAUUOUEZYQUWFUWBUW
      CUWKMUWJAUWAUUPUWKUUDCUUNUWAUUOUPFUWAUUOUWIUUQUHUQWQUWFUWKUVAYQYQUUOWRUWF
      UUOYQVBZUVAYQMUWFUVGYQVBUWLYQDWSUVGUUOYQWTXAUWLUVAUUOYQUEZYQUUOYQVPUWLUWM
      YQMUUOYQXBVFXCWQVSXDXEXFUUIUWDIUWAUUDYSUWAMYTUWCYQYSUWACXGXHXIWQXJUOIHUUD
      BCXKWGUUDBCXLWQYOUUCHIUUDUUDYOUUSYSUUDJZUJZUJZUUAUVAYSUUOUEZMZUUBUWOUUAUW
      RXMYOUUSUWNYRUVAYTUWQUVDAYSUUPUWQUUDCUUNYSUUOUPFYSUUOIUFZUUQUHUQXNURUWRUW
      PUUBUWPUWRYQUUOVGZYSUUOVGZMZUUBUWOYOUVLYSDVBZUJZUXBUUSUVLUWNUXCUVMYSDUWSV
      CXOYOUXDUXBYOUXDUWTVAMZUXAVAMZUJUXBYOUVLUXEUXCUXFYOUVLEYQJZKUXEUVLUXGYNYQ
      DEXPXQYQEVHXRYOUXCEYSJZKUXFUXCUXHYNYSDEXPXQYSEVHXRXSUWTUXAVAXTYAYBYCUWRUX
      BUJUUBYQYSUUOYDVFYEYFYGYHUULYPUUEUUFUURUUFUULYPUUEYIHIUUDBCYJYKYLYM $.
      $( [30-Nov-2012] $)
  $}

  ${
    $d A x y z $.  $d w x y z $.
    $( The size of the power set of a finite set is 2 raised to the power of
       the size of the set.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
    hashpw $p |- ( A e. Fin -> ( # ` ~P A ) = ( 2 ^ ( # ` A ) ) ) $=
      ( vx vy vz vw cv cpw chash cfv c2 cexp co wceq c0 csn cun pweq fveq2d
      fveq2 opreq2d eqeq12d cc0 c1 cc wcel 2cn exp0 ax-mp cfn 0fin eqid hash0
      biimpar mp2an opreq2i pw0 fveq2i hashsng eqtri 3eqtr4ri wn wa cmul
      opreq1 adantl caddc cdif cab cen wbr cmpt wf1o hashpwlem visset pwex
      f1oen syl wb pwfi biimpi adantr crn cdom cvv wfn snex unex df-mpt
      fnopab2 fnrndomg mp2 domfi mpan2 wfo f1ofo forn 3syl eleq1d mpbid hashen
      syl11anc mpbird cn0 hashcl nn0cn times2 wss wo ssundif sssn bitri abbii
      df-pw unab 3eqtr4i ssdif0 uneq1i eqtr4i cin ineq1i inab wi snprc con1bii
      mpbir eqeq1 mtbiri con2i imnan mpbi abf hashun mp3an3 syl5eq 3eqtr4rd
      hashunsng expp1 mpan eqtrd 3eqtr4d ex findcard2s ) BFZGZHIZJUUCHIZKLZMNGZ
      HIZJNHIZKLZMCFZGZHIZJUULHIZKLZMZUULDFZOZPZGZHIZJUUTHIZKLZMZAGZHIZJAHIZKLZ
      MBCDAUUCNMZUUEUUIUUGUUKUVJUUDUUHHUUCNQRUVJUUFUUJJKUUCNHSTUAUUCUULMZUUEUUN
      UUGUUPUVKUUDUUMHUUCUULQRUVKUUFUUOJKUUCUULHSTUAUUCUUTMZUUEUVBUUGUVDUVLUUDU
      VAHUUCUUTQRUVLUUFUVCJKUUCUUTHSTUAUUCAMZUUEUVGUUGUVIUVMUUDUVFHUUCAQRUVMUUF
      UVHJKUUCAHSTUAJUBKLZUCUUKUUIJUDUEZUVNUCMUFJUGUHUUJUBJKNUIUEZNNMZUUJUBMZUJ
      NUKUVPUVRUVQNULUMUNUOUUINOZHIZUCUUHUVSHUPUQUVPUVTUCMUJNUIURUHUSUTUULUIUEZ
      UURUULUEVAZVBZUUQUVEUWCUUQVBUUNJVCLZUUPJVCLZUVBUVDUUQUWDUWEMUWCUUNUUPJVCV
      DVEUWCUVBUWDMUUQUWCUUNUUNVFLZUUNEFZUULVGZUUSMZEVHZHIZVFLZUWDUVBUWCUUNUWKU
      UNVFUWCUUNUWKMZUUMUWJVIVJZUWBUWNUWAUWBUUMUWJEUUMUWGUUSPZVKZVLZUWNEUWJUWPU
      ULUURUWPUKUWJUKVMZUUMUWJUWPUULCVNVOZVPVQVEUWCUUMUIUEZUWJUIUEZUWMUWNVRUWAU
      WTUWBUWAUWTUULVSVTZWAZUWCUWPWBZUIUEZUXAUWAUXEUWBUWAUWTUXEUXBUWTUXDUUMWCVJ
      ZUXEUUMWDUEUWPUUMWEUXFUWSEBUUMUWOUWPUWGUUSEVNUURWFWGEBUUMUWOWHWIUUMWDUWPW
      JWKUUMUXDWLWMVQWAUWBUXEUXAVRUWAUWBUXDUWJUIUWBUWQUUMUWJUWPWNUXDUWJMUWRUUMU
      WJUWPWOUUMUWJUWPWPWQWRVEWSZUUMUWJWTXAXBTUWCUWTUWDUWFMZUXCUWTUUNXCUEUUNUDU
      EUXHUUMXDUUNXEUUNXFWQVQUWCUVBUUMUWJPZHIZUWLUVAUXIHUVAUWHNMZEVHZUWJPZUXIUW
      GUUTXGZEVHUXKUWIXHZEVHUVAUXMUXNUXOEUXNUWHUUSXGUXOUWGUULUUSXIUWHUURXJXKXLE
      UUTXMUXKUWIEXNXOUUMUXLUWJUUMUWGUULXGZEVHUXLEUULXMUXPUXKEUWGUULXPXLUSZXQXR
      UQUWCUWTUXAUXJUWLMZUXCUXGUWTUXAUUMUWJXSZNMUXRUXSUXLUWJXSZNUUMUXLUWJUXQXTU
      XTUXKUWIVBZEVHNUXKUWIEYAUYAEUXKUWIVAYBUYAVAUWIUXKUWIUXKUUSNMZUYBVAUURWDUE
      ZDVNZUYCUYBUURYCYDYEUWHUUSNYFYGYHUXKUWIYIYJYKUSUSUUMUWJYLYMXAYNYOWAUWCUVD
      UWEMUUQUWCUVDJUUOUCVFLZKLZUWEUWCUVCUYEJKUYCUWCUVCUYEMYBUYDUULUURWDYPUHTUW
      AUYFUWEMZUWBUWAUUOXCUEZUYGUULXDUVOUYHUYGUFJUUOYQYRVQWAYSWAYTUUAUUB $.
      $( [30-Nov-2012] $)
  $}

  ${
    $d x y z F $.
    $( A finite set is a function iff it is equinumerous to its domain. $)
    hashfun $p |- ( F e. Fin -> ( Fun F <-> ( # ` F ) = ( # ` dom F ) ) ) $=
      ( vx vy vz cfn wcel wfun chash cfv cdm wceq wa cen wbr simpl fundmeng
      ensymg sylc wb dmfi adantr hashen syldan mpbird wrel cv cop wi wal wn
      wne cr clt cn0 hashcl syl nn0re cvv cxp wrex wral wss df-rel dfss3 bitri
      notbii rexnal bitr4i w3a csn cdif cun c1 caddc co dmun fveq2i c0 dmsnn0
      biimpri necon1bi 3ad2ant3 uneq2d un0 syl6eq fveq2d syl5eq diffi peano2re
      cle cdom fidomdm hashdom syl11anc ltp1 lelttrd 3ad2ant1 eqbrtrd cin snfi
      incom disjdif eqtri hashun mp3an23 visset hashsng ax-mp opreq2i syl6req
      breqtrd difsnid dmeqd 3ad2ant2 3brtr3d 3exp r19.23adv syl5bi imp ltne
      syl111anc ex necon4bd wex 2nalexn df-ne anbi2i annim exbii exnal bitr2i
      2exbii c2 cpr 2re readdcl sylancr 1re opex prss undif biimpi syl5reqr
      dmprop dfsn2 eqtr4i uneq1i ad2antrl hashun2 opreq1i syl6breq 1lt2 ltadd1
      mp3an12 mpbii leadd2 mp3an3 mpbid prfi mp3an13 opth2 necon3i hashpr
      sylib opreq1d ad2antll 3eqtr3rd ltletrd 19.23adv 19.23advv dffun4
      sylanbrc impbida ) AEFZAGZAHIZAJZHIZKZUVNUVOLZUVSAUVQMNZUVTUVNUVQAMNUWAUV
      NUVOOAEPUVQAEQRUVNUVOUVQEFZUVSUWASUVNUWBUVOATZUAAUVQUBUCUDUVNUVSLAUEZBUFZ
      CUFZUGZAFUWEDUFZUGZAFLZUWFUWHKZUHZDUIZCUIBUIZUVOUVNUVSUWDUVNUWDUVPUVRUVNU
      WDUJZUVPUVRUKZUVNUWOLUVRULFZUVPULFZUVRUVPUMNZUWPUVNUWQUWOUVNUVRUNFZUWQUVN
      UWBUWTUWCUVQUOUPUVRUQUPZUAUVNUWRUWOUVNUVPUNFUWRAUOUVPUQUPZUAUVNUWOUWSUWOU
      WEURURUSZFZUJZBAUTZUVNUWSUWOUXDBAVAZUJUXFUWDUXGUWDAUXCVBUXGAVCBAUXCVDVEVF
      UXDBAVGVHUVNUXEUWSBAUVNUWEAFZUXEUWSUVNUXHUXEVIZAUWEVJZVKZUXJVLZJZHIZUXLHI
      ZUVRUVPUMUXIUXNUXKHIZVMVNVOZUXOUMUXIUXNUXKJZHIZUXQUMUXIUXNUXRUXJJZVLZHIUX
      SUXMUYAHUXKUXJVPVQUXIUYAUXRHUXIUYAUXRVRVLUXRUXIUXTVRUXRUXEUVNUXTVRKUXHUXD
      UXTVRUXDUXTVRUKUWEVSVTWAWBWCUXRWDWEWFWGUVNUXHUXSUXQUMNUXEUVNUXSUXPUXQUVNU
      XSUNFZUXSULFUVNUXREFZUYBUVNUXKEFZUYCAUXJWHZUXKTUPZUXRUOUPUXSUQUPUVNUXPUNF
      ZUXPULFZUVNUYDUYGUYEUXKUOUPUXPUQUPZUVNUYHUXQULFUYIUXPWIUPUVNUXSUXPWJNZUXR
      UXKWKNZUVNUYDUYKUYEUXKWLUPUVNUYCUYDUYJUYKSUYFUYEUXRUXKWMWNUDUVNUYHUXPUXQU
      MNUYIUXPWOUPWPWQWRUVNUXHUXQUXOKUXEUVNUXOUXPUXJHIZVNVOZUXQUVNUYDUXOUYMKZUY
      EUYDUXJEFZUXKUXJWSZVRKUYNUWEWTZUYPUXJUXKWSVRUXKUXJXAUXJAXBXCUXKUXJXDXEUPU
      YLVMUXPVNUWEURFUYLVMKBXFUWEURXGXHZXIXJWQXKUXHUVNUXNUVRKUXEUXHUXMUVQHUXHUX
      LAAUWEXLZXMWFXNUXHUVNUXOUVPKUXEUXHUXLAHUYSWFXNXOXPXQXRXSUVRUVPXTZYAYBYCXS
      UVNUVSUWNUVNUWNUVPUVRUWNUJZUWJUWFUWHUKZLZDYDZCYDBYDZUVNUWPVUAUWMUJZCYDBYD
      VUEUWMBCYEVUFVUDBCVUDUWLUJZDYDVUFVUCVUGDVUCUWJUWKUJZLVUGVUBVUHUWJUWFUWHYF
      YGUWJUWKYHVEYIUWLDYJYKYLVEUVNVUDUWPBCUVNVUCUWPDUVNVUCUWPUVNVUCLZUWQUWRUWS
      UWPUVNUWQVUCUXAUAZUVNUWRVUCUXBUAZVUIUVRYMAUWGUWIYNZVKZJZHIZVNVOZUVPVUJVUI
      YMULFZVUOULFZVUPULFZYOUVNVURVUCUVNVUOUNFZVURUVNVUNEFZVUTUVNVUMEFZVVAAVULW
      HZVUMTUPZVUNUOUPVUOUQUPZUAYMVUOYPZYQVUKVUIUVRVMVUOVNVOZVUPVUJUVNVVGULFZVU
      CUVNVMULFZVURVVHYRVVEVMVUOYPYQUAUVNVUSVUCUVNVUQVURVUSYOVVEVVFYQUAVUIUVRUX
      JVUNVLZHIZVVGWJUWJUVRVVKKUVNVUBUWJUVQVVJHUWJUVQVULJZVUNVLZVVJUWJVVMVULVUM
      VLZJUVQVULVUMVPUWJVVNAUWJVVNAKZUWJVULAVBVVOUWGUWIAUWEUWFYSZUWEUWHYSZYTVUL
      AUUAVEUUBZXMUUCVVLUXJVUNVVLUWEUWEYNUXJUWEUWEUWFUWHUUDUWEUUEUUFUUGWEWFUUHU
      VNVVKVVGWJNVUCUVNVVKUYLVUOVNVOZVVGWJUVNUYOVVAVVKVVSWJNUYQVVDUXJVUNUUIYQUY
      LVMVUOVNUYRUUJUUKUAWRUVNVVGVUPUMNZVUCUVNVMYMUMNZVVTUULUVNVURVWAVVTSZVVEVV
      IVUQVURVWBYRYOVMYMVUOUUMUUNUPUUOUAWPVUIVUPYMVUMHIZVNVOZUVPWJUVNVUPVWDWJNZ
      VUCUVNVUOVWCWJNZVWEUVNVWFVUNVUMWKNZUVNVVBVWGVVCVUMWLUPUVNVVAVVBVWFVWGSVVD
      VVCVUNVUMWMWNUDUVNVURVWCULFZVWFVWESZVVEUVNVWCUNFZVWHUVNVVBVWJVVCVUMUOUPVW
      CUQUPVURVWHVUQVWIYOVUOVWCYMUUPUUQWNUURUAVUIVVNHIZVULHIZVWCVNVOZUVPVWDUVNV
      WKVWMKZVUCUVNVVBVWNVVCVULEFVVBVULVUMWSVRKVWNUWGUWIUUSVULAXBVULVUMXDUUTUPU
      AUWJVWKUVPKUVNVUBUWJVVNAHVVRWFUUHVUBVWMVWDKUVNUWJVUBVWLYMVWCVNVUBUWGUWIUK
      VWLYMKUWGUWIUWFUWHUWEUWFUWEUWHCXFDXFUVAUVBUWGUWIVVPVVQUVCUVDUVEUVFUVGXKUV
      HUYTYAYBUVIUVJXRYCXSBCDAUVKUVLUVM $.
      $( [26-Sep-2013] $)
  $}


