$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               The ` # ` (finite set size) function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c # $.

  $( Extend the definition of a class to include the size function. $)
  chash $a class # $.

  ${
    $d x y $.
    $( Define the ` # ` function, which gives the cardinality of a finite set
       as a member of ` NN0 ` . $)
    df-hash $a |- # = ( ( rec (
                   ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) o. card ) $.
  $}

  ${
    $d A x y $.
    hashgval.1 $e |- G = ( rec ( ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) $.
    $( The value of the ` # ` function in terms of the mapping ` G ` from
       ` om ` to ` NN0 ` .  The proof avoids the use of ~ ax-ac .  (Contributed
       by Paul Chapman, 22-Jun-2011.)  (Revised by Mario Carneiro,
       12-Feb-2013.) $)
    hashgval $p |- ( A e. Fin -> ( G ` ( card ` A ) ) = ( # ` A ) ) $=
      ( vy cfn wcel chash cfv ccrd ccom cv cc0 com wceq cen wbr con0 wrex wfun
      cvv c1 caddc co cmpt crdg cres df-hash coeq1i eqtr4i fveq1i cdm cab breq2
      finnum rexbidv elabg mpbird cardf2 fdmi syl6eleqr wfn rdgfnon funres mp2b
      fnfun funeqi mpbir wf ffun ax-mp fvco mp3an12 syl syl5req ) BFGZBHIBCJKZI
      ZBJICIZBHVQHAUAALZUBUCUDUEZMUFZNUGZJKVQAUHCWCJDUIUJUKVPBJULZGZVRVSOZVPBEL
      ZVTPQZERSZAUMZWDVPBWJGWGBPQZERSZEBUOWIWLABFVTBOWHWKERVTBWGPUNUPUQURWJRJAE
      USZUTVACTZJTZWEWFWNWCTZWBRVBWBTWPMWAVCRWBVFNWBVDVECWCDVGVHWJRJVIWOWMWJRJV
      JVKBCJVLVMVNVO $.
      $( [13-Sep-2013] $) $( [22-Jun-2011] $)

    $( ` ``' G ` maps the size function's value to ` card ` .  (Contributed by
       Paul Chapman, 22-Jun-2011.) $)
    hashginv $p |- ( A e. Fin -> ( `' G ` ( # ` A ) ) = ( card ` A ) ) $=
      ( cfn wcel ccrd cfv chash wceq ccnv hashgval com wi ficardom cn0 hashgf1o
      wf1o f1ocnvfv mpan syl mpd ) BEFZBGHZCHBIHZJZUECKHUDJZABCDLUCUDMFZUFUGNZB
      OMPCRUHUIACDQMPUDUECSTUAUB $.
      $( [15-Sep-2013] $) $( [22-Jun-2011] $)
  $}

  ${
    $d x y z $.
    $( The size function maps all finite sets to their cardinality, as members
       of ` NN0 ` . $)
    hashf $p |- # : Fin --> NN0 $=
      ( vx vy vz cfn cn0 chash wf wfn cfv wcel cc0 com ccrd wceq ax-mp cen con0
      cv wbr wrex wral ffnfv cvv c1 caddc co cmpt crdg cres ccom wfun cdm df-fn
      frfnom fnfun cab cardf2 ffun funco mp2an ccnv cima dmco imaeq2i vex brcnv
      fndm rexbii elima ficardom finnum abid sylibr fdmi syl6eleqr wa eqid fvex
      funbrfvb mpbii sylancr rcla4ev syl2anc wi funbrfv breldm syl6eleq cardid2
      breq2 sylib syl eqbrtrrd ensym sylan2 isfi rexlimiva impbii 3bitr4i eqriv
      eqtri mpbir2an fneq1i mpbir hashgval wf1o hashgf1o f1of ffvelrni eqeltrrd
      df-hash rgen ) DEFGFDHZARZFIZEJZADUAADEFUBXLAUCXMUDUEUFUGZKUHLUIZMUJZDHZX
      SXRUKZXRULZDNXRDUMXQUKZMUKZXTXQLHZYBKXPUNZLXQUOOBRZXMPSBQTZAUPZQMGYCABUQZ
      YHQMUROZXQMUSUTYAMVAZXQULZVBZDXQMVCYMYKLVBZDYLLYKYDYLLNYELXQVGOVDAYNDCRZX
      MYKSZCLTXMYOMSZCLTZXMYNJXMDJZYPYQCLYOXMMCVEZAVEZVFVHCXMYKLUUAVIYSYRYSXMMI
      ZLJZXMUUBMSZYRXMVJZYSYCXMMULZJZUUDYJYSXMYHUUFYSYGXMYHJZBXMVKYGAVLZVMYHQMY
      IVNZVOYCUUGVPUUBUUBNUUDUUBVQXMUUBMXMMVRVSVTWAYQUUDCUUBLYOUUBXMMWIWBWCYQYS
      CLYOLJZYQVPXMYFPSZBLTZYSYQUUKXMYOPSZUUMYQYOXMPSUUNYQUUBYOXMPYCYQUUBYONWDY
      JXMYOMYTWEOYQYGUUBXMPSYQUUHYGYQXMUUFYHXMYOMUUAWFUUJWGUUIWJBXMWHWKWLYOXMUU
      AWMWKUULUUNBYOLYFYOXMPWIWBWNBXMWOVMWPWQWRWSWTWTXADFXRAXJXBXCXOADYSUUBBUCY
      FUDUEUFUGKUHLUIZIZXNEBXMUUOUUOVQZXDYSUUCUUPEJUUELEUUBUUOLEUUOXELEUUOGBUUO
      UUQXFLEUUOXGOXHWKXIXKXA $.
      $( [13-Sep-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d x N $.
    $( The set ` ( 1 ... N ) ` has ` N ` elements.  (Contributed by Paul
       Chapman, 22-Jun-2011.) $)
    hashfz1 $p |- ( N e. NN0 -> ( # ` ( 1 ... N ) ) = N ) $=
      ( vx cn0 wcel c1 cfz co chash cfv ccrd cvv cv caddc cmpt cc0 crdg com cfn
      cres wceq fzfi eqid hashgval ax-mp ccnv cardfz fveq2d wf1o f1ocnvfv2 mpan
      hashgf1o eqtrd syl5eqr ) ACDZEAFGZHIZUOJIZBKBLEMGNOPQSZIZAUORDUSUPTEAUABU
      OURURUBZUCUDUNUSAURUEIZURIZAUNUQVAURBURAUTUFUGQCURUHUNVBATBURUTUKQCAURUIU
      JULUM $.
      $( [15-Sep-2013] $) $( [22-Jun-2011] $)

    $( Two finite sets have the same number of elements iff they are
       equinumerous.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    hashen $p |- ( ( A e. Fin /\ B e. Fin ) ->
                   ( ( # ` A ) = ( # ` B ) <-> A ~~ B ) ) $=
      ( vx cfn wcel chash cfv wceq ccrd cen wbr fveq2 hashginv eqeqan12d syl5ib
      wa hashgval impbid wi con0 cvv cv c1 caddc co cmpt cc0 crdg com cres ccnv
      eqid wrex finnum cardid2 syl ensymg mpd breq1 anbi2d biimprcd entr syl2an
      syl6 carden2b adantl bitrd ) ADEZBDEZPZAFGZBFGZHZAIGZBIGZHZABJKZVJVMVPVMV
      KCUACUBZUCUDUEUFUGUHUIUJZUKZGZVLVTGZHVJVPVKVLVTLVHVIWAVNWBVOCAVSVSULZMCBV
      SWCMNOVPVNVSGZVOVSGZHVJVMVNVOVSLVHVIWDVKWEVLCAVSWCQCBVSWCQNORVJVPVQVHAVNJ
      KZVOBJKZVPVQSVIVHVNAJKZWFVHVRAJKCTUMWHCAUNCAUOUPVNADUQURVIVRBJKCTUMWGCBUN
      CBUOUPWFWGPZVPWFVNBJKZPZVQVPWKWIVPWJWGWFVNVOBJUSUTVAAVNBVBVDVCVIVQVPSVHAB
      DVEVFRVG $.
      $( [15-Sep-2013] $) $( [22-Jun-2011] $)
  $}

  ${
    $d x A $.
    $( The size function of the cardinality function. $)
    hashcard $p |- ( A e. Fin -> ( # ` ( card ` A ) ) = ( # ` A ) ) $=
      ( vx cfn wcel ccrd cfv cvv cv c1 caddc co cmpt cc0 crdg com chash cardidm
      cres wceq hashgval fveq2i a1i wss ficardom ssid sylancl eqid syl 3eqtr3d
      ssnnfi ) ACDZAEFZEFZBGBHIJKLMNORZFZULUNFZULPFZAPFUOUPSUKUMULUNAQUAUBUKULC
      DZUOUQSUKULODULULUCURAUDULUEULULUJUFBULUNUNUGZTUHBAUNUSTUI $.
      $( [4-Nov-2013] $) $( [19-Sep-2013] $)
  $}

  $( Closure of the ` # ` function.  (Contributed by Paul Chapman,
     26-Oct-2012.)  (Revised by Mario Carneiro, 15-Sep-2013.) $)
  hashcl $p |- ( A e. Fin -> ( # ` A ) e. NN0 ) $=
    ( cfn cn0 chash hashf ffvelrni ) BCADEF $.
    $( [15-Sep-2013] $) $( [26-Oct-2012] $)

  $( Two ways of saying a finite set is empty.  (Contributed by Paul Chapman,
     26-Oct-2012.) $)
  hash0 $p |- ( A e. Fin -> ( ( # ` A ) = 0 <-> A = (/) ) ) $=
    ( chash cfv cc0 wceq c0 cfn wcel c1 cfz co eqid cn0 0nn0 ax-mp mpbir fveq2i
    wb fz1n hashfz1 eqtr3i eqeq2i cen wbr 0fin hashen mpan2 en0 syl6bb syl5bbr
    ) ABCZDEUKFBCZEZAGHZAFEZULDUKIDJKZBCZULDUPFBUPFEZDDEZDLDMHZURUSRNDSOPQUTUQD
    ENDTOUAUBUNUMAFUCUDZUOUNFGHUMVARUEAFUFUGAUHUIUJ $.
    $( [26-Oct-2012] $)

  $( The size of a singleton.  (Contributed by Paul Chapman, 26-Oct-2012.)
     (The proof was shortened by Mario Carneiro, 13-Feb-2013.) $)
  hashsng $p |- ( A e. V -> ( # ` { A } ) = 1 ) $=
    ( wcel csn chash cfv c1 cen wbr wceq cz 1z en2sn mpan2 wb snfi hashen mp2an
    cfn ax-mp sylibr cfz co cn0 1nn0 hashfz1 fzsn fveq2d syl5reqr syl6eq ) ABCZ
    ADZEFZGDZEFZGUKULUNHIZUMUOJZUKGKCZUPLAGBKMNULSCUNSCUQUPOAPGPULUNQRUAURUOGJL
    URGGGUBUCZEFZUOGUDCUTGJUEGUFTURUSUNEGUGUHUITUJ $.
    $( [13-Feb-2013] $) $( [26-Oct-2012] $)

  $( The value of the size function on a singleton.  (Contributed by Paul
     Chapman, 26-Oct-2012.) $)
  hashsngOLD $p |- ( A e. V -> ( # ` { A } ) = 1 ) $=
    ( wcel csn c1 cen wbr chash cfv wceq cz 1z en2sn mpan2 cfz co fzsn cfn snfi
    ax-mp fveq2i cn0 1nn0 hashfz1 eqtr3i eqeq2i wb hashen mp2an bitr3i sylibr )
    ABCZADZEDZFGZUMHIZEJZULEKCZUOLAEBKMNUQUPUNHIZJZUOUSEUPEEOPZHIZUSEVAUNHURVAU
    NJLEQTUAEUBCVBEJUCEUDTUEUFUMRCUNRCUTUOUGASESUMUNUHUIUJUK $.
    $( [26-Oct-2012] $)

  ${
    $d F f $.  $d N f $.
    $( The value of the size function on a finite 1-based sequence.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    fseq1hash $p |- ( ( N e. NN0 /\ F Fn ( 1 ... N ) ) -> ( # ` F ) = N ) $=
      ( vf cn0 wcel c1 cfz wfn wa cvv chash cfv wceq adantl wi cen wbr syl cfn
      wb co oprex fnex mpan2 cv fneq1 anbi2d fveq2 eqeq1d imbi12d cdm fnfun vex
      wfun fundmen fndm breq1 mpbid fzfi enfi mpan biimpa sylancl hashen mpbird
      sylancr hashfz1 adantr eqtr3d vtoclg com12 mpd ) BDEZAFBGUAZHZIZAJEZAKLZB
      MZVOVQVMVOVNJEVQFBGUBVNJAUCUDNVQVPVSVMCUEZVNHZIZVTKLZBMZOVPVSOCAJVTAMZWBV
      PWDVSWEWAVOVMVNVTAUFUGWEWCVRBVTAKUHUIUJWBVNKLZWCBWBWFWCMZVNVTPQZWAWHVMWAV
      TUKZVTPQZWHWAVTUNWJVNVTULVTCUMZUORWAWIVNMWJWHTVNVTUPWIVNVTPUQRURNZWBVNSEZ
      VTSEZWGWHTFBUSZWBWHWMWNWLWOWHWMWNVTJEWHWMWNTWKVNVTJUTVAVBVCVNVTVDVFVEVMWF
      BMWABVGVHVIVJVKVL $.
      $( [26-Oct-2012] $)
  $}

  ${
    $d A n z $.  $d B n $.  $d G n z $.
    hashgadd.1 $e |- G = ( rec ( ( x e. _V |-> ( x + 1 ) ) , 0 ) |` om ) $.
    $( ` G ` maps ordinal addition to integer addition.  (Contributed by Paul
       Chapman, 30-Nov-2012.) $)
    hashgadd $p |- ( ( A e. om /\ B e. om ) ->
                     ( G ` ( A +o B ) ) = ( ( G ` A ) + ( G ` B ) ) ) $=
      ( com wcel coa co cfv caddc wceq wi c0 fveq2d opreq2d cc0 cn0 syl c1 csuc
      vn vz cv opreq2 fveq2 eqeq12d imbi2d cc wf1o hashgf1o f1of ax-mp ffvelrni
      wf nn0cn addid1 0z om2uz0i opreq2i a1i con0 oa0 3eqtr4rd w3a oasuc syl2an
      nnon nnacl om2uzsuci eqtrd 3adant3 ax-1cn addass mp3an3 3ad2ant3 3ad2ant2
      wa opreq1 3eqtr4d 3expia expcom a2d finds impcom ) CFGBFGZBCHIZDJZBDJZCDJ
      ZKIZLZWFBUBUDZHIZDJZWIWMDJZKIZLZMWFBNHIZDJZWINDJZKIZLZMWFBUCUDZHIZDJZWIXD
      DJZKIZLZMWFBXDUAZHIZDJZWIXJDJZKIZLZMWFWLMUBUCCWMNLZWRXCWFXPWOWTWQXBXPWNWS
      DWMNBHUEOXPWPXAWIKWMNDUFPUGUHWMXDLZWRXIWFXQWOXFWQXHXQWNXEDWMXDBHUEOXQWPXG
      WIKWMXDDUFPUGUHWMXJLZWRXOWFXRWOXLWQXNXRWNXKDWMXJBHUEOXRWPXMWIKWMXJDUFPUGU
      HWMCLZWRWLWFXSWOWHWQWKXSWNWGDWMCBHUEOXSWPWJWIKWMCDUFPUGUHWFWIQKIZWIXBWTWF
      WIUIGZXTWILWFWIRGYAFRBDFRDUJFRDUOADEUKFRDULUMZUNWIUPSZWIUQSXBXTLWFXAQWIKA
      QDUREUSUTVAWFWSBDWFBVBGZWSBLBVHZBVCSOVDXDFGZWFXIXOWFYFXIXOMWFYFXIXOWFYFXI
      VEZXLXFTKIZXNWFYFXLYHLXIWFYFVRZXLXEUAZDJZYHYIXKYJDWFYDXDVBGXKYJLYFYEXDVHB
      XDVFVGOYIXEFGYKYHLBXDVIAXEQDUREVJSVKVLYGXHTKIZWIXGTKIZKIZYHXNWFYFYLYNLZXI
      WFYAXGUIGZYOYFYCYFXGRGYPFRXDDYBUNXGUPSYAYPTUIGYOVMWIXGTVNVOVGVLXIWFYHYLLY
      FXFXHTKVSVPYFWFXNYNLXIYFXMYMWIKAXDQDUREVJPVQVTVKWAWBWCWDWE $.
      $( [15-Sep-2013] $) $( [30-Nov-2012] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Dominance relation for the size function. $)
    hashdom $p |- ( ( A e. Fin /\ B e. Fin )
         -> ( ( # ` A ) <_ ( # ` B ) <-> A ~<_ B ) ) $=
      ( vy vx cfn wcel wa ccrd cfv coa co wceq com wbr chash cle ficardom caddc
      cn0 syl wss cv wrex cdom wb nnawordex syl2an ficarddom c1 cmin fzfi ax-mp
      cfz cvv cmpt cc0 crdg cres ccnv hashgval ad2antrr hashcl ad2antlr nn0sub2
      eqid simpr syl3anc hashfz1 syl5eq opreq12d cc nn0cn pncan3 eqtrd hashgadd
      adantr sylancl 3eqtr4d fveq2d wf1o nnacl f1ocnvfv1 sylancr 3eqtr3d opreq2
      hashgf1o eqeq1d rcla4ev ex cardnn adantl opreq2d fveq2 wi ssid opreqan12d
      ssnnfi mpan2 adantlr eqeq12d nn0ge0 cr nn0re mpbid breq2 syl5ibcom sylbid
      addge01 sylan2 syl5 sylbird rexlimdva impbid 3bitr4rd ) AEFZBEFZGZAHIZBHI
      ZUAZXRCUBZJKZXSLZCMUCZABUDNAOIZBOIZPNZXOXRMFZXSMFZXTYDUEXPAQZBQZCXRXSUFUG
      ABUHXQYGYDXQYGYDXQYGGZUIYFYEUJKZUMKZHIZMFZXRYOJKZXSLZYDYNEFZYPUIYMUKZYNQU
      LZYLYQDUNDUBUIRKUOUPUQMURZIZUUBUSZIZXSUUBIZUUDIZYQXSYLUUCUUFUUDYLXRUUBIZY
      OUUBIZRKZYFUUCUUFYLUUJYEYMRKZYFYLUUHYEUUIYMRXOUUHYELXPYGDAUUBUUBVEZUTZVAY
      LUUIYNOIZYMYSUUIUUNLYTDYNUUBUULUTULYLYMSFZUUNYMLYLYESFZYFSFZYGUUOXOUUPXPY
      GAVBZVAXPUUQXOYGBVBZVCXQYGVFYEYFVDVGYMVHTVIVJXQUUKYFLZYGXOYEVKFZYFVKFZUUT
      XPXOUUPUVAUURYEVLTXPUUQUVBUUSYFVLTYEYFVMUGVPVNYLYHYPUUCUUJLXOYHXPYGYJVAZU
      UADXRYOUUBUULVOVQXPUUFYFLZXOYGDBUUBUULUTZVCVRVSYLMSUUBVTZYQMFZUUEYQLDUUBU
      ULWFZYLYHYPUVGUVCUUAXRYOWAVQMSYQUUBWBWCYLUVFYIUUGXSLUVHXPYIXOYGYKVCMSXSUU
      BWBWCWDYCYRCYOMYAYOLYBYQXSYAYOXRJWEWGWHWCWIXQYCYGCMXQYAMFZGZYCXRYAHIZJKZX
      SLZYGUVJUVLYBXSUVJUVKYAXRJUVIUVKYALXQYAWJWKWLWGUVMUVLUUBIZUUFLZUVJYGUVLXS
      UUBWMUVIXQYAEFZUVOYGWNUVIYAYAUAUVPYAWOYAYAWQWRXQUVPGZUVOYEYAOIZRKZYFLZYGU
      VQUVNUVSUUFYFXOUVPUVNUVSLXPXOUVPGZUVNUUHUVKUUBIZRKZUVSXOYHUVKMFUVNUWCLUVP
      YJYAQDXRUVKUUBUULVOUGXOUVPUUHYEUWBUVRRUUMDYAUUBUULUTWPVNWSXPUVDXOUVPUVEVC
      WTUVQYEUVSPNZUVTYGXOUVPUWDXPUWAUPUVRPNZUWDUVPUWEXOUVPUVRSFZUWEYAVBZUVRXAT
      WKXOYEXBFZUVRXBFZUWEUWDUEUVPXOUUPUWHUURYEXCTUVPUWFUWIUWGUVRXCTYEUVRXHUGXD
      WSUVSYFYEPXEXFXGXIXJXKXLXMXN $.
      $( [14-Feb-2014] $) $( [22-Sep-2013] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The size of the union of disjoint finite sets is the sum of their
       sizes.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
    hashun $p |- ( ( A e. Fin /\ B e. Fin /\ ( A i^i B ) = (/) ) ->
                   ( # ` ( A u. B ) ) = ( ( # ` A ) + ( # ` B ) ) ) $=
      ( vx cfn wcel cin c0 wceq w3a cun ccrd cfv cvv caddc co com chash 3adant3
      hashgval ficardom cv cmpt cc0 crdg cres coa ficardun fveq2d unfi eqid syl
      c1 wa hashgadd syl2an opreqan12d eqtrd 3eqtr3d ) ADEZBDEZABFGHZIZABJZKLZC
      MCUAULNOUBUCUDPUEZLZAKLZBKLZUFOZVELZVCQLZAQLZBQLZNOZVBVDVIVEABUGUHUSUTVFV
      KHZVAUSUTUMZVCDEVOABUICVCVEVEUJZSUKRUSUTVJVNHVAVPVJVGVELZVHVELZNOZVNUSVGP
      EVHPEVJVTHUTATBTCVGVHVEVQUNUOUSUTVRVLVSVMNCAVEVQSCBVEVQSUPUQRUR $.
      $( [15-Sep-2013] $) $( [30-Nov-2012] $)

    $( The size of the union of finite sets is less than or equal to the sum of
       their sizes. $)
    hashun2 $p |- ( ( A e. Fin /\ B e. Fin ) ->
                   ( # ` ( A u. B ) ) <_ ( ( # ` A ) + ( # ` B ) ) ) $=
      ( vx cfn wcel chash cfv ccrd co caddc cle wbr wss com ficardom syl2an syl
      wceq wb hashgval cun coa cdom ficardun2 nnacl cardnn sseqtr4d unfi ssnnfi
      wa ssid sylancl ficarddom syl2anc mpbird hashdom cvv cv c1 cmpt crdg cres
      cc0 fveq2d eqid hashgadd 3eqtr3d opreqan12d eqtrd breqtrd ) ADEZBDEZUJZAB
      UAZFGZAHGZBHGZUBIZFGZAFGZBFGZJIZKVMVOVSKLZVNVRUCLZVMWDVNHGZVRHGZMZVMWEVRW
      FABUDVMVRNEZWFVRRVKVPNEZVQNEZWHVLAOZBOZVPVQUEPZVRUFQZUGVMVNDEZVRDEZWDWGSA
      BUHZVMWHVRVRMWPWMVRUKVRVRUIULZVNVRUMUNUOVMWOWPWCWDSWQWRVNVRUPUNUOVMVSVPCU
      QCURUSJIUTVCVANVBZGZVQWSGZJIZWBVMWFWSGZVRWSGZVSXBVMWFVRWSWNVDVMWPXCVSRWRC
      VRWSWSVEZTQVKWIWJXDXBRVLWKWLCVPVQWSXEVFPVGVKVLWTVTXAWAJCAWSXETCBWSXETVHVI
      VJ $.
      $( [4-Nov-2013] $) $( [23-Sep-2013] $)
  $}

  ${
    hashpr.1 $e |- A e. _V $.
    hashpr.2 $e |- B e. _V $.
    $( The size of an unordered pair. $)
    hashpr $p |- ( A =/= B <-> ( # ` { A , B } ) = 2 ) $=
      ( wne cpr chash cfv c2 wceq c1 caddc csn cfn wcel snfi cvv hashsng ax-mp
      co cun df-pr fveq2i cin disjsn2 hashun mp3an12 syl syl5eq opreq12i syl6eq
      c0 df-2 syl6eqr dfsn2 preq2 syl5req fveq2d eqnetri eqnetrd necon2i impbii
      1ne2 a1i ) ABEZABFZGHZIJVEVGKKLTZIVEVGAMZGHZBMZGHZLTZVHVEVGVIVKUAZGHZVMVF
      VNGABUBUCVEVIVKUDULJZVOVMJZABUEVINOVKNOVPVQAPBPVIVKUFUGUHUIVJKVLKLAQOVJKJ
      CAQRSZBQOVLKJDBQRSUJUKUMUNABVGIABJZVGVJIVSVFVIGVSVIAAFVFAUOABAUPUQURVJIEV
      SVJKIVRVCUSVDUTVAVB $.
      $( [27-Sep-2013] $)
  $}

  $( The size of the union of a finite set with a disjoint singleton is one
     more than the size of the set.  (Contributed by Paul Chapman,
     30-Nov-2012.) $)
  hashunsng $p |- ( B e. V -> ( ( A e. Fin /\ -. B e. A ) ->
                    ( # ` ( A u. { B } ) ) = ( ( # ` A ) + 1 ) ) ) $=
    ( cfn wcel wn wa csn cun chash cfv c1 caddc co wceq cin disjsn snfi hashun
    c0 mp3an2 sylan2br hashsng opreq2d sylan9eq expcom ) ADEZBAEFZGZBCEZABHZIJK
    ZAJKZLMNZOUIUJULUMUKJKZMNZUNUHUGAUKPTOZULUPOZABQUGUKDEUQURBRAUKSUAUBUJUOLUM
    MBCUCUDUEUF $.
    $( [30-Nov-2012] $)

  ${
    $d A x y z $.  $d B x y z $.
    hashxplem.1 $e |- B e. Fin $.
    $( Lemma for ~ hashxp . $)
    hashxplem $p |- ( A e. Fin ->
                      ( # ` ( A X. B ) ) = ( ( # ` A ) x. ( # ` B ) ) ) $=
      ( cxp chash cfv cmul co wceq c0 xpeq1 fveq2d opreq1d eqeq12d cc0 cfn wcel
      fveq2 ax-mp caddc vx vy vz cv csn cun cn0 cc hashcl nn0cn mul02 3syl 0fin
      eqid hash0 biimpar mp2an opreq1i xp0r fveq2i eqtri 3eqtr4ri opreq1 adantl
      wn wa xpundir cin xpfi mpan2 inxp disjsn biimpri syl syl6eq syl5eq hashun
      snfi mp3an2 syl2an cen wbr elexi xpcomen vex xpsnen entri wb hashen mpbir
      snex opreq2i adantr c1 cvv hashunsng ax-1cn adddir mp3an23 mulid2 3eqtr4d
      wi eqtrd ex findcard2s ) UAUDZBDZEFZXFEFZBEFZGHZIJBDZEFZJEFZXJGHZIUBUDZBD
      ZEFZXPEFZXJGHZIZXPUCUDZUEZUFZBDZEFZYDEFZXJGHZIZABDZEFZAEFZXJGHZIUAUBUCAXF
      JIZXHXMXKXOYNXGXLEXFJBKLYNXIXNXJGXFJERMNXFXPIZXHXRXKXTYOXGXQEXFXPBKLYOXIX
      SXJGXFXPERMNXFYDIZXHYFXKYHYPXGYEEXFYDBKLYPXIYGXJGXFYDERMNXFAIZXHYKXKYMYQX
      GYJEXFABKLYQXIYLXJGXFAERMNOXJGHZOXOXMBPQZYROIZCYSXJUGQZXJUHQZYTBUIZXJUJZX
      JUKULSXNOXJGJPQZJJIZXNOIZUMJUNUUEUUGUUFJUOUPUQZURXMXNOXLJEBUSUTUUHVAVBXPP
      QZYBXPQVEZVFZYAYIUUKYAVFXRXJTHZXTXJTHZYFYHYAUULUUMIUUKXRXTXJTVCVDUUKYFUUL
      IYAUUKYFXQYCBDZUFZEFZUULYEUUOEXPYCBVGUTUUKUUPXRUUNEFZTHZUULUUIXQPQZXQUUNV
      HZJIZUUPUURIZUUJUUIYSUUSCXPBVIVJUUJUUTXPYCVHZBBVHZDZJXPBYCBVKUUJUVEJUVDDZ
      JUUJUVCJIZUVEUVFIUVGUUJXPYBVLVMUVCJUVDKVNUVDUSVOVPUUSUUNPQZUVAUVBYCPQYSUV
      HYBVRCYCBVIUQZXQUUNVQVSVTUUQXJXRTUUQXJIZUUNBWAWBZUUNBYCDBYCBYBWKBPCWCZWDB
      YBUVLUCWEZWFWGUVHYSUVJUVKWHUVICUUNBWIUQWJWLVOVPWMUUKYHUUMIYAUUKYHXSWNTHZX
      JGHZUUMUUKYGUVNXJGYBWOQUUKYGUVNIXBUVMXPYBWOWPSMUUIUVOUUMIUUJUUIUVOXTWNXJG
      HZTHZUUMUUIXSUGQXSUHQZUVOUVQIZXPUIXSUJUVRWNUHQUUBUVSWQYSUUBCYSUUAUUBUUCUU
      DVNSZXSWNXJWRWSULUVPXJXTTUUBUVPXJIUVTXJWTSWLVOWMXCWMXAXDXE $.
      $( [30-Nov-2012] $)
  $}

  $( The size of the Cartesian product of two finite sets is the product of
     their sizes.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
  hashxp $p |- ( ( A e. Fin /\ B e. Fin ) ->
                 ( # ` ( A X. B ) ) = ( ( # ` A ) x. ( # ` B ) ) ) $=
    ( cfn wcel cxp chash cfv cmul co wceq wi c0 cif xpeq2 fveq2 opreq2d eqeq12d
    fveq2d imbi2d 0fin elimel hashxplem dedth impcom ) BCDZACDZABEZFGZAFGZBFGZH
    IZJZUEUFULKUFAUEBLMZEZFGZUIUMFGZHIZJZKBLBUMJZULURUFUSUHUOUKUQUSUGUNFBUMANRU
    SUJUPUIHBUMFOPQSAUMBLCTUAUBUCUD $.
    $( [30-Nov-2012] $)

  ${
    $d F a b $.  $d S a b $.  $d Y a b w $.  $d Z a b w $.
    hashpwlem.1 $e |- F = ( w e. ~P Y |-> ( w u. { Z } ) ) $.
    hashpwlem.2 $e |- S = { w | ( w \ Y ) = { Z } } $.
    $( Lemma for ~ hashpw . $)
    hashpwlem $p |- ( -. Z e. Y -> F : ~P Y -1-1-onto-> S ) $=
      ( va vb wcel wceq wral cun unex wa cdif c0 wss biimpi cin wn crn cfv wf1o
      cv wi cpw wfo wrex wfn csn vex snex cmpt copab df-mpt eqtri fnopab2 uneq1
      wf a1i fvmpt adantl difundir elpw ssdif0 bitri disjsn incom eqeq1i bitr3i
      disj3 eqcomd adantr uneq12d uncom un0 syl6eq syl5eq difeq1 eqeq1d eqeltrd
      elab2 sylibr ralrimiva ffnfv inss2 dfin4 difeq2 syl5req sseq1d mpbiri cvv
      sylanbrc difexg ax-mp syl undif1 difss sseq1 mpbii ssequn1 syl5eqr eqtr2d
      sylbi fveq2 eqeq2d rcla4ev rgen dffo3 forn eqeqan12d anbi12i ssel con3rr3
      jca wb syl6ibr anim12d eqtr3 syl6 imp sylan2b unineq sylan2 expcom sylbid
      ralrimivva w3a dff1o6 biimpri mp3an1 syl2anc ) EDJZUAZCUBBKZHUEZCUCZIUEZC
      UCZKZYQYSKZUFZIDUGZLHUUDLZUUDBCUDZYOUUDBCUHZYPYOUUDBCUTZYQYTKZIUUDUIZHBLZ
      UUGYOCUUDUJZYRBJZHUUDLUUHUULYOAHUUDAUEZEUKZMZCUUNUUOAULEUMZNCAUUDUUPUNUUN
      UUDJYQUUPKOAHUOFAHUUDUUPUPUQURZVAYOUUMHUUDYOYQUUDJZOZYRYQUUOMZBUUSYRUVAKY
      OAYQUUPUVAUUDCUUNYQUUOUSFYQUUOHULZUUQNZVBZVCUUTUVADPZUUOKZUVABJUUTUVEYQDP
      ZUUODPZMZUUOYQUUODVDUUTUVIQUUOMZUUOUUTUVGQUVHUUOUUSUVGQKZYOUUSUVKUUSYQDRZ
      UVKYQDUVBVEZYQDVFVGSVCYOUVHUUOKUUSYOUUOUVHYOUUOUVHKZYOUUODTZQKZUVNYODUUOT
      ZQKUVPDEVHUVQUVOQDUUOVIVJVKUUODVLVGSVMVNVOUVJUUOQMUUOQUUOVPUUOVQUQVRVSUUN
      DPZUUOKZUVFAUVABUVCUUNUVAKUVRUVEUUOUUNUVADVTWAGWCWDWBWEHUUDBCWFWNUUKYOUUJ
      HBYQBJZYQUUOPZUUDJZYQUWACUCZKZOZUUJUVTUVGUUOKZUWEUVSUWFAYQBUVBUUNYQKUVRUV
      GUUOUUNYQDVTWAGWCUWFUWBUWDUWFUWADRZUWBUWFUWGYQDTZDRYQDWGUWFUWAUWHDUWFUWHY
      QUVGPUWAYQDWHUVGUUOYQWIWJWKWLUWADYQWMJUWAWMJUVBYQUUOWMWOWPZVEWDZUWFUWCUWA
      UUOMZYQUWFUWBUWCUWKKUWJAUWAUUPUWKUUDCUUNUWAUUOUSFUWAUUOUWIUUQNVBWQUWFUWKU
      VAYQYQUUOWRUWFUUOYQRZUVAYQKUWFUVGYQRUWLYQDWSUVGUUOYQWTXAUWLUVAUUOYQMZYQUU
      OYQVPUWLUWMYQKUUOYQXBSXCWQVSXDXPXEUUIUWDIUWAUUDYSUWAKYTUWCYQYSUWACXFXGXHW
      QXIVAIHUUDBCXJWNUUDBCXKWQYOUUCHIUUDUUDYOUUSYSUUDJZOZOZUUAUVAYSUUOMZKZUUBU
      WOUUAUWRXQYOUUSUWNYRUVAYTUWQUVDAYSUUPUWQUUDCUUNYSUUOUSFYSUUOIULZUUQNVBXLV
      CUWRUWPUUBUWPUWRYQUUOTZYSUUOTZKZUUBUWOYOUVLYSDRZOZUXBUUSUVLUWNUXCUVMYSDUW
      SVEXMYOUXDUXBYOUXDUWTQKZUXAQKZOUXBYOUVLUXEUXCUXFYOUVLEYQJZUAUXEUVLUXGYNYQ
      DEXNXOYQEVHXRYOUXCEYSJZUAUXFUXCUXHYNYSDEXNXOYSEVHXRXSUWTUXAQXTYAYBYCUWRUX
      BOUUBYQYSUUOYDSYEYFYGYHUULYPUUEUUFUURUUFUULYPUUEYIHIUUDBCYJYKYLYM $.
      $( [30-Nov-2012] $)
  $}

  ${
    $d A x y z $.  $d w x y z $.
    $( The size of the power set of a finite set is 2 raised to the power of
       the size of the set.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
    hashpw $p |- ( A e. Fin -> ( # ` ~P A ) = ( 2 ^ ( # ` A ) ) ) $=
      ( vx vw cv cpw chash cfv c2 cexp co wceq c0 cun pweq opreq2d wcel cfn cab
      c1 adantr vy vz csn fveq2d fveq2 eqeq12d cc0 cc 2cn exp0 ax-mp 0fin hash0
      eqid biimpar mp2an opreq2i pw0 fveq2i hashsng eqtri 3eqtr4ri wn wa opreq1
      cmul adantl caddc cdif cen wbr cmpt wf1o hashpwlem vex pwex f1oen wb pwfi
      syl biimpi crn cdom cvv wfn snex unex df-mpt fnopab2 fnrndomg domfi mpan2
      mp2 wfo f1ofo forn eleq1d mpbid hashen syl2anc mpbird hashcl nn0cn times2
      3syl cn0 wss wo ssundif sssn bitri abbii df-pw unab 3eqtr4i ssdif0 uneq1i
      eqtr4i cin ineq1i inab wi snprc con1bii mpbir eqeq1 mtbiri con2i mpbi abf
      imnan hashun mp3an3 syl5eq 3eqtr4rd hashunsng expp1 mpan eqtrd 3eqtr4d ex
      findcard2s ) BDZEZFGZHUUCFGZIJZKLEZFGZHLFGZIJZKUADZEZFGZHUULFGZIJZKZUULUB
      DZUCZMZEZFGZHUUTFGZIJZKZAEZFGZHAFGZIJZKBUAUBAUUCLKZUUEUUIUUGUUKUVJUUDUUHF
      UUCLNUDUVJUUFUUJHIUUCLFUEOUFUUCUULKZUUEUUNUUGUUPUVKUUDUUMFUUCUULNUDUVKUUF
      UUOHIUUCUULFUEOUFUUCUUTKZUUEUVBUUGUVDUVLUUDUVAFUUCUUTNUDUVLUUFUVCHIUUCUUT
      FUEOUFUUCAKZUUEUVGUUGUVIUVMUUDUVFFUUCANUDUVMUUFUVHHIUUCAFUEOUFHUGIJZSUUKU
      UIHUHPZUVNSKUIHUJUKUUJUGHILQPZLLKZUUJUGKZULLUNUVPUVRUVQLUMUOUPUQUUILUCZFG
      ZSUUHUVSFURUSUVPUVTSKULLQUTUKVAVBUULQPZUURUULPVCZVDZUUQUVEUWCUUQVDUUNHVFJ
      ZUUPHVFJZUVBUVDUUQUWDUWEKUWCUUNUUPHVFVEVGUWCUVBUWDKUUQUWCUUNUUNVHJZUUNCDZ
      UULVIZUUSKZCRZFGZVHJZUWDUVBUWCUUNUWKUUNVHUWCUUNUWKKZUUMUWJVJVKZUWBUWNUWAU
      WBUUMUWJCUUMUWGUUSMZVLZVMZUWNCUWJUWPUULUURUWPUNUWJUNVNZUUMUWJUWPUULUAVOVP
      ZVQVTVGUWCUUMQPZUWJQPZUWMUWNVRUWAUWTUWBUWAUWTUULVSWAZTZUWCUWPWBZQPZUXAUWA
      UXEUWBUWAUWTUXEUXBUWTUXDUUMWCVKZUXEUUMWDPUWPUUMWEUXFUWSCBUUMUWOUWPUWGUUSC
      VOUURWFWGCBUUMUWOWHWIUUMWDUWPWJWMUUMUXDWKWLVTTUWBUXEUXAVRUWAUWBUXDUWJQUWB
      UWQUUMUWJUWPWNUXDUWJKUWRUUMUWJUWPWOUUMUWJUWPWPXEWQVGWRZUUMUWJWSWTXAOUWCUW
      TUWDUWFKZUXCUWTUUNXFPUUNUHPUXHUUMXBUUNXCUUNXDXEVTUWCUVBUUMUWJMZFGZUWLUVAU
      XIFUVAUWHLKZCRZUWJMZUXIUWGUUTXGZCRUXKUWIXHZCRUVAUXMUXNUXOCUXNUWHUUSXGUXOU
      WGUULUUSXIUWHUURXJXKXLCUUTXMUXKUWICXNXOUUMUXLUWJUUMUWGUULXGZCRUXLCUULXMUX
      PUXKCUWGUULXPXLVAZXQXRUSUWCUWTUXAUXJUWLKZUXCUXGUWTUXAUUMUWJXSZLKUXRUXSUXL
      UWJXSZLUUMUXLUWJUXQXTUXTUXKUWIVDZCRLUXKUWICYAUYACUXKUWIVCYBUYAVCUWIUXKUWI
      UXKUUSLKZUYBVCUURWDPZUBVOZUYCUYBUURYCYDYEUWHUUSLYFYGYHUXKUWIYKYIYJVAVAUUM
      UWJYLYMWTYNYOTUWCUVDUWEKUUQUWCUVDHUUOSVHJZIJZUWEUWCUVCUYEHIUYCUWCUVCUYEKY
      BUYDUULUURWDYPUKOUWAUYFUWEKZUWBUWAUUOXFPZUYGUULXBUVOUYHUYGUIHUUOYQYRVTTYS
      TYTUUAUUB $.
      $( [30-Nov-2012] $)
  $}

  ${
    $d x y z F $.
    $( A finite set is a function iff it is equinumerous to its domain. $)
    hashfun $p |- ( F e. Fin -> ( Fun F <-> ( # ` F ) = ( # ` dom F ) ) ) $=
      ( vx vy vz cfn wcel chash cfv cdm wceq wbr adantr cr clt syl c1 caddc cle
      c0 c2 wfun wa cen simpl fundmeng ensymg sylc wb dmfi hashen syldan mpbird
      wrel cv cop wi wal wn wne cn0 hashcl nn0re cvv cxp wrex wral df-rel dfss3
      wss bitri notbii rexnal bitr4i w3a csn cdif co dmun fveq2i dmsnn0 biimpri
      cun necon1bi 3ad2ant3 uneq2d un0 syl6eq fveq2d syl5eq diffi peano2re cdom
      fidomdm hashdom syl2anc ltp1 lelttrd 3ad2ant1 eqbrtrd incom disjdif eqtri
      cin snfi hashun mp3an23 vex hashsng ax-mp opreq2i syl6req breqtrd difsnid
      dmeqd 3ad2ant2 3brtr3d 3exp rexlimdv syl5bi imp ltne syl3anc necon4bd wex
      ex 2nalexn df-ne anbi2i annim exbii bitr2i 2exbii cpr 2re readdcl sylancr
      exnal 1re opex ad2antrl undif biimpi syl5reqr dmprop dfsn2 eqtr4i hashun2
      prss uneq1i opreq1i syl6breq 1lt2 ltadd1 mp3an12 mpbii leadd2 mp3an3 prfi
      mpbid mp3an13 opth2 necon3i hashpr sylib opreq1d ad2antll ltletrd exlimdv
      3eqtr3rd exlimdvv dffun4 sylanbrc impbida ) AEFZAUAZAGHZAIZGHZJZUVNUVOUBZ
      UVSAUVQUCKZUVTUVNUVQAUCKUWAUVNUVOUDAEUEUVQAEUFUGUVNUVOUVQEFZUVSUWAUHUVNUW
      BUVOAUIZLAUVQUJUKULUVNUVSUBAUMZBUNZCUNZUOZAFUWEDUNZUOZAFUBZUWFUWHJZUPZDUQ
      ZCUQBUQZUVOUVNUVSUWDUVNUWDUVPUVRUVNUWDURZUVPUVRUSZUVNUWOUBUVRMFZUVPMFZUVR
      UVPNKZUWPUVNUWQUWOUVNUVRUTFZUWQUVNUWBUWTUWCUVQVAOUVRVBOZLUVNUWRUWOUVNUVPU
      TFUWRAVAUVPVBOZLUVNUWOUWSUWOUWEVCVCVDZFZURZBAVEZUVNUWSUWOUXDBAVFZURUXFUWD
      UXGUWDAUXCVIUXGAVGBAUXCVHVJVKUXDBAVLVMUVNUXEUWSBAUVNUWEAFZUXEUWSUVNUXHUXE
      VNZAUWEVOZVPZUXJWBZIZGHZUXLGHZUVRUVPNUXIUXNUXKGHZPQVQZUXONUXIUXNUXKIZGHZU
      XQNUXIUXNUXRUXJIZWBZGHUXSUXMUYAGUXKUXJVRVSUXIUYAUXRGUXIUYAUXRSWBUXRUXIUXT
      SUXRUXEUVNUXTSJUXHUXDUXTSUXDUXTSUSUWEVTWAWCWDWEUXRWFWGWHWIUVNUXHUXSUXQNKU
      XEUVNUXSUXPUXQUVNUXSUTFZUXSMFUVNUXREFZUYBUVNUXKEFZUYCAUXJWJZUXKUIOZUXRVAO
      UXSVBOUVNUXPUTFZUXPMFZUVNUYDUYGUYEUXKVAOUXPVBOZUVNUYHUXQMFUYIUXPWKOUVNUXS
      UXPRKZUXRUXKWLKZUVNUYDUYKUYEUXKWMOUVNUYCUYDUYJUYKUHUYFUYEUXRUXKWNWOULUVNU
      YHUXPUXQNKUYIUXPWPOWQWRWSUVNUXHUXQUXOJUXEUVNUXOUXPUXJGHZQVQZUXQUVNUYDUXOU
      YMJZUYEUYDUXJEFZUXKUXJXCZSJUYNUWEXDZUYPUXJUXKXCSUXKUXJWTUXJAXAXBUXKUXJXEX
      FOUYLPUXPQUWEVCFUYLPJBXGUWEVCXHXIZXJXKWRXLUXHUVNUXNUVRJUXEUXHUXMUVQGUXHUX
      LAAUWEXMZXNWHXOUXHUVNUXOUVPJUXEUXHUXLAGUYSWHXOXPXQXRXSXTUVRUVPYAZYBYEYCXT
      UVNUVSUWNUVNUWNUVPUVRUWNURZUWJUWFUWHUSZUBZDYDZCYDBYDZUVNUWPVUAUWMURZCYDBY
      DVUEUWMBCYFVUFVUDBCVUDUWLURZDYDVUFVUCVUGDVUCUWJUWKURZUBVUGVUBVUHUWJUWFUWH
      YGYHUWJUWKYIVJYJUWLDYQYKYLVJUVNVUDUWPBCUVNVUCUWPDUVNVUCUWPUVNVUCUBZUWQUWR
      UWSUWPUVNUWQVUCUXALZUVNUWRVUCUXBLZVUIUVRTAUWGUWIYMZVPZIZGHZQVQZUVPVUJVUIT
      MFZVUOMFZVUPMFZYNUVNVURVUCUVNVUOUTFZVURUVNVUNEFZVUTUVNVUMEFZVVAAVULWJZVUM
      UIOZVUNVAOVUOVBOZLTVUOYOZYPVUKVUIUVRPVUOQVQZVUPVUJUVNVVGMFZVUCUVNPMFZVURV
      VHYRVVEPVUOYOYPLUVNVUSVUCUVNVUQVURVUSYNVVEVVFYPLVUIUVRUXJVUNWBZGHZVVGRUWJ
      UVRVVKJUVNVUBUWJUVQVVJGUWJUVQVULIZVUNWBZVVJUWJVVMVULVUMWBZIUVQVULVUMVRUWJ
      VVNAUWJVVNAJZUWJVULAVIVVOUWGUWIAUWEUWFYSZUWEUWHYSZUUHVULAUUAVJUUBZXNUUCVV
      LUXJVUNVVLUWEUWEYMUXJUWEUWEUWFUWHUUDUWEUUEUUFUUIWGWHYTUVNVVKVVGRKVUCUVNVV
      KUYLVUOQVQZVVGRUVNUYOVVAVVKVVSRKUYQVVDUXJVUNUUGYPUYLPVUOQUYRUUJUUKLWSUVNV
      VGVUPNKZVUCUVNPTNKZVVTUULUVNVURVWAVVTUHZVVEVVIVUQVURVWBYRYNPTVUOUUMUUNOUU
      OLWQVUIVUPTVUMGHZQVQZUVPRUVNVUPVWDRKZVUCUVNVUOVWCRKZVWEUVNVWFVUNVUMWLKZUV
      NVVBVWGVVCVUMWMOUVNVVAVVBVWFVWGUHVVDVVCVUNVUMWNWOULUVNVURVWCMFZVWFVWEUHZV
      VEUVNVWCUTFZVWHUVNVVBVWJVVCVUMVAOVWCVBOVURVWHVUQVWIYNVUOVWCTUUPUUQWOUUSLV
      UIVVNGHZVULGHZVWCQVQZUVPVWDUVNVWKVWMJZVUCUVNVVBVWNVVCVULEFVVBVULVUMXCSJVW
      NUWGUWIUURVULAXAVULVUMXEUUTOLUWJVWKUVPJUVNVUBUWJVVNAGVVRWHYTVUBVWMVWDJUVN
      UWJVUBVWLTVWCQVUBUWGUWIUSVWLTJUWGUWIUWFUWHUWEUWFUWEUWHCXGDXGUVAUVBUWGUWIV
      VPVVQUVCUVDUVEUVFUVIXLUVGUYTYBYEUVHUVJXSYCXTBCDAUVKUVLUVM $.
      $( [26-Sep-2013] $)
  $}


