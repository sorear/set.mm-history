$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Completeness Axiom and Suprema
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w x y z v S $.  $d w y A $.
    $( If a set of reals contains a lower bound, it contains a unique lower
       bound. $)
    lbreu $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y ) ->
                E! x e. S A. y e. S x <_ y ) $=
      ( vw cr wss cv cle wbr wral wrex wa wceq wi wreu wcel breq2 rcla4v
      im2anan9r wb ssel anim12d impcom letri3 syl exbiri com23 syld com3r
      r19.21aivv anim2i ancoms breq1 ralbidv reu4 sylibr ) CEFZAGZBGZHIZBCJZACK
      ZLVBVADGZUSHIZBCJZLZURVCMZNZDCJACJZLZVAACOVBUQVJUQVIVBUQVHADCCURCPZVCCPZL
      ZVFUQVGVMVFURVCHIZVCURHIZLZUQVGNVLVAVNVKVEVOUTVNBVCCUSVCURHQRVDVOBURCUSUR
      VCHQRSVMUQVPVGVMUQVGVPVMUQLUREPZVCEPZLZVGVPTUQVMVSUQVKVQVLVRCEURUACEVCUAU
      BUCURVCUDUEUFUGUHUIUJUKULVAVEADCVGUTVDBCURVCUSHUMUNUOUP $.
      $( [9-Oct-2005] $)

    $( If a set of reals contains a lower bound, it contains a unique lower
       bound that belongs to the set. $)
    lbcl $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y ) ->
                U. { x e. S | A. y e. S x <_ y } e. S ) $=
      ( cr wss cv cle wbr wral wrex wa wreu crab cuni wcel lbreu reucl2 ssrab2
      sseli 3syl ) CDEAFBFGHBCIZACJKUAACLUAACMZNZUBOUCCOABCPUAACQUBCUCUAACRST
      $.
      $( [9-Oct-2005] $)

    $( If a set of reals contains a lower bound, the lower bound is less than
       or equal to all members of the set. $)
    lble $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y /\ A e. S ) ->
                U. { x e. S | A. y e. S x <_ y } <_ A ) $=
      ( vw wcel cr wss cv cle wbr wral wrex crab cuni wa hbra1 ax-17 hbrab
      hbuni hbbr breq2 rcla4 imp wreu lbreu wceq breq1 ralbidv cbvreuv sylib
      hbeleq ralbid reuuni3 syl sylan2 3impb 3coml ) CDFZDGHZAIZBIZJKZBDLZADMZV
      DADNZOZCJKZUSUTVEVHUSVGVBJKZBDLZVHUTVEPZUSVJVHVIVHBCDBEVGCJBEVFVDBAEDVCBD
      QEIZDFBRSTZVLJFBRVLCFBRUAVBCVGJUBUCUDVKVLVBJKZBDLZEDUEZVJVKVDADUEVPABDUFV
      DVOAEDVAVLUGVCVNBDVAVLVBJUHUIUJUKVOVDVJEADVLVAUGVNVCBDVLVAVBJUHUIVLVGUGVN
      VIBDBEVGVMULVLVGVBJUHUMUNUOUPUQUR $.
      $( [9-Oct-2005] $)

    $( If a set of reals contains a lower bound, the lower bound is its
       infimum. $)
    lbinfm $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y ) ->
                 sup ( S , RR , `' < ) = U. { x e. S | A. y e. S x <_ y } ) $=
      ( vv vz vw cr wss cv cle wbr wral wrex wa clt ccnv wn wi crab cuni csup
      wceq wcel wreu wb breq1 notbid ralbidv breq2 imbi1d anbi12d reuuni2 lbcl
      ssel adantr mpd lble 3expa lenlt ssel2 syldan adantlr sylanc mpbid cvv
      reex ssex rabexg syl uniexg visset brcnvg mpan2 3syl ad2antrr mpbird
      r19.21aiva a1d ancrd rcla4ev syl6 r19.21aiv syl12anc wor ltso cnvso mpbi
      supeu mpbi2and df-sup syl5eq ) CGHZAIBIJKBCLZACMZNZDIZEIZOPZKZQZECLZWQWPW
      RKZWQFIZWRKZFCMZRZEGLZNZDGSTZWMACSZTZCGWRUAWOXKWQWRKZQZECLZWQXKWRKZXERZEG
      LZXIXKUBZXKGUCZXHDGUDZXNXQNZXRUEWOXHYADGXKWPXKUBZXAXNXGXQYBWTXMECYBWSXLWP
      XKWQWRUFUGUHYBXFXPEGYBXBXOXEWPXKWQWRUIUJUHUKZULWOXKCUCZXSABCUMZWLYDXSRWNC
      GXKUNUOUPZWOXHDGMZXTWOXSXNXQYGYFWOXMECWOWQCUCZNZXMWQXKOKZQZYIXKWQJKZYKWLW
      NYHYLABWQCUQURXSWQGUCZYLYKUEYIXKWQUSWOXSYHWLWNYDXSYECGXKUTVAUOWLYHYMWNCGW
      QUTVBVCVDWLXMYKUEWNYHWLXLYJWLXJVEUCZXKVEUCZXLYJUEZWLCVEUCYNCGVFVGWMACVEVH
      VIXJVEVJYOWQVEUCYPEVKXKWQVEVEOVLVMVNUGVOVPVQZWOXPEGWOXPYMWOXOYDXONXEWOXOY
      DWOYDXOYEVRVSXDXOFXKCXCXKWQWRUIVTWAVRWBZXHYADXKGYCVTWCDEFGCWRGOWDGWRWDWEG
      OWFWGWHVIVCYQYRWIDEFGCWRWJWK $.
      $( [9-Oct-2005] $)

    $( If a set of reals contains a lower bound, it contains its infimum. $)
    lbinfmcl $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y ) ->
                 sup ( S , RR , `' < ) e. S ) $=
      ( cr wss cv cle wbr wral wrex wa clt ccnv csup crab cuni lbinfm lbcl
      eqeltrd ) CDEAFBFGHBCIZACJKCDLMNTACOPCABCQABCRS $.
      $( [11-Oct-2005] $)

    $( If a set of reals contains a lower bound, its infmimum is less than or
       equal to all members of the set. $)
    lbinfmle $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y /\ A e. S ) ->
                sup ( S , RR , `' < ) <_ A ) $=
      ( cr wss cv cle wbr wral wrex wcel w3a clt ccnv csup crab cuni wceq
      lbinfm 3adant3 lble eqbrtrd ) DEFZAGBGHIBDJZADKZCDLZMDENOPZUEADQRZCHUDUFU
      HUISUGABDTUAABCDUBUC $.
      $( [11-Oct-2005] $)
  $}

  ${
    $d x y z w v u t A $.
    $( A non-empty, bounded-above set of reals has a supremum.  Stronger
       version of completeness axiom (it has a slightly weaker antecedent). $)
    sup2 $p |- ( ( A C_ RR /\ A =/= (/) /\
                  E. x e. RR A. y e. A ( y < x \/ y = x ) ) ->
               E. x e. RR ( A. y e. A -. x < y /\
                  A. y e. RR ( y < x -> E. z e. A y < z ) ) ) $=
      ( cr wss c0 wne cv clt wbr wceq wo wral wrex w3a wn wi wa wcel wex c1
      caddc co peano2re adantr a1i wal ssel axlttrn 3expb ancli sylan2 ltp1
      sylan2i exp4b com34 pm2.43d imp breq1 syl5cbir adantl jaod ex syl6 com23
      a2d alimdv df-ral 3imtr4g expimpd jcad oprex eleq1 breq2 ralbidv anbi12d
      cla4ev 19.23adv cbvexv syl6ib df-rex imdistani df-3an 3imtr4i axsup syl
      ) DEFZDGHZBIZAIZJKZWJWKLZMZBDNZAEOZPZWHWIWLBDNZAEOZPZWKWJJKQBDNWLWJCIZJKZ
      CDORBENSAEOWHWISZWPSXCWSSWQWTXCWPWSWHWPWSRWIWHWKETZWOSZAUAZXDWRSZAUAZWPWS
      WHXFXAETZXBBDNZSZCUAZXHWHXEXLAWHXEWKUBUCUDZETZWJXMJKZBDNZSZXLWHXEXNXPXEXN
      RWHXDXNWOWKUEZUFUGWHXDWOXPWHXDSZWJDTZWNRZBUHXTXORZBUHWOXPXSYAYBBXSXTWNXOW
      HXDXTWNXORZRWHXTXDYCWHXTWJETZXDYCRDEWJUIYDXDYCYDXDSZWLXOWMYDXDWLXORZYDXDY
      FYDXDWLXDXOYDXDWLXDXOYEWLWKXMJKZXOXDYDXDXNSWLYGSXORZXDYDXDXNYHWJWKXMUJUKX
      DXNXRULUMWKUNZUOUPUQURUSXDWMXORYDWMXOYGXDWJWKXMJUTYIVAVBVCVDVEVFUSVGVHWNB
      DVIXOBDVIVJVKVLXKXQCXMWKUBUCVMXAXMLZXIXNXJXPXAXMEVNYJXBXOBDXAXMWJJVOVPVQV
      RVEVSXKXGCAXAWKLZXIXDXJWRXAWKEVNYKXBWLBDXAWKWJJVOVPVQVTWAWOAEWBWRAEWBVJUF
      WCWHWIWPWDWHWIWSWDWEABCDWFWG $.
      $( [19-Jan-1997] $)

    $( A version of the completeness axiom for reals. $)
    sup3 $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
                  E. x e. RR ( A. y e. A -. x < y /\
                  A. y e. RR ( y < x -> E. z e. A y < z ) ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a clt wceq wo wn wi wa wcel wb
      ssel leloe expcom syl9 imp31 ralbidva rexbidva anbi2d pm5.32i 3anass
      3bitr4i sup2 sylbi ) DEFZDGHZBIZAIZJKZBDLZAEMZNZUPUQURUSOKZURUSPQZBDLZAEM
      ZNZUSUROKRBDLVDURCIOKCDMSBELTAEMUPUQVBTZTUPUQVGTZTVCVHUPVIVJUPVBVGUQUPVAV
      FAEUPUSEUAZTUTVEBDUPVKURDUAZUTVEUBZUPVLUREUAZVKVMDEURUCVNVKVMURUSUDUEUFUG
      UHUIUJUKUPUQVBULUPUQVGULUMABCDUNUO $.
      $( [11-May-2007] $) $( [12-Oct-2004] $)

    $( Lemma for ~ infm3 . $)
    infm3lem $p |- ( x e. RR -> E. y e. RR x = -u y ) $=
      ( cv cneg cr wcel wceq wrex negeq eqeq2d rcla4ev renegcl cc recn negneg
      syl eqcomd sylanc ) ACZDZEFSTDZGZSBCZDZGZBEHSEFZUEUBBTEUCTGUDUASUCTIJKSLU
      FUASUFSMFUASGSNSOPQR $.
      $( [14-Jun-2005] $)

    $( The completeness axiom for reals in terms of infimum: a non-empty,
       bounded-below set of reals has a infimum.  (This theorem is the dual of
       ~ sup3 .) $)
    infm3 $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A x <_ y ) ->
                  E. x e. RR ( A. y e. A -. y < x /\
                  A. y e. RR ( x < y -> E. z e. A z < y ) ) ) $=
      ( vu vv vw vt cr wss c0 wne cv cle wbr wral wrex clt wn wi wa cneg wcel
      crab wex ssel pm4.71rd exbidv df-rex renegcl infm3lem eleq1 rexxfr
      bitr3i syl6bb n0 rabn0 3bitr4g wal imbi1d impexp albidv df-ral wceq
      breq2 imbi12d ralxfr rexbidv breq1 imbi2d ralbidv wb leneg ancoms
      ralbidva negeq eleq1d elrab imbi1i bitri albii 3bitr4ri syl5bbr rexbiia
      bitr4i anbi12d ssrab2 sup3 mp3an1 syl6bi notbid adantrd syl5bb ltneg
      anbi2d rexbidva anbi1i anass rexbii2 adantl sylibrd 3impib ) DIJZDKLZAMZB
      MZNOZBDPZAIQZXFXEROZSZBDPZXEXFROZCMZXFROZCDQZTZBIPZUAZAIQZXCXDXIUAZEMZFMZ
      ROZSZFGMZUBZDUCZGIUDZPZYCYBROZYCHMZROZHYIQZTZFIPZUAZEIQZXTXCYAYIKLZYCYBNO
      ZFYIPZEIQZUAYRXCXDYSXIUUBXCYCDUCZFUEZYHGIQZXDYSXCUUDYCIUCZUUCUAZFUEZUUEXC
      UUCUUGFXCUUCUUFDIYCUFUGUHUUHUUCFIQUUEUUCFIUIUUCYHFGYGIYFUJFGUKYCYGDULUMUN
      UOFDUPYHGIUQURXCXIYCUBZDUCZXEUUINOZTZFIPZAIQZUUBXCXHUUMAIXCXFDUCZXGTZBUSX
      FIUCZUUPTZBUSZXHUUMXCUUPUURBXCUUPUUQUUOUAZXGTUURXCUUOUUTXGXCUUOUUQDIXFUFU
      GZUTUUQUUOXGVAUOVBXGBDVCUUMUUPBIPUUSUUPUULBFUUIIYCUJZBFUKZXFUUIVDZUUOUUJX
      GUUKXFUUIDULZXFUUIXENVEVFVGUUPBIVCUNURVHUUNUUJYBUBZUUINOZTZFIPZEIQUUBUUMU
      VIAEUVFIYBUJZAEUKZXEUVFVDZUULUVHFIUVLUUKUVGUUJXEUVFUUINVIVJVKUMUUAUVIEIYB
      IUCZUUJYTTZFIPZUVIUUAUVMUVNUVHFIUVMUUFUAZYTUVGUUJUUFUVMYTUVGVLYCYBVMVNVJV
      OYCYIUCZYTTZFUSUUFUVNTZFUSUUAUVOUVRUVSFUVRUUFUUJUAZYTTUVSUVQUVTYTYHUUJGYC
      IYFYCVDYGUUIDYFYCVPVQVRZVSUUFUUJYTVAVTWAYTFYIVCUVNFIVCWBWCWDWEUOWFYIIJYSU
      UBYRYHGIWGEFHYIWHWIWJXCXTUUJUUIXEROZSZTZFIPZXEUUIROZYLUBZDUCZUWGUUIROZUAZ
      HIQZTZFIPZUAZAIQZYRXCXSUWNAIXCXLUWEXRUWMXCUUOXKTZBUSUUQUWPTZBUSZXLUWEXCUW
      PUWQBXCUWPUUTXKTUWQXCUUOUUTXKUVAUTUUQUUOXKVAUOVBXKBDVCUWEUWPBIPUWRUWPUWDB
      FUUIIUVBUVCUVDUUOUUJXKUWCUVEUVDXJUWBXFUUIXERVIWKVFVGUWPBIVCUNURXCUWFXNUUI
      ROZCDQZTZFIPUWMXRXCUXAUWLFIXCUWTUWKUWFXCXNDUCZUWSUAZCUEXNIUCZUXCUAZCUEZUW
      TUWKXCUXCUXECXCUXCUXDXCUXBUXDUWSDIXNUFWLUGUHUWSCDUIUWKUXCCIQUXFUXCUWJCHUW
      GIYLUJCHUKXNUWGVDUXBUWHUWSUWIXNUWGDULXNUWGUUIRVIWFUMUXCCIUIUNURVJVKXQUXAB
      FUUIIUVBUVCUVDXMUWFXPUWTXFUUIXERVEUVDXOUWSCDXFUUIXNRVEVHVFVGWMWFVHUWOUUJU
      UIUVFROZSZTZFIPZUVFUUIROZUWKTZFIPZUAZEIQYRUWNUXNAEUVFIUVJUVKUVLUWEUXJUWMU
      XMUVLUWDUXIFIUVLUWCUXHUUJUVLUWBUXGXEUVFUUIRVEWKVJVKUVLUWLUXLFIUVLUWFUXKUW
      KXEUVFUUIRVIUTVKWFUMYQUXNEIUVMYJUXJYPUXMUVMUUJYETZFIPZUXJYJUVMUXOUXIFIUVP
      YEUXHUUJUVPYDUXGYBYCWNWKVJVOUVQYETZFUSUUFUXOTZFUSYJUXPUXQUXRFUXQUVTYETUXR
      UVQUVTYEUWAVSUUFUUJYEVAVTWAYEFYIVCUXOFIVCWBWCUVMYOUXLFIUVPYKUXKYNUWKUUFUV
      MYKUXKVLYCYBWNVNUUFYNUWKVLUVMUUFUWHYMUAZHIQUWKYNUUFUXSUWJHIUUFYLIUCZUAYMU
      WIUWHYCYLWNWOWPYMUXSHYIIYLYIUCZYMUAUXTUWHUAZYMUAUXTUXSUAUYAUYBYMYHUWHGYLI
      YFYLVDYGUWGDYFYLVPVQVRWQUXTUWHYMWRVTWSWMWTVFVOWFWDWEUOXAXB $.
      $( [11-May-2007] $) $( [14-Jun-2005] $)

    $( Closure of supremum of a non-empty bounded set of reals. $)
    suprcl $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
                  sup ( A , RR , < ) e. RR ) $=
      ( vz cr wss c0 wne cv cle wbr wral wrex w3a clt wn wi wa csup wcel sup3
      ltso supcl syl ) CEFCGHBIZAIZJKBCLAEMNUFUEOKPBCLUEUFOKUEDIOKDCMQBELRAEMCE
      OSETABDCUAABDECOUBUCUD $.
      $( [13-May-2007] $) $( [12-Oct-2004] $)

    $( A member of a non-empty bounded set of reals is less than or equal to
       the set's upper bound. $)
    suprub $p |- ( ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x )
                 /\ B e. A ) -> B <_ sup ( A , RR , < ) ) $=
      ( vz cr wss c0 wne cv cle wbr wral wrex w3a wcel wa clt csup wn wi sup3
      ltso supub syl imp wb lenlt 3simp1 sseld suprcl adantr sylanc mpbird ) CF
      GZCHIZBJZAJZKLBCMAFNZOZDCPZQZDCFRSZKLZVCDRLTZUTVAVEUTURUQRLTBCMUQURRLUQEJ
      RLECNUABFMQAFNVAVEUAABECUBABEFCDRUCUDUEUFDFPZVCFPZVDVEUGVBDVCUHUTVAVFUTCF
      DUOUPUSUIUJUFUTVGVAABCUKULUMUN $.
      $( [15-May-2007] $) $( [12-Oct-2004] $)

    ${
      $d z B $.
      $( The supremum of a non-empty bounded set of reals is the least upper
         bound. $)
      suprlub $p |- ( ( ( A C_ RR /\ A =/= (/) /\
                      E. x e. RR A. y e. A y <_ x ) /\
                    ( B e. RR /\ B < sup ( A , RR , < ) ) ) ->
                    E. z e. A B < z ) $=
        ( cr wss c0 wne cv cle wbr wral wrex w3a wcel clt csup wa wn wi sup3
        ltso suplub syl imp ) DFGDHIBJZAJZKLBDMAFNOZEFPEDFQRQLSZECJZQLCDNZUIUHU
        GQLTBDMUGUHQLUGUKQLCDNUABFMSAFNUJULUAABCDUBABCFDEQUCUDUEUF $.
        $( [29-May-2007] $) $( [15-Nov-2004] $)

      $( An upper bound is not less than the supremum of a non-empty bounded
         set of reals. $)
      suprnub $p |- ( ( ( A C_ RR /\ A =/= (/) /\
                      E. x e. RR A. y e. A y <_ x ) /\
                    ( B e. RR /\ A. z e. A -. B < z ) ) ->
                    -. B < sup ( A , RR , < ) ) $=
        ( cr wss c0 wne cv cle wbr wral wrex w3a wcel clt wn wa csup wi sup3
        ltso supnub syl imp ) DFGDHIBJZAJZKLBDMAFNOZEFPECJZQLRCDMSZEDFQTQLRZUIU
        HUGQLRBDMUGUHQLUGUJQLCDNUABFMSAFNUKULUAABCDUBABCFDEQUCUDUEUF $.
        $( [21-May-2007] $) $( [15-Nov-2004] $)

      $( The supremum of a non-empty bounded set of reals is less than or equal
         to an upper bound. $)
      suprleub $p |- ( ( ( A C_ RR /\ A =/= (/) /\
                      E. x e. RR A. y e. A y <_ x ) /\
                    ( B e. RR /\ A. z e. A z <_ B ) ) ->
                    sup ( A , RR , < ) <_ B ) $=
        ( cr wss c0 wne cv cle wbr wral wrex w3a wcel wa clt csup wn wb lenlt
        ssel2 sylan an1rs ralbidva pm5.32da 3ad2ant1 suprnub suprcl adantrr
        mpbird ex sylbid imp ) DFGZDHIZBJAJKLBDMAFNZOZEFPZCJZEKLZCDMZQZDFRSZEKL
        ZUSVDUTEVARLTZCDMZQZVFUPUQVDVIUAURUPUTVCVHUPUTQVBVGCDUPVADPZUTVBVGUAZVA
        FPUTVKUPVJQVAEUBDFVAUCUDUEUFUGUHUSVIVFUSVIQVFEVERLTZABCDEUIUSUTVFVLUAZV
        HVEFPUTVMUSVEEUBABDUJUDUKULUMUNUO $.
        $( [28-May-2007] $) $( [18-Mar-2005] $)
    $}
  $}

  ${
    $d x y z A $.
    sup3i.1 $e |- ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) $.
    $( A version of the completeness axiom for reals. $)
    sup3ii $p |- E. x e. RR ( A. y e. A -. x < y /\
                  A. y e. RR ( y < x -> E. z e. A y < z ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a clt wn wi wa sup3 ax-mp ) DFGDHI
      BJZAJZKLBDMAFNOUCUBPLQBDMUBUCPLUBCJPLCDNRBFMSAFNEABCDTUA $.
      $( [14-Mar-2007] $) $( [23-Aug-1999] $)

    $( Closure of supremum of a non-empty bounded set of reals. $)
    suprclii $p |- sup ( A , RR , < ) e. RR $=
      ( vz cr clt ltso sup3ii supcli ) ABEFCGHABECDIJ $.
      $( [15-May-2007] $) $( [12-Sep-1999] $)

    $( A member of a non-empty bounded set of reals is less than or equal to
       the set's upper bound. $)
    suprubii $p |- ( B e. A -> B <_ sup ( A , RR , < ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a wcel clt csup suprub mpan ) CFGC
      HIBJAJKLBCMAFNODCPDCFQRKLEABCDST $.
      $( [16-May-2007] $) $( [12-Sep-1999] $)

    ${
      $d z B $.
      $( The supremum of a non-empty bounded set of reals is the least upper
         bound. $)
      suprlubii $p |- ( ( B e. RR /\ B < sup ( A , RR , < ) ) ->
                    E. z e. A B < z ) $=
        ( cr clt ltso sup3ii suplubi ) ABCGDEHIABCDFJK $.
        $( [16-May-2007] $) $( [15-Oct-2004] $)

      $( An upper bound is not less than the supremum of a non-empty bounded
         set of reals. $)
      suprnubii $p |- ( ( B e. RR /\ A. z e. A -. B < z ) ->
                   -. B < sup ( A , RR , < ) ) $=
        ( cr clt ltso sup3ii supnubi ) ABCGDEHIABCDFJK $.
        $( [31-May-2007] $) $( [15-Oct-2004] $)

      $( The supremum of a non-empty bounded set of reals is less than or equal
         to an upper bound. $)
      suprleubii $p |- ( ( B e. RR /\ A. z e. A z <_ B ) ->
                   sup ( A , RR , < ) <_ B ) $=
        ( cr wss c0 wne cv cle wbr wral wrex w3a wcel wa clt csup suprleub
        mpan ) DGHDIJBKAKLMBDNAGOPEGQCKELMCDNRDGSTELMFABCDEUAUB $.
        $( [1-Jun-2007] $) $( [18-Mar-2005] $)
    $}
  $}

  ${
    $d x y z $.  $d z A $.  $d y z ph $.  $d x z ps $.
    reuunineg.1 $e |- ( x = -u y -> ( ph <-> ps ) ) $.
    $( The negative of the unique real such that ` ph ` . $)
    reuunineg $p |- ( E! x e. RR ph ->
                U. { x e. RR | ph } = -u U. { y e. RR | ps } ) $=
      ( vz cr cv cneg crab cuni hbrab1 hbuni hbneg renegcl negeq cc wcel wceq
      wb negcon2 recn syl2an reuhyp reuunixfr ) ABCDFGDHZIZBDGJZKZIDFUIDFUHBDFG
      LMNUFOUIOEUFUIPCDUGCHZIZGUJOUJQRUFQRUJUGSUFUKSTUJGRUFGRUJUFUAUJUBUFUBUCUD
      UE $.
      $( [13-Jun-2005] $)
  $}

  ${
    $d x y z w v u t A $.
    $( The infimum (expressed as supremum with converse 'less-than') of a set
       of reals ` A ` . $)
    dfinfmr $p |- ( A C_ RR -> sup ( A , RR , `' < ) = U. { x e. RR |
        ( A. y e. A x <_ y /\ A. y e. RR ( x < y -> E. z e. A z < y ) ) } ) $=
      ( cr wss cv clt ccnv wbr wn wral wrex wi wa crab cuni cle csup wcel wb
      lenlt visset brcnv notbii syl6rbbr ssel2 sylan2 ancoms an1rs ralbidva
      rexbii imbi12i ralbii a1i anbi12d rabbidva unieqd df-sup syl5eq ) DEFZAGZ
      BGZHIZJZKZBDLZVCVBVDJZVCCGZVDJZCDMZNZBELZOZAEPZQVBVCRJZBDLZVBVCHJZVIVCHJZ
      CDMZNZBELZOZAEPZQDEVDSVAVOWDVAVNWCAEVAVBETZOZVGVQVMWBWFVFVPBDVAVCDTZWEVFV
      PUAZWEVAWGOZWHWEVCETZWHWIWEWJOVPVCVBHJZKVFVBVCUBVEWKVBVCHAUCZBUCZUDUEUFDE
      VCUGUHUIUJUKVMWBUAWFVLWABEVHVRVKVTVCVBHWMWLUDVJVSCDVCVIHWMCUCUDULUMUNUOUP
      UQURABCEDVDUSUT $.
      $( [9-Oct-2005] $)

    $( The infimum (expressed as supremum with converse 'less-than') of a set
       of reals ` A ` is the negative of the supremum of the negatives of its
       elements.  The antecedent ensures that ` A ` is nonempty and has a lower
       bound. $)
    infmsup $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A x <_ y ) ->
                   sup ( A , RR , `' < ) =
                 -u sup ( { z e. RR | -u z e. A } , RR , < ) ) $=
      ( vw vv vu vt cr wss c0 wne cv cle wbr wral wrex clt ccnv csup cneg wcel
      crab wceq wn wi wa infm3 3exp wreu cuni breq2 notbid ralbidv breq1
      imbi1d anbi12d reuunineg df-sup wb visset brcnv notbii ralbii rexbii
      imbi12i anbi12i a1i rabbiia unieqi eqtri syl5eq ltneg imbi2d ralbidva
      wal negeq eleq1d elrab imbi1i impexp bitri albii df-ral 3bitr4ri syl5bbr
      ancoms anbi2d rexbidva anbi1i anass rexbii2 syl5bb adantl imbi12d ssel
      pm4.71rd syl6rbb albidv renegcl infm3lem eleq1 ralxfr bitr3i 3bitr4g wex
      adantrd exbidv df-rex rexxfr rexbidv syl5rbb sylan9bbr rabbidva unieqd
      negeqd eqcomd sylan9eqr ex wor ltso cnvso mpbi supeu reubii 3imtr3i syl5
      syl6d 3imp ) DIJZDKLZAMZBMZNOBDPAIQZDIRSZTZCMZUAZDUBZCIUCZIRTZUAZUDZYJYKY
      NYMYLROZUEZBDPZYLYMROZEMZYMROZEDQZUFZBIPZUGZAIQZUUCYJYKYNUUNABEDUHUIYJUUM
      AIUJZUUCUUNYJUUOUUCUUOYJYPYMFMZUAZROZUEZBDPZUUQYMROZUUJUFZBIPZUGZFIUCZUKZ
      UAZUUBUUOUUMAIUCZUKZUVGYPUUMUVDAFYLUUQUDZUUFUUTUULUVCUVJUUEUUSBDUVJUUDUUR
      YLUUQYMRULUMUNUVJUUKUVBBIUVJUUGUVAUUJYLUUQYMRUOUPUNUQURYPYLYMYOOZUEZBDPZY
      MYLYOOZYMUUHYOOZEDQZUFZBIPZUGZAIUCZUKUVIABEIDYOUSUVTUVHUVSUUMAIUVSUUMUTYL
      IUBUVMUUFUVRUULUVLUUEBDUVKUUDYLYMRAVAZBVAZVBVCVDUVQUUKBIUVNUUGUVPUUJYMYLR
      UWBUWAVBUVOUUIEDYMUUHRUWBEVAVBVEVFVDVGZVHVIVJVKVLYJUUBUVGYJUUAUVFYJUUPGMZ
      ROZUEZGYTPZUWDUUPROZUWDHMZROZHYTQZUFZGIPZUGZFIUCZUKUVFUUAYJUWOUVEYJUWNUVD
      FIUUPIUBZUWNUWDUAZDUBZUWQUUQROZUEZUFZGIPZUUQUWQROZUWIUAZDUBZUXDUWQROZUGZH
      IQZUFZGIPZUGYJUVDUWPUWGUXBUWMUXJUWPUWRUWFUFZGIPZUXBUWGUWPUXKUXAGIUWPUWDIU
      BZUGZUWFUWTUWRUXNUWEUWSUUPUWDVMUMVNVOUWDYTUBZUWFUFZGVPUXMUXKUFZGVPUWGUXLU
      XPUXQGUXPUXMUWRUGZUWFUFUXQUXOUXRUWFYSUWRCUWDIYQUWDUDYRUWQDYQUWDVQVRVSVTUX
      MUWRUWFWAWBWCUWFGYTWDUXKGIWDWEWFUWPUWLUXIGIUXNUWHUXCUWKUXHUXMUWPUWHUXCUTU
      WDUUPVMWGUXMUWKUXHUTUWPUXMUXEUWJUGZHIQUXHUWKUXMUXSUXGHIUXMUWIIUBZUGUWJUXF
      UXEUWDUWIVMWHWIUWJUXSHYTIUWIYTUBZUWJUGUXTUXEUGZUWJUGUXTUXSUGUYAUYBUWJYSUX
      ECUWIIYQUWIUDYRUXDDYQUWIVQVRVSWJUXTUXEUWJWKWBWLWMWNWOVOUQYJUXBUUTUXJUVCYJ
      YMIUBZYMDUBZUUSUFZUFZBVPZUYEBVPUXBUUTYJUYFUYEBYJUYEUYCUYDUGZUUSUFUYFYJUYD
      UYHUUSYJUYDUYCDIYMWPWQUPUYCUYDUUSWAWRWSUXBUYEBIPUYGUYEUXABGUWQIUWDWTZBGXA
      ZYMUWQUDZUYDUWRUUSUWTYMUWQDXBUYKUURUWSYMUWQUUQRUOUMWOXCUYEBIWDXDUUSBDWDXE
      YJUXCUUHUWQROZEDQZUFZGIPUXJUVCYJUYNUXIGIYJUYMUXHUXCYJUUHDUBZUYLUGZEXFUUHI
      UBZUYPUGZEXFZUYMUXHYJUYPUYREYJUYPUYQYJUYOUYQUYLDIUUHWPXGWQXHUYLEDXIUXHUYP
      EIQUYSUYPUXGEHUXDIUWIWTEHXAUUHUXDUDUYOUXEUYLUXFUUHUXDDXBUUHUXDUWQRUOUQXJU
      YPEIXIXDXEVNUNUVBUYNBGUWQIUYIUYJUYKUVAUXCUUJUYMYMUWQUUQRULUYKUUIUYLEDYMUW
      QUUHRULXKWOXCXLUQXMXNXOFGHIYTRUSVLXPXQXRXSUVSAIQUVSAIUJUUNUUOABEIDYOIRXTI
      YOXTYAIRYBYCYDUVSUUMAIUWCVEUVSUUMAIUWCYEYFYGYHYI $.
      $( [13-May-2007] $) $( [14-Jun-2005] $)

    $( Closure of infimum of a non-empty bounded set of reals. $)
    infmrcl $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A x <_ y ) ->
                  sup ( A , RR , `' < ) e. RR ) $=
      ( vv vw vz cr wss c0 wne cv cle wbr wral wrex w3a clt ccnv csup cneg
      wcel crab infmsup ssrab2 suprcl mp3an1 wex wa ssel renegcl syl6 cc wceq
      ssel2 recn negneg 3syl pm3.27 eqeltrd ex jcad negeq eleq1d elrab ne0i
      sylbir 19.23adv imp n0 sylan2b 3adant3 wi breq2 rcla4va adantll wb
      lenegcon2 adantrr adantr mpbid exp31 syl5ib com23 r19.21adv jctild
      ralbidv rcla4ev r19.23aiv 3ad2ant3 sylanc syl ) CGHZCIJZAKZBKZLMZBCNZAGOZ
      PZCGQRSDKZTZCUAZDGUBZGQSZTZGABDCUCWSXDGUAZXEGUAXCIJZEKZFKZLMZEXCNZFGOZXFW
      SXCGHXGXLXFXBDGUDFEXCUEUFWLWMXGWRWLXICUAZFUGZXGWMWLXNXGWLXMXGFWLXMXITZGUA
      ZXOTZCUAZUHZXGWLXMXPXRWLXMXIGUAZXPCGXIUIXIUJUKWLXMXRWLXMUHZXQXICYAXTXIULU
      AXQXIUMCGXIUNXIUOXIUPUQWLXMURUSUTVAXSXOXCUAXGXBXRDXOGWTXOUMXAXQCWTXOVBVCV
      DXCXOVEVFUKVGVHFCVIVJVKWRWLXLWMWQXLAGWNGUAZWQWNTZGUAZXHYCLMZEXCNZUHXLYBWQ
      YFYDYBWQYEEXCYBXHXCUAZWQYEYBXHGUAZXHTZCUAZUHZWQYEVLYGYBYKWQYEYBYKUHZWQUHW
      NYILMZYEYKWQYMYBYJWQYMYHWPYMBYICWOYIWNLVMVNVOVOYLYMYEVPZWQYBYHYNYJWNXHVQV
      RVSVTWAXBYJDXHGWTXHUMXAYICWTXHVBVCVDWBWCWDWNUJWEXKYFFYCGXIYCUMXJYEEXCXIYC
      XHLVMWFWGUKWHWIWJXDUJWKUS $.
      $( [14-May-2007] $) $( [8-Oct-2005] $)
  $}

  ${
    $d x y z $.
    $( The set of natural numbers is unbounded above.  Theorem I.28 of
       [Apostol] p. 26. $)
    nnunb $p |- -. E. x e. RR A. y e. NN ( y < x \/ y = x ) $=
      ( vz cv clt wbr wceq wo cn wral cr wrex wn wi wa wcel pm3.24 wal wex c1
      cmin co oprex eleq1 breq1 rexbidv imbi12d cla4v ltm1 syl7 peano2rem syl5
      pm2.43d df-rex syl6ib com12 caddc wb 1re ltsubadd mp3an2 nnre sylan2
      pm5.32da exbidv breq2 anbi12d cla4ev peano2nn sylan 19.23aiv syl6bi syld
      df-ral alinexa bitr2i con1bii 3imtr4g anim2d mtoi nrex wss c0 wne nnssre
      1nn n0i ax-mp df-ne mpbir sup2 mp3an12 mto ) BDZADZEFZWNWOGHBIJAKLZWOWNEF
      ZMZBIJZWPWNCDZEFZCILZNZBKJZOZAKLZXFAKWOKPZXFWTWTMZOWTQXHXEXIWTXHWNKPZXDNZ
      BRZWNIPZWROZBSZXEXIXHXLXAIPZWOTUAUBZXAEFZOZCSZXOXLXHXTXLXHXRCILZXTXLXHYAX
      LXQKPZXHYANXHXLYBXQWOEFZYAXHXKYBYCYANZNBXQWOTUAUCWNXQGZXJYBXDYDWNXQKUDYEW
      PYCXCYAWNXQWOEUEYEXBXRCIWNXQXAEUEUFUGUGUHWOUIUJWOUKULUMXRCIUNUOUPXHXTXPWO
      XATUQUBZEFZOZCSXOXHXSYHCXHXPXRYGXHXAKPZXRYGURZXPXHTKPYIYJUSWOTXAUTVAXAVBV
      CVDVEYHXOCYFIPZYGXOXPXNYKYGOBYFXATUQUCWNYFGXMYKWRYGWNYFIUDWNYFWOEVFVGVHXA
      VIVJVKVLVMXDBKVNXOWTWTXMWSNBRXOMWSBIVNXMWRBVOVPVQVRVSVTWAIKWBIWCWDZWQXGWE
      YLIWCGMZTIPYMWFITWGWHIWCWIWJABCIWKWLWM $.
      $( [21-Jan-1997] $)
  $}

  ${
    $d n y A $.
    $( Archimedean property of real numbers.  For any real number, there is an
       integer greater than it.  Theorem I.29 of [Apostol] p. 26. $)
    arch $p |- ( A e. RR -> E. n e. NN A < n ) $=
      ( vy cv clt wbr cn wrex cr wceq breq1 rexbidv wo wral wn nnunb ralnex
      mpbir wcel wa wb axlttri nnre sylan2 eqcom orbi1i orcom bitri notbii
      syl6bb biimprd reximdva rexnal syl5ibr ralimia ax-mp vtoclri ) CDZBDZEFZB
      GHZAUSEFZBGHCAIURAJUTVBBGURAUSEKLUSUREFZUSURJZMZBGNZOZCINZVACINVHVFCIHOCB
      PVFCIQRVGVACIURISZVEOZBGHVAVGVIVJUTBGVIUSGSZTZUTVJVLUTURUSJZVCMZOZVJVIUSI
      SUTVOUAVKURUSUBUSUCUDVNVEVNVDVCMVEVMVDVCURUSUEUFVDVCUGUHUIUJUKULVEBGUMUNU
      OUPUQ $.
      $( [9-Nov-2004] $) $( [21-Jan-1997] $)
  $}

  ${
    $d n A $.
    $( There exists a natural number whose reciprocal is less than a given
       positive real.  Exercise 3 of [Apostol] p. 28. $)
    nnrecl $p |- ( ( A e. RR /\ 0 < A ) -> E. n e. NN ( 1 / n ) < A ) $=
      ( cr wcel cc0 clt wbr wa c1 cdiv co cv cn wrex wne redivcl 3expa pm3.26
      1re jctil gt0ne0 sylanc arch syl wb ltrec recgt0 jca nnre nngt0 syl2an
      cc wceq recrec recn adantr breq2d bitrd rexbidva mpbid ) ACDZEAFGZHZIAJKZ
      BLZFGZBMNZIVEJKZAFGZBMNVCVDCDZVGICDZVAHAEOZVJVCVKVAVLVJIAPQVCVAVKVAVBRSTA
      UAZUBZVDBUCUDVCVFVIBMVCVEMDZHVFVHIVDJKZFGZVIVJEVDFGZHVECDZEVEFGZHVFVQUEVC
      VOVDVEUFVCVJVRVNAUGUHVOVSVTVEUIVEUJUHUKVCVQVIUEVOVCVPAVHFAULDZVLVPAUMVCAU
      NVAWAVBAUOUPVMUBUQUPURUSUT $.
      $( [26-Jul-2006] $) $( [8-Nov-2004] $)
  $}

  ${
    $d x A $.  $d x k $.
    $( A bounded real sequence ` A ( k ) ` is less than or equal to at least
       one of its indices. $)
    bndndx $p |- ( E. x e. RR A. k e. NN ( A e. RR /\ A <_ x ) ->
                  E. k e. NN A <_ k ) $=
      ( cr wcel cv cle wbr wa cn wral wrex wi clt arch w3a lelttr ltle 3adant2
      syld exp5o com3l imp4b com23 nnre sylan2 reximdva mpd r19.35 sylib
      r19.23aiv ) BDEZBAFZGHZIZCJKZBCFZGHZCJLZADUMDEZUOURMZCJLZUPUSMUTUMUQNHZCJ
      LVBUMCOUTVCVACJUTUQDEZVCVAMUQJEUTVDIUOVCURUTVDULUNVCURMZULUTVDUNVEMULUTVD
      UNVCURULUTVDPUNVCIBUQNHZURBUMUQQULVDVFURMUTBUQRSTUAUBUCUDUQUEUFUGUHUOURCJ
      UIUJUK $.
      $( [18-Jan-2008] $)
  $}

