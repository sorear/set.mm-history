$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Completeness Axiom and Suprema
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w x y z v S $.  $d w y A $.
    $( If a set of reals contains a lower bound, it contains a unique lower
       bound. $)
    lbreu $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y ) ->
                E! x e. S A. y e. S x <_ y ) $=
      ( vw cr wss cv cle wbr wral wrex wa wceq wreu wcel breq2 rcla4v im2anan9r
      wi ssel wb anim12d impcom letri3 syl exbiri com23 com3r r19.21aivv anim2i
      syld ancoms breq1 ralbidv reu4 sylibr ) CEFZAGZBGZHIZBCJZACKZLVBVADGZUSHI
      ZBCJZLZURVCMZSZDCJACJZLZVAACNVBUQVJUQVIVBUQVHADCCURCOZVCCOZLZVFUQVGVMVFUR
      VCHIZVCURHIZLZUQVGSVLVAVNVKVEVOUTVNBVCCUSVCURHPQVDVOBURCUSURVCHPQRVMUQVPV
      GVMUQVGVPVMUQLUREOZVCEOZLZVGVPUAUQVMVSUQVKVQVLVRCEURTCEVCTUBUCURVCUDUEUFU
      GUKUHUIUJULVAVEADCVGUTVDBCURVCUSHUMUNUOUP $.
      $( [9-Oct-2005] $)

    $( If a set of reals contains a lower bound, it contains a unique lower
       bound that belongs to the set. $)
    lbcl $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y ) ->
                U. { x e. S | A. y e. S x <_ y } e. S ) $=
      ( cr wss cv cle wbr wral wrex wa wreu crab cuni lbreu reucl2 ssrab2 sseli
      wcel 3syl ) CDEAFBFGHBCIZACJKUAACLUAACMZNZUBSUCCSABCOUAACPUBCUCUAACQRT $.
      $( [9-Oct-2005] $)

    $( If a set of reals contains a lower bound, the lower bound is less than
       or equal to all members of the set. $)
    lble $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y /\ A e. S ) ->
                U. { x e. S | A. y e. S x <_ y } <_ A ) $=
      ( vw wcel cr wss cv cle wral wrex crab cuni wreu wceq breq1 ralbidv ax-17
      wbr lbreu cbvreuv sylib hbra1 hbrab hbuni hbeleq ralbid reuuni3 syl breq2
      wa hbbr rcla4 imp sylan2 3impb 3coml ) CDFZDGHZAIZBIZJTZBDKZADLZVDADMZNZC
      JTZUSUTVEVHUTVEULZUSVGVBJTZBDKZVHVIEIZVBJTZBDKZEDOZVKVIVDADOVOABDUAVDVNAE
      DVAVLPVCVMBDVAVLVBJQRUBUCVNVDVKEADVLVAPVMVCBDVLVAVBJQRVLVGPVMVJBDBEVGBEVF
      VDBAEDVCBDUDVLDFBSUEUFZUGVLVGVBJQUHUIUJUSVKVHVJVHBCDBEVGCJVPVLJFBSVLCFBSU
      MVBCVGJUKUNUOUPUQUR $.
      $( [9-Oct-2005] $)

    $( If a set of reals contains a lower bound, the lower bound is its
       infimum. $)
    lbinfm $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y ) ->
                 sup ( S , RR , `' < ) = U. { x e. S | A. y e. S x <_ y } ) $=
      ( vv vz vw cr cv cle wbr wral wrex wa clt wn wi crab wcel wb cvv wss ccnv
      csup cuni df-sup wceq wreu lbcl ssel adantr mpd lble 3expa syldan adantlr
      ssel2 lenlt syl11anc mpbid reex ssex rabexg syl uniexg visset brcnvg 3syl
      mpan2 notbid ad2antrr mpbird r19.21aiva a1d ancrd breq2 rcla4ev r19.21aiv
      syl6 breq1 ralbidv imbi1d anbi12d syl12anc ltso cnvso mpbi supeu mpbi2and
      wor reuuni2 syl5eq ) CGUAZAHBHIJBCKZACLZMZCGNUBZUCDHZEHZWPJZOZECKZWRWQWPJ
      ZWRFHZWPJZFCLZPZEGKZMZDGQUDZWMACQZUDZDEFCGWPUEWOXKWRWPJZOZECKZWRXKWPJZXEP
      ZEGKZXIXKUFZWOXKGRZXHDGUGZXNXQMZXRSWOXKCRZXSABCUHZWLYBXSPWNCGXKUIUJUKZWOX
      HDGLZXTWOXSXNXQYEYDWOXMECWOWRCRZMZXMWRXKNJZOZYGXKWRIJZYIWLWNYFYJABWRCULUM
      YGXSWRGRZYJYISWOXSYFWLWNYBXSYCCGXKUPUNUJWLYFYKWNCGWRUPUOXKWRUQURUSWLXMYIS
      WNYFWLXLYHWLXJTRZXKTRZXLYHSZWLCTRYLCGUTVAWMACTVBVCXJTVDYMWRTRYNEVEXKWRTTN
      VFVHVGVIVJVKVLZWOXPEGWOXPYKWOXOYBXOMXEWOXOYBWOYBXOYCVMVNXDXOFXKCXCXKWRWPV
      OVPVRVMVQZXHYADXKGWQXKUFZXAXNXGXQYQWTXMECYQWSXLWQXKWRWPVSVIVTYQXFXPEGYQXB
      XOXEWQXKWRWPVOWAVTWBZVPWCDEFGCWPGNWIGWPWIWDGNWEWFWGVCXHYADGXKYRWJURYOYPWH
      WK $.
      $( [9-Oct-2005] $)

    $( If a set of reals contains a lower bound, it contains its infimum. $)
    lbinfmcl $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y ) ->
                 sup ( S , RR , `' < ) e. S ) $=
      ( cr wss cv cle wbr wral wrex clt ccnv csup crab cuni lbinfm lbcl eqeltrd
      wa ) CDEAFBFGHBCIZACJSCDKLMTACNOCABCPABCQR $.
      $( [11-Oct-2005] $)

    $( If a set of reals contains a lower bound, its infmimum is less than or
       equal to all members of the set. $)
    lbinfmle $p |- ( ( S C_ RR /\ E. x e. S A. y e. S x <_ y /\ A e. S ) ->
                sup ( S , RR , `' < ) <_ A ) $=
      ( cr wss cle wbr wral wrex wcel w3a clt ccnv csup crab cuni wceq lbinfm
      cv 3adant3 lble eqbrtrd ) DEFZATBTGHBDIZADJZCDKZLDEMNOZUEADPQZCGUDUFUHUIR
      UGABDSUAABCDUBUC $.
      $( [11-Oct-2005] $)
  $}

  ${
    $d x y z w v u t A $.
    $( A non-empty, bounded-above set of reals has a supremum.  Stronger
       version of completeness axiom (it has a slightly weaker antecedent). $)
    sup2 $p |- ( ( A C_ RR /\ A =/= (/) /\
                  E. x e. RR A. y e. A ( y < x \/ y = x ) ) ->
               E. x e. RR ( A. y e. A -. x < y /\
                  A. y e. RR ( y < x -> E. z e. A y < z ) ) ) $=
      ( cr cv clt wbr wceq wral wrex w3a wi wa wcel wex c1 caddc adantr wal wss
      c0 wne wo wn co peano2re a1i ssel ltp1 ancli axlttrn 3expb sylan2 sylan2i
      exp4b com34 pm2.43d imp breq1 syl5ibrcom adantl jaod ex syl6 com23 alimdv
      a2d df-ral 3imtr4g expimpd jcad oprex eleq1 breq2 ralbidv cla4ev 19.23adv
      anbi12d cbvexv syl6ib df-rex imdistani df-3an 3imtr4i axsup syl ) DEUAZDU
      BUCZBFZAFZGHZWJWKIZUDZBDJZAEKZLZWHWIWLBDJZAEKZLZWKWJGHUEBDJWLWJCFZGHZCDKM
      BEJNAEKWHWINZWPNXCWSNWQWTXCWPWSWHWPWSMWIWHWKEOZWONZAPZXDWRNZAPZWPWSWHXFXA
      EOZXBBDJZNZCPZXHWHXEXLAWHXEWKQRUFZEOZWJXMGHZBDJZNZXLWHXEXNXPXEXNMWHXDXNWO
      WKUGZSUHWHXDWOXPWHXDNZWJDOZWNMZBTXTXOMZBTWOXPXSYAYBBXSXTWNXOWHXDXTWNXOMZM
      WHXTXDYCWHXTWJEOZXDYCMDEWJUIYDXDYCYDXDNZWLXOWMYDXDWLXOMZYDXDYFYDXDWLXDXOY
      DXDWLXDXOXDYEWLWKXMGHZXOWKUJZXDYDXDXNNWLYGNXOMZXDXNXRUKYDXDXNYIWJWKXMULUM
      UNUOUPUQURUSXDWMXOMYDXDXOWMYGYHWJWKXMGUTVAVBVCVDVEVFUSVHVGWNBDVIXOBDVIVJV
      KVLXKXQCXMWKQRVMXAXMIZXIXNXJXPXAXMEVNYJXBXOBDXAXMWJGVOVPVSVQVEVRXKXGCAXAW
      KIZXIXDXJWRXAWKEVNYKXBWLBDXAWKWJGVOVPVSVTWAWOAEWBWRAEWBVJSWCWHWIWPWDWHWIW
      SWDWEABCDWFWG $.
      $( [19-Jan-1997] $)

    $( A version of the completeness axiom for reals. $)
    sup3 $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
                  E. x e. RR ( A. y e. A -. x < y /\
                  A. y e. RR ( y < x -> E. z e. A y < z ) ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a clt wceq wo wa wcel 3anass wn wi
      wb ssel leloe expcom imp31 ralbidva rexbidva anbi2d pm5.32i 3bitr4i sylbi
      syl9 sup2 ) DEFZDGHZBIZAIZJKZBDLZAEMZNZUPUQURUSOKZURUSPQZBDLZAEMZNZUSUROK
      UABDLVDURCIOKCDMUBBELRAEMUPUQVBRZRUPUQVGRZRVCVHUPVIVJUPVBVGUQUPVAVFAEUPUS
      ESZRUTVEBDUPVKURDSZUTVEUCZUPVLURESZVKVMDEURUDVNVKVMURUSUEUFUNUGUHUIUJUKUP
      UQVBTUPUQVGTULABCDUOUM $.
      $( [11-May-2007] $) $( [12-Oct-2004] $)

    $( Lemma for ~ infm3 . $)
    infm3lem $p |- ( x e. RR -> E. y e. RR x = -u y ) $=
      ( cv cr wcel cneg wceq wrex renegcl recn negneg syl eqcomd eqeq2d rcla4ev
      cc negeq syl11anc ) ACZDEZSFZDESUAFZGZSBCZFZGZBDHSITUBSTSPEUBSGSJSKLMUFUC
      BUADUDUAGUEUBSUDUAQNOR $.
      $( [14-Jun-2005] $)

    $( The completeness axiom for reals in terms of infimum: a non-empty,
       bounded-below set of reals has a infimum.  (This theorem is the dual of
       ~ sup3 .) $)
    infm3 $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A x <_ y ) ->
                  E. x e. RR ( A. y e. A -. y < x /\
                  A. y e. RR ( x < y -> E. z e. A z < y ) ) ) $=
      ( vu vv vw vt cr cv cle wbr wral wrex clt wi wa wcel wal df-ral wss c0 wn
      wne cneg crab wex ssel pm4.71rd exbidv df-rex renegcl eleq1 rexxfr bitr3i
      infm3lem syl6bb n0 rabn0 3bitr4g imbi1d impexp albidv wceq imbi12d ralxfr
      breq2 rexbidv breq1 imbi2d negeq eleq1d elrab imbi1i bitri albii 3bitr4ri
      ralbidv wb leneg ancoms syl5bbr rexbiia bitr4i anbi12d ssrab2 sup3 mp3an1
      ralbidva syl6bi notbid adantrd syl5bb ltneg anbi1i anass rexbii2 rexbidva
      anbi2d adantl sylibrd 3impib ) DIUAZDUBUDZAJZBJZKLZBDMZAINZXFXEOLZUCZBDMZ
      XEXFOLZCJZXFOLZCDNZPZBIMZQZAINZXCXDXIQZEJZFJZOLZUCZFGJZUEZDRZGIUFZMZYCYBO
      LZYCHJZOLZHYINZPZFIMZQZEINZXTXCYAYIUBUDZYCYBKLZFYIMZEINZQYRXCXDYSXIUUBXCY
      CDRZFUGZYHGINZXDYSXCUUDYCIRZUUCQZFUGZUUEXCUUCUUGFXCUUCUUFDIYCUHUIUJUUHUUC
      FINUUEUUCFIUKUUCYHFGYGIYFULFGUPYCYGDUMUNUOUQFDURYHGIUSUTXCXIYCUEZDRZXEUUI
      KLZPZFIMZAINZUUBXCXHUUMAIXCXFDRZXGPZBSXFIRZUUPPZBSZXHUUMXCUUPUURBXCUUPUUQ
      UUOQZXGPUURXCUUOUUTXGXCUUOUUQDIXFUHUIZVAUUQUUOXGVBUQVCXGBDTUUMUUPBIMUUSUU
      PUULBFUUIIYCULZBFUPZXFUUIVDZUUOUUJXGUUKXFUUIDUMZXFUUIXEKVGVEVFUUPBITUOUTV
      HUUNUUJYBUEZUUIKLZPZFIMZEINUUBUUMUVIAEUVFIYBULZAEUPZXEUVFVDZUULUVHFIUVLUU
      KUVGUUJXEUVFUUIKVIVJVRUNUUAUVIEIUUAUUJYTPZFIMZYBIRZUVIYCYIRZYTPZFSUUFUVMP
      ZFSUUAUVNUVQUVRFUVQUUFUUJQZYTPUVRUVPUVSYTYHUUJGYCIYFYCVDYGUUIDYFYCVKVLVMZ
      VNUUFUUJYTVBVOVPYTFYITUVMFITVQUVOUVMUVHFIUVOUUFQZYTUVGUUJUUFUVOYTUVGVSYCY
      BVTWAVJWIWBWCWDUQWEYIIUAYSUUBYRYHGIWFEFHYIWGWHWJXCXTUUJUUIXEOLZUCZPZFIMZX
      EUUIOLZYLUEZDRZUWGUUIOLZQZHINZPZFIMZQZAINZYRXCXSUWNAIXCXLUWEXRUWMXCUUOXKP
      ZBSUUQUWPPZBSZXLUWEXCUWPUWQBXCUWPUUTXKPUWQXCUUOUUTXKUVAVAUUQUUOXKVBUQVCXK
      BDTUWEUWPBIMUWRUWPUWDBFUUIIUVBUVCUVDUUOUUJXKUWCUVEUVDXJUWBXFUUIXEOVIWKVEV
      FUWPBITUOUTXRUWFXNUUIOLZCDNZPZFIMXCUWMXQUXABFUUIIUVBUVCUVDXMUWFXPUWTXFUUI
      XEOVGUVDXOUWSCDXFUUIXNOVGVHVEVFXCUXAUWLFIXCUWTUWKUWFXCXNDRZUWSQZCUGXNIRZU
      XCQZCUGZUWTUWKXCUXCUXECXCUXCUXDXCUXBUXDUWSDIXNUHWLUIUJUWSCDUKUWKUXCCINUXF
      UXCUWJCHUWGIYLULCHUPXNUWGVDUXBUWHUWSUWIXNUWGDUMXNUWGUUIOVIWEUNUXCCIUKUOUT
      VJVRWMWEVHUWOUUJUUIUVFOLZUCZPZFIMZUVFUUIOLZUWKPZFIMZQZEINYRUWNUXNAEUVFIUV
      JUVKUVLUWEUXJUWMUXMUVLUWDUXIFIUVLUWCUXHUUJUVLUWBUXGXEUVFUUIOVGWKVJVRUVLUW
      LUXLFIUVLUWFUXKUWKXEUVFUUIOVIVAVRWEUNYQUXNEIUVOYJUXJYPUXMYJUUJYEPZFIMZUVO
      UXJUVPYEPZFSUUFUXOPZFSYJUXPUXQUXRFUXQUVSYEPUXRUVPUVSYEUVTVNUUFUUJYEVBVOVP
      YEFYITUXOFITVQUVOUXOUXIFIUWAYEUXHUUJUWAYDUXGYBYCWNWKVJWIWBUVOYOUXLFIUWAYK
      UXKYNUWKUUFUVOYKUXKVSYCYBWNWAUUFYNUWKVSUVOYNUWHYMQZHINUUFUWKYMUXSHYIIYLYI
      RZYMQYLIRZUWHQZYMQUYAUXSQUXTUYBYMYHUWHGYLIYFYLVDYGUWGDYFYLVKVLVMWOUYAUWHY
      MWPVOWQUUFUXSUWJHIUUFUYAQYMUWIUWHYCYLWNWSWRWMWTVEWIWEWCWDUQXAXB $.
      $( [11-May-2007] $) $( [14-Jun-2005] $)

    $( Closure of supremum of a non-empty bounded set of reals. $)
    suprcl $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
                  sup ( A , RR , < ) e. RR ) $=
      ( vz cr wss c0 wne cv cle wbr wral wrex w3a clt wn wi wa csup wcel supcl
      sup3 ltso syl ) CEFCGHBIZAIZJKBCLAEMNUFUEOKPBCLUEUFOKUEDIOKDCMQBELRAEMCEO
      SETABDCUBABDECOUCUAUD $.
      $( [13-May-2007] $) $( [12-Oct-2004] $)

    $( A member of a non-empty bounded set of reals is less than or equal to
       the set's upper bound. $)
    suprub $p |- ( ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x )
                 /\ B e. A ) -> B <_ sup ( A , RR , < ) ) $=
      ( vz cr wss c0 wne cv cle wbr wral wrex wcel wa clt wn wi imp w3a csup wb
      sup3 ltso supub syl simp1 sseld suprcl adantr lenlt syl11anc mpbird ) CFG
      ZCHIZBJZAJZKLBCMAFNZUAZDCOZPZDCFQUBZKLZVCDQLRZUTVAVEUTURUQQLRBCMUQURQLUQE
      JQLECNSBFMPAFNVAVESABECUDABEFCDQUEUFUGTVBDFOZVCFOZVDVEUCUTVAVFUTCFDUOUPUS
      UHUITUTVGVAABCUJUKDVCULUMUN $.
      $( [15-May-2007] $) $( [12-Oct-2004] $)

    ${
      $d z B $.
      $( The supremum of a non-empty bounded set of reals is the least upper
         bound. $)
      suprlub $p |- ( ( ( A C_ RR /\ A =/= (/) /\
                      E. x e. RR A. y e. A y <_ x ) /\
                    ( B e. RR /\ B < sup ( A , RR , < ) ) ) ->
                    E. z e. A B < z ) $=
        ( cr wss c0 wne cv cle wbr wral wrex w3a wcel clt csup wa wi suplub syl
        wn sup3 ltso imp ) DFGDHIBJZAJZKLBDMAFNOZEFPEDFQRQLSZECJZQLCDNZUIUHUGQL
        UCBDMUGUHQLUGUKQLCDNTBFMSAFNUJULTABCDUDABCFDEQUEUAUBUF $.
        $( [29-May-2007] $) $( [15-Nov-2004] $)

      $( An upper bound is not less than the supremum of a non-empty bounded
         set of reals. $)
      suprnubOLD $p |- ( ( ( A C_ RR /\ A =/= (/) /\
                      E. x e. RR A. y e. A y <_ x ) /\
                    ( B e. RR /\ A. z e. A -. B < z ) ) ->
                    -. B < sup ( A , RR , < ) ) $=
        ( cr wss c0 wne cv cle wbr wral wrex w3a wcel clt wn wa wi csup syl imp
        sup3 ltso supnub ) DFGDHIBJZAJZKLBDMAFNOZEFPECJZQLRCDMSZEDFQUAQLRZUIUHU
        GQLRBDMUGUHQLUGUJQLCDNTBFMSAFNUKULTABCDUDABCFDEQUEUFUBUC $.
        $( [21-May-2007] $) $( [15-Nov-2004] $)

      $( The supremum of a non-empty bounded set of reals is less than or equal
         to an upper bound. $)
      suprleubOLD $p |- ( ( ( A C_ RR /\ A =/= (/) /\
                      E. x e. RR A. y e. A y <_ x ) /\
                    ( B e. RR /\ A. z e. A z <_ B ) ) ->
                    sup ( A , RR , < ) <_ B ) $=
        ( cr wss c0 wne cv cle wbr wral wcel wa clt wn wb lenlt sylan wrex csup
        ssel2 an32s ralbidva pm5.32da 3ad2ant1 suprnubOLD suprcl adantrr mpbird
        w3a ex sylbid imp ) DFGZDHIZBJAJKLBDMAFUAZULZEFNZCJZEKLZCDMZOZDFPUBZEKL
        ZUSVDUTEVAPLQZCDMZOZVFUPUQVDVIRURUPUTVCVHUPUTOVBVGCDUPVADNZUTVBVGRZUPVJ
        OVAFNUTVKDFVAUCVAESTUDUEUFUGUSVIVFUSVIOVFEVEPLQZABCDEUHUSUTVFVLRZVHUSVE
        FNUTVMABDUIVEESTUJUKUMUNUO $.
        $( [28-May-2007] $) $( [18-Mar-2005] $)
    $}
  $}

  ${
    $d A x y z $.  $d B x z $.
    $( An upper bound is not less than the supremum of a non-empty bounded set
       of reals.  (Revised by Mario Carneiro, 12-May-2013.) $)
    suprnub $p |- ( ( ( A C_ RR /\ A =/= (/) ) /\
                  ( B e. RR /\ A. z e. A -. B < z ) ) ->
                  -. B < sup ( A , RR , < ) ) $=
      ( vy vx cr wss wa wcel cv clt wbr wn wral cle wrex breq1 wb imp wi c0 wne
      csup notbid ralbidv rcla4ev cbvralv lenlt expcom sylan9 ralbidva syl5rbbr
      wceq ssel rexbidva adantr syl5ib w3a sup3 ltso supnub 3expia com23 mpdd
      syl ) BFGZBUAUBZHZCFICAJZKLZMZABNZHZCBFKUCKLMZVHVMDJZEJZOLZDBNZEFPZVNVMVP
      VIKLZMZABNZEFPZVHVSWBVLECFVPCUMZWAVKABWDVTVJVPCVIKQUDUEUFVFWCVSRVGVFWBVRE
      FVRVIVPOLZABNVFVPFIZHZWBWEVQADBVIVOVPOQUGWGWEWAABWGVIBIZWEWARZVFWHVIFIZWF
      WIBFVIUNWJWFWIVIVPUHUIUJSUKULUOUPUQVHVSVMVNVFVGVSVMVNTZVFVGVSURVPVOKLMDBN
      VOVPKLVOVIKLABPTDFNHEFPWKEDABUSEDAFBCKUTVAVEVBVCVDS $.
      $( [12-May-2013] $) $( [15-Nov-2004] $)

    $( The supremum of a non-empty bounded set of reals is less than or equal
       to an upper bound.  (Revised by Mario Carneiro, 12-May-2013.) $)
    suprleub $p |- ( ( ( A C_ RR /\ A =/= (/) ) /\
                  ( B e. RR /\ A. z e. A z <_ B ) ) ->
                  sup ( A , RR , < ) <_ B ) $=
      ( vy vx cr wss c0 wne wa wcel cv cle wbr wral clt wn wb lenlt imp suprnub
      csup ssel2 sylan an32s ralbidva pm5.32da adantr ex sylbid wrex wceq breq1
      cbvralv breq2 ralbidv syl5bb rcla4ev suprcl 3expia simprl syl11anc mpbird
      syl5 ) BFGZBHIZJZCFKZALZCMNZABOZJZJZBFPUBZCMNZCVNPNQZVGVLVPVGVLVHCVIPNQZA
      BOZJZVPVEVLVSRVFVEVHVKVRVEVHJVJVQABVEVIBKZVHVJVQRZVEVTJVIFKVHWABFVIUCVICS
      UDUEUFUGUHVGVSVPABCUAUIUJTVMVNFKZVHVOVPRVGVLWBVLDLZELZMNZDBOZEFUKZVGWBWFV
      KECFWFVIWDMNZABOWDCULZVKWEWHDABWCVIWDMUMUNWIWHVJABWDCVIMUOUPUQURVEVFWGWBE
      DBUSUTVDTVGVHVKVAVNCSVBVC $.
      $( [12-May-2013] $) $( [18-Mar-2005] $)
  $}

  ${
    $d A b v w x z $.  $d B b v w x y z $.  $d C b w x $.  $d ph b w $.
    supmul1.1 $e |- C = { z | E. v e. B z = ( A x. v ) } $.
    supmul1.2 $e |- ( ph <-> ( ( A e. RR /\ 0 <_ A /\ A. x e. B 0 <_ x ) /\
        ( B C_ RR /\ B =/= (/) /\ E. x e. RR A. y e. B y <_ x ) ) ) $.
    $( The supremum function distributes over multiplication, in the sense that
       ` A x. ( sup B ) = sup ( A x. B ) ` , where ` A x. B ` is shorthand for
       ` { A x. b | b e. B } ` and is defined as ` C ` below.  This is the
       simple version, with only one set argument; see ~ supmul for the more
       general case with two set arguments.  (Contributed by Mario Carneiro,
       5-Jul-2013.) $)
    supmul1 $p |- ( ph ->
      ( A x. sup ( B , RR , < ) ) = sup ( C , RR , < ) ) $=
      ( vw vb cr clt cle wbr wa wcel cc0 adantr csup cmul co wceq wn wss c0 wne
      cv wral visset opreq2 eqeq2d cbvrexv eqeq1 rexbidv syl5bb elab2 wi simpl1
      wrex w3a sylbi simpr simp1d imp remulcl syl11anc eleq1a r19.23adva syl5bi
      sseld syl ssrdv wex simpr2 oprex isseti rgen r19.2z sylancl exbii rexcom4
      a1i 3bitr4i sylibr suprcl simpl2 jca suprub sylan syl31anc breq1 biimprcd
      n0 lemul2a r19.21aiv suprleub syl22anc cdiv ralbidv rcla4ev 3jca rcla4cva
      breq2 simpl3 0re letr syl111anc mp2and ex 19.23adv mulge0 anim1i prodgt02
      mpd lelttr gt0ne0 redivcl ltdivmul syl112anc mpbird suprlub syl12anc ra4e
      wb adantl simplrr adantlr eqbrtrrd mpdan expr ad2antrr lenlt mpbid mtbird
      mpi nrexdv pm2.65d eqlelt eqcomd ) AHMNUAZFGMNUAZUBUCZAUUBUUDUDZUUBUUDOPZ
      UUBUUDNPZUEZQZAUUFUUHAHMUFZHUGUHZUUDMRZKUIZUUDOPZKHUJZUUFAKHMUUMHRZUUMFLU
      IZUBUCZUDZLGVAZAUUMMRZDUIZFEUIZUBUCZUDZEGVAZUUTDUUMHKUKUVFUVBUURUDZLGVAUV
      BUUMUDZUUTUVEUVGELGUVCUUQUDUVDUURUVBUVCUUQFUBULUMUNUVHUVGUUSLGUVBUUMUURUO
      UPUQIURZAUUSUVALGAUUQGRZQZUURMRZUUSUVAUSUVKFMRZUUQMRZUVLAUVMUVJAUVMSFOPZS
      BUIZOPZBGUJZVBZGMUFZGUGUHZCUIUVPOPCGUJBMVAZVBZQZUVMJUVMUVOUVRUWCUTVCZTZAU
      VJUVNAGMUUQAUVTUWAUWBAUWDUWCJUVSUWCVDVCZVEVLVFZFUUQVGVHZUURMUUMVIVMVJVKZV
      NZAUUSKVOZLGVAZUUKAUWAUWLLGUJUWMAUWDUWAJUVSUVTUWAUWBVPVCZUWLLGUWLUVJKUURF
      UUQUBVQVRZWDVSUWLLGVTWAUUPKVOZUUTKVOUUKUWMUUPUUTKUVIWBKHWOZUUSLKGWCWEWFZA
      UVMUUCMRZUULUWEAUWCUWSUWGBCGWGVMZFUUCVGVHZAUUNKHUUPUUTAUUNUVIAUUSUUNLGUVK
      UURUUDOPZUUSUUNUSUVKUVNUWSUVMUVOQZUUQUUCOPZUXBUWHAUWSUVJUWTTZAUXCUVJAUVMU
      VOUWEAUWDUVOJUVMUVOUVRUWCWHVCZWITAUWCUVJUXDUWGBCGUUQWJWKZUUQUUCFWPWLUUSUU
      NUXBUUMUURUUDOWMWNVMVJVKWQZKHUUDWRWSAUUGUUBFWTUCZUUQNPZLGVAZAUUGUXKAUUGQZ
      UWCUXIMRZUXIUUCNPZUXKAUWCUUGUWGTUXLUUBMRZUVMFSUHZUXMAUXOUUGAUUJUUKUUMUVPO
      PZKHUJZBMVAZVBZUXOAUUJUUKUXSUWKUWRAUULUUOUXSUXAUXHUXRUUOBUUDMUVPUUDUDUXQU
      UNKHUVPUUDUUMOXEXAXBVHXCZBKHWGVMZTZAUVMUUGUWETZUXLUVMSFNPZUXPUYDUXLUVMUWS
      SUUCOPZSUUDNPZUYEUYDAUWSUUGUWTTZAUYFUUGAUWAUYFUWNUWAUVJLVOAUYFLGWOAUVJUYF
      LAUVJUYFUVKSUUQOPZUXDUYFAUVRUVJUYIAUWDUVRJUVMUVOUVRUWCXFVCUVQUYIBUUQGUVPU
      UQSOXEXDWKZUXGUVKSMRZUVNUWSUYIUXDQUYFUSUYKUVKXGWDUWHUXESUUQUUCXHXIXJXKXLV
      KXPTUXLSUUBOPZUUGQZUYGAUYLUUGAUUKUYLUWRUUKUWPAUYLUWQAUUPUYLKAUUPUYLAUUPQZ
      SUUMOPZUUMUUBOPZUYLAUUPUYOUUPUUTAUYOUVIAUUSUYOLGUVKSUUROPZUUSUYOUSUVKUVMU
      VOUVNUYIUYQUWFAUVOUVJUXFTUWHUYJFUUQXMWSUUSUYOUYQUUMUURSOXEWNVMVJVKVFAUXTU
      UPUYPUYABKHUUMWJWKZUYNUYKUVAUXOUYOUYPQUYLUSUYKUYNXGWDAUUPUVAUWJVFAUXOUUPU
      YBTSUUMUUBXHXIXJXKXLVKXPXNAUYMUYGUSZUUGAUYKUXOUULUYSUYKAXGWDUYBUXASUUBUUD
      XQXITXPFUUCXOWSZFXRVHUUBFXSXIUXLUXNUUGAUUGVDUXLUXOUWSUVMUYEUXNUUGYFUYCUYH
      UYDUYTUUBUUCFXTYAYBBCLGUXIYCYDXKAUUGUXKUEUXLUXJLGUXLUVJQZUXJUUBUURNPZVUAU
      URUUBOPZVUBUEZAUVJVUCUUGUVKUWLVUCUWOUVKUUSVUCKAUVJUUSVUCAUVJUUSQZQZUUPVUC
      VUEUUPAVUEUUTUUPUUSLGYEUVIWFYGVUFUUPQUUMUURUUBOAUVJUUSUUPYHAUUPUYPVUEUYRY
      IYJYKYLXLYQYIVUAUVLUXOVUCVUDYFAUVJUVLUUGUWIYIAUXOUUGUVJUYBYMZUURUUBYNVHYO
      VUAUXOUVNUVMUYEUXJVUBYFVUGAUVJUVNUUGUWHYIAUVMUUGUVJUWEYMUXLUYEUVJUYTTUUBU
      UQFXTYAYPYRXKYSWIAUXOUULUUEUUIYFUYBUXAUUBUUDYTVHYBUUA $.
      $( [5-Jul-2013] $)
  $}

  ${
    $d A a b v x y w z $.  $d B a b v x y w z $.  $d C a x w $.
    $d ph a b w z $.
    supmul.1 $e |- C = { z | E. v e. A E. b e. B z = ( v x. b ) } $.
    supmul.2 $e |- ( ph <-> ( ( A. x e. A 0 <_ x /\ A. x e. B 0 <_ x ) /\
        ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) /\
        ( B C_ RR /\ B =/= (/) /\ E. x e. RR A. y e. B y <_ x ) ) ) $.
    $( Lemma for ~ supmul . $)
    supmullem1 $p |- ( ph -> A. w e. C
        w <_ ( sup ( A , RR , < ) x. sup ( B , RR , < ) ) ) $=
      ( va cv cr cle wbr wcel wrex wa csup cmul co visset opreq1 eqeq2d rexbidv
      clt wceq cbvrexv eqeq1 2rexbidv syl5bb elab2 wi wss c0 wne wral w3a simp2
      cc0 sylbi suprub sylan adantrr simp3 adantrl jca simp1d sseld imp rcla4cv
      simp1l breq2 syl suprcl adantr simp1r lemul12a syl22anc ex breq1 biimprcd
      mpd syl6 r19.23advv syl5bi r19.21aiv ) AENZGOUHUAZHOUHUAZUBUCZPQZEIWJIRWJ
      MNZJNZUBUCZUIZJHSMGSZAWNDNZFNZWPUBUCZUIZJHSZFGSZWSDWJIEUDXEWTWQUIZJHSZMGS
      WTWJUIZWSXDXGFMGXAWOUIZXCXFJHXIXBWQWTXAWOWPUBUEUFUGUJXHXFWRMJGHWTWJWQUKUL
      UMKUNAWRWNMJGHAWOGRZWPHRZTZWQWMPQZWRWNUOAXLXMAXLTZWOWKPQZWPWLPQZTZXMXNXOX
      PAXJXOXKAGOUPZGUQURZCNBNZPQZCGUSBOSZUTZXJXOAVBXTPQZBGUSZYDBHUSZTZYCHOUPZH
      UQURZYACHUSBOSZUTZUTZYCLYGYCYKVAVCZBCGWOVDVEVFAXKXPXJAYKXKXPAYLYKLYGYCYKV
      GVCZBCHWPVDVEVHVIXNWOORZVBWOPQZTWKORZWPORZVBWPPQZTWLORZXQXMUOXNYOYPAXJYOX
      KAXJYOAGOWOAXRXSYBYMVJVKVLVFAXJYPXKAXJYPAYEXJYPUOAYLYELYEYFYCYKVNVCYDYPBW
      OGXTWOVBPVOVMVPVLVFVIAYQXLAYCYQYMBCGVQVPVRXNYRYSAXKYRXJAXKYRAHOWPAYHYIYJY
      NVJVKVLVHAXKYSXJAXKYSAYFXKYSUOAYLYFLYEYFYCYKVSVCYDYSBWPHXTWPVBPVOVMVPVLVH
      VIAYTXLAYKYTYNBCHVQVPVRWOWKWPWLVTWAWEWBWRWNXMWJWQWMPWCWDWFWGWHWI $.
      $( [5-Jul-2013] $)

    $( Lemma for ~ supmul . $)
    supmullem2 $p |- ( ph ->
        ( C C_ RR /\ C =/= (/) /\ E. x e. RR A. w e. C w <_ x ) ) $=
      ( va cr cv cle wral wrex wcel wceq wss c0 wne wbr co visset opreq1 eqeq2d
      cmul rexbidv cbvrexv eqeq1 2rexbidv syl5bb elab2 wa wi simp2 sylbi simp1d
      cc0 w3a sseld simp3 anim12d remulcl syl6 eleq1 biimprcd r19.23advv syl5bi
      ssrdv wex simp2d oprex isseti rgen r19.2z sylancl rexcom4 sylib r19.21aiv
      a1i a1d syl11anc n0 exbii bitri sylibr clt csup suprcl supmullem1 ralbidv
      syl breq2 rcla4ev 3jca ) AINUAIUBUCZEOZBOZPUDZEIQZBNRZAEINWTISZWTMOZJOZUI
      UEZTZJHRZMGRZAWTNSZDOZFOZXGUIUEZTZJHRZFGRZXKDWTIEUFXRXMXHTZJHRZMGRXMWTTZX
      KXQXTFMGXNXFTZXPXSJHYBXOXHXMXNXFXGUIUGUHUJUKYAXSXIMJGHXMWTXHULUMUNKUOZAXI
      XLMJGHAXFGSZXGHSZUPZXHNSZXIXLUQAYFXFNSZXGNSZUPYGAYDYHYEYIAGNXFAGNUAZGUBUC
      ZCOXAPUDZCGQBNRZAVAXAPUDZBGQYNBHQUPZYJYKYMVBZHNUAZHUBUCZYLCHQBNRZVBZVBZYP
      LYOYPYTURUSZUTVCAHNXGAYQYRYSAUUAYTLYOYPYTVDUSZUTVCVEXFXGVFVGXIXLYGWTXHNVH
      VIVGVJVKVLAXKEVMZWSAXJEVMZMGRZUUDAYKUUEMGQUUFAYJYKYMUUBVNAUUEMGAUUEYDAXIE
      VMZJHRZUUEAYRUUGJHQUUHAYQYRYSUUCVNUUGJHUUGYEEXHXFXGUIVOVPWCVQUUGJHVRVSXIJ
      EHVTWAWDWBUUEMGVRWEXJMEGVTWAWSXEEVMUUDEIWFXEXKEYCWGWHWIAGNWJWKZHNWJWKZUIU
      EZNSZWTUUKPUDZEIQZXDAUUINSZUUJNSZUULAYPUUOUUBBCGWLWOAYTUUPUUCBCHWLWOUUIUU
      JVFWEABCDEFGHIJKLWMXCUUNBUUKNXAUUKTXBUUMEIXAUUKWTPWPWNWQWEWR $.
      $( [5-Jul-2013] $)

    $( The supremum function distributes over multiplication, in the sense that
       ` ( sup A ) x. ( sup B ) = sup ( A x. B ) ` , where ` A x. B ` is
       shorthand for ` { a x. b | a e. A , b e. B } ` and is defined as ` C `
       below.  We made use of this in our definition of multiplication in the
       Dedekind cut construction of the reals (see ~ df-mp ).  (Contributed by
       Mario Carneiro, 5-Jul-2013.) $)
    supmul $p |- ( ph ->
      ( sup ( A , RR , < ) x. sup ( B , RR , < ) ) = sup ( C , RR , < ) ) $=
      ( va vw cr wceq cle wbr wa wrex wcel clt csup cmul co cv cab wss wne wral
      c0 w3a cc0 simp2 sylbi suprcl simp3 cc recn mulcom syl2an syl11anc simp2d
      syl wex n0 sylib wi simp1r breq2 rcla4cv imp suprub 0re a1i simp1d adantr
      sylan sseld letr syl111anc mp2and ex 19.23adv mpd simp1l supmul1 syl31anc
      eqid biid eqtrd remulcl eleq1a r19.23adva abssdv oprex isseti rgen r19.2z
      sylancl rexcom4 abn0 sylibr supmullem2 visset rexbidv elab adantlr cbvexv
      eqeq1 ra4e opreq1 eqeq2d cbvrexv 2rexbidv syl5bb sylan9r syl5bi r19.21aiv
      elab2 suprleub syl22anc eqbrtrd breq1 biimprcd 3simpa supmullem1 syl12anc
      jca wb letri3 mpbird ) AFNUAUBZGNUAUBZUCUDZHNUAUBZOZYNYOPQZYOYNPQZRZAYQYR
      AYNDUEZYMLUEZUCUDZOZLFSZDUFZNUAUBZYOPAYNYMYLUCUDZUUFAYLNTZYMNTZYNUUGOZAFN
      UGZFUJUHZCUEBUEZPQZCFUIBNSZUKZUUHAULUUMPQZBFUIZUUQBGUIZRZUUPGNUGZGUJUHZUU
      NCGUIBNSZUKZUKZUUPKUUTUUPUVDUMUNZBCFUOVCZAUVDUUIAUVEUVDKUUTUUPUVDUPUNZBCG
      UOVCZUUHYLUQTYMUQTZUUJUUIYLURYMURZYLYMUSUTVAAUUIULYMPQZUURUUPUUGUUFOUVIAI
      UEZGTZIVDZUVLAUVBUVOAUVAUVBUVCUVHVBZIGVEVFAUVNUVLIAUVNUVLAUVNRZULUVMPQZUV
      MYMPQZUVLAUVNUVRAUUSUVNUVRVGAUVEUUSKUURUUSUUPUVDVHUNZUUQUVRBUVMGUUMUVMULP
      VIVJVCVKAUVDUVNUVSUVHBCGUVMVLVQUVQULNTZUVMNTZUUIUVRUVSRUVLVGUWAUVQVMVNAUV
      NUWBAGNUVMAUVAUVBUVCUVHVOVRVKZAUUIUVNUVIVPULUVMYMVSVTWAWBWCWDAUVEUURKUURU
      USUUPUVDWEUNZUVFUUIUVLUURUKUUPRZBCDLYMFUUEUUEWHUWEWIWFWGWJAUUENUGUUEUJUHZ
      YONTZMUEZYOPQZMUUEUIUUFYOPQAUUDDNAUUCYTNTZLFAUUAFTZRZUUBNTZUUCUWJVGUWLUUI
      UUANTZUWMAUUIUWKUVIVPZAUWKUWNAFNUUAAUUKUULUUOUVFVOVRVKZYMUUAWKVAUUBNYTWLV
      CWMWNAUUDDVDZUWFAUUCDVDZLFSZUWQAUULUWRLFUIUWSAUUKUULUUOUVFVBUWRLFUWRUWKDU
      UBYMUUAUCWOWPVNWQUWRLFWRWSUUCLDFWTVFUUDDXAXBAHNUGZHUJUHZUWHUUMPQMHUIBNSZU
      KZUWGABCDMEFGHIJKXCZBMHUOVCZAUWIMUUEUWHUUETUWHUUBOZLFSZAUWIUUDUXGDUWHMXDZ
      YTUWHOZUUCUXFLFYTUWHUUBXIXEXFAUXFUWILFUWLUUBYOPQZUXFUWIVGUWLUUBUUAYMUCUDZ
      YOPUWLUUIUWNUUBUXKOZUWOUWPUUIUVJUUAUQTUXLUWNUVKUUAURYMUUAUSUTVAUWLUXKYTUU
      AUVMUCUDZOZIGSZDUFZNUAUBZYOPUWLUWNULUUAPQZUUSUVDUXKUXQOUWPAUWKUXRAUURUWKU
      XRVGUWDUUQUXRBUUAFUUMUUAULPVIVJVCVKAUUSUWKUVTVPAUVDUWKUVHVPUWNUXRUUSUKUVD
      RZBCDIUUAGUXPUXPWHUXSWIWFWGUWLUXPNUGUXPUJUHZUWGUWIMUXPUIUXQYOPQUWLUXODNUW
      LUXNUWJIGUWLUVNRZUXMNTZUXNUWJVGUYAUWNUWBUYBUWLUWNUVNUWPVPAUVNUWBUWKUWCXGU
      UAUVMWKVAUXMNYTWLVCWMWNAUXTUWKAUXODVDZUXTAUWHUXMOZIGSZMVDZUYCAUYDMVDZIGSZ
      UYFAUVBUYGIGUIUYHUVPUYGIGUYGUVNMUXMUUAUVMUCWOWPVNWQUYGIGWRWSUYDIMGWTVFUXO
      UYEDMUXIUXNUYDIGYTUWHUXMXIZXEZXHXBUXODXAXBVPAUWGUWKUXEVPUWLUWIMUXPUWHUXPT
      UYEUWLUWIUXOUYEDUWHUXHUYJXFUWKUYEUWHHTZAUWIUWKUYEUYKUWKUYERUYELFSZUYKUYEL
      FXJYTEUEZUVMUCUDZOZIGSZEFSZUYLDUWHHUXHUYQUXOLFSUXIUYLUYPUXOELFUYMUUAOZUYO
      UXNIGUYRUYNUXMYTUYMUUAUVMUCXKXLXEXMUXIUXNUYDLIFGUYIXNXOJXSXBWBAUXCUYKUWIV
      GUXDUXCUYKUWIBMHUWHVLWBVCXPXQXRMUXPYOXTYAYBYBUXFUWIUXJUWHUUBYOPYCYDVCWMXQ
      XRMUUEYOXTYAYBAUWTUXARZYNNTZUWHYNPQMHUIYRAUXCUYSUXDUWTUXAUXBYEVCAUUHUUIUY
      TUVGUVIYLYMWKVAZABCDMEFGHIJKYFMHYNXTYGYHAUYTUWGYPYSYIVUAUXEYNYOYJVAYK $.
      $( [5-Jul-2013] $)
  $}

  ${
    $d x y z A $.
    sup3i.1 $e |- ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) $.
    $( A version of the completeness axiom for reals. $)
    sup3ii $p |- E. x e. RR ( A. y e. A -. x < y /\
                  A. y e. RR ( y < x -> E. z e. A y < z ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a clt wn wi wa sup3 ax-mp ) DFGDHI
      BJZAJZKLBDMAFNOUCUBPLQBDMUBUCPLUBCJPLCDNRBFMSAFNEABCDTUA $.
      $( [14-Mar-2007] $) $( [23-Aug-1999] $)

    $( Closure of supremum of a non-empty bounded set of reals. $)
    suprclii $p |- sup ( A , RR , < ) e. RR $=
      ( vz cr clt ltso sup3ii supcli ) ABEFCGHABECDIJ $.
      $( [15-May-2007] $) $( [12-Sep-1999] $)

    $( A member of a non-empty bounded set of reals is less than or equal to
       the set's upper bound. $)
    suprubii $p |- ( B e. A -> B <_ sup ( A , RR , < ) ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a wcel clt csup suprub mpan ) CFGC
      HIBJAJKLBCMAFNODCPDCFQRKLEABCDST $.
      $( [16-May-2007] $) $( [12-Sep-1999] $)

    ${
      $d z B $.
      $( The supremum of a non-empty bounded set of reals is the least upper
         bound. $)
      suprlubii $p |- ( ( B e. RR /\ B < sup ( A , RR , < ) ) ->
                    E. z e. A B < z ) $=
        ( cr clt ltso sup3ii suplubi ) ABCGDEHIABCDFJK $.
        $( [16-May-2007] $) $( [15-Oct-2004] $)

      $( An upper bound is not less than the supremum of a non-empty bounded
         set of reals. $)
      suprnubiiOLD $p |- ( ( B e. RR /\ A. z e. A -. B < z ) ->
                   -. B < sup ( A , RR , < ) ) $=
        ( cr clt ltso sup3ii supnubi ) ABCGDEHIABCDFJK $.
        $( [31-May-2007] $) $( [15-Oct-2004] $)

      $( The supremum of a non-empty bounded set of reals is less than or equal
         to an upper bound. $)
      suprleubiiOLD $p |- ( ( B e. RR /\ A. z e. A z <_ B ) ->
                   sup ( A , RR , < ) <_ B ) $=
        ( cr wss c0 wne cv cle wbr wral wrex w3a wcel wa clt csup suprleubOLD
        mpan ) DGHDIJBKAKLMBDNAGOPEGQCKELMCDNRDGSTELMFABCDEUAUB $.
        $( [1-Jun-2007] $) $( [18-Mar-2005] $)
    $}
  $}

  ${
    $d z A $.  $d z B $.
    suprnubii.1 $e |- ( A C_ RR /\ A =/= (/) ) $.
    $( An upper bound is not less than the supremum of a non-empty bounded set
       of reals. $)
    suprnubii $p |- ( ( B e. RR /\ A. z e. A -. B < z ) ->
                 -. B < sup ( A , RR , < ) ) $=
      ( cr wss c0 wne wa wcel cv clt wbr wn wral csup suprnub mpan ) BEFBGHICEJ
      CAKLMNABOICBELPLMNDABCQR $.
      $( [22-May-2013] $) $( [15-Oct-2004] $)

    $( The supremum of a non-empty bounded set of reals is less than or equal
       to an upper bound. $)
    suprleubii $p |- ( ( B e. RR /\ A. z e. A z <_ B ) ->
                 sup ( A , RR , < ) <_ B ) $=
      ( cr wss c0 wne wa wcel cv cle wbr wral clt csup suprleub mpan ) BEFBGHIC
      EJAKCLMABNIBEOPCLMDABCQR $.
      $( [22-May-2013] $) $( [18-Mar-2005] $)
  $}

  ${
    $d x y z $.  $d z A $.  $d y z ph $.  $d x z ps $.
    reuunineg.1 $e |- ( x = -u y -> ( ph <-> ps ) ) $.
    $( The negative of the unique real such that ` ph ` . $)
    reuunineg $p |- ( E! x e. RR ph ->
                U. { x e. RR | ph } = -u U. { y e. RR | ps } ) $=
      ( vz cr cv cneg crab cuni hbrab1 hbuni hbneg renegcl negeq wcel wceq recn
      cc wb negcon2 syl2an reuhyp reuunixfr ) ABCDFGDHZIZBDGJZKZIDFUIDFUHBDFGLM
      NUFOUIOEUFUIPCDUGCHZIZGUJOUJGQUJTQUFTQUJUGRUFUKRUAUFGQUJSUFSUJUFUBUCUDUE
      $.
      $( [13-Jun-2005] $)
  $}

  ${
    $d x y z w v u t A $.
    $( The infimum (expressed as supremum with converse 'less-than') of a set
       of reals ` A ` . $)
    dfinfmr $p |- ( A C_ RR -> sup ( A , RR , `' < ) = U. { x e. RR |
        ( A. y e. A x <_ y /\ A. y e. RR ( x < y -> E. z e. A z < y ) ) } ) $=
      ( cr wss clt cv wbr wn wral wrex wi wa crab cuni wcel wb visset brcnv cle
      ccnv csup df-sup ssel2 lenlt notbii syl6rbbr sylan2 ancoms an32s ralbidva
      rexbii imbi12i ralbii a1i anbi12d rabbidva unieqd syl5eq ) DEFZDEGUBZUCAH
      ZBHZVBIZJZBDKZVDVCVBIZVDCHZVBIZCDLZMZBEKZNZAEOZPVCVDUAIZBDKZVCVDGIZVIVDGI
      ZCDLZMZBEKZNZAEOZPABCDEVBUDVAVOWDVAVNWCAEVAVCEQZNZVGVQVMWBWFVFVPBDVAVDDQZ
      WEVFVPRZWEVAWGNZWHWIWEVDEQZWHDEVDUEWEWJNVPVDVCGIZJVFVCVDUFVEWKVCVDGASZBSZ
      TUGUHUIUJUKULVMWBRWFVLWABEVHVRVKVTVDVCGWMWLTVJVSCDVDVIGWMCSTUMUNUOUPUQURU
      SUT $.
      $( [9-Oct-2005] $)

    $( The infimum (expressed as supremum with converse 'less-than') of a set
       of reals ` A ` is the negative of the supremum of the negatives of its
       elements.  The antecedent ensures that ` A ` is nonempty and has a lower
       bound. $)
    infmsup $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A x <_ y ) ->
                   sup ( A , RR , `' < ) =
                 -u sup ( { z e. RR | -u z e. A } , RR , < ) ) $=
      ( vw vv vu vt cr cv wbr wral wrex clt cneg wcel crab wceq wi wa ccnv csup
      wss c0 wne cle wn infm3 3exp wreu wor ltso cnvso mpbi supeu visset notbii
      brcnv ralbii rexbii imbi12i anbi12i reubii 3imtr3i cuni df-sup wb rabbiia
      a1i unieqi eqtri breq2 notbid ralbidv imbi1d anbi12d reuunineg syl5eq wal
      breq1 negeq eleq1d elrab imbi1i impexp bitri albii df-ral 3bitr4ri imbi2d
      ltneg ralbidva syl5bbr ancoms anbi1i anass rexbii2 anbi2d rexbidva syl5bb
      adantl imbi12d ssel pm4.71rd syl6rbb albidv renegcl infm3lem eleq1 ralxfr
      bitr3i 3bitr4g rexbidv wex adantrd exbidv df-rex rexxfr syl5rbb sylan9bbr
      rabbidva unieqd negeqd eqcomd sylan9eqr ex syl5 syl6d 3imp ) DIUCZDUDUEZA
      JZBJZUFKBDLAIMZDINUAZUBZCJZOZDPZCIQZINUBZOZRZYJYKYNYMYLNKZUGZBDLZYLYMNKZE
      JZYMNKZEDMZSZBILZTZAIMZUUCYJYKYNUUNABEDUHUIUUNUUMAIUJZYJUUCYLYMYOKZUGZBDL
      ZYMYLYOKZYMUUHYOKZEDMZSZBILZTZAIMUVDAIUJUUNUUOABEIDYOINUKIYOUKULINUMUNUOU
      VDUUMAIUURUUFUVCUULUUQUUEBDUUPUUDYLYMNAUPZBUPZURUQUSUVBUUKBIUUSUUGUVAUUJY
      MYLNUVFUVEURUUTUUIEDYMUUHNUVFEUPURUTVAUSVBZUTUVDUUMAIUVGVCVDYJUUOUUCUUOYJ
      YPYMFJZOZNKZUGZBDLZUVIYMNKZUUJSZBILZTZFIQZVEZOZUUBUUOYPUUMAIQZVEZUVSYPUVD
      AIQZVEUWAABEDIYOVFUWBUVTUVDUUMAIUVDUUMVGYLIPUVGVIVHVJVKUUMUVPAFYLUVIRZUUF
      UVLUULUVOUWCUUEUVKBDUWCUUDUVJYLUVIYMNVLVMVNUWCUUKUVNBIUWCUUGUVMUUJYLUVIYM
      NVTVOVNVPVQVRYJUUBUVSYJUUAUVRYJUUAUVHGJZNKZUGZGYTLZUWDUVHNKZUWDHJZNKZHYTM
      ZSZGILZTZFIQZVEUVRFGHYTINVFYJUWOUVQYJUWNUVPFIUVHIPZUWNUWDOZDPZUWQUVINKZUG
      ZSZGILZUVIUWQNKZUWIOZDPZUXDUWQNKZTZHIMZSZGILZTYJUVPUWPUWGUXBUWMUXJUWGUWRU
      WFSZGILZUWPUXBUWDYTPZUWFSZGVSUWDIPZUXKSZGVSUWGUXLUXNUXPGUXNUXOUWRTZUWFSUX
      PUXMUXQUWFYSUWRCUWDIYQUWDRYRUWQDYQUWDWAWBWCWDUXOUWRUWFWEWFWGUWFGYTWHUXKGI
      WHWIUWPUXKUXAGIUWPUXOTZUWFUWTUWRUXRUWEUWSUVHUWDWKVMWJWLWMUWPUWLUXIGIUXRUW
      HUXCUWKUXHUXOUWPUWHUXCVGUWDUVHWKWNUXOUWKUXHVGUWPUWKUXEUWJTZHIMUXOUXHUWJUX
      SHYTIUWIYTPZUWJTUWIIPZUXETZUWJTUYAUXSTUXTUYBUWJYSUXECUWIIYQUWIRYRUXDDYQUW
      IWAWBWCWOUYAUXEUWJWPWFWQUXOUXSUXGHIUXOUYATUWJUXFUXEUWDUWIWKWRWSWTXAXBWLVP
      YJUXBUVLUXJUVOYJYMIPZYMDPZUVKSZSZBVSZUYEBVSUXBUVLYJUYFUYEBYJUYEUYCUYDTZUV
      KSUYFYJUYDUYHUVKYJUYDUYCDIYMXCXDVOUYCUYDUVKWEXEXFUXBUYEBILUYGUYEUXABGUWQI
      UWDXGZBGXHZYMUWQRZUYDUWRUVKUWTYMUWQDXIUYKUVJUWSYMUWQUVINVTVMXBXJUYEBIWHXK
      UVKBDWHXLUVOUXCUUHUWQNKZEDMZSZGILYJUXJUVNUYNBGUWQIUYIUYJUYKUVMUXCUUJUYMYM
      UWQUVINVLUYKUUIUYLEDYMUWQUUHNVLXMXBXJYJUYNUXIGIYJUYMUXHUXCYJUUHDPZUYLTZEX
      NUUHIPZUYPTZEXNZUYMUXHYJUYPUYREYJUYPUYQYJUYOUYQUYLDIUUHXCXOXDXPUYLEDXQUXH
      UYPEIMUYSUYPUXGEHUXDIUWIXGEHXHUUHUXDRUYOUXEUYLUXFUUHUXDDXIUUHUXDUWQNVTVPX
      RUYPEIXQXKXLWJVNXSVPXTYAYBVRYCYDYEYFYGYHYI $.
      $( [13-May-2007] $) $( [14-Jun-2005] $)

    $( Closure of infimum of a non-empty bounded set of reals. $)
    infmrcl $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A x <_ y ) ->
                  sup ( A , RR , `' < ) e. RR ) $=
      ( vv vw vz cr wss c0 wne cv cle wbr wral cneg wcel wa renegcl syl6 wceq
      wrex w3a clt ccnv csup crab infmsup wex n0 ssel cc ssel2 recn negneg 3syl
      simpr eqeltrd ex jcad negeq eleq1d elrab ne0i sylbir 19.23adv imp sylan2b
      3adant3 wi breq2 rcla4va adantll wb lenegcon2 adantrr adantr mpbid syl5bi
      exp31 r19.21adv jctild ralbidv rcla4ev r19.23aiv 3ad2ant3 ssrab2 syl11anc
      com23 suprcl mp3an1 syl ) CGHZCIJZAKZBKZLMZBCNZAGUAZUBZCGUCUDUEDKZOZCPZDG
      UFZGUCUEZOZGABDCUGWSXDGPZXEGPWSXCIJZEKZFKZLMZEXCNZFGUAZXFWLWMXGWRWMWLXICP
      ZFUHZXGFCUIWLXNXGWLXMXGFWLXMXIOZGPZXOOZCPZQZXGWLXMXPXRWLXMXIGPZXPCGXIUJXI
      RSWLXMXRWLXMQZXQXICYAXTXIUKPXQXITCGXIULXIUMXIUNUOWLXMUPUQURUSXSXOXCPXGXBX
      RDXOGWTXOTXAXQCWTXOUTVAVBXCXOVCVDSVEVFVGVHWRWLXLWMWQXLAGWNGPZWQWNOZGPZXHY
      CLMZEXCNZQXLYBWQYFYDYBWQYEEXCYBXHXCPZWQYEYGXHGPZXHOZCPZQZYBWQYEVIXBYJDXHG
      WTXHTXAYICWTXHUTVAVBYBYKWQYEYBYKQZWQQWNYILMZYEYKWQYMYBYJWQYMYHWPYMBYICWOY
      IWNLVJVKVLVLYLYMYEVMZWQYBYHYNYJWNXHVNVOVPVQVSVRWHVTWNRWAXKYFFYCGXIYCTXJYE
      EXCXIYCXHLVJWBWCSWDWEXCGHXGXLXFXBDGWFFEXCWIWJWGXDRWKUQ $.
      $( [14-May-2007] $) $( [8-Oct-2005] $)
  $}

  ${
    $d x y z $.
    $( The set of natural numbers is unbounded above.  Theorem I.28 of
       [Apostol] p. 26. $)
    nnunb $p |- -. E. x e. RR A. y e. NN ( y < x \/ y = x ) $=
      ( vz cv clt wbr wceq cn wral cr wrex wn wi wa wcel wal wex c1 cmin c0 1re
      wo co peano2rem ltm1 oprex eleq1 breq1 rexbidv imbi12d cla4v syl7 pm2.43d
      pm3.24 syl5 df-rex syl6ib com12 wb ltsubadd mp3an2 sylan2 pm5.32da exbidv
      caddc nnre peano2nn breq2 anbi12d cla4ev sylan syl6bi syld df-ral alinexa
      19.23aiv bitr2i con1bii 3imtr4g anim2d mtoi nrex wss wne nnssre 1nn ax-mp
      n0i df-ne mpbir sup2 mp3an12 mto ) BDZADZEFZWNWOGUBBHIAJKZWOWNEFZLZBHIZWP
      WNCDZEFZCHKZMZBJIZNZAJKZXFAJWOJOZXFWTWTLZNWTUNXHXEXIWTXHWNJOZXDMZBPZWNHOZ
      WRNZBQZXEXIXHXLXAHOZWORSUCZXAEFZNZCQZXOXLXHXTXLXHXRCHKZXTXLXHYAXHXQJOZXLX
      HYAMWOUDXHXQWOEFZXLYBYAWOUEXKYBYCYAMZMBXQWORSUFWNXQGZXJYBXDYDWNXQJUGYEWPY
      CXCYAWNXQWOEUHYEXBXRCHWNXQXAEUHUIUJUJUKULUOUMXRCHUPUQURXHXTXPWOXARVEUCZEF
      ZNZCQXOXHXSYHCXHXPXRYGXPXHXAJOZXRYGUSZXAVFXHRJOYIYJUAWORXAUTVAVBVCVDYHXOC
      XPYFHOZYGXOXAVGXNYKYGNBYFXARVEUFWNYFGXMYKWRYGWNYFHUGWNYFWOEVHVIVJVKVPVLVM
      XDBJVNXOWTWTXMWSMBPXOLWSBHVNXMWRBVOVQVRVSVTWAWBHJWCHTWDZWQXGWEYLHTGLZRHOY
      MWFHRWHWGHTWIWJABCHWKWLWM $.
      $( [21-Jan-1997] $)
  $}

  ${
    $d n y A $.
    $( Archimedean property of real numbers.  For any real number, there is an
       integer greater than it.  Theorem I.29 of [Apostol] p. 26. $)
    arch $p |- ( A e. RR -> E. n e. NN A < n ) $=
      ( vy cv clt wbr cn wrex cr wceq breq1 rexbidv wo wral wn nnunb mpbir wcel
      ralnex rexnal wa wb axlttri sylan2 eqcom orbi1i orcom bitri notbii syl6bb
      nnre biimprd reximdva syl5bir ralimia ax-mp vtoclri ) CDZBDZEFZBGHZAUSEFZ
      BGHCAIURAJUTVBBGURAUSEKLUSUREFZUSURJZMZBGNZOZCINZVACINVHVFCIHOCBPVFCISQVG
      VACIVGVEOZBGHURIRZVAVEBGTVJVIUTBGVJUSGRZUAZUTVIVLUTURUSJZVCMZOZVIVKVJUSIR
      UTVOUBUSUKURUSUCUDVNVEVNVDVCMVEVMVDVCURUSUEUFVDVCUGUHUIUJULUMUNUOUPUQ $.
      $( [9-Nov-2004] $) $( [21-Jan-1997] $)
  $}

  ${
    $d n A $.
    $( There exists a natural number whose reciprocal is less than a given
       positive real.  Exercise 3 of [Apostol] p. 28. $)
    nnrecl $p |- ( ( A e. RR /\ 0 < A ) -> E. n e. NN ( 1 / n ) < A ) $=
      ( cr wcel cc0 clt wbr wa c1 cdiv co cv wrex wne simpl syl11anc jca adantr
      cn wb 1re jctil gt0ne0 redivcl 3expa arch syl recgt0 nnre nngt0 syl2an cc
      ltrec wceq recn recrec breq2d bitrd rexbidva mpbid ) ACDZEAFGZHZIAJKZBLZF
      GZBSMZIVEJKZAFGZBSMVCVDCDZVGVCICDZVAHAENZVJVCVAVKVAVBOUAUBAUCZVKVAVLVJIAU
      DUEPZVDBUFUGVCVFVIBSVCVESDZHVFVHIVDJKZFGZVIVCVJEVDFGZHVECDZEVEFGZHVFVQTVO
      VCVJVRVNAUHQVOVSVTVEUIVEUJQVDVEUMUKVCVQVITVOVCVPAVHFVCAULDZVLVPAUNVAWAVBA
      UORVMAUPPUQRURUSUT $.
      $( [26-Jul-2006] $) $( [8-Nov-2004] $)
  $}

  ${
    $d x A $.  $d x k $.
    $( A bounded real sequence ` A ( k ) ` is less than or equal to at least
       one of its indices. $)
    bndndx $p |- ( E. x e. RR A. k e. NN ( A e. RR /\ A <_ x ) ->
                  E. k e. NN A <_ k ) $=
      ( cr wcel cv cle wbr wa cn wral wrex wi clt arch nnre lelttr ltle 3adant2
      w3a syld exp5o com3l imp4b com23 sylan2 reximdva r19.35 sylib r19.23aiv
      mpd ) BDEZBAFZGHZIZCJKZBCFZGHZCJLZADUMDEZUOURMZCJLZUPUSMUTUMUQNHZCJLVBUMC
      OUTVCVACJUQJEUTUQDEZVCVAMUQPUTVDIUOVCURUTVDULUNVCURMZULUTVDUNVEMULUTVDUNV
      CURULUTVDTUNVCIBUQNHZURBUMUQQULVDVFURMUTBUQRSUAUBUCUDUEUFUGUKUOURCJUHUIUJ
      $.
      $( [18-Jan-2008] $)
  $}

