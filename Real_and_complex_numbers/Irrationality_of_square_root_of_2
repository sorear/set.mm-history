$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Irrationality of square root of 2
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    sqr2irrlem1.1 $e |- A e. NN $.
    sqr2irrlem1.2 $e |- B e. NN $.
    $( Lemma for irrationality of square root of 2.  Technical lemma used to
       simplify the main induction step. $)
    sqr2irrlem1 $p |- ( ( A ^ 2 ) = ( 2 x. ( B ^ 2 ) ) ->
                      ( ( B < A /\ ( A / 2 ) e. NN ) /\
                     ( B ^ 2 ) = ( 2 x. ( ( A / 2 ) ^ 2 ) ) ) ) $=
      ( c2 cexp co cmul wceq clt wbr cdiv cn wcel c1 nnrei resqcli recni
      mulid2i 1lt2 1re 2re nnsqcli nngt0i ltmul1ii mpbi eqbrtrri breq2 mpbiri
      cc0 cle wb 0re ltleii lt2sqi mp2an sylibr 2cn 2ne0 divmuli eleq1 nnesqi
      sylbir eqcoms redivcli remulcli mulcani nncni sqdivi sqvali opreq2i
      eqtri mulassi mulcli mulne0i divcan2i 3eqtr3i eqeq1i bitr3i biimpri
      eqcomd jca31 ) AEFGZEBEFGZHGZIZBAJKZAELGZMNZWDEWHEFGZHGZIWFWDWCJKZWGWFWLW
      DWEJKOWDHGZWDWEJWDWDBBDPZQZRZSOEJKWMWEJKTOEWDUAUBWOWDBDUCZUDUEUFUGWCWEWDJ
      UHUIUJBUKKUJAUKKWGWLULUJBUMWNBDUDUNUJAUMACPZACUDUNBAWNWRUOUPUQWIWEWCWEWCI
      WCELGZWDIZWIWCEWDWCAWRQRZURWPUSUTWTWSMNZWIWTXBWDMNWQWSWDMVAUIACVBUQVCVDWF
      WKWDWKWDIZWFXCEWKHGZWEIWFWKWDEWKEWJUBWHAEWRUBUSVEQZVFRWPURUSVGXDWCWEEEHGZ
      WJHGXFWCXFLGZHGXDWCWJXGXFHWJWCEEFGZLGXGAEACVHURUSVIXHXFWCLEURVJVKVLVKEEWJ
      URURWJXERVMWCXFXAEEURURVNEEURURUSUSVOVPVQVRVSVTWAWB $.
      $( [20-Aug-2001] $)
  $}

  $( Lemma for irrationality of square root of 2.  Eliminates hypotheses with
     weak deduction theorem. $)
  sqr2irrlem2 $p |- ( ( A e. NN /\ B e. NN ) ->
                   ( ( A ^ 2 ) = ( 2 x. ( B ^ 2 ) ) ->
                    ( ( B < A /\ ( A / 2 ) e. NN ) /\
                   ( B ^ 2 ) = ( 2 x. ( ( A / 2 ) ^ 2 ) ) ) ) ) $=
    ( cn wcel c2 cexp co cmul wceq clt wbr cdiv wa wi cif opreq1 eqeq1d breq2
    eleq1d anbi12d opreq1d opreq2d eqeq2d imbi12d breq1 anbi1d 2nn elimel
    sqr2irrlem1 dedth2h ) ACDZBCDZAEFGZEBEFGZHGZIZBAJKZAELGZCDZMZUNEUREFGZHGZIZ
    MZNUKAEOZEFGZUOIZBVEJKZVEELGZCDZMZUNEVIEFGZHGZIZMZNVFEULBEOZEFGZHGZIZVPVEJK
    ZVJMZVQVMIZMZNABEEAVEIZUPVGVDVOWDUMVFUOAVEEFPQWDUTVKVCVNWDUQVHUSVJAVEBJRWDU
    RVICAVEELPZSTWDVBVMUNWDVAVLEHWDURVIEFWEUAUBUCTUDBVPIZVGVSVOWCWFUOVRVFWFUNVQ
    EHBVPEFPZUBUCWFVKWAVNWBWFVHVTVJBVPVEJUEUFWFUNVQVMWGQTUDVEVPAECUGUHBECUGUHUI
    UJ $.
    $( [20-Aug-2001] $)

  ${
    $d x y z w $.
    $( Main theorem for irrationality of square root of 2.  There are no
       natural numbers such that the square of one is twice the square of the
       other.  Uses strong induction. $)
    sqr2irrlem3 $p |- -. E. x e. NN E. y e. NN
                            ( x ^ 2 ) = ( 2 x. ( y ^ 2 ) ) $=
      ( vz vw cv c2 cexp co cmul wceq cn wrex wcel wn wral opreq1 eqeq1d
      notbid ralbidv opreq2d eqeq2d cbvralv syl6bb clt wbr wi wa cdiv breq1
      imbi12d rcla4cva rcla4cv syl6 imp3a imnan sylib adantll sqr2irrlem2
      adantlr mtod exp31 r19.21adv indstr ralnex nrex ) AEZFGHZFBEZFGHZIHZJZBKL
      ZAKVFKMZVKNZBKOZVLNVOCEZFGHZFDEZFGHZIHZJZNZDKOZACVFVPJZVOVQVJJZNZBKOWCWDV
      NWFBKWDVKWEWDVGVQVJVFVPFGPQRSWFWBBDKVHVRJZWEWAWGVJVTVQWGVIVSFIVHVRFGPTUAR
      UBUCVMVPVFUDUEZWCUFZCKOZVNBKVMWJVHKMZVNVMWJUGWKUGVKVHVFUDUEZVFFUHHZKMZUGZ
      VIFWMFGHZIHZJZUGZWJWKWSNZVMWJWKUGZWOWRNZUFWTXAWLWNXBXAWLVIVTJZNZDKOZWNXBU
      FWIWLXEUFCVHKVPVHJZWHWLWCXEVPVHVFUDUIXFWBXDDKXFWAXCXFVQVIVTVPVHFGPQRSUJUK
      XDXBDWMKVRWMJZXCWRXGVTWQVIXGVSWPFIVRWMFGPTUARULUMUNWOWRUOUPUQVMWKVKWSUFWJ
      VFVHURUSUTVAVBVCVKBKVDUPVE $.
      $( [20-Aug-2001] $)
  $}

  ${
    sqr2irrlem4.1 $e |- A e. NN $.
    sqr2irrlem4.2 $e |- B e. NN $.
    $( Lemma for irrationality of square root of 2. $)
    sqr2irrlem4 $p |- ( ( sqr ` 2 ) = ( A / B ) <->
                      ( A ^ 2 ) = ( 2 x. ( B ^ 2 ) ) ) $=
      ( c2 cexp co cmul wceq cdiv csqr cfv nncni nnne0i sqdivi eqcomi 2cn
      nnsqcli divcan4i eqeq12i nnrei resqcli recni 2re remulcli div11i eqcom
      3bitr3i cc0 cle wbr wb 0re 2pos ltleii redivcli sqge0i sqr11i mp2an
      nngt0i divgt0ii sqrsqi ax-mp eqeq2i 3bitr2ri ) AEFGZEBEFGZHGZIZEABJGZEFGZ
      IZEKLZVKKLZIZVMVJIVFVGJGZVHVGJGZIVKEIVIVLVPVKVQEVKVPABACMBDMBDNZOPEVGQVGB
      DRZMZVGVSNZSTVFVHVGVFAACUAZUBUCVHEVGUDVGVSUAUEUCVTWAUFVKEUGUHUIEUJUKUIVKU
      JUKVOVLULUIEUMUDUNUOVJABWBBDUAZVRUPZUQEVKUDVJWDUBURUSVNVJVMUIVJUJUKVNVJIU
      IVJUMWDABWBWCACUTBDUTVAUOVJWDVBVCVDVE $.
      $( [20-Aug-2001] $)
  $}

  $( Lemma for irrationality of square root of 2.  Eliminates hypotheses with
     weak deduction theorem. $)
  sqr2irrlem5 $p |- ( ( A e. NN /\ B e. NN ) -> ( ( sqr ` 2 ) = ( A / B )
                     <-> ( A ^ 2 ) = ( 2 x. ( B ^ 2 ) ) ) ) $=
    ( cn wcel c2 csqr cfv cdiv co wceq cexp cmul wb cif opreq1 eqeq2d eqeq1d
    bibi12d opreq2 opreq2d 2nn elimel sqr2irrlem4 dedth2h ) ACDZBCDZEFGZABHIZJZ
    AEKIZEBEKIZLIZJZMUGUEAENZBHIZJZUNEKIZULJZMUGUNUFBENZHIZJZUQEUSEKIZLIZJZMABE
    EAUNJZUIUPUMURVEUHUOUGAUNBHOPVEUJUQULAUNEKOQRBUSJZUPVAURVDVFUOUTUGBUSUNHSPV
    FULVCUQVFUKVBELBUSEKOTPRUNUSAECUAUBBECUAUBUCUD $.
    $( [20-Aug-2001] $)

  ${
    $d x y $.
    $( The square root of 2 is irrational. $)
    sqr2irr $p |- ( sqr ` 2 ) e/ QQ $=
      ( vx vy c2 csqr cfv cq wnel wcel wn cv cdiv co wceq cn wrex cz cexp cmul
      sqr2irrlem3 sqr2irrlem5 2rexbiia mtbir cc0 clt wbr wa 2re 2pos sqrgt0ii
      breq2 mpbii wb nngt0 adantr cr wi nnre zre 0re w3a ltmuldivOLD ex mp3an1
      syl2an mpd ancoms syl5ibr cc nncn mul02 syl breq1d adantl sylibd
      r19.23adva anc2li elnnz syl6ibr impac reximi2 mto elq df-nel mpbir ) CDEZ
      FGWEFHZIWFWEAJZBJZKLZMZBNOZAPOZWLWKANOZWMWGCQLCWHCQLRLMZBNOANOABSWJWNABNN
      WGWHTUAUBWKWKAPNWGPHZWKWGNHZWOWKWOUCWGUDUEZUFWPWOWKWQWOWJWQBNWOWHNHZUFZWJ
      UCWHRLZWGUDUEZWQWJXAWSUCWIUDUEZWJUCWEUDUEXBCUGUHUIWEWIUCUDUJUKWRWOXAXBULZ
      WRWOUFUCWHUDUEZXCWRXDWOWHUMUNWRWHUOHZWGUOHZXDXCUPZWOWHUQWGURUCUOHZXEXFXGU
      SXHXEXFUTXDXCUCWHWGVAVBVCVDVEVFVGWRXAWQULWOWRWTUCWGUDWRWHVHHWTUCMWHVIWHVJ
      VKVLVMVNVOVPWGVQVRVSVTWAABWEWBUBWEFWCWD $.
      $( [8-Jan-2002] $)
  $}

  $( The square root of 2 exists and is a real number. $)
  sqr2re $p |- ( sqr ` 2 ) e. RR $=
    ( c2 2re 2pos sqrpclii ) ABCD $.
    $( [2-Nov-2004] $) $( [3-Dec-2004] $)

  ${
    $( The sequence ` NN ` , ` ZZ ` , ` QQ ` , ` RR ` , and ` CC ` forms a
       chain of proper subsets.  In each case the proper subset relationship is
       shown by demonstrating a number that belongs to one set but not the
       other.  We show that zero belongs to ` ZZ ` but not ` NN ` , one-half
       belongs to ` QQ ` but not ` ZZ ` , the square root of 2 belongs to
       ` RR ` but not ` QQ ` , and finally that the imaginary number ` _i `
       belongs to ` CC ` but not ` RR ` .  See ~ nthruz for a further
       refinement. $)
    nthruc $p |- ( ( NN C. ZZ /\ ZZ C. QQ ) /\ ( QQ C. RR /\ RR C. CC ) ) $=
      ( cn cz wpss cq wa cr cc wss cc0 wcel wn nnssz 0z 0nnn pm3.2i ssnelpss
      mp2 c1 c2 cdiv co zssq 1z 2nn znq mp2an halfnz csqr cfv qssre sqr2re
      wnel sqr2irr df-nel mpbi ci ax-resscn ax-icn inelr ) ABCZBDCZEDFCZFGCZEUT
      VAABHIBJZIAJKZEUTLVDVEMNOABIPQBDHRSTUAZDJZVFBJKZEVAUBVGVHRBJSAJVGUCUDRSUE
      UFUGOBDVFPQOVBVCDFHSUHUIZFJZVIDJKZEVBUJVJVKUKVIDULVKUMVIDUNUOODFVIPQFGHUP
      GJZUPFJKZEVCUQVLVMURUSOFGUPPQOO $.
      $( [12-Jan-2002] $)
  $}

  ${
    $( The sequence ` NN ` , ` NN0 ` , and ` ZZ ` forms a chain of proper
       subsets.  In each case the proper subset relationship is shown by
       demonstrating a number that belongs to one set but not the other.  We
       show that zero belongs to ` NN0 ` but not ` NN ` and minus one belongs
       to ` ZZ ` but not ` NN0 ` .  This theorem refines the chain of proper
       subsets ~ nthruc . $)
    nthruz $p |- ( NN C. NN0 /\ NN0 C. ZZ ) $=
      ( cn cn0 wpss cz wss cc0 wcel wn wa nnssnn0 0nn0 0nnn pm3.2i ssnelpss
      mp2 c1 cneg nn0ssz 1nn nnnegz ax-mp cr clt wbr 1re renegcli neg0 lt01
      eqbrtri 0re ltnegcon1i mpbir lt0nnn0 mp2an ) ABCZBDCZABEFBGZFAGHZIUOJUQUR
      KLMABFNOBDEPQZDGZUSBGHZIUPRUTVAPAGUTSPTUAUSUBGUSFUCUDZVAPUEUFVBFQZPUCUDVC
      FPUCUGUHUIPFUEUJUKULUSUMUNMBDUSNOM $.
      $( [9-May-2004] $)
  $}


