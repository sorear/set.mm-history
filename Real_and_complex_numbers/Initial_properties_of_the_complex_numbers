$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Initial properties of the complex numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    readdcani.1 $e |- A e. RR $.
    readdcani.2 $e |- B e. RR $.
    readdcani.3 $e |- C e. RR $.
    $( Cancellation law for addition over the reals.  (Contributed by Scott
       Fenton, 3-Jan-2013.) $)
    readdcani $p |- ( ( C + A ) = ( C + B ) <-> A = B ) $=
      ( caddc co wceq clt wbr readdcli ltnri breq1 mtbiri ltadd2i sylnibr breq2
      wn lttri3i sylanbrc opreq2 impbii ) CAGHZCBGHZIZABIZUFABJKZSBAJKZSUGUFUDU
      EJKZUHUFUJUEUEJKZUECBFELMZUDUEUEJNOABCDEFPQUFUEUDJKZUIUFUMUKULUDUEUEJROBA
      CEDFPQABDETUAABCGUBUC $.
      $( [3-Jan-2013] $)
  $}

  $( Cancellation law for addition over the reals. $)
  readdcan $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( C + A ) = ( C + B ) <-> A = B ) ) $=
    ( cr wcel caddc co wceq wb c1 cif opreq2 eqeq1d eqeq1 bibi12d eqeq2d opreq1
    eqeq2 1re elimel eqeq12d bibi1d readdcani dedth3h ) ADEZBDEZCDEZCAFGZCBFGZH
    ZABHZICUEAJKZFGZUIHZULBHZIUMCUFBJKZFGZHZULUPHZIUGCJKZULFGZUTUPFGZHZUSIABCJJ
    JAULHZUJUNUKUOVDUHUMUIAULCFLMAULBNOBUPHZUNURUOUSVEUIUQUMBUPCFLPBUPULROCUTHZ
    URVCUSVFUMVAUQVBCUTULFQCUTUPFQUAUBULUPUTAJDSTBJDSTCJDSTUCUD $.
    $( [3-Jan-2013] $)

  ${
    $d c y $.
    $( ` 0 ` is its own additive identity.  (Contributed by Scott Fenton,
       3-Jan-2013.) $)
    00id $p |- ( 0 + 0 ) = 0 $=
      ( vc vy cc0 cv caddc co wceq cr wcel 0re wa cmul c1 cc recnd 0cn syl11anc
      eqtr3d opreq1d remulcl wrex ax-rnegex wi opreq2 eqeq1d biimpd adantld wne
      ax-mp adantlr simplll simprl mulass mp3an3 opreq1 mulid2i syl6eq ad2antll
      ax-rrecex simpllr ad2antrl adddir mp3an1 sylancr addass eqtr2d wb readdcl
      mpan2 sylan2 readdcan mp3an2 mpbid exp32 rexlimdv mpd pm2.61ine rexlimiva
      sylancl expcom ) CADZEFZCGZAHUAZCCEFZCGZCHIZWDJACUBUIWCWFAHWAHIZWCKZWFUCW
      ACWACGZWCWFWHWJWCWFWJWBWECWACCEUDUEUFUGWIWACUHZWFWIWKKZWABDZLFZMGZBHUAZWF
      WHWKWPWCBWAUSUJWLWOWFBHWLWMHIZWOWFWLWQWOKZKZWAWMCLFZLFZCEFZWECWSXACCEWSWN
      CLFZXACWSWANIZWMNIZXCXAGZWSWAWHWCWKWRUKZOZWSWMWLWQWOULZOXDXECNIZXFPWAWMCU
      MUNQWOXCCGWLWQWOXCMCLFCWNMCLUOCPUPUQURRSWSCWTLFZXBEFZXKCEFZGZXBCGZWSXMXKX
      AEFZCEFZXLWSXKXPCEWSWBWTLFZXKXPWSWBCWTLWHWCWKWRUTSWSXDWTNIZXRXPGZXHWSWTWQ
      WTHIZWLWOWQWGYAJWMCTVIZVAZOXJXDXSXTPCWAWTVBVCQRSWSXKNIZXANIZXQXLGZWSXKWQX
      KHIZWLWOWQWGYAYGJYBCWTTVDVAZOWSXAWSWHYAXAHIZXGYCWAWTTZQOYDYEXJYFPXKXACVEU
      NQVFWSXBHIZYGXNXOVGZWSWHWQYKXGXIWHWQKYIWGYKWQWHYAYIYBYJVJJXACVHVSQYHYKWGY
      GYLJXBCXKVKVLQVMRVNVOVPVTVQVRUI $.
      $( [3-Jan-2013] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( Lemma for ~ mul02 .  If any real does not produce ` 0 ` when multiplied
       by ` 0 ` , then any complex is equal to double itself. $)
    mul02lem1 $p |- ( ( ( A e. RR /\ -. ( 0 x. A ) = 0 ) /\ B e. CC ) ->
                       B = ( B + B ) ) $=
      ( vy cr wcel cc0 cmul co wceq wn wa cc c1 caddc recnd syl11anc 0cn mp3an3
      mulcl eqtrd cv wrex wne 0re ax-mulrcl mpan df-ne biimpri ax-rrecex syl2an
      adantr opreq2i eqcomi simprl simplll simplr mul32 simprr opreq1d ad2antlr
      00id mul31 mulid2 adddi mp3an23 syl opreq12d eqeq12d mpbii exp32 rexlimdv
      3eqtrd mpd ) ADEZFAGHZFIJZKZBLEZKZVOCUAZGHZMIZCDUBZBBBNHZIZVQWCVRVNVODEZV
      OFUCZWCVPFDEVNWFUDFAUEUFWGVPVOFUGUHCVOUIUJUKVSWBWECDVSVTDEZWBWEVSWHWBKZKZ
      VTAGHZBGHZFGHZWLFFNHZGHZIWEWOWMWNFWLGVAULUMWJWMBWOWDWJWMWKFGHZBGHZMBGHZBW
      JWKLEZVRWMWQIZWJVTLEZALEZWSWJVTVSWHWBUNOZWJAVNVPVRWIUOOZVTASPZVQVRWIUPZWS
      VRFLEZWTQWKBFUQRPWJWPMBGWJWPWAMWJXAXBWPWAIZXCXDXAXBXGXHQVTAFVBRPVSWHWBURT
      USVRWRBIVQWIBVCUTVLZWJWOWMWMNHZWDWJWLLEZWOXJIZWJWSVRXKXEXFWKBSPXKXGXGXLQQ
      WLFFVDVEVFWJWMBWMBNXIXIVGTVHVIVJVKVM $.
      $( [3-Jan-2013] $)
  $}

  $( Lemma for ~ mul02 .  Zero times a real is zero. $)
  mul02lem2 $p |- ( A e. RR -> ( 0 x. A ) = 0 ) $=
    ( cr wcel cc0 cmul co wceq ci caddc wne ax-1ne0 ax-i2m1 opreq1i 00id eqtr4i
    c1 wn ax-icn ax-1cn 0re eqeq12i eqcom mulcli addassi eqeq2i bitri readdcani
    1re 3bitr3ri necon3abii mpbi wa cc mul02lem1 mpan2 opreq2d ex mt3i ) ABCZDA
    EFDGZHHEFZPIFZVAPPIFZIFZGZPDJVEQKVEPDVBPIFZVBGZDPIFZDDIFZGVEPDGVFVHVBVIVBDP
    ILMVBDVILNOUAVGVBVFGVEVFVBUBVFVDVBVAPPHHRRUCSSUDUEUFPDDUHTTUGUIUJUKUSUTQZVE
    USVJULZPVCVAIVKPUMCPVCGSAPUNUOUPUQUR $.
    $( [3-Jan-2013] $)

  ${
    $d A x y $.
    $( Multiplication by ` 0 ` .  Theorem I.6 of [Apostol] p. 18.  Based on
       ideas by Eric Schmidt.  (Contributed by Scott Fenton, 3-Jan-2013.) $)
    mul02 $p |- ( A e. CC -> ( 0 x. A ) = 0 ) $=
      ( vx vy cc wcel cv ci cmul co caddc wceq cr wrex cc0 ax-icn 0cn mul02lem2
      recn syl eqtrd wa mulcl sylancr adddi mp3an1 syl2an mul12 mp3an12 opreq2d
      cnre opreqan12d cnrei opreq2 eqeq1d syl5ibrcom rexlimivv ax-mp 0re eqtr3i
      syl6eq ) ADEABFZGCFZHIZJIZKZCLMBLMNAHIZNKZBCAUJVEVGBCLLVALEZVBLEZUAZVGVEN
      VDHIZNKVJVKNGNHIZJIZNVJVKNVAHIZNVCHIZJIZVMVHVADEZVCDEZVKVPKZVIVARVIGDEZVB
      DEZVROVBRZGVBUBUCNDEZVQVRVSPNVAVCUDUEUFVHVIVNNVOVLJVAQVIVOGNVBHIZHIZVLVIW
      AVOWEKZWBWCVTWAWFPONGVBUGUHSVIWDNGHVBQUITUKTZNNHIZVMNNVDKZCLMBLMWHVMKZBCN
      PULWIWJBCLLVJWJWIVKVMKWGWIWHVKVMNVDNHUMUNUOUPUQNLEWHNKURNQUQUSUTVEVFVKNAV
      DNHUMUNUOUPS $.
      $( [3-Jan-2013] $) $( [10-Aug-1999] $)
  $}

  $( Multiplication by ` 0 ` .  Theorem I.6 of [Apostol] p. 18. $)
  mul01 $p |- ( A e. CC -> ( A x. 0 ) = 0 ) $=
    ( cc wcel cc0 cmul co wceq 0cn mulcom mpan2 mul02 eqtrd ) ABCZADEFZDAEFZDMD
    BCNOGHADIJAKL $.
    $( [3-Jan-2013] $) $( [15-May-1999] $)

  ${
    mulzer.1 $e |- A e. CC $.
    $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
    mul02i $p |- ( 0 x. A ) = 0 $=
      ( cc wcel cc0 cmul co wceq mul02 ax-mp ) ACDEAFGEHBAIJ $.
      $( [23-Nov-1994] $)

    $( Multiplication by ` 0 ` .  Theorem I.6 of [Apostol] p. 18. $)
    mul01i $p |- ( A x. 0 ) = 0 $=
      ( cc wcel cc0 cmul co wceq mul01 ax-mp ) ACDAEFGEHBAIJ $.
      $( [3-Jan-2013] $) $( [23-Nov-1994] $)
  $}

  ${
    $d A c x $.
    $( ` 0 ` is an additive identity.  This used to be one of our complex
       number axioms, until it was found to be dependent on the others.  Based
       on ideas by Eric Schmidt.  (Contributed by Scott Fenton, 3-Jan-2013.) $)
    addid1 $p |- ( A e. CC -> ( A + 0 ) = A ) $=
      ( vc vx c1 caddc co cc0 wceq cr cc wcel wi 1re ax-1cn 0cn 0re opreq2 cmul
      wa ci cv wrex ax-rnegex wne 00id recn addass mp3an13 syl opreq1 sylan9req
      ax-mp simpr 3eqtr4a wb readdcl mpan2 readdcan mp3an3 mpancom adantr mpbid
      ax-1ne0 eqeq1d biimpcd ax-icn mulcli adddii mulid1i mul01i ax-i2m1 eqtr4i
      opreq12i eqeq12i addassi opreq2i 3eqtr3ri opreq1i eqtr3i eqcomi readdcani
      eqtri 3bitri sylib syl6 necon3d mpi adantl jca ax-rrecex w3a simp3 simp2l
      recnd mulcl syl11anc simp1l adddi mul31 syl111anc simp2r opreq1d 3ad2ant3
      mulid2 3eqtrd mul01 opreq12d eqtrd eqeq12d syl5ib 3exp exp3a rexlimdv mpd
      ex com34 imp3a ancomsd syld rexlimiv ) DBUAZEFZGHZBIUBZAJKZAGEFZAHZLZDIKZ
      YDMBDUCULYCYHBIYAIKZYCYAGEFZYAHZYAGUDZSZYHYJYCYNYJYCSZYLYMYODYKEFZYBHZYLY
      OGGEFZGYPYBUEYJYCYPYBGEFZYRYJYAJKZYSYPHZYAUFDJKYTGJKZUUANODYAGUGUHUIYBGGE
      UJUKYJYCUMUNYJYQYLUOZYCYKIKZYJUUCYJGIKUUDPYAGUPUQUUDYJYIUUCMYKYADURUSUTVA
      VBYCYMYJYCDGUDYMVCYCYAGDGYCYAGHZDGEFZGHZDGHZUUEYCUUGUUEYBUUFGYAGDEQVDVEUU
      GTTRFZUUIRFZUUFRFZUUJGRFZHZUUHUUFGUUJRQUUMUUJUUIDEFZEFZGHGDEFZYRHUUHUUKUU
      OUULGUUKUUJDRFZUULEFUUOUUJDGUUIUUITTVFVFVGZUURVGZNOVHUUQUUJUULUUNEUUJUUSV
      IUULGUUNUUJUUSVJZVKVLVMWBUUTVNUUOUUPGYRUUJUUIEFZDEFUUOUUPUUJUUIDUUSUURNVO
      UVAGDEUUIUUNRFZUUJUUIDRFZEFGUVAUUIUUIDUURUURNVHUVBUUIGRFGUUNGUUIRVKVPUUIU
      URVJWBUVCUUIUUJEUUIUURVIVPVQVRVSYRGUEVTVNDGGMPPWAWCWDWEWFWGWHWIXOYJYMYLYH
      YJYMYLYHYJYMYEYLYGYJYMYEYLYGLZLZYJYMSZYACUAZRFZDHZCIUBUVECYAWJUVFUVIUVECI
      UVFUVGIKZUVIUVEUVFUVJUVISZYEUVDYLAUVGRFZYKRFZUVLYARFZHUVFUVKYEWKZYGYKYAUV
      LRQUVOUVMYFUVNAUVOUVMUVNUVLGRFZEFZYFUVOUVLJKZYTUVMUVQHZUVOYEUVGJKZUVRUVFU
      VKYEWLZUVOUVGUVFUVJUVIYEWMWNZAUVGWOWPZUVOYAYJYMUVKYEWQWNZUVRYTUUBUVSOUVLY
      AGWRUSWPUVOUVNAUVPGEUVOUVNUVHARFZDARFZAUVOYEUVTYTUVNUWEHUWAUWBUWDAUVGYAWS
      WTUVOUVHDARUVFUVJUVIYEXAXBYEUVFUWFAHUVKAXDXCXEZUVOUVRUVPGHUWCUVLXFUIXGXHU
      WGXIXJXKXLXMXNXOXPXQXRXSXTUL $.
      $( [3-Jan-2013] $)
  $}

  ${
    addid1i.1 $e |- A e. CC $.
    $( ` 0 ` is an additive identity. $)
    addid1i $p |- ( A + 0 ) = A $=
      ( cc wcel cc0 caddc co wceq addid1 ax-mp ) ACDAEFGAHBAIJ $.
      $( [3-Jan-2013] $) $( [23-Nov-1994] $)
  $}

  ${
    $d A x a b c d $.
    $( Existence of the negative of a complex number.  (Contributed by Eric
       Schmidt, 21-May-2007.)  (Revised by Scott Fenton, 3-Jan-2013). $)
    cnegex $p |- ( A e. CC -> E. x e. CC ( A + x ) = 0 ) $=
      ( va vb vc vd cc wcel cv ci cmul co caddc wceq cr wrex cc0 wa recn ax-icn
      cnre ax-rnegex anim12i reeanv sylibr mulcl sylancr ancoms syl2an ad2antlr
      addcl ad2antrr syl11anc ad2antll ad2antrl addass syl111anc opreq1d eqtr3d
      adantr mp3an1 opreq2 mul01i syl6eq adantl opreq2d ad3antrrr addid1 eqeq1d
      adddi syl eqtrd exbiri com23 imp32 rcla4ev exp31 rexlimdvv opreq1 rexbidv
      mpd syl5ibrcom rexlimivv ) BGHBCIZJDIZKLZMLZNZDOPCOPBAIZMLZQNZAGPZCDBUAWH
      WLCDOOWDOHZWEOHZRZWLWHWGWIMLZQNZAGPZWOWDEIZMLZQNZWEFIZMLZQNZRZFOPEOPZWRWO
      XAEOPZXDFOPZRXFWMXGWNXHEWDUBFWEUBUCXAXDEFOOUDUEWOXEWREFOOWOWSOHZXBOHZRZXE
      WRWOXKRZXERZJXBKLZWSMLZGHZWGXOMLZQNZWRXKXPWOXEXIWSGHZXNGHZXPXJWSSZXJJGHZX
      BGHZXTTXBSZJXBUFZUGXTXSXPXNWSUKUHUIUJXMXQWDWFXNMLZMLZWSMLZQXLXQYHNXEXLWGX
      NMLZWSMLZXQYHXLWGGHZXTXSYJXQNXLWDGHZWFGHZYKWMYLWNXKWDSZULZXLYBWEGHZYMTWNY
      PWMXKWESUJZJWEUFUGZWDWFUKUMXLYBYCXTTXJYCWOXIYDUNZYEUGZXIXSWOXJYAUOWGXNWSU
      PUQXLYIYGWSMXLYLYMXTYIYGNYOYRYTWDWFXNUPUQURUSUTXLXAXDYHQNZXLXDXAUUAXLXDUU
      AXAXLXDRZYHWTQUUBYGWDWSMUUBYGWDQMLZWDUUBYFQWDMUUBJXCKLZYFQUUBYPYCUUDYFNZX
      LYPXDYQUTXLYCXDYSUTYBYPYCUUETJWEXBVJVAUMXDUUDQNXLXDUUDJQKLQXCQJKVBJTVCVDV
      EUSVFUUBYLUUCWDNWMYLWNXKXDYNVGWDVHVKVLURVIVMVNVOVLWQXRAXOGWIXONWPXQQWIXOW
      GMVBVIVPUMVQVRWAWHWKWQAGWHWJWPQBWGWIMVSVIVTWBWCVK $.
      $( [3-Jan-2013] $) $( [21-May-2007] $)
  $}

  ${
    $d A x $.
    $( Existence of a left inverse for addition.  (Contributed by Scott Fenton,
       3-Jan-2013.) $)
    cnegex2 $p |- ( A e. CC -> E. x e. CC ( x + A ) = 0 ) $=
      ( cc wcel ci cmul co caddc cc0 wceq cv wrex ax-icn mulcli mulcl c1 mulid2
      mpan opreq2d eqtr3d ax-1cn adddir mp3an12 ax-i2m1 opreq1i syl5eq syl11anc
      mul02 opreq1 eqeq1d rcla4ev ) BCDZEEFGZBFGZCDZUNBHGZIJZAKZBHGZIJZACLUMCDZ
      ULUOEEMMNZUMBORULUNPBFGZHGZUPIULVCBUNHBQSULUMPHGZBFGZVDIVAPCDULVFVDJVBUAU
      MPBUBUCULVFIBFGIVEIBFUDUEBUHUFTTUTUQAUNCURUNJUSUPIURUNBHUIUJUKUG $.
      $( [3-Jan-2013] $)
  $}

  ${
    $d A x y $.
    $( ` 0 ` is a left identity for addition.  This used to be one of our
       complex number axioms, until it was discovered that it was dependent on
       the others.  Based on ideas by Eric Schmidt.  (Contributed by Scott
       Fenton, 3-Jan-2013.) $)
    addid2 $p |- ( A e. CC -> ( 0 + A ) = A ) $=
      ( vx vy cc wcel cv caddc co cc0 wceq wrex cnegex ad2antrl addass 3eqtr3rd
      wa opreq2d 0cn rexlimdv mpd w3a simp1 simp2l simp3l simp2r opreq1d simp3r
      addid1 3ad2ant1 00id opreq1i mp3an12 adantr 3ad2ant3 eqtr3d eqtrd syl5eqr
      syl111anc 3expia exp3a exp32 ) ADEZABFZGHZIJZBDKIAGHZAJZBALVBVEVGBDVBVCDE
      ZVEVGVBVHVEPZPZVCCFZGHZIJZCDKZVGVHVNVBVECVCLMVJVMVGCDVJVKDEZVMVGVBVIVOVMP
      ZVGVBVIVPUAZAIGHZIVKGHZAVFVQVDVKGHZAVLGHZVSVRVQVBVHVOVTWAJVBVIVPUBVBVHVEV
      PUCVBVIVOVMUDAVCVKNURVQVDIVKGVBVHVEVPUEUFVQVLIAGVBVIVOVMUGQOZVBVIVRAJVPAU
      HUIZVQVSIIGHZVKGHZVFWDIVKGUJUKVQWEIVSGHZVFVPVBWEWFJZVIVOWGVMIDEZWHVOWGRRI
      IVKNULUMUNVQVSAIGVQVRVSAWBWCUOQUPUQOUSUTSTVAST $.
      $( [3-Jan-2013] $)
  $}

  ${
    addid2i.1 $e |- A e. CC $.
    $( ` 0 ` is a left identity for addition. $)
    addid2i $p |- ( 0 + A ) = A $=
      ( cc wcel cc0 caddc co wceq addid2 ax-mp ) ACDEAFGAHBAIJ $.
      $( [3-Jan-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.
    addcani.1 $e |- A e. CC $.
    addcani.2 $e |- B e. CC $.
    addcani.3 $e |- C e. CC $.
    $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18. $)
    addcani $p |- ( ( A + B ) = ( A + C ) <-> B = C ) $=
      ( vx caddc co wceq cc0 cc wcel ax-mp opreq2 addass mp3an23 opreq1 addid2i
      sylan9req cv wrex wi cnegex2 wa eqeq12d syl5ib rexlimiva 3eqtr3g impbii )
      ABHIZACHIZJZBCJUMKBHIZKCHIZBCGUAZAHIZKJZGLUBZUMUNUOJZUCZALMZUSDGAUDNURVAG
      LUMUPUKHIZUPULHIZJUPLMZURUEZUTUKULUPHOVFVCUNVDUOVEURVCUQBHIZUNVEVBBLMVGVC
      JDEUPABPQUQKBHRTVEURVDUQCHIZUOVEVBCLMVHVDJDFUPACPQUQKCHRTUFUGUHNBESCFSUIB
      CAHOUJ $.
      $( [3-Jan-2013] $)   $( [27-Oct-1999] $)

    $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18. $)
    addcan2i $p |- ( ( A + C ) = ( B + C ) <-> A = B ) $=
      ( vx caddc co wceq cc0 cc wcel ax-mp opreq1 addass mp3an12 opreq2 addid1i
      sylan9eq cv wrex wi cnegex wa eqeq12d syl5ib rexlimiva 3eqtr3g impbii ) A
      CHIZBCHIZJZABJUMAKHIZBKHIZABCGUAZHIZKJZGLUBZUMUNUOJZUCZCLMZUSFGCUDNURVAGL
      UMUKUPHIZULUPHIZJUPLMZURUEZUTUKULUPHOVFVCUNVDUOVEURVCAUQHIZUNALMVBVEVCVGJ
      DFACUPPQUQKAHRTVEURVDBUQHIZUOBLMVBVEVDVHJEFBCUPPQUQKBHRTUFUGUHNADSBESUIAB
      CHOUJ $.
      $( [3-Jan-2013] $)   $( [14-May-2003] $)
  $}

  $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18.  This
     proof illustrates how ~ dedth3h can be used to convert the assumptions of
     ~ addcani into antecedents.  This general method can be used to convert
     deductions into theorems as needed. $)
  addcan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( ( A + B ) = ( A + C ) <-> B = C ) ) $=
    ( cc wcel caddc co wceq cc0 cif opreq1 eqeq12d bibi1d opreq2 eqeq1d bibi12d
    wb eqeq1 0cn elimel eqeq2d eqeq2 addcani dedth3h ) ADEZBDEZCDEZABFGZACFGZHZ
    BCHZQUEAIJZBFGZULCFGZHZUKQULUFBIJZFGZUNHZUPCHZQUQULUGCIJZFGZHZUPUTHZQABCIII
    AULHZUJUOUKVDUHUMUIUNAULBFKAULCFKLMBUPHZUOURUKUSVEUMUQUNBUPULFNOBUPCRPCUTHZ
    URVBUSVCVFUNVAUQCUTULFNUACUTUPUBPULUPUTAIDSTBIDSTCIDSTUCUD $.
    $( [3-Jan-2013] $)   $( [22-Nov-1994] $)

  $( Cancellation law for addition. $)
  addcan2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A + C ) = ( B + C ) <-> A = B ) ) $=
    ( cc wcel caddc co wceq wb cc0 cif opreq1 eqeq1d eqeq1 bibi12d eqeq2d eqeq2
    opreq2 0cn elimel eqeq12d bibi1d addcan2i dedth3h ) ADEZBDEZCDEZACFGZBCFGZH
    ZABHZIUEAJKZCFGZUIHZULBHZIUMUFBJKZCFGZHZULUPHZIULUGCJKZFGZUPUTFGZHZUSIABCJJ
    JAULHZUJUNUKUOVDUHUMUIAULCFLMAULBNOBUPHZUNURUOUSVEUIUQUMBUPCFLPBUPULQOCUTHZ
    URVCUSVFUMVAUQVBCUTULFRCUTUPFRUAUBULUPUTAJDSTBJDSTCJDSTUCUD $.
    $( [3-Jan-2013] $)    $( [30-Jul-2004] $)

  ${
    addcomi.1 $e |- A e. CC $.
    addcomi.2 $e |- B e. CC $.
    $( Addition commutes.  Based on ideas by Eric Schmidt.  (Contributed by
       Scott Fenton, 3-Jan-2013.) $)
    addcomi $p |- ( A + B ) = ( B + A ) $=
      ( caddc co wceq c1 ax-1cn addcli adddiri adddii mulid1i opreq12i 3eqtr3ri
      cmul eqtri addassi 3eqtr4i mpbi addcan2i 3eqtr3i addcani ) AABEFZEFZABAEF
      ZEFZGUDUFGAAEFZBEFZUDAEFZUEUGUIBEFZUJBEFZGUIUJGUHBBEFZEFZUDUDEFZUKULUDHHE
      FZPFZAUPPFZBUPPFZEFUOUNABUPCDHHIIJKUQUDHPFZUTEFUOUDHHABCDJZIILUTUDUTUDEUD
      VAMZVBNQURUHUSUMEURAHPFZVCEFUHAHHCIILVCAVCAEACMZVDNQUSBHPFZVEEFUMBHHDIILV
      EBVEBEBDMZVFNQNOUHBBAACCJZDDRUDABVACDRSUIUJBUHBVGDJUDAVACJDUATAABCCDRABAC
      DCRUBAUDUFCVABADCJUCT $.
      $( [3-Jan-2013] $)
  $}

  $( Addition commutes.  This used to be one of our complex number axioms,
     until it was found to be dependent on the others.  Based on ideas by Eric
     Schmidt.  (Contributed by Scott Fenton, 3-Jan-2013.) $)
  addcom $p |- ( ( A e. CC /\ B e. CC ) -> ( A + B ) = ( B + A ) ) $=
    ( cc wcel caddc co cc0 cif opreq1 opreq2 eqeq12d 0cn elimel addcomi dedth2h
    wceq ) ACDZBCDZABEFZBAEFZPQAGHZBEFZBUAEFZPUARBGHZEFZUDUAEFZPABGGAUAPSUBTUCA
    UABEIAUABEJKBUDPUBUEUCUFBUDUAEJBUDUAEIKUAUDAGCLMBGCLMNO $.
    $( [3-Jan-2013] $)

