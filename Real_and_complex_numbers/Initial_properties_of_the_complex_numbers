$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Initial properties of the complex numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    readdcani.1 $e |- A e. RR $.
    readdcani.2 $e |- B e. RR $.
    readdcani.3 $e |- C e. RR $.
    $( Cancellation law for addition over the reals.  (Contributed by Scott
       Fenton, 3-Jan-2013.) $)
    readdcani $p |- ( ( C + A ) = ( C + B ) <-> A = B ) $=
      ( caddc co wceq clt wbr wn readdcli ltnri breq1 mtbiri ltadd2i sylnibr
      breq2 lttri3i sylanbrc opreq2 impbii ) CAGHZCBGHZIZABIZUFABJKZLBAJKZLUGUF
      UDUEJKZUHUFUJUEUEJKZUECBFEMNZUDUEUEJOPABCDEFQRUFUEUDJKZUIUFUMUKULUDUEUEJS
      PBACEDFQRABDETUAABCGUBUC $.
      $( [3-Jan-2013] $)
  $}

  $( Cancellation law for addition over the reals. $)
  readdcan $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( C + A ) = ( C + B ) <-> A = B ) ) $=
    ( cr wcel caddc co wceq wb c1 cif opreq2 eqeq1d eqeq1 bibi12d eqeq2d eqeq2
    opreq1 eqeq12d bibi1d 1re elimel readdcani dedth3h ) ADEZBDEZCDEZCAFGZCBFGZ
    HZABHZICUEAJKZFGZUIHZULBHZIUMCUFBJKZFGZHZULUPHZIUGCJKZULFGZUTUPFGZHZUSIABCJ
    JJAULHZUJUNUKUOVDUHUMUIAULCFLMAULBNOBUPHZUNURUOUSVEUIUQUMBUPCFLPBUPULQOCUTH
    ZURVCUSVFUMVAUQVBCUTULFRCUTUPFRSTULUPUTAJDUAUBBJDUAUBCJDUAUBUCUD $.
    $( [3-Jan-2013] $)

  ${
    $d c y $.
    $( ` 0 ` is its own additive identity.  (Contributed by Scott Fenton,
       3-Jan-2013.) $)
    00id $p |- ( 0 + 0 ) = 0 $=
      ( vc vy cc0 cv caddc co wceq cr wrex wcel 0re ax-rnegex ax-mp wa wi
      opreq2 eqeq1d biimpd adantld wne cmul c1 ax-rrecex adantlr cc simplll
      recnd simprl 0cn mulass mp3an3 syl11anc opreq1 mulid2i syl6eq ad2antll
      eqtr3d opreq1d simpllr remulcl mpan2 ad2antrl adddir mp3an1 sylancr
      addass eqtr2d wb sylan2 readdcl sylancl readdcan mp3an2 mpbid exp32
      r19.23adv mpd expcom pm2.61ine r19.23aiva ) CADZEFZCGZAHIZCCEFZCGZCHJZWDK
      ACLMWCWFAHWAHJZWCNZWFOWACWACGZWCWFWHWJWCWFWJWBWECWACCEPQRSWIWACTZWFWIWKNZ
      WABDZUAFZUBGZBHIZWFWHWKWPWCBWAUCUDWLWOWFBHWLWMHJZWOWFWLWQWONZNZWAWMCUAFZU
      AFZCEFZWECWSXACCEWSWNCUAFZXACWSWAUEJZWMUEJZXCXAGZWSWAWHWCWKWRUFZUGZWSWMWL
      WQWOUHZUGXDXECUEJZXFUIWAWMCUJUKULWOXCCGWLWQWOXCUBCUAFCWNUBCUAUMCUIUNUOUPU
      QURWSCWTUAFZXBEFZXKCEFZGZXBCGZWSXMXKXAEFZCEFZXLWSXKXPCEWSWBWTUAFZXKXPWSWB
      CWTUAWHWCWKWRUSURWSXDWTUEJZXRXPGZXHWSWTWQWTHJZWLWOWQWGYAKWMCUTVAZVBZUGXJX
      DXSXTUICWAWTVCVDULUQURWSXKUEJZXAUEJZXQXLGZWSXKWQXKHJZWLWOWQWGYAYGKYBCWTUT
      VEVBZUGWSXAWSWHYAXAHJZXGYCWAWTUTZULUGYDYEXJYFUIXKXACVFUKULVGWSXBHJZYGXNXO
      VHZWSWHWQYKXGXIWHWQNYIWGYKWQWHYAYIYBYJVIKXACVJVKULYHYKWGYGYLKXBCXKVLVMULV
      NUQVOVPVQVRVSVTM $.
      $( [3-Jan-2013] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( Lemma for ~ mul02 .  If any real does not produce ` 0 ` when multiplied
       by ` 0 ` , then any complex is equal to double itself. $)
    mul02lem1 $p |- ( ( ( A e. RR /\ -. ( 0 x. A ) = 0 ) /\ B e. CC ) ->
                       B = ( B + B ) ) $=
      ( vy cr wcel cc0 cmul co wceq wn wa cc cv c1 wrex caddc wne 0re
      ax-mulrcl mpan df-ne biimpri ax-rrecex syl2an adantr 00id opreq2i eqcomi
      simprl recnd simplll mulcl syl11anc simplr 0cn mul32 mp3an3 mul31 simprr
      eqtrd opreq1d mulid2 ad2antlr 3eqtrd adddi mp3an23 syl opreq12d eqeq12d
      mpbii exp32 r19.23adv mpd ) ADEZFAGHZFIJZKZBLEZKZVOCMZGHZNIZCDOZBBBPHZIZV
      QWCVRVNVODEZVOFQZWCVPFDEVNWFRFASTWGVPVOFUAUBCVOUCUDUEVSWBWECDVSVTDEZWBWEV
      SWHWBKZKZVTAGHZBGHZFGHZWLFFPHZGHZIWEWOWMWNFWLGUFUGUHWJWMBWOWDWJWMWKFGHZBG
      HZNBGHZBWJWKLEZVRWMWQIZWJVTLEZALEZWSWJVTVSWHWBUIUJZWJAVNVPVRWIUKUJZVTAULU
      MZVQVRWIUNZWSVRFLEZWTUOWKBFUPUQUMWJWPNBGWJWPWANWJXAXBWPWAIZXCXDXAXBXGXHUO
      VTAFURUQUMVSWHWBUSUTVAVRWRBIVQWIBVBVCVDZWJWOWMWMPHZWDWJWLLEZWOXJIZWJWSVRX
      KXEXFWKBULUMXKXGXGXLUOUOWLFFVEVFVGWJWMBWMBPXIXIVHUTVIVJVKVLVM $.
      $( [3-Jan-2013] $)
  $}

  $( Lemma for ~ mul02 .  Zero times a real is zero. $)
  mul02lem2 $p |- ( A e. RR -> ( 0 x. A ) = 0 ) $=
    ( cr wcel cc0 cmul co wceq ci c1 caddc wne wn ax-1ne0 ax-i2m1 opreq1i 00id
    eqtr4i eqeq12i eqcom ax-icn mulcli ax-1cn addassi eqeq2i bitri 1re 0re
    readdcani 3bitr3ri necon3abii mpbi wa cc mul02lem1 mpan2 opreq2d ex mt3i )
    ABCZDAEFDGZHHEFZIJFZVAIIJFZJFZGZIDKVELMVEIDVBIJFZVBGZDIJFZDDJFZGVEIDGVFVHVB
    VIVBDIJNOVBDVINPQRVGVBVFGVEVFVBSVFVDVBVAIIHHTTUAUBUBUCUDUEIDDUFUGUGUHUIUJUK
    USUTLZVEUSVJULZIVCVAJVKIUMCIVCGUBAIUNUOUPUQUR $.
    $( [3-Jan-2013] $)

  ${
    $d A x y $.
    $( Multiplication by ` 0 ` .  Theorem I.6 of [Apostol] p. 18.  Based on
       ideas by Eric Schmidt.  (Contributed by Scott Fenton, 3-Jan-2013.) $)
    mul02 $p |- ( A e. CC -> ( 0 x. A ) = 0 ) $=
      ( vx vy cc wcel cv ci cmul co caddc wceq cr wrex cc0 cnre wa recn ax-icn
      mulcl sylancr 0cn adddi mp3an1 syl2an mul02lem2 mul12 mp3an12 syl
      opreq2d eqtrd opreqan12d cnrei opreq2 eqeq1d syl5ibrcom r19.23aivv ax-mp
      0re eqtr3i syl6eq ) ADEABFZGCFZHIZJIZKZCLMBLMNAHIZNKZBCAOVEVGBCLLVALEZVBL
      EZPZVGVENVDHIZNKVJVKNGNHIZJIZNVJVKNVAHIZNVCHIZJIZVMVHVADEZVCDEZVKVPKZVIVA
      QVIGDEZVBDEZVRRVBQZGVBSTNDEZVQVRVSUANVAVCUBUCUDVHVIVNNVOVLJVAUEVIVOGNVBHI
      ZHIZVLVIWAVOWEKZWBWCVTWAWFUARNGVBUFUGUHVIWDNGHVBUEUIUJUKUJZNNHIZVMNNVDKZC
      LMBLMWHVMKZBCNUAULWIWJBCLLVJWJWIVKVMKWGWIWHVKVMNVDNHUMUNUOUPUQNLEWHNKURNU
      EUQUSUTVEVFVKNAVDNHUMUNUOUPUH $.
      $( [3-Jan-2013] $) $( [10-Aug-1999] $)
  $}

  $( Multiplication by ` 0 ` .  Theorem I.6 of [Apostol] p. 18. $)
  mul01 $p |- ( A e. CC -> ( A x. 0 ) = 0 ) $=
    ( cc wcel cc0 cmul co wceq 0cn mulcom mpan2 mul02 eqtrd ) ABCZADEFZDAEFZDMD
    BCNOGHADIJAKL $.
    $( [3-Jan-2013] $) $( [15-May-1999] $)

  ${
    mulzer.1 $e |- A e. CC $.
    $( Multiplication by 0.  Theorem I.6 of [Apostol] p. 18. $)
    mul02i $p |- ( 0 x. A ) = 0 $=
      ( cc wcel cc0 cmul co wceq mul02 ax-mp ) ACDEAFGEHBAIJ $.
      $( [23-Nov-1994] $)

    $( Multiplication by ` 0 ` .  Theorem I.6 of [Apostol] p. 18. $)
    mul01i $p |- ( A x. 0 ) = 0 $=
      ( cc wcel cc0 cmul co wceq mul01 ax-mp ) ACDAEFGEHBAIJ $.
      $( [3-Jan-2013] $) $( [23-Nov-1994] $)
  $}

  ${
    $d A c x $.
    $( ` 0 ` is an additive identity.  This used to be one of our complex
       number axioms, until it was found to be dependent on the others.  Based
       on ideas by Eric Schmidt.  (Contributed by Scott Fenton, 3-Jan-2013.) $)
    addid1 $p |- ( A e. CC -> ( A + 0 ) = A ) $=
      ( vc vx c1 cv caddc co cc0 wceq cr wrex cc wcel wi 1re ax-rnegex ax-mp
      wne wa 00id recn ax-1cn 0cn addass mp3an13 syl opreq1 sylan9req simpr
      3eqtr4a wb 0re readdcl mpan2 readdcan mp3an3 mpancom adantr mpbid
      ax-1ne0 opreq2 eqeq1d biimpcd ci cmul ax-icn mulcli adddii mulid1i
      mul01i ax-i2m1 eqtr4i opreq12i eqtri eqeq12i addassi opreq2i 3eqtr3ri
      opreq1i eqtr3i eqcomi readdcani 3bitri sylib syl6 necon3d mpi adantl jca
      ex ax-rrecex w3a simp3 simp2l recnd mulcl syl11anc simp1l adddi mul31
      syl111anc simp2r opreq1d mulid2 3ad2ant3 3eqtrd mul01 opreq12d eqtrd
      eqeq12d syl5ib 3exp exp3a r19.23adv mpd com34 imp3a ancomsd syld
      r19.23aiv ) DBEZFGZHIZBJKZALMZAHFGZAIZNZDJMZYDOBDPQYCYHBJYAJMZYCYAHFGZYAI
      ZYAHRZSZYHYJYCYNYJYCSZYLYMYODYKFGZYBIZYLYOHHFGZHYPYBTYJYCYPYBHFGZYRYJYALM
      ZYSYPIZYAUADLMYTHLMZUUAUBUCDYAHUDUEUFYBHHFUGUHYJYCUIUJYJYQYLUKZYCYKJMZYJU
      UCYJHJMUUDULYAHUMUNUUDYJYIUUCOYKYADUOUPUQURUSYCYMYJYCDHRYMUTYCYAHDHYCYAHI
      ZDHFGZHIZDHIZUUEYCUUGUUEYBUUFHYAHDFVAVBVCUUGVDVDVEGZUUIVEGZUUFVEGZUUJHVEG
      ZIZUUHUUFHUUJVEVAUUMUUJUUIDFGZFGZHIHDFGZYRIUUHUUKUUOUULHUUKUUJDVEGZUULFGU
      UOUUJDHUUIUUIVDVDVFVFVGZUURVGZUBUCVHUUQUUJUULUUNFUUJUUSVIUULHUUNUUJUUSVJZ
      VKVLVMVNUUTVOUUOUUPHYRUUJUUIFGZDFGUUOUUPUUJUUIDUUSUURUBVPUVAHDFUUIUUNVEGZ
      UUJUUIDVEGZFGHUVAUUIUUIDUURUURUBVHUVBUUIHVEGHUUNHUUIVEVKVQUUIUURVJVNUVCUU
      IUUJFUUIUURVIVQVRVSVTYRHTWAVODHHOULULWBWCWDWEWFWGWHWIWJYJYMYLYHYJYMYLYHYJ
      YMYEYLYGYJYMYEYLYGNZNZYJYMSZYACEZVEGZDIZCJKUVECYAWKUVFUVIUVECJUVFUVGJMZUV
      IUVEUVFUVJUVISZYEUVDYLAUVGVEGZYKVEGZUVLYAVEGZIUVFUVKYEWLZYGYKYAUVLVEVAUVO
      UVMYFUVNAUVOUVMUVNUVLHVEGZFGZYFUVOUVLLMZYTUVMUVQIZUVOYEUVGLMZUVRUVFUVKYEW
      MZUVOUVGUVFUVJUVIYEWNWOZAUVGWPWQZUVOYAYJYMUVKYEWRWOZUVRYTUUBUVSUCUVLYAHWS
      UPWQUVOUVNAUVPHFUVOUVNUVHAVEGZDAVEGZAUVOYEUVTYTUVNUWEIUWAUWBUWDAUVGYAWTXA
      UVOUVHDAVEUVFUVJUVIYEXBXCYEUVFUWFAIUVKAXDXEXFZUVOUVRUVPHIUWCUVLXGUFXHXIUW
      GXJXKXLXMXNXOWJXPXQXRXSXTQ $.
      $( [3-Jan-2013] $)
  $}

  ${
    addid1i.1 $e |- A e. CC $.
    $( ` 0 ` is an additive identity. $)
    addid1i $p |- ( A + 0 ) = A $=
      ( cc wcel cc0 caddc co wceq addid1 ax-mp ) ACDAEFGAHBAIJ $.
      $( [3-Jan-2013] $) $( [23-Nov-1994] $)
  $}

  ${
    $d A x a b c d $.
    $( Existence of the negative of a complex number.  (Contributed by Eric
       Schmidt, 21-May-2007.)  (Revised by Scott Fenton, 3-Jan-2013). $)
    cnegex $p |- ( A e. CC -> E. x e. CC ( A + x ) = 0 ) $=
      ( va vb vc vd cc wcel cv ci cmul co caddc wceq cr wrex cc0 cnre wa
      ax-rnegex anim12i reeanv sylibr recn ax-icn mulcl sylancr addcl ancoms
      syl2an ad2antlr ad2antrr syl11anc ad2antll ad2antrl addass syl111anc
      opreq1d eqtr3d adantr adddi mp3an1 opreq2 mul01i syl6eq adantl opreq2d
      ad3antrrr addid1 syl eqtrd eqeq1d exbiri com23 imp32 rcla4ev exp31
      r19.23advv mpd opreq1 rexbidv syl5ibrcom r19.23aivv ) BGHBCIZJDIZKLZMLZNZ
      DOPCOPBAIZMLZQNZAGPZCDBRWHWLCDOOWDOHZWEOHZSZWLWHWGWIMLZQNZAGPZWOWDEIZMLZQ
      NZWEFIZMLZQNZSZFOPEOPZWRWOXAEOPZXDFOPZSXFWMXGWNXHEWDTFWETUAXAXDEFOOUBUCWO
      XEWREFOOWOWSOHZXBOHZSZXEWRWOXKSZXESZJXBKLZWSMLZGHZWGXOMLZQNZWRXKXPWOXEXIW
      SGHZXNGHZXPXJWSUDZXJJGHZXBGHZXTUEXBUDZJXBUFZUGXTXSXPXNWSUHUIUJUKXMXQWDWFX
      NMLZMLZWSMLZQXLXQYHNXEXLWGXNMLZWSMLZXQYHXLWGGHZXTXSYJXQNXLWDGHZWFGHZYKWMY
      LWNXKWDUDZULZXLYBWEGHZYMUEWNYPWMXKWEUDUKZJWEUFUGZWDWFUHUMXLYBYCXTUEXJYCWO
      XIYDUNZYEUGZXIXSWOXJYAUOWGXNWSUPUQXLYIYGWSMXLYLYMXTYIYGNYOYRYTWDWFXNUPUQU
      RUSUTXLXAXDYHQNZXLXDXAUUAXLXDUUAXAXLXDSZYHWTQUUBYGWDWSMUUBYGWDQMLZWDUUBYF
      QWDMUUBJXCKLZYFQUUBYPYCUUDYFNZXLYPXDYQUTXLYCXDYSUTYBYPYCUUEUEJWEXBVAVBUMX
      DUUDQNXLXDUUDJQKLQXCQJKVCJUEVDVEVFUSVGUUBYLUUCWDNWMYLWNXKXDYNVHWDVIVJVKUR
      VLVMVNVOVKWQXRAXOGWIXONWPXQQWIXOWGMVCVLVPUMVQVRVSWHWKWQAGWHWJWPQBWGWIMVTV
      LWAWBWCVJ $.
      $( [3-Jan-2013] $) $( [21-May-2007] $)
  $}

  ${
    $d A x $.
    $( Existence of a left inverse for addition.  (Contributed by Scott Fenton,
       3-Jan-2013.) $)
    cnegex2 $p |- ( A e. CC -> E. x e. CC ( x + A ) = 0 ) $=
      ( cc wcel ci cmul co caddc cc0 wceq cv wrex ax-icn mulcli mulcl mpan c1
      mulid2 opreq2d ax-1cn adddir mp3an12 ax-i2m1 opreq1i mul02 syl5eq eqtr3d
      opreq1 eqeq1d rcla4ev syl11anc ) BCDZEEFGZBFGZCDZUNBHGZIJZAKZBHGZIJZACLUM
      CDZULUOEEMMNZUMBOPULUNQBFGZHGZUPIULVCBUNHBRSULUMQHGZBFGZVDIVAQCDULVFVDJVB
      TUMQBUAUBULVFIBFGIVEIBFUCUDBUEUFUGUGUTUQAUNCURUNJUSUPIURUNBHUHUIUJUK $.
      $( [3-Jan-2013] $)
  $}

  ${
    $d A x y $.
    $( ` 0 ` is a left identity for addition.  This used to be one of our
       complex number axioms, until it was discovered that it was dependent on
       the others.  Based on ideas by Eric Schmidt.  (Contributed by Scott
       Fenton, 3-Jan-2013.) $)
    addid2 $p |- ( A e. CC -> ( 0 + A ) = A ) $=
      ( vx vy cc wcel cv caddc co cc0 wceq wrex cnegex wa ad2antrl w3a simp1
      simp2l simp3l addass syl111anc simp2r opreq1d simp3r opreq2d 3eqtr3rd
      addid1 3ad2ant1 00id opreq1i 0cn mp3an12 adantr 3ad2ant3 eqtr3d eqtrd
      syl5eqr 3expia exp3a r19.23adv mpd exp32 ) ADEZABFZGHZIJZBDKIAGHZAJZBALVB
      VEVGBDVBVCDEZVEVGVBVHVEMZMZVCCFZGHZIJZCDKZVGVHVNVBVECVCLNVJVMVGCDVJVKDEZV
      MVGVBVIVOVMMZVGVBVIVPOZAIGHZIVKGHZAVFVQVDVKGHZAVLGHZVSVRVQVBVHVOVTWAJVBVI
      VPPVBVHVEVPQVBVIVOVMRAVCVKSTVQVDIVKGVBVHVEVPUAUBVQVLIAGVBVIVOVMUCUDUEZVBV
      IVRAJVPAUFUGZVQVSIIGHZVKGHZVFWDIVKGUHUIVQWEIVSGHZVFVPVBWEWFJZVIVOWGVMIDEZ
      WHVOWGUJUJIIVKSUKULUMVQVSAIGVQVRVSAWBWCUNUDUOUPUEUQURUSUTVAUSUT $.
      $( [3-Jan-2013] $)
  $}

  ${
    addid2i.1 $e |- A e. CC $.
    $( ` 0 ` is a left identity for addition. $)
    addid2i $p |- ( 0 + A ) = A $=
      ( cc wcel cc0 caddc co wceq addid2 ax-mp ) ACDEAFGAHBAIJ $.
      $( [3-Jan-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.
    addcani.1 $e |- A e. CC $.
    addcani.2 $e |- B e. CC $.
    addcani.3 $e |- C e. CC $.
    $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18. $)
    addcani $p |- ( ( A + B ) = ( A + C ) <-> B = C ) $=
      ( vx caddc co wceq cc0 cv cc wrex wi wcel cnegex2 ax-mp wa opreq2 addass
      mp3an23 opreq1 sylan9req eqeq12d syl5ib r19.23aiva addid2i 3eqtr3g
      impbii ) ABHIZACHIZJZBCJUMKBHIZKCHIZBCGLZAHIZKJZGMNZUMUNUOJZOZAMPZUSDGAQR
      URVAGMUMUPUKHIZUPULHIZJUPMPZURSZUTUKULUPHTVFVCUNVDUOVEURVCUQBHIZUNVEVBBMP
      VGVCJDEUPABUAUBUQKBHUCUDVEURVDUQCHIZUOVEVBCMPVHVDJDFUPACUAUBUQKCHUCUDUEUF
      UGRBEUHCFUHUIBCAHTUJ $.
      $( [3-Jan-2013] $)   $( [27-Oct-1999] $)

    $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18. $)
    addcan2i $p |- ( ( A + C ) = ( B + C ) <-> A = B ) $=
      ( vx caddc co wceq cc0 cv cc wrex wi wcel cnegex ax-mp wa opreq1 addass
      mp3an12 opreq2 sylan9eq eqeq12d syl5ib r19.23aiva addid1i 3eqtr3g impbii
      ) ACHIZBCHIZJZABJUMAKHIZBKHIZABCGLZHIZKJZGMNZUMUNUOJZOZCMPZUSFGCQRURVAGMU
      MUKUPHIZULUPHIZJUPMPZURSZUTUKULUPHTVFVCUNVDUOVEURVCAUQHIZUNAMPVBVEVCVGJDF
      ACUPUAUBUQKAHUCUDVEURVDBUQHIZUOBMPVBVEVDVHJEFBCUPUAUBUQKBHUCUDUEUFUGRADUH
      BEUHUIABCHTUJ $.
      $( [3-Jan-2013] $)   $( [14-May-2003] $)
  $}

  $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18.  This
     proof illustrates how ~ dedth3h can be used to convert the assumptions of
     ~ addcani into antecedents.  This general method can be used to convert
     deductions into theorems as needed. $)
  addcan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( ( A + B ) = ( A + C ) <-> B = C ) ) $=
    ( cc wcel caddc co wceq wb cc0 cif opreq1 eqeq12d bibi1d opreq2 eqeq1d
    eqeq1 bibi12d eqeq2d eqeq2 0cn elimel addcani dedth3h ) ADEZBDEZCDEZABFGZAC
    FGZHZBCHZIUEAJKZBFGZULCFGZHZUKIULUFBJKZFGZUNHZUPCHZIUQULUGCJKZFGZHZUPUTHZIA
    BCJJJAULHZUJUOUKVDUHUMUIUNAULBFLAULCFLMNBUPHZUOURUKUSVEUMUQUNBUPULFOPBUPCQR
    CUTHZURVBUSVCVFUNVAUQCUTULFOSCUTUPTRULUPUTAJDUAUBBJDUAUBCJDUAUBUCUD $.
    $( [3-Jan-2013] $)   $( [22-Nov-1994] $)

  $( Cancellation law for addition. $)
  addcan2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A + C ) = ( B + C ) <-> A = B ) ) $=
    ( cc wcel caddc co wceq wb cc0 cif opreq1 eqeq1d eqeq1 bibi12d eqeq2d
    eqeq2 opreq2 eqeq12d bibi1d 0cn elimel addcan2i dedth3h ) ADEZBDEZCDEZACFGZ
    BCFGZHZABHZIUEAJKZCFGZUIHZULBHZIUMUFBJKZCFGZHZULUPHZIULUGCJKZFGZUPUTFGZHZUS
    IABCJJJAULHZUJUNUKUOVDUHUMUIAULCFLMAULBNOBUPHZUNURUOUSVEUIUQUMBUPCFLPBUPULQ
    OCUTHZURVCUSVFUMVAUQVBCUTULFRCUTUPFRSTULUPUTAJDUAUBBJDUAUBCJDUAUBUCUD $.
    $( [3-Jan-2013] $)    $( [30-Jul-2004] $)

  ${
    addcomi.1 $e |- A e. CC $.
    addcomi.2 $e |- B e. CC $.
    $( Addition commutes.  Based on ideas by Eric Schmidt.  (Contributed by
       Scott Fenton, 3-Jan-2013.) $)
    addcomi $p |- ( A + B ) = ( B + A ) $=
      ( caddc co wceq c1 cmul ax-1cn addcli adddiri adddii mulid1i opreq12i
      eqtri 3eqtr3ri addassi 3eqtr4i addcan2i mpbi 3eqtr3i addcani ) AABEFZEFZA
      BAEFZEFZGUDUFGAAEFZBEFZUDAEFZUEUGUIBEFZUJBEFZGUIUJGUHBBEFZEFZUDUDEFZUKULU
      DHHEFZIFZAUPIFZBUPIFZEFUOUNABUPCDHHJJKLUQUDHIFZUTEFUOUDHHABCDKZJJMUTUDUTU
      DEUDVANZVBOPURUHUSUMEURAHIFZVCEFUHAHHCJJMVCAVCAEACNZVDOPUSBHIFZVEEFUMBHHD
      JJMVEBVEBEBDNZVFOPOQUHBBAACCKZDDRUDABVACDRSUIUJBUHBVGDKUDAVACKDTUAAABCCDR
      ABACDCRUBAUDUFCVABADCKUCUA $.
      $( [3-Jan-2013] $)
  $}

  $( Addition commutes.  This used to be one of our complex number axioms,
     until it was found to be dependent on the others.  Based on ideas by Eric
     Schmidt.  (Contributed by Scott Fenton, 3-Jan-2013.) $)
  addcom $p |- ( ( A e. CC /\ B e. CC ) -> ( A + B ) = ( B + A ) ) $=
    ( cc wcel caddc co wceq cc0 cif opreq1 opreq2 eqeq12d 0cn elimel addcomi
    dedth2h ) ACDZBCDZABEFZBAEFZGQAHIZBEFZBUAEFZGUARBHIZEFZUDUAEFZGABHHAUAGSUBT
    UCAUABEJAUABEKLBUDGUBUEUCUFBUDUAEKBUDUAEJLUAUDAHCMNBHCMNOP $.
    $( [3-Jan-2013] $)

