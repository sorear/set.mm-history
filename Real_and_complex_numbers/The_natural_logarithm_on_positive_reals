$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The natural logarithm on positive reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c log $.

  $( Extend class notation with the natural logarithm function on positive
     reals. $)
  clog $a class log $.

  $( Define the natural logarithm function on positive real numbers.  See
     ~ http://en.wikipedia.org/wiki/Natural_logarithm ("The natural logarithm
     function can also be defined as the inverse function of the exponential
     function"). $)
  df-log $a |- log = `' ( exp |` RR ) $.

  ${
    $d x y $.
    $( Alternate version of ~ df-log .  (Contributed by Steve Rodriguez,
       22-Oct-2007.) $)
    dflog2 $p |- log = { <. x , y >. | ( x e. ( 0 (,) +oo ) /\
                           y = ( `' ( exp |` RR ) ` x ) ) } $=
      ( clog ce cr cres ccnv cv cc0 cpnf cioo co wcel cfv wceq wa copab df-log
      wfn wf1o reeff1o f1ocnv ax-mp f1ofn fnopabfv mpbi eqtr ) CDEFZGZAHZIJKLZM
      BHUJUINOPABQZRUIUKSZUIULOUKEUITZUMEUKUHTUNUAEUKUHUBUCUKEUIUDUCABUKUIUEUFU
      G $.
      $( [22-Oct-2007] $) $( [22-Oct-2007] $)
  $}

  $( Value of the natural logarithm function on positive reals.  (Contributed
     by Steve Rodriguez, 22-Oct-2007.) $)
  logvalt $p |- ( log ` A ) = ( `' ( exp |` RR ) ` A ) $=
    ( clog ce cr cres ccnv df-log fveq1i ) ABCDEFGH $.
    $( [22-Oct-2007] $) $( [22-Oct-2007] $)

  $( Closure of the natural logarithm function on positive reals.  (Contributed
     by Steve Rodriguez, 25-Nov-2007.) $)
  logclt $p |- ( A e. RR+ -> ( log ` A ) e. RR ) $=
    ( crp wcel ce cr cres ccnv cfv clog wf1o reeff1o2 f1ocnvdm mpan logvalt
    syl5eqel ) ABCZADEFZGHZEAIHEBQJPRECKEBAQLMANO $.
    $( [26-Nov-2007] $) $( [25-Nov-2007] $)

  $( Relationship between the natural logarithm and the exponential function.
     (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  eflogt $p |- ( A e. RR+ -> ( exp ` ( log ` A ) ) = A ) $=
    ( crp wcel ce cr cres ccnv cfv clog wceq wf1o reeff1o2 f1ocnvdm mpan fvres
    syl logvalt fveq2i syl6eqr f1ocnvfv2 eqtr3d ) ABCZADEFZGHZUCHZAIHZDHZAUBUEU
    DDHZUGUBUDECZUEUHJEBUCKZUBUILEBAUCMNUDEDOPUFUDDAQRSUJUBUEAJLEBAUCTNUA $.
    $( [26-Nov-2007] $) $( [25-Nov-2007] $)

  $( Relationship between the natural logarithm and the exponential function.
     (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  logeft $p |- ( A e. RR -> ( log ` ( exp ` A ) ) = A ) $=
    ( cr wcel ce cfv clog cres ccnv fvres fveq2d logvalt syl6reqr crp wf1o
    wceq reeff1o2 f1ocnvfv1 mpan eqtrd ) ABCZADEZFEZADBGZEZUCHZEZATUFUAUEEUBTUD
    UAUEABDIJUAKLBMUCNTUFAOPBMAUCQRS $.
    $( [26-Nov-2007] $) $( [25-Nov-2007] $)

  $( Relationship between the natural logarithm and the exponential function.
     (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  logeftb $p |- ( ( A e. RR+ /\ B e. RR ) ->
                  ( ( log ` A ) = B <-> ( exp ` B ) = A ) ) $=
    ( crp wcel cr wa ce cfv wceq cres ccnv clog wb fvres eqeq1d adantr wf1o
    reeff1o2 f1ocnvfvb mp3an1 bitr3d ancoms logvalt eqeq1i syl6rbbr ) ACDZBEDZF
    BGHZAIZAGEJZKHZBIZALHZBIUGUFUIULMUGUFFBUJHZAIZUIULUGUOUIMUFUGUNUHABEGNOPECU
    JQUGUFUOULMRECBAUJSTUAUBUMUKBAUCUDUE $.
    $( [26-Nov-2007] $) $( [25-Nov-2007] $)

  $( The natural logarithm of 1.  One case of Property 1a of [Cohen] p. 301.
     (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  log1 $p |- ( log ` 1 ) = 0 $=
    ( c1 clog cfv cc0 wceq ce ef0 crp wcel cr wb 1re lt01 elrpi 0re logeftb
    mp2an mpbir ) ABCDEZDFCAEZGAHIDJISTKALMNOADPQR $.
    $( [26-Nov-2007] $) $( [25-Nov-2007] $)

  $( The natural logarithm of ` e ` .  One case of Property 1b of [Cohen]
     p. 301.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  loge $p |- ( log ` e ) = 1 $=
    ( ceu clog cfv c1 wceq ce df-e eqcomi crp wcel cr wb ere cc0 clt 1re efgt0
    breqtrr elrpi logeftb mp2an mpbir ) ABCDEZDFCZAEZAUDGHAIJDKJUCUELAMNUDAODPQ
    GRSPADTUAUB $.
    $( [26-Nov-2007] $) $( [25-Nov-2007] $)

  ${
    logoprlem.1 $e |- ( ( ( log ` A ) e. CC /\ ( log ` B ) e. CC ) ->
                        ( exp ` ( ( log ` A ) F ( log ` B ) ) ) =
                        ( ( exp ` ( log ` A ) ) G ( exp ` ( log ` B ) ) ) ) $.
    logoprlem.2 $e |- ( ( ( log ` A ) e. RR /\ ( log ` B ) e. RR ) ->
                        ( ( log ` A ) F ( log ` B ) ) e. RR ) $.
    $( Lemma for ~ logmult and ~ logdivt .  Remark of [Cohen] p. 301 ("The
       proof of Property 3 is quite similar to the proof given for
       Property 2"). $)
    logoprlem $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                      ( log ` ( A G B ) ) = ( ( log ` A ) F ( log ` B ) ) ) $=
      ( crp wcel wa clog cfv ce co eflogt opreqan12d fveq2d cr wceq cc recnt
      syl2an logeft syl eqtr3d logclt ) AGHZBGHZIZAJKZLKZBJKZLKZDMZJKZABDMZJKUI
      UKCMZUHUMUOJUFUGUJAULBDANBNOPUIQHZUKQHZUNUPRUFUGUQURIZUPLKZJKZUNUPUISHZUK
      SHZVAUNRUQURVBVCIUTUMJEPUITUKTUAUSUPQHVAUPRFUPUBUCUDAUEBUEUAUD $.
      $( [26-Nov-2007] $) $( [25-Nov-2007] $)
  $}

  $( The natural logarithm of a product is a sum of natural logarithms.
     Property 2 of [Cohen] p. 301, restricted to natural logarithms.
     (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  logmult $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                  ( log ` ( A x. B ) ) = ( ( log ` A ) + ( log ` B ) ) ) $=
    ( caddc cmul clog cfv efaddt axaddrcl logoprlem ) ABCDAEFZBEFZGJKHI $.
    $( [26-Nov-2007] $) $( [25-Nov-2007] $)

  $( The natural logarithm of a quotient is a difference of natural logarithms.
     Exercise 72(a) and Property 3 of [Cohen] p. 301, restricted to natural
     logarithms.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  logdivt $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                  ( log ` ( A / B ) ) = ( ( log ` A ) - ( log ` B ) ) ) $=
    ( cmin cdiv clog cfv efsubt resubclt logoprlem ) ABCDAEFZBEFZGJKHI $.
    $( [26-Nov-2007] $) $( [25-Nov-2007] $)

  $( The natural logarithm of positive ` A ` raised to a power.  Property 4 of
     [Cohen] p. 301-302, restricted to natural logarithms and
     nonnegative-integer powers ` N ` .  (Contributed by Steve Rodriguez,
     25-Nov-2007.) $)
  logexpt $p |- ( ( A e. RR+ /\ N e. NN0 ) ->
                  ( log ` ( A ^ N ) ) = ( N x. ( log ` A ) ) ) $=
    ( crp wcel cn0 wa clog cfv cmul co ce cexp cc wceq efexpt logclt recnd
    sylan eflogt opreq1d adantr eqtrd fveq2d cr axmulrcl ancoms nn0ret syl2an
    logeft syl eqtr3d ) ACDZBEDZFZBAGHZIJZKHZGHZABLJZGHUPUNUQUSGUNUQUOKHZBLJZUS
    UOMDUMUQVANULUOBOULUOAPZQRULVAUSNUMULUTABLASTUAUBUCUNUPUDDZURUPNUOUDDZBUDDZ
    VCULUMVEVDVCBUOUEUFVBBUGUHUPUIUJUK $.
    $( [26-Nov-2007] $) $( [25-Nov-2007] $)

  $( Exponentiation of a positive real to a nonnegative integer power.
     (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  explogt $p |- ( ( A e. RR+ /\ N e. NN0 ) ->
                  ( A ^ N ) = ( exp ` ( N x. ( log ` A ) ) ) ) $=
    ( crp wcel cn0 wa clog cfv cmul co ce cexp cc wceq efexpt logclt recnd
    sylan eflogt opreq1d adantr eqtr2d ) ACDZBEDZFBAGHZIJKHZUEKHZBLJZABLJZUEMDU
    DUFUHNUCUEBOUCUEAPQRUCUHUINUDUCUGABLASTUAUB $.
    $( [26-Nov-2007] $) $( [25-Nov-2007] $)

  $( The natural logarithm on positive reals determines an isomorphism from
     positive reals onto reals.  (Contributed by Steve Rodriguez,
     25-Nov-2007.)  $)
  logiso $p |- log Isom < , < ( RR+ , RR ) $=
    ( crp cr clt clog wiso ce cres ccnv reefiso isocnv ax-mp wceq wb df-log
    isoeq1 mpbir ) ABCCDEZABCCFBGZHZEZBACCRETIBACCRJKDSLQTMNABCCSDOKP $.
    $( [26-Nov-2007] $) $( [25-Nov-2007] $)

  ${
    $d A x y $.  $d B y $.
    $( The natural logarithm function on positive reals is strictly
       monotonic.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
    logltbt $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                    ( A < B <-> ( log ` A ) < ( log ` B ) ) ) $=
      ( vx vy crp wcel wa cv clt wbr clog cfv wb wral cr wf1o wiso logiso
      df-iso mpbi pm3.27i wceq breq1 fveq2 breq1d bibi12d breq2 breq2d rcla42v
      mpi ) AEFBEFGCHZDHZIJZUKKLZULKLZIJZMZDENCENZABIJZAKLZBKLZIJZMZEOKPZUREOII
      KQVDURGRCDEOIIKSTUAUQVCAULIJZUTUOIJZMCDABEEUKAUBZUMVEUPVFUKAULIUCVGUNUTUO
      IUKAKUDUEUFULBUBZVEUSVFVBULBAIUGVHUOVAUTIULBKUDUHUFUIUJ $.
      $( [26-Nov-2007] $) $( [25-Nov-2007] $)
  $}


