$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          _e is irrational
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    eirrlem1.1 $e |- N e. NN $.
    $( Lemma for ~ eirr . $)
    eirrlem1 $p |- ( ( N + 2 ) / ( ( N + 1 ) ^ 2 ) ) < 1 $=
      ( c2 caddc co c1 cexp cdiv clt wbr cmul cle cn wcel nnge1 ax-mp cn0 wb
      1nn0 nnnn0i nn0leltp1 mp2an mpbi cr cc0 nnrei peano2nn nngt0i ltmulgt11
      mp3an 1re remulcli lelttri 2re readdcli resqcli w3a wne nnne0i sqgt0i
      ltdivmulOLD mpan2 cmin recni mulid1i nncni sqvali ax1cn adddiri mulid2i
      opreq2i 3eqtri eqtri breq2i ltsubaddi cc wceq 2cn pnpcan df-2 eqcomi
      subaddrii breq1i 3bitr2i bitri mpbir ) ACDEZAFDEZCGEZHEFIJZFAWHKEZIJZFALJ
      ZAWKIJZWLAMNZWMBAOPZFWHIJZWNWMWQWPFQNAQNWMWQRSABTFAUAUBUCAUDNWHUDNUEAIJWQ
      WNRABUFZWHWOWHMNBAUGPZUFZABUHAWHUIUJUCFAWKUKWRAWHWRWTULZUMUBWJWGWIFKEZIJZ
      WLWGUDNZWIUDNZFUDNZWJXCRZACWRUNUOZWHWTUPZUKXDXEXFUQUEWIIJZXGWHUEURXJWHWSU
      SWHWTUTPWGWIFVAVBUJXCWGWKWHDEZIJWGWHVCEZWKIJWLXBXKWGIXBWIXKWIWIXIVDVEWIWH
      WHKEWKFWHKEZDEXKWHWHWSVFZVGAFWHABVFZVHXNVIXMWHWKDWHXNVJVKVLVMVNWGWHWKXHWT
      XAVOXLFWKIXLCFVCEZFAVPNCVPNFVPNXLXPVQXOVRVHACFVSUJCFFVRVHVHCFFDEVTWAWBVMW
      CWDWEWF $.
      $( [9-Feb-2008] $)
  $}

  ${
    $d F k x z $.  $d Q j k x y z $.
    eirrlem2.1 $e |- F = { <. j , y >. | ( j e. NN0 /\
                           y = ( ( 1 ^ j ) / ( ! ` j ) ) ) } $.
    eirrlem2.2 $e |- P e. ZZ $.
    eirrlem2.3 $e |- Q e. NN $.
    $( Lemma for ~ eirr . $)
    eirrlem2 $p |- ( ( ! ` Q ) x.
                     ( ( P / Q ) - sum_ k e. ( 0 ... Q ) ( F ` k ) ) ) e. ZZ $=
      ( vx vz cfa cfv cdiv co cc0 cfz cv csu cmin cmul cz cn0 wcel cn nnnn0i
      faccl ax-mp nncni cc zcn nnne0i divcli cuz wral nn0uz eleqtri wss fzssuz
      elnn0uz c1 cexp eftval ax1cn eftcl mpan eqeltrd sylbir rgen ssralv mp2
      fsumcl mp2an subdii wceq facnn2 opreq2i wne wa pm3.2i nnm1nn0 nncn nnne0
      jca 3syl divcan5 mp3an eqtri simpli mulcli divcan2i eqtr3i nnz zmulcl
      eqeltri fsummulc1 elfznn0 1exp opreq1d eqtrd opreq2d w3a a1i 3jca divrec
      eqtr4d syl permnn zaddcl fsumcllem zsubcl ) CLMZBCNOZPCQOZERZFMZESZTOUAOX
      LXMUAOZXLXQUAOZTOZUBXLXMXQXLCUCUDZXLUEUDCIUFZCUGUHZUIZBCBUBUDZBUJUDZHBUKU
      HZCIUIZCIULZUMCPUNMZUDZXPUJUDZEXNUOZXQUJUDCUCYJYBUPUQZXNYJURYLEYJUOYMPCUS
      YLEYJXOYJUDXOUCUDZYLXOUTYOXPVAXOVBOZXOLMZNOZUJAVADFXOGVCZVAUJUDYOYRUJUDVD
      VAXOVEVFVGVHVIYLEXNYJVJVKZXPEPCVLVMVNXRUBUDXSUBUDXTUBUDXRCVATOZLMZBUAOZUB
      XLUUCXLNOZUAOXRUUCUUDXMXLUAUUDUUCUUBCUAOZNOZXMXLUUEUUCNCUEUDZXLUUEVOICVPU
      HVQYFCUJUDZCPVRZVSUUBUJUDZUUBPVRZVSZUUFXMVOYGUUHUUIYHYIVTUUGUULIUUGUUAUCU
      DZUUBUEUDZUULCWAZUUAUGZUUNUUJUUKUUBWBUUBWCWDWEUHZBCUUBWFWGWHVQUUCXLUUBBUU
      JUUKUUQWIYGWJYDXLYCULWKWLUUBUBUDZYEUUCUBUDUUGUURIUUGUUMUUNUURUUOUUPUUBWMW
      EUHHUUBBWNVMWOXSXNXLXPUAOZESZUBYKXLUJUDZYMXSUUTVOYNYDYTXPXLEPCWPWGYKUUSUB
      UDZEXNUOUUTUBUDYNUVBEXNXOXNUDZUUSUEUDUVBUVCUUSXLYQNOZUEUVCYOUUSUVDVOXOCWQ
      YOUUSXLVAYQNOZUAOZUVDYOXPUVEXLUAYOXPYRUVEYSYOYPVAYQNXOWRWSWTXAYOYQUEUDZUV
      AYQUJUDZYQPVRZXBUVDUVFVOXOUGUVGUVAUVHUVIUVAUVGYDXCYQWBYQWCXDXLYQXEWEXFXGY
      AUVCUVDUEUDYBXOCXHVFVGUUSWMXGVIJKUUSUBEPCJRKRXIXJVMWOXRXSXKVMWO $.
      $( [9-Feb-2008] $)

    $( Lemma for ~ eirr . $)
    eirrlem3 $p |- ( ( ! ` Q ) x.
                     sum_ k e. ( ZZ>= ` ( Q + 1 ) ) ( F ` k ) ) < 1 $=
      ( vx cfa cfv c1 caddc co cuz cv csu cmul c2 cexp cdiv cle wbr clt cn
      wcel wceq peano2nn ax-mp eqid ef1tlubi mp2an nncni ax1cn addassi df-2
      opreq2i eqtr4i cn0 nnnn0i faccl mulassi facp1 opreq1i sqvali 3eqtr4i
      opreq12i breqtri cc0 wb nngt0i cz cr wral cop cseqz cli wex nnz nn0uz
      eleqtri wa uztrn elnn0uz sylibr mpan2 eftval 1re reeftcl mpan eqeltrd
      syl rgen cc eftlex nn0ex fopabex2 isumrecl mp3an nnrei 2re readdcli
      resqcli remulcli sqcli nnne0i wne sqne0 mpbir mulne0i redivcli lemul2i
      mpbi recni mulcli divassi pm3.2i divcan5 eqtr3i eirrlem1 lelttri ) CKLZCM
      NOZPLZEQZFLZERZSOZCTNOZYDTUAOZUBOZUCUDYLMUEUDYIMUEUDYIYCYJYCYKSOZUBOZSOZY
      LUCYHYNUCUDZYIYOUCUDZYHYDMNOZYDKLZYDSOZUBOZYNUCYDUFUGZMMUHYHUUAUCUDCUFUGU
      UBICUIUJZMUKAMDEFYDGULUMYRYJYTYMUBYRCMMNOZNOYJCMMCIUNUOUOUPTUUDCNUQURUSYC
      YDSOZYDSOYCYDYDSOZSOYTYMYCYDYDYCCUTUGZYCUFUGCIVAZCVBUJZUNZYDUUCUNZUUKVCYS
      UUEYDSUUGYSUUEUHUUHCVDUJVEYKUUFYCSYDUUKVFURVGVHVIVJYCUEUDYPYQVKYCUUIVLYHY
      NYCYDVMUGZYGVNUGZEYEVOYDNVPFVQOJQVRUDJVSZYHVNUGUUBUULUUCYDVTUJUUMEYEYFYEU
      GZYFUTUGZUUMUUOYDVJPLZUGZUUPYDUTUUQYDUUCVAWAWBUUOUURWCYFUUQUGUUPYDYFVJWDY
      FWEWFWGUUPYGMYFUAOYFKLUBOZVNAMDFYFGWHMVNUGUUPUUSVNUGWIMYFWJWKWLWMWNMWOUGU
      UBUUNUOUUCJAMDFYDGWPUMJEFYDDAUTMDQZUAOUUTKLUBOFWQGWRWSWTZYJYMCTCIXAXBXCZY
      CYKYCUUIXAZYDYDUUCXAXDZXEYCYKUUJYDUUKXFZYCUUIXGZYKVJXHZYDVJXHZYDUUCXGYDWO
      UGUVGUVHVKUUKYDXIUJXJZXKZXLUVCXMUJXNYCYJSOYMUBOZYOYLYCYJYMUUJYJUVBXOZYCYK
      UUJUVEXPUVJXQYJWOUGYKWOUGZUVGWCYCWOUGZYCVJXHZWCUVKYLUHUVLUVMUVGUVEUVIXRUV
      NUVOUUJUVFXRYJYKYCXSWTXTVICIYAYIYLMYCYHUVCUVAXEYJYKUVBUVDUVIXLWIYBUM $.
      $( [9-Feb-2008] $)

    $( Lemma for ~ eirr . $)
    eirrlem4 $p |- 0 < ( ( ! ` Q ) x.
                         sum_ k e. ( ZZ>= ` ( Q + 1 ) ) ( F ` k ) ) $=
      ( vx cfa cfv c1 caddc co cuz cv csu cn0 wcel cn nnnn0i faccl ax-mp nnrei
      cz cr wral cop cseqz cli wbr wex nn0p1nn nn0z cc0 nn0uz eleqtri uztrn
      mpan2 elnn0uz cexp cdiv eftval 1re reeftcl mpan eqeltrd sylbir syl rgen
      cc ax1cn eftlex mp2an nn0ex fopabex2 isumrecl mp3an nngt0i clt wa 1exp
      syl6eqel lt01 syl5breqr nnre nngt0 jca divgt0 syl21anc breqtrrd cseq0
      efseq0ex addex seq0seqz breq1i exbii mpbi iserzgt0 mulgt0ii ) CKLZCMNOZPL
      ZEQZFLZERZXBCSTZXBUATCIUBZCUCUDZUEXCUFTZXFUGTZEXDUHXCNUIFUJOJQZUKULJUMZXG
      UGTXCSTXKXCXHXCUATZXICUNUDZUBZXCUOUDXLEXDXEXDTZXEUPPLZTZXLXRXCXSTZXTXCSXS
      XQUQURZXCXEUPUSUTXTXESTZXLXEVAZYCXFMXEVBOZXEKLZVCOZUGAMDFXEGVDZMUGTYCYGUG
      TVEMXEVFVGVHZVIVJVKMVLTZXOXNVMXPJAMDFXCGVNVOJEFXCDASMDQZVBOYKKLVCOFVPGVQZ
      VRVSXBXJVTYAXLUPXFWAULZWBZEXSUHUPNUIFUJOZXMUKULZJUMZUPXGWAULYBYNEXSXTYCYN
      YDYCXLYMYIYCUPYGXFWAYCYEUGTUPYEWAULYFUGTZUPYFWAULZWBZUPYGWAULYCYEMUGXEWCZ
      VEWDYCUPMYEWAWEUUAWFYCYFUATZYTXEUCUUBYRYSYFWGYFWHWIVJYEYFWJWKYHWLWIVIVKNF
      WMOZXMUKULZJUMZYQYJUUEVMJAMDFGWNUDUUDYPJUUCYOXMUKNFWOYLWPWQWRWSJEFUPXCYLW
      TVSXA $.
      $( [9-Feb-2008] $)

    $( Lemma for ~ eirr . $)
    eirrlem5 $p |- -. _e = ( P / Q ) $=
      ( vk vx ceu cdiv co wceq cfa cfv c1 caddc cuz cv csu cmul cz wcel cc0
      clt wbr wn 0z eirrlem4 eirrlem3 ax1cn addid2i breqtrri btwnnz mp3an cfz
      cmin cn0 cr ce cexp cc efval ax-mp df-e eftval sumeq2i 3eqtr4i ere
      eqeltrri recni wral nnnn0i nn0uz eleqtri wss fzssuz elnn0uz eftcl mpan
      eqeltrd sylbir rgen ssralv mp2 fsumcl mp2an cop cseqz cli wex cn
      peano2nn nnz eftlex nn0ex fopabex2 isumcl fopab cseq0 efseq0ex
      isum0spliti eqcomi subaddrii opreq2i eqeq1i biimpi opreq1d opreq2d
      syl5eqr eirrlem2 syl6eqel mto ) KBCLMZNZCOPZCQRMZSPITZEPZIUAZUBMZUCUDZUEU
      CUDUEYBUFUGYBUEQRMZUFUGYCUHUIABCDIEFGHUJYBQYDUFABCDIEFGHUKQULUMUNUEYBUOUP
      XPYBXQXOUECUQMZXTIUAZURMZUBMZUCXPYBXQUSXTIUAZYFURMZUBMYHYJYAXQUBYIYFYAYIK
      YIUTQVAPZUSQXSVBMXSOPLMZIUAZKYIQVCUDZYKYMNULQIVDVEVFUSXTYLIAQDEXSFVGZVHVI
      ZVJVKVLCUESPZUDXTVCUDZIYEVMZYFVCUDCUSYQCHVNZVOVPYEYQVQYRIYQVMYSUECVRYRIYQ
      XSYQUDXSUSUDZYRXSVSUUAXTYLVCYOYNUUAYLVCUDULQXSVTWAWBWCWDYRIYEYQWEWFXTIUEC
      WGWHXRUCUDZXRRWIEWJMJTZWKUGJWLZYAVCUDXRWMUDZUUBCWMUDUUEHCWNVEZXRWOVEYNUUE
      UUDULUUFJAQDEXRFWPWHJIEXRDAUSQDTZVBMUUGOPLMZEWQFWRWSWHYIYFYARMJIECYTDAUSV
      CUUHEFYNUUGUSUDUUHVCUDULQUUGVTWAWTYNREXAMUUCWKUGJWLULJAQDEFXBVEXCXDXEXFXP
      YJYGXQUBXPYIXOYFURXPYIXONKYIXOYPXGXHXIXJXKABCDIEFGHXLXMXN $.
      $( [9-Feb-2008] $)
  $}

  ${
    $d j q y $.  $d p q $.
    $( ` _e ` is irrational.  (Contributed by Paul Chapman, 9-Feb-2008.) $)
    eirr $p |- _e e/ QQ $=
      ( vp vq vy vj ceu cq wnel wcel wn cv cdiv co wceq cn wrex cz cc0 cif c1
      opreq1 eqeq2d notbid opreq2 cn0 cexp cfa cfv wa copab eqid 0z elimel 1nn
      eirrlem5 dedth2h nrexdv nrex elq mtbir df-nel mpbir ) EFGEFHZIVBEAJZBJZKL
      ZMZBNOZAPOVGAPVCPHZVFBNVHVDNHZVFIEVHVCQRZVDKLZMZIEVJVIVDSRZKLZMZIVCVDQSVC
      VJMZVFVLVPVEVKEVCVJVDKTUAUBVDVMMZVLVOVQVKVNEVDVMVJKUCUAUBCVJVMDDJZUDHCJSV
      RUELVRUFUGKLMUHDCUIZVSUJVCQPUKULVDSNUMULUNUOUPUQABEURUSEFUTVA $.
      $( [9-Feb-2008] $)
  $}

  $( MAY BE REVISED - make obsolete and use egt2lt3 instead $)
  $( Euler's constant ` _e ` = 2.71828... is greater than 2. $)
  egt2OLD $p |- 2 < _e $=
    ( c2 ceu clt wbr cle wne 2re ere ltleni ege2 wceq wn cq wcel wnel eirr
    df-nel mpbi cn 2nn nnq ax-mp eleq1 mpbiri mto df-ne mpbir mpbir2an ) ABCDAB
    EDBAFZABGHIJUIBAKZLUJBMNZBMOUKLPBMQRUJUKAMNZASNULTAUAUBBAMUCUDUEBAUFUGUH $.
    $( [28-Nov-2008] $)

  $( MAY BE REVISED - make obsolete and use egt2lt3 instead $)
  $( Euler's constant ` _e ` = 2.71828... is less than 3. $)
  elt3OLD $p |- _e < 3 $=
    ( ceu c3 clt wbr cle wne ere 3re ltleni ele3 wceq wn cq wcel wnel eirr
    df-nel mpbi cn 3nn nnq ax-mp eleq1 mpbii mto df-ne mpbir mpbir2an ) ABCDABE
    DBAFZABGHIJUIBAKZLUJAMNZAMOUKLPAMQRUJBMNZUKBSNULTBUAUBBAMUCUDUEBAUFUGUH $.
    $( [28-Nov-2008] $)

  $( Euler's constant ` _e ` = 2.71828... is bounded by 2 and 3. $)
  egt2lt3 $p |- ( 2 < _e /\ _e < 3 ) $=
    ( c2 ceu clt wbr c3 cle wne 2re ere ltleni ege2le3 simpli wceq wn cq wcel
    wnel eirr df-nel mpbi cn 2nn nnq ax-mp eleq1 mpbiri mto df-ne mpbir
    mpbir2an 3re simpri 3nn mpbii pm3.2i ) ABCDZBECDZUPABFDZBAGZABHIJURBEFDZKLU
    SBAMZNVABOPZBOQVBNRBOSTZVAVBAOPZAUAPVDUBAUCUDBAOUEUFUGBAUHUIUJUQUTEBGZBEIUK
    JURUTKULVEEBMZNVFVBVCVFEOPZVBEUAPVGUMEUCUDEBOUEUNUGEBUHUIUJUO $.
    $( [17-May-2009] $) $( [28-Nov-2008] $)


