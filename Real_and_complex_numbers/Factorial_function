$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Factorial function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ! $. $( Factorial. $)

  $( Extend class notation to include the factorial of nonnegative integers. $)
  cfa $a class ! $.

  $( Define the factorial function on nonnegative integers.  In the literature,
     the factorial function is written as a postscript exclamation point. $)
  df-fac $a |- ! = ( { <. 0 , 1 >. } u. seq 1 ( x. , _I ) ) $.

  $( Value of the factorial function for positive integers.  (Revised by Mario
     Carneiro, 13-Jul-2013.) $)
  facnn $p |- ( N e. NN ->
      ( ! ` N ) = ( seq 1 ( x. , _I ) ` N ) ) $=
    ( cfa cfv cmul cid c1 cseq wceq cn0 cc0 csn cdif cn 0nn0 elexi 1nn cun cres
    dfn2 eqtr3i cop df-fac cuz nnuz reseq2i cz wcel wfn 1z seqfn fnresdm uneq2i
    mp2b eqtr4i fvsnun2 eleq2s ) ABCADEFGZCHAIJKLZMJFIAUQBJINOFMPOBJFUAKZUQQUSU
    QURRZQUBUTUQUSUQFUCCZRZUTUQVAURUQMVAURUDSTUEFUFUGUQVAUHVBUQHUIDEFUJVAUQUKUM
    TULUNUOSUP $.
    $( [13-Jul-2013] $) $( [2-Dec-2004] $)

  $( The factorial of 0.  (Revised by Mario Carneiro, 13-Jul-2013.) $)
  fac0 $p |- ( ! ` 0 ) = 1 $=
    ( cc0 cn0 cmul cid cseq cfa 0nn0 elexi 1nn cop csn cun cdif cres df-fac cuz
    c1 cn cfv eqtr3i nnuz dfn2 reseq2i cz wcel wfn wceq 1z seqfn fnresdm uneq2i
    mp2b eqtr4i fvsnun1 ) AQBCDQEZFABGHQRIHFAQJKZUOLUPUOBAKMZNZLOURUOUPUOQPSZNZ
    URUOUSUQUORUSUQUAUBTUCQUDUEUOUSUFUTUOUGUHCDQUIUSUOUJULTUKUMUN $.
    $( [13-Jul-2013] $) $( [2-Dec-2004] $)

  $( The factorial of 1.  (Revised by Mario Carneiro, 13-Jul-2013.) $)
  fac1 $p |- ( ! ` 1 ) = 1 $=
    ( c1 cfa cfv cmul cid cseq cn wcel wceq 1nn facnn ax-mp cz seq1 fvi 3eqtri
    1z ) ABCZADEAFCZAECZAAGHZRSIJAKLAMHSTIQDEANLUATAIJAGOLP $.
    $( [13-Jul-2013] $) $( [2-Dec-2004] $)

  $( The factorial of a successor.  (Revised by Mario Carneiro,
     13-Jul-2013.) $)
  facp1 $p |- ( N e. NN0 ->
               ( ! ` ( N + 1 ) ) = ( ( ! ` N ) x. ( N + 1 ) ) ) $=
    ( cn0 wcel cn cc0 wceq wo c1 caddc co cfa cfv cmul elnn0 cid cseq facnn cvv
    ax-1cn eqtri peano2nn syl cuz seqp1 nnuz eleq2s opreq1d oprex ax-mp opreq2i
    fvi syl6reqr 3eqtrd addid2i fveq2i fac1 opreq1 fveq2 opreq12d fac0 opreq12i
    fveq2d mulid1i syl6eq 3eqtr4a jaoi sylbi ) ABCADCZAEFZGAHIJZKLZAKLZVJMJZFZA
    NVHVNVIVHVKVJMOHPZLZAVOLZVJOLZMJZVMVHVJDCVKVPFAUAVJQUBVPVSFAHUCLDMOHAUDUEUF
    VHVMVQVJMJVSVHVLVQVJMAQUGVRVJVQMVJRCVRVJFAHIUHVJRUKUIUJULUMVIEHIJZKLZHVKVMW
    AHKLHVTHKHSUNZUOUPTVIVJVTKAEHIUQZVBVIVMEKLZVTMJZHVIVLWDVJVTMAEKURWCUSWEHHMJ
    HWDHVTHMUTWBVAHSVCTVDVEVFVG $.
    $( [13-Jul-2013] $) $( [2-Dec-2004] $)

  $( The factorial of 2. $)
  fac2 $p |- ( ! ` 2 ) = 2 $=
    ( c2 cfa cfv c1 caddc co cmul df-2 fveq2i wcel wceq 1nn0 facp1 ax-mp eqcomi
    cn0 fac1 opreq12i 2cn mulid2i eqtri 3eqtri ) ABCDDEFZBCZDBCZUCGFZAAUCBHIDPJ
    UDUFKLDMNUFDAGFAUEDUCAGQAUCHORASTUAUB $.
    $( [17-Mar-2005] $)

  $( The factorial of 3. $)
  fac3 $p |- ( ! ` 3 ) = 6 $=
    ( c3 cfa cfv c2 c1 caddc co cmul df-3 fveq2i cn0 wcel wceq 2nn0 facp1 ax-mp
    c6 fac2 eqcomi 3eqtri opreq12i 2cn 3re recni mulcomi 3t2e6 ) ABCDEFGZBCZDBC
    ZUGHGZQAUGBIJDKLUHUJMNDOPUJDAHGADHGQUIDUGAHRAUGISUADAUBAUCUDUEUFTT $.
    $( [17-Mar-2005] $)

  $( Value of the factorial function expressed recursively. $)
  facnn2 $p |- ( N e. NN ->
               ( ! ` N ) = ( ( ! ` ( N - 1 ) ) x. N ) ) $=
    ( cn wcel cc c1 cmin co cn0 wa cfa cfv cmul wceq elnnnn0 caddc facp1 adantl
    ax-1cn npcan adantr mpan2 fveq2d opreq2d 3eqtr3d sylbi ) ABCADCZAEFGZHCZIZA
    JKZUGJKZALGZMANUIUGEOGZJKZUKUMLGZUJULUHUNUOMUFUGPQUFUNUJMUHUFUMAJUFEDCUMAMR
    AESUAZUBTUFUOULMUHUFUMAUKLUPUCTUDUE $.
    $( [2-Dec-2004] $)

  ${
    $d j k N $.
    $( Closure of the factorial function. $)
    faccl $p |- ( N e. NN0 -> ( ! ` N ) e. NN ) $=
      ( vj vk cv cfa cfv cn wcel cc0 c1 caddc co wceq fveq2 eleq1d fac0 eqeltri
      1nn cn0 wa cmul facp1 adantl nn0p1nn nnmulcl sylan2 eqeltrd expcom nn0ind
      ) BDZEFZGHIEFZGHCDZEFZGHZUMJKLZEFZGHZAEFZGHBCAUJIMUKULGUJIENOUJUMMUKUNGUJ
      UMENOUJUPMUKUQGUJUPENOUJAMUKUSGUJAENOULJGPRQUOUMSHZURUOUTTUQUNUPUALZGUTUQ
      VAMUOUMUBUCUTUOUPGHVAGHUMUDUNUPUEUFUGUHUI $.
      $( [2-Dec-2004] $)
  $}

  $( The factorial function is nonzero. $)
  facne0 $p |- ( N e. NN0 -> ( ! ` N ) =/= 0 ) $=
    ( cn0 wcel cfa cfv cn cc0 wne faccl nnne0 syl ) ABCADEZFCLGHAILJK $.
    $( [26-Apr-2005] $)

  ${
    $d j M $.  $d j k N $.
    $( A natural number divides the factorial of an equal or larger number. $)
    facdiv $p |- ( ( M e. NN0 /\ N e. NN /\ N <_ M ) ->
                  ( ( ! ` M ) / N ) e. NN ) $=
      ( cn0 wcel cn cle wbr cfa cfv cdiv co cc0 wceq breq2 fveq2 opreq1d eleq1d
      wi imbi12d imbi2d vj vk cv c1 caddc clt wn nngt0 cr wb nnre ltnle sylancr
      0re mpbid pm2.21d wa cmul wo peano2nn0 nn0re leloe syl2an nnnn0 nn0leltp1
      syl sylan nn0p1nn nnmulcl sylan2 expcom adantl wne faccl nncn nn0cn nnne0
      cc jca adantr div23 syl3anc sylibrd imim2d sylbird divcan4 eqeltrd opreq2
      com23 syl5ibcom a1dd jaod sylbid com34 com12 imp4d facp1 exp4d a2d nn0ind
      ex 3imp ) ACDBEDZBAFGZAHIZBJKZEDZXCBUAUCZFGZXHHIZBJKZEDZRZRXCBLFGZLHIZBJK
      ZEDZRZRXCBUBUCZFGZXSHIZBJKZEDZRZRXCBXSUDUEKZFGZYEHIZBJKZEDZRZRXCXDXGRZRUA
      UBAXHLMZXMXRXCYLXIXNXLXQXHLBFNYLXKXPEYLXJXOBJXHLHOPQSTXHXSMZXMYDXCYMXIXTX
      LYCXHXSBFNYMXKYBEYMXJYABJXHXSHOPQSTXHYEMZXMYJXCYNXIYFXLYIXHYEBFNYNXKYHEYN
      XJYGBJXHYEHOPQSTXHAMZXMYKXCYOXIXDXLXGXHABFNYOXKXFEYOXJXEBJXHAHOPQSTXCXNXQ
      XCLBUFGZXNUGZBUHXCLUIDBUIDZYPYQUJUNBUKZLBULUMUOUPXSCDZXCYDYJYTXCYDYFYIYTX
      CYDYFUQUQYAYEURKZBJKZEDZYIYTXCYDYFUUCXCYTYDYFUUCRRXCYTYFYDUUCXCYTYFYDUUCR
      ZRXCYTUQZYFBYEUFGZBYEMZUSZUUDXCYRYEUIDZYFUUHUJYTYSYTYECDZUUIXSUTZYEVAVFBY
      EVBVCUUEUUFUUDUUGUUEUUFXTUUDXCBCDYTXTUUFUJBVDBXSVEVGUUEYDXTUUCUUEYCUUCXTU
      UEYCYBYEURKZEDZUUCYTYCUUMRXCYCYTUUMYTYCYEEDUUMXSVHYBYEVIVJVKVLUUEUUBUULEU
      UEYAVRDZYEVRDZBVRDZBLVMZUQZUUBUULMYTUUNXCYTYAEDZUUNXSVNZYAVOVFVLZYTUUOXCY
      TUUJUUOUUKYEVPVFVLXCUURYTXCUUPUUQBVOZBVQZVSVTYAYEBWAWBQWCWDWIWEUUEUUGUUCY
      DUUEYABURKZBJKZEDUUGUUCUUEUVEYAEUUEUUNUUPUUQUVEYAMUVAXCUUPYTUVBVTXCUUQYTU
      VCVTYABWFWBYTUUSXCUUTVLWGUUGUVEUUBEUUGUVDUUABJBYEYAURWHPQWJWKWLWMXAWNWOWP
      YTYHUUBEYTYGUUABJXSWQPQWCWRWSWTXB $.
      $( [2-May-2005] $)
  $}

  $( No natural number (greater than one) divides the factorial plus one of an
     equal or larger number. $)
  facndiv $p |- ( ( ( M e. NN0 /\ N e. NN ) /\ ( 1 < N /\ N <_ M ) ) ->
                -. ( ( ( ! ` M ) + 1 ) / N ) e. ZZ ) $=
    ( cn0 wcel cn wa c1 clt wbr cle cfa co cdiv cz cmin syl wceq nncn ad2antrr
    cc cfv caddc wn cr recnz sylan ad2ant2lr facdiv 3expa nnz adantrl zsubcl ex
    nnre syl5com cc0 wne faccl peano2cn nnne0 ad2antlr divsubdir syl3anc ax-1cn
    jca pncan2 sylancl opreq1d eqtr3d eleq1d sylibd mtod ) ACDZBEDZFZGBHIZBAJIZ
    FZFZAKUAZGUBLZBMLZNDZGBMLZNDZVNVPWEUCZVMVQVNBUDDVPWFBUNBUEUFUGVSWCWBVTBMLZO
    LZNDZWEVSWGNDZWCWIVOVQWJVPVOVQFWGEDZWJVMVNVQWKABUHUIWGUJPUKWCWJWIWBWGULUMUO
    VSWHWDNVSWAVTOLZBMLZWHWDVSWATDZVTTDZBTDZBUPUQZFZWMWHQVMWNVNVRVMWOWNVMVTEDWO
    AURVTRPZVTUSPSVMWOVNVRWSSVNWRVMVRVNWPWQBRBUTVEVAWAVTBVBVCVMWMWDQVNVRVMWLGBM
    VMWOGTDWLGQWSVDVTGVFVGVHSVIVJVKVL $.
    $( [3-May-2005] $)

  ${
    $d j k M $.  $d j N $.
    $( Ordering property of factorial. $)
    facwordi $p |- ( ( M e. NN0 /\ N e. NN0 /\ M <_ N ) ->
                  ( ! ` M ) <_ ( ! ` N ) ) $=
      ( cn0 wcel cle wbr cfa cfv wa wi cc0 c1 breq2 anbi2d fveq2 breq2d imbi12d
      wceq cr syl vj vk cv caddc weq nn0le0eq0 biimpa fveq2d fac0 eqeltri leidi
      co 1re syl6eqbr impexp clt wo wb nn0re peano2re leloe syl2an nn0leltp1 cn
      cmul faccl nncn mulid1 3syl nnre nnnn0 nn0ge0 jca nn0p1nn lemul2a mp3anl1
      cc nnge1 syl21anc eqbrtrrd facp1 breqtrrd adantl adantr peano2nn0 syl3anc
      letr mpan2d imim2d com23 sylbird leid biimpcd syl5 a1dd jaod sylbid com13
      ex com4l a2d imp4a syl5bi nn0ind 3impib 3com12 ) BCDZACDZABEFZAGHZBGHZEFZ
      XGXHXIXLXHAUAUCZEFZIZXJXMGHZEFZJXHAKEFZIZXJKGHZEFZJXHAUBUCZEFZIZXJYBGHZEF
      ZJZXHAYBLUDULZEFZIZXJYHGHZEFZJZXHXIIZXLJUAUBBXMKRZXOXSXQYAYOXNXRXHXMKAEMN
      YOXPXTXJEXMKGOPQUAUBUEZXOYDXQYFYPXNYCXHXMYBAEMNYPXPYEXJEXMYBGOPQXMYHRZXOY
      JXQYLYQXNYIXHXMYHAEMNYQXPYKXJEXMYHGOPQXMBRZXOYNXQXLYRXNXIXHXMBAEMNYRXPXKX
      JEXMBGOPQXSXJXTXTEXSAKGXHXRAKRAUFUGUHXTXTLSUIUMUJUKUNYGXHYCYFJZJZYBCDZYMX
      HYCYFUOUUAYTXHYIYLUUAXHYSYIYLJYIUUAXHYSYLXHUUAYIYSYLJZXHUUAYIUUBJXHUUAIZY
      IAYHUPFZAYHRZUQZUUBXHASDYHSDZYIUUFURUUAAUSUUAYBSDUUGYBUSYBUTTZAYHVAVBUUCU
      UDUUBUUEUUCUUDYCUUBAYBVCUUCYSYCYLUUCYFYLYCUUCYFYEYKEFZYLUUAUUIXHUUAYEYEYH
      VEULZYKEUUAYELVEULZYEUUJEUUAYEVDDZYEVQDUUKYERYBVFZYEVGYEVHVIUUAUUGYESDZKY
      EEFZIZLYHEFZUUKUUJEFZUUHUUAUUNUUOUUAUULUUNUUMYEVJTZUUAUULYECDUUOUUMYEVKYE
      VLVIVMUUAYHVDDUUQYBVNYHVRTLSDUUGUUPUUQUURUMLYHYEVOVPVSVTYBWAWBWCUUCXJSDZU
      UNYKSDZYFUUIIYLJXHUUTUUAXHXJVDDUUTAVFXJVJTZWDUUAUUNXHUUSWCUUAUVAXHUUAYHCD
      YKVDDUVAYBWEYHVFYKVJVIWCXJYEYKWGWFWHWIWJWKUUCUUEYLYSXHUUEYLJUUAUUEXJYKRZX
      HYLAYHGOXHUUTXJXJEFZUVCYLJUVBXJWLUVCUVDYLXJYKXJEMWMVIWNWDWOWPWQWSWRWTXAXB
      XCXDXEXF $.
      $( [9-Dec-2005] $)

    $( A lower bound for the factorial function. $)
    faclbnd $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                   ( M ^ ( N + 1 ) ) <_ ( ( M ^ M ) x. ( ! ` N ) ) ) $=
      ( wcel cc0 wceq c1 caddc co cexp cfa cfv cle wbr wi opreq2d cr syl adantr
      cmul wa vj vk cn0 cn wo elnn0 opreq1 fveq2 breq12d imbi2d cuz nnre elnnuz
      cv nnge1 biimpi expwordi syl3anc ax-1cn addid2i opreq2i a1i fac0 cc nnnn0
      reexpcl syl2anc recnd mulid1 syl5eq clt nn0re peano2re lelttric ad3antrrr
      3brtr4d syl2an simpllr peano2nn0 nngt0 0re ltle mpan expge0 faccl remulcl
      sylc 3syl jca31 jca jca32 simplr simprr lemul12a anandis nncn expp1 facp1
      adantl nn0cn peano2cn mulass eqtr4d exp32 com23 nn0ltp1le ancoms ad2antrr
      wb simpr cz ad2antlr nn0z nnz mpbird anim12i id nn0ge0 lemulge11 letrd ex
      eluz sylbid a1dd jaod mpd expcom a2d nn0ind impcom nn0p1nn 0exp 0cn ax-mp
      exp0 opreq1i mulid2 opreq12 anidms opreq1d syl5ibr imp jaoian sylanb ) AU
      CCZAUDCZADEZUEBUCCZABFGHZIHZAAIHZBJKZSHZLMZAUFUUFUUHUUNUUGUUHUUFUUNUUFAUA
      UNZFGHZIHZUUKUUOJKZSHZLMZNUUFADFGHZIHZUUKDJKZSHZLMZNUUFAUBUNZFGHZIHZUUKUV
      FJKZSHZLMZNUUFAUVGFGHZIHZUUKUVGJKZSHZLMZNUUFUUNNUAUBBUUODEZUUTUVEUUFUVQUU
      QUVBUUSUVDLUVQUUPUVAAIUUODFGUGOUVQUURUVCUUKSUUODJUHOUIUJUUOUVFEZUUTUVKUUF
      UVRUUQUVHUUSUVJLUVRUUPUVGAIUUOUVFFGUGOUVRUURUVIUUKSUUOUVFJUHOUIUJUUOUVGEZ
      UUTUVPUUFUVSUUQUVMUUSUVOLUVSUUPUVLAIUUOUVGFGUGOUVSUURUVNUUKSUUOUVGJUHOUIU
      JUUOBEZUUTUUNUUFUVTUUQUUJUUSUUMLUVTUUPUUIAIUUOBFGUGOUVTUURUULUUKSUUOBJUHO
      UIUJUUFAFIHZUUKUVBUVDLUUFAPCZFALMZAFUKKCZUWAUUKLMAULZAUOZUUFUWDAUMUPAFAUQ
      URUVBUWAEUUFUVAFAIFUSUTVAVBUUFUVDUUKFSHZUUKUVCFUUKSVCVAUUFUUKVDCZUWGUUKEU
      UFUUKUUFUWBUUEUUKPCZUWEAVEZAAVFZVGZVHZUUKVIQVJVPUVFUCCZUUFUVKUVPUUFUWNUVK
      UVPNZUUFUWNTZAUVGLMZUVGAVKMZUEZUWOUUFUWBUVGPCZUWSUWNUWEUWNUVFPCZUWTUVFVLZ
      UVFVMZQAUVGVNVQUWPUWQUWOUWRUWPUVKUWQUVPUWPUVKUWQUVPUWPUVKUWQTZTUVHASHZUVJ
      UVGSHZUVMUVOLUWPUVKUWQUXEUXFLMZUWPUVKTZUWPUWQTZTZUVHPCZDUVHLMZTUVJPCZTZUW
      BDALMZTZUWTTTUXDUXGUXJUXNUXPUWTUXJUXKUXLUXMUXJUWBUVGUCCZUXKUUFUWBUWNUVKUX
      IUWEVOZUXJUWNUXQUUFUWNUVKUXIVRZUVFVSZQZAUVGVFVGUXJUWBUXQUXOUXLUXRUYAUXJUW
      BDAVKMZUXOUXRUUFUYBUWNUVKUXIAVTVODPCUWBUYBUXONWADAWBWCWGZAUVGWDURUXJUWIUV
      IPCZUXMUXJUWBUUEUWIUXRUUFUUEUWNUVKUXIUWJVOUWKVGUXJUWNUVIUDCZUYDUXSUVFWEZU
      VIULZWHUUKUVIWFVGWIUXJUWBUXOUXRUYCWJUXJUWNUXAUWTUXSUXBUXCWHWKUXJUVKUWQUWP
      UVKUXIWLUXHUWPUWQWMWJUVHUVJAUVGWNWGWOUWPUVMUXEEZUXDUUFAVDCUXQUYHUWNAWPUXT
      AUVGWQVQRUWPUVOUXFEUXDUWPUVOUUKUVIUVGSHZSHZUXFUWPUVNUYIUUKSUWNUVNUYIEUUFU
      VFWRWSOUWPUWHUVIVDCZUVGVDCZUXFUYJEUUFUWHUWNUWMRUWNUYKUUFUWNUVIUWNUYEUYDUY
      FUYGQVHWSUWNUYLUUFUWNUVFVDCUYLUVFWTUVFXAQWSUUKUVIUVGXBURXCRVPXDXEUWPUWRUV
      PUVKUWPUWRUVLALMZUVPUWNUUFUWRUYMXIZUWNUXQUUEUYNUUFUXTUWJUVGAXFVQXGUWPUYMU
      VPUWPUYMTZUVMUUKUVOUWPUVMPCZUYMUUFUWBUVLUCCZUYPUWNUWEUWNUXQUYQUXTUVGVSQZA
      UVLVFVQRUUFUWIUWNUYMUWLXHUWPUVOPCZUYMUUFUWIUVNPCZUYSUWNUWLUWNUXQUVNUDCZUY
      TUXTUVGWEZUVNULWHZUUKUVNWFVQRUYOUWBUWCAUVLUKKCZUVMUUKLMUUFUWBUWNUYMUWEXHU
      WPUWCUYMUUFUWCUWNUWFRRUYOVUDUYMUWPUYMXJUYOUVLXKCZAXKCZVUDUYMXIUYOUYQVUEUW
      NUYQUUFUYMUYRXLUVLXMQUUFVUFUWNUYMAXNXHUVLAYBVGXOAUVLAUQURUWPUUKUVOLMZUYMU
      WPUWIUYTTDUUKLMZFUVNLMZTVUGUUFUWIUWNUYTUWLVUCXPUUFVUHUWNVUIUUFUUEVUHUWJUU
      EUWBUUEUXOVUHAVLUUEXQAXRAAWDURQUWNUXQVUAVUIUXTVUBUVNUOWHXPUUKUVNXSVGRXTYA
      YCYDYEYFYGYHYIYJUUGUUHUUNUUHUUNUUGDUUIIHZDDIHZUULSHZLMUUHDUULVUJVULLUUHUU
      LUDCZUULUCCDUULLMBWEZUULVEUULXRWHUUHUUIUDCVUJDEBYKUUIYLQUUHVULFUULSHZUULV
      UKFUULSDVDCVUKFEYMDYOYNYPUUHVUMUULVDCVUOUULEVUNUULWPUULYQWHVJVPUUGUUJVUJU
      UMVULLADUUIIUGUUGUUKVUKUULSUUGUUKVUKEADADIYRYSYTUIUUAUUBUUCUUD $.
      $( [17-Dec-2005] $)
  $}

  $( A lower bound for the factorial function. $)
  faclbnd2 $p |- ( N e. NN0 -> ( ( 2 ^ N ) / 2 ) <_ ( ! ` N ) ) $=
    ( cn0 wcel c2 cexp co cdiv c1 cle cmul c4 sq2 cc wceq 2cn mpan wa cc0 wbr
    cr caddc cfa cfv 2t2e4 eqtr4i opreq2i expp1 opreq1d syl5eq expcl wne pm3.2i
    2ne0 divmuldiv mpanr12 sylancl dividi halfcl 3syl 3eqtr2rd 2nn0 faclbnd 2re
    mulid1 wb peano2nn0 reexpcl sylancr faccl nnre syl clt 4re eqeltri breqtrri
    cn 4pos ledivmul mp3an3 syl2anc mpbird eqbrtrd ) ABCZDAEFZDGFZDAHUAFZEFZDDE
    FZGFZAUBUCZIWCWIWDDJFZDDJFZGFZWEDDGFZJFZWEWCWIWGWLGFWMWHWLWGGWHKWLLUDUEUFWC
    WGWKWLGDMCZWCWGWKNODAUGPUHUIWCWDMCZWPWOWMNZWPWCWQODAUJPZOWQWPQWPDRUKZQZXAWR
    WPWTOUMULZXBWDDDDUNUOUPWCWOWEHJFZWEWNHWEJDOUMUQUFWCWQWEMCXCWENWSWDURWEVDUSU
    IUTWCWIWJISZWGWHWJJFISZDBCWCXEVADAVBPWCWGTCZWJTCZXDXEVEZWCDTCWFBCXFVCAVFDWF
    VGVHWCWJVPCXGAVIWJVJVKXFXGWHTCZRWHVLSZQXHXIXJWHKTLVMVNRKWHVLVQLVOULWGWJWHVR
    VSVTWAWB $.
    $( [17-Dec-2005] $)

  $( A lower bound for the factorial function. $)
  faclbnd3 $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                 ( M ^ N ) <_ ( ( M ^ M ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cn cc0 wceq wo cexp co cle wbr wa c1 cr adantr sylan reexpcl 1re
    cmul cfa cfv elnn0 caddc cuz nnre nnge1 cz nn0z uzid peano2uz 3syl expwordi
    adantl syl3anc nnnn0 faclbnd wi nn0re peano2nn0 syl2an mpancom remulcl letr
    faccl syl mp2and 0exp 0re lt01 ltleii syl6eqbr opreq2 cc exp0 ax-mp eqbrtri
    0cn leidi jaoi sylbi 1nn nnmulcl sylancr mpan mp3an2 syl2anc opreq1 opreq12
    wb anidms syl6eq opreq1d breq12d mpbird jaoian sylanb ) ACDZAEDZAFGZHBCDZAB
    IJZAAIJZBUAUBZTJZKLZAUCWSXAXFWTWSXAMZXBABNUDJZIJZKLZXIXEKLZXFXGAODZNAKLZXHB
    UEUBZDZXJWSXLXAAUFPWSXMXAAUGPXGBUHDZBXNDXOXAXPWSBUIUNBUJBBUKULABXHUMUOWSWRX
    AXKAUPZABUQQWSWRXAXJXKMXFURZXQWRXAMXBODZXIODZXEODZXRWRXLXAXSAUSZABRQWRXLXHC
    DXTXAYBBUTAXHRVAWRXCODZXDODZYAXAXLWRYCYBAARVBXAXDEDZYDBVEZXDUFVFZXCXDVCVAXB
    XIXEVDUOQVGWTXAMXFFBIJZNXDTJZKLZXAYJWTXAYHNKLZNYIKLZYJXABEDZBFGZHYKBUCYMYKY
    NYMYHFNKBVHFNVISVJVKVLYNYHFFIJZNKBFFIVMYONNKFVNDYONGVRFVOVPZNSVSVQVLVTWAXAY
    IEDZYLXANEDYEYQWBYFNXDWCWDYIUGVFXAYHODZYIODZYKYLMYJURZFODXAYRVIFBRWEXANODZY
    DYSSYGNXDVCWDYRUUAYSYTSYHNYIVDWFWGVGUNWTXFYJWJXAWTXBYHXEYIKAFBIWHWTXCNXDTWT
    XCYONWTXCYOGAFAFIWIWKYPWLWMWNPWOWPWQ $.
    $( [19-Dec-2005] $)

  ${
    faclbnd4lem1.1 $e |- N e. NN $.
    faclbnd4lem1.2 $e |- K e. NN0 $.
    faclbnd4lem1.3 $e |- M e. NN0 $.
    $( Lemma for ~ faclbnd4 .  Prepare the induction step. $)
    faclbnd4lem1 $p |- ( ( ( ( N - 1 ) ^ K ) x. ( M ^ ( N - 1 ) ) ) <_
       ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` ( N - 1 ) ) ) ->
                   ( ( N ^ ( K + 1 ) ) x. ( M ^ N ) ) <_ ( ( (
  2 ^ ( ( K + 1 ) ^ 2 ) ) x. ( M ^ ( M + ( K + 1 ) ) ) ) x. ( ! ` N ) ) ) $=
      ( c1 cle wbr co cexp cmul c2 cr wcel mp2an cc0 wa cn0 remulcli cmin caddc
      clt wo cfa cfv nnrei 1re lelttric wceq letri3i cn nnge1 mpbiran2 0re lt01
      ax-mp ltleii pm3.2i 2re 1nn nn0nnaddcl nnnn0i 2nn0 reexpcl nn0rei nn0ge0i
      nn0expcl cc 2cn exp0 cuz 1lt2 nn0uz eleqtri expwordi mp3an eqbrtrri elnn0
      nncn exp1 syl nnuz mp3an13 eqbrtrrd breq1 mpbiri jaoi lemul12a mp2 opreq1
      sylbi cz nnzi 1exp syl6eq opreq2 nn0cni opreq12d fveq2 fac1 opreq2d recni
      mulcli mulid1i breq12d sylbir adantr faccl nn0mulcli nncni expp1 opreq12i
      a1i expm1t elnnnn0 simpri mulassi eqtri nn0addcli w3a 3pm3.2i wb nnltp1le
      mpbi df-2 breq1i bitr4i expubnd mp3an12 lemul1a mul4i syl6breq mpan letrd
      opreq2i ax-1cn eqtr3i nn0zi sqvali sylancr mul12i 3eqtr2i addassi 3eqtr4i
      adantl mul32i facnn2 syl5eqbr expadd eluz1i eqcomi adddii eqtr4i mpbir2an
      ltp1i 3brtr4i syl6breqr jaoian ) CGHIZGCUCIZUDZCGUAJZAKJZBUVCKJZLJZMAMKJZ
      KJZBBAUBJZKJZLJZUVCUEUFZLJZHIZCAGUBJZKJZBCKJZLJZMUVOMKJZKJZBBUVOUBJZKJZLJ
      ZCUEUFZLJZHIZCNOZGNOZUVBCDUGZUHCGUIPUUTUVNUWFUVAUUTUWFUVNUUTCGUJZUWFUWJUU
      TGCHIZCGUWIUHUKCULOZUWKDCUMUQUNUWJUWFGBLJZUWCHIZUWHQGHIZRZUVTNOZRZBNOZQBH
      IZRZUWBNOZRZRGUVTHIZBUWBHIZRUWNUWRUXCUWPUWQUWHUWOUHQGUOUHUPURUSMNOZUVSSOZ
      UWQUTUVOSOZMSOZUXGUVOASOZGULOZUVOULOZEVAAGVBPZVCZVDUVOMVHPZMUVSVEPZUSUXAU
      XBUWSUWTBFVFZBFVGUSUWBBSOZUWASOUWBSOFUWAUXRUXLUWAULOFUXMBUVOVBPZVCBUWAVHP
      ZVFUSUSUXDUXEMQKJZGUVTHMVIOZUYAGUJVJMVKUQUXFGMHIZUVSQVLUFZOUYAUVTHIUTGMUH
      UTVMURZUVSSUYDUXOVNVOMQUVSVPVQVRUXRUXEFUXRBULOZBQUJZUDUXEBVSUYFUXEUYGUYFB
      GKJZBUWBHUYFBVIOZUYHBUJZBVTBWAZWBUYFGBHIZUYHUWBHIZBUMUWSUYLUWAGVLUFZOUYMU
      XQUWAULUYNUXSWCVOBGUWAVPWDWBWEUYGUXEQUWBHIUWBUXTVGBQUWBHWFWGWHWLUQUSGUVTB
      UWBWIWJUWJUVRUWMUWEUWCHUWJUVPGUVQBLUWJUVPGUVOKJZGCGUVOKWKUVOWMOUYOGUJUVOU
      XMWNUVOWOUQWPUWJUVQUYHBCGBKWQUYIUYJBFWRZUYKUQWPWSUWJUWEUWCGLJUWCUWJUWDGUW
      CLUWJUWDGUEUFGCGUEWTXAWPXBUWCUVTUWBUVTUXPXCZUWBUXTWRZXDXEWPXFWGXGXHUVAUVN
      RZUVRUVTUWBUWDLJZLJZUWEHUYSUVRUVHMAKJZLJZUYTLJZVUAUVRNOUYSUVPUVQUWGUXHUVP
      NOUWIUXNCUVOVEPUWSCSOZUVQNOUXQCDVCZBCVEPTXNVUDNOUYSVUCUYTUVHVUBUXFUVGSOZU
      VHNOUTUXJUXIVUGEVDAMVHPZMUVGVEPZVUBUXIUXJVUBSOVDEMAVHPZVFZTZUYTUWBUWDUXTU
      WDVUEUWDULOVUFCXIUQZVCXJZVFZTXNVUANOUYSUVTUYTUXPVUOTXNUYSUVRCAKJZCUVEBLJZ
      LJZLJZVUDHUVRVUPCLJZVUQLJVUSUVPVUTUVQVUQLCVIOZUXJUVPVUTUJCDXKZECAXLPUYIUW
      LUVQVUQUJUYPDBCXOPXMVUPCVUQVUPUWGUXJVUPNOZUWIECAVEPZXCVVBVUQUVEBUXRUVCSOZ
      UVESOFVVAVVEUWLVVAVVERDCXPYEXQZBUVCVHPZFXJZWRZXRXSUYSVUSUVMVUBCLJZBLJZLJZ
      VUDHUYSVUSUVFVVKLJZVVLVUSNOUYSVUPVURVVDVURCVUQVUFVVHXJZVFZTXNVVMNOUYSUVFV
      VKUVDUVEUVCNOUXJUVDNOUVCVVFVFEUVCAVEPZUVEVVGVFTZVVKVVJBVUBCVUJVUFXJZFXJZV
      FZTXNVVLNOUYSUVMVVKUVKUVLUVHUVJVUIUWSUVISOZUVJNOUXQBAFEXTZBUVIVEPZTZUVLVV
      EUVLULOVVFUVCXIUQZUGTZVVTTXNUVAVUSVVMHIUVNUVAVUSVUBUVDLJZVURLJZVVMHUVAVVC
      VWGNOZVURNOZQVURHIZRZYAVUPVWGHIZVUSVWHHIVVCVWIVWLVVDVUBUVDVUKVVPTVWJVWKVV
      OVURVVNVGUSYBUVAMCHIZVWMUVAGGUBJZCHIZVWNUXKUWLUVAVWPYCVADGCYDPMVWOCHYFYGY
      HUWGUXJVWNVWMUWIECAYIYJWLVUPVWGVURYKUUAVWHVVJUVDVUQLJZLJVVJUVFBLJZLJVVMVU
      BUVDCVUQVUBVUJWRZUVDVVPXCZVVBVVIYLVWRVWQVVJLUVDUVEBVWTUVEVVGWRUYPXRYPVVJU
      VFBVVJVVRWRUVFVVQXCUYPUUBUUCYMXHUVNVVMVVLHIZUVAUVFNOZUVMNOZVVKNOZQVVKHIZR
      ZYAUVNVXAVXBVXCVXFVVQVWFVXDVXEVVTVVKVVSVGUSYBUVFUVMVVKYKYNUUFYOVVLVUCUWBL
      JZUWDLJZVUDUVMVUBBLJZCLJZLJUVKVXILJZUVLCLJZLJVVLVXHUVKUVLVXICUVKVWDXCUVLV
      WEXKVUBBVWSUYPXDVVBYLVVKVXJUVMLVUBCBVWSVVBUYPUUGYPVXGVXKUWDVXLLVUCUVJBLJZ
      LJVXGVXKVXMUWBVUCLBUVIGUBJZKJZVXMUWBUYIVWAVXOVXMUJUYPVWBBUVIXLPVXNUWABKBA
      GUYPAEWRZYQUUDYPYRYPUVHVUBUVJBUVHVUIXCVWSUVJVWCXCUYPYLYRUWLUWDVXLUJDCUUHU
      QXMUUEVUCUWBUWDVUCVULXCUYRUWDVUMXKZXRXSYMUUIVUDVUAHIZUYSVUCNOZUWQUYTNOZQU
      YTHIZRZYAVUCUVTHIVXRVXSUWQVYBVULUXPVXTVYAVUOUYTVUNVGUSYBMUVGAUBJZKJZVUCUV
      THUYBVUGUXJVYDVUCUJVJVUHEMUVGAUUJVQUXFUYCUVSVYCVLUFOZVYDUVTHIUTUYEVYEUVSW
      MOVYCUVSHIVYCUVSVYCUVGAVUHEXTYSUUKUVSUXOYSAUVOLJZUVOUVOLJZVYCUVSHANOZUVON
      OZVYIQUVOHIZRZYAAUVOHIVYFVYGHIVYHVYIVYKAEVFZUVOUXMUGZVYIVYJVYMUVOUXNVGUSY
      BAUVOVYLVYMAVYLUUPURAUVOUVOYKPVYCAALJZAGLJZUBJVYFUVGVYNAVYOUBAVXPYTVYOAAV
      XPXEUULXMAAGVXPVXPYQUUMUUNUVOUVOUXMXKYTUUQUUOMVYCUVSVPVQVRVUCUVTUYTYKPXNY
      OUVTUWBUWDUYQUYRVXQXRUURUUSYN $.
      $( [20-Dec-2005] $)
  $}

  $( Lemma for ~ faclbnd4 .  Use the weak deduction theorem to convert the
     hypotheses of ~ faclbnd4lem1 to antecedents. $)
  faclbnd4lem2 $p |- ( ( M e. NN0 /\ K e. NN0 /\ N e. NN ) ->
           ( ( ( ( N - 1 ) ^ K ) x. ( M ^ ( N - 1 ) ) ) <_
       ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` ( N - 1 ) ) ) ->
                   ( ( N ^ ( K + 1 ) ) x. ( M ^ N ) ) <_ ( ( (
  2 ^ ( ( K + 1 ) ^ 2 ) ) x. ( M ^ ( M + ( K + 1 ) ) ) ) x. ( ! ` N ) ) ) ) $=
    ( cn0 c1 co cexp cmul caddc cfa cfv cle wbr opreq1 opreq2d opreq12d opreq1d
    c2 wi breq12d wcel cn cmin cif wceq id imbi12d opreq2 fveq2d fveq2 1nn 1nn0
    elimel faclbnd4lem1 dedth3h ) BDUAZADUAZCUBUAZCEUCFZAGFZBUSGFZHFZRARGFZGFZB
    BAIFZGFZHFZUSJKZHFZLMZCAEIFZGFZBCGFZHFZRVKRGFZGFZBBVKIFZGFZHFZCJKZHFZLMZSUT
    UPBEUDZUSGFZHFZVDWCWCAIFZGFZHFZVHHFZLMZVLWCCGFZHFZVPWCWCVKIFZGFZHFZVTHFZLMZ
    SUSUQAEUDZGFZWDHFZRWRRGFZGFZWCWCWRIFZGFZHFZVHHFZLMZCWREIFZGFZWKHFZRXHRGFZGF
    ZWCWCXHIFZGFZHFZVTHFZLMZSURCEUDZEUCFZWRGFZWCXSGFZHFZXEXSJKZHFZLMZXRXHGFZWCX
    RGFZHFZXOXRJKZHFZLMZSBACEEEBWCUEZVJWJWBWQYLVBWEVIWILYLVAWDUTHBWCUSGNOYLVGWH
    VHHYLVFWGVDHYLBWCVEWFGYLUFZBWCAINPOQTYLVNWLWAWPLYLVMWKVLHBWCCGNOYLVSWOVTHYL
    VRWNVPHYLBWCVQWMGYMBWCVKINPOQTUGAWRUEZWJXGWQXQYNWEWTWIXFLYNUTWSWDHAWRUSGUHQ
    YNWHXEVHHYNVDXBWGXDHYNVCXARGAWRRGNOYNWFXCWCGAWRWCIUHOPQTYNWLXJWPXPLYNVLXIWK
    HYNVKXHCGAWREINZOQYNWOXOVTHYNVPXLWNXNHYNVOXKRGYNVKXHRGYOQOYNWMXMWCGYNVKXHWC
    IYOOOPQTUGCXRUEZXGYEXQYKYPWTYBXFYDLYPWSXTWDYAHYPUSXSWRGCXREUCNZQYPUSXSWCGYQ
    OPYPVHYCXEHYPUSXSJYQUIOTYPXJYHXPYJLYPXIYFWKYGHCXRXHGNCXRWCGUHPYPVTYIXOHCXRJ
    UJOTUGWRWCXRCEUBUKUMAEDULUMBEDULUMUNUO $.
    $( [23-Dec-2005] $)


  $( Lemma for ~ faclbnd4 .  The ` N = 0 ` case. $)
  faclbnd4lem3 $p |- ( ( ( M e. NN0
            /\ K e. NN0 ) /\ N = 0 ) -> ( ( N ^ K ) x. ( M ^ N ) )
       <_ ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel wa cc0 wceq cexp co cmul c2 caddc cle wbr c1 cn adantl syl6eq cc
    cfa cfv wo elnn0 0exp nnnn0 nn0expcl mpan2 sylancr nn0addcl syldan nn0mulcl
    2nn0 syl2anc sylan2 nn0ge0 syl eqbrtrd 0nn0 sylancl nnexpcl mpdan id opreq1
    0cn addid1i opreq12d exp0 ax-mp syl6eqel jaoi sylbi nnmulcl mpan nnge1 3syl
    1nn adantr wb opreq2 opreq2d 2cn breq12d mpbird jaodan sylan2b nn0cn mulid1
    sq0i sylan9eq 3brtr4d fveq2 fac0 ) BDEZADEZFZCGHZFCAIJZBCIJZKJZLALIJZIJZBBA
    MJZIJZKJZCUAUBZKJZNOZGAIJZBGIJZKJZXEPKJZNOZWPXMWQWPXIXEXKXLNWOWNAQEZAGHZUCX
    IXENOZAUDWNXNXPXOWNXNFZXIGXENXNXIGHWNAUERXQXEDEZGXENOXNWNWOXRAUFWPXBDEZXDDE
    ZXRWOXSWNWOLDEZXADEZXSUMWOYAYBUMALUGUHLXAUGUIRWNWOXCDEXTBAUJBXCUGUKXBXDULUN
    ZUOXEUPUQURWNXOFXPPPBBGMJZIJZKJZNOZWNYGXOWNYEQEZYFQEZYGWNBQEZBGHZUCYHBUDYJY
    HYKYJYDDEZYHYJWNGDEZYLBUFUSBGUJUTBYDVAVBYKYEPQYKYEGGIJZPYKBGYDGIYKVCYKYDGGM
    JGBGGMVDGVEVFSVGGTEYNPHVEGVHVIZSVQVJVKVLPQEYHYIVQPYEVMVNYFVOVPVRXOXPYGVSWNX
    OXIPXEYFNXOXIYNPAGGIVTYOSXOXBPXDYEKXOXBLGIJZPXOXAGLIAWIWALTEYPPHWBLVHVISXOX
    CYDBIAGBMVTWAVGWCRWDWEWFWNWOXKXIPKJZXIWNXJPXIKWNBTEXJPHBWGBVHUQWAWOXIDEZXIT
    EYQXIHYMWOYRUSGAUGVNXIWGXIWHVPWJWPXRXETEXLXEHYCXEWGXEWHVPWKVRWQXHXMVSWPWQWT
    XKXGXLNWQWRXIWSXJKCGAIVDCGBIVTVGWQXFPXEKWQXFGUAUBPCGUAWLWMSWAWCRWD $.
    $( [23-Dec-2005] $)

  ${
    $d j m n M $.  $d j m n K $.  $d j n N $.
    $( Lemma for ~ faclbnd4 .  Prove the ` 0 < N ` case by induction on
       ` K ` . $)
    faclbnd4lem4 $p |- ( ( N e. NN /\ K e. NN0 /\ M e. NN0 ) ->
                   ( ( N ^ K ) x. ( M ^ N ) ) <_
              ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
      ( vn cn wcel cexp co cmul c2 caddc cle wbr wa cc0 c1 wceq opreq2d opreq1d
      opreq2 vm vj cn0 cfa cfv cv wral wi opreq1 opreq12d fveq2 breq12d cbvralv
      cmin clt wo cr nnre 1re lelttric sylancl ancli andi sylib nnge1 adantr wb
      letri3 biimpar anassrs mpdan ax-1cn subidi syl6eq syl faclbnd4lem3 sylan2
      a1d 1nn nnsub mpan biimpa rcla4v adantl faclbnd4lem2 3expa syld ralrimdva
      jaodan syl5bi expcom a2d nnnn0 faclbnd3 cc nncn nn0cn expcl syl2an mulid2
      exp0 eqtrd sq0 opreq2i 2cn ax-mp eqtri a1i addid1 mpancom 3brtr4d ralbidv
      ralrimiva imbi2d nn0indALT imp rcla4va 3impb ) CEFZAUCFZBUCFZCAGHZBCGHZIH
      ZJAJGHZGHZBBAKHZGHZIHZCUDUEZIHZLMZXTYANXSDUFZAGHZBYMGHZIHZYIYMUDUEZIHZLMZ
      DEUGZYLXTYAYTYAYMUAUFZGHZYOIHZJUUAJGHZGHZBBUUAKHZGHZIHZYQIHZLMZDEUGZUHYAY
      MOGHZYOIHZJOJGHZGHZBBOKHZGHZIHZYQIHZLMZDEUGZUHYAYMUBUFZGHZYOIHZJUVBJGHZGH
      ZBBUVBKHZGHZIHZYQIHZLMZDEUGZUHYAYMUVBPKHZGHZYOIHZJUVMJGHZGHZBBUVMKHZGHZIH
      ZYQIHZLMZDEUGZUHYAYTUHUAUBAUVBUCFZYAUVLUWCYAUWDUVLUWCUHUVLUUAUVBGHZBUUAGH
      ZIHZUVIUUAUDUEZIHZLMZUAEUGZYAUWDNZUWCUVKUWJDUAEYMUUAQZUVDUWGUVJUWILUWMUVC
      UWEYOUWFIYMUUAUVBGUIYMUUABGTUJUWMYQUWHUVIIYMUUAUDUKRULUMUWLUWKUWBDEUWLYME
      FZNUWKYMPUNHZUVBGHZBUWOGHZIHZUVIUWOUDUEZIHZLMZUWBUWNUWLUWNYMPLMZNZUWNPYMU
      OMZNZUPZUWKUXAUHZUWNUWNUXBUXDUPZNUXFUWNUXHUWNYMUQFZPUQFZUXHYMURZUSYMPUTVA
      VBUWNUXBUXDVCVDUWLUXCUXGUXEUWLUXCNUXAUWKUXCUWLUWOOQZUXAUXCYMPQZUXLUXCPYML
      MZUXMUWNUXNUXBYMVEVFUWNUXBUXNUXMUWNUXMUXBUXNNZUWNUXIUXJUXMUXOVGUXKUSYMPVH
      VAVIVJVKUXMUWOPPUNHOYMPPUNUIPVLVMVNVOUVBBUWOVPVQVRUXEUXGUWLUXEUWOEFZUXGUW
      NUXDUXPPEFUWNUXDUXPVGVSPYMVTWAWBUWJUXAUAUWOEUUAUWOQZUWGUWRUWIUWTLUXQUWEUW
      PUWFUWQIUUAUWOUVBGUIUUAUWOBGTUJUXQUWHUWSUVIIUUAUWOUDUKRULWCVOWDWIVQYAUWDU
      WNUXAUWBUHUVBBYMWEWFWGWHWJWKWLYAUUTDEYAUWNNZYOBBGHZYQIHZUUMUUSLUWNYAYMUCF
      ZYOUXTLMYMWMZBYMWNVQUXRUUMPYOIHZYOUWNUUMUYCQYAUWNUULPYOIUWNYMWOFUULPQYMWP
      YMXAVOSWDUXRYOWOFZUYCYOQYABWOFZUYAUYDUWNBWQZUYBBYMWRWSYOWTVOXBYAUUSUXTQUW
      NYAUURUXSYQIYAUURPUXSIHZUXSYAUUOPUUQUXSIUUOPQYAUUOJOGHZPUUNOJGXCXDJWOFUYH
      PQXEJXAXFXGXHYAUUPBBGYAUYEUUPBQUYFBXIVORUJYAUXSWOFZUYGUXSQUYEYAUYIUYFBBWR
      XJUXSWTVOXBSVFXKXMUUAOQZUUKUVAYAUYJUUJUUTDEUYJUUCUUMUUIUUSLUYJUUBUULYOIUU
      AOYMGTSUYJUUHUURYQIUYJUUEUUOUUGUUQIUYJUUDUUNJGUUAOJGUIRUYJUUFUUPBGUUAOBKT
      RUJSULXLXNUUAUVBQZUUKUVLYAUYKUUJUVKDEUYKUUCUVDUUIUVJLUYKUUBUVCYOIUUAUVBYM
      GTSUYKUUHUVIYQIUYKUUEUVFUUGUVHIUYKUUDUVEJGUUAUVBJGUIRUYKUUFUVGBGUUAUVBBKT
      RUJSULXLXNUUAUVMQZUUKUWCYAUYLUUJUWBDEUYLUUCUVOUUIUWALUYLUUBUVNYOIUUAUVMYM
      GTSUYLUUHUVTYQIUYLUUEUVQUUGUVSIUYLUUDUVPJGUUAUVMJGUIRUYLUUFUVRBGUUAUVMBKT
      RUJSULXLXNUUAAQZUUKYTYAUYMUUJYSDEUYMUUCYPUUIYRLUYMUUBYNYOIUUAAYMGTSUYMUUH
      YIYQIUYMUUEYFUUGYHIUYMUUDYEJGUUAAJGUIRUYMUUFYGBGUUAABKTRUJSULXLXNXOXPYSYL
      DCEYMCQZYPYDYRYKLUYNYNYBYOYCIYMCAGUIYMCBGTUJUYNYQYJYIIYMCUDUKRULXQVQXR $.
      $( [19-Dec-2005] $)
  $}

  $( Variant of ~ faclbnd5 providing a non-strict lower bound. $)
  faclbnd4 $p |- ( ( N e. NN0 /\ K e. NN0 /\ M e. NN0 ) ->
                 ( ( N ^ K ) x. ( M ^ N ) ) <_
            ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cexp co cmul c2 caddc cfa cfv cle wbr wa cn cc0 wceq wo 3com13
    elnn0 faclbnd4lem4 3expa faclbnd4lem3 jaodan sylan2b 3impa ) BDEZADEZCDEZCA
    FGBCFGHGIAIFGFGBBAJGFGHGCKLHGMNZUHUIUJUKUJUHUIOZCPEZCQRZSUKCUAULUMUKUNUHUIU
    MUKUMUIUHUKABCUBTUCABCUDUEUFUGT $.
    $( [23-Dec-2005] $)

  $( The factorial function grows faster than powers and exponentiations.  If
     we consider ` K ` and ` M ` to be constants, the right-hand side of the
     inequality is a constant times ` N ` -factorial. $)
  faclbnd5 $p |- ( ( N e. NN0 /\ K e. NN0 /\ M e. NN ) ->
                 ( ( N ^ K ) x. ( M ^ N ) ) <
      ( ( 2 x. ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cn cexp co cmul c2 clt wbr wa cr reexpcl nnre remulcl syl2an syl
    cc caddc cfa cfv nn0re sylan ancoms anandirs 2nn 2nn0 mpan2 nnexpcl sylancr
    nn0expcl nnnn0 nn0addcl sylan2 anabss7 nnmulcl anabss5 2re faclbnd4 syl3an3
    faccl cle 3coml 3expa c1 1lt2 cc0 wb nngt0 ltmulgt12 mp3an2 syl2anc lelttrd
    mpbii wceq recnd 2cn mulass mp3an1 breqtrrd 3impa 3comr ) ADEZBFEZCDEZCAGHZ
    BCGHZIHZJJAJGHZGHZBBAUAHZGHZIHZIHCUBUCZIHZKLZWEWFWGWRWEWFMZWGMZWJJWOWPIHZIH
    ZWQKWTWJXAXBWEWFWGWJNEZWEWGMWHNEZWINEZXCWFWGMWGWEXDWGCNEWEXDCUDCAOUEUFWFBNE
    WGXEBPBCOUEWHWIQRUGWSWONEZWPNEZXANEZWGWSWOFEZXFWEWFXIWEWLFEZWNFEZXIWSWEJFEW
    KDEZXJUHWEJDEXLUIAJUMUJJWKUKULWEWFXKWSWFWMDEZXKWFWEBDEZXMBUNZXNWEXMBAUOUFUP
    BWMUKUPUQWLWNURRUSZWOPSZWGWPFEZXGCVCZWPPSZWOWPQRZWTJNEZXHXBNEUTYAJXAQULWEWF
    WGWJXAVDLZWGWEWFYCWFWGWEXNYCXOABCVAVBVEVFWTVGJKLZXAXBKLZVHWTXHVIXAKLZYDYEVJ
    ZYAWTXAFEZYFWSXIXRYHWGXPXSWOWPURRXAVKSXHYBYFYGUTXAJVLVMVNVPVOWSWOTEZWPTEZWQ
    XBVQZWGWSWOXQVRWGWPXTVRJTEYIYJYKVSJWOWPVTWARWBWCWD $.
    $( [24-Dec-2005] $)

  ${
    $d M m $.  $d N k m $.
    $( Geometric lower bound for the factorial function, where N is usually
       held constant.  (Contributed by Paul Chapman, 28-Dec-2007.) $)
    faclbnd6 $p |- ( ( N e. NN0 /\ M e. NN0 ) ->
             ( ( ! ` N ) x. ( ( N + 1 ) ^ M ) ) <_ ( ! ` ( N + M ) ) ) $=
      ( cn0 wcel cfa cfv c1 caddc co cexp cmul cle wbr cc0 opreq2 cr syl adantr
      wceq wa vm vk cv wi opreq2d fveq2d breq12d imbi2d cn faccl nnre leid 3syl
      cc nn0cn peano2cn exp0 nncn mulid1 eqtrd addid1 3brtr4d peano2nn0 reexpcl
      weq nn0re sylan remulcl syl2anc nnnn0 nn0ge0 simpr expge0 mulge0 syl22anc
      syl3anc jca nn0addcl readdcl syl2an jca31 adantl wb leadd2 mpbid eqbrtrrd
      0re mp3an1 leadd1 mp3an3 ax-1cn addass breqtrd lemul12a sylc expp1 mulass
      1re expcl eqtr4d facp1 3eqtr3d ex expcom a2d nn0ind impcom ) ACDBCDZBEFZB
      GHIZAJIZKIZBAHIZEFZLMZXHXIXJUAUCZJIZKIZBXPHIZEFZLMZUDXHXIXJNJIZKIZBNHIZEF
      ZLMZUDXHXIXJUBUCZJIZKIZBYGHIZEFZLMZUDXHXIXJYGGHIZJIZKIZBYMHIZEFZLMZUDXHXO
      UDUAUBAXPNSZYAYFXHYSXRYCXTYELYSXQYBXIKXPNXJJOUEYSXSYDEXPNBHOUFUGUHUAUBVEZ
      YAYLXHYTXRYIXTYKLYTXQYHXIKXPYGXJJOUEYTXSYJEXPYGBHOUFUGUHXPYMSZYAYRXHUUAXR
      YOXTYQLUUAXQYNXIKXPYMXJJOUEUUAXSYPEXPYMBHOUFUGUHXPASZYAXOXHUUBXRXLXTXNLUU
      BXQXKXIKXPAXJJOUEUUBXSXMEXPABHOUFUGUHXHXIXIYCYELXHXIUIDZXIPDZXIXILMBUJZXI
      UKZXIULUMXHYCXIGKIZXIXHYBGXIKXHXJUNDZYBGSXHBUNDZUUHBUOZBUPQZXJUQQUEXHXIUN
      DZUUGXISXHUUCUULUUEXIURQZXIUSQUTXHYDBEXHUUIYDBSZUUJBVAQZUFVBYGCDZXHYLYRXH
      UUPYLYRUDXHUUPTZYLYRUUQYLTZYIXJKIZYKYPKIZYOYQLUURYIPDZNYILMZTZYKPDZTXJPDZ
      NXJLMZTYPPDZTZTZYLXJYPLMZTUUSUUTLMUUQUVIYLUUQUVCUVDUVHUUQUVAUVBUUQUUDYHPD
      ZUVAXHUUDUUPXHUUCUUDUUEUUFQRZXHUVEUUPUVKXHXJCDZUVEBVCZXJVFQZXJYGVDVGZXIYH
      VHVIUUQUUDNXILMZUVKNYHLMZUVBUVLXHUVQUUPXHUUCUVQUUEUUCXICDUVQXIVJXIVKQQRUV
      PUUQUVEUUPUVFUVRXHUVEUUPUVORZXHUUPVLXHUVFUUPXHUVMUVFUVNXJVKQRZXJYGVMVPXIY
      HVNVOVQUUQYKUIDZUVDUUQYJCDZUWABYGVRZYJUJQYKUKQUUQUVEUVFUVGUVSUVTXHBPDZYMP
      DZUVGUUPBVFZUUPYMCDUWEYGVCYMVFQBYMVSVTWAWARUURYLUVJUUQYLVLUUQUVJYLUUQXJYJ
      GHIZYPLUUQBYJLMZXJUWGLMZUUQYDBYJLXHUUNUUPUUORUUQNYGLMZYDYJLMZUUPUWJXHYGVK
      WBUUQYGPDZUWDUWJUWKWCZUUPUWLXHYGVFWBXHUWDUUPUWFRZNPDUWLUWDUWMWGNYGBWDWHVI
      WEWFUUQUWDYJPDZUWHUWIWCZUWNUUQUWBUWOUWCYJVFQUWDUWOGPDUWPWRBYJGWIWJVIWEXHU
      UIYGUNDZUWGYPSZUUPUUJYGUOUUIUWQGUNDUWRWKBYGGWLWJVTZWMRVQYIYKXJYPWNWOUUQYO
      UUSSYLUUQYOXIYHXJKIZKIZUUSUUQYNUWTXIKXHUUHUUPYNUWTSUUKXJYGWPVGUEUUQUULYHU
      NDZUUHUUSUXASXHUULUUPUUMRXHUUHUUPUXBUUKXJYGWSVGXHUUHUUPUUKRXIYHXJWQVPWTRU
      UQYQUUTSYLUUQUWGEFZYKUWGKIZYQUUTUUQUWBUXCUXDSUWCYJXAQUUQUWGYPEUWSUFUUQUWG
      YPYKKUWSUEXBRVBXCXDXEXFXG $.
      $( [28-Dec-2007] $)
  $}

  $( The product of two factorials is greater than or equal to the factorial of
     (the floor of) their average. $)
  facavg $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
       ( ! ` ( |_ ` ( ( M + N ) / 2 ) ) ) <_ ( ( ! ` M ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel wa co cle wbr cfv cfa cr syl 3syl adantr letr syl3anc cc0 adantl
    wi c1 caddc c2 cdiv wo cfl cmul nn0re avgle syl2an nn0addcl rehalfcl reflcl
    flle mpand nn0ge0 halfnneg2 mpbid flge0nn0 syl2anc simpl facwordi 3exp sylc
    wb wceq cn cc faccl nncn mulid1 nnre nnnn0 jca 1re lemul2a mp3anl1 syl21anc
    nnge1 eqbrtrrd remulcl mpan2d 3syld simpr mulid2 lemul1a jaod mpd ) ACDZBCD
    ZEZABUAFZUBUCFZAGHZWLBGHZUDZWLUEIZJIZAJIZBJIZUFFZGHZWHAKDZBKDZWOWIAUGZBUGZA
    BUHUIWJWMXAWNWJWMWPAGHZWQWRGHZXAWJWPWLGHZWMXFWJWKKDZWLKDZXHWJWKCDZXIABUJZWK
    UGZLZWKUKZWLUMMZWJWPKDZXJXBXHWMEXFSWJXIXJXQXNXOWLULMZWJXKXIXJXLXMXOMZWHXBWI
    XDNWPWLAOPUNWJWPCDZWHXFXGSWJXJQWLGHZXTXSWJQWKGHZYAWJXKYBXLWKUOLWJXKXIYBYAVD
    XLXMWKUPMUQWLURUSZWHWIUTXTWHXFXGWPAVAVBVCWJXGWRWTGHZXAWJWRTUFFZWRWTGWHYEWRV
    EZWIWHWRVFDZWRVGDYFAVHZWRVIWRVJMNWJWSKDZWRKDZQWRGHZEZTWSGHZYEWTGHZWIYIWHWIW
    SVFDZYIBVHZWSVKLZRZWHYLWIWHYJYKWHYGYJYHWRVKLZWHYGWRCDYKYHWRVLWRUOMVMNWIYMWH
    WIYOYMYPWSVRLRTKDZYIYLYMYNVNTWSWRVOVPVQVSWJWQKDZYJWTKDZXGYDEXASWJXTWQVFDUUA
    YCWPVHWQVKMZWHYJWIYSNZWHYJYIUUBWIYSYQWRWSVTUIZWQWRWTOPWAWBWJWNWPBGHZWQWSGHZ
    XAWJXHWNUUFXPWJXQXJXCXHWNEUUFSXRXSWIXCWHXERWPWLBOPUNWJXTWIUUFUUGSYCWHWIWCXT
    WIUUFUUGWPBVAVBVCWJUUGWSWTGHZXAWJTWSUFFZWSWTGWIUUIWSVEZWHWIYOWSVGDUUJYPWSVI
    WSWDMRWJYJYIQWSGHZEZTWRGHZUUIWTGHZUUDWIUULWHWIYIUUKYQWIYOWSCDUUKYPWSVLWSUOM
    VMRWHUUMWIWHYGUUMYHWRVRLNYTYJUULUUMUUNVNTWRWSWEVPVQVSWJUUAYIUUBUUGUUHEXASUU
    CYRUUEWQWSWTOPWAWBWFWG $.
    $( [9-Dec-2005] $)

