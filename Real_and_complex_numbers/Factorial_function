$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Factorial function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ! $. $( Factorial. $)

  $( Extend class notation to include the factorial of nonnegative integers. $)
  cfa $a class ! $.

  $( Define the factorial function on nonnegative integers.  In the literature,
     the factorial function is written as a postscript exclamation point. $)
  df-fac $a |- ! = ( { <. 0 , 1 >. } u. seq 1 ( x. , _I ) ) $.

  $( Value of the factorial function for positive integers.  (Revised by Mario
     Carneiro, 13-Jul-2013.) $)
  facnn $p |- ( N e. NN ->
      ( ! ` N ) = ( seq 1 ( x. , _I ) ` N ) ) $=
    ( cfa cfv cmul cid c1 cseq wceq cn0 cc0 csn cdif cn 0nn0 elisseti 1nn cop
    cun cres df-fac cuz nnuz dfn2 eqtr3i reseq2i cz wcel wfn 1z seqfn fnresdm
    mp2b uneq2i eqtr4i fvsnun2 eleq2s ) ABCADEFGZCHAIJKLZMJFIAUQBJINOFMPOBJFQKZ
    UQRUSUQURSZRTUTUQUSUQFUACZSZUTUQVAURUQMVAURUBUCUDUEFUFUGUQVAUHVBUQHUIDEFUJV
    AUQUKULUDUMUNUOUCUP $.
    $( [13-Jul-2013] $) $( [2-Dec-2004] $)

  $( The factorial of 0.  (Revised by Mario Carneiro, 13-Jul-2013.) $)
  fac0 $p |- ( ! ` 0 ) = 1 $=
    ( cc0 c1 cn0 cmul cid cseq cfa 0nn0 elisseti cn 1nn cop csn cun cdif cres
    df-fac cuz cfv nnuz dfn2 eqtr3i reseq2i cz wcel wfn wceq 1z seqfn fnresdm
    mp2b uneq2i eqtr4i fvsnun1 ) ABCDEBFZGACHIBJKIGABLMZUONUPUOCAMOZPZNQURUOUPU
    OBRSZPZURUOUSUQUOJUSUQTUAUBUCBUDUEUOUSUFUTUOUGUHDEBUIUSUOUJUKUBULUMUN $.
    $( [13-Jul-2013] $) $( [2-Dec-2004] $)

  $( The factorial of 1.  (Revised by Mario Carneiro, 13-Jul-2013.) $)
  fac1 $p |- ( ! ` 1 ) = 1 $=
    ( c1 cfa cfv cmul cid cseq cn wcel wceq 1nn facnn ax-mp cz 1z seq1NEW fvi
    3eqtri ) ABCZADEAFCZAECZAAGHZRSIJAKLAMHSTINDEAOLUATAIJAGPLQ $.
    $( [13-Jul-2013] $) $( [2-Dec-2004] $)

  $( The factorial of a successor.  (Revised by Mario Carneiro,
     13-Jul-2013.) $)
  facp1 $p |- ( N e. NN0 ->
               ( ! ` ( N + 1 ) ) = ( ( ! ` N ) x. ( N + 1 ) ) ) $=
    ( cn0 wcel cn cc0 wceq wo c1 caddc co cfa cfv cmul elnn0 cid cseq peano2nn
    facnn syl cuz seqp1 nnuz eleq2s opreq1d cvv oprex fvi ax-mp opreq2i
    syl6reqr 3eqtrd ax-1cn addid2i fveq2i fac1 eqtri opreq1 fveq2d fveq2
    opreq12d fac0 opreq12i mulid1i syl6eq 3eqtr4a jaoi sylbi ) ABCADCZAEFZGAHIJ
    ZKLZAKLZVJMJZFZANVHVNVIVHVKVJMOHPZLZAVOLZVJOLZMJZVMVHVJDCVKVPFAQVJRSVPVSFAH
    TLDMOHAUAUBUCVHVMVQVJMJVSVHVLVQVJMARUDVRVJVQMVJUECVRVJFAHIUFVJUEUGUHUIUJUKV
    IEHIJZKLZHVKVMWAHKLHVTHKHULUMZUNUOUPVIVJVTKAEHIUQZURVIVMEKLZVTMJZHVIVLWDVJV
    TMAEKUSWCUTWEHHMJHWDHVTHMVAWBVBHULVCUPVDVEVFVG $.
    $( [13-Jul-2013] $) $( [2-Dec-2004] $)

  $( The factorial of 2. $)
  fac2 $p |- ( ! ` 2 ) = 2 $=
    ( c2 cfa cfv c1 caddc co cmul df-2 fveq2i cn0 wcel wceq 1nn0 facp1 ax-mp
    fac1 eqcomi opreq12i 2cn mulid2i eqtri 3eqtri ) ABCDDEFZBCZDBCZUCGFZAAUCBHI
    DJKUDUFLMDNOUFDAGFAUEDUCAGPAUCHQRASTUAUB $.
    $( [17-Mar-2005] $)

  $( The factorial of 3. $)
  fac3 $p |- ( ! ` 3 ) = 6 $=
    ( c3 cfa cfv c2 c1 caddc co cmul c6 df-3 fveq2i cn0 wcel wceq 2nn0 facp1
    ax-mp fac2 eqcomi opreq12i 2cn 3re recni mulcomi 3t2e6 3eqtri ) ABCDEFGZBCZ
    DBCZUGHGZIAUGBJKDLMUHUJNODPQUJDAHGADHGIUIDUGAHRAUGJSTDAUAAUBUCUDUEUFUF $.
    $( [17-Mar-2005] $)

  $( Value of the factorial function expressed recursively. $)
  facnn2 $p |- ( N e. NN ->
               ( ! ` N ) = ( ( ! ` ( N - 1 ) ) x. N ) ) $=
    ( cn wcel cc c1 cmin co cn0 wa cfa cfv cmul wceq elnnnn0 caddc facp1
    adantl ax-1cn npcan mpan2 fveq2d adantr opreq2d 3eqtr3d sylbi ) ABCADCZAEFG
    ZHCZIZAJKZUGJKZALGZMANUIUGEOGZJKZUKUMLGZUJULUHUNUOMUFUGPQUFUNUJMUHUFUMAJUFE
    DCUMAMRAESTZUAUBUFUOULMUHUFUMAUKLUPUCUBUDUE $.
    $( [2-Dec-2004] $)

  ${
    $d j k N $.
    $( Closure of the factorial function. $)
    faccl $p |- ( N e. NN0 -> ( ! ` N ) e. NN ) $=
      ( vj vk cv cfa cfv cn wcel cc0 c1 caddc co wceq fveq2 eleq1d fac0 1nn
      eqeltri cn0 wa cmul facp1 adantl nn0p1nn nnmulcl sylan2 eqeltrd expcom
      nn0ind ) BDZEFZGHIEFZGHCDZEFZGHZUMJKLZEFZGHZAEFZGHBCAUJIMUKULGUJIENOUJUMM
      UKUNGUJUMENOUJUPMUKUQGUJUPENOUJAMUKUSGUJAENOULJGPQRUOUMSHZURUOUTTUQUNUPUA
      LZGUTUQVAMUOUMUBUCUTUOUPGHVAGHUMUDUNUPUEUFUGUHUI $.
      $( [2-Dec-2004] $)
  $}

  $( The factorial function is nonzero. $)
  facne0 $p |- ( N e. NN0 -> ( ! ` N ) =/= 0 ) $=
    ( cn0 wcel cfa cfv cn cc0 wne faccl nnne0 syl ) ABCADEZFCLGHAILJK $.
    $( [26-Apr-2005] $)

  ${
    $d j M $.  $d j k N $.
    $( A natural number divides the factorial of an equal or larger number. $)
    facdiv $p |- ( ( M e. NN0 /\ N e. NN /\ N <_ M ) ->
                  ( ( ! ` M ) / N ) e. NN ) $=
      ( vj vk cn0 wcel cn cle wbr cfa cfv cdiv co cv wi cc0 c1 caddc wceq
      breq2 fveq2 opreq1d eleq1d imbi12d imbi2d clt wn nngt0 cr wb 0re nnre
      ltnle sylancr mpbid pm2.21d wa cmul wo peano2nn0 nn0re syl leloe syl2an
      nnnn0 nn0leltp1 sylan nn0p1nn nnmulcl sylan2 expcom adantl cc wne faccl
      nncn nn0cn nnne0 jca adantr div23 syl111anc sylibrd imim2d com23 sylbird
      divcan4 eqeltrd opreq2 syl5ibcom a1dd jaod sylbid ex com34 com12 imp4d
      facp1 exp4d a2d nn0ind 3imp ) AEFBGFZBAHIZAJKZBLMZGFZXCBCNZHIZXHJKZBLMZGF
      ZOZOXCBPHIZPJKZBLMZGFZOZOXCBDNZHIZXSJKZBLMZGFZOZOXCBXSQRMZHIZYEJKZBLMZGFZ
      OZOXCXDXGOZOCDAXHPSZXMXRXCYLXIXNXLXQXHPBHTYLXKXPGYLXJXOBLXHPJUAUBUCUDUEXH
      XSSZXMYDXCYMXIXTXLYCXHXSBHTYMXKYBGYMXJYABLXHXSJUAUBUCUDUEXHYESZXMYJXCYNXI
      YFXLYIXHYEBHTYNXKYHGYNXJYGBLXHYEJUAUBUCUDUEXHASZXMYKXCYOXIXDXLXGXHABHTYOX
      KXFGYOXJXEBLXHAJUAUBUCUDUEXCXNXQXCPBUFIZXNUGZBUHXCPUIFBUIFZYPYQUJUKBULZPB
      UMUNUOUPXSEFZXCYDYJYTXCYDYFYIYTXCYDYFUQUQYAYEURMZBLMZGFZYIYTXCYDYFUUCXCYT
      YDYFUUCOOXCYTYFYDUUCXCYTYFYDUUCOZOXCYTUQZYFBYEUFIZBYESZUSZUUDXCYRYEUIFZYF
      UUHUJYTYSYTYEEFZUUIXSUTZYEVAVBBYEVCVDUUEUUFUUDUUGUUEUUFXTUUDXCBEFYTXTUUFU
      JBVEBXSVFVGUUEYDXTUUCUUEYCUUCXTUUEYCYBYEURMZGFZUUCYTYCUUMOXCYCYTUUMYTYCYE
      GFUUMXSVHYBYEVIVJVKVLUUEUUBUULGUUEYAVMFZYEVMFZBVMFZBPVNZUQZUUBUULSYTUUNXC
      YTYAGFZUUNXSVOZYAVPVBVLZYTUUOXCYTUUJUUOUUKYEVQVBVLXCUURYTXCUUPUUQBVPZBVRZ
      VSVTYAYEBWAWBUCWCWDWEWFUUEUUGUUCYDUUEYABURMZBLMZGFUUGUUCUUEUVEYAGUUEUUNUU
      PUUQUVEYASUVAXCUUPYTUVBVTXCUUQYTUVCVTYABWGWBYTUUSXCUUTVLWHUUGUVEUUBGUUGUV
      DUUABLBYEYAURWIUBUCWJWKWLWMWNWOWPWQYTYHUUBGYTYGUUABLXSWRUBUCWCWSWTXAXB $.
      $( [2-May-2005] $)
  $}

  $( No natural number (greater than one) divides the factorial plus one of an
     equal or larger number. $)
  facndiv $p |- ( ( ( M e. NN0 /\ N e. NN ) /\ ( 1 < N /\ N <_ M ) ) ->
                -. ( ( ( ! ` M ) + 1 ) / N ) e. ZZ ) $=
    ( cn0 wcel cn wa c1 clt wbr cle cfa cfv caddc co cdiv cz wn cr nnre recnz
    sylan ad2ant2lr cmin facdiv 3expa nnz syl adantrl zsubcl ex syl5com cc cc0
    wne wceq faccl nncn peano2cn ad2antrr nnne0 jca ad2antlr divsubdir
    syl111anc ax-1cn pncan2 sylancl opreq1d eqtr3d eleq1d sylibd mtod ) ACDZBED
    ZFZGBHIZBAJIZFZFZAKLZGMNZBONZPDZGBONZPDZVNVPWEQZVMVQVNBRDVPWFBSBTUAUBVSWCWB
    VTBONZUCNZPDZWEVSWGPDZWCWIVOVQWJVPVOVQFWGEDZWJVMVNVQWKABUDUEWGUFUGUHWCWJWIW
    BWGUIUJUKVSWHWDPVSWAVTUCNZBONZWHWDVSWAULDZVTULDZBULDZBUMUNZFZWMWHUOVMWNVNVR
    VMWOWNVMVTEDWOAUPVTUQUGZVTURUGUSVMWOVNVRWSUSVNWRVMVRVNWPWQBUQBUTVAVBWAVTBVC
    VDVMWMWDUOVNVRVMWLGBOVMWOGULDWLGUOWSVEVTGVFVGVHUSVIVJVKVL $.
    $( [3-May-2005] $)

  ${
    $d j k M $.  $d j N $.
    $( Ordering property of factorial. $)
    facwordi $p |- ( ( M e. NN0 /\ N e. NN0 /\ M <_ N ) ->
                  ( ! ` M ) <_ ( ! ` N ) ) $=
      ( vj vk cn0 wcel cle wbr cfa cfv cv wa wi cc0 c1 caddc co wceq breq2
      anbi2d fveq2 breq2d imbi12d weq nn0le0eq0 biimpa fveq2d cr fac0 1re
      eqeltri leidi syl6eqbr impexp clt wo wb nn0re peano2re syl leloe syl2an
      nn0leltp1 cmul cn cc faccl nncn mulid1 3syl nnre nnnn0 nn0ge0 jca
      nn0p1nn nnge1 lemul2a mp3anl1 syl21anc eqbrtrrd facp1 breqtrrd adantl
      adantr peano2nn0 letr syl111anc mpan2d imim2d com23 sylbird leid biimpcd
      syl5 a1dd jaod sylbid ex com13 com4l a2d imp4a syl5bi nn0ind 3impib
      3com12 ) BEFZAEFZABGHZAIJZBIJZGHZXGXHXIXLXHACKZGHZLZXJXMIJZGHZMXHANGHZLZX
      JNIJZGHZMXHADKZGHZLZXJYBIJZGHZMZXHAYBOPQZGHZLZXJYHIJZGHZMZXHXILZXLMCDBXMN
      RZXOXSXQYAYOXNXRXHXMNAGSTYOXPXTXJGXMNIUAUBUCCDUDZXOYDXQYFYPXNYCXHXMYBAGST
      YPXPYEXJGXMYBIUAUBUCXMYHRZXOYJXQYLYQXNYIXHXMYHAGSTYQXPYKXJGXMYHIUAUBUCXMB
      RZXOYNXQXLYRXNXIXHXMBAGSTYRXPXKXJGXMBIUAUBUCXSXJXTXTGXSANIXHXRANRAUEUFUGX
      TXTOUHUIUJUKULUMYGXHYCYFMZMZYBEFZYMXHYCYFUNUUAYTXHYIYLUUAXHYSYIYLMYIUUAXH
      YSYLXHUUAYIYSYLMZXHUUAYIUUBMXHUUALZYIAYHUOHZAYHRZUPZUUBXHAUHFYHUHFZYIUUFU
      QUUAAURUUAYBUHFUUGYBURYBUSUTZAYHVAVBUUCUUDUUBUUEUUCUUDYCUUBAYBVCUUCYSYCYL
      UUCYFYLYCUUCYFYEYKGHZYLUUAUUIXHUUAYEYEYHVDQZYKGUUAYEOVDQZYEUUJGUUAYEVEFZY
      EVFFUUKYERYBVGZYEVHYEVIVJUUAUUGYEUHFZNYEGHZLZOYHGHZUUKUUJGHZUUHUUAUUNUUOU
      UAUULUUNUUMYEVKUTZUUAUULYEEFUUOUUMYEVLYEVMVJVNUUAYHVEFUUQYBVOYHVPUTOUHFUU
      GUUPUUQUURUJOYHYEVQVRVSVTYBWAWBWCUUCXJUHFZUUNYKUHFZYFUUILYLMXHUUTUUAXHXJV
      EFUUTAVGXJVKUTZWDUUAUUNXHUUSWCUUAUVAXHUUAYHEFYKVEFUVAYBWEYHVGYKVKVJWCXJYE
      YKWFWGWHWIWJWKUUCUUEYLYSXHUUEYLMUUAUUEXJYKRZXHYLAYHIUAXHUUTXJXJGHZUVCYLMU
      VBXJWLUVCUVDYLXJYKXJGSWMVJWNWDWOWPWQWRWSWTXAXBXCXDXEXF $.
      $( [9-Dec-2005] $)

    $( A lower bound for the factorial function. $)
    faclbnd $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                   ( M ^ ( N + 1 ) ) <_ ( ( M ^ M ) x. ( ! ` N ) ) ) $=
      ( vj vk cn0 wcel cn cc0 wceq wo c1 caddc co cexp cfa cfv cmul cle wbr
      elnn0 cv wi opreq1 opreq2d fveq2 breq12d imbi2d weq cr nnre 1nn0 a1i
      nnnn0 nnge1 expwordi syl32anc ax-1cn addid2i opreq2i fac0 cc reexpcl
      syl11anc recnd mulid1 syl syl5eq 3brtr4d wa clt nn0re peano2re lelttric
      syl2an ad3antrrr simpllr peano2nn0 nngt0 0re ltle mpan sylc expge0
      syl111anc faccl 3syl remulcl jca31 jca jca32 simplr simprr lemul12a
      anandis nncn expp1 adantr facp1 adantl nn0cn peano2cn mulass eqtr4d
      exp32 com23 wb nn0ltp1le ancoms ad2antrr ad2antlr anim1i syl31anc
      anim12i id nn0ge0 lemulge11 letrd ex sylbid a1dd jaod mpd expcom a2d
      nn0ind impcom nn0p1nn 0exp 0cn exp0 ax-mp opreq1i mulid2 opreq12 anidms
      opreq1d syl5ibr imp jaoian sylanb ) AEFZAGFZAHIZJBEFZABKLMZNMZAANMZBOPZQM
      ZRSZATUUBUUDUUJUUCUUDUUBUUJUUBACUAZKLMZNMZUUGUUKOPZQMZRSZUBUUBAHKLMZNMZUU
      GHOPZQMZRSZUBUUBADUAZKLMZNMZUUGUVBOPZQMZRSZUBUUBAUVCKLMZNMZUUGUVCOPZQMZRS
      ZUBUUBUUJUBCDBUUKHIZUUPUVAUUBUVMUUMUURUUOUUTRUVMUULUUQANUUKHKLUCUDUVMUUNU
      USUUGQUUKHOUEUDUFUGCDUHZUUPUVGUUBUVNUUMUVDUUOUVFRUVNUULUVCANUUKUVBKLUCUDU
      VNUUNUVEUUGQUUKUVBOUEUDUFUGUUKUVCIZUUPUVLUUBUVOUUMUVIUUOUVKRUVOUULUVHANUU
      KUVCKLUCUDUVOUUNUVJUUGQUUKUVCOUEUDUFUGUUKBIZUUPUUJUUBUVPUUMUUFUUOUUIRUVPU
      ULUUEANUUKBKLUCUDUVPUUNUUHUUGQUUKBOUEUDUFUGUUBAKNMZUUGUURUUTRUUBAUIFZKEFZ
      UUAKARSZUVTUVQUUGRSAUJZUVSUUBUKULAUMZAUNZUWCAKAUOUPUURUVQIUUBUUQKANKUQURU
      SULUUBUUTUUGKQMZUUGUUSKUUGQUTUSUUBUUGVAFZUWDUUGIUUBUUGUUBUVRUUAUUGUIFZUWA
      UWBAAVBZVCZVDZUUGVEVFVGVHUVBEFZUUBUVGUVLUUBUWJUVGUVLUBZUUBUWJVIZAUVCRSZUV
      CAVJSZJZUWKUUBUVRUVCUIFZUWOUWJUWAUWJUVBUIFZUWPUVBVKZUVBVLZVFAUVCVMVNUWLUW
      MUWKUWNUWLUVGUWMUVLUWLUVGUWMUVLUWLUVGUWMVIZVIUVDAQMZUVFUVCQMZUVIUVKRUWLUV
      GUWMUXAUXBRSZUWLUVGVIZUWLUWMVIZVIZUVDUIFZHUVDRSZVIUVFUIFZVIZUVRHARSZVIZUW
      PVIVIUWTUXCUXFUXJUXLUWPUXFUXGUXHUXIUXFUVRUVCEFZUXGUUBUVRUWJUVGUXEUWAVOZUX
      FUWJUXMUUBUWJUVGUXEVPZUVBVQZVFZAUVCVBVCUXFUVRUXMUXKUXHUXNUXQUXFUVRHAVJSZU
      XKUXNUUBUXRUWJUVGUXEAVRVOHUIFUVRUXRUXKUBVSHAVTWAWBZAUVCWCWDUXFUWFUVEUIFZU
      XIUXFUVRUUAUWFUXNUUBUUAUWJUVGUXEUWBVOUWGVCUXFUWJUVEGFZUXTUXOUVBWEZUVEUJZW
      FUUGUVEWGVCWHUXFUVRUXKUXNUXSWIUXFUWJUWQUWPUXOUWRUWSWFWJUXFUVGUWMUWLUVGUXE
      WKUXDUWLUWMWLWIUVDUVFAUVCWMWBWNUWLUVIUXAIZUWTUUBAVAFUXMUYDUWJAWOUXPAUVCWP
      VNWQUWLUVKUXBIUWTUWLUVKUUGUVEUVCQMZQMZUXBUWLUVJUYEUUGQUWJUVJUYEIUUBUVBWRW
      SUDUWLUWEUVEVAFZUVCVAFZUXBUYFIUUBUWEUWJUWIWQUWJUYGUUBUWJUVEUWJUYAUXTUYBUY
      CVFVDWSUWJUYHUUBUWJUVBVAFUYHUVBWTUVBXAVFWSUUGUVEUVCXBWDXCWQVHXDXEUWLUWNUV
      LUVGUWLUWNUVHARSZUVLUWJUUBUWNUYIXFZUWJUXMUUAUYJUUBUXPUWBUVCAXGVNXHUWLUYIU
      VLUWLUYIVIZUVIUUGUVKUWLUVIUIFZUYIUUBUVRUVHEFZUYLUWJUWAUWJUXMUYMUXPUVCVQVF
      ZAUVHVBVNWQUUBUWFUWJUYIUWHXIUWLUVKUIFZUYIUUBUWFUVJUIFZUYOUWJUWHUWJUXMUVJG
      FZUYPUXPUVCWEZUVJUJWFZUUGUVJWGVNWQUYKUVRUYMUUAUVTUYIVIUVIUUGRSUUBUVRUWJUY
      IUWAXIUWJUYMUUBUYIUYNXJUUBUUAUWJUYIUWBXIUWLUVTUYIUUBUVTUWJUWCWQXKAUVHAUOX
      LUWLUUGUVKRSZUYIUWLUWFUYPVIHUUGRSZKUVJRSZVIUYTUUBUWFUWJUYPUWHUYSXMUUBVUAU
      WJVUBUUBUUAVUAUWBUUAUVRUUAUXKVUAAVKUUAXNAXOAAWCWDVFUWJUXMUYQVUBUXPUYRUVJU
      NWFXMUUGUVJXPVCWQXQXRXSXTYAYBYCYDYEYFUUCUUDUUJUUDUUJUUCHUUENMZHHNMZUUHQMZ
      RSUUDHUUHVUCVUERUUDUUHGFZUUHEFHUUHRSBWEZUUHUMUUHXOWFUUDUUEGFVUCHIBYGUUEYH
      VFUUDVUEKUUHQMZUUHVUDKUUHQHVAFVUDKIYIHYJYKYLUUDVUFUUHVAFVUHUUHIVUGUUHWOUU
      HYMWFVGVHUUCUUFVUCUUIVUERAHUUENUCUUCUUGVUDUUHQUUCUUGVUDIAHAHNYNYOYPUFYQYR
      YSYT $.
      $( [17-Dec-2005] $)
  $}

  $( A lower bound for the factorial function. $)
  faclbnd2 $p |- ( N e. NN0 -> ( ( 2 ^ N ) / 2 ) <_ ( ! ` N ) ) $=
    ( cn0 wcel c2 cexp co cdiv c1 caddc cfa cfv cle cmul c4 sq2 2t2e4 eqtr4i
    opreq2i cc wceq 2cn expp1 mpan opreq1d syl5eq expcl wa cc0 wne 2ne0 pm3.2i
    divmuldiv mpanr12 sylancl dividi halfcl mulid1 3syl 3eqtr2rd wbr 2nn0
    faclbnd cr wb 2re peano2nn0 reexpcl sylancr cn faccl nnre syl clt 4re
    eqeltri 4pos breqtrri ledivmul mp3an3 syl11anc mpbird eqbrtrd ) ABCZDAEFZDG
    FZDAHIFZEFZDDEFZGFZAJKZLWCWIWDDMFZDDMFZGFZWEDDGFZMFZWEWCWIWGWLGFWMWHWLWGGWH
    NWLOPQRWCWGWKWLGDSCZWCWGWKTUADAUBUCUDUEWCWDSCZWPWOWMTZWPWCWQUADAUFUCZUAWQWP
    UGWPDUHUIZUGZXAWRWPWTUAUJUKZXBWDDDDULUMUNWCWOWEHMFZWEWNHWEMDUAUJUORWCWQWESC
    XCWETWSWDUPWEUQURUEUSWCWIWJLUTZWGWHWJMFLUTZDBCWCXEVADAVBUCWCWGVCCZWJVCCZXDX
    EVDZWCDVCCWFBCXFVEAVFDWFVGVHWCWJVICXGAVJWJVKVLXFXGWHVCCZUHWHVMUTZUGXHXIXJWH
    NVCOVNVOUHNWHVMVPOVQUKWGWJWHVRVSVTWAWB $.
    $( [17-Dec-2005] $)

  $( A lower bound for the factorial function. $)
  faclbnd3 $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                 ( M ^ N ) <_ ( ( M ^ M ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cn cc0 wceq wo cexp co cfa cfv cmul cle wbr elnn0 wa c1 caddc
    cr nnre adantr simpr peano2nn0 adantl nnge1 nn0re leid syl letrp1
    syl111anc anim12i expwordi syl31anc nnnn0 faclbnd sylan wi reexpcl syl2an
    mpancom faccl remulcl letr mp2and 0exp 0re 1re lt01 ltleii syl6eqbr opreq2
    cc 0cn exp0 ax-mp leidi eqbrtri jaoi sylbi 1nn nnmulcl sylancr mpan mp3an2
    syl11anc wb opreq1 opreq12 anidms syl6eq opreq1d breq12d mpbird jaoian
    sylanb ) ACDZAEDZAFGZHBCDZABIJZAAIJZBKLZMJZNOZAPWRWTXEWSWRWTQZXAABRSJZIJZNO
    ZXHXDNOZXEXFATDZWTXGCDZRANOZBXGNOZQXIWRXKWTAUAUBWRWTUCWTXLWRBUDZUEWRXMWTXNA
    UFWTBTDZXPBBNOZXNBUGZXRWTXPXQXRBUHUIBBUJUKULABXGUMUNWRWQWTXJAUOZABUPUQWRWQW
    TXIXJQXEURZXSWQWTQXATDZXHTDZXDTDZXTWQXKWTYAAUGZABUSUQWQXKXLYBWTYDXOAXGUSUTW
    QXBTDZXCTDZYCWTXKWQYEYDAAUSVAWTXCEDZYFBVBZXCUAUIZXBXCVCUTXAXHXDVDUKUQVEWSWT
    QXEFBIJZRXCMJZNOZWTYLWSWTYJRNOZRYKNOZYLWTBEDZBFGZHYMBPYOYMYPYOYJFRNBVFFRVGV
    HVIVJVKYPYJFFIJZRNBFFIVLYQRRNFVMDYQRGVNFVOVPZRVHVQVRVKVSVTWTYKEDZYNWTREDYGY
    SWAYHRXCWBWCYKUFUIWTYJTDZYKTDZYMYNQYLURZFTDWTYTVGFBUSWDWTRTDZYFUUAVHYIRXCVC
    WCYTUUCUUAUUBVHYJRYKVDWEWFVEUEWSXEYLWGWTWSXAYJXDYKNAFBIWHWSXBRXCMWSXBYQRWSX
    BYQGAFAFIWIWJYRWKWLWMUBWNWOWP $.
    $( [19-Dec-2005] $)

  ${
    faclbnd4lem1.1 $e |- N e. NN $.
    faclbnd4lem1.2 $e |- K e. NN0 $.
    faclbnd4lem1.3 $e |- M e. NN0 $.
    $( Lemma for ~ faclbnd4 .  Prepare the induction step. _Warning:  The HTML
       proof page is 0.6 megabyte in size_. $)
    faclbnd4lem1 $p |- ( ( ( ( N - 1 ) ^ K ) x. ( M ^ ( N - 1 ) ) ) <_
       ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` ( N - 1 ) ) ) ->
                   ( ( N ^ ( K + 1 ) ) x. ( M ^ N ) ) <_ ( ( (
  2 ^ ( ( K + 1 ) ^ 2 ) ) x. ( M ^ ( M + ( K + 1 ) ) ) ) x. ( ! ` N ) ) ) $=
      ( c1 cle wbr clt wo cmin co cexp cmul c2 caddc cfa cfv cr wcel nnrei 1re
      lelttric mp2an wceq letri3i cn nnge1 ax-mp mpbiran2 cc0 wa 0re lt01
      ltleii pm3.2i cn0 2re 1nn nn0nnaddcl nnnn0i 2nn0 nn0expcl reexpcl nn0rei
      nn0ge0i cc 2cn exp0 w3a 0nn0 3pm3.2i 1lt2 expwordi eqbrtrri elnn0 nncn
      exp1 syl 1nn0 mpan sylancl eqbrtrrd breq1 mpbiri jaoi sylbi lemul12a mp2
      opreq1 1exp syl6eq opreq2 nn0cni opreq12d fveq2 fac1 opreq2d recni
      mulcli mulid1i breq12d sylbir adantr remulcli a1i faccl nn0mulcli nncni
      expp1 expm1t opreq12i elnnnn0 mpbi simpri mulassi eqtri nn0addcli wb
      nnltp1le df-2 breq1i bitr4i expubnd mp3an12 lemul1a sylancr mul4i
      opreq2i mul12i 3eqtr2i syl6breq adantl letrd mul32i ax-1cn addassi
      eqtr3i facnn2 3eqtr4i syl5eqbr expadd mp3an ltp1i sqvali eqcomi adddii
      eqtr4i 3brtr4i syl6breqr jaoian ) CGHIZGCJIZKZCGLMZANMZBUUPNMZOMZPAPNMZNM
      ZBBAQMZNMZOMZUUPRSZOMZHIZCAGQMZNMZBCNMZOMZPUVHPNMZNMZBBUVHQMZNMZOMZCRSZOM
      ZHIZCTUAZGTUAZUUOCDUBZUCCGUDUEUUMUVGUVSUUNUUMUVSUVGUUMCGUFZUVSUWCUUMGCHIZ
      CGUWBUCUGCUHUAZUWDDCUIUJUKUWCUVSGBOMZUVPHIZUWAULGHIZUMZUVMTUAZUMZBTUAZULB
      HIZUMZUVOTUAZUMZUMGUVMHIZBUVOHIZUMUWGUWKUWPUWIUWJUWAUWHUCULGUNUCUOUPUQPTU
      AZUVLURUAZUWJUSUVHURUAZPURUAZUWTUVHAURUAZGUHUAZUVHUHUAZEUTAGVAUEZVBZVCUVH
      PVDUEZPUVLVEUEZUQUWNUWOUWLUWMBFVFZBFVGUQUVOBURUAZUVNURUAZUVOURUAFUVNUXKUX
      EUVNUHUAZFUXFBUVHVAUEZVBZBUVNVDUEZVFUQUQUWQUWRPULNMZGUVMHPVHUAZUXQGUFVIPV
      JUJUWSULURUAZUWTVKGPHIZULUVLHIZUMUXQUVMHIUWSUXSUWTUSVLUXHVMUXTUYAGPUCUSVN
      UPZUVLUXHVGUQPULUVLVOUEVPUXKUWRFUXKBUHUAZBULUFZKUWRBVQUYCUWRUYDUYCBGNMZBU
      VOHUYCBVHUAZUYEBUFZBVRBVSZVTUYCGBHIZGUVNHIZUYEUVOHIZBUIUXMUYJUXNUVNUIUJUW
      LGURUAZUXLVKUYIUYJUMUYKUWLUYLUXLUXJWAUXOVMBGUVNVOWBWCWDUYDUWRULUVOHIUVOUX
      PVGBULUVOHWEWFWGWHUJUQGUVMBUVOWIWJUWCUVKUWFUVRUVPHUWCUVIGUVJBOUWCUVIGUVHN
      MZGCGUVHNWKUXAUYMGUFUXGUVHWLUJWMUWCUVJUYEBCGBNWNUYFUYGBFWOZUYHUJWMWPUWCUV
      RUVPGOMUVPUWCUVQGUVPOUWCUVQGRSGCGRWQWRWMWSUVPUVMUVOUVMUXIWTZUVOUXPWOZXAXB
      WMXCWFXDXEUUNUVGUMZUVKUVMUVOUVQOMZOMZUVRHUYQUVKUVAPANMZOMZUYROMZUYSUVKTUA
      UYQUVIUVJUVTUXAUVITUAUWBUXGCUVHVEUEUWLCURUAZUVJTUAUXJCDVBZBCVEUEXFXGVUBTU
      AUYQVUAUYRUVAUYTUWSUUTURUAZUVATUAUSUXCUXBVUEEVCAPVDUEZPUUTVEUEZUYTUXBUXCU
      YTURUAVCEPAVDUEZVFZXFZUYRUVOUVQUXPUVQVUCUVQUHUAVUDCXHUJZVBXIZVFZXFXGUYSTU
      AUYQUVMUYRUXIVUMXFXGUYQUVKCANMZCUURBOMZOMZOMZVUBHUVKVUNCOMZVUOOMVUQUVIVUR
      UVJVUOOCVHUAZUXCUVIVURUFCDXJZECAXKUEUYFUWEUVJVUOUFUYNDBCXLUEXMVUNCVUOVUNU
      VTUXCVUNTUAZUWBECAVEUEZWTVUTVUOUURBUXKUUPURUAZUURURUAFVUSVVCUWEVUSVVCUMDC
      XNXOXPZBUUPVDUEZFXIZWOZXQXRUYQVUQUVFUYTCOMZBOMZOMZVUBHUYQVUQUUSVVIOMZVVJV
      UQTUAUYQVUNVUPVVBVUPCVUOVUDVVFXIZVFZXFXGVVKTUAUYQUUSVVIUUQUURUUPTUAUXCUUQ
      TUAUUPVVDVFEUUPAVEUEZUURVVEVFXFZVVIVVHBUYTCVUHVUDXIZFXIZVFZXFXGVVJTUAUYQU
      VFVVIUVDUVEUVAUVCVUGUWLUVBURUAZUVCTUAUXJBAFEXSZBUVBVEUEZXFZUVEVVCUVEUHUAV
      VDUUPXHUJZUBXFZVVRXFXGUUNVUQVVKHIUVGUUNVUQUYTUUQOMZVUPOMZVVKHUUNVVAVWETUA
      ZVUPTUAZULVUPHIZUMZVKVUNVWEHIZVUQVWFHIVVAVWGVWJVVBUYTUUQVUIVVNXFVWHVWIVVM
      VUPVVLVGUQVMUUNPCHIZVWKUUNGGQMZCHIZVWLUXDUWEUUNVWNXTUTDGCYAUEPVWMCHYBYCYD
      UVTUXCVWLVWKUWBECAYEYFWHVUNVWEVUPYGYHVWFVVHUUQVUOOMZOMVVHUUSBOMZOMVVKUYTU
      UQCVUOUYTVUHWOZUUQVVNWTZVUTVVGYIVWPVWOVVHOUUQUURBVWRUURVVEWOUYNXQYJVVHUUS
      BVVHVVPWOUUSVVOWTUYNYKYLYMXEUVGVVKVVJHIZUUNUUSTUAZUVFTUAZVVITUAZULVVIHIZU
      MZVKUVGVWSVWTVXAVXDVVOVWDVXBVXCVVRVVIVVQVGUQVMUUSUVFVVIYGWBYNYOVVJVUAUVOO
      MZUVQOMZVUBUVFUYTBOMZCOMZOMUVDVXGOMZUVECOMZOMVVJVXFUVDUVEVXGCUVDVWBWTUVEV
      WCXJUYTBVWQUYNXAVUTYIVVIVXHUVFOUYTCBVWQVUTUYNYPYJVXEVXIUVQVXJOVUAUVCBOMZO
      MVXEVXIVXKUVOVUAOBUVBGQMZNMZVXKUVOUYFVVSVXMVXKUFUYNVVTBUVBXKUEVXLUVNBNBAG
      UYNAEWOZYQYRYJYSYJUVAUYTUVCBUVAVUGWTVWQUVCVWAWTUYNYIYSUWEUVQVXJUFDCYTUJXM
      UUAVUAUVOUVQVUAVUJWTUYPUVQVUKXJZXQXRYMUUBVUBUYSHIZUYQVUATUAZUWJUYRTUAZULU
      YRHIZUMZVKVUAUVMHIVXPVXQUWJVXTVUJUXIVXRVXSVUMUYRVULVGUQVMPUUTAQMZNMZVUAUV
      MHUXRVUEUXCVYBVUAUFVIVUFEPUUTAUUCUUDUWSVYAURUAZUWTVKUXTVYAUVLHIZUMVYBUVMH
      IUWSVYCUWTUSUUTAVUFEXSUXHVMUXTVYDUYBAUVHOMZUVHUVHOMZVYAUVLHATUAZUVHTUAZVY
      HULUVHHIZUMZVKAUVHHIVYEVYFHIVYGVYHVYJAEVFZUVHUXFUBZVYHVYIVYLUVHUXGVGUQVMA
      UVHVYKVYLAVYKUUEUPAUVHUVHYGUEVYAAAOMZAGOMZQMVYEUUTVYMAVYNQAVXNUUFVYNAAVXN
      XBUUGXMAAGVXNVXNYQUUHUUIUVHUVHUXFXJUUFUUJUQPVYAUVLVOUEVPVUAUVMUYRYGUEXGYO
      UVMUVOUVQUYOUYPVXOXQUUKUULWB $.
      $( [20-Dec-2005] $)
  $}

  $( Lemma for ~ faclbnd4 .  Use the weak deduction theorem to convert the
     hypotheses of ~ faclbnd4lem1 to antecedents. $)
  faclbnd4lem2 $p |- ( ( M e. NN0 /\ K e. NN0 /\ N e. NN ) ->
           ( ( ( ( N - 1 ) ^ K ) x. ( M ^ ( N - 1 ) ) ) <_
       ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` ( N - 1 ) ) ) ->
                   ( ( N ^ ( K + 1 ) ) x. ( M ^ N ) ) <_ ( ( (
  2 ^ ( ( K + 1 ) ^ 2 ) ) x. ( M ^ ( M + ( K + 1 ) ) ) ) x. ( ! ` N ) ) ) ) $=
    ( cn0 wcel cn c1 cmin co cexp cmul c2 caddc cfa cfv cle wbr wi cif wceq
    opreq1 opreq2d id opreq12d opreq1d breq12d imbi12d opreq2 fveq2d fveq2 1nn
    elimel 1nn0 faclbnd4lem1 dedth3h ) BDEZADEZCFEZCGHIZAJIZBUSJIZKIZLALJIZJIZB
    BAMIZJIZKIZUSNOZKIZPQZCAGMIZJIZBCJIZKIZLVKLJIZJIZBBVKMIZJIZKIZCNOZKIZPQZRUT
    UPBGSZUSJIZKIZVDWCWCAMIZJIZKIZVHKIZPQZVLWCCJIZKIZVPWCWCVKMIZJIZKIZVTKIZPQZR
    USUQAGSZJIZWDKIZLWRLJIZJIZWCWCWRMIZJIZKIZVHKIZPQZCWRGMIZJIZWKKIZLXHLJIZJIZW
    CWCXHMIZJIZKIZVTKIZPQZRURCGSZGHIZWRJIZWCXSJIZKIZXEXSNOZKIZPQZXRXHJIZWCXRJIZ
    KIZXOXRNOZKIZPQZRBACGGGBWCTZVJWJWBWQYLVBWEVIWIPYLVAWDUTKBWCUSJUAUBYLVGWHVHK
    YLVFWGVDKYLBWCVEWFJYLUCZBWCAMUAUDUBUEUFYLVNWLWAWPPYLVMWKVLKBWCCJUAUBYLVSWOV
    TKYLVRWNVPKYLBWCVQWMJYMBWCVKMUAUDUBUEUFUGAWRTZWJXGWQXQYNWEWTWIXFPYNUTWSWDKA
    WRUSJUHUEYNWHXEVHKYNVDXBWGXDKYNVCXALJAWRLJUAUBYNWFXCWCJAWRWCMUHUBUDUEUFYNWL
    XJWPXPPYNVLXIWKKYNVKXHCJAWRGMUAZUBUEYNWOXOVTKYNVPXLWNXNKYNVOXKLJYNVKXHLJYOU
    EUBYNWMXMWCJYNVKXHWCMYOUBUBUDUEUFUGCXRTZXGYEXQYKYPWTYBXFYDPYPWSXTWDYAKYPUSX
    SWRJCXRGHUAZUEYPUSXSWCJYQUBUDYPVHYCXEKYPUSXSNYQUIUBUFYPXJYHXPYJPYPXIYFWKYGK
    CXRXHJUACXRWCJUHUDYPVTYIXOKCXRNUJUBUFUGWRWCXRCGFUKULAGDUMULBGDUMULUNUO $.
    $( [23-Dec-2005] $)


  $( Lemma for ~ faclbnd4 .  The ` N = 0 ` case. $)
  faclbnd4lem3 $p |- ( ( ( M e. NN0
            /\ K e. NN0 ) /\ N = 0 ) -> ( ( N ^ K ) x. ( M ^ N ) )
       <_ ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel wa cc0 wceq cexp co cmul c2 caddc cfa cfv cle wbr c1 cn wo
    elnn0 0exp adantl nnnn0 2nn0 nn0expcl mpan2 sylancr nn0addcl syldan
    nn0mulcl syl11anc sylan2 nn0ge0 syl eqbrtrd 0nn0 sylancl nnexpcl mpdan id
    opreq1 0cn addid1i syl6eq opreq12d cc exp0 ax-mp 1nn syl6eqel jaoi sylbi
    nnmulcl mpan nnge1 3syl adantr wb opreq2 sq0i opreq2d 2cn breq12d mpbird
    jaodan sylan2b nn0cn mulid1 sylan9eq 3brtr4d fveq2 fac0 ) BDEZADEZFZCGHZFCA
    IJZBCIJZKJZLALIJZIJZBBAMJZIJZKJZCNOZKJZPQZGAIJZBGIJZKJZXERKJZPQZWPXMWQWPXIX
    EXKXLPWOWNASEZAGHZTXIXEPQZAUAWNXNXPXOWNXNFZXIGXEPXNXIGHWNAUBUCXQXEDEZGXEPQX
    NWNWOXRAUDWPXBDEZXDDEZXRWOXSWNWOLDEZXADEZXSUEWOYAYBUEALUFUGLXAUFUHUCWNWOXCD
    EXTBAUIBXCUFUJXBXDUKULZUMXEUNUOUPWNXOFXPRRBBGMJZIJZKJZPQZWNYGXOWNYESEZYFSEZ
    YGWNBSEZBGHZTYHBUAYJYHYKYJYDDEZYHYJWNGDEZYLBUDUQBGUIURBYDUSUTYKYERSYKYEGGIJ
    ZRYKBGYDGIYKVAYKYDGGMJGBGGMVBGVCVDVEVFGVGEYNRHVCGVHVIZVEVJVKVLVMRSEYHYIVJRY
    EVNVOYFVPVQVRXOXPYGVSWNXOXIRXEYFPXOXIYNRAGGIVTYOVEXOXBRXDYEKXOXBLGIJZRXOXAG
    LIAWAWBLVGEYPRHWCLVHVIVEXOXCYDBIAGBMVTWBVFWDUCWEWFWGWNWOXKXIRKJZXIWNXJRXIKW
    NBVGEXJRHBWHBVHUOWBWOXIDEZXIVGEYQXIHYMWOYRUQGAUFVOXIWHXIWIVQWJWPXRXEVGEXLXE
    HYCXEWHXEWIVQWKVRWQXHXMVSWPWQWTXKXGXLPWQWRXIWSXJKCGAIVBCGBIVTVFWQXFRXEKWQXF
    GNORCGNWLWMVEWBWDUCWE $.
    $( [23-Dec-2005] $)

  ${
    $d j m n M $.  $d j m n K $.  $d j n N $.
    $( Lemma for ~ faclbnd4 .  Prove the ` 0 < N ` case by induction on
       ` K ` . $)
    faclbnd4lem4 $p |- ( ( N e. NN /\ K e. NN0 /\ M e. NN0 ) ->
                   ( ( N ^ K ) x. ( M ^ N ) ) <_
              ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
      ( vn vm vj cn wcel cn0 cexp co cmul c2 caddc cfa cfv cle wbr wa cv wral
      wi cc0 c1 wceq opreq1 opreq2 opreq12d fveq2 opreq2d breq12d cbvralv cmin
      clt wo cr nnre 1re lelttric sylancl ancli andi sylib nnge1 adantr wb
      letri3 biimpar anassrs mpdan ax-1cn subidi syl6eq syl faclbnd4lem3
      sylan2 a1d 1nn nnsub mpan biimpa rcla4v adantl jaodan faclbnd4lem2 3expa
      syld r19.21adva syl5bi expcom a2d nnnn0 faclbnd3 cc nncn exp0 opreq1d
      nn0cn expcl syl2an mulid2 eqtrd sq0 opreq2i 2cn ax-mp eqtri a1i addid1
      mpancom 3brtr4d r19.21aiva ralbidv imbi2d nn0indALT imp rcla4va 3impb )
      CGHZAIHZBIHZCAJKZBCJKZLKZMAMJKZJKZBBANKZJKZLKZCOPZLKZQRZXTYASXSDTZAJKZBYM
      JKZLKZYIYMOPZLKZQRZDGUAZYLXTYAYTYAYMETZJKZYOLKZMUUAMJKZJKZBBUUANKZJKZLKZY
      QLKZQRZDGUAZUBYAYMUCJKZYOLKZMUCMJKZJKZBBUCNKZJKZLKZYQLKZQRZDGUAZUBYAYMFTZ
      JKZYOLKZMUVBMJKZJKZBBUVBNKZJKZLKZYQLKZQRZDGUAZUBYAYMUVBUDNKZJKZYOLKZMUVMM
      JKZJKZBBUVMNKZJKZLKZYQLKZQRZDGUAZUBYAYTUBEFAUVBIHZYAUVLUWCYAUWDUVLUWCUBUV
      LUUAUVBJKZBUUAJKZLKZUVIUUAOPZLKZQRZEGUAZYAUWDSZUWCUVKUWJDEGYMUUAUEZUVDUWG
      UVJUWIQUWMUVCUWEYOUWFLYMUUAUVBJUFYMUUABJUGUHUWMYQUWHUVILYMUUAOUIUJUKULUWL
      UWKUWBDGUWLYMGHZSUWKYMUDUMKZUVBJKZBUWOJKZLKZUVIUWOOPZLKZQRZUWBUWNUWLUWNYM
      UDQRZSZUWNUDYMUNRZSZUOZUWKUXAUBZUWNUWNUXBUXDUOZSUXFUWNUXHUWNYMUPHZUDUPHZU
      XHYMUQZURYMUDUSUTVAUWNUXBUXDVBVCUWLUXCUXGUXEUWLUXCSUXAUWKUXCUWLUWOUCUEZUX
      AUXCYMUDUEZUXLUXCUDYMQRZUXMUWNUXNUXBYMVDVEUWNUXBUXNUXMUWNUXMUXBUXNSZUWNUX
      IUXJUXMUXOVFUXKURYMUDVGUTVHVIVJUXMUWOUDUDUMKUCYMUDUDUMUFUDVKVLVMVNUVBBUWO
      VOVPVQUXEUXGUWLUXEUWOGHZUXGUWNUXDUXPUDGHUWNUXDUXPVFVRUDYMVSVTWAUWJUXAEUWO
      GUUAUWOUEZUWGUWRUWIUWTQUXQUWEUWPUWFUWQLUUAUWOUVBJUFUUAUWOBJUGUHUXQUWHUWSU
      VILUUAUWOOUIUJUKWBVNWCWDVPYAUWDUWNUXAUWBUBUVBBYMWEWFWGWHWIWJWKYAUUTDGYAUW
      NSZYOBBJKZYQLKZUUMUUSQUWNYAYMIHZYOUXTQRYMWLZBYMWMVPUXRUUMUDYOLKZYOUWNUUMU
      YCUEYAUWNUULUDYOLUWNYMWNHUULUDUEYMWOYMWPVNWQWCUXRYOWNHZUYCYOUEYABWNHZUYAU
      YDUWNBWRZUYBBYMWSWTYOXAVNXBYAUUSUXTUEUWNYAUURUXSYQLYAUURUDUXSLKZUXSYAUUOU
      DUUQUXSLUUOUDUEYAUUOMUCJKZUDUUNUCMJXCXDMWNHUYHUDUEXEMWPXFXGXHYAUUPBBJYAUY
      EUUPBUEUYFBXIVNUJUHYAUXSWNHZUYGUXSUEUYEYAUYIUYFBBWSXJUXSXAVNXBWQVEXKXLUUA
      UCUEZUUKUVAYAUYJUUJUUTDGUYJUUCUUMUUIUUSQUYJUUBUULYOLUUAUCYMJUGWQUYJUUHUUR
      YQLUYJUUEUUOUUGUUQLUYJUUDUUNMJUUAUCMJUFUJUYJUUFUUPBJUUAUCBNUGUJUHWQUKXMXN
      UUAUVBUEZUUKUVLYAUYKUUJUVKDGUYKUUCUVDUUIUVJQUYKUUBUVCYOLUUAUVBYMJUGWQUYKU
      UHUVIYQLUYKUUEUVFUUGUVHLUYKUUDUVEMJUUAUVBMJUFUJUYKUUFUVGBJUUAUVBBNUGUJUHW
      QUKXMXNUUAUVMUEZUUKUWCYAUYLUUJUWBDGUYLUUCUVOUUIUWAQUYLUUBUVNYOLUUAUVMYMJU
      GWQUYLUUHUVTYQLUYLUUEUVQUUGUVSLUYLUUDUVPMJUUAUVMMJUFUJUYLUUFUVRBJUUAUVMBN
      UGUJUHWQUKXMXNUUAAUEZUUKYTYAUYMUUJYSDGUYMUUCYPUUIYRQUYMUUBYNYOLUUAAYMJUGW
      QUYMUUHYIYQLUYMUUEYFUUGYHLUYMUUDYEMJUUAAMJUFUJUYMUUFYGBJUUAABNUGUJUHWQUKX
      MXNXOXPYSYLDCGYMCUEZYPYDYRYKQUYNYNYBYOYCLYMCAJUFYMCBJUGUHUYNYQYJYILYMCOUI
      UJUKXQVPXR $.
      $( [19-Dec-2005] $)
  $}

  $( Variant of ~ faclbnd5 providing a non-strict lower bound. $)
  faclbnd4 $p |- ( ( N e. NN0 /\ K e. NN0 /\ M e. NN0 ) ->
                 ( ( N ^ K ) x. ( M ^ N ) ) <_
            ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cexp co cmul c2 caddc cfa cfv cle wbr wa cn cc0 wceq wo elnn0
    faclbnd4lem4 3com13 3expa faclbnd4lem3 jaodan sylan2b 3impa ) BDEZADEZCDEZC
    AFGBCFGHGIAIFGFGBBAJGFGHGCKLHGMNZUHUIUJUKUJUHUIOZCPEZCQRZSUKCTULUMUKUNUHUIU
    MUKUMUIUHUKABCUAUBUCABCUDUEUFUGUB $.
    $( [23-Dec-2005] $)

  $( The factorial function grows faster than powers and exponentiations.  If
     we consider ` K ` and ` M ` to be constants, the right-hand side of the
     inequality is a constant times ` N ` -factorial. $)
  faclbnd5 $p |- ( ( N e. NN0 /\ K e. NN0 /\ M e. NN ) ->
                 ( ( N ^ K ) x. ( M ^ N ) ) <
      ( ( 2 x. ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cn cexp co cmul c2 caddc cfa cfv clt wbr wa cr nn0re reexpcl
    sylan ancoms nnre remulcl syl2an anandirs 2nn 2nn0 nn0expcl mpan2 nnexpcl
    sylancr nnnn0 nn0addcl sylan2 anabss7 nnmulcl anabss5 syl faccl 2re cle
    faclbnd4 syl3an3 3coml 3expa c1 1lt2 cc0 wb nngt0 ltmulgt12 mp3an2
    syl11anc mpbii lelttrd cc wceq recnd 2cn mulass mp3an1 breqtrrd 3impa
    3comr ) ADEZBFEZCDEZCAGHZBCGHZIHZJJAJGHZGHZBBAKHZGHZIHZIHCLMZIHZNOZWEWFWGWR
    WEWFPZWGPZWJJWOWPIHZIHZWQNWTWJXAXBWEWFWGWJQEZWEWGPWHQEZWIQEZXCWFWGPWGWEXDWG
    CQEWEXDCRCASTUAWFBQEWGXEBUBBCSTWHWIUCUDUEWSWOQEZWPQEZXAQEZWGWSWOFEZXFWEWFXI
    WEWLFEZWNFEZXIWSWEJFEWKDEZXJUFWEJDEXLUGAJUHUIJWKUJUKWEWFXKWSWFWMDEZXKWFWEBD
    EZXMBULZXNWEXMBAUMUAUNBWMUJUNUOWLWNUPUDUQZWOUBURZWGWPFEZXGCUSZWPUBURZWOWPUC
    UDZWTJQEZXHXBQEUTYAJXAUCUKWEWFWGWJXAVAOZWGWEWFYCWFWGWEXNYCXOABCVBVCVDVEWTVF
    JNOZXAXBNOZVGWTXHVHXANOZYDYEVIZYAWTXAFEZYFWSXIXRYHWGXPXSWOWPUPUDXAVJURXHYBY
    FYGUTXAJVKVLVMVNVOWSWOVPEZWPVPEZWQXBVQZWGWSWOXQVRWGWPXTVRJVPEYIYJYKVSJWOWPV
    TWAUDWBWCWD $.
    $( [24-Dec-2005] $)

  ${
    $d M m $.  $d N k m $.
    $( Geometric lower bound for the factorial function, where N is usually
       held constant.  (Contributed by Paul Chapman, 28-Dec-2007.) $)
    faclbnd6 $p |- ( ( N e. NN0 /\ M e. NN0 ) ->
             ( ( ! ` N ) x. ( ( N + 1 ) ^ M ) ) <_ ( ! ` ( N + M ) ) ) $=
      ( vm vk cn0 wcel cfa cfv c1 caddc co cexp cmul cle wbr cv wi cc0 wceq
      opreq2 opreq2d fveq2d breq12d imbi2d weq cn cr faccl nnre leid 3syl cc
      nn0cn peano2cn syl exp0 nncn mulid1 eqtrd addid1 3brtr4d wa adantr
      peano2nn0 nn0re reexpcl sylan remulcl syl11anc nnnn0 nn0ge0 simpr expge0
      syl111anc mulge0 syl22anc jca nn0addcl readdcl syl2an jca31 adantl wb
      0re leadd2 mp3an1 mpbid eqbrtrrd 1re leadd1 mp3an3 ax-1cn addass breqtrd
      lemul12a sylc expp1 expcl mulass eqtr4d facp1 3eqtr3d ex expcom a2d
      nn0ind impcom ) AEFBEFZBGHZBIJKZALKZMKZBAJKZGHZNOZXHXIXJCPZLKZMKZBXPJKZGH
      ZNOZQXHXIXJRLKZMKZBRJKZGHZNOZQXHXIXJDPZLKZMKZBYGJKZGHZNOZQXHXIXJYGIJKZLKZ
      MKZBYMJKZGHZNOZQXHXOQCDAXPRSZYAYFXHYSXRYCXTYENYSXQYBXIMXPRXJLTUAYSXSYDGXP
      RBJTUBUCUDCDUEZYAYLXHYTXRYIXTYKNYTXQYHXIMXPYGXJLTUAYTXSYJGXPYGBJTUBUCUDXP
      YMSZYAYRXHUUAXRYOXTYQNUUAXQYNXIMXPYMXJLTUAUUAXSYPGXPYMBJTUBUCUDXPASZYAXOX
      HUUBXRXLXTXNNUUBXQXKXIMXPAXJLTUAUUBXSXMGXPABJTUBUCUDXHXIXIYCYENXHXIUFFZXI
      UGFZXIXINOBUHZXIUIZXIUJUKXHYCXIIMKZXIXHYBIXIMXHXJULFZYBISXHBULFZUUHBUMZBU
      NUOZXJUPUOUAXHXIULFZUUGXISXHUUCUULUUEXIUQUOZXIURUOUSXHYDBGXHUUIYDBSZUUJBU
      TUOZUBVAYGEFZXHYLYRXHUUPYLYRQXHUUPVBZYLYRUUQYLVBZYIXJMKZYKYPMKZYOYQNUURYI
      UGFZRYINOZVBZYKUGFZVBXJUGFZRXJNOZVBYPUGFZVBZVBZYLXJYPNOZVBUUSUUTNOUUQUVIY
      LUUQUVCUVDUVHUUQUVAUVBUUQUUDYHUGFZUVAXHUUDUUPXHUUCUUDUUEUUFUOVCZXHUVEUUPU
      VKXHXJEFZUVEBVDZXJVEUOZXJYGVFVGZXIYHVHVIUUQUUDRXINOZUVKRYHNOZUVBUVLXHUVQU
      UPXHUUCUVQUUEUUCXIEFUVQXIVJXIVKUOUOVCUVPUUQUVEUUPUVFUVRXHUVEUUPUVOVCZXHUU
      PVLXHUVFUUPXHUVMUVFUVNXJVKUOVCZXJYGVMVNXIYHVOVPVQUUQYKUFFZUVDUUQYJEFZUWAB
      YGVRZYJUHUOYKUIUOUUQUVEUVFUVGUVSUVTXHBUGFZYMUGFZUVGUUPBVEZUUPYMEFUWEYGVDY
      MVEUOBYMVSVTWAWAVCUURYLUVJUUQYLVLUUQUVJYLUUQXJYJIJKZYPNUUQBYJNOZXJUWGNOZU
      UQYDBYJNXHUUNUUPUUOVCUUQRYGNOZYDYJNOZUUPUWJXHYGVKWBUUQYGUGFZUWDUWJUWKWCZU
      UPUWLXHYGVEWBXHUWDUUPUWFVCZRUGFUWLUWDUWMWDRYGBWEWFVIWGWHUUQUWDYJUGFZUWHUW
      IWCZUWNUUQUWBUWOUWCYJVEUOUWDUWOIUGFUWPWIBYJIWJWKVIWGXHUUIYGULFZUWGYPSZUUP
      UUJYGUMUUIUWQIULFUWRWLBYGIWMWKVTZWNVCVQYIYKXJYPWOWPUUQYOUUSSYLUUQYOXIYHXJ
      MKZMKZUUSUUQYNUWTXIMXHUUHUUPYNUWTSUUKXJYGWQVGUAUUQUULYHULFZUUHUUSUXASXHUU
      LUUPUUMVCXHUUHUUPUXBUUKXJYGWRVGXHUUHUUPUUKVCXIYHXJWSVNWTVCUUQYQUUTSYLUUQU
      WGGHZYKUWGMKZYQUUTUUQUWBUXCUXDSUWCYJXAUOUUQUWGYPGUWSUBUUQUWGYPYKMUWSUAXBV
      CVAXCXDXEXFXG $.
      $( [28-Dec-2007] $)
  $}

  $( The product of two factorials is greater than or equal to the factorial of
     (the floor of) their average. $)
  facavg $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
       ( ! ` ( |_ ` ( ( M + N ) / 2 ) ) ) <_ ( ( ! ` M ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel wa caddc co c2 cdiv cle wbr wo cfl cfv cfa cmul cr nn0re avgle
    syl2an nn0addcl syl rehalfcl flle 3syl wi reflcl adantr letr syl111anc
    mpand cc0 nn0ge0 wb halfnneg2 mpbid flge0nn0 syl11anc simpl facwordi 3exp
    sylc c1 wceq cn cc faccl nncn mulid1 nnre adantl nnnn0 jca nnge1 1re
    lemul2a mp3anl1 syl21anc eqbrtrrd remulcl mpan2d 3syld simpr mulid2
    lemul1a jaod mpd ) ACDZBCDZEZABFGZHIGZAJKZWLBJKZLZWLMNZONZAONZBONZPGZJKZWHA
    QDZBQDZWOWIARZBRZABSTWJWMXAWNWJWMWPAJKZWQWRJKZXAWJWPWLJKZWMXFWJWKQDZWLQDZXH
    WJWKCDZXIABUAZWKRZUBZWKUCZWLUDUEZWJWPQDZXJXBXHWMEXFUFWJXIXJXQXNXOWLUGUEZWJX
    KXIXJXLXMXOUEZWHXBWIXDUHWPWLAUIUJUKWJWPCDZWHXFXGUFWJXJULWLJKZXTXSWJULWKJKZY
    AWJXKYBXLWKUMUBWJXKXIYBYAUNXLXMWKUOUEUPWLUQURZWHWIUSXTWHXFXGWPAUTVAVBWJXGWR
    WTJKZXAWJWRVCPGZWRWTJWHYEWRVDZWIWHWRVEDZWRVFDYFAVGZWRVHWRVIUEUHWJWSQDZWRQDZ
    ULWRJKZEZVCWSJKZYEWTJKZWIYIWHWIWSVEDZYIBVGZWSVJUBZVKZWHYLWIWHYJYKWHYGYJYHWR
    VJUBZWHYGWRCDYKYHWRVLWRUMUEVMUHWIYMWHWIYOYMYPWSVNUBVKVCQDZYIYLYMYNVOVCWSWRV
    PVQVRVSWJWQQDZYJWTQDZXGYDEXAUFWJXTWQVEDUUAYCWPVGWQVJUEZWHYJWIYSUHZWHYJYIUUB
    WIYSYQWRWSVTTZWQWRWTUIUJWAWBWJWNWPBJKZWQWSJKZXAWJXHWNUUFXPWJXQXJXCXHWNEUUFU
    FXRXSWIXCWHXEVKWPWLBUIUJUKWJXTWIUUFUUGUFYCWHWIWCXTWIUUFUUGWPBUTVAVBWJUUGWSW
    TJKZXAWJVCWSPGZWSWTJWIUUIWSVDZWHWIYOWSVFDUUJYPWSVHWSWDUEVKWJYJYIULWSJKZEZVC
    WRJKZUUIWTJKZUUDWIUULWHWIYIUUKYQWIYOWSCDUUKYPWSVLWSUMUEVMVKWHUUMWIWHYGUUMYH
    WRVNUBUHYTYJUULUUMUUNVOVCWRWSWEVQVRVSWJUUAYIUUBUUGUUHEXAUFUUCYRUUEWQWSWTUIU
    JWAWBWFWG $.
    $( [9-Dec-2005] $)

