$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Factorial function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ! $. $( Factorial. $)

  $( Extend class notation to include the factorial of nonnegative integers. $)
  cfa $a class ! $.

  ${
    $d x y $.
    $( Define the factorial function on nonnegative integers.  In the
       literature, the factorial function is written as a postscript
       exclamation point. $)
    df-fac $a |- ! = ( { <. 0 , 1 >. } u. ( x. seq1 ( I |` NN ) ) ) $.
  $}

  $( Value of the factorial function for positive integers. $)
  facnn $p |- ( N e. NN -> ( ! ` N ) = ( ( x. seq1 ( I |` NN ) ) ` N ) ) $=
    ( cn wcel cn0 cc0 csn cdif cfa cfv cmul cid cres cseq1 co wceq dfn2 eleq2i
    c1 0nn0 elisseti 1nn cop cun df-fac reseq2 ax-mp wfn mulex cvv nnex
    resiexg seq1fn fnresdm eqtr3i uneq2i eqtr4i fvsnun2 sylbi ) ABCADEFGZCAHIAJ
    KBLZMNZIOBUSAPQERDAVAHEDSTRBUATHERUBFZVAUCVBVAUSLZUCUDVCVAVBVABLZVCVABUSOVD
    VCOPBUSVAUEUFVABUGVDVAOJUTUHBUICUTUICUJBUIUKUFULBVAUMUFUNUOUPUQUR $.
    $( [3-Dec-2004] $) $( [2-Dec-2004] $)

  $( The factorial of 0. $)
  fac0 $p |- ( ! ` 0 ) = 1 $=
    ( cc0 c1 cn0 cmul cid cn cres cseq1 co cfa 0nn0 elisseti 1nn cop csn cun
    cdif df-fac wfn wceq mulex cvv wcel nnex resiexg ax-mp seq1fn fnresdm dfn2
    reseq2 eqtr3i uneq2i eqtri fvsnun1 ) ABCDEFGZHIZJACKLBFMLJABNOZUPPUQUPCAOQZ
    GZPRUPUSUQUPFGZUPUSUPFSUTUPTDUOUAFUBUCUOUBUCUDFUBUEUFUGFUPUHUFFURTUTUSTUIFU
    RUPUJUFUKULUMUN $.
    $( [3-Dec-2004] $) $( [2-Dec-2004] $)

  $( The factorial of 1. $)
  fac1 $p |- ( ! ` 1 ) = 1 $=
    ( c1 cfa cfv cmul cid cn cres cseq1 co wcel wceq 1nn facnn ax-mp mulex
    wfun cvv funi nnex resfunexg mp2an seq11 fvres fvi eqtri 3eqtri ) ABCZADEFG
    ZHICZAUHCZAAFJZUGUIKLAMNDUHOEPFQJUHQJRSEFQTUAUBUJAECZAUKUJULKLAFEUCNUKULAKL
    AFUDNUEUF $.
    $( [3-Dec-2004] $) $( [2-Dec-2004] $)

  $( The factorial of a successor. $)
  facp1 $p |- ( N e. NN0 ->
               ( ! ` ( N + 1 ) ) = ( ( ! ` N ) x. ( N + 1 ) ) ) $=
    ( cn0 wcel cn cc0 wceq wo c1 caddc co cfa cfv cmul elnn0 cid cres cseq1
    peano2nn facnn syl mulex wfun cvv funi nnex resfunexg mp2an seq1p1 fvres
    oprex fvi ax-mp syl6eq opreq2d opreq1d eqtr4d 3eqtrd ax1cn addid2i fveq2i
    fac1 eqtri opreq1 fveq2d fveq2 opreq12d fac0 opreq12i mulid1i 3eqtr4a jaoi
    sylbi ) ABCADCZAEFZGAHIJZKLZAKLZVOMJZFZANVMVSVNVMVPVOMODPZQJZLZAWALZVOVTLZM
    JZVRVMVODCZVPWBFARZVOSTAMVTUAOUBDUCCVTUCCUDUEODUCUFUGUHVMWEWCVOMJVRVMWDVOWC
    MVMWDVOOLZVOVMWFWDWHFWGVODOUITVOUCCWHVOFAHIUJVOUCUKULUMUNVMVQWCVOMASUOUPUQV
    NEHIJZKLZHVPVRWJHKLHWIHKHURUSZUTVAVBVNVOWIKAEHIVCZVDVNVREKLZWIMJZHVNVQWMVOW
    IMAEKVEWLVFWNHHMJHWMHWIHMVGWKVHHURVIVBUMVJVKVL $.
    $( [3-Dec-2004] $) $( [2-Dec-2004] $)

  $( The factorial of 2. $)
  fac2 $p |- ( ! ` 2 ) = 2 $=
    ( c2 cfa cfv c1 caddc co cmul df-2 fveq2i cn0 wcel wceq 1nn0 facp1 ax-mp
    fac1 eqcomi opreq12i 2cn mulid2i eqtri 3eqtri ) ABCDDEFZBCZDBCZUCGFZAAUCBHI
    DJKUDUFLMDNOUFDAGFAUEDUCAGPAUCHQRASTUAUB $.
    $( [7-Jul-2005] $) $( [17-Mar-2005] $)

  $( The factorial of 3. $)
  fac3 $p |- ( ! ` 3 ) = 6 $=
    ( c3 cfa cfv c2 c1 caddc co cmul c6 df-3 fveq2i cn0 wcel wceq 2nn0 facp1
    ax-mp fac2 eqcomi opreq12i 2cn 3re recni mulcomi 3t2e6 3eqtri ) ABCDEFGZBCZ
    DBCZUGHGZIAUGBJKDLMUHUJNODPQUJDAHGADHGIUIDUGAHRAUGJSTDAUAAUBUCUDUEUFUF $.
    $( [19-Jul-2005] $) $( [17-Mar-2005] $)

  $( Value of the factorial function expressed recursively. $)
  facnn2 $p |- ( N e. NN ->
               ( ! ` N ) = ( ( ! ` ( N - 1 ) ) x. N ) ) $=
    ( cn wcel cc c1 cmin co cn0 wa cfa cfv cmul wceq elnnnn0 caddc facp1
    adantl ax1cn npcan mpan2 fveq2d adantr opreq2d 3eqtr3d sylbi ) ABCADCZAEFGZ
    HCZIZAJKZUGJKZALGZMANUIUGEOGZJKZUKUMLGZUJULUHUNUOMUFUGPQUFUNUJMUHUFUMAJUFED
    CUMAMRAESTZUAUBUFUOULMUHUFUMAUKLUPUCUBUDUE $.
    $( [5-Feb-2005] $) $( [2-Dec-2004] $)

  ${
    $d j k N $.
    $( Closure of the factorial function. $)
    faccl $p |- ( N e. NN0 -> ( ! ` N ) e. NN ) $=
      ( vj vk cv cfa cfv cn wcel cc0 c1 caddc co wceq fveq2 eleq1d fac0 1nn
      eqeltri cn0 wa cmul facp1 adantl nnmulcl nn0p1nn sylan2 eqeltrd expcom
      nn0ind ) BDZEFZGHIEFZGHCDZEFZGHZUMJKLZEFZGHZAEFZGHBCAUJIMUKULGUJIENOUJUMM
      UKUNGUJUMENOUJUPMUKUQGUJUPENOUJAMUKUSGUJAENOULJGPQRUOUMSHZURUOUTTUQUNUPUA
      LZGUTUQVAMUOUMUBUCUOUPGHVAGHUTUNUPUDUMUEUFUGUHUI $.
      $( [29-Dec-2004] $) $( [2-Dec-2004] $)
  $}

  $( The factorial function is nonzero. $)
  facne0 $p |- ( N e. NN0 -> ( ! ` N ) =/= 0 ) $=
    ( cn0 wcel cfa cfv cn cc0 wne faccl nnne0 syl ) ABCADEZFCLGHAILJK $.
    $( [3-May-2005] $) $( [26-Apr-2005] $)

  ${
    $d j M $.  $d j k N $.
    $( A natural number divides the factorial of an equal or larger number. $)
    facdiv $p |- ( ( M e. NN0 /\ N e. NN /\ N <_ M ) ->
                  ( ( ! ` M ) / N ) e. NN ) $=
      ( vj vk cn0 wcel cn cle wbr cfa cfv cdiv co cv wi cc0 c1 caddc wceq
      breq2 fveq2 opreq1d eleq1d imbi12d imbi2d clt wn nngt0 cr wb nnre 0re
      ltnle mpan syl mpbid pm2.21d wa cmul wo leloe peano2nn0 nn0re syl2an
      nn0leltp1 nnnn0 sylan nnmulcl nn0p1nn sylan2 expcom adantl cc wne div23
      faccl nncn nn0cn nnne0 jca adantr syl3anc sylibrd imim2d com23 sylbird
      opreq2 divcan4 eqeltrd syl5cbi a1dd jaod sylbid ex com34 com12 imp4d
      facp1 exp4d a2d nn0ind 3imp ) AEFBGFZBAHIZAJKZBLMZGFZXCBCNZHIZXHJKZBLMZGF
      ZOZOXCBPHIZPJKZBLMZGFZOZOXCBDNZHIZXSJKZBLMZGFZOZOXCBXSQRMZHIZYEJKZBLMZGFZ
      OZOXCXDXGOZOCDAXHPSZXMXRXCYLXIXNXLXQXHPBHTYLXKXPGYLXJXOBLXHPJUAUBUCUDUEXH
      XSSZXMYDXCYMXIXTXLYCXHXSBHTYMXKYBGYMXJYABLXHXSJUAUBUCUDUEXHYESZXMYJXCYNXI
      YFXLYIXHYEBHTYNXKYHGYNXJYGBLXHYEJUAUBUCUDUEXHASZXMYKXCYOXIXDXLXGXHABHTYOX
      KXFGYOXJXEBLXHAJUAUBUCUDUEXCXNXQXCPBUFIZXNUGZBUHXCBUIFZYPYQUJZBUKZPUIFYRY
      SULPBUMUNUOUPUQXSEFZXCYDYJUUAXCYDYFYIUUAXCYDYFURURYAYEUSMZBLMZGFZYIUUAXCY
      DYFUUDXCUUAYDYFUUDOOXCUUAYFYDUUDXCUUAYFYDUUDOZOXCUUAURZYFBYEUFIZBYESZUTZU
      UEYRYEUIFZYFUUIUJXCUUABYEVAYTUUAYEEFZUUJXSVBZYEVCUOVDUUFUUGUUEUUHUUFUUGXT
      UUEBEFUUAXTUUGUJXCBXSVEBVFVGUUFYDXTUUDUUFYCUUDXTUUFYCYBYEUSMZGFZUUDUUAYCU
      UNOXCYCUUAUUNYCYEGFUUNUUAYBYEVHXSVIVJVKVLUUFUUCUUMGYAVMFZYEVMFZBVMFZBPVNZ
      URZUUCUUMSUUFYAYEBVOUUAUUOXCUUAYAGFZUUOXSVPZYAVQUOVLZUUAUUPXCUUAUUKUUPUUL
      YEVRUOVLXCUUSUUAXCUUQUURBVQZBVSZVTWAWBUCWCWDWEWFUUFUUHUUDYDUUHYABUSMZBLMZ
      GFUUDUUFUUHUVFUUCGUUHUVEUUBBLBYEYAUSWGUBUCUUFUVFYAGUUOUUQUURUVFYASUUFYABW
      HUVBXCUUQUUAUVCWAXCUURUUAUVDWAWBUUAUUTXCUVAVLWIWJWKWLWMWNWOWPWQUUAYHUUCGU
      UAYGUUBBLXSWRUBUCWCWSWTXAXB $.
      $( [6-May-2005] $) $( [2-May-2005] $)
  $}

  $( No natural number (greater than one) divides the factorial plus one of an
     equal or larger number. $)
  facndiv $p |- ( ( ( M e. NN0 /\ N e. NN ) /\ ( 1 < N /\ N <_ M ) ) ->
                -. ( ( ( ! ` M ) + 1 ) / N ) e. ZZ ) $=
    ( cn0 wcel cn wa c1 clt wbr cle cfa cfv caddc co cdiv cz wn cr recnz nnre
    sylan ad2ant2lr cmin zsubcl ex facdiv 3expa nnz syl adantrl syl5com cc cc0
    wne wceq divsubdir faccl nncn peano2cn ad2antrr nnne0 jca ad2antlr syl3anc
    ax1cn pncan2 mpan2 opreq1d eqtr3d eleq1d sylibd mtod ) ACDZBEDZFZGBHIZBAJIZ
    FZFZAKLZGMNZBONZPDZGBONZPDZVNVPWEQZVMVQBRDVPWFVNBSBTUAUBVSWCWBVTBONZUCNZPDZ
    WEWCWGPDZWIVSWCWJWIWBWGUDUEVOVQWJVPVOVQFWGEDZWJVMVNVQWKABUFUGWGUHUIUJUKVSWH
    WDPVSWAVTUCNZBONZWHWDWAULDZVTULDZBULDZBUMUNZFZWMWHUOVSWAVTBUPVMWNVNVRVMWOWN
    VMVTEDWOAUQVTURUIZVTUSUIUTVMWOVNVRWSUTVNWRVMVRVNWPWQBURBVAVBVCVDVMWMWDUOVNV
    RVMWLGBOVMWOWLGUOZWSWOGULDWTVEVTGVFVGUIVHUTVIVJVKVL $.
    $( [8-May-2005] $) $( [3-May-2005] $)

  ${
    $d j k M $.  $d j N $.
    $( Ordering property of factorial. $)
    facwordi $p |- ( ( M e. NN0 /\ N e. NN0 /\ M <_ N ) ->
                  ( ! ` M ) <_ ( ! ` N ) ) $=
      ( vj vk cn0 wcel cle wbr cfa cfv cv wa wi cc0 c1 caddc co wceq breq2
      anbi2d fveq2 breq2d imbi12d nn0le0eq0 biimpa fveq2d cr fac0 1re eqeltri
      leidi syl6eqbr clt wo wb leloe nn0re peano2re syl syl2an nn0leltp1 cmul
      cn cc faccl nncn ax1id 3syl lemul2aOLD mp3anl1 nnre nnnn0 nn0ge0 0re
      leadd1 mp3an13 mpbid ax1cn addid2i syl5eqbrr syl2anc eqbrtrrd facp1
      breqtrrd adantl letr adantr peano2nn0 syl3anc mpan2d imim2d com23
      sylbird leid biimpcd syl5 a1dd jaod sylbid ex com13 com4l a2d imp4a
      impexp syl5ib nn0ind 3impib 3com12 ) BEFZAEFZABGHZAIJZBIJZGHZXJXKXLXOXKAC
      KZGHZLZXMXPIJZGHZMXKANGHZLZXMNIJZGHZMXKADKZGHZLZXMYEIJZGHZMZXKAYEOPQZGHZL
      ZXMYKIJZGHZMZXKXLLZXOMCDBXPNRZXRYBXTYDYRXQYAXKXPNAGSTYRXSYCXMGXPNIUAUBUCX
      PYERZXRYGXTYIYSXQYFXKXPYEAGSTYSXSYHXMGXPYEIUAUBUCXPYKRZXRYMXTYOYTXQYLXKXP
      YKAGSTYTXSYNXMGXPYKIUAUBUCXPBRZXRYQXTXOUUAXQXLXKXPBAGSTUUAXSXNXMGXPBIUAUB
      UCYBXMYCYCGYBANIXKYAANRAUDUEUFYCYCOUGUHUIUJUKULYEEFZXKYFYIMZMZYPYJUUBUUDX
      KYLYOUUBXKUUCYLYOMYLUUBXKUUCYOXKUUBYLUUCYOMZXKUUBYLUUEMXKUUBLZYLAYKUMHZAY
      KRZUNZUUEAUGFYKUGFZYLUUIUOXKUUBAYKUPAUQUUBYEUGFZUUJYEUQZYEURUSZUTUUFUUGUU
      EUUHUUFUUGYFUUEAYEVAUUFUUCYFYOUUFYIYOYFUUFYIYHYNGHZYOUUBUUNXKUUBYHYHYKVBQ
      ZYNGUUBYHOVBQZYHUUOGUUBYHVCFZYHVDFUUPYHRYEVEZYHVFYHVGVHUUJYHUGFZNYHGHZOYK
      GHZUUPUUOGHZUUBOUGFZUUJUUSUUTUVALUVBUIOYKYHVIVJUUMUUBUUQUUSUURYHVKUSZUUBU
      UQYHEFUUTUURYHVLYHVMVHUUBNOPQZYKOGUUBNYEGHZUVEYKGHZYEVMUUBUUKUVFUVGUOZUUL
      NUGFUUKUVCUVHVNUINYEOVOVPUSVQOVRVSVTWAWBYEWCWDWEXMUGFZUUSYNUGFZYIUUNLYOMU
      UFXMYHYNWFXKUVIUUBXKXMVCFUVIAVEXMVKUSZWGUUBUUSXKUVDWEUUBUVJXKUUBYKEFYNVCF
      UVJYEWHYKVEYNVKVHWEWIWJWKWLWMUUFUUHYOUUCXKUUHYOMUUBXKXMYNRZYOUUHXKUVIXMXM
      GHZUVLYOMUVKXMWNUVLUVMYOXMYNXMGSWOVHAYKIUAWPWGWQWRWSWTXAXBXCXDXKYFYIXEXFX
      GXHXI $.
      $( [30-Dec-2005] $) $( [9-Dec-2005] $)

    $( A lower bound for the factorial function. $)
    faclbnd $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                   ( M ^ ( N + 1 ) ) <_ ( ( M ^ M ) x. ( ! ` N ) ) ) $=
      ( vj vk cn wcel cc0 wceq wo cn0 c1 caddc co cexp cfa cfv cmul cle wbr cv
      wi opreq1 opreq2d fveq2 breq12d imbi2d cr w3a wa expwordi nnre 1nn0 a1i
      nnnn0 3jca nnge1 jca sylanc ax1cn addid2i opreq2i cc reexpcl recnd ax1id
      syl fac0 syl5eq 3brtr4d clt lelttric nn0re peano2re syl2an lemul12aOLD
      peano2nn0 adantr remulcl faccl expge0 adantl nn0ge0 syl3anc anim1i
      ad2antrr ad2antlr anandis expp1 nncn facp1 mulass nn0cn peano2cn eqtr4d
      exp32 com23 wb nn0ltp1le ancoms 3syl lemulge11 anim12i id letrd ex
      sylbid a1dd jaod mpd expcom a2d nn0ind impcom opreq12 anidms opreq1d
      nn0p1nn 0exp mulid2 0cn exp0 ax-mp opreq1i syl5bir imp jaoian elnn0
      sylanb ) AEFZAGHZIBJFZABKLMZNMZAANMZBOPZQMZRSZAJFZYIYKYQYJYKYIYQYIACTZKLM
      ZNMZYNYSOPZQMZRSZUAYIAGKLMZNMZYNGOPZQMZRSZUAYIADTZKLMZNMZYNUUJOPZQMZRSZUA
      YIAUUKKLMZNMZYNUUKOPZQMZRSZUAYIYQUACDBYSGHZUUDUUIYIUVAUUAUUFUUCUUHRUVAYTU
      UEANYSGKLUBUCUVAUUBUUGYNQYSGOUDUCUEUFYSUUJHZUUDUUOYIUVBUUAUULUUCUUNRUVBYT
      UUKANYSUUJKLUBUCUVBUUBUUMYNQYSUUJOUDUCUEUFYSUUKHZUUDUUTYIUVCUUAUUQUUCUUSR
      UVCYTUUPANYSUUKKLUBUCUVCUUBUURYNQYSUUKOUDUCUEUFYSBHZUUDYQYIUVDUUAYMUUCYPR
      UVDYTYLANYSBKLUBUCUVDUUBYOYNQYSBOUDUCUEUFYIAKNMZYNUUFUUHRAUGFZKJFZYRUHKAR
      SZUVHUIUVEYNRSYIAKAUJYIUVFUVGYRAUKZUVGYIULUMAUNZUOYIUVHUVHAUPZUVKUQURUUFU
      VEHYIUUEKANKUSUTVAUMYIYNKQMZYNUUHYIYNVBFZUVLYNHYIYNUVFYRYNUGFZYIAAVCUVIUV
      JURZVDZYNVEVFUUGKYNQVGVAVHVIUUJJFZYIUUOUUTYIUVQUUOUUTUAZYIUVQUIZAUUKRSZUU
      KAVJSZIZUVRUVFUUKUGFZUWBYIUVQAUUKVKUVIUVQUUJUGFUWCUUJVLUUJVMVFZVNUVSUVTUV
      RUWAUVSUUOUVTUUTUVSUUOUVTUUTUVSUUOUVTUIZUIUULAQMZUUNUUKQMZUUQUUSRUVSUUOUV
      TUWFUWGRSZUULUGFZUUNUGFZUIZGUULRSZUUOUIZUIUVFUWCUIZGARSZUVTUIZUIUWHUVSUUO
      UIZUVSUVTUIZUULUUNAUUKVOUWQUWKUWMUWQUWIUWJUVSUWIUUOUVFUUKJFZUWIYIUVQAUUKV
      CUVIUUJVPZVNVQUVSUWJUUOUVNUUMUGFZUWJYIUVQYNUUMVRUVOUVQUUMEFUXAUUJVSUUMUKV
      FZVNVQUQUVSUWLUUOUVFUWSUWOUWLUVSAUUKVTYIUVFUVQUVIVQUVQUWSYIUWTWAYIUWOUVQY
      IYRUWOUVJAWBZVFVQZWCWDUQUWRUWNUWPUWRUVFUWCYIUVFUVQUVTUVIWEUVQUWCYIUVTUWDW
      FUQUVSUWOUVTUXDWDUQVNWGUVSUUQUWFHZUWEAVBFUWSUXEYIUVQAUUKWHAWIUWTVNVQUVSUU
      SUWGHUWEUVSUUSYNUUMUUKQMZQMZUWGUVSUURUXFYNQUVQUURUXFHYIUUJWJWAUCUVMUUMVBF
      ZUUKVBFZUWGUXGHUVSYNUUMUUKWKYIUVMUVQUVPVQUVQUXHYIUVQUUMUXBVDWAUVQUXIYIUVQ
      UUJVBFUXIUUJWLUUJWMVFWAWCWNVQVIWOWPUVSUWAUUTUUOUVSUWAUUPARSZUUTUVQYIUWAUX
      JWQZUWSYRUXKUVQYIUUKAWRUWTUVJVNWSUVSUXJUUTUVSUXJUIZUUQYNUUSUVSUUQUGFZUXJU
      VFUUPJFZUXMYIUVQAUUPVCUVIUVQUWSUXNUWTUUKVPVFZVNVQYIUVNUVQUXJUVOWEUVSUUSUG
      FZUXJUVNUURUGFZUXPYIUVQYNUURVRUVOUVQUWSUUREFZUXQUWTUUKVSZUURUKWTZVNVQUVFU
      XNYRUHUVHUXJUIUUQYNRSUXLAUUPAUJUXLUVFUXNYRYIUVFUVQUXJUVIWEUVQUXNYIUXJUXOW
      FYIYRUVQUXJUVJWEUOUVSUVHUXJYIUVHUVQUVKVQWDURUVSYNUUSRSZUXJUVNUXQUIGYNRSZK
      UURRSZUIUYAUVSYNUURXAYIUVNUVQUXQUVOUXTXBYIUYBUVQUYCYIYRUYBUVJUVFYRUWOUYBY
      RAAVTAVLYRXCUXCWCVFUVQUWSUXRUYCUWTUXSUURUPWTXBURVQXDXEXFXGXHXIXJXKXLXMYJY
      KYQYJYQGYLNMZGGNMZYOQMZRSYKYJYMUYDYPUYFRAGYLNUBYJYNUYEYOQYJYNUYEHAGAGNXNX
      OXPUEYKGYOUYDUYFRYKYOEFZYOJFGYORSBVSZYOUNYOWBWTYKYLEFUYDGHBXQYLXRVFYKKYOQ
      MZYOUYFYKUYGYOVBFUYIYOHUYHYOWIYOXSWTUYEKYOQGVBFUYEKHXTGYAYBYCVHVIYDYEYFAY
      GYH $.
      $( [18-Dec-2005] $) $( [17-Dec-2005] $)
  $}

  $( A lower bound for the factorial function. $)
  faclbnd2 $p |- ( N e. NN0 -> ( ( 2 ^ N ) / 2 ) <_ ( ! ` N ) ) $=
    ( cn0 wcel c2 cexp co cdiv c1 caddc cfa cfv cle cmul cc wceq 2cn expp1
    mpan opreq1d c4 sq2 2t2e4 eqtr4i opreq2i syl5eq expcl wa cc0 wne 2ne0
    pm3.2i divmuldiv mpanr12 mpan2 syl halfcl ax1id 3syl dividi 3eqtr2rd wbr
    2nn0 faclbnd cr wb clt 4re eqeltri 4pos breqtrri ledivmul mp3an3 peano2nn0
    2re reexpcl cn faccl nnre sylanc mpbird eqbrtrd ) ABCZDAEFZDGFZDAHIFZEFZDDE
    FZGFZAJKZLWBWHWCDMFZDDMFZGFZWDDDGFZMFZWDWBWFWKGFWLWHWBWFWJWKGDNCZWBWFWJOPDA
    QRSWGWKWFGWGTWKUAUBUCUDUEWBWCNCZWNWLOZWOWBWPPDAUFRZWPWOWQPWPWOUGWODUHUIZUGZ
    WTWQWOWSPUJUKZXAWCDDDULUMUNUOWBWDHMFZWDWNWBWPWDNCXBWDOWRWCUPWDUQURWMHWDMDPU
    JUSUDUEUTWBWHWILVAZWFWGWIMFLVAZDBCWBXDVBDAVCRWFVDCZWIVDCZXCXDVEZWBXEXFWGVDC
    ZUHWGVFVAZUGXGXHXIWGTVDUAVGVHUHTWGVFVIUAVJUKWFWIWGVKVLWBWEBCZXEAVMDVDCXJXEV
    NDWEVORUOWBWIVPCXFAVQWIVRUOVSVTWA $.
    $( [18-Dec-2005] $) $( [17-Dec-2005] $)

  $( A lower bound for the factorial function. $)
  faclbnd3 $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                 ( M ^ N ) <_ ( ( M ^ M ) x. ( ! ` N ) ) ) $=
    ( cn wcel cc0 wceq wo cn0 cexp co cfa cfv cmul cle wbr wa c1 caddc cr w3a
    expwordi nnre adantr pm3.27 peano2nn0 adantl 3jca nnge1 letrp1 nn0re leid
    syl syl3anc anim12i sylanc faclbnd nnnn0 sylan wi letr reexpcl syl2an
    remulcl mpancom faccl mp2and elnn0 0exp 0re 1re lt01 ltleii syl6eqbr
    opreq2 cc 0cn exp0 ax-mp leidi eqbrtri jaoi sylbi 1nn nnmulcl mpan mp3an2
    wb opreq1 opreq12 anidms syl6eq opreq1d breq12d mpbird jaoian sylanb ) ACDZ
    AEFZGBHDZABIJZAAIJZBKLZMJZNOZAHDZWQWSXDWRWQWSPZWTABQRJZIJZNOZXHXCNOZXDASDZW
    SXGHDZTQANOZBXGNOZPXIXFABXGUAXFXKWSXLWQXKWSAUBUCWQWSUDWSXLWQBUEZUFUGWQXMWSX
    NAUHBSDZXPBBNOZXNWSBBUIBUJZXRWSXPXQXRBUKULUMUNUOXEWSXJWQABUPAUQZURXEWSXIXJP
    XDUSZWQWTSDZXHSDZXCSDZXTXEWSPWTXHXCUTXKWSYAXEABVAAUJZURXKXLYBXEWSAXGVAYDXOV
    BXASDZXBSDZYCXEWSXAXBVCXKXEYEYDAAVAVDWSXBCDZYFBVEZXBUBULZVBUMXSURVFWRWSPXDE
    BIJZQXBMJZNOZWSYLWRWSYJQNOZQYKNOZYLWSBCDZBEFZGYMBVGYOYMYPYOYJEQNBVHEQVIVJVK
    VLVMYPYJEEIJZQNBEEIVNYQQQNEVODYQQFVPEVQVRZQVJVSVTVMWAWBWSYKCDZYNWSYGYSYHQCD
    YGYSWCQXBWDWEULYKUHULYJSDZYKSDZYMYNPYLUSZWSYTQSDZUUAUUBVJYJQYKUTWFESDWSYTVI
    EBVAWEWSYFUUAYIUUCYFUUAVJQXBVCWEULUOVFUFWRXDYLWGWSWRWTYJXCYKNAEBIWHWRXAQXBM
    WRXAYQQWRXAYQFAEAEIWIWJYRWKWLWMUCWNWOAVGWP $.
    $( [24-Dec-2005] $) $( [19-Dec-2005] $)

  ${
    faclbnd4lem1.1 $e |- N e. NN $.
    faclbnd4lem1.2 $e |- K e. NN0 $.
    faclbnd4lem1.3 $e |- M e. NN0 $.
    $( Lemma for ~ faclbnd4 .  Prepare the induction step.  _Warning:  The HTML
       proof page is 0.6 megabyte in size_. $)
    faclbnd4lem1 $p |- ( ( ( ( N - 1 ) ^ K ) x. ( M ^ ( N - 1 ) ) ) <_
       ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` ( N - 1 ) ) ) ->
                   ( ( N ^ ( K + 1 ) ) x. ( M ^ N ) ) <_ ( ( (
  2 ^ ( ( K + 1 ) ^ 2 ) ) x. ( M ^ ( M + ( K + 1 ) ) ) ) x. ( ! ` N ) ) ) $=
      ( c1 cle wbr clt wo cmin co cexp cmul c2 caddc cfa cfv cr wcel nnrei 1re
      lelttric mp2an wceq letri3i cn nnge1 ax-mp mpbiran2 cc0 wa 0re lt01
      ltleii pm3.2i cn0 2re 1nn nn0nnaddcl nnnn0i 2nn0 nn0expcl reexpcl nn0rei
      nn0ge0i cc 2cn exp0 w3a 0nn0 3pm3.2i 1lt2 expwordi eqbrtrri elnn0 nncn
      exp1 syl 1nn0 mpan mpan2 eqbrtrrd breq1 mpbiri jaoi sylbi lemul12a mp2
      opreq1 1exp syl6eq opreq2 nn0cni opreq12d fveq2 fac1 opreq2d recni
      mulcli mulid1i breq12d sylbir adantr remulcli a1i faccl nn0mulcli
      elnnnn0 mpbi pm3.27i nn0addcli wb nnltp1le df-2 breq1i bitr4i expubnd
      mp3an12 lemul1a nncni mul4i mulassi opreq2i mul12i 3eqtr2i syl6breq
      adantl letrd mul23i expp1 ax1cn addassi eqtr3i facnn2 opreq12i 3eqtr4i
      eqtri expm1t syl5eqbr expadd mp3an ltp1i sqvali eqcomi adddii eqtr4i
      3brtr4i syl6breqr jaoian ) CGHIZGCJIZKZCGLMZANMZBUUONMZOMZPAPNMZNMZBBAQMZ
      NMZOMZUUORSZOMZHIZCAGQMZNMZBCNMZOMZPUVGPNMZNMZBBUVGQMZNMZOMZCRSZOMZHIZCTU
      AZGTUAZUUNCDUBZUCCGUDUEUULUVFUVRUUMUULUVRUVFUULCGUFZUVRUWBUULGCHIZCGUWAUC
      UGCUHUAZUWCDCUIUJUKUWBUVRGBOMZUVOHIZUVTULGHIZUMZUVLTUAZUMZBTUAZULBHIZUMZU
      VNTUAZUMZUMGUVLHIZBUVNHIZUMUWFUWJUWOUWHUWIUVTUWGUCULGUNUCUOUPUQPTUAZUVKUR
      UAZUWIUSUVGURUAZPURUAZUWSUVGAURUAZGUHUAZUVGUHUAZEUTAGVAUEZVBZVCUVGPVDUEZP
      UVKVEUEZUQUWMUWNUWKUWLBFVFZBFVGUQUVNBURUAZUVMURUAZUVNURUAFUVMUXJUXDUVMUHU
      AZFUXEBUVGVAUEZVBZBUVMVDUEZVFUQUQUWPUWQPULNMZGUVLHPVHUAZUXPGUFVIPVJUJUWRU
      LURUAZUWSVKGPHIZULUVKHIZUMUXPUVLHIUWRUXRUWSUSVLUXGVMUXSUXTGPUCUSVNUPZUVKU
      XGVGUQPULUVKVOUEVPUXJUWQFUXJBUHUAZBULUFZKUWQBVQUYBUWQUYCUYBBGNMZBUVNHUYBB
      VHUAZUYDBUFZBVRBVSZVTUYBGBHIZUYDUVNHIZBUIUYHGUVMHIZUYIUXLUYJUXMUVMUIUJUWK
      GURUAZUXKVKUYHUYJUMUYIUWKUYKUXKUXIWAUXNVMBGUVMVOWBWCVTWDUYCUWQULUVNHIUVNU
      XOVGBULUVNHWEWFWGWHUJUQGUVLBUVNWIWJUWBUVJUWEUVQUVOHUWBUVHGUVIBOUWBUVHGUVG
      NMZGCGUVGNWKUWTUYLGUFUXFUVGWLUJWMUWBUVIUYDBCGBNWNUYEUYFBFWOZUYGUJWMWPUWBU
      VQUVOGOMUVOUWBUVPGUVOOUWBUVPGRSGCGRWQWRWMWSUVOUVLUVNUVLUXHWTZUVNUXOWOZXAX
      BWMXCWFXDXEUUMUVFUMZUVJUVLUVNUVPOMZOMZUVQHUYPUVJUUTPANMZOMZUYQOMZUYRUVJTU
      AUYPUVHUVIUVSUWTUVHTUAUWAUXFCUVGVEUEUWKCURUAZUVITUAUXICDVBZBCVEUEXFXGVUAT
      UAUYPUYTUYQUUTUYSUWRUUSURUAZUUTTUAUSUXBUXAVUDEVCAPVDUEZPUUSVEUEZUYSUXAUXB
      UYSURUAVCEPAVDUEZVFZXFZUYQUVNUVPUXOUVPVUBUVPUHUAVUCCXHUJZVBXIZVFZXFXGUYRT
      UAUYPUVLUYQUXHVULXFXGUYPCANMZCUUQBOMZOMZOMZVUAUVJHUYPVUPUVEUYSCOMZBOMZOMZ
      VUAHUYPVUPUURVUROMZVUSVUPTUAUYPVUMVUOUVSUXBVUMTUAZUWAECAVEUEZVUOCVUNVUCUU
      QBUXJUUOURUAZUUQURUAFCVHUAZVVCUWDVVDVVCUMDCXJXKXLZBUUOVDUEZFXIZXIZVFZXFXG
      VUTTUAUYPUURVURUUPUUQUUOTUAUXBUUPTUAUUOVVEVFEUUOAVEUEZUUQVVFVFXFZVURVUQBU
      YSCVUGVUCXIZFXIZVFZXFXGVUSTUAUYPUVEVURUVCUVDUUTUVBVUFUWKUVAURUAZUVBTUAUXI
      BAFEXMZBUVAVEUEZXFZUVDVVCUVDUHUAVVEUUOXHUJZUBXFZVVNXFXGUUMVUPVUTHIUVFUUMV
      UPUYSUUPOMZVUOOMZVUTHUUMVUMVWAHIZVUPVWBHIZUUMPCHIZVWCUUMGGQMZCHIZVWEUXCUW
      DUUMVWGXNUTDGCXOUEPVWFCHXPXQXRUVSUXBVWEVWCUWAECAXSXTWHVVAVWATUAZVUOTUAZUL
      VUOHIZUMZVKVWCVWDVVAVWHVWKVVBUYSUUPVUHVVJXFVWIVWJVVIVUOVVHVGUQVMVUMVWAVUO
      YAWBVTVWBVUQUUPVUNOMZOMVUQUURBOMZOMVUTUYSUUPCVUNUYSVUGWOZUUPVVJWTZCDYBZVU
      NVVGWOZYCVWMVWLVUQOUUPUUQBVWOUUQVVFWOUYMYDYEVUQUURBVUQVVLWOUURVVKWTUYMYFY
      GYHXEUVFVUTVUSHIZUUMUURTUAZUVETUAZVURTUAZULVURHIZUMZVKUVFVWRVWSVWTVXCVVKV
      VTVXAVXBVVNVURVVMVGUQVMUURUVEVURYAWBYIYJVUSUYTUVNOMZUVPOMZVUAUVEUYSBOMZCO
      MZOMUVCVXFOMZUVDCOMZOMVUSVXEUVCUVDVXFCUVCVVRWTUVDVVSYBUYSBVWNUYMXAVWPYCVU
      RVXGUVEOUYSCBVWNVWPUYMYKYEVXDVXHUVPVXIOUYTUVBBOMZOMVXDVXHVXJUVNUYTOBUVAGQ
      MZNMZVXJUVNUYEVVOVXLVXJUFUYMVVPBUVAYLUEVXKUVMBNBAGUYMAEWOZYMYNYEYOYEUUTUY
      SUVBBUUTVUFWTVWNUVBVVQWTUYMYCYOUWDUVPVXIUFDCYPUJYQYRUYTUVNUVPUYTVUIWTUYOU
      VPVUJYBZYDYSYHUVJVUMCOMZVUNOMVUPUVHVXOUVIVUNOVVDUXBUVHVXOUFVWPECAYLUEUYEU
      WDUVIVUNUFUYMDBCYTUEYQVUMCVUNVUMVVBWTVWPVWQYDYSUUAVUAUYRHIZUYPUYTTUAZUWIU
      YQTUAZULUYQHIZUMZVKUYTUVLHIVXPVXQUWIVXTVUIUXHVXRVXSVULUYQVUKVGUQVMPUUSAQM
      ZNMZUYTUVLHUXQVUDUXBVYBUYTUFVIVUEEPUUSAUUBUUCUWRVYAURUAZUWSVKUXSVYAUVKHIZ
      UMVYBUVLHIUWRVYCUWSUSUUSAVUEEXMUXGVMUXSVYDUYAAUVGOMZUVGUVGOMZVYAUVKHATUAZ
      UVGTUAZVYHULUVGHIZUMZVKAUVGHIVYEVYFHIVYGVYHVYJAEVFZUVGUXEUBZVYHVYIVYLUVGU
      XFVGUQVMAUVGVYKVYLAVYKUUDUPAUVGUVGYAUEVYAAAOMZAGOMZQMVYEUUSVYMAVYNQAVXMUU
      EVYNAAVXMXBUUFYQAAGVXMVXMYMUUGUUHUVGUVGUXEYBUUEUUIUQPVYAUVKVOUEVPUYTUVLUY
      QYAUEXGYJUVLUVNUVPUYNUYOVXNYDUUJUUKWB $.
      $( [27-Jan-2006] $) $( [20-Dec-2005] $)
  $}

  $( Lemma for ~ faclbnd4 .  Use the weak deduction theorem to convert
     the hypotheses of ~ faclbnd4lem1 to antecedents. $)
  faclbnd4lem2 $p |- ( ( M e. NN0 /\ K e. NN0 /\ N e. NN ) ->
           ( ( ( ( N - 1 ) ^ K ) x. ( M ^ ( N - 1 ) ) ) <_
       ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` ( N - 1 ) ) ) ->
                   ( ( N ^ ( K + 1 ) ) x. ( M ^ N ) ) <_ ( ( (
  2 ^ ( ( K + 1 ) ^ 2 ) ) x. ( M ^ ( M + ( K + 1 ) ) ) ) x. ( ! ` N ) ) ) ) $=
    ( cn0 wcel cn c1 cmin co cexp cmul c2 caddc cfa cfv cle wbr wi cif wceq
    opreq1 opreq2d id opreq12d opreq1d breq12d imbi12d opreq2 fveq2d fveq2 1nn
    elimel 1nn0 faclbnd4lem1 dedth3h ) BDEZADEZCFEZCGHIZAJIZBUSJIZKIZLALJIZJIZB
    BAMIZJIZKIZUSNOZKIZPQZCAGMIZJIZBCJIZKIZLVKLJIZJIZBBVKMIZJIZKIZCNOZKIZPQZRUT
    UPBGSZUSJIZKIZVDWCWCAMIZJIZKIZVHKIZPQZVLWCCJIZKIZVPWCWCVKMIZJIZKIZVTKIZPQZR
    BACGGGUSUQAGSZJIZWDKIZLWRLJIZJIZWCWCWRMIZJIZKIZVHKIZPQZCWRGMIZJIZWKKIZLXHLJ
    IZJIZWCWCXHMIZJIZKIZVTKIZPQZRURCGSZGHIZWRJIZWCXSJIZKIZXEXSNOZKIZPQZXRXHJIZW
    CXRJIZKIZXOXRNOZKIZPQZRBWCTZVJWJWBWQYLVBWEVIWIPYLVAWDUTKBWCUSJUAUBYLVGWHVHK
    YLVFWGVDKYLBWCVEWFJYLUCZBWCAMUAUDUBUEUFYLVNWLWAWPPYLVMWKVLKBWCCJUAUBYLVSWOV
    TKYLVRWNVPKYLBWCVQWMJYMBWCVKMUAUDUBUEUFUGAWRTZWJXGWQXQYNWEWTWIXFPYNUTWSWDKA
    WRUSJUHUEYNWHXEVHKYNVDXBWGXDKYNVCXALJAWRLJUAUBYNWFXCWCJAWRWCMUHUBUDUEUFYNWL
    XJWPXPPYNVLXIWKKYNVKXHCJAWRGMUAZUBUEYNWOXOVTKYNVPXLWNXNKYNVOXKLJYNVKXHLJYOU
    EUBYNWMXMWCJYNVKXHWCMYOUBUBUDUEUFUGCXRTZXGYEXQYKYPWTYBXFYDPYPWSXTWDYAKYPUSX
    SWRJCXRGHUAZUEYPUSXSWCJYQUBUDYPVHYCXEKYPUSXSNYQUIUBUFYPXJYHXPYJPYPXIYFWKYGK
    CXRXHJUACXRWCJUHUDYPVTYIXOKCXRNUJUBUFUGWRWCXRCGFUKULAGDUMULBGDUMULUNUO $.
    $( [27-Jan-2006] $) $( [23-Dec-2005] $)


  $( Lemma for ~ faclbnd4 .  The ` N = 0 ` case. $)
  faclbnd4lem3 $p |- ( ( ( M e. NN0
            /\ K e. NN0 ) /\ N = 0 ) -> ( ( N ^ K ) x. ( M ^ N ) )
       <_ ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel wa cc0 wceq cexp co cmul c2 caddc cfa cfv cle wbr c1 cn wo 0exp
    adantl nn0mulcl 2nn0 nn0expcl mpan2 mpan syl nn0addcl syldan sylanc nnnn0
    sylan2 nn0ge0 eqbrtrd elnn0 0nn0 nnexpcl mpdan id opreq1 0cn addid1i
    syl6eq opreq12d cc exp0 ax-mp 1nn syl6eqel jaoi sylbi nnmulcl nnge1 3syl
    adantr wb opreq2 sq0i opreq2d 2cn breq12d mpbird jaodan sylan2b nn0cn
    ax1id sylan9eq 3brtr4d fveq2 fac0 ) BDEZADEZFZCGHZFCAIJZBCIJZKJZLALIJZIJZBB
    AMJZIJZKJZCNOZKJZPQZGAIJZBGIJZKJZXCRKJZPQZWNXKWOWNXGXCXIXJPWLASEZAGHZTXGXCP
    QZWMWLXLXNXMWLXLFZXGGXCPXLXGGHWLAUAUBXOXCDEZGXCPQWLWMXPXLWTDEZXBDEZXPWNWTXB
    UCWMXQWLWMWSDEZXQWMLDEZXSUDALUEUFXTXSXQUDLWSUEUGUHUBWLWMXADEXRBAUIBXAUEUJUK
    ZAULUMXCUNUHUOWLXMFXNRRBBGMJZIJZKJZPQZWLYEXMWLYCSEZYDSEZYEWLBSEZBGHZTYFBUPY
    HYFYIYHYBDEZYFYHWLYJBULWLGDEZYJUQBGUIUFUHBYBURUSYIYCRSYIYCGGIJZRYIBGYBGIYIU
    TYIYBGGMJGBGGMVAGVBVCVDVEGVFEYLRHVBGVGVHZVDVIVJVKVLRSEYFYGVIRYCVMUGYDVNVOVP
    XMXNYEVQWLXMXGRXCYDPXMXGYLRAGGIVRYMVDXMWTRXBYCKXMWTLGIJZRXMWSGLIAVSVTLVFEYN
    RHWALVGVHVDXMXAYBBIAGBMVRVTVEWBUBWCWDAUPWEWLWMXIXGRKJZXGWLXHRXGKWLBVFEXHRHB
    WFBVGUHVTWMXGDEZXGVFEYOXGHYKWMYPUQGAUEUGXGWFXGWGVOWHWNXPXCVFEXJXCHYAXCWFXCW
    GVOWIVPWOXFXKVQWNWOWRXIXEXJPWOWPXGWQXHKCGAIVACGBIVRVEWOXDRXCKWOXDGNORCGNWJW
    KVDVTWBUBWC $.
    $( [28-Jan-2006] $) $( [23-Dec-2005] $)

  ${
    $d j m n M $.  $d j m n K $.  $d j n N $.
    $( Lemma for ~ faclbnd4 .  Prove the ` 0 < N ` case by induction on
       ` K ` . $)
    faclbnd4lem4 $p |- ( ( N e. NN /\ K e. NN0 /\ M e. NN0 ) ->
                   ( ( N ^ K ) x. ( M ^ N ) ) <_
              ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
      ( vn vm vj cn wcel cn0 cexp co cmul c2 caddc cfa cfv cle wbr cv wral wa
      wceq opreq1 opreq2 opreq12d fveq2 opreq2d breq12d rcla4va wi cc0 c1 cmin
      clt wo faclbnd4lem3 nnge1 adantr cr wb nnre 1re letri3 mpan2 syl biimpar
      anassrs mpdan ax1cn subidi syl6eq sylan2 a1d 1nn nnsub mpan biimpa
      rcla4v adantl jaodan lelttric ancli andi sylib faclbnd4lem2 3expa syld
      r19.21adva cbvralv syl5ib expcom a2d faclbnd3 nnnn0 cc nncn exp0 opreq1d
      expcl nn0cn syl2an mulid2 eqtrd sq0 opreq2i 2cn ax-mp eqtri a1i addid1
      mpancom 3brtr4d r19.21aiva ralbidv imbi2d nn0indALT imp 3impb ) CGHZAIHZB
      IHZCAJKZBCJKZLKZMAMJKZJKZBBANKZJKZLKZCOPZLKZQRZXSDSZAJKZBYMJKZLKZYIYMOPZL
      KZQRZDGTZYLXTYAUAYSYLDCGYMCUBZYPYDYRYKQUUAYNYBYOYCLYMCAJUCYMCBJUDUEUUAYQY
      JYILYMCOUFUGUHUIXTYAYTYAYMESZJKZYOLKZMUUBMJKZJKZBBUUBNKZJKZLKZYQLKZQRZDGT
      ZUJYAYMUKJKZYOLKZMUKMJKZJKZBBUKNKZJKZLKZYQLKZQRZDGTZUJYAYMFSZJKZYOLKZMUVC
      MJKZJKZBBUVCNKZJKZLKZYQLKZQRZDGTZUJYAYMUVCULNKZJKZYOLKZMUVNMJKZJKZBBUVNNK
      ZJKZLKZYQLKZQRZDGTZUJYAYTUJEFAUVCIHZYAUVMUWDYAUWEUVMUWDUJYAUWEUAZUUBUVCJK
      ZBUUBJKZLKZUVJUUBOPZLKZQRZEGTZUWDUVMUWFUWMUWCDGUWFYMGHZUAUWMYMULUMKZUVCJK
      ZBUWOJKZLKZUVJUWOOPZLKZQRZUWCUWFUWNYMULQRZUAZUWNULYMUNRZUAZUOZUWMUXAUJZUW
      NUWFUXCUXGUXEUWFUXCUAUXAUWMUWFUWOUKUBZUXAUXCUVCBUWOUPUXCYMULUBZUXHUXCULYM
      QRZUXIUWNUXJUXBYMUQURUWNUXBUXJUXIUWNUXIUXBUXJUAZUWNYMUSHZUXIUXKUTZYMVAZUX
      LULUSHZUXMVBYMULVCVDVEVFVGVHUXIUWOULULUMKUKYMULULUMUCULVIVJVKVEVLVMUXEUXG
      UWFUXEUWOGHZUXGUWNUXDUXPULGHUWNUXDUXPUTVNULYMVOVPVQUWLUXAEUWOGUUBUWOUBZUW
      IUWRUWKUWTQUXQUWGUWPUWHUWQLUUBUWOUVCJUCUUBUWOBJUDUEUXQUWJUWSUVJLUUBUWOOUF
      UGUHVRVEVSVTUWNUWNUXBUXDUOZUAUXFUWNUXRUWNUXLUXRUXNUXLUXOUXRVBYMULWAVDVEWB
      UWNUXBUXDWCWDVLYAUWEUWNUXAUWCUJUVCBYMWEWFWGWHUVLUWLDEGYMUUBUBZUVEUWIUVKUW
      KQUXSUVDUWGYOUWHLYMUUBUVCJUCYMUUBBJUDUEUXSYQUWJUVJLYMUUBOUFUGUHWIWJWKWLYA
      UVADGYAUWNUAZYOBBJKZYQLKZUUNUUTQYAYMIHZYOUYBQRUWNBYMWMYMWNZVLUXTUUNULYOLK
      ZYOUWNUUNUYEUBYAUWNUUMULYOLUWNYMWOHUUMULUBYMWPYMWQVEWRVSUXTYOWOHZUYEYOUBB
      WOHZUYCUYFYAUWNBYMWSBWTZUYDXAYOXBVEXCYAUUTUYBUBUWNYAUUSUYAYQLYAUUSULUYALK
      ZUYAYAUUPULUURUYALUUPULUBYAUUPMUKJKZULUUOUKMJXDXEMWOHUYJULUBXFMWQXGXHXIYA
      UUQBBJYAUYGUUQBUBUYHBXJVEUGUEYAUYAWOHZUYIUYAUBUYGYAUYKUYHBBWSXKUYAXBVEXCW
      RURXLXMUUBUKUBZUULUVBYAUYLUUKUVADGUYLUUDUUNUUJUUTQUYLUUCUUMYOLUUBUKYMJUDW
      RUYLUUIUUSYQLUYLUUFUUPUUHUURLUYLUUEUUOMJUUBUKMJUCUGUYLUUGUUQBJUUBUKBNUDUG
      UEWRUHXNXOUUBUVCUBZUULUVMYAUYMUUKUVLDGUYMUUDUVEUUJUVKQUYMUUCUVDYOLUUBUVCY
      MJUDWRUYMUUIUVJYQLUYMUUFUVGUUHUVILUYMUUEUVFMJUUBUVCMJUCUGUYMUUGUVHBJUUBUV
      CBNUDUGUEWRUHXNXOUUBUVNUBZUULUWDYAUYNUUKUWCDGUYNUUDUVPUUJUWBQUYNUUCUVOYOL
      UUBUVNYMJUDWRUYNUUIUWAYQLUYNUUFUVRUUHUVTLUYNUUEUVQMJUUBUVNMJUCUGUYNUUGUVS
      BJUUBUVNBNUDUGUEWRUHXNXOUUBAUBZUULYTYAUYOUUKYSDGUYOUUDYPUUJYRQUYOUUCYNYOL
      UUBAYMJUDWRUYOUUIYIYQLUYOUUFYFUUHYHLUYOUUEYEMJUUBAMJUCUGUYOUUGYGBJUUBABNU
      DUGUEWRUHXNXOXPXQVLXR $.
      $( [28-Jan-2006] $) $( [19-Dec-2005] $)
  $}

  $( Variant of ~ faclbnd5 providing a non-strict lower bound. $)
  faclbnd4 $p |- ( ( N e. NN0 /\ K e. NN0 /\ M e. NN0 ) ->
                 ( ( N ^ K ) x. ( M ^ N ) ) <_
            ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cexp co cmul c2 caddc cfa cfv cle wbr wa cn cc0 wceq wo
    faclbnd4lem4 3com13 3expa faclbnd4lem3 jaodan elnn0 sylan2b 3impa ) BDEZADE
    ZCDEZCAFGBCFGHGIAIFGFGBBAJGFGHGCKLHGMNZUHUIUJUKUHUIOZCPEZCQRZSUKUJULUMUKUNU
    HUIUMUKUMUIUHUKABCTUAUBABCUCUDCUEUFUGUA $.
    $( [28-Jan-2006] $) $( [23-Dec-2005] $)

  $( The factorial function grows faster than powers and exponentiations.  If
     we consider ` K ` and ` M ` to be constants, the right-hand side of the
     inequality is a constant times ` N ` -factorial. $)
  faclbnd5 $p |- ( ( N e. NN0 /\ K e. NN0 /\ M e. NN ) ->
                 ( ( N ^ K ) x. ( M ^ N ) ) <
      ( ( 2 x. ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cn cexp co cmul c2 caddc cfa cfv clt wbr wa cr remulcl reexpcl
    nn0re sylan ancoms nnre syl2an anandirs nnmulcl 2nn0 nn0expcl mpan2 2nn
    nnexpcl mpan syl nn0addcl nnnn0 sylan2 anabss7 anabss5 faccl 2re cle
    faclbnd4 syl3an3 3coml 3expa c1 1lt2 cc0 wb ltmulgt12 mp3an2 nngt0 sylanc
    mpbii lelttrd cc wceq 2cn mulass mp3an1 recnd breqtrrd 3impa 3comr ) ADEZBF
    EZCDEZCAGHZBCGHZIHZJJAJGHZGHZBBAKHZGHZIHZIHCLMZIHZNOZWEWFWGWRWEWFPZWGPZWJJW
    OWPIHZIHZWQNWTWJXAXBWEWFWGWJQEZWHQEZWIQEZXCWEWGPWFWGPWHWIRWGWEXDCQEWEXDWGCA
    SCTUAUBBQEWGXEWFBCSBUCUAUDUEWOQEZWPQEZXAQEZWSWGWOWPRWSWOFEZXFWEWFXIWLFEZWNF
    EZXIWEWSWLWNUFWEWKDEZXJWEJDEXLUGAJUHUIJFEXLXJUJJWKUKULUMWEWFXKWFWMDEZXKWSBW
    MUKWEBDEZXMWFXNWEXMBAUNUBBUOZUPUPUQUDURZWOUCUMZWGWPFEZXGCUSZWPUCUMZUDZWTXHX
    BQEZYAJQEZXHYBUTJXARULUMWEWFWGWJXAVAOZWGWEWFYDWGWEXNYDWFABCVBXOVCVDVEWTVFJN
    OZXAXBNOZVGXHVHXANOZYEYFVIZWTXHYCYGYHUTXAJVJVKYAWTXAFEZYGXIXRYIWSWGWOWPUFXP
    XSUDXAVLUMVMVNVOWOVPEZWPVPEZWQXBVQZWSWGJVPEYJYKYLVRJWOWPVSVTWSWOXQWAWGWPXTW
    AUDWBWCWD $.
    $( [16-Feb-2006] $) $( [24-Dec-2005] $)

  ${
    $d M m $.  $d N k m $.
    $( Geometric lower bound for the factorial function, where N is usually
       held constant.  (Contributed by Paul Chapman, 28-Dec-2007.) $)
    faclbnd6 $p |- ( ( N e. NN0 /\ M e. NN0 ) ->
             ( ( ! ` N ) x. ( ( N + 1 ) ^ M ) ) <_ ( ! ` ( N + M ) ) ) $=
      ( vm vk cn0 wcel cfa cfv c1 caddc co cexp cmul cle wbr cv wi cc0 wceq
      opreq2 opreq2d fveq2d breq12d imbi2d cn cr faccl nnre leid 3syl cc nn0cn
      peano2cn syl exp0 nncn ax1id eqtrd addid1 3brtr4d wa lemul12aOLD remulcl
      adantr reexpcl nn0re peano2re sylan sylanc nn0addcl jca nn0mulcl nnnn0
      nn0expcl peano2nn0 nn0ge0 anim1i ax1cn addass mp3an3 syl2an eqeltrrd
      adantl wb addge02 mpbid add12 breqtrrd expp1 mulass expcl syl3anc eqtr4d
      facp1 3eqtr3d ex expcom a2d nn0ind impcom ) AEFBEFZBGHZBIJKZALKZMKZBAJKZG
      HZNOZXAXBXCCPZLKZMKZBXIJKZGHZNOZQXAXBXCRLKZMKZBRJKZGHZNOZQXAXBXCDPZLKZMKZ
      BXTJKZGHZNOZQXAXBXCXTIJKZLKZMKZBYFJKZGHZNOZQXAXHQCDAXIRSZXNXSXAYLXKXPXMXR
      NYLXJXOXBMXIRXCLTUAYLXLXQGXIRBJTUBUCUDXIXTSZXNYEXAYMXKYBXMYDNYMXJYAXBMXIX
      TXCLTUAYMXLYCGXIXTBJTUBUCUDXIYFSZXNYKXAYNXKYHXMYJNYNXJYGXBMXIYFXCLTUAYNXL
      YIGXIYFBJTUBUCUDXIASZXNXHXAYOXKXEXMXGNYOXJXDXBMXIAXCLTUAYOXLXFGXIABJTUBUC
      UDXAXBXBXPXRNXAXBUEFZXBUFFZXBXBNOBUGZXBUHZXBUIUJXAXPXBIMKZXBXAXOIXBMXAXCU
      KFZXOISXABUKFZUUABULZBUMUNZXCUOUNUAXAXBUKFZYTXBSXAYPUUEYRXBUPUNZXBUQUNURX
      AXQBGXAUUBXQBSUUCBUSUNUBUTXTEFZXAYEYKXAUUGYEYKQXAUUGVAZYEYKUUHYEVAZYBXCMK
      ZYDYIMKZYHYJNYBUFFZYDUFFZVAZRYBNOZYEVAZVAXCUFFZYIUFFZVAZRXCNOZXCYINOZVAZV
      AZUUJUUKNOUUIYBYDXCYIVBUUIUUNUUPUUHUUNYEUUHUULUUMYQYAUFFZUULUUHXBYAVCXAYQ
      UUGXAYPYQYRYSUNVDUUQUUGUVDXAXCXTVEXABUFFUUQBVFBVGUNZVHVIUUHYCEFZYDUEFUUMB
      XTVJZYCUGYDUHUJVKVDUUHUUOYEUUHYBEFZUUOXBEFZYAEFZUVHUUHXBYAVLXAUVIUUGXAYPU
      VIYRXBVMUNVDXCEFZUUGUVJXAXCXTVNBVOZVHVIYBVPUNVQVKUUHUVCYEUUHUUSUVBUUHUUQU
      URXAUUQUUGUVEVDUUHYCIJKZYIUFUUBXTUKFZUVMYISZXAUUGUUBUVNIUKFZUVOVRBXTIVSVT
      UUCXTULZWAZUUHUVFUVMEFUVMUFFUVGYCVOUVMVFUJWBVKUUHUUTUVAXAUUTUUGXAUVKUUTUV
      LXCVPUNVDUUHXCXTXCJKZYINUUHRXTNOZXCUVSNOZUUGUVTXAXTVPWCUUQXTUFFUVTUWAWDXA
      UUGXCXTWEUVEXTVFWAWFUUBUVNYIUVSSZXAUUGUUBUVNUVPUWBVRBXTIWGVTUUCUVQWAWHVKV
      KVDVIUUHYHUUJSYEUUHYHXBYAXCMKZMKZUUJUUHYGUWCXBMUUAUUGYGUWCSXAXCXTWIUUDVHU
      AUUEYAUKFZUUAUUJUWDSUUHXBYAXCWJXAUUEUUGUUFVDUUAUUGUWEXAXCXTWKUUDVHXAUUAUU
      GUUDVDWLWMVDUUHYJUUKSYEUUHUVMGHZYDUVMMKZYJUUKUUHUVFUWFUWGSUVGYCWNUNUUHUVM
      YIGUVRUBUUHUVMYIYDMUVRUAWOVDUTWPWQWRWSWT $.
      $( [29-Dec-2007] $) $( [28-Dec-2007] $)
  $}

  $( The product of two factorials is greater than or equal to the factorial of
     (the floor of) their average. $)
  facavg $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
       ( ! ` ( |_ ` ( ( M + N ) / 2 ) ) ) <_ ( ( ! ` M ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel wa caddc co c2 cdiv cle wbr wo cfl cfv cfa cmul cr avgle nn0re
    syl2an nn0addcl syl rehalfcl flle 3syl wi letr reflcl adantr syl3anc mpand
    facwordi 3exp cc0 flge0nn0 nn0ge0 wb halfnneg2 mpbid sylanc pm3.26 sylc
    syld c1 wceq cn cc faccl nncn ax1id 1re lemul2aOLD mp3anl1 nnre anim12i
    ancoms nnnn0 nnge1 eqbrtrrd remulcl mpan2d adantl pm3.27 mulid2 lemul1aOLD
    jaod mpd ) ACDZBCDZEZABFGZHIGZAJKZWLBJKZLZWLMNZONZAONZBONZPGZJKZAQDZBQDZWOW
    HWIABRASZBSZTWJWMXAWNWJWMWQWRJKZXAWJWMWPAJKZXFWJWPWLJKZWMXGWJWKQDZWLQDZXHWJ
    WKCDZXIABUAZWKSZUBZWKUCZWLUDUEZWPQDZXJXBXHWMEXGUFWJWPWLAUGWJXIXJXQXNXOWLUHU
    EZWJXKXIXJXLXMXOUEZWHXBWIXDUIUJUKWPCDZWHXGXFUFWJXTWHXGXFWPAULUMXJUNWLJKZXTW
    JWLUOXSWJUNWKJKZYAWJXKYBXLWKUPUBWJXKXIYBYAUQXLXMWKURUEUSUTZWHWIVAVBVCWJXFWR
    WTJKZXAWJWRVDPGZWRWTJWHYEWRVEZWIWHWRVFDZWRVGDYFAVHZWRVIWRVJUEUIWSQDZWRQDZEZ
    UNWRJKZVDWSJKZEZYEWTJKZWJVDQDZYIYJYNYOVKVDWSWRVLVMWIWHYKWIYIWHYJWIWSVFDZYIB
    VHZWSVNUBZWHYGYJYHWRVNUBZVOVPWHYLWIYMWHYGWRCDYLYHWRVQWRUPUEWIYQYMYRWSVRUBVO
    UTVSWQQDZYJWTQDZXFYDEXAUFWJWQWRWTUGWJXTWQVFDUUAYCWPVHWQVNUEZWHYJWIYTUIYJYIU
    UBWHWIWRWSVTYTYSTZUJWAVCWJWNWQWSJKZXAWJWNWPBJKZUUEWJXHWNUUFXPXQXJXCXHWNEUUF
    UFWJWPWLBUGXRXSWIXCWHXEWBUJUKXTWIUUFUUEUFWJXTWIUUFUUEWPBULUMYCWHWIWCVBVCWJU
    UEWSWTJKZXAWJVDWSPGZWSWTJWIUUHWSVEZWHWIYQWSVGDUUIYRWSVIWSWDUEWBYJYIEUNWSJKZ
    VDWRJKZEZUUHWTJKZWJYPYJYIUULUUMVKVDWRWSWEVMWHYJWIYIYTYSVOWIWHUULWIUUJWHUUKW
    IYQWSCDUUJYRWSVQWSUPUEWHYGUUKYHWRVRUBVOVPUTVSUUAYIUUBUUEUUGEXAUFWJWQWSWTUGU
    UCWIYIWHYSWBUUDUJWAVCWFWG $.
    $( [6-Feb-2006] $) $( [9-Dec-2005] $)

