$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Factorial function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ! $. $( Factorial. $)

  $( Extend class notation to include the factorial of nonnegative integers. $)
  cfa $a class ! $.

  ${
    $d x y $.
    $( Define the factorial function on nonnegative integers.  In the
       literature, the factorial function is written as a postscript
       exclamation point. $)
    df-fac $a |- ! = ( ( x. seq1 ( I |` NN ) ) u. { <. 0 , 1 >. } ) $.
  $}

  $( Value of the factorial function for positive integers. $)
  facnnt $p |- ( N e. NN -> ( ! ` N ) = ( ( x. seq1 ( I |` NN ) ) ` N ) ) $=
    ( cn wcel cfa cres cfv cmul cid cseq1 co wceq cc0 c1 cop csn cun c0 df-fac
    reseq1 ax-mp resundir cin incom wn 0nnn disjsn mpbir eqtr wfn wb wf1o cn0
    0nn0 elisseti 1nn f1osn f1ofn fnresdisj mpbi uneq2i un0 3eqtr fveq1i a1i
    fvres 3eqtr3d ) ABCZADBEZFZAGHBEIJZBEZFZADFAVJFVIVLKVGAVHVKVHVJLMNOZPZBEZVK
    QPZVKDVNKVHVOKRDVNBSTVOVKVMBEZPVPVJVMBUAVQQVKLOZBUBZQKZVQQKZVSBVRUBZQVRBUCW
    BQKLBCUDUEBLUFUGUHVMVRUIZVTWAUJVRMOZVMUKWCLMLULUMUNMBUOUNUPVRWDVMUQTVRBVMUR
    TUSUTUHVKVAVBVCVDABDVEABVJVEVF $.
    $( [3-Dec-2004] $) $( [2-Dec-2004] $)

  $( The factorial of 0. $)
  fac0 $p |- ( ! ` 0 ) = 1 $=
    ( cc0 cfa csn cres cfv c1 cop cmul cid cn cseq1 co cun wceq df-fac reseq1
    ax-mp resundir c0 cin wcel wn 0nnn disjsn mpbir wfn wb mulex wfun cvv funi
    nnex resfunexg mp2an seq1fn fnresdisj mpbi wrel cdm wss cn0 0nn0 elisseti
    relsn dmsnop eqimssi relssres uneq12i uncom un0 3eqtr fveq1i snid fvres
    1nn0 fvsn 3eqtr3 ) ABACZDZEZAAFGCZEABEZFAVSWAVSHIJDZKLZWAMZVRDZWDVRDZWAVRDZ
    MZWABWENVSWFNOBWEVRPQWDWAVRRWISWAMWASMWAWGSWHWAJVRTSNZWGSNZWJAJUAUBUCJAUDUE
    WDJUFWJWKUGHWCUHIUIJUJUAWCUJUAUKULIJUJUMUNUOJVRWDUPQUQWAURWAUSZVRUTWHWANAFA
    VAVBVCZVDWLVRAFVEVFWAVRVGUNVHSWAVIWAVJVKVKVLAVRUAVTWBNAWMVMAVRBVNQAFWMFVAVO
    VCVPVQ $.
    $( [3-Dec-2004] $) $( [2-Dec-2004] $)

  $( The factorial of 1. $)
  fac1 $p |- ( ! ` 1 ) = 1 $=
    ( c1 cfa cfv cmul cid cn cres cseq1 co wcel wceq 1nn facnnt ax-mp mulex
    wfun cvv funi nnex resfunexg mp2an seq11 fvres fvi eqtr 3eqtr ) ABCZADEFGZH
    ICZAUHCZAAFJZUGUIKLAMNDUHOEPFQJUHQJRSEFQTUAUBUJAECZAUKUJULKLAFEUCNUKULAKLAF
    UDNUEUF $.
    $( [3-Dec-2004] $) $( [2-Dec-2004] $)

  $( The factorial of a successor. $)
  facp1t $p |- ( N e. NN0 ->
               ( ! ` ( N + 1 ) ) = ( ( ! ` N ) x. ( N + 1 ) ) ) $=
    ( cn0 wcel cn cc0 wceq wo c1 caddc co cfa cfv cmul elnn0 cid cres cseq1
    peano2nn facnnt syl mulex wfun cvv funi nnex resfunexg mp2an seq1p1 fvres
    oprex fvi ax-mp syl6eq opreq2d opreq1d eqtr4d 3eqtrd ax1cn addid2 fveq2i
    fac1 eqtr opreq1 fveq2d fveq2 opreq12d fac0 opreq12i mulid1 3eqtr4a jaoi
    sylbi ) ABCADCZAEFZGAHIJZKLZAKLZVOMJZFZANVMVSVNVMVPVOMODPZQJZLZAWALZVOVTLZM
    JZVRVMVODCZVPWBFARZVOSTAMVTUAOUBDUCCVTUCCUDUEODUCUFUGUHVMWEWCVOMJVRVMWDVOWC
    MVMWDVOOLZVOVMWFWDWHFWGVODOUITVOUCCWHVOFAHIUJVOUCUKULUMUNVMVQWCVOMASUOUPUQV
    NEHIJZKLZHVPVRWJHKLHWIHKHURUSZUTVAVBVNVOWIKAEHIVCZVDVNVREKLZWIMJZHVNVQWMVOW
    IMAEKVEWLVFWNHHMJHWMHWIHMVGWKVHHURVIVBUMVJVKVL $.
    $( [3-Dec-2004] $) $( [2-Dec-2004] $)

  $( The factorial of 2. $)
  fac2 $p |- ( ! ` 2 ) = 2 $=
    ( c2 cfa cfv c1 caddc co cmul df-2 fveq2i cn0 wcel wceq 1nn0 facp1t ax-mp
    fac1 eqcomi opreq12i 2cn mulid2 eqtr 3eqtr ) ABCDDEFZBCZDBCZUCGFZAAUCBHIDJK
    UDUFLMDNOUFDAGFAUEDUCAGPAUCHQRASTUAUB $.
    $( [7-Jul-2005] $) $( [17-Mar-2005] $)

  $( The factorial of 3. $)
  fac3 $p |- ( ! ` 3 ) = 6 $=
    ( c3 cfa cfv c2 c1 caddc co cmul c6 df-3 fveq2i cn0 wcel wceq 2nn0 facp1t
    ax-mp fac2 eqcomi opreq12i 2cn 3re recn mulcom 3t2e6 3eqtr ) ABCDEFGZBCZDBC
    ZUGHGZIAUGBJKDLMUHUJNODPQUJDAHGADHGIUIDUGAHRAUGJSTDAUAAUBUCUDUEUFUF $.
    $( [19-Jul-2005] $) $( [17-Mar-2005] $)

  $( Value of the factorial function expressed recursively. $)
  facnn2t $p |- ( N e. NN ->
               ( ! ` N ) = ( ( ! ` ( N - 1 ) ) x. N ) ) $=
    ( cn wcel cc c1 cmin co cn0 wa cfa cfv cmul wceq elnnnn0 caddc facp1t
    adantl ax1cn npcant mpan2 fveq2d adantr opreq2d 3eqtr3d sylbi ) ABCADCZAEFG
    ZHCZIZAJKZUGJKZALGZMANUIUGEOGZJKZUKUMLGZUJULUHUNUOMUFUGPQUFUNUJMUHUFUMAJUFE
    DCUMAMRAESTZUAUBUFUOULMUHUFUMAUKLUPUCUBUDUE $.
    $( [5-Feb-2005] $) $( [2-Dec-2004] $)

  ${
    $d j k N $.
    $( Closure of the factorial function. $)
    facclt $p |- ( N e. NN0 -> ( ! ` N ) e. NN ) $=
      ( vj vk cv cfa cfv cn wcel cc0 c1 caddc co wceq fveq2 eleq1d fac0 1nn
      eqeltr cn0 wa cmul facp1t adantl nnmulclt nn0p1nnt sylan2 eqeltrd expcom
      nn0ind ) BDZEFZGHIEFZGHCDZEFZGHZUMJKLZEFZGHZAEFZGHBCAUJIMUKULGUJIENOUJUMM
      UKUNGUJUMENOUJUPMUKUQGUJUPENOUJAMUKUSGUJAENOULJGPQRUOUMSHZURUOUTTUQUNUPUA
      LZGUTUQVAMUOUMUBUCUOUPGHVAGHUTUNUPUDUMUEUFUGUHUI $.
      $( [29-Dec-2004] $) $( [2-Dec-2004] $)
  $}

  $( The factorial function is nonzero. $)
  facne0t $p |- ( N e. NN0 -> ( ! ` N ) =/= 0 ) $=
    ( cn0 wcel cfa cfv cn cc0 wne facclt nnne0t syl ) ABCADEZFCLGHAILJK $.
    $( [3-May-2005] $) $( [26-Apr-2005] $)

  ${
    $d j M $.  $d j k N $.
    $( A natural number divides the factorial of an equal or larger number. $)
    facdivt $p |- ( ( M e. NN0 /\ N e. NN /\ N <_ M ) ->
                  ( ( ! ` M ) / N ) e. NN ) $=
      ( vj vk cn0 wcel cn cle wbr cfa cfv cdiv co cv wi cc0 c1 caddc wceq
      breq2 fveq2 opreq1d eleq1d imbi12d imbi2d clt wn nngt0t cr wb nnret 0re
      ltnlet mpan syl mpbid pm2.21d wa cmul wo leloet peano2nn0 nn0ret syl2an
      nn0leltp1t nnnn0t sylan nnmulclt nn0p1nnt sylan2 expcom adantl cc w3a
      wne div23t facclt nncnt recnd adantr 3jca nnne0t sylanc sylibrd imim2d
      com23 sylbird opreq2 divcan4t syl3anc eqeltrd syl5bi com12 a1dd jaod
      sylbid ex com34 imp4d facp1t exp4d a2d nn0ind 3imp ) AEFBGFZBAHIZAJKZBLMZ
      GFZXEBCNZHIZXJJKZBLMZGFZOZOXEBPHIZPJKZBLMZGFZOZOXEBDNZHIZYAJKZBLMZGFZOZOX
      EBYAQRMZHIZYGJKZBLMZGFZOZOXEXFXIOZOCDAXJPSZXOXTXEYNXKXPXNXSXJPBHTYNXMXRGY
      NXLXQBLXJPJUAUBUCUDUEXJYASZXOYFXEYOXKYBXNYEXJYABHTYOXMYDGYOXLYCBLXJYAJUAU
      BUCUDUEXJYGSZXOYLXEYPXKYHXNYKXJYGBHTYPXMYJGYPXLYIBLXJYGJUAUBUCUDUEXJASZXO
      YMXEYQXKXFXNXIXJABHTYQXMXHGYQXLXGBLXJAJUAUBUCUDUEXEXPXSXEPBUFIZXPUGZBUHXE
      BUIFZYRYSUJZBUKZPUIFYTUUAULPBUMUNUOUPUQYAEFZXEYFYLUUCXEYFYHYKUUCXEYFYHURU
      RYCYGUSMZBLMZGFZYKUUCXEYFYHUUFXEUUCYFYHUUFOOXEUUCYHYFUUFXEUUCYHYFUUFOZOXE
      UUCURZYHBYGUFIZBYGSZUTZUUGYTYGUIFZYHUUKUJXEUUCBYGVAUUBUUCYGEFUULYAVBYGVCU
      OZVDUUHUUIUUGUUJUUHUUIYBUUGBEFUUCYBUUIUJXEBYAVEBVFVGUUHYFYBUUFUUHYEUUFYBU
      UHYEYDYGUSMZGFZUUFUUCYEUUOOXEYEUUCUUOYEYGGFUUOUUCYDYGVHYAVIVJVKVLUUHUUEUU
      NGYCVMFZYGVMFZBVMFZVNBPVOZUUEUUNSUUHYCYGBVPUUHUUPUUQUURUUCUUPXEUUCYCGFZUU
      PYAVQZYCVRUOVLZUUCUUQXEUUCYGUUMVSVLXEUURUUCBVRVTZWAXEUUSUUCBWBVTZWCUCWDWE
      WFWGUUHUUJUUFYFUUJUUHUUFUUJYCBUSMZBLMZGFUUFUUHUUJUVFUUEGUUJUVEUUDBLBYGYCU
      SWHUBUCUUHUVFYCGUURUUPUUSUVFYCSUUHBYCWIUVCUVBUVDWJUUCUUTXEUVAVLWKWLWMWNWO
      WPWQWRWMWSUUCYJUUEGUUCYIUUDBLYAWTUBUCWDXAXBXCXD $.
      $( [6-May-2005] $) $( [2-May-2005] $)
  $}

  $( No natural number (greater than one) divides the factorial plus one of an
     equal or larger number. $)
  facndivt $p |- ( ( ( M e. NN0 /\ N e. NN ) /\ ( 1 < N /\ N <_ M ) ) ->
                -. ( ( ( ! ` M ) + 1 ) / N ) e. ZZ ) $=
    ( cn0 wcel cn wa c1 clt wbr cle cfa cfv caddc co cdiv cz wn cr recnzt
    nnret sylan adantrr adantll cmin zsubclt ex facdivt 3expa nnzt syl adantrl
    syl5com cc w3a cc0 wne wceq divsubdirt facclt nncnt peano2cn ad2antrr
    ad2antlr 3jca nnne0t sylanc ax1cn pncan2t mpan2 opreq1d eqtr3d eleq1d
    sylibd mtod ) ACDZBEDZFZGBHIZBAJIZFZFZAKLZGMNZBONZPDZGBONZPDZVPVTWGQZVOVPVR
    WHVSBRDVRWHVPBSBTUAUBUCWAWEWDWBBONZUDNZPDZWGWEWIPDZWKWAWEWLWKWDWIUEUFVQVSWL
    VRVQVSFWIEDZWLVOVPVSWMABUGUHWIUIUJUKULWAWJWFPWAWCWBUDNZBONZWJWFWCUMDZWBUMDZ
    BUMDZUNBUOUPZWOWJUQWAWCWBBURWAWPWQWRVOWPVPVTVOWQWPVOWBEDWQAUSWBUTUJZWBVAUJV
    BVOWQVPVTWTVBVPWRVOVTBUTVCVDVPWSVOVTBVEVCVFVOWOWFUQVPVTVOWNGBOVOWQWNGUQZWTW
    QGUMDXAVGWBGVHVIUJVJVBVKVLVMVN $.
    $( [8-May-2005] $) $( [3-May-2005] $)

  ${
    $d j k M $.  $d j N $.
    $( Ordering property of factorial. $)
    facwordit $p |- ( ( M e. NN0 /\ N e. NN0 /\ M <_ N ) ->
                  ( ! ` M ) <_ ( ! ` N ) ) $=
      ( vj vk cn0 wcel cle wbr cfa cfv cv wa wi cc0 c1 caddc co wceq breq2
      anbi2d fveq2 breq2d imbi12d nn0le0eq0t biimpa fveq2d cr fac0 1re eqeltr
      leid syl6eqbr clt wo wb leloet nn0ret peano2re syl syl2an nn0leltp1t
      cmul cn cc facclt nncnt ax1id 3syl lemul2it mp3anl1 nnret jca nnnn0t
      nn0ge0t 0re leadd1t mp3an13 mpbid ax1cn addid2 syl5eqbrr sylanc eqbrtrrd
      facp1t breqtrrd adantl letrt adantr peano2nn0 syl3anc mpan2d imim2d
      com23 sylbird leidt biimpcd syl5 a1dd jaod sylbid ex com13 com4l a2d
      imp4a impexp syl5ib nn0ind 3impib 3com12 ) BEFZAEFZABGHZAIJZBIJZGHZXKXLXM
      XPXLACKZGHZLZXNXQIJZGHZMXLANGHZLZXNNIJZGHZMXLADKZGHZLZXNYFIJZGHZMZXLAYFOP
      QZGHZLZXNYLIJZGHZMZXLXMLZXPMCDBXQNRZXSYCYAYEYSXRYBXLXQNAGSTYSXTYDXNGXQNIU
      AUBUCXQYFRZXSYHYAYJYTXRYGXLXQYFAGSTYTXTYIXNGXQYFIUAUBUCXQYLRZXSYNYAYPUUAX
      RYMXLXQYLAGSTUUAXTYOXNGXQYLIUAUBUCXQBRZXSYRYAXPUUBXRXMXLXQBAGSTUUBXTXOXNG
      XQBIUAUBUCYCXNYDYDGYCANIXLYBANRAUDUEUFYDYDOUGUHUIUJUKULYFEFZXLYGYJMZMZYQY
      KUUCUUEXLYMYPUUCXLUUDYMYPMYMUUCXLUUDYPXLUUCYMUUDYPMZXLUUCYMUUFMXLUUCLZYMA
      YLUMHZAYLRZUNZUUFAUGFYLUGFZYMUUJUOXLUUCAYLUPAUQUUCYFUGFZUUKYFUQZYFURUSZUT
      UUGUUHUUFUUIUUGUUHYGUUFAYFVAUUGUUDYGYPUUGYJYPYGUUGYJYIYOGHZYPUUCUUOXLUUCY
      IYIYLVBQZYOGUUCYIOVBQZYIUUPGUUCYIVCFZYIVDFUUQYIRYFVEZYIVFYIVGVHUUKYIUGFZL
      NYIGHZOYLGHZLZUUQUUPGHZUUCOUGFZUUKUUTUVCUVDUIOYLYIVIVJUUCUUKUUTUUNUUCUURU
      UTUUSYIVKUSZVLUUCUVAUVBUUCUURYIEFUVAUUSYIVMYIVNVHUUCNOPQZYLOGUUCNYFGHZUVG
      YLGHZYFVNUUCUULUVHUVIUOZUUMNUGFUULUVEUVJVOUINYFOVPVQUSVROVSVTWAVLWBWCYFWD
      WEWFXNUGFZUUTYOUGFZYJUUOLYPMUUGXNYIYOWGXLUVKUUCXLXNVCFUVKAVEXNVKUSZWHUUCU
      UTXLUVFWFUUCUVLXLUUCYLEFYOVCFUVLYFWIYLVEYOVKVHWFWJWKWLWMWNUUGUUIYPUUDXLUU
      IYPMUUCXLXNYORZYPUUIXLUVKXNXNGHZUVNYPMUVMXNWOUVNUVOYPXNYOXNGSWPVHAYLIUAWQ
      WHWRWSWTXAXBXCXDXEXLYGYJXFXGXHXIXJ $.
      $( [30-Dec-2005] $) $( [9-Dec-2005] $)

    $( A lower bound for the factorial function. $)
    faclbnd $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                   ( M ^ ( N + 1 ) ) <_ ( ( M ^ M ) x. ( ! ` N ) ) ) $=
      ( vj vk cn wcel cc0 wceq wo cn0 c1 caddc co cexp cfa cfv cmul cle wbr cv
      wi opreq1 opreq2d fveq2 breq12d imbi2d cr w3a wa expwordit nnret 1nn0
      a1i nnnn0t 3jca nnge1t jca sylanc ax1cn addid2 opreq2i cc reexpclt recnd
      ax1id syl fac0 syl5eq 3brtr4d clt lelttrit nn0ret peano2re syl2an
      lemul12it peano2nn0 adantr axmulrcl facclt expge0t adantl nn0ge0t
      syl3anc anim1i ad2antrr ad2antlr anandis expp1t nncnt facp1t axmulass
      nn0cnt peano2cn eqtr4d exp32 com23 wb nn0ltp1let ancoms 3syl lemulge11t
      anim12i id letrd ex sylbid a1dd jaod mpd expcom a2d nn0ind impcom
      opreq12 anidms opreq1d nn0p1nnt 0expt mulid2t 0cn exp0t ax-mp opreq1i
      syl5bir imp jaoian elnn0 sylanb ) AEFZAGHZIBJFZABKLMZNMZAANMZBOPZQMZRSZAJ
      FZYIYKYQYJYKYIYQYIACTZKLMZNMZYNYSOPZQMZRSZUAYIAGKLMZNMZYNGOPZQMZRSZUAYIAD
      TZKLMZNMZYNUUJOPZQMZRSZUAYIAUUKKLMZNMZYNUUKOPZQMZRSZUAYIYQUACDBYSGHZUUDUU
      IYIUVAUUAUUFUUCUUHRUVAYTUUEANYSGKLUBUCUVAUUBUUGYNQYSGOUDUCUEUFYSUUJHZUUDU
      UOYIUVBUUAUULUUCUUNRUVBYTUUKANYSUUJKLUBUCUVBUUBUUMYNQYSUUJOUDUCUEUFYSUUKH
      ZUUDUUTYIUVCUUAUUQUUCUUSRUVCYTUUPANYSUUKKLUBUCUVCUUBUURYNQYSUUKOUDUCUEUFY
      SBHZUUDYQYIUVDUUAYMUUCYPRUVDYTYLANYSBKLUBUCUVDUUBYOYNQYSBOUDUCUEUFYIAKNMZ
      YNUUFUUHRAUGFZKJFZYRUHKARSZUVHUIUVEYNRSYIAKAUJYIUVFUVGYRAUKZUVGYIULUMAUNZ
      UOYIUVHUVHAUPZUVKUQURUUFUVEHYIUUEKANKUSUTVAUMYIYNKQMZYNUUHYIYNVBFZUVLYNHY
      IYNUVFYRYNUGFZYIAAVCUVIUVJURZVDZYNVEVFUUGKYNQVGVAVHVIUUJJFZYIUUOUUTYIUVQU
      UOUUTUAZYIUVQUIZAUUKRSZUUKAVJSZIZUVRUVFUUKUGFZUWBYIUVQAUUKVKUVIUVQUUJUGFU
      WCUUJVLUUJVMVFZVNUVSUVTUVRUWAUVSUUOUVTUUTUVSUUOUVTUUTUVSUUOUVTUIZUIUULAQM
      ZUUNUUKQMZUUQUUSRUVSUUOUVTUWFUWGRSZUULUGFZUUNUGFZUIZGUULRSZUUOUIZUIUVFUWC
      UIZGARSZUVTUIZUIUWHUVSUUOUIZUVSUVTUIZUULUUNAUUKVOUWQUWKUWMUWQUWIUWJUVSUWI
      UUOUVFUUKJFZUWIYIUVQAUUKVCUVIUUJVPZVNVQUVSUWJUUOUVNUUMUGFZUWJYIUVQYNUUMVR
      UVOUVQUUMEFUXAUUJVSUUMUKVFZVNVQUQUVSUWLUUOUVFUWSUWOUWLUVSAUUKVTYIUVFUVQUV
      IVQUVQUWSYIUWTWAYIUWOUVQYIYRUWOUVJAWBZVFVQZWCWDUQUWRUWNUWPUWRUVFUWCYIUVFU
      VQUVTUVIWEUVQUWCYIUVTUWDWFUQUVSUWOUVTUXDWDUQVNWGUVSUUQUWFHZUWEAVBFUWSUXEY
      IUVQAUUKWHAWIUWTVNVQUVSUUSUWGHUWEUVSUUSYNUUMUUKQMZQMZUWGUVSUURUXFYNQUVQUU
      RUXFHYIUUJWJWAUCUVMUUMVBFZUUKVBFZUWGUXGHUVSYNUUMUUKWKYIUVMUVQUVPVQUVQUXHY
      IUVQUUMUXBVDWAUVQUXIYIUVQUUJVBFUXIUUJWLUUJWMVFWAWCWNVQVIWOWPUVSUWAUUTUUOU
      VSUWAUUPARSZUUTUVQYIUWAUXJWQZUWSYRUXKUVQYIUUKAWRUWTUVJVNWSUVSUXJUUTUVSUXJ
      UIZUUQYNUUSUVSUUQUGFZUXJUVFUUPJFZUXMYIUVQAUUPVCUVIUVQUWSUXNUWTUUKVPVFZVNV
      QYIUVNUVQUXJUVOWEUVSUUSUGFZUXJUVNUURUGFZUXPYIUVQYNUURVRUVOUVQUWSUUREFZUXQ
      UWTUUKVSZUURUKWTZVNVQUVFUXNYRUHUVHUXJUIUUQYNRSUXLAUUPAUJUXLUVFUXNYRYIUVFU
      VQUXJUVIWEUVQUXNYIUXJUXOWFYIYRUVQUXJUVJWEUOUVSUVHUXJYIUVHUVQUVKVQWDURUVSY
      NUUSRSZUXJUVNUXQUIGYNRSZKUURRSZUIUYAUVSYNUURXAYIUVNUVQUXQUVOUXTXBYIUYBUVQ
      UYCYIYRUYBUVJUVFYRUWOUYBYRAAVTAVLYRXCUXCWCVFUVQUWSUXRUYCUWTUXSUURUPWTXBUR
      VQXDXEXFXGXHXIXJXKXLXMYJYKYQYJYQGYLNMZGGNMZYOQMZRSYKYJYMUYDYPUYFRAGYLNUBY
      JYNUYEYOQYJYNUYEHAGAGNXNXOXPUEYKGYOUYDUYFRYKYOEFZYOJFGYORSBVSZYOUNYOWBWTY
      KYLEFUYDGHBXQYLXRVFYKKYOQMZYOUYFYKUYGYOVBFUYIYOHUYHYOWIYOXSWTUYEKYOQGVBFU
      YEKHXTGYAYBYCVHVIYDYEYFAYGYH $.
      $( [18-Dec-2005] $) $( [17-Dec-2005] $)
  $}

  $( A lower bound for the factorial function. $)
  faclbnd2 $p |- ( N e. NN0 -> ( ( 2 ^ N ) / 2 ) <_ ( ! ` N ) ) $=
    ( cn0 wcel c2 cexp co cdiv c1 caddc cfa cfv cle cmul cc wceq 2cn expp1t
    mpan opreq1d c4 sq2 2t2e4 eqtr4 opreq2i syl5eq expclt wa pm3.2i cc0 wne
    2re 2pos gt0ne0i divmuldivt mpan2 syl halfclt ax1id 3syl divid 3eqtr2rd
    wbr 2nn0 faclbnd cr wb 4re eqeltr w3a clt 4pos breqtrr ledivmult mp3an2
    peano2nn0 reexpclt cn facclt nnret sylanc mpbird eqbrtrd ) ABCZDAEFZDGFZDAH
    IFZEFZDDEFZGFZAJKZLWCWIWDDMFZDDMFZGFZWEDDGFZMFZWEWCWGWLGFWMWIWCWGWKWLGDNCZW
    CWGWKOPDAQRSWHWLWGGWHTWLUAUBUCUDUEWCWDNCZWOWMOZWPWCWQPDAUFRZWQWPWRPWQWPUGZW
    PWPUGZWRWPWPPPUHWTXAUGDUIUJZXBUGWRXBXBDUKULUMZXCUHWDDDDUNUOUOUOUPWCWEHMFZWE
    WOWCWQWENCXDWEOWSWDUQWEURUSWNHWEMDPXCUTUDUEVAWCWIWJLVBZWGWHWJMFLVBZDBCWCXFV
    CDAVDRWGVECZWJVECZXEXFVFZWCXGWHVECZXHXIWHTVEUAVGVHXGXJXHVIUIWHVJVBXIUITWHVJ
    VKUAVLWGWHWJVMUOVNWCWFBCZXGAVODVECXKXGUKDWFVPRUPWCWJVQCXHAVRWJVSUPVTWAWB $.
    $( [18-Dec-2005] $) $( [17-Dec-2005] $)

  $( A lower bound for the factorial function. $)
  faclbnd3 $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                 ( M ^ N ) <_ ( ( M ^ M ) x. ( ! ` N ) ) ) $=
    ( cn wcel cc0 wceq wo cn0 cexp co cfa cfv cmul cle wbr wa c1 caddc cr w3a
    expwordit nnret adantr pm3.27 peano2nn0 adantl 3jca nnge1t letrp1t nn0ret
    leidt syl syl3anc anim12i sylanc faclbnd nnnn0t sylan wi letrt reexpclt
    syl2an axmulrcl mpancom facclt mp2and elnn0 0expt 0re 1re lt01 ltlei
    syl6eqbr opreq2 cc 0cn exp0t ax-mp leid eqbrtr jaoi sylbi 1nn nnmulclt
    mpan mp3an2 wb opreq1 opreq12 anidms syl6eq opreq1d breq12d mpbird jaoian
    sylanb ) ACDZAEFZGBHDZABIJZAAIJZBKLZMJZNOZAHDZWQWSXDWRWQWSPZWTABQRJZIJZNOZX
    HXCNOZXDASDZWSXGHDZTQANOZBXGNOZPXIXFABXGUAXFXKWSXLWQXKWSAUBUCWQWSUDWSXLWQBU
    EZUFUGWQXMWSXNAUHBSDZXPBBNOZXNWSBBUIBUJZXRWSXPXQXRBUKULUMUNUOXEWSXJWQABUPAU
    QZURXEWSXIXJPXDUSZWQWTSDZXHSDZXCSDZXTXEWSPWTXHXCUTXKWSYAXEABVAAUJZURXKXLYBX
    EWSAXGVAYDXOVBXASDZXBSDZYCXEWSXAXBVCXKXEYEYDAAVAVDWSXBCDZYFBVEZXBUBULZVBUMX
    SURVFWRWSPXDEBIJZQXBMJZNOZWSYLWRWSYJQNOZQYKNOZYLWSBCDZBEFZGYMBVGYOYMYPYOYJE
    QNBVHEQVIVJVKVLVMYPYJEEIJZQNBEEIVNYQQQNEVODYQQFVPEVQVRZQVJVSVTVMWAWBWSYKCDZ
    YNWSYGYSYHQCDYGYSWCQXBWDWEULYKUHULYJSDZYKSDZYMYNPYLUSZWSYTQSDZUUAUUBVJYJQYK
    UTWFESDWSYTVIEBVAWEWSYFUUAYIUUCYFUUAVJQXBVCWEULUOVFUFWRXDYLWGWSWRWTYJXCYKNA
    EBIWHWRXAQXBMWRXAYQQWRXAYQFAEAEIWIWJYRWKWLWMUCWNWOAVGWP $.
    $( [24-Dec-2005] $) $( [19-Dec-2005] $)

  ${
    faclbnd4lem1.1 $e |- N e. NN $.
    faclbnd4lem1.2 $e |- K e. NN0 $.
    faclbnd4lem1.3 $e |- M e. NN0 $.
    $( Lemma for ~ faclbnd4 .  Prepare the induction step.  _Warning:  The HTML
       proof page is 0.6 megabyte in size_. $)
    faclbnd4lem1 $p |- ( ( ( ( N - 1 ) ^ K ) x. ( M ^ ( N - 1 ) ) ) <_
       ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` ( N - 1 ) ) ) ->
                   ( ( N ^ ( K + 1 ) ) x. ( M ^ N ) ) <_ ( ( (
  2 ^ ( ( K + 1 ) ^ 2 ) ) x. ( M ^ ( M + ( K + 1 ) ) ) ) x. ( ! ` N ) ) ) $=
      ( c1 cle wbr clt wo cmin co cexp cmul c2 caddc cfa cfv cr wcel nnre 1re
      lelttrit mp2an wceq letri3 cn nnge1t ax-mp mpbiran2 wa cc0 cn0 2re 1nn
      nn0nnaddclt nnnn0 2nn0 nn0expclt reexpclt pm3.2i 0re lt01 ltlei cc 2cn
      exp0t w3a 0nn0 3pm3.2i 1lt2 nn0ge0 expwordit eqbrtrr nn0re elnn0 nncnt
      exp1t syl 1nn0 mpan mpan2 eqbrtrrd breq1 mpbiri jaoi sylbi lemul12it
      opreq1 1expt syl6eq opreq2 nn0cn opreq12d fveq2 fac1 opreq2d recn mulcl
      mulid1 breq12d sylbir adantr remulcl a1i facclt nn0mulcl elnnnn0 mpbi
      pm3.27i nn0addcl wb nnltp1let df-2 breq1i bitr4 expbndt mp3an12 lemul1it
      nncn mul4 mulass opreq2i mul12 3eqtr2 syl6breq adantl letrd mul23 expp1t
      ax1cn addass eqtr3 facnn2t opreq12i 3eqtr4 eqtr expm1t syl5eqbr expaddt
      mp3an nn0addge1 sqval eqcomi adddi eqtr4 3brtr4 syl6breqr jaoian ) CGHIZG
      CJIZKZCGLMZANMZBUUNNMZOMZPAPNMZNMZBBAQMZNMZOMZUUNRSZOMZHIZCAGQMZNMZBCNMZO
      MZPUVFPNMZNMZBBUVFQMZNMZOMZCRSZOMZHIZCTUAZGTUAZUUMCDUBZUCCGUDUEUUKUVEUVQU
      ULUUKUVQUVEUUKCGUFZUVQUWAUUKGCHIZCGUVTUCUGCUHUAZUWBDCUIUJUKUWAUVQGBOMZUVN
      HIZUVSUVKTUAZULZUMGHIZGUVKHIZULZULBTUAZUVMTUAZULZUMBHIZBUVMHIZULZULUWEUWG
      UWJUVSUWFUCPTUAZUVJUNUAZUWFUOUVFUNUAZPUNUAZUWRUVFAUNUAZGUHUAZUVFUHUAZEUPA
      GUQUEZURZUSUVFPUTUEZPUVJVAUEZVBUWHUWIUMGVCUCVDVEPUMNMZGUVKHPVFUAZUXHGUFVG
      PVHUJUWQUMUNUAZUWRVIGPHIZUMUVJHIZULUXHUVKHIUWQUXJUWRUOVJUXFVKUXKUXLGPUCUO
      VLVEZUVJUXFVMVBPUMUVJVNUEVOVBVBUWMUWPUWKUWLBFVPZUVMBUNUAZUVLUNUAZUVMUNUAF
      UVLUXOUXCUVLUHUAZFUXDBUVFUQUEZURZBUVLUTUEZVPVBUWNUWOBFVMUXOUWOFUXOBUHUAZB
      UMUFZKUWOBVQUYAUWOUYBUYABGNMZBUVMHUYABVFUAZUYCBUFZBVRBVSZVTUYAGBHIZUYCUVM
      HIZBUIUYGGUVLHIZUYHUXQUYIUXRUVLUIUJUWKGUNUAZUXPVIUYGUYIULUYHUWKUYJUXPUXNW
      AUXSVKBGUVLVNWBWCVTWDUYBUWOUMUVMHIUVMUXTVMBUMUVMHWEWFWGWHUJVBVBGUVKBUVMWI
      UEUWAUVIUWDUVPUVNHUWAUVGGUVHBOUWAUVGGUVFNMZGCGUVFNWJUWSUYKGUFUXEUVFWKUJWL
      UWAUVHUYCBCGBNWMUYDUYEBFWNZUYFUJWLWOUWAUVPUVNGOMUVNUWAUVOGUVNOUWAUVOGRSGC
      GRWPWQWLWRUVNUVKUVMUVKUXGWSZUVMUXTWNZWTXAWLXBWFXCXDUULUVEULZUVIUVKUVMUVOO
      MZOMZUVPHUYOUVIUUSPANMZOMZUYPOMZUYQUVITUAUYOUVGUVHUVRUWSUVGTUAUVTUXECUVFV
      AUEUWKCUNUAZUVHTUAUXNCDURZBCVAUEXEXFUYTTUAUYOUYSUYPUUSUYRUWQUURUNUAZUUSTU
      AUOUXAUWTVUCEUSAPUTUEZPUURVAUEZUYRUWTUXAUYRUNUAUSEPAUTUEZVPZXEZUYPUVMUVOU
      XTUVOVUAUVOUHUAVUBCXGUJZURXHZVPZXEXFUYQTUAUYOUVKUYPUXGVUKXEXFUYOCANMZCUUP
      BOMZOMZOMZUYTUVIHUYOVUOUVDUYRCOMZBOMZOMZUYTHUYOVUOUUQVUQOMZVURVUOTUAUYOVU
      LVUNUVRUXAVULTUAZUVTECAVAUEZVUNCVUMVUBUUPBUXOUUNUNUAZUUPUNUAFCVFUAZVVBUWC
      VVCVVBULDCXIXJXKZBUUNUTUEZFXHZXHZVPZXEXFVUSTUAUYOUUQVUQUUOUUPUUNTUAUXAUUO
      TUAUUNVVDVPEUUNAVAUEZUUPVVEVPXEZVUQVUPBUYRCVUFVUBXHZFXHZVPZXEXFVURTUAUYOU
      VDVUQUVBUVCUUSUVAVUEUWKUUTUNUAZUVATUAUXNBAFEXLZBUUTVAUEZXEZUVCVVBUVCUHUAV
      VDUUNXGUJZUBXEZVVMXEXFUULVUOVUSHIUVEUULVUOUYRUUOOMZVUNOMZVUSHUULVULVVTHIZ
      VUOVWAHIZUULPCHIZVWBUULGGQMZCHIZVWDUXBUWCUULVWFXMUPDGCXNUEPVWECHXOXPXQUVR
      UXAVWDVWBUVTECAXRXSWHUMVUNHIZVWBVWCVUNVVGVMVUTVVTTUAZVUNTUAZVIVWGVWBULVWC
      VUTVWHVWIVVAUYRUUOVUGVVIXEVVHVKVULVVTVUNXTWBWBVTVWAVUPUUOVUMOMZOMVUPUUQBO
      MZOMVUSUYRUUOCVUMUYRVUFWNZUUOVVIWSZCDYAZVUMVVFWNZYBVWKVWJVUPOUUOUUPBVWMUU
      PVVEWNUYLYCYDVUPUUQBVUPVVKWNUUQVVJWSUYLYEYFYGXDUVEVUSVURHIZUULUMVUQHIZUVE
      VWPVUQVVLVMUUQTUAZUVDTUAZVUQTUAZVIVWQUVEULVWPVWRVWSVWTVVJVVSVVMVKUUQUVDVU
      QXTWBWBYHYIVURUYSUVMOMZUVOOMZUYTUVDUYRBOMZCOMZOMUVBVXCOMZUVCCOMZOMVURVXBU
      VBUVCVXCCUVBVVQWSUVCVVRYAUYRBVWLUYLWTVWNYBVUQVXDUVDOUYRCBVWLVWNUYLYJYDVXA
      VXEUVOVXFOUYSUVABOMZOMVXAVXEVXGUVMUYSOBUUTGQMZNMZVXGUVMUYDVVNVXIVXGUFUYLV
      VOBUUTYKUEVXHUVLBNBAGUYLAEWNZYLYMYDYNYDUUSUYRUVABUUSVUEWSVWLUVAVVPWSUYLYB
      YNUWCUVOVXFUFDCYOUJYPYQUYSUVMUVOUYSVUHWSUYNUVOVUIYAZYCYRYGUVIVULCOMZVUMOM
      VUOUVGVXLUVHVUMOVVCUXAUVGVXLUFVWNECAYKUEUYDUWCUVHVUMUFUYLDBCYSUEYPVULCVUM
      VULVVAWSVWNVWOYCYRYTUYTUYQHIZUYOUYSTUAZUWFUYPTUAZVIUMUYPHIZUYSUVKHIZULVXM
      VXNUWFVXOVUHUXGVUKVKVXPVXQUYPVUJVMPUURAQMZNMZUYSUVKHUXIVUCUXAVXSUYSUFVGVU
      DEPUURAUUAUUBUWQVXRUNUAZUWRVIUXKVXRUVJHIZULVXSUVKHIUWQVXTUWRUOUURAVUDEXLU
      XFVKUXKVYAUXMAUVFOMZUVFUVFOMZVXRUVJHATUAZUVFTUAZVYEVIUMUVFHIZAUVFHIZULVYB
      VYCHIVYDVYEVYEAEVPZUVFUXDUBZVYIVKVYFVYGUVFUXEVMAGVYHWAUUCVBAUVFUVFXTUEVXR
      AAOMZAGOMZQMVYBUURVYJAVYKQAVXJUUDVYKAAVXJXAUUEYPAAGVXJVXJYLUUFUUGUVFUVFUX
      DYAUUDUUHVBPVXRUVJVNUEVOVBUYSUVKUYPXTUEXFYIUVKUVMUVOUYMUYNVXKYCUUIUUJWB
      $.
      $( [27-Jan-2006] $) $( [20-Dec-2005] $)
  $}

  $( Lemma for ~ faclbnd4 .  Use the weak deduction theorem to convert
     the hypotheses of ~ faclbnd4lem1 to antecedents. $)
  faclbnd4lem2 $p |- ( ( M e. NN0 /\ K e. NN0 /\ N e. NN ) ->
           ( ( ( ( N - 1 ) ^ K ) x. ( M ^ ( N - 1 ) ) ) <_
       ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` ( N - 1 ) ) ) ->
                   ( ( N ^ ( K + 1 ) ) x. ( M ^ N ) ) <_ ( ( (
  2 ^ ( ( K + 1 ) ^ 2 ) ) x. ( M ^ ( M + ( K + 1 ) ) ) ) x. ( ! ` N ) ) ) ) $=
    ( cn0 wcel cn c1 cmin co cexp cmul c2 caddc cfa cfv cle wbr wi cif wceq
    opreq1 opreq2d id opreq12d opreq1d breq12d imbi12d opreq2 fveq2d fveq2 1nn
    elimel 1nn0 faclbnd4lem1 dedth3h ) BDEZADEZCFEZCGHIZAJIZBUSJIZKIZLALJIZJIZB
    BAMIZJIZKIZUSNOZKIZPQZCAGMIZJIZBCJIZKIZLVKLJIZJIZBBVKMIZJIZKIZCNOZKIZPQZRUT
    UPBGSZUSJIZKIZVDWCWCAMIZJIZKIZVHKIZPQZVLWCCJIZKIZVPWCWCVKMIZJIZKIZVTKIZPQZR
    BACGGGUSUQAGSZJIZWDKIZLWRLJIZJIZWCWCWRMIZJIZKIZVHKIZPQZCWRGMIZJIZWKKIZLXHLJ
    IZJIZWCWCXHMIZJIZKIZVTKIZPQZRURCGSZGHIZWRJIZWCXSJIZKIZXEXSNOZKIZPQZXRXHJIZW
    CXRJIZKIZXOXRNOZKIZPQZRBWCTZVJWJWBWQYLVBWEVIWIPYLVAWDUTKBWCUSJUAUBYLVGWHVHK
    YLVFWGVDKYLBWCVEWFJYLUCZBWCAMUAUDUBUEUFYLVNWLWAWPPYLVMWKVLKBWCCJUAUBYLVSWOV
    TKYLVRWNVPKYLBWCVQWMJYMBWCVKMUAUDUBUEUFUGAWRTZWJXGWQXQYNWEWTWIXFPYNUTWSWDKA
    WRUSJUHUEYNWHXEVHKYNVDXBWGXDKYNVCXALJAWRLJUAUBYNWFXCWCJAWRWCMUHUBUDUEUFYNWL
    XJWPXPPYNVLXIWKKYNVKXHCJAWRGMUAZUBUEYNWOXOVTKYNVPXLWNXNKYNVOXKLJYNVKXHLJYOU
    EUBYNWMXMWCJYNVKXHWCMYOUBUBUDUEUFUGCXRTZXGYEXQYKYPWTYBXFYDPYPWSXTWDYAKYPUSX
    SWRJCXRGHUAZUEYPUSXSWCJYQUBUDYPVHYCXEKYPUSXSNYQUIUBUFYPXJYHXPYJPYPXIYFWKYGK
    CXRXHJUACXRWCJUHUDYPVTYIXOKCXRNUJUBUFUGWRWCXRCGFUKULAGDUMULBGDUMULUNUO $.
    $( [27-Jan-2006] $) $( [23-Dec-2005] $)


  $( Lemma for ~ faclbnd4 .  The ` N = 0 ` case. $)
  faclbnd4lem3 $p |- ( ( ( M e. NN0
            /\ K e. NN0 ) /\ N = 0 ) -> ( ( N ^ K ) x. ( M ^ N ) )
       <_ ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel wa cc0 wceq cexp co cmul c2 caddc cfa cfv cle wbr c1 cn wo
    0expt adantl nn0mulclt 2nn0 nn0expclt mpan2 mpan syl nn0addclt syldan
    sylanc nnnn0t sylan2 nn0ge0t eqbrtrd elnn0 0nn0 nnexpclt mpdan id opreq1
    0cn addid1 syl6eq opreq12d cc exp0t ax-mp 1nn syl6eqel jaoi sylbi nnmulclt
    nnge1t 3syl adantr wb opreq2 sq0 opreq2d 2cn breq12d mpbird jaodan sylan2b
    nn0cnt ax1id sylan9eq 3brtr4d fveq2 fac0 ) BDEZADEZFZCGHZFCAIJZBCIJZKJZLALI
    JZIJZBBAMJZIJZKJZCNOZKJZPQZGAIJZBGIJZKJZXCRKJZPQZWNXKWOWNXGXCXIXJPWLASEZAGH
    ZTXGXCPQZWMWLXLXNXMWLXLFZXGGXCPXLXGGHWLAUAUBXOXCDEZGXCPQWLWMXPXLWTDEZXBDEZX
    PWNWTXBUCWMXQWLWMWSDEZXQWMLDEZXSUDALUEUFXTXSXQUDLWSUEUGUHUBWLWMXADEXRBAUIBX
    AUEUJUKZAULUMXCUNUHUOWLXMFXNRRBBGMJZIJZKJZPQZWLYEXMWLYCSEZYDSEZYEWLBSEZBGHZ
    TYFBUPYHYFYIYHYBDEZYFYHWLYJBULWLGDEZYJUQBGUIUFUHBYBURUSYIYCRSYIYCGGIJZRYIBG
    YBGIYIUTYIYBGGMJGBGGMVAGVBVCVDVEGVFEYLRHVBGVGVHZVDVIVJVKVLRSEYFYGVIRYCVMUGY
    DVNVOVPXMXNYEVQWLXMXGRXCYDPXMXGYLRAGGIVRYMVDXMWTRXBYCKXMWTLGIJZRXMWSGLIXMWS
    GLIJGAGLIVAVSVDVTLVFEYNRHWALVGVHVDXMXAYBBIAGBMVRVTVEWBUBWCWDAUPWEWLWMXIXGRK
    JZXGWLXHRXGKWLBVFEXHRHBWFBVGUHVTWMXGDEZXGVFEYOXGHYKWMYPUQGAUEUGXGWFXGWGVOWH
    WNXPXCVFEXJXCHYAXCWFXCWGVOWIVPWOXFXKVQWNWOWRXIXEXJPWOWPXGWQXHKCGAIVACGBIVRV
    EWOXDRXCKWOXDGNORCGNWJWKVDVTWBUBWC $.
    $( [28-Jan-2006] $) $( [23-Dec-2005] $)

  ${
    $d j m n M $.  $d j m n K $.  $d j n N $.
    $( Lemma for ~ faclbnd4 .  Prove the ` 0 < N ` case by induction on
       ` K ` . $)
    faclbnd4lem4 $p |- ( ( N e. NN /\ K e. NN0 /\ M e. NN0 ) ->
                   ( ( N ^ K ) x. ( M ^ N ) ) <_
              ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
      ( vn vm vj cn wcel cn0 cexp co cmul c2 caddc cfa cfv cle wbr cv wral wa
      wceq opreq1 opreq2 opreq12d fveq2 opreq2d breq12d rcla4va wi cc0 c1 cmin
      clt wo faclbnd4lem3 nnge1t adantr cr wb nnret 1re letri3t mpan2 syl
      biimpar anassrs mpdan ax1cn subid syl6eq sylan2 a1d 1nn nnsubt mpan
      biimpa rcla4v adantl jaodan lelttrit ancli andi sylib faclbnd4lem2 3expa
      syld ex com23 r19.21adv cbvralv syl5ib expcom a2d faclbnd3 nnnn0t cc
      nncnt exp0t opreq1d expclt nn0cnt syl2an mulid2t eqtrd sq0 opreq2i 2cn
      ax-mp eqtr a1i ax0id mpancom 3brtr4d r19.21aiva ralbidv imbi2d nn0indALT
      imp 3impb ) CGHZAIHZBIHZCAJKZBCJKZLKZMAMJKZJKZBBANKZJKZLKZCOPZLKZQRZYADSZ
      AJKZBYOJKZLKZYKYOOPZLKZQRZDGTZYNYBYCUAUUAYNDCGYOCUBZYRYFYTYMQUUCYPYDYQYEL
      YOCAJUCYOCBJUDUEUUCYSYLYKLYOCOUFUGUHUIYBYCUUBYCYOESZJKZYQLKZMUUDMJKZJKZBB
      UUDNKZJKZLKZYSLKZQRZDGTZUJYCYOUKJKZYQLKZMUKMJKZJKZBBUKNKZJKZLKZYSLKZQRZDG
      TZUJYCYOFSZJKZYQLKZMUVEMJKZJKZBBUVENKZJKZLKZYSLKZQRZDGTZUJYCYOUVEULNKZJKZ
      YQLKZMUVPMJKZJKZBBUVPNKZJKZLKZYSLKZQRZDGTZUJYCUUBUJEFAUVEIHZYCUVOUWFYCUWG
      UVOUWFUJYCUWGUAZUUDUVEJKZBUUDJKZLKZUVLUUDOPZLKZQRZEGTZUWFUVOUWHUWOUWEDGUW
      HYOGHZUWOUWEUWHUWPUWOUWEUJUWHUWPUAUWOYOULUMKZUVEJKZBUWQJKZLKZUVLUWQOPZLKZ
      QRZUWEUWHUWPYOULQRZUAZUWPULYOUNRZUAZUOZUWOUXCUJZUWPUWHUXEUXIUXGUWHUXEUAUX
      CUWOUWHUWQUKUBZUXCUXEUVEBUWQUPUXEYOULUBZUXJUXEULYOQRZUXKUWPUXLUXDYOUQURUW
      PUXDUXLUXKUWPUXKUXDUXLUAZUWPYOUSHZUXKUXMUTZYOVAZUXNULUSHZUXOVBYOULVCVDVEV
      FVGVHUXKUWQULULUMKUKYOULULUMUCULVIVJVKVEVLVMUXGUXIUWHUXGUWQGHZUXIUWPUXFUX
      RULGHUWPUXFUXRUTVNULYOVOVPVQUWNUXCEUWQGUUDUWQUBZUWKUWTUWMUXBQUXSUWIUWRUWJ
      UWSLUUDUWQUVEJUCUUDUWQBJUDUEUXSUWLUXAUVLLUUDUWQOUFUGUHVRVEVSVTUWPUWPUXDUX
      FUOZUAUXHUWPUXTUWPUXNUXTUXPUXNUXQUXTVBYOULWAVDVEWBUWPUXDUXFWCWDVLYCUWGUWP
      UXCUWEUJUVEBYOWEWFWGWHWIWJUVNUWNDEGYOUUDUBZUVGUWKUVMUWMQUYAUVFUWIYQUWJLYO
      UUDUVEJUCYOUUDBJUDUEUYAYSUWLUVLLYOUUDOUFUGUHWKWLWMWNYCUVCDGYCUWPUAZYQBBJK
      ZYSLKZUUPUVBQYCYOIHZYQUYDQRUWPBYOWOYOWPZVLUYBUUPULYQLKZYQUWPUUPUYGUBYCUWP
      UUOULYQLUWPYOWQHUUOULUBYOWRYOWSVEWTVSUYBYQWQHZUYGYQUBBWQHZUYEUYHYCUWPBYOX
      ABXBZUYFXCYQXDVEXEYCUVBUYDUBUWPYCUVAUYCYSLYCUVAULUYCLKZUYCYCUURULUUTUYCLU
      URULUBYCUURMUKJKZULUUQUKMJXFXGMWQHUYLULUBXHMWSXIXJXKYCUUSBBJYCUYIUUSBUBUY
      JBXLVEUGUEYCUYCWQHZUYKUYCUBUYIYCUYMUYJBBXAXMUYCXDVEXEWTURXNXOUUDUKUBZUUNU
      VDYCUYNUUMUVCDGUYNUUFUUPUULUVBQUYNUUEUUOYQLUUDUKYOJUDWTUYNUUKUVAYSLUYNUUH
      UURUUJUUTLUYNUUGUUQMJUUDUKMJUCUGUYNUUIUUSBJUUDUKBNUDUGUEWTUHXPXQUUDUVEUBZ
      UUNUVOYCUYOUUMUVNDGUYOUUFUVGUULUVMQUYOUUEUVFYQLUUDUVEYOJUDWTUYOUUKUVLYSLU
      YOUUHUVIUUJUVKLUYOUUGUVHMJUUDUVEMJUCUGUYOUUIUVJBJUUDUVEBNUDUGUEWTUHXPXQUU
      DUVPUBZUUNUWFYCUYPUUMUWEDGUYPUUFUVRUULUWDQUYPUUEUVQYQLUUDUVPYOJUDWTUYPUUK
      UWCYSLUYPUUHUVTUUJUWBLUYPUUGUVSMJUUDUVPMJUCUGUYPUUIUWABJUUDUVPBNUDUGUEWTU
      HXPXQUUDAUBZUUNUUBYCUYQUUMUUADGUYQUUFYRUULYTQUYQUUEYPYQLUUDAYOJUDWTUYQUUK
      YKYSLUYQUUHYHUUJYJLUYQUUGYGMJUUDAMJUCUGUYQUUIYIBJUUDABNUDUGUEWTUHXPXQXRXS
      VLXT $.
      $( [28-Jan-2006] $) $( [19-Dec-2005] $)
  $}

  $( The factorial function grows faster than powers and exponentiations.  If
     we consider ` K ` and ` M ` to be constants, the right-hand side of the
     inequality is a constant times ` N ` -factorial. $)
  faclbnd4 $p |- ( ( N e. NN0 /\ K e. NN0 /\ M e. NN0 ) ->
                 ( ( N ^ K ) x. ( M ^ N ) ) <_
            ( ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cexp co cmul c2 caddc cfa cfv cle wbr wa cn cc0 wceq wo
    faclbnd4lem4 3com13 3expa faclbnd4lem3 jaodan elnn0 sylan2b 3impa ) BDEZADE
    ZCDEZCAFGBCFGHGIAIFGFGBBAJGFGHGCKLHGMNZUHUIUJUKUHUIOZCPEZCQRZSUKUJULUMUKUNU
    HUIUMUKUMUIUHUKABCTUAUBABCUCUDCUEUFUGUA $.
    $( [28-Jan-2006] $) $( [23-Dec-2005] $)

  $( Variant of ~ faclbnd4 providing a strict lower bound. $)
  faclbnd5 $p |- ( ( N e. NN0 /\ K e. NN0 /\ M e. NN ) ->
                 ( ( N ^ K ) x. ( M ^ N ) ) <
      ( ( 2 x. ( ( 2 ^ ( K ^ 2 ) ) x. ( M ^ ( M + K ) ) ) ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel cn cexp co cmul c2 caddc cfa cfv clt wbr wa cr axmulrcl
    reexpclt nn0ret sylan ancoms nnret syl2an anandirs nnmulclt 2nn0 nn0expclt
    mpan2 2nn nnexpclt mpan syl nn0addclt nnnn0t sylan2 anabss7 anabss5 facclt
    2re cle faclbnd4 syl3an3 3coml 3expa c1 1lt2 cc0 wb ltmulgt12t mp3an2
    nngt0t sylanc mpbii lelttrd cc wceq 2cn axmulass mp3an1 recnd breqtrrd
    3impa 3comr ) ADEZBFEZCDEZCAGHZBCGHZIHZJJAJGHZGHZBBAKHZGHZIHZIHCLMZIHZNOZWE
    WFWGWRWEWFPZWGPZWJJWOWPIHZIHZWQNWTWJXAXBWEWFWGWJQEZWHQEZWIQEZXCWEWGPWFWGPWH
    WIRWGWEXDCQEWEXDWGCASCTUAUBBQEWGXEWFBCSBUCUAUDUEWOQEZWPQEZXAQEZWSWGWOWPRWSW
    OFEZXFWEWFXIWLFEZWNFEZXIWEWSWLWNUFWEWKDEZXJWEJDEXLUGAJUHUIJFEXLXJUJJWKUKULU
    MWEWFXKWFWMDEZXKWSBWMUKWEBDEZXMWFXNWEXMBAUNUBBUOZUPUPUQUDURZWOUCUMZWGWPFEZX
    GCUSZWPUCUMZUDZWTXHXBQEZYAJQEZXHYBUTJXARULUMWEWFWGWJXAVAOZWGWEWFYDWGWEXNYDW
    FABCVBXOVCVDVEWTVFJNOZXAXBNOZVGXHVHXANOZYEYFVIZWTXHYCYGYHUTXAJVJVKYAWTXAFEZ
    YGXIXRYIWSWGWOWPUFXPXSUDXAVLUMVMVNVOWOVPEZWPVPEZWQXBVQZWSWGJVPEYJYKYLVRJWOW
    PVSVTWSWOXQWAWGWPXTWAUDWBWCWD $.
    $( [16-Feb-2006] $) $( [24-Dec-2005] $)

  $( The product of two factorials is greater than or equal to the factorial of
     (the floor of) their average. $)
  facavgt $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
       ( ! ` ( floor ` ( ( M + N ) / 2 ) ) ) <_ ( ( ! ` M ) x. ( ! ` N ) ) ) $=
    ( cn0 wcel wa caddc co c2 cdiv cle wbr wo cfl cfv cfa cmul cr avglet
    nn0ret syl2an nn0addclt syl rehalfclt fllet 3syl wi letrt cz flclt zret
    adantr syl3anc mpand facwordit 3exp cc0 flge0nn0t nn0ge0t wb halfnneg2t
    mpbid sylanc pm3.26 sylc syld c1 wceq cn cc facclt nncnt ax1id 1re
    lemul2it mp3anl1 nnret anim12i ancoms nnnn0t nnge1t eqbrtrrd axmulrcl
    mpan2d adantl pm3.27 mulid2t lemul1it jaod mpd ) ACDZBCDZEZABFGZHIGZAJKZWNB
    JKZLZWNMNZONZAONZBONZPGZJKZAQDZBQDZWQWJWKABRASZBSZTWLWOXCWPWLWOWSWTJKZXCWLW
    OWRAJKZXHWLWRWNJKZWOXIWLWMQDZWNQDZXJWLWMCDZXKABUAZWMSZUBZWMUCZWNUDUEZWRQDZX
    LXDXJWOEXIUFWLWRWNAUGWLXKXLXSXPXQXLWRUHDXSWNUIWRUJUBUEZWLXMXKXLXNXOXQUEZWJX
    DWKXFUKULUMWRCDZWJXIXHUFWLYBWJXIXHWRAUNUOXLUPWNJKZYBWLWNUQYAWLUPWMJKZYCWLXM
    YDXNWMURUBWLXMXKYDYCUSXNXOWMUTUEVAVBZWJWKVCVDVEWLXHWTXBJKZXCWLWTVFPGZWTXBJW
    JYGWTVGZWKWJWTVHDZWTVIDYHAVJZWTVKWTVLUEUKXAQDZWTQDZEZUPWTJKZVFXAJKZEZYGXBJK
    ZWLVFQDZYKYLYPYQVMVFXAWTVNVOWKWJYMWKYKWJYLWKXAVHDZYKBVJZXAVPUBZWJYIYLYJWTVP
    UBZVQVRWJYNWKYOWJYIWTCDYNYJWTVSWTURUEWKYSYOYTXAVTUBVQVBWAWSQDZYLXBQDZXHYFEX
    CUFWLWSWTXBUGWLYBWSVHDUUCYEWRVJWSVPUEZWJYLWKUUBUKYLYKUUDWJWKWTXAWBUUBUUATZU
    LWCVEWLWPWSXAJKZXCWLWPWRBJKZUUGWLXJWPUUHXRXSXLXEXJWPEUUHUFWLWRWNBUGXTYAWKXE
    WJXGWDULUMYBWKUUHUUGUFWLYBWKUUHUUGWRBUNUOYEWJWKWEVDVEWLUUGXAXBJKZXCWLVFXAPG
    ZXAXBJWKUUJXAVGZWJWKYSXAVIDUUKYTXAVKXAWFUEWDYLYKEUPXAJKZVFWTJKZEZUUJXBJKZWL
    YRYLYKUUNUUOVMVFWTXAWGVOWJYLWKYKUUBUUAVQWKWJUUNWKUULWJUUMWKYSXACDUULYTXAVSX
    AURUEWJYIUUMYJWTVTUBVQVRVBWAUUCYKUUDUUGUUIEXCUFWLWSXAXBUGUUEWKYKWJUUAWDUUFU
    LWCVEWHWI $.
    $( [6-Feb-2006] $) $( [9-Dec-2005] $)

