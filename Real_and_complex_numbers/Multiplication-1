$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Multiplication
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Product of two sums.  (The proof was shortened by Andrew Salmon,
     19-Nov-2011.) $)
  muladd $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
               ( ( A + B ) x. ( C + D ) ) =
       ( ( ( A x. C ) + ( D x. B ) ) + ( ( A x. D ) + ( C x. B ) ) ) ) $=
    ( cc wcel wa caddc co cmul wceq addcl axdistr 3expb sylan adddir 3expa
    adantrr adantrl opreq12d mulcl ad2ant2r ad2ant2lr syl2an anandirs add32
    syl111anc mulcom ad2ant2l opreq2d ad2ant2rl addass ancoms 3eqtr3d an4s
    3eqtrd ) AEFZBEFZGZCEFZDEFZGZGZABHIZCDHIJIZVDCJIZVDDJIZHIZACJIZBCJIZHIZADJI
    ZBDJIZHIZHIZVIDBJIZHIZVLCBJIZHIHIZUSVDEFZVBVEVHKZABLVTUTVAWAVDCDMNOVCVFVKVG
    VNHUSUTVFVKKZVAUQURUTWBABCPQRUSVAVGVNKZUTUQURVAWCABDPQSTVCVOVIVNHIZVJHIZVQV
    LHIZVRHIZVSVCVIEFZVJEFZVNEFZVOWEKUQUTWHURVAACUAZUBZURUTWIUQVABCUAUCUSVAWJUT
    UQURVAWJUQVAGVLEFZVMEFZWJURVAGZADUAZBDUAZVLVMLUDUESVIVJVNUFUGVCWDWFVJVRHVCV
    IVLHIZVMHIZWRVPHIZWDWFVCVMVPWRHURVAVMVPKUQUTBDUHUIUJVCWHWMWNWSWDKWLUQVAWMUR
    UTWPUKZURVAWNUQUTWQUIVIVLVMULUGVCWHWMVPEFZWTWFKWLXAURVAXBUQUTVAURXBDBUAUMZU
    IVIVLVPUFUGUNURUTVJVRKUQVABCUHUCTVCVQEFZWMVREFZWGVSKUQUTURVAXDUQUTGWHXBXDWO
    WKXCVIVPLUDUOXAURUTXEUQVAUTURXECBUAUMUCVQVLVRULUGUPUP $.
    $( [19-Nov-2011] $) $( [14-Jan-2006] $)

  ${
    muladdi.1 $e |- A e. CC $.
    muladdi.2 $e |- B e. CC $.
    muladdi.3 $e |- C e. CC $.
    muladdi.4 $e |- D e. CC $.
    $( Product of two sums. $)
    muladdi $p |- ( ( A + B ) x. ( C + D ) ) =
        ( ( ( A x. C ) + ( D x. B ) ) + ( ( A x. D ) + ( C x. B ) ) ) $=
      ( cc wcel caddc co cmul wceq muladd mp4an ) AIJBIJCIJDIJABKLCDKLMLACMLDBM
      LKLADMLCBMLKLKLNEFGHABCDOP $.
      $( [17-May-1999] $)
  $}

  $( Distribution of multiplication over subtraction.  Theorem I.5 of [Apostol]
     p. 18. $)
  subdi $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( A x. ( B - C ) ) = ( ( A x. B ) - ( A x. C ) ) ) $=
    ( cc wcel w3a cmul co cmin wceq caddc simp1 simp3 subcl 3adant1 adddi
    syl111anc pncan3 ancoms opreq2d eqtr3d wb mulcl 3adant3 3adant2 wa sylan2
    3impb subadd mpbird eqcomd ) ADEZBDEZCDEZFZABGHZACGHZIHZABCIHZGHZUOURUTJZUQ
    UTKHZUPJZUOACUSKHZGHZVBUPUOULUNUSDEZVEVBJULUMUNLULUMUNMUMUNVFULBCNZOACUSPQU
    OVDBAGUMUNVDBJZULUNUMVHCBRSOTUAUOUPDEZUQDEZUTDEZVAVCUBULUMVIUNABUCUDULUNVJU
    MACUCUEULUMUNVKUMUNUFULVFVKVGAUSUCUGUHUPUQUTUIQUJUK $.
    $( [18-Nov-2004] $)

  $( Distribution of multiplication over subtraction.  Theorem I.5 of [Apostol]
     p. 18. $)
  subdir $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A - B ) x. C ) = ( ( A x. C ) - ( B x. C ) ) ) $=
    ( cc wcel w3a cmin co cmul wceq subdi 3coml wa subcl mulcom sylan 3impa
    3adant2 3adant1 opreq12d 3eqtr4d ) ADEZBDEZCDEZFZCABGHZIHZCAIHZCBIHZGHZUFCI
    HZACIHZBCIHZGHUDUBUCUGUJJCABKLUBUCUDUKUGJZUBUCMUFDEUDUNABNUFCOPQUEULUHUMUIG
    UBUDULUHJUCACORUCUDUMUIJUBBCOSTUA $.
    $( [30-Dec-2005] $)

  ${
    subdi.1 $e |- A e. CC $.
    subdi.2 $e |- B e. CC $.
    subdi.3 $e |- C e. CC $.
    $( Distribution of multiplication over subtraction.  Theorem I.5 of
       [Apostol] p. 18. $)
    subdii $p |- ( A x. ( B - C ) ) = ( ( A x. B ) - ( A x. C ) ) $=
      ( cc wcel cmin co cmul wceq subdi mp3an ) AGHBGHCGHABCIJKJABKJACKJIJLDEFA
      BCMN $.
      $( [26-Nov-1994] $)

    $( Distribution of multiplication over subtraction.  Theorem I.5 of
       [Apostol] p. 18. $)
    subdiri $p |- ( ( A - B ) x. C ) = ( ( A x. C ) - ( B x. C ) ) $=
      ( cc wcel cmin co cmul wceq subdir mp3an ) AGHBGHCGHABIJCKJACKJBCKJIJLDEF
      ABCMN $.
      $( [8-May-1999] $)
  $}

  $( The imaginary unit ` _i ` is not zero. $)
  ine0 $p |- _i =/= 0 $=
    ( ci cc0 wne wceq wn c1 ax1ne0 df-ne mpbi caddc co cmul opreq2 axicn
    mul01i syl6req opreq1d ax1cn addid2i axi2m1 3eqtr3g mto mpbir ) ABCABDZEUDF
    BDZFBCUEEGFBHIUDBFJKAALKZFJKFBUDBUFFJUDUFABLKBABALMANOPQFRSTUAUBABHUC $.
    $( [29-Apr-2005] $) $( [6-May-1999] $)

  $( Closure law for negative of reals.  The weak deduction theorem ~ dedth is
     used to convert hypothesis of the inference (deduction) form of this
     theorem, ~ renegcli , to an antecedent. $)
  renegcl $p |- ( A e. RR -> -u A e. RR ) $=
    ( cr wcel cneg c1 cif wceq negeq eleq1d 1re elimel renegcli dedth ) ABCZADZ
    BCNAEFZDZBCAEAPGOQBAPHIPAEBJKLM $.
    $( [20-Jan-1997] $)

  $( Closure law for subtraction of reals. $)
  resubcl $p |- ( ( A e. RR /\ B e. RR ) -> ( A - B ) e. RR ) $=
    ( cr wcel wa cneg caddc co cmin cc wceq recn negsub syl2an renegcl readdcl
    sylan2 eqeltrrd ) ACDZBCDZEABFZGHZABIHZCSAJDBJDUBUCKTALBLABMNTSUACDUBCDBOAU
    APQR $.
    $( [2-Aug-2006] $) $( [20-Jan-1997] $)

  ${
    resubcl.1 $e |- A e. RR $.
    resubcl.2 $e |- B e. RR $.
    $( Closure law for subtraction of reals. $)
    resubcli $p |- ( A - B ) e. RR $=
      ( cr wcel cmin co resubcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [17-Jan-1997] $)
  $}

  $( 0 is a real number. $)
  0reALT $p |- 0 e. RR $=
    ( c1 cmin co cc0 cr ax1cn subidi 1re resubcli eqeltrri ) AABCDEAFGAAHHIJ $.
    $( [19-Feb-2005] $)

  $( "Reverse" second Peano postulate analog for reals. $)
  peano2rem $p |- ( N e. RR -> ( N - 1 ) e. RR ) $=
    ( cr wcel c1 cmin co 1re resubcl mpan2 ) ABCDBCADEFBCGADHI $.
    $( [6-Feb-2007] $)

  ${
    mulneg.1 $e |- A e. CC $.
    mulneg.2 $e |- B e. CC $.
    $( Product with negative is negative of product.  Theorem I.12 of [Apostol]
       p. 18. $)
    mulneg1i $p |- ( -u A x. B ) = -u ( A x. B ) $=
      ( cc0 cmul co cmin cneg mul01i mulcomi opreq12i df-neg opreq1i 0cn
      subcli subdii 3eqtri 3eqtr4i ) BEFGZBAFGZHGZEABFGZHGAIZBFGZUCITEUAUCHBDJB
      ADCKLUEEAHGZBFGBUFFGUBUDUFBFAMNUFBEAOCPDKBEADOCQRUCMS $.
      $( [10-Feb-1995] $)

    $( Product with negative is negative of product. $)
    mulneg2i $p |- ( A x. -u B ) = -u ( A x. B ) $=
      ( cneg cmul co negcli mulcomi mulneg1i negeqi 3eqtri ) ABEZFGMAFGBAFGZEAB
      FGZEAMCBDHIBADCJNOBADCIKL $.
      $( [31-Jul-1999] $)

    $( Product of two negatives.  Theorem I.12 of [Apostol] p. 18. $)
    mul2negi $p |- ( -u A x. -u B ) = ( A x. B ) $=
      ( cneg cmul co negcli mulneg1i mulcomi eqtri negeqi mulcli negnegi
      3eqtri ) AEBEZFGZBAFGZABFGQAPFGZEREZERAPCBDHZISTSPAFGTAPCUAJBADCIKLRBADCM
      NOBADCJK $.
      $( [14-Feb-1995] $)

    $( Distribution of negative over addition.  (The proof was shortened by
       Andrew Salmon, 19-Nov-2011.) $)
    negdii $p |- -u ( A + B ) = ( -u A + -u B ) $=
      ( caddc co cneg wceq cc0 addcli negidi opreq12i 0cn addid1i eqtri negcli
      add4i 3eqtr2i addcani mpbi ) ABEFZUAGZEFZUAAGZBGZEFZEFZHUBUFHUCIAUDEFZBUE
      EFZEFZUGUAABCDJZKUJIIEFIUHIUIIEACKBDKLIMNOAUDBUECACPZDBDPZQRUAUBUFUKUAUKP
      UDUEULUMJST $.
      $( [19-Nov-2011] $) $( [28-Jul-1999] $)

    $( Distribution of negative over subtraction. $)
    negsubdii $p |- -u ( A - B ) = ( -u A + B ) $=
      ( cneg caddc co cmin negcli negdii negsubi negeqi negnegi opreq2i
      3eqtr3i ) ABEZFGZEAEZPEZFGABHGZERBFGAPCBDIJQTABCDKLSBRFBDMNO $.
      $( [6-Aug-1999] $)

    $( Distribution of negative over subtraction. $)
    negsubdi2i $p |- -u ( A - B ) = ( B - A ) $=
      ( cmin co cneg caddc negsubdii negcli addcomi negsubi 3eqtri ) ABEFGAGZBH
      FBNHFBAEFABCDINBACJDKBADCLM $.
      $( [1-Oct-1999] $)
  $}

  $( Product with negative is negative of product.  Theorem I.12 of [Apostol]
     p. 18. $)
  mulneg1 $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( -u A x. B ) = -u ( A x. B ) ) $=
    ( cc wcel cneg cmul co wceq cc0 cif negeq opreq1d opreq1 negeqd eqeq12d
    opreq2 0cn elimel mulneg1i dedth2h ) ACDZBCDZAEZBFGZABFGZEZHUAAIJZEZBFGZUGB
    FGZEZHUHUBBIJZFGZUGULFGZEZHABIIAUGHZUDUIUFUKUPUCUHBFAUGKLUPUEUJAUGBFMNOBULH
    ZUIUMUKUOBULUHFPUQUJUNBULUGFPNOUGULAICQRBICQRST $.
    $( [14-May-1999] $)

  $( The product with a negative is the negative of the product. $)
  mulneg2 $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( A x. -u B ) = -u ( A x. B ) ) $=
    ( cc wcel wa cneg cmul co wceq mulneg1 ancoms negcl mulcom sylan2 negeqd
    3eqtr4d ) ACDZBCDZEZBFZAGHZBAGHZFZATGHZABGHZFRQUAUCIBAJKRQTCDUDUAIBLATMNSUE
    UBABMOP $.
    $( [30-Jul-2004] $)

  $( Swap the negative sign in a product. $)
  mulneg12 $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( -u A x. B ) = ( A x. -u B ) ) $=
    ( cc wcel wa cneg cmul co mulneg1 mulneg2 eqtr4d ) ACDBCDEAFBGHABGHFABFGHAB
    IABJK $.
    $( [30-Jul-2004] $)

  $( Product of two negatives.  Theorem I.12 of [Apostol] p. 18.  (The proof
     was shortened by Andrew Salmon, 19-Nov-2011.) $)
  mul2neg $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A x. -u B ) = ( A x. B ) ) $=
    ( cc wcel wa cneg cmul co wceq negcl mulneg12 sylan2 negneg adantl opreq2d
    eqtrd ) ACDZBCDZEZAFBFZGHZATFZGHZABGHRQTCDUAUCIBJATKLSUBBAGRUBBIQBMNOP $.
    $( [19-Nov-2011] $) $( [30-Jul-2004] $)

  $( Distribution of negative over addition. $)
  negdi $p |- ( ( A e. CC /\ B e. CC ) ->
                 -u ( A + B ) = ( -u A + -u B ) ) $=
    ( cc wcel caddc co cneg wceq cc0 cif opreq1 negeqd negeq opreq1d eqeq12d
    opreq2 opreq2d 0cn elimel negdii dedth2h ) ACDZBCDZABEFZGZAGZBGZEFZHUBAIJZB
    EFZGZUIGZUGEFZHUIUCBIJZEFZGZULUNGZEFZHABIIAUIHZUEUKUHUMUSUDUJAUIBEKLUSUFULU
    GEAUIMNOBUNHZUKUPUMURUTUJUOBUNUIEPLUTUGUQULEBUNMQOUIUNAICRSBICRSTUA $.
    $( [10-May-2004] $)

  $( Distribution of negative over addition. $)
  negdi2 $p |- ( ( A e. CC /\ B e. CC ) ->
                 -u ( A + B ) = ( -u A - B ) ) $=
    ( cc wcel wa caddc co cneg cmin negdi wceq negcl negsub sylan eqtrd ) ACDZB
    CDZEABFGHAHZBHFGZRBIGZABJPRCDQSTKALRBMNO $.
    $( [1-Jan-2006] $)

  $( Distribution of negative over subtraction. $)
  negsubdi $p |- ( ( A e. CC /\ B e. CC ) -> -u ( A - B ) = ( -u A + B ) ) $=
    ( cc wcel wa cneg caddc co cmin wceq negcl negdi sylan2 negsub negeqd
    negneg adantl opreq2d 3eqtr3d ) ACDZBCDZEZABFZGHZFZAFZUCFZGHZABIHZFUFBGHUAT
    UCCDUEUHJBKAUCLMUBUDUIABNOUBUGBUFGUAUGBJTBPQRS $.
    $( [1-Aug-2006] $) $( [15-Nov-2004] $)

  $( Distribution of negative over subtraction. $)
  negsubdi2 $p |- ( ( A e. CC /\ B e. CC ) -> -u ( A - B ) = ( B - A ) ) $=
    ( cc wcel wa cmin co cneg caddc negsubdi wceq negcl addcom sylan negsub
    ancoms 3eqtrd ) ACDZBCDZEABFGHAHZBIGZBTIGZBAFGZABJRTCDSUAUBKALTBMNSRUBUCKBA
    OPQ $.
    $( [1-Aug-2006] $) $( [4-Oct-1999] $)

  $( Relationship between subtraction and negative.  (Contributed by Paul
     Chapman, 8-Oct-2007.) $)
  neg2sub $p |- ( ( A e. CC /\ B e. CC ) -> ( -u A - -u B ) = ( B - A ) ) $=
    ( cc wcel wa cneg cmin co caddc wceq negcl subneg sylan negsubdi negsubdi2
    3eqtr2d ) ACDZBCDZEAFZBFGHZSBIHZABGHFBAGHQSCDRTUAJAKSBLMABNABOP $.
    $( [8-Oct-2007] $)

  $( Convert a subtraction to addition using multiplication by a negative. $)
  submul2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( A - ( B x. C ) ) = ( A + ( B x. -u C ) ) ) $=
    ( cc wcel cmul co cmin cneg caddc wceq wa mulneg2 adantl opreq2d mulcl
    negsub sylan2 eqtr2d 3impb ) ADEZBDEZCDEZABCFGZHGZABCIFGZJGZKUAUBUCLZLZUGAU
    DIZJGZUEUIUFUJAJUHUFUJKUABCMNOUHUAUDDEUKUEKBCPAUDQRST $.
    $( [2-Feb-2007] $)

  $( Law for double subtraction. $)
  subsub2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( A - ( B - C ) ) = ( A + ( C - B ) ) ) $=
    ( cc wcel w3a cmin co cneg caddc wceq wa subcl negsub sylan2 3impb
    negsubdi2 opreq2d 3adant1 eqtr3d ) ADEZBDEZCDEZFABCGHZIZJHZAUDGHZACBGHZJHZU
    AUBUCUFUGKZUBUCLZUAUDDEUJBCMAUDNOPUBUCUFUIKUAUKUEUHAJBCQRST $.
    $( [2-Aug-2006] $) $( [30-Jun-2005] $)

  $( Law for double subtraction. $)
  subsub $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( A - ( B - C ) ) = ( ( A - B ) + C ) ) $=
    ( cc wcel w3a cmin co caddc subsub2 wceq addsubass addsub eqtr3d 3com23
    eqtrd ) ADEZBDEZCDEZFABCGHGHACBGHIHZABGHCIHZABCJQSRTUAKQSRFACIHBGHTUAACBLAC
    BMNOP $.
    $( [13-May-2004] $)

  $( Law for double subtraction. $)
  subsub3 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( A - ( B - C ) ) = ( ( A + C ) - B ) ) $=
    ( cc wcel w3a cmin co caddc subsub2 wceq addsubass 3com23 eqtr4d ) ADEZBDEZ
    CDEZFABCGHGHACBGHIHZACIHBGHZABCJOQPSRKACBLMN $.
    $( [27-Jul-2005] $)

  $( Law for double subtraction. $)
  subsub4 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A - B ) - C ) = ( A - ( B + C ) ) ) $=
    ( cc wcel w3a cneg cmin co caddc wceq negcl subsub syl3an3 subneg 3adant1
    opreq2d wa subcl negsub sylan 3impa 3eqtr3rd ) ADEZBDEZCDEZFZABCGZHIZHIZABH
    IZUHJIZABCJIZHIUKCHIZUFUDUEUHDEUJULKCLABUHMNUGUIUMAHUEUFUIUMKUDBCOPQUDUEUFU
    LUNKZUDUERUKDEUFUOABSUKCTUAUBUC $.
    $( [2-Aug-2006] $) $( [19-Aug-2005] $)

  $( Swap the second and third terms in a double subtraction. $)
  sub23 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A - B ) - C ) = ( ( A - C ) - B ) ) $=
    ( cc wcel w3a caddc co cmin wceq addcom 3adant1 opreq2d subsub4 3com23
    3eqtr4d ) ADEZBDEZCDEZFZABCGHZIHACBGHZIHZABIHCIHACIHBIHZTUAUBAIRSUAUBJQBCKL
    MABCNQSRUDUCJACBNOP $.
    $( [19-Aug-2005] $)

  $( Cancellation law for subtraction. $)
  nnncan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - ( B - C ) ) - C ) = ( A - B ) ) $=
    ( cc wcel w3a cmin co caddc wceq subcl 3adant1 subsub4 syld3an2 wa npcan
    opreq2d eqtrd ) ADEZBDEZCDEZFABCGHZGHCGHZAUBCIHZGHZABGHZSUBDEZTUAUCUEJTUAUG
    SBCKLAUBCMNTUAUEUFJSTUAOUDBAGBCPQLR $.
    $( [4-Sep-2005] $)

  $( Cancellation law for subtraction.  (The proof was shortened by Andrew
     Salmon, 19-Nov-2011.) $)
  nnncan1 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) - ( A - C ) ) = ( C - B ) ) $=
    ( cc wcel w3a cmin co wceq subcl 3adant2 sub23 syld3an3 wa caddc subsub2
    3anidm12 pncan3 eqtrd opreq1d ) ADEZBDEZCDEZFZABGHACGHZGHZAUEGHZBGHZCBGHUAU
    BUCUEDEZUFUHIUAUCUIUBACJKABUELMUDUGCBGUAUCUGCIUBUAUCNUGACAGHOHZCUAUCUGUJIAA
    CPQACRSKTS $.
    $( [19-Nov-2011] $) $( [8-Feb-2005] $)

  $( Cancellation law for subtraction. $)
  nnncan2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - C ) - ( B - C ) ) = ( A - B ) ) $=
    ( cc wcel w3a cmin co wceq subcl 3adant1 sub23 syld3an2 nnncan eqtr3d ) ADE
    ZBDEZCDEZFABCGHZGHCGHZACGHSGHZABGHPSDEZQRTUAIQRUBPBCJKASCLMABCNO $.
    $( [1-Oct-2005] $)

  $( Cancellation law for subtraction.  (The proof was shortened by Andrew
     Salmon, 19-Nov-2011.) $)
  nncan $p |- ( ( A e. CC /\ B e. CC ) -> ( A - ( A - B ) ) = B ) $=
    ( cc wcel wa cmin co caddc wceq subsub2 3anidm12 pncan3 eqtrd ) ACDZBCDZEAA
    BFGFGZABAFGHGZBNOPQIAABJKABLM $.
    $( [19-Nov-2011] $) $( [21-Jun-2005] $)

  $( Cancellation law for subtraction. $)
  nppcan2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - ( B + C ) ) + C ) = ( A - B ) ) $=
    ( cc wcel w3a caddc co cmin wceq addcl 3adant1 subsub syld3an2 pncan
    opreq2d eqtr3d ) ADEZBDEZCDEZFZABCGHZCIHZIHZAUBIHCGHZABIHRUBDEZSTUDUEJSTUFR
    BCKLAUBCMNUAUCBAISTUCBJRBCOLPQ $.
    $( [29-Sep-2005] $)

  $( Product with minus one is negative. $)
  mulm1 $p |- ( A e. CC -> ( -u 1 x. A ) = -u A ) $=
    ( cc wcel c1 cneg cmul co wceq ax1cn mulneg1 mpan mulid2 negeqd eqtrd ) ABC
    ZDEAFGZDAFGZEZAEDBCOPRHIDAJKOQAALMN $.
    $( [16-Nov-1999] $)

  ${
    mulm1.1 $e |- A e. CC $.
    $( Product with minus one is negative. $)
    mulm1i $p |- ( -u 1 x. A ) = -u A $=
      ( cc wcel c1 cneg cmul co wceq mulm1 ax-mp ) ACDEFAGHAFIBAJK $.
      $( [31-Jul-1999] $)
  $}

  $( Rearrangement of 4 terms in a mixed addition and subtraction. $)
  addsub4 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A + B ) - ( C + D ) ) = ( ( A - C ) + ( B - D ) ) ) $=
    ( cc wcel wa caddc co cneg cmin wceq negdi adantl opreq2d negcl anim12i
    add4 sylan2 eqtrd addcl negsub syl2an opreqan12d an4s 3eqtr3d ) AEFZBEFZGZC
    EFZDEFZGZGZABHIZCDHIZJZHIZACJZHIZBDJZHIZHIZUNUOKIZACKIZBDKIZHIZUMUQUNURUTHI
    ZHIZVBUMUPVGUNHULUPVGLUICDMNOULUIUREFZUTEFZGVHVBLUJVIUKVJCPDPQABURUTRSTUIUN
    EFUOEFUQVCLULABUACDUAUNUOUBUCUGUJUHUKVBVFLUGUJGUHUKGUSVDVAVEHACUBBDUBUDUEUF
    $.
    $( [4-Mar-2005] $)

  ${
    addsub4.1 $e |- A e. CC $.
    addsub4.2 $e |- B e. CC $.
    addsub4.3 $e |- C e. CC $.
    addsub4.4 $e |- D e. CC $.
    $( Rearrangement of 4 terms in a mixed addition and subtraction. $)
    addsub4i $p |- ( ( A + B ) - ( C + D ) ) =
                 ( ( A - C ) + ( B - D ) ) $=
      ( cc wcel caddc co cmin wceq addsub4 mp4an ) AIJBIJCIJDIJABKLCDKLMLACMLBD
      MLKLNEFGHABCDOP $.
      $( [17-Oct-1999] $)
  $}

  $( Rearrangement of 4 terms in a mixed addition and subtraction. $)
  subadd4 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A - B ) - ( C - D ) ) = ( ( A + D ) - ( B + C ) ) ) $=
    ( cc wcel wa cmin co caddc wceq subcl subsub2 3expb sylan addsub4 an42s
    eqtr4d ) AEFZBEFZGZCEFZDEFZGZGABHIZCDHIHIZUEDCHIJIZADJIBCJIHIZUAUEEFZUDUFUG
    KZABLUIUBUCUJUECDMNOSUCTUBUHUGKADBCPQR $.
    $( [24-Aug-2006] $)

  $( Rearrangement of 4 terms in a subtraction. $)
  sub4 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A - B ) - ( C - D ) ) = ( ( A - C ) - ( B - D ) ) ) $=
    ( cc wcel wa caddc co cmin wceq addcom ad2ant2lr opreq2d subadd4 an4s
    3eqtr4d ) AEFZBEFZGCEFZDEFZGGZADHIZBCHIZJIUCCBHIZJIZABJICDJIJIACJIBDJIJIZUB
    UDUEUCJSTUDUEKRUABCLMNABCDORTSUAUGUFKACBDOPQ $.
    $( [23-Nov-2007] $)

  $( Product of two differences. $)
  mulsub $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
               ( ( A - B ) x. ( C - D ) ) =
       ( ( ( A x. C ) + ( D x. B ) ) - ( ( A x. D ) + ( C x. B ) ) ) ) $=
    ( cc wcel wa cneg caddc co cmul cmin negsub opreqan12d wceq negcl muladd
    sylanr2 sylanl2 mul2neg ancoms opreq2d ad2ant2l mulneg2 mulcl negdi syl2an
    eqtr4d ancom2s an42s opreq12d addcl an4s syl11anc 3eqtrd eqtr3d ) AEFZBEFZG
    ZCEFZDEFZGZGZABHZIJZCDHZIJZKJZABLJZCDLJZKJACKJZDBKJZIJZADKJZCBKJZIJZLJZUSVB
    VEVIVGVJKABMCDMNVCVHVKVFVDKJZIJZAVFKJZCVDKJZIJZIJZVMVPHZIJZVQURUQVDEFZVBVHW
    COZBPVAUQWFGUTVFEFWGDPAVDCVFQRSVCVSVMWBWDIURVAVSVMOUQUTURVAGZVRVLVKIVAURVRV
    LODBTUAUBUCUQVAURUTWBWDOZUQVAGZUTURWIWJUTURGZGWBVNHZVOHZIJZWDWJWKVTWLWAWMIA
    DUDCBUDNWJVNEFZVOEFZWDWNOWKADUEZCBUEZVNVOUFUGUHUIUJUKVCVMEFZVPEFZWEVQOUQUTU
    RVAWSUQUTGVKEFVLEFZWSWHACUEVAURXADBUEUAVKVLULUGUMUQVAURUTWTWJWOWPWTURUTGWQU
    TURWPWRUAVNVOULUGUJVMVPMUNUOUP $.
    $( [14-Jan-2006] $)

  $( Cancellation law for mixed addition and subtraction. $)
  pnpcan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) - ( A + C ) ) = ( B - C ) ) $=
    ( cc wcel caddc co cmin wceq wa addsub4 anandis cc0 subid opreq1d subcl
    addid2 syl sylan9eq eqtrd 3impb ) ADEZBDEZCDEZABFGACFGHGZBCHGZIUBUCUDJZJUEA
    AHGZUFFGZUFUBUCUDUEUIIABACKLUBUGUIMUFFGZUFUBUHMUFFANOUGUFDEUJUFIBCPUFQRSTUA
    $.
    $( [4-Mar-2005] $)

  $( Cancellation law for mixed addition and subtraction.  (Contributed by
     Scott Fenton, 9-Jun-2006.) $)
  pnpcan2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A + C ) - ( B + C ) ) = ( A - B ) ) $=
    ( cc wcel w3a caddc co cmin wceq addcom 3adant2 3adant1 opreq12d pnpcan
    3coml eqtrd ) ADEZBDEZCDEZFZACGHZBCGHZIHCAGHZCBGHZIHZABIHZUAUBUDUCUEIRTUBUD
    JSACKLSTUCUEJRBCKMNTRSUFUGJCABOPQ $.
    $( [9-Jun-2006] $)

  $( Cancellation law for mixed addition and subtraction. $)
  pnncan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) - ( A - C ) ) = ( B + C ) ) $=
    ( cc wcel w3a caddc co cneg cmin wceq negcl pnpcan syl3an3 negsub 3adant2
    opreq2d subneg 3adant1 3eqtr3d ) ADEZBDEZCDEZFZABGHZACIZGHZJHZBUFJHZUEACJHZ
    JHBCGHZUCUAUBUFDEUHUIKCLABUFMNUDUGUJUEJUAUCUGUJKUBACOPQUBUCUIUKKUABCRST $.
    $( [2-Aug-2006] $) $( [30-Jun-2005] $)

  $( Cancellation law for mixed addition and subtraction. $)
  ppncan $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) + ( C - B ) ) = ( A + C ) ) $=
    ( cc wcel w3a caddc co cmin wceq addcom 3adant3 opreq1d addcl subsub2
    syld3an1 pnncan 3com12 3eqtr3d ) ADEZBDEZCDEZFZABGHZBCIHZIHZBAGHZUEIHZUDCBI
    HGHZACGHZUCUDUGUEITUAUDUGJUBABKLMUDDEZUATUBUFUIJTUAUKUBABNLUDBCOPUATUBUHUJJ
    BACQRS $.
    $( [30-Jun-2005] $)

  ${
    pnncan.1 $e |- A e. CC $.
    pnncan.2 $e |- B e. CC $.
    pnncan.3 $e |- C e. CC $.
    $( Cancellation law for mixed addition and subtraction. $)
    pnncani $p |- ( ( A + B ) - ( A - C ) ) = ( B + C ) $=
      ( cc wcel caddc co cmin wceq pnncan mp3an ) AGHBGHCGHABIJACKJKJBCIJLDEFAB
      CMN $.
      $( [14-Jan-2006] $)
  $}

