$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Principle of mathematical induction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Substitutions. $)
    nnind.1 $e |- ( x = 1 -> ( ph <-> ps ) ) $.
    nnind.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    nnind.3 $e |- ( x = ( y + 1 ) -> ( ph <-> th ) ) $.
    nnind.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    nnind.5 $e |- ps $.
    $( Induction hypothesis. $)
    nnind.6 $e |- ( y e. NN -> ( ch -> th ) ) $.
    $( Principle of Mathematical Induction (inference schema).  The first
       four hypotheses give us the substitution instances we need; the last
       two are the basis and the induction hypothesis.  See ~ nnaddclt for
       an example of its use.  See ~ nn0ind for induction on nonnegative
       integers and ~ uzind , ~ uzind4 for induction on an arbitrary set of
       upper integers.  See ~ indstr for strong induction. $)
    nnind $p |- ( A e. NN -> ta ) $=
      ( cn wcel crab wa c1 cv caddc co wral wss elrab 1nn mpbir2an ssrab2
      sseli peano2nn a1d anim12d 3imtr4g mpcom rgen nnex rabex peano5nn mp2an
      sylib pm3.27d ) HOPZVBEVBHAFOQZPVBEROVCHSVCPZGTZSUAUBZVCPZGVCUCOVCUDVDSOP
      BABFSOIUEUFMUGVGGVCVEOPZVEVCPZVGVCOVEAFOUHUIVHVHCRVFOPZDRVIVGVHVHVJCDVHVJ
      VHVEUJUKNULACFVEOJUEADFVFOKUEUMUNUOGVCAFOUPUQURUSUIAEFHOLUEUTVA $.
      $( [10-Jan-1997] $)
  $}

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Induction hypothesis. $)
    nnindALT.6 $e |- ( y e. NN -> ( ch -> th ) ) $.
    $( Basis. $)
    nnindALT.5 $e |- ps $.
    $( Substitutions. $)
    nnindALT.1 $e |- ( x = 1 -> ( ph <-> ps ) ) $.
    nnindALT.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    nnindALT.3 $e |- ( x = ( y + 1 ) -> ( ph <-> th ) ) $.
    nnindALT.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Principle of Mathematical Induction (inference schema).  The last
       four hypotheses give us the substitution instances we need; the first
       two are the induction hypothesis and the basis.  (This ALT version of
       ~ nnind is easier to use with the Proof Assistant since 'assign last'
       will be applied to the substitution instances first.  We may switch to
       it as the official version.) $)
    nnindALT $p |- ( A e. NN -> ta ) $=
      ( nnind ) ABCDEFGHKLMNJIO $.
      $( [19-Aug-2007] $) $( [7-Dec-2005] $)
  $}

