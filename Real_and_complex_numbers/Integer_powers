$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Integer powers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ^ $. $( Exponentiation. $)

  $( Extend class notation to include exponentiation of a complex number to an
     integer power. $)
  cexp $a class ^ $.

  ${
    $d x y $.
    $( Define exponentiation to nonnegative integer powers.  This definition is
       not meant to be used directly; instead, ~ exp0 and ~ expp1 provide a the
       standard recursive definition.  The up-arrow notation is used by Donald
       Knuth for iterated exponentiation (_Science_ 194, 1235-1242, 1976) and
       is convenient for us since we don't have superscripts. 10-Jun-2005:  The
       definition was extended to include zero exponents, so that ` 0 ^ 0 = 1 `
       per the convention of Definition 10-4.1 of [Gleason] p. 134.  (Based on
       definition contributed by Raph Levien, 15-Oct-2004.) $)
    df-exp $a |- ^ = ( x e. CC , y e. NN0 |-> (
      seq 0 ( x. , ( { <. 0 , 1 >. } u. ( NN X. { x } ) ) ) ` y ) ) $.

    $(
    Raph Levien's original definition from set.mm of 15-Oct-2004:
    df-exp $a |- ^ = { <. <. x , y >. , z >. | ( ( x e. CC /\ y e. NN0 ) /\
          z = ( ( rec ( { <. w , v >. | v = ( w x. x ) } , 1 ) o.
        `' ( rec ( { <. w , v >. | v = ( w + 1 ) } , 0 ) |` om ) ) ` y ) ) } $.
    $)
  $}

  ${
    $d x y A $.  $d y N $.
    $( Value of exponentiation to nonnegative integer powers.  (Revised by
       Mario Carneiro, 2-Jul-2013.) $)
    expval $p |- ( ( A e. CC /\ N e. NN0 ) -> ( A ^ N ) =
      ( seq 0 ( x. , ( { <. 0 , 1 >. } u. ( NN X. { A } ) ) ) ` N ) ) $=
      ( vx vy cc cn0 cv cmul cc0 c1 cop csn cn cxp cun cseq cfv cexp wceq sneq
      xpeq2d uneq2d seqeq3d fveq1d fveq2 df-exp fvex ovmpt2 ) CDABEFDGZHIJKLZMC
      GZLZNZOZIPZQBHUJMALZNZOZIPZQRUIUSQUKASZUIUOUSUTUNURHIUTUMUQUJUTULUPMUKATU
      AUBUCUDUIBUSUECDUFBUSUGUH $.
      $( [2-Jul-2013] $) $( [20-May-2004] $)

    $( Value of a complex number raised to the 0th power.  Note that under our
       definition, ` 0 ^ 0 = 1 ` , following the convention used by Gleason.
       Part of Definition 10-4.1 of [Gleason] p. 134.  (Revised by Mario
       Carneiro, 2-Jul-2013.) $)
    exp0 $p |- ( A e. CC -> ( A ^ 0 ) = 1 ) $=
      ( cc wcel cc0 cexp co cmul c1 cop csn cn cxp cun wceq cz ax-mp elexi cres
      cfv 0z cseq 0nn0 expval mpan2 seq1NEW ax-1cn cdif 0nnn difsn reseq2i wrel
      cn0 wn cdm relxp dmxpss relssres mp2an eqtr2i uneq2i fvsnun1 eqtri syl6eq
      wss ) ABCZADEFZDGDHIJZKAJZLZMZDUASZHVEDULCVFVKNUBADUCUDVKDVJSZHDOCVKVLNTG
      VJDUEPDHKVIVJDOTQHBUFQVIVIKDJUGZRZVGVNVIKRZVIVMKVIDKCUMVMKNUHDKUIPUJVIUKV
      IUNKVDVOVINKVHUOKVHUPVIKUQURUSUTVAVBVC $.
      $( [2-Jul-2013] $) $( [20-May-2004] $)
  $}

  $( Value of a complex number raised to a nonnegative integer power plus one.
     Part of Definition 10-4.1 of [Gleason] p. 134.  (Revised by Mario
     Carneiro, 2-Jul-2013.) $)
  expp1 $p |- ( ( A e. CC /\ N e. NN0 ) -> ( A ^ ( N + 1 ) ) =
               ( ( A ^ N ) x. A ) ) $=
    ( cc wcel cn0 wa c1 co cmul cc0 csn cn cexp wceq adantl expval sylan2 elexi
    cfv cres caddc cop cxp cun cseq cuz seqp1 nn0uz eleq2s peano2nn0 nn0p1nn wn
    cdif 0nnn difsn ax-mp syl6eleqr 0cn ax-1cn reseq2i wrel cdm dmxpss relssres
    wss relxp mp2an eqtr2i uneq2i fvsnun2 syl fvconst2g eqtr2d opreq12d 3eqtr4d
    ) ACDZBEDZFZBGUAHZIJGUBKZLAKZUCZUDZJUEZSZBWDSZVSWCSZIHZAVSMHZABMHZAIHVQWEWH
    NZVPWKBJUFSEIWCJBUGUHUIOVQVPVSEDWIWENBUJAVSPQVRWJWFAWGIABPVRWGVSWBSZAVQWGWL
    NZVPVQVSLJKUMZDWMVQVSLWNBUKZJLDULWNLNUNJLUOUPZUQJGLVSWBWCJCURRGCUSRWBWBWNTZ
    VTWQWBLTZWBWNLWBWPUTWBVAWBVBLVEWRWBNLWAVFLWAVCWBLVDVGVHVIVJVKOVQVPVSLDWLANW
    OLAVSCVLQVMVNVO $.
    $( [2-Jul-2013] $) $( [20-May-2005] $)

  $( Value of a complex number raised to the first power.  (Revised by Mario
     Carneiro, 2-Jul-2013.) $)
  exp1 $p |- ( A e. CC -> ( A ^ 1 ) = A ) $=
    ( cc wcel c1 cexp cc0 caddc wceq ax-1cn addid2 ax-mp opreq2i cmul cn0 expp1
    co 0nn0 mpan2 exp0 opreq1d mulid2 3eqtrd syl5eqr ) ABCZADEPAFDGPZEPZAUEDAED
    BCUEDHIDJKLUDUFAFEPZAMPZDAMPAUDFNCUFUHHQAFORUDUGDAMASTAUAUBUC $.
    $( [2-Jul-2013] $) $( [20-Oct-2004] $)

  ${
    $d x y z w A $.  $d z B $.  $d x y z w F $.
    expcllem.1 $e |- F C_ CC $.
    expcllem.2 $e |- ( ( x e. F /\ y e. F ) -> ( x x. y ) e. F ) $.
    expcllem.3 $e |- 1 e. F $.
    $( Lemma for proving nonnegative integer exponentiation closure laws. $)
    expcllem $p |- ( ( A e. F /\ B e. NN0 ) -> ( A ^ B ) e. F ) $=
      ( wcel cc0 wceq cexp co cv wi c1 opreq2 eleq1d imbi2d cmul vz vw cn elnn0
      cn0 wo caddc cc sseli exp1 syl wa opreq1 vtocl2ga ancoms adantlr wb nnnn0
      ibir expp1 syl2an adantr mpbird exp31 com12 a2d impcom sylan9eqr syl6eqel
      nnind exp0 jaodan sylan2b ) DUEICEIZDUCIZDJKZUFCDLMZEIZDUDVNVOVRVPVOVNVRV
      NCUANZLMZEIZOVNCPLMZEIZOVNCUBNZLMZEIZOVNCWDPUGMZLMZEIZOVNVROUAUBDVSPKZWAW
      CVNWJVTWBEVSPCLQRSVSWDKZWAWFVNWKVTWEEVSWDCLQRSVSWGKZWAWIVNWLVTWHEVSWGCLQR
      SVSDKZWAVRVNWMVTVQEVSDCLQRSVNWCVNWBCEVNCUHIZWBCKEUHCFUIZCUJUKRUSWDUCIZVNW
      FWIVNWPWFWIOVNWPWFWIVNWPULZWFULWIWECTMZEIZVNWFWSWPWFVNWSANZBNZTMZEIWEXATM
      ZEIWSABWECEEWTWEKXBXCEWTWEXATUMRXACKXCWREXACWETQRGUNUOUPWQWIWSUQWFWQWHWRE
      VNWNWDUEIWHWRKWPWOWDURCWDUTVARVBVCVDVEVFVJVGVNVPULVQPEVPVNVQCJLMZPDJCLQVN
      WNXDPKWOCVKUKVHHVIVLVM $.
      $( [14-Dec-2005] $)
  $}

  ${
    $d x y A $.  $d x y N $.
    $( Closure of exponentiation of nonnegative integers. $)
    nnexpcl $p |- ( ( A e. NN /\ N e. NN0 ) -> ( A ^ N ) e. NN ) $=
      ( vx vy cn nnsscn cv nnmulcl 1nn expcllem ) CDABEFCGDGHIJ $.
      $( [16-Dec-2005] $)

    $( Closure of exponentiation of nonnegative integers. $)
    nn0expcl $p |- ( ( A e. NN0 /\ N e. NN0 ) -> ( A ^ N ) e. NN0 ) $=
      ( vx vy cn0 nn0sscn cv nn0mulcl 1nn0 expcllem ) CDABEFCGDGHIJ $.
      $( [14-Dec-2005] $)

    $( Closure of exponentiation of integers. $)
    zexpcl $p |- ( ( A e. ZZ /\ N e. NN0 ) -> ( A ^ N ) e. ZZ ) $=
      ( vx vy cz zsscn cv zmulcl 1z expcllem ) CDABEFCGDGHIJ $.
      $( [16-Dec-2005] $)

    $( Closure of exponentiation of rationals. $)
    qexpcl $p |- ( ( A e. QQ /\ N e. NN0 ) -> ( A ^ N ) e. QQ ) $=
      ( vx vy cq qsscn cv qmulcl c1 cz wcel 1z zq ax-mp expcllem ) CDABEFCGDGHI
      JKIEKLIMNO $.
      $( [16-Dec-2005] $)

    $( Closure of exponentiation of reals. $)
    reexpcl $p |- ( ( A e. RR /\ N e. NN0 ) -> ( A ^ N ) e. RR ) $=
      ( vx vy cr ax-resscn cv remulcl 1re expcllem ) CDABEFCGDGHIJ $.
      $( [14-Dec-2005] $)

    $( Closure law for nonnegative integer exponentiation. $)
    expcl $p |- ( ( A e. CC /\ N e. NN0 ) -> ( A ^ N ) e. CC ) $=
      ( vx vy cc ssid cv mulcl ax-1cn expcllem ) CDABEEFCGDGHIJ $.
      $( [26-May-2005] $)

    $( Closure law for exponentiation of positive reals. $)
    rpexpcl $p |- ( ( A e. RR+ /\ N e. NN0 ) -> ( A ^ N ) e. RR+ ) $=
      ( vx vy crp cr cc rpssre ax-resscn sstri cv rpmulcl 1rp expcllem ) CDABEE
      FGHIJCKDKLMN $.
      $( [24-Feb-2008] $)
  $}

  $( Exponentiation in terms of predecessor exponent. $)
  expm1t $p |- ( ( A e. CC /\ N e. NN ) -> ( A ^ N ) =
               ( ( A ^ ( N - 1 ) ) x. A ) ) $=
    ( cc wcel cn wa c1 cmin co caddc cexp cmul wceq nncn ax-1cn sylancl opreq2d
    npcan adantl cn0 nnm1nn0 expp1 sylan2 eqtr3d ) ACDZBEDZFABGHIZGJIZKIZABKIZA
    UGKIALIZUFUIUJMUEUFUHBAKUFBCDGCDUHBMBNOBGRPQSUFUEUGTDUIUKMBUAAUGUBUCUD $.
    $( [19-Dec-2005] $)

  ${
    $d j k N $.
    $( Value of one raised to a nonnegative integer power. $)
    1exp $p |- ( N e. NN0 -> ( 1 ^ N ) = 1 ) $=
      ( vj vk c1 cv cexp co wceq cc0 caddc opreq2 eqeq1d wcel ax-1cn exp0 ax-mp
      cc cn0 cmul opreq1 mulid1i syl6eq expp1 mpan syl5ibr nn0ind ) DBEZFGZDHDI
      FGZDHZDCEZFGZDHZDUKDJGZFGZDHZDAFGZDHBCAUGIHUHUIDUGIDFKLUGUKHUHULDUGUKDFKL
      UGUNHUHUODUGUNDFKLUGAHUHUQDUGADFKLDQMZUJNDOPUMUPUKRMZULDSGZDHUMUTDDSGDULD
      DSTDNUAUBUSUOUTDURUSUOUTHNDUKUCUDLUEUF $.
      $( [15-Dec-2005] $)
  $}

  ${
    $d j k A $.  $d j k N $.
    $( Natural number exponentiation is 0 iff its mantissa is 0. $)
    expeq0 $p |- ( ( A e. CC /\ N e. NN ) -> ( ( A ^ N ) = 0 <-> A = 0 ) ) $=
      ( vj vk cn wcel cc cexp co cc0 wb cv wi c1 opreq2 eqeq1d bibi1d imbi2d wa
      wceq caddc exp1 wo cn0 nnnn0 cmul expp1 expcl simpl mul0or syl2anc sylan2
      bitrd bi1 idd jaod olc impbid1 sylan9bb exp31 com12 a2d nnind impcom ) BE
      FAGFZABHIZJTZAJTZKZVEACLZHIZJTZVHKZMVEANHIZJTZVHKZMVEADLZHIZJTZVHKZMVEAVQ
      NUAIZHIZJTZVHKZMVEVIMCDBVJNTZVMVPVEWEVLVOVHWEVKVNJVJNAHOPQRVJVQTZVMVTVEWF
      VLVSVHWFVKVRJVJVQAHOPQRVJWATZVMWDVEWGVLWCVHWGVKWBJVJWAAHOPQRVJBTZVMVIVEWH
      VLVGVHWHVKVFJVJBAHOPQRVEVNAJAUBPVQEFZVEVTWDVEWIVTWDMVEWIVTWDVEWISWCVSVHUC
      ZVTVHWIVEVQUDFZWCWJKVQUEVEWKSZWCVRAUFIZJTZWJWLWBWMJAVQUGPWLVRGFVEWNWJKAVQ
      UHVEWKUIVRAUJUKUMULVTWJVHVTVSVHVHVSVHUNVTVHUOUPVHVSUQURUSUTVAVBVCVD $.
      $( [27-Jul-2006] $) $( [23-Feb-2005] $)

    $( Natural number exponentiation is nonzero iff its mantissa is nonzero. $)
    expne0 $p |- ( ( A e. CC /\ N e. NN ) ->
                  ( ( A ^ N ) =/= 0 <-> A =/= 0 ) ) $=
      ( cc wcel cn wa cexp co cc0 expeq0 necon3bid ) ACDBEDFABGHIAIABJK $.
      $( [10-Jul-2008] $) $( [6-May-2005] $)

    $( Nonnegative integer exponentiation is nonzero if its mantissa is
       nonzero. $)
    expne0i $p |- ( ( A e. CC /\ A =/= 0 /\ N e. NN0 )
                -> ( A ^ N ) =/= 0 ) $=
      ( cc wcel cc0 wne cn0 cexp co wa cn wceq wo elnn0 expne0 biimpar an32s c1
      ax-1ne0 opreq2 exp0 sylan9eqr neeq1d mpbiri adantlr jaodan sylan2b 3impa
      ) ACDZAEFZBGDZABHIZEFZUKUIUJJZBKDZBELZMUMBNUNUOUMUPUIUOUJUMUIUOJUMUJABOPQ
      UIUPUMUJUIUPJZUMREFSUQULREUPUIULAEHIRBEAHTAUAUBUCUDUEUFUGUH $.
      $( [1-Jan-2009] $) $( [2-Aug-2006] $)

    $( Nonnegative integer exponentiation with a positive mantissa is
       positive. $)
    expgt0 $p |- ( ( A e. RR /\ N e. NN0 /\ 0 < A ) -> 0 < ( A ^ N ) ) $=
      ( vj vk cr wcel cn0 cc0 clt cexp co wi wa cv c1 wceq opreq2 breq2d imbi2d
      wbr caddc lt01 recn exp0 syl syl5breqr adantr cmul reexpcl simpl axmulgt0
      syl2anc expp1 sylan sylibrd exp4b com12 com34 imp3a a2d nn0ind exp3a 3imp
      cc ) AEFZBGFZHAITZHABJKZITZVFVEVGVILVFVEVGVIVEVGMZHACNZJKZITZLVJHAHJKZITZ
      LVJHADNZJKZITZLVJHAVPOUAKZJKZITZLVJVILCDBVKHPZVMVOVJWBVLVNHIVKHAJQRSVKVPP
      ZVMVRVJWCVLVQHIVKVPAJQRSVKVSPZVMWAVJWDVLVTHIVKVSAJQRSVKBPZVMVIVJWEVLVHHIV
      KBAJQRSVEVOVGVEHOVNIUBVEAVDFZVNOPAUCZAUDUEUFUGVPGFZVJVRWAWHVEVGVRWALWHVEV
      RVGWAVEWHVRVGWALLVEWHVRVGWAVEWHMZVRVGMZHVQAUHKZITZWAWIVQEFVEWJWLLAVPUIVEW
      HUJVQAUKULWIVTWKHIVEWFWHVTWKPWGAVPUMUNRUOUPUQURUSUTVAVBUQVC $.
      $( [16-Dec-2005] $)
  $}

  $( Value of zero raised to a natural number power. $)
  0exp $p |- ( N e. NN -> ( 0 ^ N ) = 0 ) $=
    ( cn wcel cc0 cexp co wceq eqid cc wb 0cn expeq0 mpan mpbiri ) ABCZDAEFDGZD
    DGZDHDICOPQJKDALMN $.
    $( [19-Aug-2004] $)

  $( Nonnegative integer exponentiation with a nonnegative mantissa is
     nonnegative. $)
  expge0 $p |- ( ( A e. RR /\ N e. NN0 /\ 0 <_ A ) -> 0 <_ ( A ^ N ) ) $=
    ( cr wcel cn0 cc0 cle wbr cexp co cn wceq wo wi elnn0 clt wb 0re leloe c1
    wa expgt0 syl3an2 3expia 0exp eqcomd opreq1 eqeq2d syl5ibcom adantl orim12d
    nnnn0 mpan adantr reexpcl sylan2 sylancr 3imtr4d ex 1re lt01 ltleii cc recn
    exp0 syl syl5breqr opreq2 breq2d syl5ibrcom a1dd jaod syl5bi 3imp ) ACDZBED
    ZFAGHZFABIJZGHZVPBKDZBFLZMVOVQVSNZBOVOVTWBWAVOVTWBVOVTUAZFAPHZFALZMZFVRPHZF
    VRLZMZVQVSWCWDWGWEWHVOVTWDWGVTVOVPWDWGBULZABUBUCUDVTWEWHNVOVTFFBIJZLWEWHVTW
    KFBUEUFWEWKVRFFABIUGUHUIUJUKVOVQWFQZVTFCDZVOWLRFASUMUNWCWMVRCDZVSWIQRVTVOVP
    WNWJABUOUPFVRSUQURUSVOWAVSVQVOVSWAFAFIJZGHVOFTWOGFTRUTVAVBVOAVCDWOTLAVDAVEV
    FVGWAVRWOFGBFAIVHVIVJVKVLVMVN $.
    $( [16-Dec-2005] $)

  ${
    $d x y A $.  $d x N $.
    $( Natural number exponentiation with a mantissa greater than 1 is greater
       than 1. $)
    expgt1 $p |- ( ( A e. RR /\ N e. NN /\ 1 < A ) -> 1 < ( A ^ N ) ) $=
      ( vx vy cr wcel cn c1 clt wbr cexp co wi wa cv opreq2 breq2d imbi2d sylan
      wceq caddc cc recn exp1 syl biimpar cn0 nnnn0 reexpcl simpl jca mulgt1 ex
      cmul expp1 sylibrd sylan2 exp4b com12 com34 imp3a a2d nnind exp3a 3imp )
      AEFZBGFZHAIJZHABKLZIJZVGVFVHVJMVGVFVHVJVFVHNZHACOZKLZIJZMVKHAHKLZIJZMVKHA
      DOZKLZIJZMVKHAVQHUALZKLZIJZMVKVJMCDBVLHTZVNVPVKWCVMVOHIVLHAKPQRVLVQTZVNVS
      VKWDVMVRHIVLVQAKPQRVLVTTZVNWBVKWEVMWAHIVLVTAKPQRVLBTZVNVJVKWFVMVIHIVLBAKP
      QRVFVPVHVFVOAHIVFAUBFZVOATAUCZAUDUEQUFVQGFZVKVSWBWIVFVHVSWBMWIVFVSVHWBVFW
      IVSVHWBMMVFWIVSVHWBWIVFVQUGFZVSVHNZWBMVQUHVFWJNZWKHVRAUNLZIJZWBWLWKWNWLVR
      EFZVFNWKWNWLWOVFAVQUIVFWJUJUKVRAULSUMWLWAWMHIVFWGWJWAWMTWHAVQUOSQUPUQURUS
      UTVAVBVCVDUSVE $.
      $( [27-Jul-2006] $) $( [13-Feb-2005] $)
  $}

  $( Nonnegative integer exponentiation with a mantissa greater than or equal
     to 1 is greater than or equal to 1. $)
  expge1 $p |- ( ( A e. RR /\ N e. NN0 /\ 1 <_ A ) -> 1 <_ ( A ^ N ) ) $=
    ( cr wcel cn0 c1 cle wbr cexp co cc0 wceq wo wi wa clt syl wb 1re leloe a1d
    elnn0 expgt1 3expia 1exp eqcomd opreq1 eqeq2d syl5ibcom adantl orim12d mpan
    cn nnnn0 adantr reexpcl sylan2 sylancr 3imtr4d leidi cc recn exp0 sylan9eqr
    opreq2 syl5breqr jaodan sylan2b 3impia ) ACDZBEDZFAGHZFABIJZGHZVKVJBUMDZBKL
    ZMVLVNNZBUBVJVOVQVPVJVOOZFAPHZFALZMZFVMPHZFVMLZMZVLVNVRVSWBVTWCVJVOVSWBABUC
    UDVOVTWCNVJVOFFBIJZLVTWCVOWEFVOVKWEFLBUNZBUEQUFVTWEVMFFABIUGUHUIUJUKVJVLWAR
    ZVOFCDZVJWGSFATULUOVRWHVMCDZVNWDRSVOVJVKWIWFABUPUQFVMTURUSVJVPOZVNVLWJFFVMG
    FSUTVPVJVMAKIJZFBKAIVEVJAVADWKFLAVBAVCQVDVFUAVGVHVI $.
    $( [16-Oct-2007] $) $( [21-Feb-2005] $)

  ${
    $d j k A $.  $d j k B $.  $d j k M $.  $d j N $.
    $( Natural number exponentiation of a product.  Proposition 10-4.2(c) of
       [Gleason] p. 135, restricted to nonnegative integer exponents. $)
    mulexp $p |- ( ( A e. CC /\ B e. CC /\ N e. NN0 ) ->
                  ( ( A x. B ) ^ N ) = ( ( A ^ N ) x. ( B ^ N ) ) ) $=
      ( vj cc wcel cmul co cexp wceq wa cc0 opreq2 opreq12d eqeq12d imbi2d exp0
      wi c1 expp1 vk cn0 cv caddc mulcl opreqan12d ax-1cn mulid1i syl6eq eqtr4d
      syl sylan adantr opreq1 expcl anim12i anandirs simpl mul4 syl2anc adantlr
      adantl adantll 3eqtrd exp31 com12 a2d nn0ind exp3a com3l 3imp ) AEFZBEFZC
      UBFZABGHZCIHZACIHZBCIHZGHZJZVNVLVMVTVNVLVMVTVLVMKZVODUCZIHZAWBIHZBWBIHZGH
      ZJZRWAVOLIHZALIHZBLIHZGHZJZRWAVOUAUCZIHZAWMIHZBWMIHZGHZJZRWAVOWMSUDHZIHZA
      WSIHZBWSIHZGHZJZRWAVTRDUACWBLJZWGWLWAXEWCWHWFWKWBLVOIMXEWDWIWEWJGWBLAIMWB
      LBIMNOPWBWMJZWGWRWAXFWCWNWFWQWBWMVOIMXFWDWOWEWPGWBWMAIMWBWMBIMNOPWBWSJZWG
      XDWAXGWCWTWFXCWBWSVOIMXGWDXAWEXBGWBWSAIMWBWSBIMNOPWBCJZWGVTWAXHWCVPWFVSWB
      CVOIMXHWDVQWEVRGWBCAIMWBCBIMNOPWAWHSWKWAVOEFZWHSJABUEZVOQUKWAWKSSGHSVLVMW
      ISWJSGAQBQUFSUGUHUIUJWMUBFZWAWRXDWAXKWRXDRWAXKWRXDWAXKKZWRKWTWNVOGHZWQVOG
      HZXCXLWTXMJZWRWAXIXKXOXJVOWMTULUMWRXMXNJXLWNWQVOGUNVBXLXNXCJWRXLXNWOAGHZW
      PBGHZGHZXCXLWOEFZWPEFZKZWAXNXRJVLVMXKYAVLXKKXSVMXKKXTAWMUOBWMUOUPUQWAXKUR
      WOWPABUSUTXLXAXPXBXQGVLXKXAXPJVMAWMTVAVMXKXBXQJVLBWMTVCNUJUMVDVEVFVGVHVIV
      JVK $.
      $( [13-Feb-2005] $)

    $( Nonnegative integer exponentiation of a reciprocal. $)
    exprec $p |- ( ( A e. CC /\ A =/= 0 /\ N e. NN0 ) ->
                  ( ( 1 / A ) ^ N ) = ( 1 / ( A ^ N ) ) ) $=
      ( vj cc wcel cc0 c1 cdiv co cexp wceq wa wi opreq2 opreq2d eqeq12d imbi2d
      ax-1cn adantr cmul vk wne cn0 cv caddc reccl exp0 syl div1i syl6eq eqtr4d
      opreq1 ad2antll expp1 sylan an32s adantrr expcl expne0i 3com23 id adantlr
      3expa divmuldiv mpanl12 mulid1i opreq1i syl6req eqtrd 3eqtr4d exp43 com12
      syl21anc imp3a a2d nn0ind 3impia ) ADEZAFUBZBUCEZGAHIZBJIZGABJIZHIZKZVTVR
      VSLZWEWFWACUDZJIZGAWGJIZHIZKZMWFWAFJIZGAFJIZHIZKZMWFWAUAUDZJIZGAWPJIZHIZK
      ZMWFWAWPGUEIZJIZGAXAJIZHIZKZMWFWEMCUABWGFKZWKWOWFXFWHWLWJWNWGFWAJNXFWIWMG
      HWGFAJNOPQWGWPKZWKWTWFXGWHWQWJWSWGWPWAJNXGWIWRGHWGWPAJNOPQWGXAKZWKXEWFXHW
      HXBWJXDWGXAWAJNXHWIXCGHWGXAAJNOPQWGBKZWKWEWFXIWHWBWJWDWGBWAJNXIWIWCGHWGBA
      JNOPQWFWLGWNWFWADEZWLGKAUFZWAUGUHVRWNGKVSVRWNGGHIGVRWMGGHAUGOGRUIUJSUKWPU
      CEZWFWTXEXLVRVSWTXEMZVRXLVSXMMVRXLVSWTXEVRXLLZVSWTLLWQWATIZWSWATIZXBXDWTX
      OXPKXNVSWQWSWATULUMXNVSXBXOKZWTVRVSXLXQWFXJXLXQXKWAWPUNUOUPUQXNVSXDXPKWTX
      NVSLZXDGWRATIZHIZXPXNXDXTKVSXNXCXSGHAWPUNOSXRXPGGTIZXSHIZXTXRWRDEZWRFUBZW
      FXPYBKZXNYCVSAWPURSVRXLVSYDVRVSXLYDAWPUSUTVCVRVSWFXLWFVAVBGDEZYFYCYDLWFLY
      ERRGGWRAVDVEVMYAGXSHGRVFVGVHVIUQVJVKVLVNVOVPVLVQ $.
      $( [7-Mar-2009] $) $( [2-Aug-2006] $)

    $( Nonnegative integer exponentiation of a reciprocal. $)
    exprecOLD $p |- ( ( A e. CC /\ N e. NN0 /\ A =/= 0 ) ->
                  ( ( 1 / A ) ^ N ) = ( 1 / ( A ^ N ) ) ) $=
      ( vj cc wcel cc0 c1 cdiv co cexp wceq wi wa opreq2 opreq2d eqeq12d imbi2d
      ax-1cn adantr cmul vk cn0 wne cv caddc reccl exp0 syl div1i syl6eq eqtr4d
      opreq1 ad2antll expp1 sylan an32s adantrr expcl jctil simpl jca w3a simp1
      simp3 simp2 expne0i syl3anc 3expa simplll simplr anim12i simprr divmuldiv
      simpr syl22anc syl12anc mulid1i opreq1i syl6req eqtrd 3eqtr4d exp43 com12
      imp3a a2d nn0ind exp3a 3imp ) ADEZBUBEZAFUCZGAHIZBJIZGABJIZHIZKZWJWIWKWPL
      WJWIWKWPWIWKMZWLCUDZJIZGAWRJIZHIZKZLWQWLFJIZGAFJIZHIZKZLWQWLUAUDZJIZGAXGJ
      IZHIZKZLWQWLXGGUEIZJIZGAXLJIZHIZKZLWQWPLCUABWRFKZXBXFWQXQWSXCXAXEWRFWLJNX
      QWTXDGHWRFAJNOPQWRXGKZXBXKWQXRWSXHXAXJWRXGWLJNXRWTXIGHWRXGAJNOPQWRXLKZXBX
      PWQXSWSXMXAXOWRXLWLJNXSWTXNGHWRXLAJNOPQWRBKZXBWPWQXTWSWMXAWOWRBWLJNXTWTWN
      GHWRBAJNOPQWQXCGXEWQWLDEZXCGKAUFZWLUGUHWIXEGKWKWIXEGGHIGWIXDGGHAUGOGRUIUJ
      SUKXGUBEZWQXKXPYCWIWKXKXPLZWIYCWKYDLWIYCWKXKXPWIYCMZWKXKMMXHWLTIZXJWLTIZX
      MXOXKYFYGKYEWKXHXJWLTULUMYEWKXMYFKZXKWIWKYCYHWQYAYCYHYBWLXGUNUOUPUQYEWKXO
      YGKXKYEWKMZXOGXIATIZHIZYGYEXOYKKWKYEXNYJGHAXGUNOSYIYGGGTIZYJHIZYKYIGDEZXI
      DEZMZYNWIMZMZXIFUCZWKYGYMKZYEYRWKYEYPYQYEYOYNAXGURRUSYEWIYNWIYCUTRUSVASWI
      YCWKYSWIYCWKVBWIWKYCYSWIYCWKVCWIYCWKVDWIYCWKVEAXGVFVGVHYEWKVNYRYSWKMZMYNY
      NYOYSMWQYTYNYOYQUUAVIZUUBYRYOUUAYSYNYOYQVJYSWKUTVKYRWIUUAWKYPYNWIVLYSWKVN
      VKGGXIAVMVOVPYLGYJHGRVQVRVSVTUQWAWBWCWDWEWFWGWCWH $.
      $( [2-Aug-2006] $)

    $( Sum of exponents law for nonnegative integer exponentiation.
       Proposition 10-4.2(a) of [Gleason] p. 135. $)
    expadd $p |- ( ( A e. CC /\ M e. NN0 /\ N e. NN0 ) ->
                  ( A ^ ( M + N ) ) = ( ( A ^ M ) x. ( A ^ N ) ) ) $=
      ( vj vk cc wcel cn0 caddc co cexp cmul wceq wi cc0 opreq2 opreq2d eqeq12d
      c1 imbi2d wa cv expcl mulid1 syl exp0 adantr nn0cn addid1 adantl 3eqtr4rd
      opreq1 ax-1cn addass mp3an3 syl2an adantll simpll nn0addcl syl2anc eqtr3d
      expp1 adantlr mulass syl3anc eqtr4d syl5ibr expcom a2d nn0ind exp3a com3l
      3imp ) AFGZBHGZCHGZABCIJZKJZABKJZACKJZLJZMZVPVNVOWBVPVNVOWBVNVOUAZABDUBZI
      JZKJZVSAWDKJZLJZMZNWCABOIJZKJZVSAOKJZLJZMZNWCABEUBZIJZKJZVSAWOKJZLJZMZNWC
      ABWOSIJZIJZKJZVSAXAKJZLJZMZNWCWBNDECWDOMZWIWNWCXGWFWKWHWMXGWEWJAKWDOBIPQX
      GWGWLVSLWDOAKPQRTWDWOMZWIWTWCXHWFWQWHWSXHWEWPAKWDWOBIPQXHWGWRVSLWDWOAKPQR
      TWDXAMZWIXFWCXIWFXCWHXEXIWEXBAKWDXABIPQXIWGXDVSLWDXAAKPQRTWDCMZWIWBWCXJWF
      VRWHWAXJWEVQAKWDCBIPQXJWGVTVSLWDCAKPQRTWCVSSLJZVSWMWKWCVSFGZXKVSMABUCZVSU
      DUEWCWLSVSLVNWLSMVOAUFUGQWCWJBAKVOWJBMZVNVOBFGZXNBUHZBUIUEUJQUKWOHGZWCWTX
      FWCXQWTXFNWTXFWCXQUAZWQALJZWSALJZMWQWSALULXRXCXSXEXTXRAWPSIJZKJZXCXSXRYAX
      BAKVOXQYAXBMZVNVOXOWOFGZYCXQXPWOUHXOYDSFGYCUMBWOSUNUOUPUQQXRVNWPHGZYBXSMV
      NVOXQURZVOXQYEVNBWOUSUQAWPVBUTVAXRXEVSWRALJZLJZXTXRXDYGVSLVNXQXDYGMVOAWOV
      BVCQXRXLWRFGZVNXTYHMWCXLXQXMUGVNXQYIVOAWOUCVCYFVSWRAVDVEVFRVGVHVIVJVKVLVM
      $.
      $( [30-Nov-2004] $)

    $( Product of exponents law for natural number exponentiation.  Proposition
       10-4.2(b) of [Gleason] p. 135, restricted to nonnegative integer
       exponents. $)
    expmul $p |- ( ( A e. CC /\ M e. NN0 /\ N e. NN0 ) ->
               ( A ^ ( M x. N ) ) = ( ( A ^ M ) ^ N ) ) $=
      ( vj cc wcel cn0 cmul co cexp wceq wa wi cc0 caddc opreq2 opreq2d eqeq12d
      c1 imbi2d vk cv nn0cn mul01 syl exp0 sylan9eqr expcl eqtr4d opreq1 ax-1cn
      mp3an3 mulid1 adantr syl2an adantll simpll nn0mulcl simplr expadd syl3anc
      adddi eqtrd expp1 sylan syl5ibr expcom a2d nn0ind exp3a com3l 3imp ) AEFZ
      BGFZCGFZABCHIZJIZABJIZCJIZKZVOVMVNVTVOVMVNVTVMVNLZABDUBZHIZJIZVRWBJIZKZMW
      AABNHIZJIZVRNJIZKZMWAABUAUBZHIZJIZVRWKJIZKZMWAABWKSOIZHIZJIZVRWPJIZKZMWAV
      TMDUACWBNKZWFWJWAXAWDWHWEWIXAWCWGAJWBNBHPQWBNVRJPRTWBWKKZWFWOWAXBWDWMWEWN
      XBWCWLAJWBWKBHPQWBWKVRJPRTWBWPKZWFWTWAXCWDWRWEWSXCWCWQAJWBWPBHPQWBWPVRJPR
      TWBCKZWFVTWAXDWDVQWEVSXDWCVPAJWBCBHPQWBCVRJPRTWAWHSWIVNVMWHANJISVNWGNAJVN
      BEFZWGNKBUCZBUDUEQAUFUGWAVREFZWISKABUHZVRUFUEUIWKGFZWAWOWTWAXIWOWTMWOWTWA
      XILZWMVRHIZWNVRHIZKWMWNVRHUJXJWRXKWSXLXJWRAWLBOIZJIZXKXJWQXMAJVNXIWQXMKZV
      MVNXEWKEFZXOXIXFWKUCXEXPLZWQWLBSHIZOIZXMXEXPSEFWQXSKUKBWKSVBULXQXRBWLOXEX
      RBKXPBUMUNQVCUOUPQXJVMWLGFZVNXNXKKVMVNXIUQVNXIXTVMBWKURUPVMVNXIUSAWLBUTVA
      VCWAXGXIWSXLKXHVRWKVDVERVFVGVHVIVJVKVL $.
      $( [4-Jan-2006] $)
  $}

  $( Exponent subtraction law for nonnegative integer exponentiation. $)
  expsub $p |- ( ( ( ( A e. CC /\ A =/= 0 ) /\ M e. NN0 /\ N e. NN0 ) /\
      N <_ M ) -> ( A ^ ( M - N ) ) = ( ( A ^ M ) / ( A ^ N ) ) ) $=
    ( cc wcel cc0 wne wa cn0 w3a cexp co wceq ancoms nn0cn adantr expcl adantlr
    wb 3adant2 cle wbr cdiv cmin cmul caddc simpl1l simpl3 nn0sub biimpa expadd
    3adantl1 syl3anc pncan3 syl2an opreq2d 3adant1 eqtr3d 3adant3 syl2anc 3expa
    expne0i divmul syl112anc mpbird eqcomd ) ADEZAFGZHZBIEZCIEZJZCBUAUBZHZABKLZ
    ACKLZUCLZABCUDLZKLZVNVQVSMZVPVSUELZVOMZVNACVRUFLZKLZWAVOVNVGVKVRIEZWDWAMVGV
    HVJVKVMUGZVIVJVKVMUHVJVKVMWEVIVJVKHZVMWEVKVJVMWESCBUINUJULZACVRUKUMVLWDVOMZ
    VMVJVKWIVIWGWCBAKVKVJWCBMZVKCDEBDEWJVJCOBOCBUNUONUPUQPURVNVODEZVSDEZVPDEZVP
    FGZVTWBSVLWKVMVIVJWKVKVGVJWKVHABQRUSPVNVGWEWLWFWHAVRQUTVLWMVMVIVKWMVJVGVKWM
    VHACQRTPVLWNVMVIVKWNVJVGVHVKWNACVBVATPVOVSVPVCVDVEVF $.
    $( [12-Jan-2009] $) $( [2-Aug-2006] $)

  $( Exponent subtraction law for nonnegative integer exponentiation. $)
  expsubOLD $p |- ( ( ( A e. CC /\ M e. NN0 /\ N e. NN0 ) /\
  ( A =/= 0 /\ N <_ M ) ) -> ( A ^ ( M - N ) ) = ( ( A ^ M ) / ( A ^ N ) ) ) $=
    ( cc wcel cn0 w3a cc0 wne wa cexp co wb ancoms syl3anc nn0cn adantr adantrl
    wceq expcl cdiv cmin cmul caddc simpl1 simpl3 nn0sub biimpa 3adantl1 expadd
    cle pncan3 syl2an opreq2d 3adant1 3adant3 syl2anc 3adant2 simp1 simp3 simp2
    wbr eqtr3d expne0i 3expa 3adantl2 adantrr divmul syl112anc mpbird eqcomd )
    ADEZBFEZCFEZGZAHIZCBUKVBZJZJZABKLZACKLZUALZABCUBLZKLZVSWBWDSZWAWDUCLZVTSZVO
    VQWGVPVOVQJZACWCUDLZKLZWFVTWHVLVNWCFEZWJWFSVLVMVNVQUEZVLVMVNVQUFVMVNVQWKVLV
    MVNJZVQWKVNVMVQWKMCBUGNUHUIZACWCUJOVOWJVTSZVQVMVNWOVLWMWIBAKVNVMWIBSZVNCDEB
    DEWPVMCPBPCBULUMNUNUOQVCRVSVTDEZWDDEZWADEZWAHIZWEWGMVOWQVRVLVMWQVNABTUPQVOV
    QWRVPWHVLWKWRWLWNAWCTUQRVOWSVRVLVNWSVMACTURQVOVPWTVQVLVNVPWTVMVLVNVPWTVLVNV
    PGVLVPVNWTVLVNVPUSVLVNVPUTVLVNVPVAACVDOVEVFVGVTWDWAVHVIVJVK $.
    $( [2-Aug-2006] $)

  $( Value of a complex number raised to a nonnegative integer power minus
     one. $)
  expm1 $p |- ( ( A e. CC /\ A =/= 0 /\ N e. NN ) -> ( A ^ ( N - 1 ) ) =
               ( ( A ^ N ) / A ) ) $=
    ( cc wcel cc0 wne cn w3a c1 cmin cexp cdiv cn0 cle wbr wceq 3simpa 3ad2ant3
    co wa nnnn0 1nn0 a1i nnge1 expsub syl31anc exp1 3ad2ant1 opreq2d eqtrd ) AC
    DZAEFZBGDZHZABIJSKSZABKSZAIKSZLSZUPALSUNUKULTBMDZIMDZIBNOZUOURPUKULUMQUMUKU
    SULBUARUTUNUBUCUMUKVAULBUDRABIUEUFUNUQAUPLUKULUQAPUMAUGUHUIUJ $.
    $( [25-Dec-2008] $)

  $( Nonnegative integer exponentiation of a quotient. $)
  expdiv $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) /\ N e. NN0 ) ->
                ( ( A / B ) ^ N ) = ( ( A ^ N ) / ( B ^ N ) ) ) $=
    ( cc wcel cc0 wne wa cn0 w3a cdiv co cexp c1 cmul wceq divrec 3expa 3adant1
    expcl 3adant3 opreq1d mulexp syl3an2 exprec opreq2d 3adant2 adantlr expne0i
    3expb reccl syl3anc eqtr4d 3eqtrd ) ADEZBDEZBFGZHZCIEZJZABKLZCMLANBKLZOLZCM
    LZACMLZVBCMLZOLZVEBCMLZKLZUTVAVCCMUOURVAVCPZUSUOUPUQVJABQUJUAUBURUOVBDEUSVD
    VGPBUKAVBCUCUDUTVGVENVHKLZOLZVIUTVFVKVEOURUSVFVKPZUOUPUQUSVMBCUERSUFUTVEDEZ
    VHDEZVHFGZVIVLPUOUSVNURACTUGURUSVOUOUPUSVOUQBCTUHSURUSVPUOUPUQUSVPBCUIRSVEV
    HQULUMUN $.
    $( [12-Mar-2009] $) $( [2-Aug-2006] $)

  $( Ordering relationship for exponentiation. $)
  expordi $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             ( 1 < A /\ M < N ) ) -> ( A ^ M ) < ( A ^ N ) ) $=
    ( cr wcel cn0 c1 clt wbr wa cexp co adantrl cc0 syl 3adant3 adantr wi wceq
    cc w3a cmul cmin cn simpl1 wb cz znnsub syl2an 3adant1 biimpa simprl expgt1
    nn0z syl3anc nnnn0 reexpcl syl2anc lt01 0re 1re axlttrn mpani expgt0 3expia
    mp3an12 syld imp ltmul1 mp3an1 syl12anc mpbid recn expcl sylan mulid2 caddc
    adantrr simp1 recnd simpl2 expadd syl3an2 nn0cn npcan ancoms opreq2d eqtr3d
    3jca 3brtr3d ) ADEZBFEZCFEZUAZGAHIZBCHIZJZJZGABKLZUBLZACBUCLZKLZWSUBLZWSACK
    LZHWRGXBHIZWTXCHIZWRWKXAUDEZWOXEWKWLWMWQUEWNWPXGWOWNWPXGWLWMWPXGUFZWKWLBUGE
    CUGEXHWMBUNCUNBCUHUIUJUKZMWNWOWPULAXAUMUOWRXBDEZWSDEZNWSHIZXEXFUFZWNWPXJWOW
    NWPJZWKXAFEZXJWKWLWMWPUEXNXGXOXIXAUPZOAXAUQURMWNXKWQWKWLXKWMABUQPQWNWOXLWPW
    NWOXLWKWLWOXLRWMWKWLJZWONAHIZXLWKWOXRRWLWKNGHIZWOXRUSNDEGDEZWKXSWOJXRRUTVAN
    GAVBVFVCQWKWLXRXLABVDVEVGPVHVRXTXJXKXLJXMVAGXBWSVIVJVKVLWNWTWSSZWQWKWLYAWMX
    QWSTEZYAWKATEZWLYBAVMABVNVOWSVPOPQWRAXABVQLZKLZXCXDWRYCXGWLUAZYEXCSZWNWPYFW
    OXNYCXGWLWNYCWPWNAWKWLWMVSVTQXIWKWLWMWPWAWIMXGYCXOWLYGXPAXABWBWCOWNYEXDSWQW
    NYDCAKWLWMYDCSZWKWMWLYHWMCTEBTEYHWLCWDBWDCBWEUIWFUJWGQWHWJ $.
    $( [2-Aug-2006] $)

  $( Cancellation law for exponentiation. $)
  expcan $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             1 < A ) -> ( ( A ^ M ) = ( A ^ N ) <-> M = N ) ) $=
    ( cr wcel cn0 w3a clt wbr wa cexp co wceq wo wne expordi wi wb nn0re lttri2
    expr exp32 3com23 imp orim12d syl2an 3adant1 adantr reexpcl 3imtr4d necon4d
    c1 3impdi opreq2 impbid1 ) ADEZBFEZCFEZGZULAHIZJZABKLZACKLZMBCMVABCVBVCVABC
    HIZCBHIZNZVBVCHIZVCVBHIZNZBCOZVBVCOZVAVDVGVEVHUSUTVDVGABCPUAUSUTVEVHQZUPURU
    QUTVLQUPURUQGUTVEVHACBPUBUCUDUEUSVJVFRZUTUQURVMUPUQBDECDEVMURBSCSBCTUFUGUHU
    SVKVIRZUTUPUQURVNUPUQJVBDEVCDEVNUPURJABUIACUIVBVCTUFUMUHUJUKBCAKUNUO $.
    $( [2-Aug-2006] $)

  $( Ordering law for exponentiation. $)
  expord $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             1 < A ) -> ( M < N <-> ( A ^ M ) < ( A ^ N ) ) ) $=
    ( cr wcel cn0 w3a clt wbr wa cexp co expordi wceq wo wn wi reexpcl axlttri
    wb c1 expr expcan biimprd exp32 3com23 imp con3d syl2an 3impdi adantr nn0re
    orim12d 3adant1 3imtr4d impbid ) ADEZBFEZCFEZGZUAAHIZJZBCHIZABKLZACKLZHIZUT
    VAVCVFABCMUBVBVDVENZVEVDHIZOZPZBCNZCBHIZOZPZVFVCVBVMVIVBVKVGVLVHVBVGVKABCUC
    UDUTVAVLVHQZUQUSURVAVOQUQUSURGVAVLVHACBMUEUFUGUMUHUTVFVJTZVAUQURUSVPUQURJVD
    DEVEDEVPUQUSJABRACRVDVESUIUJUKUTVCVNTZVAURUSVQUQURBDECDEVQUSBULCULBCSUIUNUK
    UOUP $.
    $( [2-Aug-2006] $)

  $( Weak ordering relationship for exponentiation. $)
  expwordi $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             ( 1 <_ A /\ M <_ N ) ) -> ( A ^ M ) <_ ( A ^ N ) ) $=
    ( cr wcel cn0 c1 cle wbr cexp co clt wceq wo wi wb 1re leloe wa nn0re leidi
    w3a 3ad2ant1 expord biimpd opreq2 a1i orim12d syl2an 3adant1 adantr reexpcl
    mpan 3adant3 3adant2 syl2anc 3imtr4d 1exp breqan12d mpbiri opreq1 syl5ibcom
    ex breq12d a1dd jaod sylbid imp32 ) ADEZBFEZCFEZUBZGAHIZBCHIZABJKZACJKZHIZV
    LVMGALIZGAMZNZVNVQOZVIVJVMVTPZVKGDEVIWBQGARUMUCVLVRWAVSVLVRWAVLVRSZBCLIZBCM
    ZNZVOVPLIZVOVPMZNZVNVQWCWDWGWEWHWCWDWGABCUDUEWEWHOWCBCAJUFUGUHVLVNWFPZVRVJV
    KWJVIVJBDECDEWJVKBTCTBCRUIUJUKVLVQWIPZVRVLVODEZVPDEZWKVIVJWLVKABULUNVIVKWMV
    JACULUOVOVPRUPUKUQVCVLVSVQVNVJVKVSVQOVIVJVKSZGBJKZGCJKZHIZVSVQWNWQGGHIGQUAV
    JVKWOGWPGHBURCURUSUTVSWOVOWPVPHGABJVAGACJVAVDVBUJVEVFVGVH $.
    $( [9-Oct-2006] $) $( [14-Dec-2005] $)

  $( The power of a positive number smaller than 1 decreases as its exponent
     increases. $)
  expord2 $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             ( 0 < A /\ A < 1 ) ) -> ( M < N <-> ( A ^ N ) < ( A ^ M ) ) ) $=
    ( cr wcel cn0 w3a cc0 clt wbr c1 wa cdiv co wb idd adantrr 3ad2antl1 adantr
    cexp wne gt0ne0 rereccl syldan expcom 3anim123d impcom reclt1 biimpa anasss
    expord syl2anc reexpcl 3adantl2 expgt0 3expa 3adantl3 syl22anc cc wceq recn
    ltrec 3ad2ant1 simpl2 exprec syl3anc simpl3 breq12d bitr4d ) ADEZBFEZCFEZGZ
    HAIJZAKIJZLZLZBCIJZKAMNZBTNZVSCTNZIJZACTNZABTNZIJZVQVSDEZVKVLGZKVSIJZVRWBOV
    MVNWGVOVNVMWGVNVJWFVKVKVLVLVJVNWFVJVNAHUAZWFAUBZAUCUDUEVNVKPVNVLPUFUGQVJVKV
    PWHVLVJVNVOWHVJVNLVOWHAUHUIUJRVSBCUKULVMVNWEWBOVOVMVNLZWEKWDMNZKWCMNZIJZWBW
    KWCDEZHWCIJZWDDEZHWDIJZWEWNOVJVLVNWOVKVJVLLWOVNACUMSUNVJVLVNWPVKVJVLVNWPACU
    OUPUNVJVKVNWQVLVJVKLWQVNABUMSUQVJVKVNWRVLVJVKVNWRABUOUPUQWCWDVBURWKVTWLWAWM
    IWKAUSEZWIVKVTWLUTVMWSVNVJVKWSVLAVAVCSZVJVKVNWIVLWJRZVJVKVLVNVDABVEVFWKWSWI
    VLWAWMUTWTXAVJVKVLVNVGACVEVFVHVIQVI $.
    $( [10-Jul-2007] $) $( [2-Aug-2006] $)

  $( Weak ordering relationship for exponentiation.  (Contributed by Paul
     Chapman, 14-Jan-2008.) $)
  expword2i $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
                       ( 0 < A /\ A <_ 1 /\ M < N ) ) ->
                     ( A ^ N ) <_ ( A ^ M ) ) $=
    ( clt wbr c1 cle w3a cr wcel cn0 wa cexp co wi wceq ex imp3a adantr adantl
    cc0 df-3an wo expord2 biimpd exp3a com34 imp eqtr4d 3adant1 opreq1 3eqtr4rd
    orim12d wb 1re leloe mpan2 3ad2ant1 reexpcl 3adant2 3adant3 syl2anc 3imtr4d
    1exp sylan2b ) UAADEZAFGEZBCDEZHAIJZBKJZCKJZHZVFVGLZVHLZACMNZABMNZGEZVFVGVH
    UBVLVNVQVLVMVHVQVLVFVGVHVQOVLVFVHVGVQVLVFVHVGVQOZVLVFVHLZVRVLVSLZAFDEZAFPZU
    CZVOVPDEZVOVPPZUCZVGVQVTWAWDWBWEVLVSWAWDOZVLVFVHWGVLVFWAVHWDVLVFWAVHWDOZVLV
    FWALZWHVLWILVHWDABCUDUEQUFUGRUHVLWBWEOVSVLWBWEVLWBLFBMNZFCMNZVPVOVLWJWKPZWB
    VJVKWLVIVJVKLWJFWKVJWJFPVKBVDSVKWKFPVJCVDTUIUJSWBVPWJPVLAFBMUKTWBVOWKPVLAFC
    MUKTULQSUMVLVGWCUNZVSVIVJWMVKVIFIJWMUOAFUPUQURSVLVQWFUNZVSVLVOIJZVPIJZWNVIV
    KWOVJACUSUTVIVJWPVKABUSVAVOVPUPVBSVCQUFUGRRUHVE $.
    $( [14-Jan-2008] $)

  ${
    $d j k A $.  $d j k B $.  $d j k N $.
    $( Weak mantissa ordering relationship for exponentiation. $)
    expmwordi $p |- ( ( ( A e. RR /\ B e. RR /\ N e. NN0 ) /\
               ( 0 <_ A /\ A <_ B ) ) -> ( A ^ N ) <_ ( B ^ N ) ) $=
      ( vj cr wcel cc0 cle wbr wa cexp co wi c1 sylan adantr wceq opreq2 imbi2d
      breq12d vk cn0 cv caddc cmul simpll reexpcl simplll simpr simplrl syl3anc
      expge0 simplr jca31 simpl anim12i simpllr jca32 simplrr jca lemul12a sylc
      cc recn expp1 adantlr adantll 3brtr4d ex expcom a2d exp0 1re leidi adantl
      syl6eqbr breqtrrd syl2an weq nn0indALT exp4c com3l 3imp1 ) AEFZBEFZCUBFZG
      AHIZABHIZJZACKLZBCKLZHIZWFWDWEWIWLMWFWDWEWIWLWDWEJZWIJZADUCZKLZBWOKLZHIZM
      WNAGKLZBGKLZHIZMWNAUAUCZKLZBXBKLZHIZMWNAXBNUDLZKLZBXFKLZHIZMWNWLMDUACXBUB
      FZWNXEXIWNXJXEXIMWNXJJZXEXIXKXEJZXCAUELZXDBUELZXGXHHXLXCEFZGXCHIZJXDEFZJZ
      WDWGJZWEJJZXEWHJXMXNHIXKXTXEXKXRXSWEXKXOXPXQWNWDXJXOWDWEWIUFAXBUGOXKWDXJW
      GXPWDWEWIXJUHWNXJUIWMWGWHXJUJAXBULUKWNWEXJXQWDWEWIUMBXBUGOUNWNXSXJWMWDWIW
      GWDWEUOWGWHUOUPPWDWEWIXJUQURPXLXEWHXKXEUIXKWHXEWMWGWHXJUSPUTXCXDABVAVBXKX
      GXMQZXEWMXJYAWIWDXJYAWEWDAVCFZXJYAAVDZAXBVEOVFVFPXKXHXNQZXEWMXJYDWIWEXJYD
      WDWEBVCFZXJYDBVDZBXBVEOVGVFPVHVIVJVKWMXAWIWDYBYEXAWEYCYFYBYEJZWSNWTHYGWSN
      NHYBWSNQYEAVLPNVMVNVPYEWTNQYBBVLVOVQVRPWOGQZWRXAWNYHWPWSWQWTHWOGAKRWOGBKR
      TSDUAVSZWRXEWNYIWPXCWQXDHWOXBAKRWOXBBKRTSWOXFQZWRXIWNYJWPXGWQXHHWOXFAKRWO
      XFBKRTSWOCQZWRWLWNYKWPWJWQWKHWOCAKRWOCBKRTSVTWAWBWC $.
      $( [18-Dec-2005] $)
  $}

  $( Nonnegative integer exponentiation with a mantissa between 0 and 1
     inclusive is less than or equal to 1.  (Contributed by Paul Chapman,
     29-Dec-2007.) $)
  exple1 $p |- ( ( ( A e. RR /\ 0 <_ A /\ A <_ 1 ) /\ N e. NN0 ) ->
                  ( A ^ N ) <_ 1 ) $=
    ( cr wcel cc0 cle wbr c1 w3a cexp co wi clt wceq wb wa cdiv 1re lt01 3expa
    cn0 wo 0re leloe mpan biimpa wne simp1 gt0ne0 3adant3 rereccl syl2anc simp3
    lerec2 mpanl12 ax-1cn div1i breq2i syl6bb mpbird jca expge1 3com23 sylan cc
    recnd exprec breqtrd reexpcl 3ad2antl1 expgt0 3adantl3 mpbid syl6breq ex cn
    3expia elnn0 0exp ltleii syl6eqbr opreq2 exp0 ax-mp syl6eq leidi jaoi sylbi
    0cn opreq1 breq1d syl5ib a1d adantl jaodan syldan 3impia imp ) ACDZEAFGZAHF
    GZIBUADZABJKZHFGZWSWTXAXBXDLZWSWTEAMGZEANZUBZXAXELZWSWTXHECDWSWTXHOUCEAUDUE
    UFWSXFXIXGWSXFXAXEWSXFXAIZXBXDXJXBPZXCHHQKZHFXKHHXCQKZFGZXCXLFGZXKHHAQKZBJK
    ZXMFXJXPCDZHXPFGZPXBHXQFGZXJXRXSXJWSAEUGZXRWSXFXAUHZWSXFYAXAAUIUJZAUKULXJXS
    XAWSXFXAUMXJXSAXLFGZXAWSXFXSYDOZXAHCDZEHMGZWSXFPYERSHAUNUOUJXLHAFHUPUQZURUS
    UTVAXRXSXBXTXRXBXSXTXPBVBVCTVDXJAVEDZYAPXBXQXMNZXJYIYAXJAYBVFYCVAYIYAXBYJAB
    VGTVDVHXKXCCDZEXCMGZXNXOOZWSXFXBYKXAABVIVJWSXFXBYLXAWSXFXBYLWSXBXFYLABVKVCT
    VLYFYGYKYLPYMRSHXCUNUOULVMYHVNVOVQXGXIWSXGXEXAXBEBJKZHFGZXGXDXBBVPDZBENZUBY
    OBVRYPYOYQYPYNEHFBVSEHUCRSVTWAYQYNHHFYQYNEEJKZHBEEJWBEVEDYRHNWIEWCWDWEHRWFW
    AWGWHXGYNXCHFEABJWJWKWLWMWNWOWPWQWR $.
    $( [29-Dec-2007] $)

  $( An upper bound on ` A ^ N ` when ` 2 <_ A ` . $)
  expubnd $p |- ( ( A e. RR /\ N e. NN0 /\ 2 <_ A ) ->
                  ( A ^ N ) <_ ( ( 2 ^ N ) x. ( ( A - 1 ) ^ N ) ) ) $=
    ( cr wcel c2 cle wbr cexp co c1 cmin cmul cc0 wa 2re 0re caddc wceq cc 2cn
    cn0 simp1 peano2rem remulcl sylancr 3ad2ant1 simp2 2pos ltleii letr mp3an12
    w3a wi mpani wb resubcl mpan2 leadd2 mp3an1 mpdan biimpa recn npcan sylancl
    imp adantr ax-1cn mp3an13 2times mulid1i a1i opreq12d addsub mp3an3 3eqtrrd
    subdi anidms syl 3brtr3d jca 3adant2 expmwordi syl31anc recnd sylan 3adant3
    mulexp breqtrd ) ACDZBUADZEAFGZULZABHIZEAJKIZLIZBHIZEBHIWNBHILIZFWLWIWOCDZW
    JMAFGZAWOFGZNZWMWPFGWIWJWKUBWIWJWRWKWIECDZWNCDWROAUCZEWNUDUEUFWIWJWKUGWIWKX
    AWJWIWKNZWSWTWIWKWSWIMEFGZWKWSMEPOUHUIMCDXBWIXEWKNWSUMPOMEAUJUKUNVEXDAEKIZE
    QIZXFAQIZAWOFWIWKXGXHFGZWIXFCDZWKXIUOZWIXBXJOAEUPUQXBWIXJXKOEAXFURUSUTVAWIX
    GARZWKWIASDZESDZXLAVBZTAEVCVDVFWIXHWORZWKWIXMXPXOXMWOEALIZEJLIZKIZAAQIZEKIZ
    XHXNXMJSDWOXSRTVGEAJVPVHXMXQXTXREKAVIXRERXMETVJVKVLXMYAXHRZXMXMXNYBTAAEVMVN
    VQVOVRVFVSVTWAAWOBWBWCWIWJWPWQRZWKWIWNSDZWJYCWIWNXCWDXNYDWJYCTEWNBWGUSWEWFW
    H $.
    $( [19-Dec-2005] $)

  $( Value of the square of a complex number.  (Contributed by Raph Levien,
     10-Apr-2004.) $)
  sqval $p |- ( A e. CC -> ( A ^ 2 ) = ( A x. A ) ) $=
    ( cc wcel c2 cexp co c1 cmul caddc df-2 opreq2i cn0 wceq expp1 mpan2 syl5eq
    1nn0 exp1 opreq1d eqtrd ) ABCZADEFZAGEFZAHFZAAHFUAUBAGGIFZEFZUDDUEAEJKUAGLC
    UFUDMQAGNOPUAUCAAHARST $.
    $( [28-Jul-2006] $) $( [10-Apr-2004] $)

  $( The square of the negative of a number.) $)
  sqneg $p |- ( A e. CC -> ( -u A ^ 2 ) = ( A ^ 2 ) ) $=
    ( cc wcel cneg cmul co c2 cexp wceq mul2neg anidms negcl sqval syl 3eqtr4d
    ) ABCZADZQEFZAAEFZQGHFZAGHFPRSIAAJKPQBCTRIALQMNAMO $.
    $( [18-Feb-2007] $) $( [15-Jan-2006] $)

  $( Closure of square. $)
  sqcl $p |- ( A e. CC -> ( A ^ 2 ) e. CC ) $=
    ( cc wcel c2 cexp co cmul sqval mulcl anidms eqeltrd ) ABCZADEFAAGFZBAHLMBC
    AAIJK $.
    $( [10-Aug-1999] $)

  $( Distribution of square over multiplication. $)
  sqmul $p |- ( ( A e. CC /\ B e. CC ) ->
             ( ( A x. B ) ^ 2 ) = ( ( A ^ 2 ) x. ( B ^ 2 ) ) ) $=
    ( cc wcel c2 cn0 cmul co cexp wceq 2nn0 mulexp mp3an3 ) ACDBCDEFDABGHEIHAEI
    HBEIHGHJKABELM $.
    $( [21-Mar-2008] $)

  $( A number is zero iff its square is zero. $)
  sqeq0 $p |- ( A e. CC -> ( ( A ^ 2 ) = 0 <-> A = 0 ) ) $=
    ( cc wcel c2 cn cexp co cc0 wceq wb 2nn expeq0 mpan2 ) ABCDECADFGHIAHIJKADL
    M $.
    $( [11-Mar-2006] $)

  ${
    sqval.1 $e |- A e. CC $.
    $( Value of square.  Inference version. $)
    sqvali $p |- ( A ^ 2 ) = ( A x. A ) $=
      ( cc wcel c2 cexp co cmul wceq sqval ax-mp ) ACDAEFGAAHGIBAJK $.
      $( [1-Aug-1999] $)

    $( Closure of square. $)
    sqcli $p |- ( A ^ 2 ) e. CC $=
      ( cc wcel c2 cexp co sqcl ax-mp ) ACDAEFGCDBAHI $.
      $( [2-Aug-1999] $)

    $( A number is zero iff its square is zero. $)
    sqeq0i $p |- ( ( A ^ 2 ) = 0 <-> A = 0 ) $=
      ( cc wcel c2 cexp co cc0 wceq wb sqeq0 ax-mp ) ACDAEFGHIAHIJBAKL $.
      $( [2-Oct-1999] $)

    ${
      sqmul.2 $e |- B e. CC $.
      $( Distribution of square over multiplication. $)
      sqmuli $p |- ( ( A x. B ) ^ 2 ) = ( ( A ^ 2 ) x. ( B ^ 2 ) ) $=
        ( cc wcel cmul co c2 cexp wceq sqmul mp2an ) AEFBEFABGHIJHAIJHBIJHGHKCD
        ABLM $.
        $( [31-Jul-2006] $) $( [3-Sep-1999] $)

      ${
        sqdiv.3 $e |- B =/= 0 $.
        $( Distribution of square over division. $)
        sqdivi $p |- ( ( A / B ) ^ 2 ) = ( ( A ^ 2 ) / ( B ^ 2 ) ) $=
          ( cdiv co cmul c2 cexp divmuldivi divcli sqvali opreq12i 3eqtr4i ) AB
          FGZPHGAAHGZBBHGZFGPIJGAIJGZBIJGZFGABABCDCDEEKPABCDELMSQTRFACMBDMNO $.
          $( [20-Aug-2001] $)
      $}
    $}

    ${
      sqreci.1 $e |- A =/= 0 $.
      $( Square of reciprocal. $)
      sqrecii $p |- ( ( 1 / A ) ^ 2 ) = ( 1 / ( A ^ 2 ) ) $=
        ( c1 cdiv co cmul c2 cexp ax-1cn divmuldivi mulid1i eqtri reccli sqvali
        opreq1i opreq2i 3eqtr4i ) DAEFZSGFZDAAGFZEFZSHIFDAHIFZEFTDDGFZUAEFUBDAD
        AJBJBCCKUDDUAEDJLPMSABCNOUCUADEABOQR $.
        $( [17-Sep-1999] $)
    $}
  $}

  $( Distribution of square over division.  (Contributed by Scott Fenton,
     7-Jun-2013.)  (Proof shortened by Mario Carneiro, 9-Jul-2013.) $)
  sqdiv $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
      ( ( A / B ) ^ 2 ) = ( ( A ^ 2 ) / ( B ^ 2 ) ) ) $=
    ( cc wcel cc0 wne w3a cdiv co cmul c2 cexp wa wceq simp1 divmuldiv syl22anc
    3simpc divcl sqval syl opreq12 syl2an 3adant3 3eqtr4d ) ACDZBCDZBEFZGZABHIZ
    UJJIZAAJIZBBJIZHIZUJKLIZAKLIZBKLIZHIZUIUFUFUGUHMZUSUKUNNUFUGUHOZUTUFUGUHRZV
    AAABBPQUIUJCDUOUKNABSUJTUAUFUGURUNNZUHUFUPULNUQUMNVBUGATBTUPULUQUMHUBUCUDUE
    $.
    $( [9-Jul-2013] $) $( [7-Jun-2013] $)

  $( A number is nonzero iff its square is nonzero. $)
  sqne0 $p |- ( A e. CC -> ( ( A ^ 2 ) =/= 0 <-> A =/= 0 ) ) $=
    ( cc wcel c2 cexp co cc0 sqeq0 necon3bid ) ABCADEFGAGAHI $.
    $( [11-Mar-2006] $)

  $( Closure of the square of a real number. $)
  resqcl $p |- ( A e. RR -> ( A ^ 2 ) e. RR ) $=
    ( cr wcel c2 cn0 cexp co 2nn0 reexpcl mpan2 ) ABCDECADFGBCHADIJ $.
    $( [29-Jul-2006] $) $( [18-Oct-1999] $)

  $( The square of a nonzero real is positive. $)
  sqgt0 $p |- ( ( A e. RR /\ A =/= 0 ) -> 0 < ( A ^ 2 ) ) $=
    ( cr wcel cc0 wne wa cmul co c2 cexp clt msqgt0 wceq cc recn sqval breqtrrd
    syl adantr ) ABCZADEZFDAAGHZAIJHZKALTUCUBMZUATANCUDAOAPRSQ $.
    $( [8-Sep-2007] $)

  ${
    resqcl.1 $e |- A e. RR $.
    $( Closure of square in reals. $)
    resqcli $p |- ( A ^ 2 ) e. RR $=
      ( cr wcel c2 cexp co resqcl ax-mp ) ACDAEFGCDBAHI $.
      $( [2-Aug-1999] $)

    ${
      lt2sq.2 $e |- B e. RR $.
      $( The square function on nonnegative reals is strictly monotonic. $)
      lt2sqi $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( A < B <-> ( A ^ 2 ) < ( B ^ 2 ) ) ) $=
        ( cc0 cle wbr wa clt cmul co cexp lt2msqi recni sqvali breq12i syl6bbr
        c2 ) EAFGEBFGHABIGAAJKZBBJKZIGARLKZBRLKZIGABCDMUASUBTIAACNOBBDNOPQ $.
        $( [12-Sep-1999] $)

      $( The square function on nonnegative reals is monotonic. $)
      le2sqi $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( A <_ B <-> ( A ^ 2 ) <_ ( B ^ 2 ) ) ) $=
        ( cc0 cle wbr wa cmul co c2 cexp le2msqi recni sqvali breq12i syl6bbr )
        EAFGEBFGHABFGAAIJZBBIJZFGAKLJZBKLJZFGABCDMTRUASFAACNOBBDNOPQ $.
        $( [12-Sep-1999] $)

      $( The square function is one-to-one for nonnegative reals. $)
      sq11i $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( ( A ^ 2 ) = ( B ^ 2 ) <-> A = B ) ) $=
        ( c2 cexp co wceq cmul cc0 cle wbr recni sqvali eqeq12i msq11i syl5bb
        wa ) AEFGZBEFGZHAAIGZBBIGZHJAKLJBKLRABHSUATUBAACMNBBDMNOABCDPQ $.
        $( [27-Oct-1999] $)
    $}

    $( The square of a nonzero real is positive. $)
    sqgt0i $p |- ( A =/= 0 -> 0 < ( A ^ 2 ) ) $=
      ( cr wcel cc0 wne c2 cexp co clt wbr sqgt0 mpan ) ACDAEFEAGHIJKBALM $.
      $( [17-Sep-1999] $)

    $( A square of a real is nonnegative. $)
    sqge0i $p |- 0 <_ ( A ^ 2 ) $=
      ( cc0 cmul co c2 cexp cle msqge0i recni sqvali breqtrri ) CAADEAFGEHABIAA
      BJKL $.
      $( [3-Aug-1999] $)
  $}

  $( The square function is one-to-one for nonnegative reals. $)
  sq11 $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
               ( ( A ^ 2 ) = ( B ^ 2 ) <-> A = B ) ) $=
    ( cr wcel cc0 cle wbr c2 cexp co wceq wb wa wi breq2 opreq1 bibi12d imbi12d
    cif 0re anbi1d eqeq1d eqeq1 anbi2d eqeq2d eqeq2 elimel sq11i dedth2h an4s
    imp ) ACDZBCDZEAFGZEBFGZAHIJZBHIJZKZABKZLZULUMMUNUOMZUTULUMVAUTNEULAESZFGZU
    OMZVBHIJZUQKZVBBKZLZNVCEUMBESZFGZMZVEVIHIJZKZVBVIKZLZNABEEAVBKZVAVDUTVHVPUN
    VCUOAVBEFOUAVPURVFUSVGVPUPVEUQAVBHIPUBAVBBUCQRBVIKZVDVKVHVOVQUOVJVCBVIEFOUD
    VQVFVMVGVNVQUQVLVEBVIHIPUEBVIVBUFQRVBVIAECTUGBECTUGUHUIUKUJ $.
    $( [13-Mar-2006] $) $( [8-Apr-2001] $)

  $( The square function on nonnegative reals is strictly monotonic. $)
  lt2sq $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
              ( A < B <-> ( A ^ 2 ) < ( B ^ 2 ) ) ) $=
    ( cr wcel cc0 cle wbr wa clt cmul co c2 cexp lt2msq wb recn sqval breqan12d
    cc syl2an ad2ant2r bitr4d ) ACDZEAFGZHBCDZEBFGZHHABIGAAJKZBBJKZIGZALMKZBLMK
    ZIGZABNUCUEULUIOZUDUFUCASDZBSDZUMUEAPBPUNUOUJUGUKUHIAQBQRTUAUB $.
    $( [24-Feb-2006] $)

  $( The square function on nonnegative reals is monotonic. $)
  le2sq $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
              ( A <_ B <-> ( A ^ 2 ) <_ ( B ^ 2 ) ) ) $=
    ( cr wcel cc0 cle wbr wa cmul co c2 cexp le2msq wb cc recn breqan12d syl2an
    sqval ad2ant2r bitr4d ) ACDZEAFGZHBCDZEBFGZHHABFGAAIJZBBIJZFGZAKLJZBKLJZFGZ
    ABMUBUDUKUHNZUCUEUBAODZBODZULUDAPBPUMUNUIUFUJUGFASBSQRTUA $.
    $( [26-Jul-2006] $) $( [18-Oct-1999] $)

  $( The square of a 'less than or equal to' ordering. $)
  le2sq2 $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ A <_ B ) ) ->
              ( A ^ 2 ) <_ ( B ^ 2 ) ) $=
    ( cr wcel cc0 cle wbr wa c2 cexp co simprr wb simprl 0re mp3an1 exp4b com23
    wi letr imp43 jca le2sq syldan mpbid ) ACDZEAFGZHZBCDZABFGZHZHZUJAIJKBIJKFG
    ZUHUIUJLUHUKUIEBFGZHUJUMMULUIUNUHUIUJNUFUGUIUJUNUFUIUGUJUNSUFUIUGUJUNECDUFU
    IUGUJHUNSOEABTPQRUAUBABUCUDUE $.
    $( [21-Mar-2008] $)

  $( A square of a real is nonnegative. $)
  sqge0 $p |- ( A e. RR -> 0 <_ ( A ^ 2 ) ) $=
    ( cr wcel cc0 c2 cexp co cle wbr cif wceq opreq1 breq2d elimel sqge0i dedth
    0re ) ABCZDAEFGZHIDRADJZEFGZHIADATKSUADHATEFLMTADBQNOP $.
    $( [18-Oct-1999] $)

  ${
    sumsqne0.1 $e |- A e. RR $.
    sumsqne0.2 $e |- B e. RR $.
    $( The sum of two squares is nonzero iff one of its terms is nonzero. $)
    sumsqne0i $p |- ( ( A =/= 0 \/ B =/= 0 ) <->
                   ( ( A ^ 2 ) + ( B ^ 2 ) ) =/= 0 ) $=
      ( cc0 wne c2 cexp co caddc clt wbr sqgt0i cle sqge0i cr wcel resqcli wceq
      wb wo addge01 mp2an mpbi readdcli ltletri mpan2 gt0ne0i 3syl addge02 jaoi
      0re wn wa opreq1 opreqan12d cn 2nn 0exp ax-mp opreq12i 0cn addid1i syl6eq
      eqtri con3i df-ne neorian 3imtr4i impbii ) AEFZBEFZUAZAGHIZBGHIZJIZEFZVKV
      QVLVKEVNKLZEVPKLZVQACMVRVNVPNLZVSEVONLZVTBDOVNPQZVOPQZWAVTTACRZBDRZVNVOUB
      UCUDEVNVPULWDVNVOWDWEUEZUFUGVPWFUHZUIVLEVOKLZVSVQBDMWHVOVPNLZVSEVNNLZWIAC
      OWCWBWJWITWEWDVOVNUJUCUDEVOVPULWEWFUFUGWGUIUKVPESZUMAESZBESZUNZUMVQVMWNWK
      WNVPEGHIZWOJIZEWLWMVNWOVOWOJAEGHUOBEGHUOUPWPEEJIEWOEWOEJGUQQWOESURGUSUTZW
      QVAEVBVCVEVDVFVPEVGAEBEVHVIVJ $.
      $( [25-Jul-2006] $) $( [29-Apr-2005] $)
  $}

  $( The square of 0 is 0. $)
  sq0 $p |- ( 0 ^ 2 ) = 0 $=
    ( cc0 c2 cexp co wceq eqid 0cn sqeq0i mpbir ) ABCDAEAAEAFAGHI $.
    $( [6-Jun-2006] $)

  $( If a number is zero, its square is zero.  (Contributed by FL,
     10-Dec-2006.) $)
  sq0i $p |- ( A = 0 -> ( A ^ 2 ) = 0 ) $=
    ( cc0 wceq c2 cexp co opreq1 sq0 syl6eq ) ABCADEFBDEFBABDEGHI $.
    $( [10-Dec-2006] $)

  $( The square of 1 is 1. $)
  sq1 $p |- ( 1 ^ 2 ) = 1 $=
    ( c2 cn0 wcel c1 cexp co wceq 2nn0 1exp ax-mp ) ABCDAEFDGHAIJ $.
    $( [8-May-2007] $) $( [22-Aug-1999] $)

  $( The square of 2 is 4. $)
  sq2 $p |- ( 2 ^ 2 ) = 4 $=
    ( c2 cexp co cmul c4 2cn sqvali 2t2e4 eqtri ) AABCAADCEAFGHI $.
    $( [22-Aug-1999] $)

  $( The square of 3 is 9. $)
  sq3 $p |- ( 3 ^ 2 ) = 9 $=
    ( c3 c2 cexp co cmul c9 3re recni sqvali 3t3e9 eqtri ) ABCDAAEDFAAGHIJK $.
    $( [26-Apr-2006] $)

  $( The cube of 2 is 8. $)
  cu2 $p |- ( 2 ^ 3 ) = 8 $=
    ( c2 c3 cexp co c1 caddc cmul c8 df-3 opreq2i wcel cn0 wceq 2cn expp1 mp2an
    cc 2nn0 c4 sq2 opreq1i 4t2e8 eqtri 3eqtri ) ABCDAAEFDZCDZAACDZAGDZHBUEACIJA
    QKALKUFUHMNRAAOPUHSAGDHUGSAGTUAUBUCUD $.
    $( [28-Jul-2006] $) $( [2-Aug-2004] $)

  $( The reciprocal of ` _i ` . $)
  irec $p |- ( 1 / _i ) = -u _i $=
    ( c1 ci cdiv co cneg wceq cmul ax-icn mulneg2i ax-1cn mulcli negcon2i mpbir
    ixi eqtr4i negcli ine0 divmuli ) ABCDBEZFBSGDZAFTBBGDZEZABBHHIAUBFUAAEFNAUA
    JBBHHKLMOABSJHBHPQRM $.
    $( [11-Oct-1999] $)

  $( ` _i ` squared. $)
  i2 $p |- ( _i ^ 2 ) = -u 1 $=
    ( ci c2 cexp co cmul c1 cneg ax-icn sqvali ixi eqtri ) ABCDAAEDFGAHIJK $.
    $( [29-May-2005] $) $( [6-May-1999] $)

  $( ` _i ` cubed. $)
  i3 $p |- ( _i ^ 3 ) = -u _i $=
    ( ci c3 cexp co c2 c1 caddc cmul cneg df-3 opreq2i cc wcel wceq ax-icn 2nn0
    cn0 expp1 mp2an i2 opreq1i mulm1i eqtri 3eqtri ) ABCDAEFGDZCDZAECDZAHDZAIZB
    UEACJKALMEQMUFUHNOPAERSUHFIZAHDUIUGUJAHTUAAOUBUCUD $.
    $( [31-Jan-2007] $)

  $( ` _i ` to the fourth power. $)
  i4 $p |- ( _i ^ 4 ) = 1 $=
    ( ci c2 caddc co cexp cmul c4 c1 cc wcel cn0 wceq ax-icn expadd mp3an 2p2e4
    2nn0 opreq2i i2 ax-1cn cneg opreq12i mul2negi mulid1i 3eqtri 3eqtr3i ) ABBC
    DZEDZABEDZUIFDZAGEDHAIJBKJZUKUHUJLMQQABBNOUGGAEPRUJHUAZULFDHHFDHUIULUIULFSS
    UBHHTTUCHTUDUEUF $.
    $( [31-Jan-2007] $)

  $( A counterexample showing that exponentiation is not associative.
     (Contributed by Stefan Allan and G&eacute;rard Lang, 21-Sep-2010.) $)
  expnass $p |- ( ( 3 ^ 3 ) ^ 3 ) < ( 3 ^ ( 3 ^ 3 ) ) $=
    ( c3 cmul co cexp clt cc wcel cn0 wceq 3nn nncni mp3an wbr 3re 1lt3 expordi
    w3a c2 mpanr12 eqbrtrri nnnn0i expmul cr nn0mulcli nn0expcl mp2an c1 sqvali
    2nn0 2lt3 ) AAABCZDCZAADCZADCZAUMDCZEAFGAHGZUPULUNIAJKZAJUAZURAAAUBLAUCGZUK
    HGZUMHGZULUOEMZNAAURURUDUPUPVAURURAAUEUFUSUTVAQUGAEMZUKUMEMVBOARDCZUKUMEAUQ
    UHUSRHGZUPVDUMEMZNUIURUSVEUPQVCRAEMVFOUJARAPSLTAUKUMPSLT $.
    $( [21-Sep-2010] $)

  $( Cancel one factor of a square in a ` <_ ` comparison.  Unlike ~ lemul1 ,
     the common factor ` A ` may be zero. $)
  sqlecan $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
      ( ( A ^ 2 ) <_ ( B x. A ) <-> A <_ B ) ) $=
    ( cr wcel cc0 cle wbr wa c2 cexp co cmul wb clt wceq wi 0re cc recn syl w3a
    wo leloe mpan sqval breq1d 3ad2ant1 lemul1 3exp exp4a pm2.43a adantrd com23
    bitr4d sq0 leidi eqbrtri mul01 syl5breqr adantl opreq1 opreq2 breq12d mpbid
    adantr adantrr breq1 biimpa adantrl 2thd ex a1i jaod sylbid imp31 ) ACDZEAF
    GZBCDZEBFGZHZAIJKZBALKZFGZABFGZMZVPVQEANGZEAOZUBZVTWEPZECDVPVQWHMQEAUCUDVPW
    FWIWGVPVTWFWEVPVRWFWEPZVSVRVPWJVPVRVPWFWEVPVRVPWFHZWEVPVRWKUAWCAALKZWBFGZWD
    VPVRWCWMMWKVPWAWLWBFVPARDWAWLOASAUETUFUGABAUHUNUIUJUKULUMWGWIPVPWGVTWEWGVTH
    WCWDWGVRWCVSWGVRHEIJKZBELKZFGZWCVRWPWGVRWNEWOFWNEEFUOEQUPUQVRBRDWOEOBSBURTU
    SUTWGWPWCMVRWGWNWAWOWBFEAIJVAEABLVBVCVEVDVFWGVSWDVRWGVSWDEABFVGVHVIVJVKVLVM
    VNVO $.
    $( [17-Jan-2008] $)

  $( Factor the difference of two squares. $)
  subsq $p |- ( ( A e. CC /\ B e. CC ) ->
          ( ( A ^ 2 ) - ( B ^ 2 ) ) = ( ( A + B ) x. ( A - B ) ) ) $=
    ( cc wcel caddc co cmin cmul c2 cexp wceq subcl syl3anc subdi sqval opreq1d
    adantr eqtr4d adantl syl2anc wa simpl simpr adddir 3anidm12 mulcom opreq12d
    sqcl mulcl addsubass addcom pncan3 eqtrd eqtr3d 3eqtrrd ) ACDZBCDZUAZABEFAB
    GFZHFZAUSHFZBUSHFZEFZAIJFZABHFZGFZVEBIJFZGFZEFZVDVGGFZURUPUQUSCDUTVCKUPUQUB
    ZUPUQUCZABLABUSUDMURVAVFVBVHEURVAAAHFZVEGFZVFUPUQVAVNKAABNUEURVDVMVEGUPVDVM
    KUQAOQPRURVBBAHFZBBHFZGFZVHURUQUPUQVBVQKVLVKVLBABNMURVEVOVGVPGABUFUQVGVPKUP
    BOSUGRUGURVFVEEFZVGGFZVIVJURVFCDZVECDZVGCDZVSVIKURVDCDZWAVTUPWCUQAUHQZABUIZ
    VDVELTZWEUQWBUPBUHSVFVEVGUJMURVRVDVGGURVRVEVFEFZVDURVTWAVRWGKWFWEVFVEUKTURW
    AWCWGVDKWEWDVEVDULTUMPUNUO $.
    $( [21-Feb-2008] $)

  $( Express the difference of the squares of two numbers as a polynomial in
     the difference of the numbers. $)
  subsq2 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A ^ 2 ) - ( B ^ 2 ) ) =
    ( ( ( A - B ) ^ 2 ) + ( ( 2 x. B ) x. ( A - B ) ) ) ) $=
    ( cc wcel wa caddc co cmin cmul c2 cexp wceq 2cn mulcl mpan adantl subadd23
    mpd3an3 opreq1d eqtrd 2times pncan anidms opreq2d subcl adddir eqtr3d subsq
    syl3anc sqval syl 3eqtr4d ) ACDZBCDZEZABFGZABHGZIGZUQUQIGZJBIGZUQIGZFGZAJKG
    BJKGHGUQJKGZVAFGUOUQUTFGZUQIGZURVBUOVDUPUQIUOVDAUTBHGZFGZUPUMUNUTCDZVDVGLUN
    VHUMJCDUNVHMJBNOPZABUTQRUOVFBAFUNVFBLUMUNVFBBFGZBHGZBUNUTVJBHBUASUNVKBLBBUB
    UCTPUDTSUOUQCDZVHVLVEVBLABUEZVIVMUQUTUQUFUIUGABUHUOVCUSVAFUOVLVCUSLVMUQUJUK
    SUL $.
    $( [21-Feb-2008] $)

  ${
    binom2.1 $e |- A e. CC $.
    binom2.2 $e |- B e. CC $.
    $( The square of a binomial. $)
    binom2i $p |- ( ( A + B ) ^ 2 ) = ( ( ( A ^ 2 ) + ( 2 x.
                ( A x. B ) ) ) + ( B ^ 2 ) ) $=
      ( caddc co cmul cexp addcli adddii adddiri mulcomi opreq2i eqtri opreq12i
      c2 mulcli addassi opreq1i sqvali eqtr3i 3eqtri 2timesi 3eqtr4i ) ABEFZUEG
      FZAAGFZABGFZUHEFZEFZBBGFZEFZUEPHFAPHFZPUHGFZEFZBPHFZEFUFUEAGFZUEBGFZEFUGU
      HEFZUHUKEFZEFZULUEABABCDIZCDJUQUSURUTEUQUGBAGFZEFUSABACDCKVCUHUGEBADCLMNA
      BBCDDKOUSUHEFZUKEFVAULUSUHUKUGUHAACCQZABCDQZIVFBBDDQRVDUJUKEUGUHUHVEVFVFR
      SUAUBUEVBTUOUJUPUKEUMUGUNUIEACTUHVFUCOBDTOUD $.
      $( [11-Aug-1999] $)

    $( Product of sum and difference. $)
    binom2aiOLD $p |- ( ( A + B ) x. ( A - B ) ) = ( ( A ^ 2 ) - ( B ^ 2 ) ) $=
      ( caddc co cmin cmul c2 cexp subcli adddiri subdii sqvali opreq1i mulcomi
      eqtr4i opreq12i sqcli mulcli addsubassi addcomi pncan3i eqtr3i 3eqtri
      eqtri ) ABEFABGFZHFAUGHFZBUGHFZEFAIJFZABHFZGFZUKBIJFZGFZEFZUJUMGFZABUGCDA
      BCDKLUHULUIUNEUHAAHFZUKGFULAABCCDMUJUQUKGACNOQUIBAHFZBBHFZGFUNBABDCDMUKUR
      UMUSGABCDPBDNRQRULUKEFZUMGFUOUPULUKUMUJUKACSZABCDTZKZVBBDSUAUTUJUMGUTUKUL
      EFUJULUKVCVBUBUKUJVBVAUCUFOUDUE $.
      $( [7-Feb-2005] $)

    $( Factor the difference of two squares. $)
    subsqi $p |- ( ( A ^ 2 ) - ( B ^ 2 ) ) = ( ( A + B ) x. ( A - B ) ) $=
      ( cc wcel c2 cexp co cmin caddc cmul wceq subsq mp2an ) AEFBEFAGHIBGHIJIA
      BKIABJILIMCDABNO $.
      $( [24-Apr-2008] $) $( [7-Feb-2005] $)

    $( The squares of two complex numbers are equal iff one number equals the
       other or its negative.  Lemma 15-4.7 of [Gleason] p. 311 and its
       converse. $)
    sqeqori $p |- ( ( A ^ 2 ) = ( B ^ 2 ) <-> ( A = B \/ A = -u B ) ) $=
      ( c2 cexp co wceq caddc cc0 cmin wo cneg cmul subsqi eqeq1i sqcli subeq0i
      addcli subcli mul0ori 3bitr3i orcom subnegi negcli bitr3i orbi12i 3bitri
      ) AEFGZBEFGZHZABIGZJHZABKGZJHZLZUOUMLABHZABMZHZLUIUJKGZJHULUNNGZJHUKUPUTV
      AJABCDOPUIUJACQBDQRULUNABCDSABCDTUAUBUMUOUCUOUQUMUSABCDRUMAURKGZJHUSVBULJ
      ABCDUDPAURCBDUERUFUGUH $.
      $( [15-Jan-2006] $)

    $( The two solutions to the difference of squares set equal to zero. $)
    subsq0i $p |- ( ( ( A ^ 2 ) - ( B ^ 2 ) ) = 0
                  <-> ( A = B \/ A = -u B ) ) $=
      ( c2 cexp co cmin cc0 wceq cneg wo sqcli subeq0i sqeqori bitri ) AEFGZBEF
      GZHGIJQRJABJABKJLQRACMBDMNABCDOP $.
      $( [4-Aug-2007] $) $( [25-Apr-2006] $)
  $}

  $( The squares of two complex numbers are equal iff one number equals the
     other or its negative.  Lemma 15-4.7 of [Gleason] p. 311 and its
     converse.  (Contributed by Paul Chapman, 15-Mar-2008.) $)
  sqeqor $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( ( A ^ 2 ) = ( B ^ 2 ) <-> ( A = B \/ A = -u B ) ) ) $=
    ( cc wcel c2 cexp co wceq wo wb cc0 cif opreq1 eqeq1 orbi12d bibi12d eqeq2d
    cneg 0cn elimel eqeq1d eqeq2 negeq sqeqori dedth2h ) ACDZBCDZAEFGZBEFGZHZAB
    HZABRZHZIZJUFAKLZEFGZUIHZUOBHZUOULHZIZJUPUGBKLZEFGZHZUOVAHZUOVARZHZIZJABKKA
    UOHZUJUQUNUTVHUHUPUIAUOEFMUAVHUKURUMUSAUOBNAUOULNOPBVAHZUQVCUTVGVIUIVBUPBVA
    EFMQVIURVDUSVFBVAUOUBVIULVEUOBVAUCQOPUOVAAKCSTBKCSTUDUE $.
    $( [15-Mar-2008] $)

  $( The square of a binomial.  (Contributed by FL, 10-Dec-2006.) $)
  binom2 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + B ) ^ 2 ) =
     ( ( ( A ^ 2 ) + ( 2 x. ( A x. B ) ) ) + ( B ^ 2 ) ) ) $=
    ( cc wcel caddc co c2 cexp cmul wceq cc0 cif opreq1 opreq1d opreq2d eqeq12d
    opreq12d opreq2 0cn elimel binom2i dedth2h ) ACDZBCDZABEFZGHFZAGHFZGABIFZIF
    ZEFZBGHFZEFZJUCAKLZBEFZGHFZUMGHFZGUMBIFZIFZEFZUKEFZJUMUDBKLZEFZGHFZUPGUMVAI
    FZIFZEFZVAGHFZEFZJABKKAUMJZUFUOULUTVIUEUNGHAUMBEMNVIUJUSUKEVIUGUPUIUREAUMGH
    MVIUHUQGIAUMBIMOQNPBVAJZUOVCUTVHVJUNVBGHBVAUMERNVJUSVFUKVGEVJURVEUPEVJUQVDG
    IBVAUMIROOBVAGHMQPUMVAAKCSTBKCSTUAUB $.
    $( [27-Nov-2006] $)

  $( If a complex number equals its square, it must be 0 or 1. $)
  sq01 $p |- ( A e. CC -> ( ( A ^ 2 ) = A <-> ( A = 0 \/ A = 1 ) ) ) $=
    ( cc wcel c2 cexp co wceq cc0 c1 wo wa wn wne wi cmul wb ex opreq1 3eqtr4a
    id df-ne sqval mulid1 eqcomd adantr ax-1cn mulcan mp3an2 bitrd biimpd com23
    eqeq12d anabss5 imp syl5bir orrd sq0 sq1 jaoi impbid1 ) ABCZADEFZAGZAHGZAIG
    ZJZVAVCVFVAVCKZVDVEVDLAHMZVGVEAHUAVAVCVHVENVAVHVCVEVAVHVCVENVAVHKZVCVEVIVCA
    AOFZAIOFZGZVEVAVCVLPVHVAVBVJAVKAUBVAVKAAUCUDULUEVAVHVLVEPZVAIBCVIVMUFAIAUGU
    HUMUIUJQUKUNUOUPQVDVCVEVDHDEFHVBAUQAHDERVDTSVEIDEFIVBAURAIDERVETSUSUT $.
    $( [6-Jun-2006] $)

  ${
    crrecz.1 $e |- A e. RR $.
    crrecz.2 $e |- B e. RR $.
    $( Reciprocal of a complex number in terms of real and imaginary
       components.  Remark in [Apostol] p. 361.  (The proof was shortened by
       Jeff Hankins, 16-Dec-2013.) $)
    crreczi $p |- ( ( A =/= 0 \/ B =/= 0 ) -> ( 1 / ( A + ( _i x. B ) ) ) =
                    ( ( A - ( _i x. B ) ) / ( ( A ^ 2 ) + ( B ^ 2 ) ) ) ) $=
      ( cc0 wne c1 ci cmul co caddc cdiv c2 cexp wceq cneg sqcli ax-1cn cc wcel
      wo cmin recni axicn mulcli negsubi sqmuli opreq1i mulneg1i 3eqtri negnegi
      i2 negeqi mulid2i opreq2i subsqi 3eqtr3ri sumsqne0i addcli divasszi sylbi
      a1i subcli divid mpan 3eqtr3d wb divclzi crne0i biimpi wa divmul syl12anc
      mp3an1 mpbird ) AEFBEFUAZGAHBIJZKJZLJAVQUBJZAMNJZBMNJZKJZLJZOZVRWCIJZGOZV
      PVRVSIJZWBLJZWBWBLJZWEGWHWIOVPWGWBWBLVTVQMNJZPZKJVTWJUBJWBWGVTWJAACUCZQZV
      QHBUDBDUCZUEZQUFWKWAVTKWKGWAIJZPZPWPWAWJWQWJHMNJZWAIJGPZWAIJWQHBUDWNUGWRW
      SWAIULUHGWARBWNQZUIUJUMWPGWARWTUEUKWAWTUNUJUOAVQWLWOUPUQUHVBVPWBEFZWHWEOA
      BCDURZVRVSWBAVQWLWOUSZAVQWLWOVCZVTWAWMWTUSZUTVAVPXAWIGOZXBWBSTXAXFXEWBVDV
      EVAVFVPWCSTZVRSTZVREFZWDWFVGZVPXAXGXBVSWBXDXEVHVAXHVPXCVBVPXIABCDVIVJGSTX
      GXHXIVKXJRGWCVRVLVNVMVO $.
      $( [16-Dec-2013] $) $( [29-Apr-2005] $)

    $( Obsolete version of ~ crreczi as of 16-Dec-2013. $)
    crrecziOLD $p |- ( ( A =/= 0 \/ B =/= 0 ) -> ( 1 / ( A + ( _i x. B ) ) ) =
                    ( ( A - ( _i x. B ) ) / ( ( A ^ 2 ) + ( B ^ 2 ) ) ) ) $=
      ( cc0 wne c1 ci cmul co caddc cdiv c2 cexp wceq cneg ax-icn cc wcel wa wo
      cmin renegcli crne0i recni negne0bi orbi2i mulneg2i opreq2i mulcli eqtr2i
      negsubi neeq1i 3bitr4i subcli divid sylbi opreq2d addcli ax-1cn divmuldiv
      mpan mpanl12 mpanr1 mpanl1 sylancb recclzi mulid1 3eqtr3rd mulid2i sqmuli
      syl sqcli opreq1i mulneg1i 3eqtri negeqi negnegi subsqi 3eqtr3ri opreq12i
      i2 syl6eq ) AEFZBEFZUAZGAHBIJZKJZLJZGAWGUBJZIJZWHWJIJZLJZWJAMNJZBMNJZKJZL
      JWFWIWJWJLJZIJZWIGIJZWMWIWFWQGWIIWFWJEFZWQGOZWDBPZEFZUAAHXBIJZKJZEFWFWTAX
      BCBDUCUDWEXCWDBBDUEZUFUGWJXEEXEAWGPZKJWJXDXGAKHBQXFUHUIAWGACUEZHBQXFUJZUL
      UKUMUNZWJRSZWTXAAWGXHXIUOZWJUPVBUQURWFWHEFZWTWRWMOZABCDUDZXJWHRSZXMWTXNAW
      GXHXIUSZXPXMTZXKWTXNXLGRSXKXRXKWTTTXNUTXLGWJWHWJVAVCVDVEVFWFWIRSZWSWIOWFX
      MXSXOWHXQVGUQWIVHVLVIWKWJWLWPLWJXLVJWNWGMNJZPZKJWNXTUBJWPWLWNXTAXHVMWGXIV
      MULYAWOWNKYAGWOIJZPZPYBWOXTYCXTHMNJZWOIJGPZWOIJYCHBQXFVKYDYEWOIWBVNGWOUTB
      XFVMZVOVPVQYBGWOUTYFUJVRWOYFVJVPUIAWGXHXIVSVTWAWC $.
      $( [11-Mar-2008] $) $( [29-Apr-2005] $)
  $}

  ${
    $d j k A $.  $d j k N $.
    $( Bernoulli's inequality, due to Johan Bernoulli (1667-1748). $)
    bernneq $p |- ( ( A e. RR /\ N e. NN0 /\ -u 1 <_ A ) ->
                 ( 1 + ( A x. N ) ) <_ ( ( 1 + A ) ^ N ) ) $=
      ( cr wcel c1 cle wbr cmul co caddc cexp wi cc0 wceq opreq2 opreq2d adantr
      wa cc syl2anc vj vk cn0 cneg cv breq12d imbi2d recn ax-1cn addid1i syl6eq
      mul01 1re leidi addcl mpan syl5breqr eqbrtrd nn0re remulcl sylan2 readdcl
      syl sylancr simpl reexpcl sylan ax-mulrcl anidms msqge0 jca nn0ge0 mulge0
      exp0 syl2an nn0cn adantl mul32 syl3anc breqtrd addge01 mpbid mulcl addass
      wb muladd11 eqtr4d renegcli leadd2 mp3an13 negidi breq1i ad2ant2r lemul1a
      syl6bb biimpa simprr letrd adddi mp3an3 mulid1 eqtrd mp3an1 expp1 3brtr4d
      syl31anc exp43 com12 imp3a a2d nn0ind exp3a 3imp ) ACDZBUCDZEUDZAFGZEABHI
      ZJIZEAJIZBKIZFGZXOXNXQYBLXOXNXQYBXNXQRZEAUAUEZHIZJIZXTYDKIZFGZLYCEAMHIZJI
      ZXTMKIZFGZLYCEAUBUEZHIZJIZXTYMKIZFGZLYCEAYMEJIZHIZJIZXTYRKIZFGZLYCYBLUAUB
      BYDMNZYHYLYCUUCYFYJYGYKFUUCYEYIEJYDMAHOPYDMXTKOUFUGYDYMNZYHYQYCUUDYFYOYGY
      PFUUDYEYNEJYDYMAHOPYDYMXTKOUFUGYDYRNZYHUUBYCUUEYFYTYGUUAFUUEYEYSEJYDYRAHO
      PYDYRXTKOUFUGYDBNZYHYBYCUUFYFXSYGYAFUUFYEXREJYDBAHOPYDBXTKOUFUGXNYLXQXNAS
      DZYLAUHZUUGYJEYKFUUGYJEMJIEUUGYIMEJAULPEUIUJUKUUGEEYKFEUMUNUUGXTSDZYKENES
      DZUUGUUIUIEAUOZUPXTVNVCUQURVCQYMUCDZYCYQUUBUULXNXQYQUUBLZXNUULXQUUMLXNUUL
      XQYQUUBXNUULRZXQYQRZRZYOAJIZYPXTHIZYTUUAFUUPUUQYOXTHIZUURUUNUUQCDZUUOUUNY
      OCDZXNUUTUUNECDZYNCDZUVAUMUULXNYMCDZUVCYMUSZAYMUTZVAEYNVBVDZXNUULVEYOAVBT
      ZQUUNUUSCDZUUOUUNUVAXTCDZUVIUVGXNUVJUULUVBXNUVJUMEAVBUPZQZYOXTUTTQUUNUURC
      DZUUOUUNYPCDZUVJUVMXNUVJUULUVNUVKXTYMVFVGZUVLYPXTUTTQUUNUUQUUSFGUUOUUNUUQ
      UUQYNAHIZJIZUUSFUUNMUVPFGZUUQUVQFGZUUNMAAHIZYMHIZUVPFXNUVTCDZMUVTFGZRUVDM
      YMFGZRMUWAFGUULXNUWBUWCXNUWBAAVHVIAVJVKUULUVDUWDUVEYMVLVKUVTYMVMVOUUNUUGU
      UGYMSDZUWAUVPNXNUUGUULUUHQZUWFUULUWEXNYMVPZVQAAYMVRVSVTUUNUUTUVPCDZUVRUVS
      WEUVHUULXNUVDUWHUVEXNUVDRUVCXNUWHUVFXNUVDVEYNAUTTVAUUQUVPWATWBXNUUGUWEUVQ
      UUSNUULUUHUWGUUGUWERZUVQYOAUVPJIJIZUUSUWIYOSDZUUGUVPSDZUVQUWJNUWIUUJYNSDZ
      UWKUIAYMWCZEYNUOVDUUGUWEVEZUWIUWMUUGUWLUWNUWOYNAWCTYOAUVPWDVSUWIUWMUUGUUS
      UWJNUWNUWOYNAWFTWGVOVTQUUPUVAUVNUVJMXTFGZRYQUUSUURFGUUNUVAUUOUVGQUUNUVNUU
      OUVOQUUPUVJUWPUUNUVJUUOUVLQXNXQUWPUULYQXNXQUWPXNXQEXPJIZXTFGZUWPXPCDXNUVB
      XQUWRWEEUMWHUMXPAEWIWJUWQMXTFEUIWKWLWOWPWMVKUUNXQYQWQYOYPXTWNXFWRUUNYTUUQ
      NZUUOXNUUGUWEUWSUULUUHUWGUWIYTEYNAJIZJIZUUQUWIYSUWTEJUWIYSYNAEHIZJIZUWTUU
      GUWEUUJYSUXCNUIAYMEWSWTUWIUXBAYNJUUGUXBANUWEAXAQPXBPUWIUWMUUGUUQUXANZUWNU
      WOUUJUWMUUGUXDUIEYNAWDXCTWGVOQUUNUUAUURNZUUOXNUUIUULUXEXNUUJUUGUUIUIUUHUU
      KVDXTYMXDVGQXEXGXHXIXJXKXLXHXM $.
      $( [25-Jul-2009] $) $( [21-Feb-2005] $)

    $( Bernoulli's inequality, due to Johan Bernoulli (1667-1748). $)
    bernneqOLD $p |- ( ( A e. RR /\ N e. NN0 /\ -u 1 <_ A ) ->
                 ( 1 + ( A x. N ) ) <_ ( ( 1 + A ) ^ N ) ) $=
      ( cr wcel c1 cle wbr cmul co caddc cexp wi cc0 wceq opreq2 opreq2d adantr
      wa cc syl2anc vj vk cn0 cneg cv breq12d imbi2d recn ax-1cn addid1i syl6eq
      mul01 1re leidi addcl mpan syl5breqr eqbrtrd nn0re remulcl sylan2 readdcl
      exp0 syl sylancr simpl reexpcl sylan anidms msqge0 nn0ge0 mulge0OLD nn0cn
      anim12i adantl mul32 syl3anc breqtrd wb addge01 mpbid mulcl addass eqtr4d
      muladd11 syl2an renegcli leadd2 mp3an13 negidi breq1i syl6bb ad2ant2r jca
      biimpa simprr syl31anc letrd adddi mp3an3 mulid1 eqtrd mp3an1 expp1 exp43
      lemul1a 3brtr4d com12 imp3a a2d nn0ind exp3a 3imp ) ACDZBUCDZEUDZAFGZEABH
      IZJIZEAJIZBKIZFGZXOXNXQYBLXOXNXQYBXNXQRZEAUAUEZHIZJIZXTYDKIZFGZLYCEAMHIZJ
      IZXTMKIZFGZLYCEAUBUEZHIZJIZXTYMKIZFGZLYCEAYMEJIZHIZJIZXTYRKIZFGZLYCYBLUAU
      BBYDMNZYHYLYCUUCYFYJYGYKFUUCYEYIEJYDMAHOPYDMXTKOUFUGYDYMNZYHYQYCUUDYFYOYG
      YPFUUDYEYNEJYDYMAHOPYDYMXTKOUFUGYDYRNZYHUUBYCUUEYFYTYGUUAFUUEYEYSEJYDYRAH
      OPYDYRXTKOUFUGYDBNZYHYBYCUUFYFXSYGYAFUUFYEXREJYDBAHOPYDBXTKOUFUGXNYLXQXNA
      SDZYLAUHZUUGYJEYKFUUGYJEMJIEUUGYIMEJAULPEUIUJUKUUGEEYKFEUMUNUUGXTSDZYKENE
      SDZUUGUUIUIEAUOZUPXTVCVDUQURVDQYMUCDZYCYQUUBUULXNXQYQUUBLZXNUULXQUUMLXNUU
      LXQYQUUBXNUULRZXQYQRZRZYOAJIZYPXTHIZYTUUAFUUPUUQYOXTHIZUURUUNUUQCDZUUOUUN
      YOCDZXNUUTUUNECDZYNCDZUVAUMUULXNYMCDZUVCYMUSZAYMUTZVAEYNVBVEZXNUULVFYOAVB
      TZQUUNUUSCDZUUOUUNUVAXTCDZUVIUVGXNUVJUULUVBXNUVJUMEAVBUPZQZYOXTUTTQUUNUUR
      CDZUUOUUNYPCDZUVJUVMXNUVJUULUVNUVKXTYMVGVHZUVLYPXTUTTQUUNUUQUUSFGUUOUUNUU
      QUUQYNAHIZJIZUUSFUUNMUVPFGZUUQUVQFGZUUNMAAHIZYMHIZUVPFUUNUVTCDZUVDRMUVTFG
      ZMYMFGZRMUWAFGXNUWBUULUVDXNUWBAAUTVIUVEVNXNUWCUULUWDAVJYMVKVNUVTYMVLTUUNU
      UGUUGYMSDZUWAUVPNXNUUGUULUUHQZUWFUULUWEXNYMVMZVOAAYMVPVQVRUUNUUTUVPCDZUVR
      UVSVSUVHUULXNUVDUWHUVEXNUVDRUVCXNUWHUVFXNUVDVFYNAUTTVAUUQUVPVTTWAXNUUGUWE
      UVQUUSNUULUUHUWGUUGUWERZUVQYOAUVPJIJIZUUSUWIYOSDZUUGUVPSDZUVQUWJNUWIUUJYN
      SDZUWKUIAYMWBZEYNUOVEUUGUWEVFZUWIUWMUUGUWLUWNUWOYNAWBTYOAUVPWCVQUWIUWMUUG
      UUSUWJNUWNUWOYNAWETWDWFVRQUUPUVAUVNUVJMXTFGZRYQUUSUURFGUUNUVAUUOUVGQUUNUV
      NUUOUVOQUUPUVJUWPUUNUVJUUOUVLQXNXQUWPUULYQXNXQUWPXNXQEXPJIZXTFGZUWPXPCDXN
      UVBXQUWRVSEUMWGUMXPAEWHWIUWQMXTFEUIWJWKWLWOWMWNUUNXQYQWPYOYPXTXFWQWRUUNYT
      UUQNZUUOXNUUGUWEUWSUULUUHUWGUWIYTEYNAJIZJIZUUQUWIYSUWTEJUWIYSYNAEHIZJIZUW
      TUUGUWEUUJYSUXCNUIAYMEWSWTUWIUXBAYNJUUGUXBANUWEAXAQPXBPUWIUWMUUGUUQUXANZU
      WNUWOUUJUWMUUGUXDUIEYNAWCXCTWDWFQUUNUUAUURNZUUOXNUUIUULUXEXNUUJUUGUUIUIUU
      HUUKVEXTYMXDVHQXGXEXHXIXJXKXLXHXM $.
      $( [11-Oct-2006] $) $( [21-Feb-2005] $)
  $}

  $( Variation of Bernoulli's inequality ~ bernneq . $)
  bernneq2 $p |- ( ( A e. RR /\ N e. NN0 /\ 0 <_ A ) ->
               ( ( ( A - 1 ) x. N ) + 1 ) <_ ( A ^ N ) ) $=
    ( cr wcel cn0 cc0 cle wbr w3a c1 cmin co caddc cexp 3ad2ant1 wa wceq ax-1cn
    cc sylancr cmul cneg peano2rem simp2 df-neg 0re 1re lesub1 mp3an13 syl5eqbr
    biimpa 3adant2 bernneq syl3anc recnd nn0cn mulcl syl2an addcom 3adant3 recn
    wb pncan3 opreq1d 3brtr3d ) ACDZBEDZFAGHZIZJAJKLZBUALZMLZJVJMLZBNLZVKJMLZAB
    NLZGVIVJCDZVGJUBZVJGHZVLVNGHVFVGVQVHAUCZOVFVGVHUDVFVHVSVGVFVHPVRFJKLZVJGJUE
    VFVHWAVJGHZFCDVFJCDVHWBVBUFUGFAJUHUIUKUJULVJBUMUNVFVGVLVOQZVHVFVGPJSDZVKSDZ
    WCRVFVJSDBSDWEVGVFVJVTUOBUPVJBUQURJVKUSTUTVFVGVNVPQVHVFVMABNVFWDASDVMAQRAVA
    JAVCTVDOVE $.
    $( [18-Oct-2007] $)
  $( A corrolary of ~ bernneq .  (Contributed by Mario Carneiro,
     11-Mar-2014.) $)
  bernneq3 $p |- ( ( P e. ( ZZ>= ` 2 ) /\ N e. NN0 ) ->
                   N < ( P ^ N ) ) $=
    ( c2 wcel cn0 wa c1 caddc co cr adantl peano2re syl adantr syl2anc wbr cmul
    cle cc0 nn0ge0 cuz cfv cexp nn0re eluzelre reexpcl clt ltp1 cmin cn uz2m1nn
    simpr nnre ax-mulrcl cc wceq recnd mulid2 1re a1i lemul1a syl31anc eqbrtrrd
    nnge1 wb leadd1 syl3anc mpbid 2nn0 nn0uz uztrn2 mpan bernneq2 letrd ltletrd
    jca ) ACUAUBDZBEDZFZBBGHIZABUCIZVRBJDZVQBUDKZVSWBVTJDWCBLMZVSAJDZVRWAJDVQWE
    VRCAUENZVQVRULZABUFOZVSWBBVTUGPWCBUHMVSVTAGUIIZBQIZGHIZWAWDVSWJJDZWKJDVSWIJ
    DZWBWLVSWIUJDZWMVQWNVRAUKNZWIUMMZWCWIBUNOZWJLMWHVSBWJRPZVTWKRPZVSGBQIZBWJRV
    SBUODWTBUPVSBWCUQBURMVSGJDZWMWBSBRPZFGWIRPZWTWJRPXAVSUSUTZWPVSWBXBWCVRXBVQB
    TKVPVSWNXCWOWIVDMGWIBVAVBVCVSWBWLXAWRWSVEWCWQXDBWJGVFVGVHVSWEVRSARPZWKWARPW
    FWGVQXEVRVQAEDZXECEDVQXFVISACEVJVKVLATMNABVMVGVNVO $.
    $( [11-Mar-2014] $)

  ${
    $d j k A $.  $d j k B $.
    $( MAY BE REVISED - note that min w/ ad2antlr, ad2antrl make compressed
       proof longer $)
    $( Exponentiation with a mantissa greater than 1 has no upper bound. $)
    expnbnd $p |- ( ( A e. RR /\ B e. RR /\ 1 < B ) ->
          E. k e. NN A < ( B ^ k ) ) $=
      ( cr wcel c1 clt wbr cexp co cn wa 1re syl adantr cle cc0 adantl syl2anc
      wb w3a cv wrex 1nn wi axlttrn mp3an2 exp4b com34 3imp1 wceq recn 3ad2ant2
      cc exp1 breqtrrd opreq2 breq2d rcla4ev sylancr cmin cfl cfv caddc cn0 wne
      cdiv peano2rem posdif biimpa gt0ne0 syldan redivcl syl3anc adantll subge0
      mpan sylan mpan2 biimparc divge0 syl2an flge0nn0 nn0p1nn simplr peano2nn0
      jca cmul nn0re remulcl peano2re simprl reexpcl ltdivmulOLD syl31anc mpbid
      flltp1 ltsubadd lt01 0re mp3an12 mpani ltle syld imp bernneq2 exp43 com4l
      ltletrd simp1 a1i pm2.61ltlei ) ADEZBDEZFBGHZUAZABCUBZIJZGHZCKUCZAFXPAFGH
      ZLZFKEABFIJZGHZXTUDYBABYCGXMXNXOYAABGHZXMXNYAXOYEXMXNYAXOYEXMFDEZXNYAXOLY
      EUEMAFBUFUGUHUIUJXPYCBUKZYAXNXMYGXOXNBUNEYGBULBUONUMOUPXSYDCFKXQFUKXRYCAG
      XQFBIUQURUSUTXMXNXOFAPHZXTYHXMXNXOXTYHXMXNXOXTYHXMLZXNXOLZLZAFVAJZBFVAJZV
      GJZVBVCZFVDJZKEZABYPIJZGHZXTYKYOVEEZYQYKYNDEZQYNPHZYTXMYJUUAYHXMYJLYLDEZY
      MDEZYMQVFZUUAXMUUCYJAVHZOYJUUDXMXNUUDXOBVHZOZRYJUUEXMXNXOQYMGHZUUEXNXOUUI
      YFXNXOUUITMFBVIVQVJZXNUUDUUIUUEUUGYMVKVRVLRYLYMVMVNVOZYIUUCQYLPHZLUUDUUIL
      UUBYJYIUUCUULXMUUCYHUUFRZXMUULYHXMYFUULYHTMAFVPVSVTWGYJUUDUUIUUHUUJWGYLYM
      WAWBYNWCSZYOWDNYKAYMYPWHJZFVDJZYRYHXMYJWEZYKUUODEZUUPDEYKUUDYPDEZUURYJUUD
      YIUUHRZYKYPVEEZUUSYKYTUVAUUNYOWFNZYPWINZYMYPWJSZUUOWKNYKXNUVAYRDEYIXNXOWL
      ZUVBBYPWMSYKYLUUOGHZAUUPGHZYKYNYPGHZUVFYKUUAUVHUUKYNWQNYKUUCUUDUUSUUIUVHU
      VFTYIUUCYJUUMOUUTUVCYJUUIYIUUJRYLYMYPWNWOWPYKXMUURUVFUVGTZUUQUVDXMYFUURUV
      IMAFUUOWRUGSWPYKXNUVAQBPHZUUPYRPHUVEUVBYJUVJYIXNXOUVJXNXOQBGHZUVJXNQFGHZX
      OUVKWSQDEZYFXNUVLXOLUVKUEWTMQFBUFXAXBUVMXNUVKUVJUEWTQBXCVQXDXERBYPXFVNXIX
      SYSCYPKXQYPUKXRYRAGXQYPBIUQURUSSXGXHUJXMXNXOXJYFXPMXKXL $.
      $( [20-Oct-2007] $)

    $( The reciprocal of exponentiation with a mantissa greater than 1 has no
       lower bound. $)
    expnlbnd $p |- ( ( A e. RR+ /\ B e. RR /\ 1 < B ) ->
          E. k e. NN ( 1 / ( B ^ k ) ) < A ) $=
      ( crp wcel cr c1 clt wbr w3a cdiv co cv cexp cn wrex cc0 syl2anc adantr
      wa wne rpre rpne0 rereccl expnbnd syl3an1 wb elrp biimpi 3ad2ant1 reexpcl
      cn0 nnnn0 sylan2 adantlr simpll adantl lt01 wi 0re 1re lttr mp3an12 mpani
      imp expgt0 syl3anc jca 3adantl1 ltrec1 rexbidva mpbid ) ADEZBFEZGBHIZJZGA
      KLZBCMZNLZHIZCOPZGVSKLAHIZCOPVMVQFEZVNVOWAVMAFEZAQUAWCAUBAUCAUDRVQBCUEUFV
      PVTWBCOVPVROEZTWDQAHITZVSFEZQVSHIZTZVTWBUGVPWFWEVMVNWFVOVMWFAUHUIUJSVNVOW
      EWIVMVNVOTZWETZWGWHVNWEWGVOWEVNVRULEZWGVRUMZBVRUKUNUOWKVNWLQBHIZWHVNVOWEU
      PWEWLWJWMUQWJWNWEVNVOWNVNQGHIZVOWNURQFEGFEVNWOVOTWNUSUTVAQGBVBVCVDVESBVRV
      FVGVHVIAVSVJRVKVL $.
      $( [18-Jul-2008] $)

    $( The reciprocal of exponentiation with a mantissa greater than 1 has no
       lower bound. $)
    expnlbnd2 $p |- ( ( A e. RR+ /\ B e. RR /\ 1 < B ) ->
          E. j e. NN A. k e. NN ( j <_ k -> ( 1 / ( B ^ k ) ) < A ) ) $=
      ( wcel cr c1 clt wbr co cn cle wi wa ad2antrr cc0 adantlr adantr syl3anc
      ex crp w3a cv cexp cdiv wrex wral expnlbnd cn0 simpll adantl ad2antlr 1re
      nnnn0 ltle mpan imp anim1i expwordi syl31anc reexpcl sylan2 lt01 0re lttr
      mp3an12 mpani expgt0 lerec syl22anc sylibd 3adantl1 imp31 wne recn gt0ne0
      wb cc syldan expne0i rereccl syl2anc rpre 3ad2ant1 lelttr mpand ralrimdva
      com23 reximdva mpd ) AUAEZBFEZGBHIZUBZGBCUCZUDJZUEJZAHIZCKUFWODUCZLIZGBWS
      UDJZUEJZAHIZMZDKUGZCKUFABCUHWNWRXECKWNWOKEZNZWRXDDKXGWSKEZNZWTWRXCXIWTWRX
      CMXIWTNXBWQLIZWRXCXGXHWTXJWLWMXFXHWTXJMZMWKWLWMNZXFNZXHXKXMXHNZWTWPXALIZX
      JXNWTXOXNWTNWLWOUIEZWSUIEZGBLIZWTNXOXMWLXHWTWLWMXFUJZOXMXPXHWTXFXPXLWOUNZ
      UKZOXHXQXMWTWSUNZULXNXRWTXLXRXFXHWLWMXRGFEZWLWMXRMUMGBUOUPUQOURBWOWSUSUTT
      XNWPFEZPWPHIZXAFEZPXAHIZXOXJVQXMYDXHWLXFYDWMXFWLXPYDXTBWOVAVBQZRXMYEXHXMW
      LXPPBHIZYEXSYAXLYIXFWLWMYIWLPGHIZWMYIVCPFEYCWLYJWMNYIMVDUMPGBVEVFVGUQZRBW
      OVHSRXLXHYFXFWLXHYFWMXHWLXQYFYBBWSVAVBQZQXLXHYGXFXLXHNZWLXQYIYGWLWMXHUJXH
      XQXLYBUKZXLYIXHYKRBWSVHSQWPXAVIVJVKTVLVMXIXJWRNXCMZWTXIXBFEZWQFEZAFEZYOWN
      XHYPXFWLWMXHYPWKYMYFXAPVNZYPYLYMBVREZBPVNZXQYSWLYTWMXHBVOZOXLUUAXHWLWMYIU
      UAYKBVPVSZRYNBWSVTSXAWAWBVLQXGYQXHWLWMXFYQWKXMYDWPPVNZYQYHXMYTUUAXPUUDWLY
      TWMXFUUBOXLUUAXFUUCRYABWOVTSWPWAWBVLRWNYRXFXHWKWLYRWMAWCWDOXBWQAWESRWFTWH
      WGWIWJ $.
      $( [18-Jul-2008] $)
  $}

  $( Two ways to express the ` K ` th digit in the decimal (when base
     ` B = 10 ` ) expansion of a number ` A ` . ` K = 1 ` corresponds to the
     first digit after the decimal point. $)
  digit2 $p |- ( ( A e. RR /\ B e. NN /\ K e. NN )
    -> ( ( |_ ` ( ( B ^ K ) x. A ) ) mod B ) = ( ( |_ ` ( ( B ^ K ) x.
      A ) ) - ( B x. ( |_ ` ( ( B ^ ( K - 1 ) ) x. A ) ) ) ) ) $=
    ( cr wcel cn cexp co cmul cfl cfv cdiv cmin wceq wa syl2an 3ad2ant2 syl2anc
    sylan cc w3a cmo crp cn0 nnre nnnn0 reexpcl remulcl 3impa 3comr reflcl nnrp
    syl modval simp2 fldiv cc0 wne nncn expcl 3adant1 recn 3ad2ant1 nnne0 div23
    c1 jca syl3anc expm1 3expa opreq1d eqtr4d fveq2d eqtrd opreq2d ) ADEZBFEZCF
    EZUAZBCGHZAIHZJKZBUBHZWBBWBBLHJKZIHZMHZWBBBCVFMHGHZAIHZJKZIHZMHVSWBDEZBUCEZ
    WCWFNVSWADEZWKVQVRVPWMVQVRVPWMVQVROVTDEZVPWMVQBDECUDEZWNVRBUECUFZBCUGPVTAUH
    SUIUJZWAUKUMVQVPWLVRBULQWBBUNRVSWEWJWBMVSWDWIBIVSWDWABLHZJKZWIVSWMVQWDWSNWQ
    VPVQVRUOWABUPRVSWRWHJVSWRVTBLHZAIHZWHVSVTTEZATEZBTEZBUQURZOZWRXANVQVRXBVPVQ
    XDWOXBVRBUSZWPBCUTPVAVPVQXCVRAVBVCVQVPXFVRVQXDXEXGBVDVGZQVTABVEVHVSWGWTAIVQ
    VRWGWTNZVPVQXFVRXIXHXDXEVRXIBCVIVJSVAVKVLVMVNVOVOVN $.
    $( [25-Dec-2008] $)

  $( Two ways to express the ` K ` th digit in the decimal expansion of a
     number ` A ` (when base ` B = 10 ` ). ` K = 1 ` corresponds to the first
     digit after the decimal point. $)
  digit1 $p |- ( ( A e. RR /\ B e. NN /\ K e. NN ) -> ( ( |_ ` ( ( B ^ K ) x.
        A ) ) mod B ) = ( ( ( |_ ` ( ( B ^ K ) x. A ) ) mod ( B ^ K ) )
        - ( ( B x. ( |_ ` ( ( B ^ ( K - 1 ) ) x. A ) ) ) mod ( B ^ K ) ) ) ) $=
    ( cn wcel cr cexp co cmul cfl cfv cmo c1 wceq wa cle wbr syl syl2anc adantr
    cmin digit2 3coml 3expa opreq1d crp cc0 clt cn0 nnre reexpcl syl2an remulcl
    nnnn0 sylan reflcl nnrp ad2antrr modcl nnexpcl sylan2 modge0 nncn exp1 1nn0
    modlt cc a1i adantl nnge1 anim12i expwordi syl31anc eqbrtrrd modid syl22anc
    ltletrd simpll nnm1nn0 syl3anc expm1t expcl simpl mulcom eqtrd opreq2d recn
    modmulnn mulass fveq2d opreq12d 3brtr4d modsubdir mpbid 3eqtr3d 3impa 3comr
    wb ) BDEZCDEZAFEZBCGHZAIHZJKZBLHZXDXBLHZBBCMUAHZGHZAIHZJKZIHZXBLHZUAHZNZWSW
    TXAXNWSWTOZXAOZXEXBLHZXDXKUAHZXBLHZXEXMXPXEXRXBLWSWTXAXEXRNZXAWSWTXTABCUBUC
    UDUEXPXEFEZXBUFEZUGXEPQZXEXBUHQXQXENXPXDFEZBUFEZYAXPXCFEZYDXOXBFEZXAYFWSBFE
    ZCUIEZYGWTBUJZCUNZBCUKULZXBAUMUOXCUPRZWSYEWTXABUQURZXDBUSSZXOYBXAXOXBDEZYBW
    TWSYIYPYKBCUTVAXBUQRTZXPYDYEYCYMYNXDBVBSXPXEBXBYOWSYHWTXAYJURZXOYGXAYLTXPYD
    YEXEBUHQYMYNXDBVFSXOBXBPQXAXOBMGHZBXBPWSYSBNZWTWSBVGEZYTBVCZBVDRTXOYHMUIEZY
    IMBPQZMCPQZOYSXBPQWSYHWTYJTUUCXOVEVHWTYIWSYKVIWSUUDWTUUEBVJCVJVKBMCVLVMVNTV
    QXEXBVOVPXPXLXFPQZXSXMNZXPXKBXHIHZLHZBXIIHZJKZUUHLHZXLXFPXPWSXIFEZXHDEZUUIU
    ULPQWSWTXAVRXOXHFEZXAUUMWSYHXGUIEZUUOWTYJCVSZBXGUKULXHAUMUOZXOUUNXAWTWSUUPU
    UNUUQBXGUTVATXIXHBWHVTXPXBUUHXKLXOXBUUHNZXAWSUUAWTUUSUUBUUAWTOZXBXHBIHZUUHB
    CWAUUTXHVGEZUUAUVAUUHNWTUUAUUPUVBUUQBXGWBZVAUUAWTWCXHBWDSWEUOTZWFXPXDUUKXBU
    UHLXPXCUUJJXPXCUUHAIHZUUJXPXBUUHAIUVDUEXPUUAUVBAVGEZUVEUUJNWSUUAWTXAUUBURXO
    UVBXAWSUUAUUPUVBWTUUBUUQUVCULTXAUVFXOAWGVIBXHAWIVTWEWJUVDWKWLXPYDXKFEZYBUUF
    UUGWRYMXPYHXJFEZUVGYRXPUUMUVHUURXIUPRBXJUMSYQXDXKXBWMVTWNWOWPWQ $.
    $( [3-Jan-2009] $)

$(
  @( Two ways to express the ` K ` th digit in the decimal expansion of a
     number ` A ` (when base ` B = 10 ` ). ` K = 1 ` corresponds to the first
     digit after the decimal point. @)
  digit3 @p |- ( ( A e. RR /\ B e. NN /\ K e. NN ) -> ( ( B ^ K ) x. ( ( A mod
 ( 1 / ( B ^ K ) ) ) - ( A mod ( 1 / ( B ^ ( K - 1 ) ) ) ) ) ) = ( ( |_ ` ( ( B
   ^ K ) x. A ) ) - ( B x. ( |_ ` ( ( B ^ ( K - 1 ) ) x. A ) ) ) ) ) @=
    ( cr wcel cn w3a cexp co cmul cfl cfv c1 cmin cdiv cmo opreq12d cc wceq
    subdi wa crp cn0 rpexpcl nnrp nnnn0 syl2anOLD rpre syl recnd 3adant1 mulcl
    nnm1nn0 rpreccl rpcn remulcl reexpcl nnre sylan2OLD 3impb reflcl
    sylancOLD cc0
    wne ax-1ne0 redivcl mp3an3 1re sylanclOLD syl3anc eqcomd eqtrd eqid a1i
    nnncan1 recn 3ad2ant1 modval ax-1cn pm3.2i divdiv2 mp3an2 expcl sylanOLD
    expne0i 3expa rpcnne0 jca fveq2d opreq2d adantlr div1 ) ADEZBFEZCFEZGZBCHIZ
    AJIKLBBCMNIZHIZAJIKLJINIZWQAMWQOIZPIZAMWSOIZPIZNIZJIZWPWTWQXCAWSJIZKLZJIZXA
    AWQJIZMOIZKLZJIZNIZJIZXFWPWTWQXIJIWQXMJINIZXO????????QWPXOXPWQREZXIREZXMREZ
    XOXPSWPWQXIXMT??????WNWOUAZWQUBEZWQDEZBUBEZCUCEZYAWNWOBCUDBUEZCUFZUGZWQUHUI
    ZUJUKXCREZXHREXRWPXCXHULWNWOYIWMXTXCUBEZYIXTWSUBEZYJYCWRUCEZYKWNWOBWRUDYECU
    MZUGWSUNUIZXCUOUIUKWPXHWPXGDEZXHDEWMWNWOYOWMWSDEZYOXTAWSUPBDEYLYPWNWOBWRUQB
    URYMUGZUSUTXGVAUIUJVBZWPXMXADEZXLDEZXMDEWPXAXLUPWNWOYSWMXTXAUBEZYSXTYAUUAYG
    WQUNUIZXAUHUIUKWPXKDEZYTXJDEZMDEZUUCWPUUDUUEMVCVDZUUCVEXJMVFVGWMWNWOUUDWMYB
    UUDXTAWQUPYHUSUTVHVIXKVAUIVBUJZVJVKVLWPWQWQXNXEJWQWQSWPWQVMVNWPXNAXMNIZAXIN
    IZNIZXEWPUUJXNAREZXSXRUUJXNSWPAXMXIVOWMWNUUKWOAVPZVQUUGYRVJVKWPXEUUJWMWNWOX
    EUUJSWMXTUAZXBUUHXDUUINUUMXBAXAAXAOIZKLZJIZNIZUUHWMUUAXBUUQSXTAXAVRUUBUSUUM
    UUPXMANUUMUUOXLXAJUUMUUNXKKUUKXQWQVCVDZUAZUUNXKSZWMXTUUKMREZUUFUAZUUSUUTUVA
    UUFVSVEVTZAMWQWAWBUULYCYDUUSWNWOYCYDUAXQUURBREZYDXQYCBCWCBUOWDUVDBVCVDZUAZY
    DUURYCUVDUVEYDUURBCWEWFBWGZWDWHYEYFUGUGWIWJWJVLUUMXDAXCAXCOIZKLZJIZNIZUUIWM
    YJXDUVKSXTAXCVRYNUSUUMUVJXIANUUMUVIXHXCJUUMUVHXGKUUMUVHXGMOIZXGUUKWSREZWSVC
    VDZUAZUVHUVLSZWMXTUUKUVBUVOUVPUVCAMWSWAWBUULUVFYLUVOWNWOUVFYLUAUVMUVNUVDYLU
    VMUVEBWRWCWKUVDUVEYLUVNBWRWEWFWHWNYCUVFYEUVGUIYMUGUGUUMXGREZUVLXGSUUKUVMUVQ
    WMXTAWSULUULXTWSYQUJUGXGWLUIVLWIWJWJVLQUTVKVLQVLVK @.
    @( [ ?] @) @( [27-Dec-2008] @)
$)
  ${
    $d k x A $.  $d k x B $.  $d k x D $.  $d x C $.
    $( Exponentiation property of the modulo operation.  (Contributed by Mario
       Carneiro, 28-Feb-2014.) $)
    modexp $p |- ( ( ( A e. ZZ /\ B e. ZZ ) /\ ( C e. NN0 /\ D e. RR+ )
         /\ ( A mod D ) = ( B mod D ) )
         -> ( ( A ^ C ) mod D ) = ( ( B ^ C ) mod D ) ) $=
      ( cz wcel cmo co wceq wi cc0 c1 opreq2 opreq1d eqeq12d imbi2d syl syl2anc
      cexp cmul vx vk wa cn0 crp w3a simp2l id 3adant2l cv caddc cc exp0 eqcomd
      zcn sylan9eq 3ad2ant1 cr simp21l simp1 zexpcl zre simp21r simp3 syl221anc
      simp22 modmul1 simp23 recnd mulcom 3eqtr4d eqtrd expp1 3exp nn0ind sylc
      a2d ) AEFZBEFZUCZCUDFZDUEFZUCADGHBDGHIZUFWAVTWBWCUFZACSHZDGHZBCSHZDGHZIZV
      TWAWBWCUGVTWBWCWDWAWDUHUIWDAUAUJZSHZDGHZBWJSHZDGHZIZJWDAKSHZDGHZBKSHZDGHZ
      IZJWDAUBUJZSHZDGHZBXASHZDGHZIZJWDAXALUKHZSHZDGHZBXGSHZDGHZIZJWDWIJUAUBCWJ
      KIZWOWTWDXMWLWQWNWSXMWKWPDGWJKASMNXMWMWRDGWJKBSMNOPWJXAIZWOXFWDXNWLXCWNXE
      XNWKXBDGWJXAASMNXNWMXDDGWJXABSMNOPWJXGIZWOXLWDXOWLXIWNXKXOWKXHDGWJXGASMNX
      OWMXJDGWJXGBSMNOPWJCIZWOWIWDXPWLWFWNWHXPWKWEDGWJCASMNXPWMWGDGWJCBSMNOPVTW
      BWTWCVTWPWRDGVRVSWPLWRVRAULFZWPLIAUOAUMQVSWRLVSBULFZWRLIBUOBUMQUNUPNUQXAU
      DFZWDXFXLXSWDXFXLXSWDXFUFZXBATHZDGHZXDBTHZDGHZXIXKXTYBXDATHZDGHZYDXTXBURF
      ZXDURFZVRWBXFYBYFIXTXBEFZYGXTVRXSYIVRVSWBWCXSXFUSZXSWDXFUTZAXAVARXBVBQXTX
      DEFZYHXTVSXSYLVRVSWBWCXSXFVCZYKBXAVARZXDVBQZYJXSVTWBWCXFVFZXSWDXFVDXBXDAD
      VGVEXTAXDTHZDGHZBXDTHZDGHZYFYDXTAURFZBURFZYLWBWCYRYTIXTVRUUAYJAVBQZXTVSUU
      BYMBVBQZYNYPXSVTWBWCXFVHABXDDVGVEXTYEYQDGXTXDULFZXQYEYQIXTXDYOVIZXTAUUCVI
      ZXDAVJRNXTYCYSDGXTUUEXRYCYSIUUFXTBUUDVIZXDBVJRNVKVLXTXHYADGXTXQXSXHYAIUUG
      YKAXAVMRNXTXJYCDGXTXRXSXJYCIUUHYKBXAVMRNVKVNVQVOVP $.
      $( [28-Feb-2014] $)
  $}


