$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Integer powers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c recNN0 $. $( Recursion on nonnegative integers. $)
  $c ^ $. $( Exponentiation. $)

  $( Extend class notation to include exponentiation of a complex number to an
     integer power. $)
  cexp $a class ^ $.

  ${
    $d x y z w v u t $.
    $( Define exponentiation to nonnegative integer powers.  The first ` rec `
       provides a recursion from ` om ` to complex numbers, and the second
       ` rec ` is a direct one-to-one map from nonnegative integers to
       ` om ` .  See ~ exp0 , ~ exp1 , ~ expp1 , and ~ expp1z for the standard
       recursive definition.  The up-arrow notation is used by Donald Knuth for
       iterated exponentiation (_Science_ 194, 1235-1242, 1976) and is
       convenient for us since we don't have superscripts.  $)
    df-exp $a |- ^ = { <. <. x , y >. , z >. | ( ( x e. CC /\ y e. NN0 ) /\
          z = ( ( rec ( { <. w , v >. | v = ( w x. x ) } , 1 ) o.
        `' ( rec ( { <. w , v >. | v = ( w + 1 ) } , 0 ) |` om ) ) ` y ) ) } $.
  $}

  ${
    $d x y z w v $.  $d z w v G $.  $d z w v A $. $d z w v B $.
    om2nn0.1 $e |- G = ( rec ( { <. x , y >. | y = ( x + 1 ) } , 0 ) |` om ) $.
    $( The mapping ` G ` is a one-to-one mapping from ` om ` onto ` NN0 ` that
       will be used to construct a recursive definition generator on ` NN0 ` .
       Ordinal natural number 0 maps to complex number 0, 1 maps to 1, etc.
       This theorem shows the value of ` G ` at natural number zero.
       (Contributed by Raph Levien, 10-Apr-04.) $)
    om2nn00 $p |- ( G ` (/) ) = 0 $=
      ( c0 cfv cv c1 caddc co wceq copab cc0 crdg com cres fveq1i cn0 wcel
      0nn0 frzer ax-mp eqtr ) ECFEBGAGHIJKABLZMNOPZFZMECUEDQMRSUFMKTMRUDUAUBUC
      $.
      $( [24-May-04] $) $( [19-May-04] $)

    $( The value of ` G ` (see ~ om2nn00 ) at a successor.  (Contributed by
       Raph Levien, 10-Apr-04.) $)
    om2nn0suc $p |- ( A e. om -> ( G ` suc A ) = ( ( G ` A ) + 1 ) ) $=
      ( vw vz cv csuc cfv c1 caddc co wceq com suceq fveq2d fveq2 opreq1d
      cleq12d wcel cvv oprex cc0 ax-17 wel copab crdg cres wal hbopab1 hbrdg
      hbres hbfv hbopr fveq1i opreq1i eleq2i bial 3imtr4 opreq1 frsucopab
      mpan2 vtoclga ) FHZIZDJZVEDJZKLMZNZCIZDJZCDJZKLMZNFCOVECNZVGVLVIVNVOVFVKD
      VECPQVOVHVMKLVECDRSTVEOUAVIUBUAVJVHKLUCABGUDVEAHZKLMZVIUBDGHZUDUAAUEZGFUF
      AUEZVRVEBHVQNZABUGZUDUHZOUIZJZKLMZUAZWGAUJVRVIUAZWHAUJAGWEKLAGVEWDAGWCOAG
      UDWBWAABGUKVSULVROUAAUEUMVTUNVRLUAAUEVRKUAAUEUOVIWFVRVHWEKLVEDWDEUPUQURZW
      HWGAWIUSUTEVPVHKLVAVBVCVD $.
      $( [24-May-04] $) $( [19-May-04] $)

    $( Functionality of ` G ` (see ~ om2nn00 ).  (Contributed by Raph Levien,
       10-Apr-04.) $)
    om2nn0fnom $p |- G Fn om $=
      ( com wfn cv c1 caddc co wceq copab cc0 crdg cres frfnom wb fneq1 ax-mp
      mpbir ) CEFZBGAGHIJKABLZMNEOZEFZMUBPCUCKUAUDQDECUCRST $.
      $( [24-May-04] $) $( [19-May-04] $)

    $( The value ` G ` (see ~ om2nn00 ) at an ordinal natural number is a
       nonnegative integer (a member of the ` NN0 ` subset of complex numbers
       ` CC ` ).  (Contributed by Raph Levien, 10-Apr-04.) $)
    om2nn0nn0 $p |- ( A e. om -> ( G ` A ) e. NN0 ) $=
      ( vz vw cv cfv cn0 wcel c0 csuc wceq fveq2 eleq1d weq cc0 om2nn00 0nn0
      eqeltr com c1 caddc co om2nn0suc peano2nn0 syl5bir finds ) FHZDIZJKLDIZJK
      GHZDIZJKZUMMZDIZJKZCDIZJKFGCUJLNUKULJUJLDOPFGQUKUNJUJUMDOPUJUPNUKUQJUJUPD
      OPUJCNUKUSJUJCDOPULRJABDESTUAUMUBKZURUNUCUDUEZJKUOUTUQVAJABUMDEUFPUNUGUHU
      I $.
      $( [24-May-04] $) $( [19-May-04] $)

    $( Less-than relation for ` G ` (see ~ om2nn00 ).  (Contributed by Raph
       Levien, 10-Apr-04.) $)
    om2nn0lt $p |- ( ( A e. om /\ B e. om ) ->
                   ( A e. B -> ( G ` A ) < ( G ` B ) ) ) $=
      ( vz vw com wcel cfv clt wbr wi cv c0 csuc wceq eleq2 fveq2 breq2d
      bi2imd bilimd weq noel pm2.21i a1i wa wo wb elsuc2g bicomd adantl cle cr
      leloet cn0 om2nn0nn0 nn0ret syl syl2an c1 caddc co nn0lelt1 om2nn0suc
      bitr4d bitr3d id im2ord syl5bi exp com12 a2d finds imp ) CIJZDIJZCDJZCEKZ
      DEKZLMZNZVRVQWCVQCGOZJZVTWDEKZLMZNZNVQCPJZVTPEKZLMZNZNVQCHOZJZVTWMEKZLMZN
      ZNVQCWMQZJZVTWREKZLMZNZNVQWCNGHDWDPRZWHWLVQXCWEWIWGWKWDPCSXCWFWJVTLWDPETU
      AUBUCGHUDZWHWQVQXDWEWNWGWPWDWMCSXDWFWOVTLWDWMETUAUBUCWDWRRZWHXBVQXEWEWSWG
      XAWDWRCSXEWFWTVTLWDWRETUAUBUCWDDRZWHWCVQXFWEVSWGWBWDDCSXFWFWAVTLWDDETUAUB
      UCWLVQWIWKCUEUFUGWMIJZVQWQXBVQXGWQXBNZVQXGXHVQXGUHZWNCWMRZUIZWPVTWORZUIZN
      XBWQXIXKWSXMXAXGXKWSUJVQXGWSXKCWMIUKULUMXIVTWOUNMZXMXAVTUOJZWOUOJZXNXMUJV
      QXGVTWOUPVQVTUQJZXOABCEFURZVTUSUTXGWOUQJZXPABWMEFURZWOUSUTVAXIXNVTWOVBVCV
      DZLMZXAXQXSXNYBUJVQXGVTWOVEXRXTVAXGXAYBUJVQXGWTYAVTLABWMEFVFUAUMVGVHUBWQW
      NWPXJXLWQVIXJXLNWQCWMETUGVJVKVLVMVNVOVMVP $.
      $( [25-May-04] $) $( [19-May-04] $)

    $( Equality relation for ` G ` (see ~ om2nn00 ).  (Contributed by Raph
       Levien, 10-Apr-04.) $)
    om2nn0eq $p |- ( ( A e. om /\ B e. om ) ->
                   ( ( G ` A ) = ( G ` B ) -> A = B ) ) $=
      ( com wcel wa cfv wceq clt wbr wo wn cr wb lttri3t cn0 om2nn0nn0 nn0ret
      syl syl2an ioran syl6bbr word ordtri3 nnord bicon2d om2nn0lt wi ancoms
      im2ord sylbird con1d sylbid ) CGHZDGHZIZCEJZDEJZKZUTVALMZVAUTLMZNZOZCDKZU
      SVBVCOVDOIZVFUTPHZVAPHZVBVHQUQURUTVARUQUTSHVIABCEFTUTUAUBURVASHVJABDEFTVA
      UAUBUCVCVDUDUEUSVGVEUSVGOCDHZDCHZNZVEUSVGVMCUFDUFVGVMOQUQURCDUGCUHDUHUCUI
      USVKVCVLVDABCDEFUJURUQVLVDUKABDCEFUJULUMUNUOUP $.
      $( [25-May-04] $) $( [19-May-04] $)

    $( Range of ` G ` (see ~ om2nn00 ).  (Contributed by Raph Levien,
       10-Apr-04.) $)
    om2nn0ran $p |- ran G = NN0 $=
      ( vv vz vw crn cn0 cv wcel cfv wceq com wrex wfn wb om2nn0fnom fvelrn
      ax-mp eleq1 om2nn0nn0 syl5bi com12 r19.23aiv sylbi cc0 c1 caddc co c0
      om2nn00 peano1 fnfvrn mp2an eqeltrr wi wa csuc om2nn0suc opreq1 sylan9eq
      cleqcomd peano2 mpan syl adantr eqeltrd exp a1i nn0ind impbi cleqri ) ECH
      ZIEJZVNKZVOIKZVPFJZCLZVOMZFNOZVQCNPZVPWAQABCDRZFNVOCSTVTVQFNVTVRNKZVQVTVS
      IKVQWDVSVOIUAABVRCDUBUCUDUEUFVRVNKUGVNKGJZVNKZWEUHUIUJZVNKZVPFGVOVRUGVNUA
      VRWEVNUAVRWGVNUAVRVOVNUAUKCLZUGVNABCDULWBUKNKWIVNKWCUMNUKCUNUOUPWFWHUQWEI
      KWFVSWEMZFNOZWHWBWFWKQWCFNWECSTWJWHFNWDWJWHWDWJURZWGVRUSZCLZVNWLWNWGWDWJW
      NVSUHUIUJWGABVRCDUTVSWEUHUIVAVBVCWDWNVNKZWJWDWMNKZWOVRVDWBWPWOWCNWMCUNVEV
      FVGVHVIUEUFVJVKVLVM $.
      $( [26-May-04] $) $( [19-May-04] $)

    $( ` G ` (see ~ om2nn00 ) is a one-to-one onto mapping.  (Contributed by
       Raph Levien, 10-Apr-04.) $)
    om2nn0f1o $p |- G : om -1-1-onto-> NN0 $=
      ( vz vw com cn0 wf1o wf1 crn wceq wa wf cv cfv weq wi wral wfn wss
      om2nn0fnom om2nn0ran eqimss ax-mp pm3.2i df-f mpbir om2nn0eq rgen2 f1fv
      f1o5 ) GHCIGHCJZCKZHLZMUMUOUMGHCNZEOZCPFOZCPLEFQRZFGSEGSZMUPUTUPCGTZUNHUA
      ZMVAVBABCDUBUOVBABCDUCZUNHUDUEUFGHCUGUHUSEFGABUQURCDUIUJUFEFGHCUKUHVCUFGH
      CULUH $.
      $( [26-May-04] $) $( [19-May-04] $)

    $( A helper lemma for the value of an ` NN0 ` -based recursive definition
       generator with characteristic function ` F ` and initial value ` A ` .
       Normally ` F ` is an operation on ` NN0 ` , and ` A ` is a member of
       ` NN0 ` , and   (Contributed by Raph Levien, 10-Apr-04.) $)
    nn0rval $p |- ( B e. NN0 -> ( ( rec ( F , A ) o. `' G ) ` B ) =
                  ( rec ( F , A ) ` ( `' G ` B ) ) ) $=
      ( cn0 wcel ccnv cdm crdg ccom cfv wceq crn om2nn0ran df-rn eqtr3 eleq2i
      wfun wa con0 wfn rdgfnon fnfun ax-mp com wf1o om2nn0f1o f1ocnv f1ofun
      pm3.2i fvco mpan sylbi ) DHIDFJZKZIZDECLZUQMNDUQNUTNOZHURDFPHURABFGQFRSTU
      TUAZUQUAZUBUSVAVBVCUTUCUDVBCEUEUCUTUFUGHUHUQUIZVCUHHFUIVDABFGUJUHHFUKUGHU
      HUQULUGUMDUTUQUNUOUP $.
      $( [26-May-04] $) $( [20-May-04] $)

    $( A helper lemma for the value of an ` NN0 ` -based recursive definition
       generator.  (Contributed by Raph Levien, 10-Apr-04.) $)
    nn0rzer $p |- ( A e. B -> ( ( rec ( F , A ) o. `' G ) ` 0 ) = A ) $=
      ( wcel c0 crdg cfv cc0 ccnv ccom rdgzert cn0 wceq 0nn0 nn0rval ax-mp
      om2nn00 com wf1o wi om2nn0f1o peano1 f1ocnvfv mp2an fveq2i eqtr syl5eq )
      CDHIECJZKZCLULFMZNKZCDEOUOLUNKZULKZUMLPHUOUQQRABCLEFGSTUPIULIFKLQZUPIQZAB
      FGUAUBPFUCIUBHURUSUDABFGUEUFUBPILFUGUHTUIUJUK $.
      $( [26-May-04] $) $( [20-May-04] $)

    $( A helper lemma for the value of an ` NN0 ` -based recursive definition
       generator.  (Contributed by Raph Levien, 10-Apr-04.) $)
    nn0rsuc $p |- ( B e. NN0 -> ( ( rec ( F , A ) o. `' G ) ` ( B + 1 ) )
                  = ( F ` ( ( rec ( F , A ) o. `' G ) ` B ) ) ) $=
      ( cn0 wcel c1 caddc co crdg ccnv ccom cfv csuc wceq peano2nn0 nn0rval
      syl com wf wf1o om2nn0f1o f1ocnv ax-mp f1of ffvrn mpan om2nn0suc
      f1ocnvfv2 opreq1d eqtrd wi peano2 f1ocnvfv 3syl mpd fveq2d con0 nnont
      rdgsuct 3eqtrd eqtr4d ) DHIZDJKLZECMZFNZOZPZDVIPZVHPZEPZDVJPZEPVFVKVGVIPZ
      VHPZVLQZVHPZVNVFVGHIVKVQRDSABCVGEFGTUAVFVPVRVHVFVRFPZVGRZVPVRRZVFVTVLFPZJ
      KLZVGVFVLUBIZVTWDRHUBVIUCZVFWEHUBVIUDZWFUBHFUDZWGABFGUEZUBHFUFUGHUBVIUHUG
      HUBDVIUIUJZABVLFGUKUAVFWCDJKWHVFWCDRWIUBHDFULUJUMUNVFWEVRUBIZWAWBUOZWJVLU
      PWHWKWLWIUBHVRVGFUQUJURUSUTVFWEVLVAIVSVNRWJVLVBCVLEVCURVDVFVOVMEABCDEFGTU
      TVE $.
      $( [26-May-04] $) $( [20-May-04] $)

    $( A helper lemma for the value of an ` NN0 ` -based recursive definition
       generator.  (Contributed by Raph Levien, 10-Apr-04.) $)
    nn0rone $p |- ( A e. B -> ( ( rec ( F , A ) o. `' G ) ` 1 ) =
                  ( F ` A ) ) $=
      ( wcel cc0 crdg ccnv ccom cfv c1 nn0rzer fveq2d caddc co 1cn addid2
      fveq2i cn0 wceq 0nn0 nn0rsuc ax-mp eqtr3 syl5eq ) CDHZIECJFKLZMZEMZCEMNUJ
      MZUIUKCEABCDEFGOPINQRZUJMZUMULUNNUJNSTUAIUBHUOULUCUDABCIEFGUEUFUGUH $.
      $( [27-May-04] $) $( [20-May-04] $)

    $( A helper lemma for the value of an ` NN0 ` -based recursive definition
       generator.  (Contributed by Raph Levien, 10-Apr-04.) $)
    nn0rfnnn0 $p |- ( rec ( F , A ) o. `' G ) Fn NN0 $=
      ( crdg ccnv ccom cn0 wfn cvv wf con0 rdgfnon fnf mpbi com wss wf1o
      om2nn0f1o f1ocnv ax-mp f1of omsson fss mp2an fco mpbir ) DCGZEHZIZJKJLULM
      ZNLUJMZJNUKMZUMUJNKUNCDONUJPQJRUKMZRNSUOJRUKTZUPRJETUQABEFUARJEUBUCJRUKUD
      UCUEJRNUKUFUGJNLUJUKUHUGJULPUI $.
      $( [27-May-04] $) $( [20-May-04] $)

    ${
      $d z w v F $.  $d x y A $.
      on2nn0.2 $e |- F = { <. x , y >. | y = ( x x. A ) } $.
      $( Value of exponentiation to nonnegative integer powers.  ` F ` is the
         characteristic function that generates exponentiation.  (Contributed
         by Raph Levien, 10-Apr-04.) $)
      expvalt $p |- ( ( A e. CC /\ B e. NN0 ) -> ( A ^ B ) =
                     ( ( rec ( F , 1 ) o. `' G ) ` B ) ) $=
        ( vz vw vv cc cn0 cv cmulc co wceq copab c1 crdg caddc cc0 com cres
        ccnv ccom cfv cexp fvex opreq2 cleq2d biopabdv rdgeq1 syl coeq1d
        fveq1d fveq2 ax-mp coeq1i cnveq coeq2i eqtr fveq1i syl5eqr df-exp
        oprabval2 ) IJKCDLMJNZBNZANZINZOPZQZABRZSTZVHVISUAPQABRUBTUCUDZUEZUFZUG
        DESTZFUEZUFZUGZUHVGVHVICOPZQZABRZSTZVPUFZUGZDVTUIVJCQZVGVQWFWHVNWEVPWHV
        MWDQVNWEQWHVLWCABWHVKWBVHVJCVIOUJUKULSVMWDUMUNUOUPVGDQVGVTUGWAWGVGDVTUQ
        VGVTWFVTWEVSUFWFVRWEVSEWDQVRWEQHSEWDUMURUSVSVPWEFVOQVSVPQGFVOUTURVAVBVC
        VDIJKABVEVF $.
        $( [27-May-04] $) $( [20-May-04] $)
    $}
  $}

  ${
    $d x y z A $.  $d x z B $.
    $( Value of a complex number raised to the 0th power.  Note that under our
       definition, 0 to the 0th power is 1, which although logically
       consistent is not universally agreed upon in the literature.  To prevent
       conflicts with other possible definitions it is preferable to base
       theorems on ~ exp0 instead, which leaves 0 to the 0th power undefined.
       (Contributed by Raph Levien, 10-Apr-04.) $)
    exp00 $p |- ( A e. CC -> ( A ^ 0 ) = 1 ) $=
      ( vy vx cc wcel cc0 cexp co cv cmulc wceq copab c1 crdg caddc com cres
      ccnv ccom cfv cn0 0nn0 cleqid expvalt mpan2 1nn0 nn0rzer ax-mp syl6eq )
      ADEZAFGHZFBIZCIZAJHKCBLZMNULUMMOHKCBLFNPQZRSTZMUJFUAEUKUPKUBCBAFUNUOUOUCZ
      UNUCUDUEMUAEUPMKUFCBMUAUNUOUQUGUHUI $.
      $( [28-May-04] $) $( [20-May-04] $)

    $( Value of a complex number raised to an integer power plus one.  Because
       this theorem allows 0 to the 0th power, it is preferable to use ~ expp1
       or ~ expp1z instead.  (Contributed by Raph Levien, 10-Apr-04.) $)
    expp10 $p |- ( ( A e. CC /\ B e. NN0 ) -> ( A ^ ( B + 1 ) ) =
                 ( ( A ^ B ) x. A ) ) $=
      ( vy vx vz cc wcel cn0 wa c1 caddc co cv cmulc wceq copab crdg cc0 com
      cres ccnv ccom cfv cexp cleqid nn0rsuc hbopab1 ax-17 hbrdg hbres hbcnv
      hbco hbfv hbopr fvex oprex opreq1 fvopabf syl6eq adantl expvalt
      peano2nn0 sylan5 opreq1d 3eqtr4d ) AFGZBHGZIZBJKLZCMZDMZANLZOZDCPZJQZVJVK
      JKLOZDCPZRQZSTZUAZUBZUCZBWAUCZANLZAVIUDLZABUDLZANLVGWBWDOVFVGWBWCVNUCWDDC
      JBVNVSVSUEZUFDCEWCVLWDDEBWADEVOVTDEJVNVMDCEUGEMZJGDUHUIDEVSDEVRSDERVQVPDC
      EUGWHRGDUHUIWHSGDUHUJUKULWHBGDUHUMZDEWCANWIWHNGDUHWHAGDUHUNBWAUOWCANUPVKW
      CANUQURUSUTVFVIHGWEWBOVGDCAVIVNVSWGVNUEZVABVBVCVHWFWCANDCABVNVSWGWJVAVDVE
      $.
      $( [28-May-04] $) $( [20-May-04] $)

    $( Closure law for nonnegative integer exponentiation.  Because this
       theorem allows 0 to the 0th power, it is preferable to use ~ expclt
       instead. $)
    expclt0 $p |- ( ( A e. CC /\ B e. NN0 ) -> ( A ^ B ) e. CC ) $=
      ( vx vy cc wcel cn0 cexp co cv wi cc0 c1 caddc wceq opreq2 eleq1d bilimd
      weq 1cn exp00 mpbiri wa cmulc axmulcl exp com12 adantr expp10 bicomd
      sylibd a2d nn0ind imp ) AEFZBGFZABHIZEFZUPUOURUOACJZHIZEFZKUOALHIZEFZKUOA
      DJZHIZEFZKUOAVDMNIZHIZEFZKUOURKCDBUSLOZVAVCUOVJUTVBEUSLAHPQRCDSZVAVFUOVKU
      TVEEUSVDAHPQRUSVGOZVAVIUOVLUTVHEUSVGAHPQRUSBOZVAURUOVMUTUQEUSBAHPQRUOVCME
      FTUOVBMEAUAQUBVDGFZUOVFVIUOVNVFVIKZUOVNVOUOVNUCZVFVEAUDIZEFZVIUOVFVRKVNVF
      UOVRVFUOVRVEAUEUFUGUHVPVIVRVPVHVQEAVDUIQUJUKUFUGULUMUGUN $.
      $( [?] $) $( [26-May-04] $)

    $( Value of a nonzero complex number raised to the 0th power. $)
    exp0 $p |- ( ( A e. CC /\ A =/= 0 ) -> ( A ^ 0 ) = 1 ) $=
      ( cc wcel cc0 cexp co c1 wceq wne exp00 adantr ) ABCADEFGHADIAJK $.
      $( [19-Aug-04] $) $( [31-Jul-04] $)

    $( Value of a complex number raised to a natural number power plus one. $)
    expp1 $p |- ( ( A e. CC /\ B e. NN ) -> ( A ^ ( B + 1 ) ) =
                 ( ( A ^ B ) x. A ) ) $=
      ( cc wcel cn0 c1 caddc co cexp cmulc wceq cn expp10 nnnn0t sylan5 ) ACDBE
      DABFGHIHABIHAJHKBLDABMBNO $.
      $( [1-Aug-04] $) $( [31-Jul-04] $)

    $( Value of a nonzero complex number raised to a nonnegative integer power
       plus one. $)
    expp1z $p |- ( ( A e. CC /\ B e. NN0 /\ A =/= 0 ) -> ( A ^ ( B + 1 ) ) =
                 ( ( A ^ B ) x. A ) ) $=
      ( cc wcel cn0 c1 caddc co cexp cmulc wceq cc0 wne expp10 3adant3 ) ACDBED
      ABFGHIHABIHAJHKALMABNO $.
      $( [19-Aug-04] $) $( [1-Aug-04] $)
  $}

  $( Value of a complex number raised to the first power.  (Contributed by Raph
     Levien, 10-Apr-04.) $)
  exp1 $p |- ( A e. CC -> ( A ^ 1 ) = A ) $=
    ( cc wcel c1 cexp co cc0 cmulc caddc cn0 wceq 0nn0 expp10 mpan2 1cn addid2
    opreq2i syl5eqr exp00 opreq1d mulid2t 3eqtrd ) ABCZADEFZAGEFZAHFZDAHFAUCAGD
    IFZEFZUFUDUCGJCUHUFKLAGMNUGDAEDOPQRUCUEDAHASTAUAUB $.
    $( [28-May-04] $) $( [20-May-04] $)

  ${
    $d x y A $.
    $( Value of zero raised to a natural number power. $)
    0exp $p |- ( A e. NN -> ( 0 ^ A ) = 0 ) $=
      ( vx vy cc0 cv cexp co wceq c1 caddc opreq2 cleq1d weq cc wcel 0cn exp1
      ax-mp cn cmulc expp1 mpan opreq1 mulzer1 syl6eq syl5bir ind ) DBEZFGZDHDI
      FGZDHZDCEZFGZDHZDULIJGZFGZDHZDAFGZDHBCAUHIHUIUJDUHIDFKLBCMUIUMDUHULDFKLUH
      UOHUIUPDUHUODFKLUHAHUIURDUHADFKLDNOZUKPDQRULSOZUQUMDTGZDHUNUTUPVADUSUTUPV
      AHPDULUAUBLUNVADDTGDUMDDTUCDPUDUEUFUG $.
      $( [?] $) $( [19-Aug-04] $)

    $( Value of one raised to a nonnegative integer power. $)
    1exp $p |- ( A e. NN0 -> ( 1 ^ A ) = 1 ) $=
      ( vx vy c1 cv cexp co wceq cc0 caddc opreq2 cleq1d weq cc wcel wne 1cn
      ax1ne0 exp0 mp2an cn0 cmulc expp1z mp3an3 mpan opreq1 mulid1 syl6eq
      syl5bir nn0ind ) DBEZFGZDHDIFGZDHZDCEZFGZDHZDUODJGZFGZDHZDAFGZDHBCAUKIHUL
      UMDUKIDFKLBCMULUPDUKUODFKLUKURHULUSDUKURDFKLUKAHULVADUKADFKLDNOZDIPZUNQRD
      STUOUAOZUTUPDUBGZDHUQVDUSVEDVBVDUSVEHZQVBVDVCVFRDUOUCUDUELUQVEDDUBGDUPDDU
      BUFDQUGUHUIUJ $.
      $( [?] $) $( [19-Aug-04] $)
  $}

  ${
    $d x y z w A $.  $d z B $.  $d x y z w F $.
    expcllem.1 $e |- F (_ CC $.
    expcllem.2 $e |- ( ( x e. F /\ y e. F ) -> ( x x. y ) e. F ) $.
    $( Lemma for proving natural number exponentiation closure laws. $)
    expcllem $p |- ( ( A e. F /\ B e. NN ) -> ( A ^ B ) e. F ) $=
      ( vz vw cn wcel cexp co cv wi c1 caddc wceq opreq2 eleq1d bilimd weq cc
      sseli exp1 syl ibir wa cmulc opreq1 vtocl2ga ancoms adantlr wb expp1
      sylan adantr mpbird exp31 com12 a2d ind imp ) DJKZCEKZCDLMZEKZVDVEVGVECHN
      ZLMZEKZOVECPLMZEKZOVECINZLMZEKZOVECVMPQMZLMZEKZOVEVGOHIDVHPRZVJVLVEVSVIVK
      EVHPCLSTUAHIUBZVJVOVEVTVIVNEVHVMCLSTUAVHVPRZVJVRVEWAVIVQEVHVPCLSTUAVHDRZV
      JVGVEWBVIVFEVHDCLSTUAVEVLVEVKCEVECUCKZVKCREUCCFUDZCUEUFTUGVMJKZVEVOVRVEWE
      VOVROVEWEVOVRVEWEUHZVOUHVRVNCUIMZEKZVEVOWHWEVOVEWHANZBNZUIMZEKVNWJUIMZEKW
      HABVNCEEWIVNRWKWLEWIVNWJUIUJTWJCRWLWGEWJCVNUISTGUKULUMWFVRWHUNVOWFVQWGEWC
      WEVQWGRVECVMUOWDUPTUQURUSUTVAVBVCUL $.
      $( [3-Aug-04] $) $( [3-Aug-04] $)
  $}

  $( Value of the square of a complex number.  (Contributed by Raph Levien,
     10-Apr-04.) $)
  exp2 $p |- ( A e. CC -> ( A ^ 2 ) = ( A x. A ) ) $=
    ( cc wcel c2 cexp co c1 cmulc caddc cn wceq 1nn expp1 mpan2 df-2 opreq2i
    syl5eq exp1 opreq1d eqtrd ) ABCZADEFZAGEFZAHFZAAHFUAAGGIFZEFZUDUBUAGJCUFUDK
    LAGMNDUEAEOPQUAUCAAHARST $.
    $( [28-May-04] $) $( [20-May-04] $)

  $( Closure of square. $)
  sqclt $p |- ( A e. CC -> ( A ^ 2 ) e. CC ) $=
    ( cc wcel c2 cexp co cmulc exp2 axmulcl anidms eqeltrd ) ABCZADEFAAGFZBAHLM
    BCAAIJK $.
    $( [10-Aug-99] $)

  ${
    sqval.1 $e |- A e. CC $.
    $( Value of square.  Inference version. $)
    sqval $p |- ( A ^ 2 ) = ( A x. A ) $=
      ( cc wcel c2 cexp co cmulc wceq exp2 ax-mp ) ACDAEFGAAHGIBAJK $.
      $( [1-Aug-99] $)

    $( Closure of square. $)
    sqcl $p |- ( A ^ 2 ) e. CC $=
      ( cc wcel c2 cexp co sqclt ax-mp ) ACDAEFGCDBAHI $.
      $( [2-Aug-99] $)

    $( A number is zero iff its square is zero. $)
    sqe0 $p |- ( ( A ^ 2 ) = 0 <-> A = 0 ) $=
      ( c2 cexp co cc0 wceq cmulc sqval cleq1i sq0 bitr ) ACDEZFGAAHEZFGAFGMNFA
      BIJABKL $.
      $( [2-Oct-99] $)

    ${
      sqmul.2 $e |- B e. CC $.
      $( Distribution of square over multiplication. $)
      sqmul $p |- ( ( A x. B ) ^ 2 ) = ( ( A ^ 2 ) x. ( B ^ 2 ) ) $=
        ( cmulc co c2 cexp mul4 mulcl sqval opreq12i 3eqtr4 ) ABEFZNEFAAEFZBBEF
        ZEFNGHFAGHFZBGHFZEFABABCDCDINABCDJKQORPEACKBDKLM $.
        $( [3-Sep-99] $)

      ${
        sqdiv.3 $e |- B =/= 0 $.
        $( Distribution of square over division. $)
        sqdiv $p |- ( ( A / B ) ^ 2 ) = ( ( A ^ 2 ) / ( B ^ 2 ) ) $=
          ( cdiv co cmulc c2 cexp divmuldiv divcl sqval opreq12i 3eqtr4 ) ABFGZ
          PHGAAHGZBBHGZFGPIJGAIJGZBIJGZFGABABCDCDEEKPABCDELMSQTRFACMBDMNO $.
          $( [20-Aug-01] $)
      $}
    $}

    ${
      sqreci.1 $e |- A =/= 0 $.
      $( Square of reciprocal. $)
      sqreci $p |- ( ( 1 / A ) ^ 2 ) = ( 1 / ( A ^ 2 ) ) $=
        ( c1 cdiv co cmulc c2 cexp 1cn divmuldiv mulid1 opreq1i eqtr divcl
        sqval opreq2i 3eqtr4 ) DAEFZSGFZDAAGFZEFZSHIFDAHIFZEFTDDGFZUAEFUBDADAJB
        JBCCKUDDUAEDJLMNSDAJBCOPUCUADEABPQR $.
        $( [17-Sep-99] $)
    $}
  $}

  ${
    $d x y A $.
    $( Closure of nonnegative integer exponentiation of natural numbers. $)
    nnexpclt $p |- ( ( A e. NN /\ B e. NN0 ) -> ( A ^ B ) e. NN ) $=
      ( vx vy cn wcel cc0 wceq wo cexp co cn0 wa andi nnsscn cv nnmulclt
      expcllem c1 1nn opreq2 cc wne exp0 nncnt nnne0t sylan13 sylan9eqr eleq1d
      mpbiri jaoi sylbi elnn0 sylan5b ) AEFZBEFZBGHZIZABJKZEFZBLFUOURMUOUPMZUOU
      QMZIUTUOUPUQNVAUTVBCDABEOCPDPQRVBUTSEFTVBUSSEUQUOUSAGJKZSBGAJUAAUBFAGUCVC
      SHUOAUDAUEAUFUGUHUIUJUKULBUMUN $.
      $( [?] $) $( [31-Jul-04] $)

    $( Closure of natural number exponentiation of nonnegative integers. $)
    nn0expclt $p |- ( ( A e. NN0 /\ B e. NN ) -> ( A ^ B ) e. NN0 ) $=
      ( vx vy cn0 nn0sscn cv nn0mulclt expcllem ) CDABEFCGDGHI $.
      $( [?] $) $( [22-Jul-04] $)

    $( Closure of natural number exponentiation of integers. $)
    zexpclt $p |- ( ( A e. ZZ /\ B e. NN ) -> ( A ^ B ) e. ZZ ) $=
      ( vx vy cz zsscn cv zmulclt expcllem ) CDABEFCGDGHI $.
      $( [?] $) $( [31-Jul-04] $)

    $( Closure of natural number exponentiation of rationals. $)
    qexpclt $p |- ( ( A e. QQ /\ B e. NN ) -> ( A ^ B ) e. QQ ) $=
      ( vx vy cq qsscn cv qmulclt expcllem ) CDABEFCGDGHI $.
      $( [?] $) $( [2-Aug-04] $)

    $( Closure of natural number exponentiation of reals. $)
    reexpclt $p |- ( ( A e. RR /\ B e. NN ) -> ( A ^ B ) e. RR ) $=
      ( vx vy cr axresscn cv axmulrcl expcllem ) CDABEFCGDGHI $.
      $( [1-Aug-04] $) $( [31-Jul-04] $)

    $( Closure law for natural number exponentiation. $)
    expclt $p |- ( ( A e. CC /\ B e. NN ) -> ( A ^ B ) e. CC ) $=
      ( vx vy cc ssid cv axmulcl expcllem ) CDABEEFCGDGHI $.
      $( [?] $) $( [31-Jul-04] $)
  $}

  $( Closure of the square of a real number. $)
  sqreclt $p |- ( A e. RR -> ( A ^ 2 ) e. RR ) $=
    ( cr wcel c2 cn cexp co 2nn reexpclt mpan2 ) ABCDECADFGBCHADIJ $.
    $( [18-Oct-99] $)

  ${
    sqrecl.1 $e |- A e. RR $.
    $( Closure of square in reals. $)
    sqrecl $p |- ( A ^ 2 ) e. RR $=
      ( cr wcel c2 cexp co sqreclt ax-mp ) ACDAEFGCDBAHI $.
      $( [2-Aug-99] $)

    ${
      lt2sqe.2 $e |- B e. RR $.
      $( Two nonnegative reals compare the same as their squares. $)
      lt2sqe $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( A < B <-> ( A ^ 2 ) < ( B ^ 2 ) ) ) $=
        ( cc0 cle wbr wa clt cmulc co c2 cexp lt2sq recn sqval breq12i syl6bbr
        ) EAFGEBFGHABIGAAJKZBBJKZIGALMKZBLMKZIGABCDNUASUBTIAACOPBBDOPQR $.
        $( [12-Sep-99] $)

      $( Two nonnegative reals compare the same as their squares. $)
      le2sqe $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( A <_ B <-> ( A ^ 2 ) <_ ( B ^ 2 ) ) ) $=
        ( cc0 cle wbr wa cmulc co c2 cexp le2sq recn sqval breq12i syl6bbr ) EA
        FGEBFGHABFGAAIJZBBIJZFGAKLJZBKLJZFGABCDMTRUASFAACNOBBDNOPQ $.
        $( [12-Sep-99] $)

      $( The square function is one-to-one for nonnegative reals. $)
      sqe11 $p |-  ( ( 0 <_ A /\ 0 <_ B ) ->
                ( ( A ^ 2 ) = ( B ^ 2 ) <-> A = B ) ) $=
        ( cc0 cle wbr wa cmulc co wceq c2 cexp sq11 recn sqval cleq12i syl5bb
        ) EAFGEBFGHAAIJZBBIJZKABKALMJZBLMJZKABCDNUASUBTAACOPBBDOPQR $.
        $( [27-Oct-99] $)
    $}

    $( The square of a non-zero real is positive. $)
    sqegt0 $p |- ( A =/= 0 -> 0 < ( A ^ 2 ) ) $=
      ( cc0 wceq wn cmulc co clt wbr wne c2 cexp sqgt0 df-ne recn sqval breq2i
      3imtr4 ) ACDECAAFGZHIACJCAKLGZHIABMACNTSCHAABOPQR $.
      $( [17-Sep-99] $)

    $( A square of a real is nonnegative. $)
    sqege0 $p |- 0 <_ ( A ^ 2 ) $=
      ( cc0 cmulc co c2 cexp cle sqge0 recn sqval breqtrr ) CAADEAFGEHABIAABJKL
      $.
      $( [3-Aug-99] $)
  $}

  $( The square function is one-to-one for nonnegative reals. $)
  sqe11t $p |-  ( ( A e. RR /\ B e. RR ) -> ( ( 0 <_ A /\ 0 <_ B ) ->
            ( ( A ^ 2 ) = ( B ^ 2 ) <-> A = B ) ) ) $=
    ( cr wcel cc0 cle wbr wa c2 cexp co wceq wb wi cded breq2 birand opreq1
    cleq1d cleq1 bi2bid bi2imd biland cleq2d cleq2 ax0re elimel sqe11 dedth2h
    ) ACDZBCDZEAFGZEBFGZHZAIJKZBIJKZLZABLZMZNEUJAEOZFGZUMHZUTIJKZUPLZUTBLZMZNVA
    EUKBEOZFGZHZVCVGIJKZLZUTVGLZMZNABEEAUTLZUNVBUSVFVNULVAUMAUTEFPQVNUQVDURVEVN
    UOVCUPAUTIJRSAUTBTUAUBBVGLZVBVIVFVMVOUMVHVABVGEFPUCVOVDVKVEVLVOUPVJVCBVGIJR
    UDBVGUTUEUAUBUTVGAECUFUGBECUFUGUHUI $.
    $( [8-Apr-01] $)

  $( Two nonnegative numbers compare the same as their squares. $)
  lt2sqet $p |- ( ( A e. RR /\ B e. RR ) -> ( ( 0 <_ A /\ 0 <_ B ) ->
              ( A < B <-> ( A ^ 2 ) < ( B ^ 2 ) ) ) ) $=
    ( cr wcel cc0 cle wbr wa clt c2 cexp co wb wi cded wceq breq2 birand breq1
    opreq1 breq1d bi2bid bi2imd biland breq2d ax0re elimel lt2sqe dedth2h ) ACD
    ZBCDZEAFGZEBFGZHZABIGZAJKLZBJKLZIGZMZNEUJAEOZFGZUMHZUTBIGZUTJKLZUQIGZMZNVAE
    UKBEOZFGZHZUTVGIGZVDVGJKLZIGZMZNABEEAUTPZUNVBUSVFVNULVAUMAUTEFQRVNUOVCURVEA
    UTBISVNUPVDUQIAUTJKTUAUBUCBVGPZVBVIVFVMVOUMVHVABVGEFQUDVOVCVJVEVLBVGUTIQVOU
    QVKVDIBVGJKTUEUBUCUTVGAECUFUGBECUFUGUHUI $.
    $( [26-Sep-99] $)

  $( Two nonnegative numbers compare the same as their squares. $)
  le2sqet $p |- ( ( A e. RR /\ B e. RR ) -> ( ( 0 <_ A /\ 0 <_ B ) ->
              ( A <_ B <-> ( A ^ 2 ) <_ ( B ^ 2 ) ) ) ) $=
    ( cr wcel wa cc0 cle wbr c2 cexp co wb cmulc le2sqt imp cc exp2 breqan12d
    recnt syl2an adantr bitr4d exp ) ACDZBCDZEZFAGHFBGHEZABGHZAIJKZBIJKZGHZLUFU
    GEUHAAMKZBBMKZGHZUKUFUGUHUNLABNOUFUKUNLZUGAPDZBPDZUOUDUEUPUQUIULUJUMGAQBQRA
    SBSTUAUBUC $.
    $( [18-Oct-99] $)

  $( A square of a real is nonnegative. $)
  sqege0t $p |- ( A e. RR -> 0 <_ ( A ^ 2 ) ) $=
    ( cr wcel cc0 c2 cexp co cle wbr cded wceq opreq1 breq2d ax0re elimel
    sqege0 dedth ) ABCZDAEFGZHIDRADJZEFGZHIADATKSUADHATEFLMTADBNOPQ $.
    $( [18-Oct-99] $)

  $( The square of 1 is 1. $)
  sq1 $p |- ( 1 ^ 2 ) = 1 $=
    ( c1 c2 cexp co cmulc 1cn sqval mulid1 eqtr ) ABCDAAEDAAFGAFHI $.
    $( [22-Aug-99] $)

  $( The square of 2 is 4. $)
  sq2 $p |- ( 2 ^ 2 ) = 4 $=
    ( c2 cexp co cmulc c4 2cn sqval 2t2e4 eqtr ) AABCAADCEAFGHI $.
    $( [22-Aug-99] $)

  $( The cube of 2 is 8. $)
  cu2 $p |- ( 2 ^ 3 ) = 8 $=
    ( c2 c3 cexp co c1 caddc cmulc c8 df-3 opreq2i cc wcel cn wceq 2cn 2nn
    expp1 mp2an c4 sq2 opreq1i 4t2e8 eqtr 3eqtr ) ABCDAAEFDZCDZAACDZAGDZHBUEACI
    JAKLAMLUFUHNOPAAQRUHSAGDHUGSAGTUAUBUCUD $.
    $( [8-Sep-04] $) $( [2-Aug-04] $)

  ${
    binom.1 $e |- A e. CC $.
    binom.2 $e |- B e. CC $.
    $( Binomial expansion. $)
    binom $p |- ( ( A + B ) ^ 2 ) = ( ( ( A ^ 2 ) + ( 2 x.
                ( A x. B ) ) ) + ( B ^ 2 ) ) $=
      ( caddc co cmulc c2 cexp addcl distr1 distr2 mulcom opreq2i eqtr
      opreq12i mulcl addass opreq1i eqtr3 3eqtr sqval 2times 3eqtr4 ) ABEFZUEGF
      ZAAGFZABGFZUHEFZEFZBBGFZEFZUEHIFAHIFZHUHGFZEFZBHIFZEFUFUEAGFZUEBGFZEFUGUH
      EFZUHUKEFZEFZULUEABABCDJZCDKUQUSURUTEUQUGBAGFZEFUSABACDCLVCUHUGEBADCMNOAB
      BCDDLPUSUHEFZUKEFVAULUSUHUKUGUHAACCQZABCDQZJVFBBDDQRVDUJUKEUGUHUHVEVFVFRS
      TUAUEVBUBUOUJUPUKEUMUGUNUIEACUBUHVFUCPBDUBPUD $.
      $( [11-Aug-99] $)
  $}

