$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Integer powers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ^ $. $( Exponentiation. $)

  $( Extend class notation to include exponentiation of a complex number to an
     integer power. $)
  cexp $a class ^ $.

  ${
    $d x y z $.
    $( Define exponentiation to nonnegative integer powers.  This definition is
       not meant to be used directly; instead, ~ exp0 and ~ expp1 provide a the
       standard recursive definition.  The up-arrow notation is used by Donald
       Knuth for iterated exponentiation (_Science_ 194, 1235-1242, 1976) and
       is convenient for us since we don't have superscripts.  See ~ expnnval
       for a description of how the recursive sequence builder is used.
       10-Jun-2005:  The definition was extended to include zero exponents, so
       that ` 0 ^ 0 = 1 ` per the convention of Definition 10-4.1 of [Gleason]
       p. 134.  (Based on definition contributed by Raph Levien,
       15-Oct-2004.) $)
    df-exp $a |- ^ = { <. <. x , y >. , z >. | ( ( x e. CC /\ y e. NN0 ) /\
          z = if ( y = 0 , 1 , ( ( x. seq1 ( NN X. { x } ) ) ` y ) ) ) } $.

    $(
    Raph Levien's original definition from set.mm of 15-Oct-2004:
    df-exp $a |- ^ = { <. <. x , y >. , z >. | ( ( x e. CC /\ y e. NN0 ) /\
          z = ( ( rec ( { <. w , v >. | v = ( w x. x ) } , 1 ) o.
        `' ( rec ( { <. w , v >. | v = ( w + 1 ) } , 0 ) |` om ) ) ` y ) ) } $.

    Old NN-based version from set.mm of 21-May-2005:
    df-exp $a |- ^ = { <. <. x , y >. , z >. | ( ( x e. CC /\ y e. NN ) /\
          z = ( ( x. seq1 ( NN X. { x } ) ) ` y ) ) }  $.
    dfexp2 $p |- ^ = { <. <. x , y >. , z >. | ( ( x e. CC /\ y e. NN0 ) /\
        z = ( ( x. seq1 ( { <. 0 , 1 >. } u. ( NN X. { x } ) ) ) ` y ) ) } $=
          z = ( ( x. seq0 ( NN0 X. { if ( y = 0 , 1 , x ) } ) ) ` y ) ) } $=
    $)
  $}

  ${
    $d x y z A $.  $d x y z N $.
    $( Value of exponentiation to nonnegative integer powers. $)
    expval $p |- ( ( A e. CC /\ N e. NN0 ) -> ( A ^ N ) =
                   if ( N = 0 , 1 , ( ( x. seq1 ( NN X. { A } ) ) ` N ) ) ) $=
      ( vx vy vz cc cn0 cv cc0 wceq c1 cmul cn csn cxp cseq1 co cfv cif cexp
      1nn0 elisseti fvex ifex sneq xpeq2 syl opreq2d fveq1d ifeq2d eqeq1 fveq2
      ifbieq2d df-exp oprabval2 ) CDEABFGDHZIJZKUPLMCHZNZOZPQZRZSBIJZKBLMANZOZP
      QZRZSTUQKUPVFRZSVCKVGKGUAUBBVFUCUDURAJZUQVBVHKVIUPVAVFVIUTVELPVIUSVDJUTVE
      JURAUEUSVDMUFUGUHUIUJUPBJUQVCVHVGKUPBIUKUPBVFULUMCDEUNUO $.
      $( [11-Jun-2005] $) $( [20-May-2004] $)

    $( Value of a complex number raised to the 0th power.  Note that under our
       definition, ` 0 ^ 0 = 1 ` , following the convention used by Gleason.
       Part of Definition 10-4.1 of [Gleason] p. 134. $)
    exp0 $p |- ( A e. CC -> ( A ^ 0 ) = 1 ) $=
      ( cc wcel cc0 cexp co wceq c1 cmul cn csn cxp cseq1 cfv cif cn0 0nn0
      expval mpan2 eqid iftrue ax-mp syl6eq ) ABCZADEFZDDGZHDIJAKLMFNZOZHUDDPCU
      EUHGQADRSUFUHHGDTUFHUGUAUBUC $.
      $( [11-Jun-2005] $) $( [20-May-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( Value of exponentiation to natural number powers. ` NN X. { A } ` is the
       constant function with value ` A ` .  The ` seq1 ` operation produces
       the sequence (i.e. function on ` NN ` ) with values ` A ` , ` A x. A ` ,
       ` ( A x. A ) x. A ` ,... that we evaluate at index ` B ` . $)
    expnnval $p |- ( ( A e. CC /\ B e. NN ) -> ( A ^ B ) =
                   ( ( x. seq1 ( NN X. { A } ) ) ` B ) ) $=
      ( cc wcel cn wa cexp co cc0 wceq c1 cmul csn cxp cseq1 cfv cif cn0 nnnn0
      expval sylan2 wn wne nnne0 df-ne sylib iffalse syl adantl eqtrd ) ACDZBED
      ZFABGHZBIJZKBLEAMNOHPZQZUOULUKBRDUMUPJBSABTUAULUPUOJZUKULUNUBZUQULBIUCURB
      UDBIUEUFUNKUOUGUHUIUJ $.
      $( [19-Oct-2004] $)
  $}

  $( Value of a complex number raised to the first power. $)
  exp1 $p |- ( A e. CC -> ( A ^ 1 ) = A ) $=
    ( cc wcel c1 cexp co cmul cn csn cxp cseq1 cfv wceq 1nn expnnval mpan2
    mulex nnex snex xpex seq11 wf fconstg fvconst sylancl syl5eq eqtrd ) ABCZAD
    EFZDGHAIZJZKFLZAUHDHCZUIULMNADOPUHULDUKLZAGUKQHUJRASTUAUHHUJUKUBUMUNAMHABUC
    NHADUKUDUEUFUG $.
    $( [20-Oct-2004] $)

  $( Value of a complex number raised to a nonnegative integer power plus one.
     Part of Definition 10-4.1 of [Gleason] p. 134. $)
  expp1 $p |- ( ( A e. CC /\ N e. NN0 ) -> ( A ^ ( N + 1 ) ) =
               ( ( A ^ N ) x. A ) ) $=
    ( cc wcel cn0 c1 caddc co cexp cmul wceq cn cc0 wo elnn0 wa csn cxp cseq1
    cfv mulex nnex snex xpex seq1p1 adantl wf fconstg peano2nn fvconst syl2an
    opreq2d eqtrd expnnval sylan2 opreq1d 3eqtr4d ex mulid2 exp0 ax1cn addid2i
    opreq2i exp1 syl5eq 3eqtr4rd opreq1 opreq2 eqeq12d syl5ibrcom jaod syl5bi
    imp ) ACDZBEDZABFGHZIHZABIHZAJHZKZVOBLDZBMKZNVNVTBOVNWAVTWBVNWAVTVNWAPZVPJL
    AQZRZSHZTZBWFTZAJHZVQVSWCWGWHVPWETZJHZWIWAWGWKKVNBJWEUALWDUBAUCUDUEUFWCWJAW
    HJVNLWDWEUGVPLDZWJAKWALACUHBUIZLAVPWEUJUKULUMWAVNWLVQWGKWMAVPUNUOWCVRWHAJAB
    UNUPUQURVNVTWBAMFGHZIHZAMIHZAJHZKVNFAJHAWQWOAUSVNWPFAJAUTUPVNWOAFIHAWNFAIFV
    AVBVCAVDVEVFWBVQWOVSWQWBVPWNAIBMFGVGULWBVRWPAJBMAIVHUPVIVJVKVLVM $.
    $( [27-Jul-2006] $) $( [20-May-2005] $)

  ${
    $d x y z w A $.  $d z B $.  $d x y z w F $.
    expcllem.1 $e |- F C_ CC $.
    expcllem.2 $e |- ( ( x e. F /\ y e. F ) -> ( x x. y ) e. F ) $.
    expcllem.3 $e |- 1 e. F $.
    $( Lemma for proving nonnegative integer exponentiation closure laws. $)
    expcllem $p |- ( ( A e. F /\ B e. NN0 ) -> ( A ^ B ) e. F ) $=
      ( vz vw cn0 wcel cn cc0 wceq wo cexp co elnn0 cv wi c1 caddc opreq2
      eleq1d imbi2d cc sseli exp1 syl ibir wa cmul opreq1 vtocl2ga ancoms
      adantlr wb nnnn0 expp1 syl2an adantr mpbird exp31 com12 a2d nnind impcom
      exp0 sylan9eqr syl6eqel jaodan sylan2b ) DKLCELZDMLZDNOZPCDQRZELZDSVNVOVR
      VPVOVNVRVNCITZQRZELZUAVNCUBQRZELZUAVNCJTZQRZELZUAVNCWDUBUCRZQRZELZUAVNVRU
      AIJDVSUBOZWAWCVNWJVTWBEVSUBCQUDUEUFVSWDOZWAWFVNWKVTWEEVSWDCQUDUEUFVSWGOZW
      AWIVNWLVTWHEVSWGCQUDUEUFVSDOZWAVRVNWMVTVQEVSDCQUDUEUFVNWCVNWBCEVNCUGLZWBC
      OEUGCFUHZCUIUJUEUKWDMLZVNWFWIVNWPWFWIUAVNWPWFWIVNWPULZWFULWIWECUMRZELZVNW
      FWSWPWFVNWSATZBTZUMRZELWEXAUMRZELWSABWECEEWTWEOXBXCEWTWEXAUMUNUEXACOXCWRE
      XACWEUMUDUEGUOUPUQWQWIWSURWFWQWHWREVNWNWDKLWHWROWPWOWDUSCWDUTVAUEVBVCVDVE
      VFVGVHVNVPULVQUBEVPVNVQCNQRZUBDNCQUDVNWNXDUBOWOCVIUJVJHVKVLVM $.
      $( [14-Dec-2005] $)
  $}

  ${
    $d x y A $.  $d x y N $.
    $( Closure of exponentiation of nonnegative integers. $)
    nnexpcl $p |- ( ( A e. NN /\ N e. NN0 ) -> ( A ^ N ) e. NN ) $=
      ( vx vy cn nnsscn cv nnmulcl 1nn expcllem ) CDABEFCGDGHIJ $.
      $( [16-Dec-2005] $)

    $( Closure of exponentiation of nonnegative integers. $)
    nn0expcl $p |- ( ( A e. NN0 /\ N e. NN0 ) -> ( A ^ N ) e. NN0 ) $=
      ( vx vy cn0 nn0sscn cv nn0mulcl 1nn0 expcllem ) CDABEFCGDGHIJ $.
      $( [14-Dec-2005] $)

    $( Closure of exponentiation of integers. $)
    zexpcl $p |- ( ( A e. ZZ /\ N e. NN0 ) -> ( A ^ N ) e. ZZ ) $=
      ( vx vy cz zsscn cv zmulcl 1z expcllem ) CDABEFCGDGHIJ $.
      $( [16-Dec-2005] $)

    $( Closure of exponentiation of rationals. $)
    qexpcl $p |- ( ( A e. QQ /\ N e. NN0 ) -> ( A ^ N ) e. QQ ) $=
      ( vx vy cq qsscn cv qmulcl c1 cz wcel 1z zq ax-mp expcllem ) CDABEFCGDGHI
      JKIEKLIMNO $.
      $( [16-Dec-2005] $)

    $( Closure of exponentiation of reals. $)
    reexpcl $p |- ( ( A e. RR /\ N e. NN0 ) -> ( A ^ N ) e. RR ) $=
      ( vx vy cr axresscn cv remulcl 1re expcllem ) CDABEFCGDGHIJ $.
      $( [14-Dec-2005] $)

    $( Closure law for nonnegative integer exponentiation. $)
    expcl $p |- ( ( A e. CC /\ N e. NN0 ) -> ( A ^ N ) e. CC ) $=
      ( vx vy cc ssid cv mulcl ax1cn expcllem ) CDABEEFCGDGHIJ $.
      $( [26-May-2005] $)

    $( Closure law for exponentiation of positive reals. $)
    rpexpcl $p |- ( ( A e. RR+ /\ N e. NN0 ) -> ( A ^ N ) e. RR+ ) $=
      ( vx vy crp cr cc rpssre axresscn sstri cv rpmulcl 1rp expcllem ) CDABEEF
      GHIJCKDKLMN $.
      $( [24-Feb-2008] $)
  $}

  $( Exponentiation in terms of predecessor exponent. $)
  expm1t $p |- ( ( A e. CC /\ N e. NN ) -> ( A ^ N ) =
               ( ( A ^ ( N - 1 ) ) x. A ) ) $=
    ( cc wcel cn wa c1 cmin co caddc cexp cmul wceq nncn ax1cn npcan sylancl
    opreq2d adantl cn0 nnm1nn0 expp1 sylan2 eqtr3d ) ACDZBEDZFABGHIZGJIZKIZABKI
    ZAUGKIALIZUFUIUJMUEUFUHBAKUFBCDGCDUHBMBNOBGPQRSUFUEUGTDUIUKMBUAAUGUBUCUD $.
    $( [19-Dec-2005] $)

  ${
    $d j k N $.
    $( Value of one raised to a nonnegative integer power. $)
    1exp $p |- ( N e. NN0 -> ( 1 ^ N ) = 1 ) $=
      ( vj vk c1 cv cexp co wceq cc0 caddc opreq2 eqeq1d cc wcel ax1cn exp0
      ax-mp cn0 cmul opreq1 mulid1i syl6eq expp1 mpan syl5ibr nn0ind ) DBEZFGZD
      HDIFGZDHZDCEZFGZDHZDUKDJGZFGZDHZDAFGZDHBCAUGIHUHUIDUGIDFKLUGUKHUHULDUGUKD
      FKLUGUNHUHUODUGUNDFKLUGAHUHUQDUGADFKLDMNZUJODPQUMUPUKRNZULDSGZDHUMUTDDSGD
      ULDDSTDOUAUBUSUOUTDURUSUOUTHODUKUCUDLUEUF $.
      $( [15-Dec-2005] $)
  $}

  ${
    $d j k A $.  $d j k N $.
    $( Natural number exponentiation is 0 iff its mantissa is 0. $)
    expeq0 $p |- ( ( A e. CC /\ N e. NN ) -> ( ( A ^ N ) = 0 <-> A = 0 ) ) $=
      ( vj vk cn wcel cc cexp co cc0 wceq wb cv wi c1 caddc opreq2 eqeq1d
      bibi1d imbi2d exp1 wa wo cn0 nnnn0 cmul expp1 expcl simpl mul0or
      syl11anc bitrd sylan2 bi1 idd jaod olc impbid1 sylan9bb exp31 com12 a2d
      nnind impcom ) BEFAGFZABHIZJKZAJKZLZVEACMZHIZJKZVHLZNVEAOHIZJKZVHLZNVEADM
      ZHIZJKZVHLZNVEAVQOPIZHIZJKZVHLZNVEVINCDBVJOKZVMVPVEWEVLVOVHWEVKVNJVJOAHQR
      STVJVQKZVMVTVEWFVLVSVHWFVKVRJVJVQAHQRSTVJWAKZVMWDVEWGVLWCVHWGVKWBJVJWAAHQ
      RSTVJBKZVMVIVEWHVLVGVHWHVKVFJVJBAHQRSTVEVNAJAUARVQEFZVEVTWDVEWIVTWDNVEWIV
      TWDVEWIUBWCVSVHUCZVTVHWIVEVQUDFZWCWJLVQUEVEWKUBZWCVRAUFIZJKZWJWLWBWMJAVQU
      GRWLVRGFVEWNWJLAVQUHVEWKUIVRAUJUKULUMVTWJVHVTVSVHVHVSVHUNVTVHUOUPVHVSUQUR
      USUTVAVBVCVD $.
      $( [27-Jul-2006] $) $( [23-Feb-2005] $)

    $( Natural number exponentiation is nonzero iff its mantissa is nonzero. $)
    expne0 $p |- ( ( A e. CC /\ N e. NN ) ->
                  ( ( A ^ N ) =/= 0 <-> A =/= 0 ) ) $=
      ( cc wcel cn wa cexp co cc0 expeq0 necon3bid ) ACDBEDFABGHIAIABJK $.
      $( [10-Jul-2008] $) $( [6-May-2005] $)

    $( Nonnegative integer exponentiation is nonzero if its mantissa is
       nonzero. $)
    expne0i $p |- ( ( A e. CC /\ A =/= 0 /\ N e. NN0 )
                -> ( A ^ N ) =/= 0 ) $=
      ( cc wcel cc0 wne cn0 cexp co wa cn wceq wo elnn0 expne0 biimpar an32s
      c1 ax1ne0 opreq2 exp0 sylan9eqr neeq1d mpbiri adantlr jaodan sylan2b
      3impa ) ACDZAEFZBGDZABHIZEFZUKUIUJJZBKDZBELZMUMBNUNUOUMUPUIUOUJUMUIUOJUMU
      JABOPQUIUPUMUJUIUPJZUMREFSUQULREUPUIULAEHIRBEAHTAUAUBUCUDUEUFUGUH $.
      $( [1-Jan-2009] $) $( [2-Aug-2006] $)

    $( Nonnegative integer exponentiation with a positive mantissa is
       positive. $)
    expgt0 $p |- ( ( A e. RR /\ N e. NN0 /\ 0 < A ) -> 0 < ( A ^ N ) ) $=
      ( vj vk cr wcel cn0 cc0 clt wbr cexp co wi wa cv c1 caddc wceq opreq2
      breq2d imbi2d lt01 cc recn exp0 syl syl5breqr adantr cmul reexpcl simpl
      axmulgt0 syl11anc expp1 sylan sylibrd exp4b com12 com34 imp3a a2d nn0ind
      exp3a 3imp ) AEFZBGFZHAIJZHABKLZIJZVFVEVGVIMVFVEVGVIVEVGNZHACOZKLZIJZMVJH
      AHKLZIJZMVJHADOZKLZIJZMVJHAVPPQLZKLZIJZMVJVIMCDBVKHRZVMVOVJWBVLVNHIVKHAKS
      TUAVKVPRZVMVRVJWCVLVQHIVKVPAKSTUAVKVSRZVMWAVJWDVLVTHIVKVSAKSTUAVKBRZVMVIV
      JWEVLVHHIVKBAKSTUAVEVOVGVEHPVNIUBVEAUCFZVNPRAUDZAUEUFUGUHVPGFZVJVRWAWHVEV
      GVRWAMWHVEVRVGWAVEWHVRVGWAMMVEWHVRVGWAVEWHNZVRVGNZHVQAUILZIJZWAWIVQEFVEWJ
      WLMAVPUJVEWHUKVQAULUMWIVTWKHIVEWFWHVTWKRWGAVPUNUOTUPUQURUSUTVAVBVCURVD $.
      $( [16-Dec-2005] $)
  $}

  $( Value of zero raised to a natural number power. $)
  0exp $p |- ( N e. NN -> ( 0 ^ N ) = 0 ) $=
    ( cn wcel cc0 cexp co wceq eqid cc wb 0cn expeq0 mpan mpbiri ) ABCZDAEFDGZD
    DGZDHDICOPQJKDALMN $.
    $( [19-Aug-2004] $)

  $( Nonnegative integer exponentiation with a nonnegative mantissa is
     nonnegative. $)
  expge0 $p |- ( ( A e. RR /\ N e. NN0 /\ 0 <_ A ) -> 0 <_ ( A ^ N ) ) $=
    ( cr wcel cn0 cc0 cle wbr cexp co cn wceq wo wi elnn0 wa clt nnnn0 expgt0
    syl3an2 3expia 0exp eqcomd opreq1 eqeq2d syl5ibcom adantl orim12d wb 0re
    leloe mpan adantr reexpcl sylan2 sylancr 3imtr4d ex c1 1re lt01 ltleii cc
    recn exp0 syl syl5breqr opreq2 breq2d syl5ibrcom a1dd jaod syl5bi 3imp ) AC
    DZBEDZFAGHZFABIJZGHZVPBKDZBFLZMVOVQVSNZBOVOVTWBWAVOVTWBVOVTPZFAQHZFALZMZFVR
    QHZFVRLZMZVQVSWCWDWGWEWHVOVTWDWGVTVOVPWDWGBRZABSTUAVTWEWHNVOVTFFBIJZLWEWHVT
    WKFBUBUCWEWKVRFFABIUDUEUFUGUHVOVQWFUIZVTFCDZVOWLUJFAUKULUMWCWMVRCDZVSWIUIUJ
    VTVOVPWNWJABUNUOFVRUKUPUQURVOWAVSVQVOVSWAFAFIJZGHVOFUSWOGFUSUJUTVAVBVOAVCDW
    OUSLAVDAVEVFVGWAVRWOFGBFAIVHVIVJVKVLVMVN $.
    $( [16-Dec-2005] $)

  ${
    $d x y A $.  $d x N $.
    $( Natural number exponentiation with a mantissa greater than 1 is greater
       than 1. $)
    expgt1 $p |- ( ( A e. RR /\ N e. NN /\ 1 < A ) -> 1 < ( A ^ N ) ) $=
      ( vx vy cr wcel cn c1 clt wbr cexp co wi wa cv caddc wceq opreq2 breq2d
      imbi2d cc recn exp1 syl biimpar cn0 nnnn0 cmul reexpcl simpl jca mulgt1
      sylan ex expp1 sylibrd sylan2 exp4b com12 com34 imp3a a2d nnind exp3a
      3imp ) AEFZBGFZHAIJZHABKLZIJZVGVFVHVJMVGVFVHVJVFVHNZHACOZKLZIJZMVKHAHKLZI
      JZMVKHADOZKLZIJZMVKHAVQHPLZKLZIJZMVKVJMCDBVLHQZVNVPVKWCVMVOHIVLHAKRSTVLVQ
      QZVNVSVKWDVMVRHIVLVQAKRSTVLVTQZVNWBVKWEVMWAHIVLVTAKRSTVLBQZVNVJVKWFVMVIHI
      VLBAKRSTVFVPVHVFVOAHIVFAUAFZVOAQAUBZAUCUDSUEVQGFZVKVSWBWIVFVHVSWBMWIVFVSV
      HWBVFWIVSVHWBMMVFWIVSVHWBWIVFVQUFFZVSVHNZWBMVQUGVFWJNZWKHVRAUHLZIJZWBWLWK
      WNWLVREFZVFNWKWNWLWOVFAVQUIVFWJUJUKVRAULUMUNWLWAWMHIVFWGWJWAWMQWHAVQUOUMS
      UPUQURUSUTVAVBVCVDUSVE $.
      $( [27-Jul-2006] $) $( [13-Feb-2005] $)
  $}

  $( Nonnegative integer exponentiation with a mantissa greater than or equal
     to 1 is greater than or equal to 1. $)
  expge1 $p |- ( ( A e. RR /\ N e. NN0 /\ 1 <_ A ) -> 1 <_ ( A ^ N ) ) $=
    ( cr wcel cn0 c1 cle wbr cexp co cn cc0 wceq wo wi elnn0 wa clt expgt1
    3expia nnnn0 1exp syl eqcomd opreq1 eqeq2d syl5ibcom adantl orim12d wb 1re
    leloe mpan adantr reexpcl sylan2 sylancr 3imtr4d leidi opreq2 cc recn exp0
    sylan9eqr syl5breqr a1d jaodan sylan2b 3impia ) ACDZBEDZFAGHZFABIJZGHZVKVJB
    KDZBLMZNVLVNOZBPVJVOVQVPVJVOQZFARHZFAMZNZFVMRHZFVMMZNZVLVNVRVSWBVTWCVJVOVSW
    BABSTVOVTWCOVJVOFFBIJZMVTWCVOWEFVOVKWEFMBUAZBUBUCUDVTWEVMFFABIUEUFUGUHUIVJV
    LWAUJZVOFCDZVJWGUKFAULUMUNVRWHVMCDZVNWDUJUKVOVJVKWIWFABUOUPFVMULUQURVJVPQZV
    NVLWJFFVMGFUKUSVPVJVMALIJZFBLAIUTVJAVADWKFMAVBAVCUCVDVEVFVGVHVI $.
    $( [16-Oct-2007] $) $( [21-Feb-2005] $)

  ${
    $d j k A $.  $d j k B $.  $d j k M $.  $d j N $.
    $( Natural number exponentiation of a product.  Proposition 10-4.2(c) of
       [Gleason] p. 135, restricted to nonnegative integer exponents. $)
    mulexp $p |- ( ( A e. CC /\ B e. CC /\ N e. NN0 ) ->
                  ( ( A x. B ) ^ N ) = ( ( A ^ N ) x. ( B ^ N ) ) ) $=
      ( vj vk cc wcel cn0 cmul co cexp wceq wa cv wi cc0 c1 caddc opreq2
      opreq12d eqeq12d imbi2d mulcl exp0 syl opreqan12d ax1cn mulid1i syl6eq
      eqtr4d expp1 sylan adantr opreq1 adantl expcl anim12i anandirs simpl
      mul4 syl11anc adantlr adantll 3eqtrd exp31 com12 a2d nn0ind exp3a com3l
      3imp ) AFGZBFGZCHGZABIJZCKJZACKJZBCKJZIJZLZVNVLVMVTVNVLVMVTVLVMMZVODNZKJZ
      AWBKJZBWBKJZIJZLZOWAVOPKJZAPKJZBPKJZIJZLZOWAVOENZKJZAWMKJZBWMKJZIJZLZOWAV
      OWMQRJZKJZAWSKJZBWSKJZIJZLZOWAVTODECWBPLZWGWLWAXEWCWHWFWKWBPVOKSXEWDWIWEW
      JIWBPAKSWBPBKSTUAUBWBWMLZWGWRWAXFWCWNWFWQWBWMVOKSXFWDWOWEWPIWBWMAKSWBWMBK
      STUAUBWBWSLZWGXDWAXGWCWTWFXCWBWSVOKSXGWDXAWEXBIWBWSAKSWBWSBKSTUAUBWBCLZWG
      VTWAXHWCVPWFVSWBCVOKSXHWDVQWEVRIWBCAKSWBCBKSTUAUBWAWHQWKWAVOFGZWHQLABUCZV
      OUDUEWAWKQQIJQVLVMWIQWJQIAUDBUDUFQUGUHUIUJWMHGZWAWRXDWAXKWRXDOWAXKWRXDWAX
      KMZWRMWTWNVOIJZWQVOIJZXCXLWTXMLZWRWAXIXKXOXJVOWMUKULUMWRXMXNLXLWNWQVOIUNU
      OXLXNXCLWRXLXNWOAIJZWPBIJZIJZXCXLWOFGZWPFGZMZWAXNXRLVLVMXKYAVLXKMXSVMXKMX
      TAWMUPBWMUPUQURWAXKUSWOWPABUTVAXLXAXPXBXQIVLXKXAXPLVMAWMUKVBVMXKXBXQLVLBW
      MUKVCTUJUMVDVEVFVGVHVIVJVK $.
      $( [13-Feb-2005] $)

    $( Nonnegative integer exponentiation of a reciprocal. $)
    exprec $p |- ( ( A e. CC /\ A =/= 0 /\ N e. NN0 ) ->
                  ( ( 1 / A ) ^ N ) = ( 1 / ( A ^ N ) ) ) $=
      ( vj vk cc wcel cc0 wne cn0 c1 cdiv co cexp wceq wa cv wi caddc opreq2
      opreq2d eqeq12d imbi2d reccl exp0 syl ax1cn div1i syl6eq adantr eqtr4d
      cmul opreq1 ad2antll expp1 sylan an32s adantrr expcl expne0i 3com23
      3expa id adantlr divmuldiv mpanl12 syl21anc mulid1i opreq1i syl6req
      eqtrd 3eqtr4d exp43 com12 imp3a a2d nn0ind 3impia ) AEFZAGHZBIFZJAKLZBMLZ
      JABMLZKLZNZVTVRVSOZWEWFWACPZMLZJAWGMLZKLZNZQWFWAGMLZJAGMLZKLZNZQWFWADPZML
      ZJAWPMLZKLZNZQWFWAWPJRLZMLZJAXAMLZKLZNZQWFWEQCDBWGGNZWKWOWFXFWHWLWJWNWGGW
      AMSXFWIWMJKWGGAMSTUAUBWGWPNZWKWTWFXGWHWQWJWSWGWPWAMSXGWIWRJKWGWPAMSTUAUBW
      GXANZWKXEWFXHWHXBWJXDWGXAWAMSXHWIXCJKWGXAAMSTUAUBWGBNZWKWEWFXIWHWBWJWDWGB
      WAMSXIWIWCJKWGBAMSTUAUBWFWLJWNWFWAEFZWLJNAUCZWAUDUEVRWNJNVSVRWNJJKLJVRWMJ
      JKAUDTJUFUGUHUIUJWPIFZWFWTXEXLVRVSWTXEQZVRXLVSXMQVRXLVSWTXEVRXLOZVSWTOOWQ
      WAUKLZWSWAUKLZXBXDWTXOXPNXNVSWQWSWAUKULUMXNVSXBXONZWTVRVSXLXQWFXJXLXQXKWA
      WPUNUOUPUQXNVSXDXPNWTXNVSOZXDJWRAUKLZKLZXPXNXDXTNVSXNXCXSJKAWPUNTUIXRXPJJ
      UKLZXSKLZXTXRWREFZWRGHZWFXPYBNZXNYCVSAWPURUIVRXLVSYDVRVSXLYDAWPUSUTVAVRVS
      WFXLWFVBVCJEFZYFYCYDOWFOYEUFUFJJWRAVDVEVFYAJXSKJUFVGVHVIVJUQVKVLVMVNVOVPV
      MVQ $.
      $( [7-Mar-2009] $) $( [2-Aug-2006] $)

    $( Nonnegative integer exponentiation of a reciprocal. $)
    exprecOLD $p |- ( ( A e. CC /\ N e. NN0 /\ A =/= 0 ) ->
                  ( ( 1 / A ) ^ N ) = ( 1 / ( A ^ N ) ) ) $=
      ( vj vk cc wcel cn0 cc0 wne c1 cdiv co cexp wceq wi wa cv caddc opreq2
      opreq2d eqeq12d imbi2d reccl exp0 syl ax1cn div1i syl6eq adantr eqtr4d
      cmul opreq1 ad2antll expp1 sylan an32s adantrr expcl jctil simpl jca w3a
      simp1 simp3 simp2 expne0i syl111anc 3expa simpr simplll simplr anim12i
      simprr divmuldiv syl22anc syl12anc mulid1i opreq1i syl6req eqtrd 3eqtr4d
      exp43 com12 imp3a a2d nn0ind exp3a 3imp ) AEFZBGFZAHIZJAKLZBMLZJABMLZKLZN
      ZWJWIWKWPOWJWIWKWPWIWKPZWLCQZMLZJAWRMLZKLZNZOWQWLHMLZJAHMLZKLZNZOWQWLDQZM
      LZJAXGMLZKLZNZOWQWLXGJRLZMLZJAXLMLZKLZNZOWQWPOCDBWRHNZXBXFWQXQWSXCXAXEWRH
      WLMSXQWTXDJKWRHAMSTUAUBWRXGNZXBXKWQXRWSXHXAXJWRXGWLMSXRWTXIJKWRXGAMSTUAUB
      WRXLNZXBXPWQXSWSXMXAXOWRXLWLMSXSWTXNJKWRXLAMSTUAUBWRBNZXBWPWQXTWSWMXAWOWR
      BWLMSXTWTWNJKWRBAMSTUAUBWQXCJXEWQWLEFZXCJNAUCZWLUDUEWIXEJNWKWIXEJJKLJWIXD
      JJKAUDTJUFUGUHUIUJXGGFZWQXKXPYCWIWKXKXPOZWIYCWKYDOWIYCWKXKXPWIYCPZWKXKPPX
      HWLUKLZXJWLUKLZXMXOXKYFYGNYEWKXHXJWLUKULUMYEWKXMYFNZXKWIWKYCYHWQYAYCYHYBW
      LXGUNUOUPUQYEWKXOYGNXKYEWKPZXOJXIAUKLZKLZYGYEXOYKNWKYEXNYJJKAXGUNTUIYIYGJ
      JUKLZYJKLZYKYIJEFZXIEFZPZYNWIPZPZXIHIZWKYGYMNZYEYRWKYEYPYQYEYOYNAXGURUFUS
      YEWIYNWIYCUTUFUSVAUIWIYCWKYSWIYCWKVBWIWKYCYSWIYCWKVCWIYCWKVDWIYCWKVEAXGVF
      VGVHYEWKVIYRYSWKPZPYNYNYOYSPWQYTYNYOYQUUAVJZUUBYRYOUUAYSYNYOYQVKYSWKUTVLY
      RWIUUAWKYPYNWIVMYSWKVIVLJJXIAVNVOVPYLJYJKJUFVQVRVSVTUQWAWBWCWDWEWFWGWCWH
      $.
      $( [2-Aug-2006] $)

    $( Sum of exponents law for nonnegative integer exponentiation.
       Proposition 10-4.2(a) of [Gleason] p. 135. $)
    expadd $p |- ( ( A e. CC /\ M e. NN0 /\ N e. NN0 ) ->
                  ( A ^ ( M + N ) ) = ( ( A ^ M ) x. ( A ^ N ) ) ) $=
      ( vj vk cc wcel cn0 caddc co cexp cmul wceq wa cv wi cc0 c1 opreq2
      opreq2d eqeq12d imbi2d expcl mulid1 syl exp0 adantr nn0cn addid1 adantl
      3eqtr4rd opreq1 ax1cn addass mp3an3 syl2an adantll simpll nn0addcl expp1
      syl11anc eqtr3d adantlr mulass syl111anc eqtr4d syl5ibr expcom a2d
      nn0ind exp3a com3l 3imp ) AFGZBHGZCHGZABCIJZKJZABKJZACKJZLJZMZVPVNVOWBVPV
      NVOWBVNVONZABDOZIJZKJZVSAWDKJZLJZMZPWCABQIJZKJZVSAQKJZLJZMZPWCABEOZIJZKJZ
      VSAWOKJZLJZMZPWCABWORIJZIJZKJZVSAXAKJZLJZMZPWCWBPDECWDQMZWIWNWCXGWFWKWHWM
      XGWEWJAKWDQBISTXGWGWLVSLWDQAKSTUAUBWDWOMZWIWTWCXHWFWQWHWSXHWEWPAKWDWOBIST
      XHWGWRVSLWDWOAKSTUAUBWDXAMZWIXFWCXIWFXCWHXEXIWEXBAKWDXABISTXIWGXDVSLWDXAA
      KSTUAUBWDCMZWIWBWCXJWFVRWHWAXJWEVQAKWDCBISTXJWGVTVSLWDCAKSTUAUBWCVSRLJZVS
      WMWKWCVSFGZXKVSMABUCZVSUDUEWCWLRVSLVNWLRMVOAUFUGTWCWJBAKVOWJBMZVNVOBFGZXN
      BUHZBUIUEUJTUKWOHGZWCWTXFWCXQWTXFPWTXFWCXQNZWQALJZWSALJZMWQWSALULXRXCXSXE
      XTXRAWPRIJZKJZXCXSXRYAXBAKVOXQYAXBMZVNVOXOWOFGZYCXQXPWOUHXOYDRFGYCUMBWORU
      NUOUPUQTXRVNWPHGZYBXSMVNVOXQURZVOXQYEVNBWOUSUQAWPUTVAVBXRXEVSWRALJZLJZXTX
      RXDYGVSLVNXQXDYGMVOAWOUTVCTXRXLWRFGZVNXTYHMWCXLXQXMUGVNXQYIVOAWOUCVCYFVSW
      RAVDVEVFUAVGVHVIVJVKVLVM $.
      $( [30-Nov-2004] $)

    $( Product of exponents law for natural number exponentiation.  Proposition
       10-4.2(b) of [Gleason] p. 135, restricted to nonnegative integer
       exponents. $)
    expmul $p |- ( ( A e. CC /\ M e. NN0 /\ N e. NN0 ) ->
               ( A ^ ( M x. N ) ) = ( ( A ^ M ) ^ N ) ) $=
      ( vj vk cc wcel cn0 cmul co cexp wceq wa cv wi cc0 c1 caddc opreq2
      opreq2d eqeq12d imbi2d nn0cn mul01 syl exp0 sylan9eqr expcl eqtr4d
      opreq1 ax1cn adddi mp3an3 mulid1 adantr eqtrd syl2an adantll simpll
      nn0mulcl simplr expadd syl111anc expp1 sylan syl5ibr expcom a2d nn0ind
      exp3a com3l 3imp ) AFGZBHGZCHGZABCIJZKJZABKJZCKJZLZVOVMVNVTVOVMVNVTVMVNMZ
      ABDNZIJZKJZVRWBKJZLZOWAABPIJZKJZVRPKJZLZOWAABENZIJZKJZVRWKKJZLZOWAABWKQRJ
      ZIJZKJZVRWPKJZLZOWAVTODECWBPLZWFWJWAXAWDWHWEWIXAWCWGAKWBPBISTWBPVRKSUAUBW
      BWKLZWFWOWAXBWDWMWEWNXBWCWLAKWBWKBISTWBWKVRKSUAUBWBWPLZWFWTWAXCWDWRWEWSXC
      WCWQAKWBWPBISTWBWPVRKSUAUBWBCLZWFVTWAXDWDVQWEVSXDWCVPAKWBCBISTWBCVRKSUAUB
      WAWHQWIVNVMWHAPKJQVNWGPAKVNBFGZWGPLBUCZBUDUETAUFUGWAVRFGZWIQLABUHZVRUFUEU
      IWKHGZWAWOWTWAXIWOWTOWOWTWAXIMZWMVRIJZWNVRIJZLWMWNVRIUJXJWRXKWSXLXJWRAWLB
      RJZKJZXKXJWQXMAKVNXIWQXMLZVMVNXEWKFGZXOXIXFWKUCXEXPMZWQWLBQIJZRJZXMXEXPQF
      GWQXSLUKBWKQULUMXQXRBWLRXEXRBLXPBUNUOTUPUQURTXJVMWLHGZVNXNXKLVMVNXIUSVNXI
      XTVMBWKUTURVMVNXIVAAWLBVBVCUPWAXGXIWSXLLXHVRWKVDVEUAVFVGVHVIVJVKVL $.
      $( [4-Jan-2006] $)
  $}

  $( Exponent subtraction law for nonnegative integer exponentiation. $)
  expsub $p |- ( ( ( ( A e. CC /\ A =/= 0 ) /\ M e. NN0 /\ N e. NN0 ) /\
      N <_ M ) -> ( A ^ ( M - N ) ) = ( ( A ^ M ) / ( A ^ N ) ) ) $=
    ( cc wcel cc0 wne wa cn0 w3a cle wbr cexp co cdiv cmin wceq cmul caddc
    simpl1l simpl3 wb nn0sub ancoms biimpa 3adantl1 expadd syl111anc nn0cn
    pncan3 syl2an opreq2d 3adant1 adantr eqtr3d expcl adantlr 3adant3 syl11anc
    3adant2 expne0i 3expa divmul syl112anc mpbird eqcomd ) ADEZAFGZHZBIEZCIEZJZ
    CBKLZHZABMNZACMNZONZABCPNZMNZVNVQVSQZVPVSRNZVOQZVNACVRSNZMNZWAVOVNVGVKVRIEZ
    WDWAQVGVHVJVKVMTZVIVJVKVMUAVJVKVMWEVIVJVKHZVMWEVKVJVMWEUBCBUCUDUEUFZACVRUGU
    HVLWDVOQZVMVJVKWIVIWGWCBAMVKVJWCBQZVKCDEBDEWJVJCUIBUICBUJUKUDULUMUNUOVNVODE
    ZVSDEZVPDEZVPFGZVTWBUBVLWKVMVIVJWKVKVGVJWKVHABUPUQURUNVNVGWEWLWFWHAVRUPUSVL
    WMVMVIVKWMVJVGVKWMVHACUPUQUTUNVLWNVMVIVKWNVJVGVHVKWNACVAVBUTUNVOVSVPVCVDVEV
    F $.
    $( [12-Jan-2009] $) $( [2-Aug-2006] $)

  $( Exponent subtraction law for nonnegative integer exponentiation. $)
  expsubOLD $p |- ( ( ( A e. CC /\ M e. NN0 /\ N e. NN0 ) /\
  ( A =/= 0 /\ N <_ M ) ) -> ( A ^ ( M - N ) ) = ( ( A ^ M ) / ( A ^ N ) ) ) $=
    ( cc wcel cn0 w3a cc0 wne cle wbr wa cexp co cdiv cmin wceq cmul caddc
    simpl1 simpl3 wb nn0sub ancoms biimpa 3adantl1 expadd syl111anc nn0cn
    pncan3 syl2an opreq2d 3adant1 adantr eqtr3d adantrl expcl 3adant3 syl11anc
    3adant2 simp1 simp3 simp2 expne0i 3expa 3adantl2 adantrr divmul syl112anc
    mpbird eqcomd ) ADEZBFEZCFEZGZAHIZCBJKZLZLZABMNZACMNZONZABCPNZMNZVSWBWDQZWA
    WDRNZVTQZVOVQWGVPVOVQLZACWCSNZMNZWFVTWHVLVNWCFEZWJWFQVLVMVNVQTZVLVMVNVQUAVM
    VNVQWKVLVMVNLZVQWKVNVMVQWKUBCBUCUDUEUFZACWCUGUHVOWJVTQZVQVMVNWOVLWMWIBAMVNV
    MWIBQZVNCDEBDEWPVMCUIBUICBUJUKUDULUMUNUOUPVSVTDEZWDDEZWADEZWAHIZWEWGUBVOWQV
    RVLVMWQVNABUQURUNVOVQWRVPWHVLWKWRWLWNAWCUQUSUPVOWSVRVLVNWSVMACUQUTUNVOVPWTV
    QVLVNVPWTVMVLVNVPWTVLVNVPGVLVPVNWTVLVNVPVAVLVNVPVBVLVNVPVCACVDUHVEVFVGVTWDW
    AVHVIVJVK $.
    $( [2-Aug-2006] $)

  $( Value of a complex number raised to a nonnegative integer power minus
     one. $)
  expm1 $p |- ( ( A e. CC /\ A =/= 0 /\ N e. NN ) -> ( A ^ ( N - 1 ) ) =
               ( ( A ^ N ) / A ) ) $=
    ( cc wcel cc0 wne cn w3a c1 cmin co cexp cdiv wa cn0 cle wbr wceq 3simpa
    nnnn0 3ad2ant3 1nn0 a1i nnge1 expsub syl31anc exp1 3ad2ant1 opreq2d eqtrd
    ) ACDZAEFZBGDZHZABIJKLKZABLKZAILKZMKZUPAMKUNUKULNBODZIODZIBPQZUOURRUKULUMSU
    MUKUSULBTUAUTUNUBUCUMUKVAULBUDUAABIUEUFUNUQAUPMUKULUQARUMAUGUHUIUJ $.
    $( [25-Dec-2008] $)

  $( Nonnegative integer exponentiation of a quotient. $)
  expdiv $p |- ( ( A e. CC /\ ( B e. CC /\ B =/= 0 ) /\ N e. NN0 ) ->
                ( ( A / B ) ^ N ) = ( ( A ^ N ) / ( B ^ N ) ) ) $=
    ( cc wcel cc0 wne wa cn0 w3a cdiv co cexp c1 cmul wceq divrec 3expb
    3adant3 opreq1d reccl mulexp syl3an2 exprec 3expa 3adant1 opreq2d expcl
    3adant2 adantlr expne0i syl111anc eqtr4d 3eqtrd ) ADEZBDEZBFGZHZCIEZJZABKLZ
    CMLANBKLZOLZCMLZACMLZVBCMLZOLZVEBCMLZKLZUTVAVCCMUOURVAVCPZUSUOUPUQVJABQRSTU
    RUOVBDEUSVDVGPBUAAVBCUBUCUTVGVENVHKLZOLZVIUTVFVKVEOURUSVFVKPZUOUPUQUSVMBCUD
    UEUFUGUTVEDEZVHDEZVHFGZVIVLPUOUSVNURACUHUIURUSVOUOUPUSVOUQBCUHUJUFURUSVPUOU
    PUQUSVPBCUKUEUFVEVHQULUMUN $.
    $( [12-Mar-2009] $) $( [2-Aug-2006] $)

  $( Ordering relationship for exponentiation. $)
  expordi $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             ( 1 < A /\ M < N ) ) -> ( A ^ M ) < ( A ^ N ) ) $=
    ( cr wcel cn0 w3a c1 clt wbr wa cexp co cmul cmin cn simpl1 wb cz nn0z
    znnsub syl2an 3adant1 biimpa adantrl simprl expgt1 syl111anc cc0 nnnn0 syl
    reexpcl syl11anc 3adant3 adantr wi lt01 0re 1re axlttrn mp3an12 mpani
    expgt0 3expia syld imp adantrr ltmul1 mp3an1 syl12anc mpbid wceq cc recn
    expcl sylan mulid2 caddc simp1 recnd simpl2 3jca expadd syl3an2 nn0cn
    npcan ancoms opreq2d eqtr3d 3brtr3d ) ADEZBFEZCFEZGZHAIJZBCIJZKZKZHABLMZNMZ
    ACBOMZLMZWSNMZWSACLMZIWRHXBIJZWTXCIJZWRWKXAPEZWOXEWKWLWMWQQWNWPXGWOWNWPXGWL
    WMWPXGRZWKWLBSECSEXHWMBTCTBCUAUBUCUDZUEWNWOWPUFAXAUGUHWRXBDEZWSDEZUIWSIJZXE
    XFRZWNWPXJWOWNWPKZWKXAFEZXJWKWLWMWPQXNXGXOXIXAUJZUKAXAULUMUEWNXKWQWKWLXKWMA
    BULUNUOWNWOXLWPWNWOXLWKWLWOXLUPWMWKWLKZWOUIAIJZXLWKWOXRUPWLWKUIHIJZWOXRUQUI
    DEHDEZWKXSWOKXRUPURUSUIHAUTVAVBUOWKWLXRXLABVCVDVEUNVFVGXTXJXKXLKXMUSHXBWSVH
    VIVJVKWNWTWSVLZWQWKWLYAWMXQWSVMEZYAWKAVMEZWLYBAVNABVOVPWSVQUKUNUOWRAXABVRMZ
    LMZXCXDWRYCXGWLGZYEXCVLZWNWPYFWOXNYCXGWLWNYCWPWNAWKWLWMVSVTUOXIWKWLWMWPWAWB
    UEXGYCXOWLYGXPAXABWCWDUKWNYEXDVLWQWNYDCALWLWMYDCVLZWKWMWLYHWMCVMEBVMEYHWLCW
    EBWECBWFUBWGUCWHUOWIWJ $.
    $( [2-Aug-2006] $)

  $( Cancellation law for exponentiation. $)
  expcan $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             1 < A ) -> ( ( A ^ M ) = ( A ^ N ) <-> M = N ) ) $=
    ( cr wcel cn0 w3a c1 clt wbr wa cexp co wceq wo wne expordi expr wi exp32
    3com23 imp orim12d wb nn0re lttri2 syl2an 3adant1 adantr reexpcl 3impdi
    3imtr4d necon4d opreq2 impbid1 ) ADEZBFEZCFEZGZHAIJZKZABLMZACLMZNBCNVABCVBV
    CVABCIJZCBIJZOZVBVCIJZVCVBIJZOZBCPZVBVCPZVAVDVGVEVHUSUTVDVGABCQRUSUTVEVHSZU
    PURUQUTVLSUPURUQGUTVEVHACBQTUAUBUCUSVJVFUDZUTUQURVMUPUQBDECDEVMURBUECUEBCUF
    UGUHUIUSVKVIUDZUTUPUQURVNUPUQKVBDEVCDEVNUPURKABUJACUJVBVCUFUGUKUIULUMBCALUN
    UO $.
    $( [2-Aug-2006] $)

  $( Ordering law for exponentiation. $)
  expord $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             1 < A ) -> ( M < N <-> ( A ^ M ) < ( A ^ N ) ) ) $=
    ( cr wcel cn0 w3a c1 clt wbr wa cexp co expordi expr wceq wo wn expcan
    biimprd wi exp32 3com23 imp orim12d con3d wb reexpcl axlttri syl2an 3impdi
    adantr nn0re 3adant1 3imtr4d impbid ) ADEZBFEZCFEZGZHAIJZKZBCIJZABLMZACLMZI
    JZUTVAVCVFABCNOVBVDVEPZVEVDIJZQZRZBCPZCBIJZQZRZVFVCVBVMVIVBVKVGVLVHVBVGVKAB
    CSTUTVAVLVHUAZUQUSURVAVOUAUQUSURGVAVLVHACBNUBUCUDUEUFUTVFVJUGZVAUQURUSVPUQU
    RKVDDEVEDEVPUQUSKABUHACUHVDVEUIUJUKULUTVCVNUGZVAURUSVQUQURBDECDEVQUSBUMCUMB
    CUIUJUNULUOUP $.
    $( [2-Aug-2006] $)

  $( Weak ordering relationship for exponentiation. $)
  expwordi $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             ( 1 <_ A /\ M <_ N ) ) -> ( A ^ M ) <_ ( A ^ N ) ) $=
    ( cr wcel cn0 w3a c1 cle wbr cexp co clt wceq wo wi wb 1re leloe mpan
    3ad2ant1 wa expord biimpd opreq2 a1i orim12d nn0re syl2an 3adant1 adantr
    reexpcl 3adant3 3adant2 syl11anc 3imtr4d ex leidi 1exp breqan12d mpbiri
    opreq1 breq12d syl5ibcom a1dd jaod sylbid imp32 ) ADEZBFEZCFEZGZHAIJZBCIJZA
    BKLZACKLZIJZVLVMHAMJZHANZOZVNVQPZVIVJVMVTQZVKHDEVIWBRHASTUAVLVRWAVSVLVRWAVL
    VRUBZBCMJZBCNZOZVOVPMJZVOVPNZOZVNVQWCWDWGWEWHWCWDWGABCUCUDWEWHPWCBCAKUEUFUG
    VLVNWFQZVRVJVKWJVIVJBDECDEWJVKBUHCUHBCSUIUJUKVLVQWIQZVRVLVODEZVPDEZWKVIVJWL
    VKABULUMVIVKWMVJACULUNVOVPSUOUKUPUQVLVSVQVNVJVKVSVQPVIVJVKUBZHBKLZHCKLZIJZV
    SVQWNWQHHIJHRURVJVKWOHWPHIBUSCUSUTVAVSWOVOWPVPIHABKVBHACKVBVCVDUJVEVFVGVH
    $.
    $( [9-Oct-2006] $) $( [14-Dec-2005] $)

  $( The power of a positive number smaller than 1 decreases as its exponent
     increases. $)
  expord2 $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             ( 0 < A /\ A < 1 ) ) -> ( M < N <-> ( A ^ N ) < ( A ^ M ) ) ) $=
    ( cr wcel cn0 w3a cc0 clt wbr c1 wa cdiv co cexp wb wne gt0ne0 rereccl
    syldan expcom idd 3anim123d impcom adantrr reclt1 biimpa anasss 3ad2antl1
    expord syl11anc reexpcl adantr 3adantl2 expgt0 3expa 3adantl3 ltrec
    syl22anc cc recn id 3anim123i wceq exprecOLD breq12d bitr4d ) ADEZBFEZCFEZG
    ZHAIJZAKIJZLZLZBCIJZKAMNZBONZVQCONZIJZACONZABONZIJZVOVQDEZVIVJGZKVQIJZVPVTP
    VKVLWEVMVLVKWEVLVHWDVIVIVJVJVHVLWDVHVLAHQZWDARZASTUAVLVIUBVLVJUBUCUDUEVHVIV
    NWFVJVHVLVMWFVHVLLVMWFAUFUGUHUIVQBCUJUKVKVLWCVTPVMVKVLLZWCKWBMNZKWAMNZIJZVT
    WIWADEZHWAIJZWBDEZHWBIJZWCWLPVHVJVLWMVIVHVJLWMVLACULUMUNVHVJVLWNVIVHVJVLWNA
    CUOUPUNVHVIVLWOVJVHVILWOVLABULUMUQVHVIVLWPVJVHVIVLWPABUOUPUQWAWBURUSWIAUTEZ
    VIVJGZWGVTWLPVKWRVLVHWQVIVIVJVJAVAVIVBVJVBVCUMVHVIVLWGVJWHUIWRWGLVRWJVSWKIW
    QVIWGVRWJVDZVJWQVIWGWSABVEUPUQWQVJWGVSWKVDZVIWQVJWGWTACVEUPUNVFUKVGUEVG $.
    $( [10-Jul-2007] $) $( [2-Aug-2006] $)

  $( Weak ordering relationship for exponentiation.  (Contributed by Paul
     Chapman, 14-Jan-2008.) $)
  expword2i $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
                       ( 0 < A /\ A <_ 1 /\ M < N ) ) ->
                     ( A ^ N ) <_ ( A ^ M ) ) $=
    ( cc0 clt wbr c1 cle w3a cr wcel cn0 wa cexp co df-3an wi wceq wo expord2
    biimpd ex exp3a com34 imp3a imp 1exp adantr adantl eqtr4d 3adant1 opreq1
    3eqtr4rd orim12d wb 1re leloe mpan2 3ad2ant1 reexpcl 3adant2 3adant3
    syl11anc 3imtr4d sylan2b ) DAEFZAGHFZBCEFZIAJKZBLKZCLKZIZVFVGMZVHMZACNOZABN
    OZHFZVFVGVHPVLVNVQVLVMVHVQVLVFVGVHVQQVLVFVHVGVQVLVFVHVGVQQZVLVFVHMZVRVLVSMZ
    AGEFZAGRZSZVOVPEFZVOVPRZSZVGVQVTWAWDWBWEVLVSWAWDQZVLVFVHWGVLVFWAVHWDVLVFWAV
    HWDQZVLVFWAMZWHVLWIMVHWDABCTUAUBUCUDUEUFVLWBWEQVSVLWBWEVLWBMGBNOZGCNOZVPVOV
    LWJWKRZWBVJVKWLVIVJVKMWJGWKVJWJGRVKBUGUHVKWKGRVJCUGUIUJUKUHWBVPWJRVLAGBNULU
    IWBVOWKRVLAGCNULUIUMUBUHUNVLVGWCUOZVSVIVJWMVKVIGJKWMUPAGUQURUSUHVLVQWFUOZVS
    VLVOJKZVPJKZWNVIVKWOVJACUTVAVIVJWPVKABUTVBVOVPUQVCUHVDUBUCUDUEUEUFVE $.
    $( [14-Jan-2008] $)

  ${
    $d j k A $.  $d j k B $.  $d j k N $.
    $( Weak mantissa ordering relationship for exponentiation. $)
    expmwordi $p |- ( ( ( A e. RR /\ B e. RR /\ N e. NN0 ) /\
               ( 0 <_ A /\ A <_ B ) ) -> ( A ^ N ) <_ ( B ^ N ) ) $=
      ( vj vk cr wcel cn0 cc0 cle wbr wa cexp co wi cv c1 caddc cmul reexpcl
      adantlr adantr adantll expge0 3expa an32s adantllr adantlrr anim1i jca31
      simpll lemul12aOLD syl11anc wceq cc recn expp1 sylan 3brtr4d ex expcom
      a2d exp0 1re leidi syl6eqbr adantl breqtrrd syl2an opreq2 breq12d imbi2d
      nn0indALT exp4c com3l 3imp1 ) AFGZBFGZCHGZIAJKZABJKZLZACMNZBCMNZJKZVSVQVR
      WBWEOVSVQVRWBWEVQVRLZWBLZADPZMNZBWHMNZJKZOWGAIMNZBIMNZJKZOWGAEPZMNZBWOMNZ
      JKZOWGAWOQRNZMNZBWSMNZJKZOWGWEODECWOHGZWGWRXBWGXCWRXBOWGXCLZWRXBXDWRLZWPA
      SNZWQBSNZWTXAJXEWPFGZWQFGZLIWPJKZWRLZLWGXFXGJKXEXHXIXKXDXHWRWFXCXHWBVQXCX
      HVRAWOTUAUAUBXDXIWRWFXCXIWBVRXCXIVQBWOTUCUAUBXDXJWRWFVTXCXJWAVQVTXCXJVRVQ
      XCVTXJVQXCVTXJAWOUDUEUFUGUHUIUJWGXCWRUKWPWQABULUMXDWTXFUNZWRWFXCXLWBVQXCX
      LVRVQAUOGZXCXLAUPZAWOUQURUAUAUBXDXAXGUNZWRWFXCXOWBVRXCXOVQVRBUOGZXCXOBUPZ
      BWOUQURUCUAUBUSUTVAVBWFWNWBVQXMXPWNVRXNXQXMXPLZWLQWMJXRWLQQJXMWLQUNXPAVCU
      BQVDVEVFXPWMQUNXMBVCVGVHVIUBWHIUNZWKWNWGXSWIWLWJWMJWHIAMVJWHIBMVJVKVLWHWO
      UNZWKWRWGXTWIWPWJWQJWHWOAMVJWHWOBMVJVKVLWHWSUNZWKXBWGYAWIWTWJXAJWHWSAMVJW
      HWSBMVJVKVLWHCUNZWKWEWGYBWIWCWJWDJWHCAMVJWHCBMVJVKVLVMVNVOVP $.
      $( [18-Dec-2005] $)
  $}

  $( Nonnegative integer exponentiation with a mantissa between 0 and 1
     inclusive is less than or equal to 1.  (Contributed by Paul Chapman,
     29-Dec-2007.) $)
  exple1 $p |- ( ( ( A e. RR /\ 0 <_ A /\ A <_ 1 ) /\ N e. NN0 ) ->
                  ( A ^ N ) <_ 1 ) $=
    ( cr wcel cc0 cle wbr c1 w3a cn0 cexp co wi clt wceq wo wb 0re leloe mpan
    biimpa wa cdiv wne simp1 gt0ne0 3adant3 rereccl syl11anc simp3 1re lt01
    lerec2 mpanl12 ax1cn div1i breq2i syl6bb mpbird jca expge1 3com23 3expa
    sylan cc recnd exprecOLD breqtrd reexpcl 3ad2antl1 expgt0 3adantl3 mpbid
    syl6breq ex 3expia cn elnn0 0exp ltleii syl6eqbr opreq2 0cn exp0 ax-mp
    syl6eq leidi jaoi sylbi opreq1 breq1d syl5ib a1d adantl jaodan syldan
    3impia imp ) ACDZEAFGZAHFGZIBJDZABKLZHFGZWSWTXAXBXDMZWSWTEANGZEAOZPZXAXEMZW
    SWTXHECDWSWTXHQREASTUAWSXFXIXGWSXFXAXEWSXFXAIZXBXDXJXBUBZXCHHUCLZHFXKHHXCUC
    LZFGZXCXLFGZXKHHAUCLZBKLZXMFXJXPCDZHXPFGZUBXBHXQFGZXJXRXSXJWSAEUDZXRWSXFXAU
    EZWSXFYAXAAUFUGZAUHUIXJXSXAWSXFXAUJXJXSAXLFGZXAWSXFXSYDQZXAHCDZEHNGZWSXFUBY
    EUKULHAUMUNUGXLHAFHUOUPZUQURUSUTXRXSXBXTXRXBXSXTXPBVAVBVCVDXJAVEDZYAUBXBXQX
    MOZXJYIYAXJAYBVFYCUTYIYAXBYJYIXBYAYJABVGVBVCVDVHXKXCCDZEXCNGZXNXOQZWSXFXBYK
    XAABVIVJWSXFXBYLXAWSXFXBYLWSXBXFYLABVKVBVCVLYFYGYKYLUBYMUKULHXCUMUNUIVMYHVN
    VOVPXGXIWSXGXEXAXBEBKLZHFGZXGXDXBBVQDZBEOZPYOBVRYPYOYQYPYNEHFBVSEHRUKULVTWA
    YQYNHHFYQYNEEKLZHBEEKWBEVEDYRHOWCEWDWEWFHUKWGWAWHWIXGYNXCHFEABKWJWKWLWMWNWO
    WPWQWR $.
    $( [29-Dec-2007] $)

  $( An upper bound on ` A ^ N ` when ` 2 <_ A ` . $)
  expubnd $p |- ( ( A e. RR /\ N e. NN0 /\ 2 <_ A ) ->
                  ( A ^ N ) <_ ( ( 2 ^ N ) x. ( ( A - 1 ) ^ N ) ) ) $=
    ( cr wcel cn0 c2 cle wbr w3a cexp co c1 cmin cmul cc0 wa simp1 2re
    peano2rem remulcl sylancr 3ad2ant1 simp2 0re 2pos ltleii wi letr mp3an12
    mpani imp caddc wb resubcl mpan2 leadd2 mp3an1 mpdan biimpa wceq cc recn
    2cn npcan sylancl adantr ax1cn subdi mp3an13 2times mulid1i a1i opreq12d
    addsub mp3an3 anidms 3eqtrrd syl 3brtr3d jca 3adant2 expmwordi syl31anc
    recnd mulexp sylan 3adant3 breqtrd ) ACDZBEDZFAGHZIZABJKZFALMKZNKZBJKZFBJKW
    NBJKNKZGWLWIWOCDZWJOAGHZAWOGHZPZWMWPGHWIWJWKQWIWJWRWKWIFCDZWNCDWRRASZFWNTUA
    UBWIWJWKUCWIWKXAWJWIWKPZWSWTWIWKWSWIOFGHZWKWSOFUDRUEUFOCDXBWIXEWKPWSUGUDROF
    AUHUIUJUKXDAFMKZFULKZXFAULKZAWOGWIWKXGXHGHZWIXFCDZWKXIUMZWIXBXJRAFUNUOXBWIX
    JXKRFAXFUPUQURUSWIXGAUTZWKWIAVADZFVADZXLAVBZVCAFVDVEVFWIXHWOUTZWKWIXMXPXOXM
    WOFANKZFLNKZMKZAAULKZFMKZXHXNXMLVADWOXSUTVCVGFALVHVIXMXQXTXRFMAVJXRFUTXMFVC
    VKVLVMXMYAXHUTZXMXMXNYBVCAAFVNVOVPVQVRVFVSVTWAAWOBWBWCWIWJWPWQUTZWKWIWNVADZ
    WJYCWIWNXCWDXNYDWJYCVCFWNBWEUQWFWGWH $.
    $( [19-Dec-2005] $)

  $( Value of the square of a complex number.  (Contributed by Raph Levien,
     10-Apr-2004.) $)
  sqval $p |- ( A e. CC -> ( A ^ 2 ) = ( A x. A ) ) $=
    ( cc wcel c2 cexp co c1 cmul caddc df-2 opreq2i cn0 wceq 1nn0 expp1 mpan2
    syl5eq exp1 opreq1d eqtrd ) ABCZADEFZAGEFZAHFZAAHFUAUBAGGIFZEFZUDDUEAEJKUAG
    LCUFUDMNAGOPQUAUCAAHARST $.
    $( [28-Jul-2006] $) $( [10-Apr-2004] $)

  $( The square of the negative of a number.) $)
  sqneg $p |- ( A e. CC -> ( -u A ^ 2 ) = ( A ^ 2 ) ) $=
    ( cc wcel cneg cmul co c2 cexp wceq mul2neg anidms negcl sqval syl 3eqtr4d
    ) ABCZADZQEFZAAEFZQGHFZAGHFPRSIAAJKPQBCTRIALQMNAMO $.
    $( [18-Feb-2007] $) $( [15-Jan-2006] $)

  $( Closure of square. $)
  sqcl $p |- ( A e. CC -> ( A ^ 2 ) e. CC ) $=
    ( cc wcel c2 cexp co cmul sqval mulcl anidms eqeltrd ) ABCZADEFAAGFZBAHLMBC
    AAIJK $.
    $( [10-Aug-1999] $)

  $( Distribution of square over multiplication. $)
  sqmul $p |- ( ( A e. CC /\ B e. CC ) ->
             ( ( A x. B ) ^ 2 ) = ( ( A ^ 2 ) x. ( B ^ 2 ) ) ) $=
    ( cc wcel c2 cn0 cmul co cexp wceq 2nn0 mulexp mp3an3 ) ACDBCDEFDABGHEIHAEI
    HBEIHGHJKABELM $.
    $( [21-Mar-2008] $)

  $( A number is zero iff its square is zero. $)
  sqeq0 $p |- ( A e. CC -> ( ( A ^ 2 ) = 0 <-> A = 0 ) ) $=
    ( cc wcel c2 cn cexp co cc0 wceq wb 2nn expeq0 mpan2 ) ABCDECADFGHIAHIJKADL
    M $.
    $( [11-Mar-2006] $)

  ${
    sqval.1 $e |- A e. CC $.
    $( Value of square.  Inference version. $)
    sqvali $p |- ( A ^ 2 ) = ( A x. A ) $=
      ( cc wcel c2 cexp co cmul wceq sqval ax-mp ) ACDAEFGAAHGIBAJK $.
      $( [1-Aug-1999] $)

    $( Closure of square. $)
    sqcli $p |- ( A ^ 2 ) e. CC $=
      ( cc wcel c2 cexp co sqcl ax-mp ) ACDAEFGCDBAHI $.
      $( [2-Aug-1999] $)

    $( A number is zero iff its square is zero. $)
    sqeq0i $p |- ( ( A ^ 2 ) = 0 <-> A = 0 ) $=
      ( cc wcel c2 cexp co cc0 wceq wb sqeq0 ax-mp ) ACDAEFGHIAHIJBAKL $.
      $( [2-Oct-1999] $)

    ${
      sqmul.2 $e |- B e. CC $.
      $( Distribution of square over multiplication. $)
      sqmuli $p |- ( ( A x. B ) ^ 2 ) = ( ( A ^ 2 ) x. ( B ^ 2 ) ) $=
        ( cc wcel cmul co c2 cexp wceq sqmul mp2an ) AEFBEFABGHIJHAIJHBIJHGHKCD
        ABLM $.
        $( [31-Jul-2006] $) $( [3-Sep-1999] $)

      ${
        sqdiv.3 $e |- B =/= 0 $.
        $( Distribution of square over division. $)
        sqdivi $p |- ( ( A / B ) ^ 2 ) = ( ( A ^ 2 ) / ( B ^ 2 ) ) $=
          ( cdiv co cmul c2 cexp divmuldivi divcli sqvali opreq12i 3eqtr4i ) AB
          FGZPHGAAHGZBBHGZFGPIJGAIJGZBIJGZFGABABCDCDEEKPABCDELMSQTRFACMBDMNO $.
          $( [20-Aug-2001] $)
      $}
    $}

    ${
      sqreci.1 $e |- A =/= 0 $.
      $( Square of reciprocal. $)
      sqrecii $p |- ( ( 1 / A ) ^ 2 ) = ( 1 / ( A ^ 2 ) ) $=
        ( c1 cdiv co cmul c2 cexp ax1cn divmuldivi mulid1i opreq1i eqtri
        reccli sqvali opreq2i 3eqtr4i ) DAEFZSGFZDAAGFZEFZSHIFDAHIFZEFTDDGFZUAE
        FUBDADAJBJBCCKUDDUAEDJLMNSABCOPUCUADEABPQR $.
        $( [17-Sep-1999] $)
    $}
  $}

  $( A number is nonzero iff its square is nonzero. $)
  sqne0 $p |- ( A e. CC -> ( ( A ^ 2 ) =/= 0 <-> A =/= 0 ) ) $=
    ( cc wcel c2 cexp co cc0 sqeq0 necon3bid ) ABCADEFGAGAHI $.
    $( [11-Mar-2006] $)

  $( Closure of the square of a real number. $)
  resqcl $p |- ( A e. RR -> ( A ^ 2 ) e. RR ) $=
    ( cr wcel c2 cn0 cexp co 2nn0 reexpcl mpan2 ) ABCDECADFGBCHADIJ $.
    $( [29-Jul-2006] $) $( [18-Oct-1999] $)

  $( The square of a nonzero real is positive. $)
  sqgt0 $p |- ( ( A e. RR /\ A =/= 0 ) -> 0 < ( A ^ 2 ) ) $=
    ( cr wcel cc0 wne wa cmul co c2 cexp clt msqgt0 wceq cc recn sqval syl
    adantr breqtrrd ) ABCZADEZFDAAGHZAIJHZKALTUCUBMZUATANCUDAOAPQRS $.
    $( [8-Sep-2007] $)

  ${
    resqcl.1 $e |- A e. RR $.
    $( Closure of square in reals. $)
    resqcli $p |- ( A ^ 2 ) e. RR $=
      ( cr wcel c2 cexp co resqcl ax-mp ) ACDAEFGCDBAHI $.
      $( [2-Aug-1999] $)

    ${
      lt2sq.2 $e |- B e. RR $.
      $( The square function on nonnegative reals is strictly monotonic. $)
      lt2sqi $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( A < B <-> ( A ^ 2 ) < ( B ^ 2 ) ) ) $=
        ( cc0 cle wbr wa clt cmul co c2 cexp lt2msqi recni sqvali breq12i
        syl6bbr ) EAFGEBFGHABIGAAJKZBBJKZIGALMKZBLMKZIGABCDNUASUBTIAACOPBBDOPQR
        $.
        $( [12-Sep-1999] $)

      $( The square function on nonnegative reals is monotonic. $)
      le2sqi $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( A <_ B <-> ( A ^ 2 ) <_ ( B ^ 2 ) ) ) $=
        ( cc0 cle wbr wa cmul co c2 cexp le2msqi recni sqvali breq12i syl6bbr
        ) EAFGEBFGHABFGAAIJZBBIJZFGAKLJZBKLJZFGABCDMTRUASFAACNOBBDNOPQ $.
        $( [12-Sep-1999] $)

      $( The square function is one-to-one for nonnegative reals. $)
      sq11i $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( ( A ^ 2 ) = ( B ^ 2 ) <-> A = B ) ) $=
        ( c2 cexp co wceq cmul cc0 cle wbr wa recni sqvali eqeq12i msq11i
        syl5bb ) AEFGZBEFGZHAAIGZBBIGZHJAKLJBKLMABHSUATUBAACNOBBDNOPABCDQR $.
        $( [27-Oct-1999] $)
    $}

    $( The square of a nonzero real is positive. $)
    sqgt0i $p |- ( A =/= 0 -> 0 < ( A ^ 2 ) ) $=
      ( cr wcel cc0 wne c2 cexp co clt wbr sqgt0 mpan ) ACDAEFEAGHIJKBALM $.
      $( [17-Sep-1999] $)

    $( A square of a real is nonnegative. $)
    sqge0i $p |- 0 <_ ( A ^ 2 ) $=
      ( cc0 cmul co c2 cexp cle msqge0i recni sqvali breqtrri ) CAADEAFGEHABIAA
      BJKL $.
      $( [3-Aug-1999] $)
  $}

  $( The square function is one-to-one for nonnegative reals. $)
  sq11 $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
               ( ( A ^ 2 ) = ( B ^ 2 ) <-> A = B ) ) $=
    ( cr wcel cc0 cle wbr c2 cexp co wceq wb wa wi cif breq2 anbi1d opreq1
    eqeq1d eqeq1 bibi12d imbi12d anbi2d eqeq2d eqeq2 0re elimel sq11i dedth2h
    imp an4s ) ACDZBCDZEAFGZEBFGZAHIJZBHIJZKZABKZLZULUMMUNUOMZUTULUMVAUTNEULAEO
    ZFGZUOMZVBHIJZUQKZVBBKZLZNVCEUMBEOZFGZMZVEVIHIJZKZVBVIKZLZNABEEAVBKZVAVDUTV
    HVPUNVCUOAVBEFPQVPURVFUSVGVPUPVEUQAVBHIRSAVBBTUAUBBVIKZVDVKVHVOVQUOVJVCBVIE
    FPUCVQVFVMVGVNVQUQVLVEBVIHIRUDBVIVBUEUAUBVBVIAECUFUGBECUFUGUHUIUJUK $.
    $( [13-Mar-2006] $) $( [8-Apr-2001] $)

  $( The square function on nonnegative reals is strictly monotonic. $)
  lt2sq $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
              ( A < B <-> ( A ^ 2 ) < ( B ^ 2 ) ) ) $=
    ( cr wcel cc0 cle wbr wa clt cmul co c2 cexp lt2msq wb cc recn sqval
    breqan12d syl2an ad2ant2r bitr4d ) ACDZEAFGZHBCDZEBFGZHHABIGAAJKZBBJKZIGZAL
    MKZBLMKZIGZABNUCUEULUIOZUDUFUCAPDZBPDZUMUEAQBQUNUOUJUGUKUHIARBRSTUAUB $.
    $( [24-Feb-2006] $)

  $( The square function on nonnegative reals is monotonic. $)
  le2sq $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
              ( A <_ B <-> ( A ^ 2 ) <_ ( B ^ 2 ) ) ) $=
    ( cr wcel cc0 cle wbr wa cmul co c2 cexp le2msq wb cc recn sqval breqan12d
    syl2an ad2ant2r bitr4d ) ACDZEAFGZHBCDZEBFGZHHABFGAAIJZBBIJZFGZAKLJZBKLJZFG
    ZABMUBUDUKUHNZUCUEUBAODZBODZULUDAPBPUMUNUIUFUJUGFAQBQRSTUA $.
    $( [26-Jul-2006] $) $( [18-Oct-1999] $)

  $( The square of a 'less than or equal to' ordering. $)
  le2sq2 $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ A <_ B ) ) ->
              ( A ^ 2 ) <_ ( B ^ 2 ) ) $=
    ( cr wcel cc0 cle wbr wa c2 cexp co simprr wb simprl wi 0re letr mp3an1
    exp4b com23 imp43 jca le2sq syldan mpbid ) ACDZEAFGZHZBCDZABFGZHZHZUJAIJKBI
    JKFGZUHUIUJLUHUKUIEBFGZHUJUMMULUIUNUHUIUJNUFUGUIUJUNUFUIUGUJUNOUFUIUGUJUNEC
    DUFUIUGUJHUNOPEABQRSTUAUBABUCUDUE $.
    $( [21-Mar-2008] $)

  $( A square of a real is nonnegative. $)
  sqge0 $p |- ( A e. RR -> 0 <_ ( A ^ 2 ) ) $=
    ( cr wcel cc0 c2 cexp co cle wbr cif wceq opreq1 breq2d 0re elimel sqge0i
    dedth ) ABCZDAEFGZHIDRADJZEFGZHIADATKSUADHATEFLMTADBNOPQ $.
    $( [18-Oct-1999] $)

  ${
    sumsqne0.1 $e |- A e. RR $.
    sumsqne0.2 $e |- B e. RR $.
    $( The sum of two squares is nonzero iff one of its terms is nonzero. $)
    sumsqne0i $p |- ( ( A =/= 0 \/ B =/= 0 ) <->
                   ( ( A ^ 2 ) + ( B ^ 2 ) ) =/= 0 ) $=
      ( cc0 wne wo c2 cexp co caddc clt wbr sqgt0i cle sqge0i cr wcel wb
      resqcli addge01 mp2an mpbi 0re readdcli ltletri mpan2 gt0ne0i 3syl
      addge02 jaoi wceq wn wa opreq1 opreqan12d cn 2nn 0exp ax-mp opreq12i 0cn
      addid1i eqtri syl6eq con3i df-ne neorian 3imtr4i impbii ) AEFZBEFZGZAHIJZ
      BHIJZKJZEFZVKVQVLVKEVNLMZEVPLMZVQACNVRVNVPOMZVSEVOOMZVTBDPVNQRZVOQRZWAVTS
      ACTZBDTZVNVOUAUBUCEVNVPUDWDVNVOWDWEUEZUFUGVPWFUHZUIVLEVOLMZVSVQBDNWHVOVPO
      MZVSEVNOMZWIACPWCWBWJWISWEWDVOVNUJUBUCEVOVPUDWEWFUFUGWGUIUKVPEULZUMAEULZB
      EULZUNZUMVQVMWNWKWNVPEHIJZWOKJZEWLWMVNWOVOWOKAEHIUOBEHIUOUPWPEEKJEWOEWOEK
      HUQRWOEULURHUSUTZWQVAEVBVCVDVEVFVPEVGAEBEVHVIVJ $.
      $( [25-Jul-2006] $) $( [29-Apr-2005] $)
  $}

  $( The square of 0 is 0. $)
  sq0 $p |- ( 0 ^ 2 ) = 0 $=
    ( cc0 c2 cexp co wceq eqid 0cn sqeq0i mpbir ) ABCDAEAAEAFAGHI $.
    $( [6-Jun-2006] $)

  $( If a number is zero, its square is zero.  (Contributed by FL,
     10-Dec-2006.) $)
  sq0i $p |- ( A = 0 -> ( A ^ 2 ) = 0 ) $=
    ( cc0 wceq c2 cexp co opreq1 sq0 syl6eq ) ABCADEFBDEFBABDEGHI $.
    $( [10-Dec-2006] $)

  $( The square of 1 is 1. $)
  sq1 $p |- ( 1 ^ 2 ) = 1 $=
    ( c2 cn0 wcel c1 cexp co wceq 2nn0 1exp ax-mp ) ABCDAEFDGHAIJ $.
    $( [8-May-2007] $) $( [22-Aug-1999] $)

  $( The square of 2 is 4. $)
  sq2 $p |- ( 2 ^ 2 ) = 4 $=
    ( c2 cexp co cmul c4 2cn sqvali 2t2e4 eqtri ) AABCAADCEAFGHI $.
    $( [22-Aug-1999] $)

  $( The square of 3 is 9. $)
  sq3 $p |- ( 3 ^ 2 ) = 9 $=
    ( c3 c2 cexp co cmul c9 3re recni sqvali 3t3e9 eqtri ) ABCDAAEDFAAGHIJK $.
    $( [26-Apr-2006] $)

  $( The cube of 2 is 8. $)
  cu2 $p |- ( 2 ^ 3 ) = 8 $=
    ( c2 c3 cexp co c1 caddc cmul c8 df-3 opreq2i cc wcel cn0 wceq 2cn 2nn0
    expp1 mp2an c4 sq2 opreq1i 4t2e8 eqtri 3eqtri ) ABCDAAEFDZCDZAACDZAGDZHBUEA
    CIJAKLAMLUFUHNOPAAQRUHSAGDHUGSAGTUAUBUCUD $.
    $( [28-Jul-2006] $) $( [2-Aug-2004] $)

  $( A counterexample showing that exponentiation is not associative.
     (Contributed by Stefan Allan and G&eacute;rard Lang, 21-Sep-2010.) $)
  expnass $p |- ( ( 3 ^ 3 ) ^ 3 ) < ( 3 ^ ( 3 ^ 3 ) ) $=
    ( c3 cmul co cexp clt cc wcel cn0 wceq 3nn nncni nnnn0i expmul mp3an cr
    wbr 3re nn0mulcli nn0expcl mp2an w3a c1 1lt3 c2 sqvali 2nn0 2lt3 expordi
    mpanr12 eqbrtrri ) AAABCZDCZAADCZADCZAUMDCZEAFGAHGZUPULUNIAJKZAJLZURAAAMNAO
    GZUKHGZUMHGZULUOEPZQAAURURRUPUPVAURURAASTUSUTVAUAUBAEPZUKUMEPVBUCAUDDCZUKUM
    EAUQUEUSUDHGZUPVDUMEPZQUFURUSVEUPUAVCUDAEPVFUCUGAUDAUHUINUJAUKUMUHUINUJ $.
    $( [21-Sep-2010] $)

  $( Cancel one factor of a square in a ` <_ ` comparison.  Unlike ~ lemul1 ,
     the common factor ` A ` may be zero. $)
  sqlecan $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
      ( ( A ^ 2 ) <_ ( B x. A ) <-> A <_ B ) ) $=
    ( cr wcel cc0 cle wbr wa c2 cexp co cmul wb clt wceq wo wi 0re leloe mpan
    w3a cc recn sqval syl 3ad2ant1 adantr breq1d lemul1OLD bitr4d 3exp1
    pm2.43a adantrd com23 sq0 leidi eqbrtri mul01 syl5breqr adantl opreq1
    opreq2 breq12d mpbid adantrr breq1 biimpa adantrl 2thd ex a1i jaod sylbid
    imp31 ) ACDZEAFGZBCDZEBFGZHZAIJKZBALKZFGZABFGZMZVOVPEANGZEAOZPZVSWDQZECDVOV
    PWGMREASTVOWEWHWFVOVSWEWDVOVQWEWDQZVRVQVOWIVOVQVOWEWDVOVQVOUAZWEHZWBAALKZWA
    FGWCWKVTWLWAFWJVTWLOZWEVOVQWMVOVOAUBDWMAUCAUDUEUFUGUHABAUIUJUKULUMUNWFWHQVO
    WFVSWDWFVSHWBWCWFVQWBVRWFVQHEIJKZBELKZFGZWBVQWPWFVQWNEWOFWNEEFUOERUPUQVQBUB
    DWOEOBUCBURUEUSUTWFWPWBMVQWFWNVTWOWAFEAIJVAEABLVBVCUGVDVEWFVRWCVQWFVRWCEABF
    VFVGVHVIVJVKVLVMVN $.
    $( [17-Jan-2008] $)

  $( Factor the difference of two squares. $)
  subsq $p |- ( ( A e. CC /\ B e. CC ) ->
          ( ( A ^ 2 ) - ( B ^ 2 ) ) = ( ( A + B ) x. ( A - B ) ) ) $=
    ( cc wcel wa caddc co cmin cmul c2 cexp wceq simpl simpr subcl adddir
    syl111anc subdi 3anidm12 sqval adantr opreq1d eqtr4d mulcom adantl
    opreq12d sqcl mulcl syl11anc addsubass addcom pncan3 eqtrd eqtr3d 3eqtrrd
    ) ACDZBCDZEZABFGABHGZIGZAUSIGZBUSIGZFGZAJKGZABIGZHGZVEBJKGZHGZFGZVDVGHGZURU
    PUQUSCDUTVCLUPUQMZUPUQNZABOABUSPQURVAVFVBVHFURVAAAIGZVEHGZVFUPUQVAVNLAABRSU
    RVDVMVEHUPVDVMLUQATUAUBUCURVBBAIGZBBIGZHGZVHURUQUPUQVBVQLVLVKVLBABRQURVEVOV
    GVPHABUDUQVGVPLUPBTUEUFUCUFURVFVEFGZVGHGZVIVJURVFCDZVECDZVGCDZVSVILURVDCDZW
    AVTUPWCUQAUGUAZABUHZVDVEOUIZWEUQWBUPBUGUEVFVEVGUJQURVRVDVGHURVRVEVFFGZVDURV
    TWAVRWGLWFWEVFVEUKUIURWAWCWGVDLWEWDVEVDULUIUMUBUNUO $.
    $( [21-Feb-2008] $)

  $( Express the difference of the squares of two numbers as a polynomial in
     the difference of the numbers. $)
  subsq2 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A ^ 2 ) - ( B ^ 2 ) ) =
    ( ( ( A - B ) ^ 2 ) + ( ( 2 x. B ) x. ( A - B ) ) ) ) $=
    ( cc wcel wa caddc co cmin cmul c2 cexp wceq 2cn mulcl mpan adantl
    subadd23 mpd3an3 2times opreq1d pncan anidms eqtrd opreq2d subcl adddir
    syl111anc eqtr3d subsq sqval syl 3eqtr4d ) ACDZBCDZEZABFGZABHGZIGZUQUQIGZJB
    IGZUQIGZFGZAJKGBJKGHGUQJKGZVAFGUOUQUTFGZUQIGZURVBUOVDUPUQIUOVDAUTBHGZFGZUPU
    MUNUTCDZVDVGLUNVHUMJCDUNVHMJBNOPZABUTQRUOVFBAFUNVFBLUMUNVFBBFGZBHGZBUNUTVJB
    HBSTUNVKBLBBUAUBUCPUDUCTUOUQCDZVHVLVEVBLABUEZVIVMUQUTUQUFUGUHABUIUOVCUSVAFU
    OVLVCUSLVMUQUJUKTUL $.
    $( [21-Feb-2008] $)

  ${
    binom2.1 $e |- A e. CC $.
    binom2.2 $e |- B e. CC $.
    $( The square of a binomial. $)
    binom2i $p |- ( ( A + B ) ^ 2 ) = ( ( ( A ^ 2 ) + ( 2 x.
                ( A x. B ) ) ) + ( B ^ 2 ) ) $=
      ( caddc co cmul c2 cexp addcli adddii adddiri mulcomi opreq2i eqtri
      opreq12i mulcli addassi opreq1i eqtr3i 3eqtri sqvali 2timesi 3eqtr4i ) AB
      EFZUEGFZAAGFZABGFZUHEFZEFZBBGFZEFZUEHIFAHIFZHUHGFZEFZBHIFZEFUFUEAGFZUEBGF
      ZEFUGUHEFZUHUKEFZEFZULUEABABCDJZCDKUQUSURUTEUQUGBAGFZEFUSABACDCLVCUHUGEBA
      DCMNOABBCDDLPUSUHEFZUKEFVAULUSUHUKUGUHAACCQZABCDQZJVFBBDDQRVDUJUKEUGUHUHV
      EVFVFRSTUAUEVBUBUOUJUPUKEUMUGUNUIEACUBUHVFUCPBDUBPUD $.
      $( [11-Aug-1999] $)

    $( Product of sum and difference. $)
    binom2aiOLD $p |- ( ( A + B ) x. ( A - B ) ) = ( ( A ^ 2 ) - ( B ^ 2 ) ) $=
      ( caddc co cmin cmul c2 cexp subcli adddiri subdii sqvali opreq1i eqtr4i
      mulcomi opreq12i sqcli mulcli addsubassi addcomi pncan3i eqtri eqtr3i
      3eqtri ) ABEFABGFZHFAUGHFZBUGHFZEFAIJFZABHFZGFZUKBIJFZGFZEFZUJUMGFZABUGCD
      ABCDKLUHULUIUNEUHAAHFZUKGFULAABCCDMUJUQUKGACNOPUIBAHFZBBHFZGFUNBABDCDMUKU
      RUMUSGABCDQBDNRPRULUKEFZUMGFUOUPULUKUMUJUKACSZABCDTZKZVBBDSUAUTUJUMGUTUKU
      LEFUJULUKVCVBUBUKUJVBVAUCUDOUEUF $.
      $( [7-Feb-2005] $)

    $( Factor the difference of two squares. $)
    subsqi $p |- ( ( A ^ 2 ) - ( B ^ 2 ) ) = ( ( A + B ) x. ( A - B ) ) $=
      ( cc wcel c2 cexp co cmin caddc cmul wceq subsq mp2an ) AEFBEFAGHIBGHIJIA
      BKIABJILIMCDABNO $.
      $( [24-Apr-2008] $) $( [7-Feb-2005] $)

    $( The squares of two complex numbers are equal iff one number equals the
       other or its negative.  Lemma 15-4.7 of [Gleason] p. 311 and its
       converse. $)
    sqeqori $p |- ( ( A ^ 2 ) = ( B ^ 2 ) <-> ( A = B \/ A = -u B ) ) $=
      ( c2 cexp co wceq caddc cc0 cmin wo cneg cmul binom2aiOLD eqeq1i addcli
      subcli mul0ori sqcli subeq0i 3bitr3ri orcom subnegi negcli bitr3i
      orbi12i 3bitri ) AEFGZBEFGZHZABIGZJHZABKGZJHZLZUOUMLABHZABMZHZLULUNNGZJHU
      IUJKGZJHUPUKUTVAJABCDOPULUNABCDQABCDRSUIUJACTBDTUAUBUMUOUCUOUQUMUSABCDUAU
      MAURKGZJHUSVBULJABCDUDPAURCBDUEUAUFUGUH $.
      $( [15-Jan-2006] $)

    $( The two solutions to the difference of squares set equal to zero. $)
    subsq0i $p |- ( ( ( A ^ 2 ) - ( B ^ 2 ) ) = 0
                  <-> ( A = B \/ A = -u B ) ) $=
      ( c2 cexp co cmin cc0 wceq cneg wo sqcli subeq0i sqeqori bitri ) AEFGZBEF
      GZHGIJQRJABJABKJLQRACMBDMNABCDOP $.
      $( [4-Aug-2007] $) $( [25-Apr-2006] $)
  $}

  $( The squares of two complex numbers are equal iff one number equals the
     other or its negative.  Lemma 15-4.7 of [Gleason] p. 311 and its
     converse.  (Contributed by Paul Chapman, 15-Mar-2008.) $)
  sqeqor $p |- ( ( A e. CC /\ B e. CC ) ->
                  ( ( A ^ 2 ) = ( B ^ 2 ) <-> ( A = B \/ A = -u B ) ) ) $=
    ( cc wcel c2 cexp co wceq cneg wo wb cc0 cif opreq1 eqeq1d eqeq1 orbi12d
    bibi12d eqeq2d eqeq2 negeq 0cn elimel sqeqori dedth2h ) ACDZBCDZAEFGZBEFGZH
    ZABHZABIZHZJZKUFALMZEFGZUIHZUOBHZUOULHZJZKUPUGBLMZEFGZHZUOVAHZUOVAIZHZJZKAB
    LLAUOHZUJUQUNUTVHUHUPUIAUOEFNOVHUKURUMUSAUOBPAUOULPQRBVAHZUQVCUTVGVIUIVBUPB
    VAEFNSVIURVDUSVFBVAUOTVIULVEUOBVAUASQRUOVAALCUBUCBLCUBUCUDUE $.
    $( [15-Mar-2008] $)

  $( The square of a binomial.  (Contributed by FL, 10-Dec-2006.) $)
  binom2 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + B ) ^ 2 ) =
     ( ( ( A ^ 2 ) + ( 2 x. ( A x. B ) ) ) + ( B ^ 2 ) ) ) $=
    ( cc wcel caddc co c2 cexp cmul wceq cc0 cif opreq1 opreq1d opreq2d
    opreq12d eqeq12d opreq2 0cn elimel binom2i dedth2h ) ACDZBCDZABEFZGHFZAGHFZ
    GABIFZIFZEFZBGHFZEFZJUCAKLZBEFZGHFZUMGHFZGUMBIFZIFZEFZUKEFZJUMUDBKLZEFZGHFZ
    UPGUMVAIFZIFZEFZVAGHFZEFZJABKKAUMJZUFUOULUTVIUEUNGHAUMBEMNVIUJUSUKEVIUGUPUI
    UREAUMGHMVIUHUQGIAUMBIMOPNQBVAJZUOVCUTVHVJUNVBGHBVAUMERNVJUSVFUKVGEVJURVEUP
    EVJUQVDGIBVAUMIROOBVAGHMPQUMVAAKCSTBKCSTUAUB $.
    $( [27-Nov-2006] $)

  $( If a complex number equals its square, it must be 0 or 1. $)
  sq01 $p |- ( A e. CC -> ( ( A ^ 2 ) = A <-> ( A = 0 \/ A = 1 ) ) ) $=
    ( cc wcel c2 cexp co wceq cc0 c1 wo wa wn wne df-ne wi cmul wb sqval
    mulid1 eqcomd eqeq12d adantr ax1cn mulcan mp3an2 anabss5 bitrd biimpd ex
    com23 imp syl5bir orrd sq0 opreq1 id 3eqtr4a sq1 jaoi impbid1 ) ABCZADEFZAG
    ZAHGZAIGZJZVAVCVFVAVCKZVDVEVDLAHMZVGVEAHNVAVCVHVEOVAVHVCVEVAVHVCVEOVAVHKZVC
    VEVIVCAAPFZAIPFZGZVEVAVCVLQVHVAVBVJAVKARVAVKAASTUAUBVAVHVLVEQZVAIBCVIVMUCAI
    AUDUEUFUGUHUIUJUKULUMUIVDVCVEVDHDEFHVBAUNAHDEUOVDUPUQVEIDEFIVBAURAIDEUOVEUP
    UQUSUT $.
    $( [6-Jun-2006] $)

  ${
    $d j k A $.  $d j k N $.
    $( Bernoulli's inequality, due to Johan Bernoulli (1667-1748). $)
    bernneq $p |- ( ( A e. RR /\ N e. NN0 /\ -u 1 <_ A ) ->
                 ( 1 + ( A x. N ) ) <_ ( ( 1 + A ) ^ N ) ) $=
      ( vj vk cr wcel cn0 c1 cneg cle wbr cmul co caddc cexp wi wa cv cc0 wceq
      opreq2 opreq2d breq12d imbi2d cc recn mul01 ax1cn addid1i syl6eq 1re
      leidi addcl mpan exp0 syl syl5breqr eqbrtrd adantr nn0re remulcl sylan2
      readdcl sylancr simpl syl11anc reexpcl sylan axmulrcl anidms msqge0 jca
      nn0ge0 mulge0 syl2an nn0cn adantl mul32 syl111anc breqtrd wb addge01
      mpbid mulcl addass muladd11 eqtr4d renegcli leadd2 mp3an13 negidi breq1i
      syl6bb biimpa ad2ant2r simprr lemul1a syl31anc letrd adddi mp3an3 mulid1
      eqtrd mp3an1 expp1 3brtr4d exp43 com12 imp3a a2d nn0ind exp3a 3imp ) AEFZ
      BGFZHIZAJKZHABLMZNMZHANMZBOMZJKZXOXNXQYBPXOXNXQYBXNXQQZHACRZLMZNMZXTYDOMZ
      JKZPYCHASLMZNMZXTSOMZJKZPYCHADRZLMZNMZXTYMOMZJKZPYCHAYMHNMZLMZNMZXTYROMZJ
      KZPYCYBPCDBYDSTZYHYLYCUUCYFYJYGYKJUUCYEYIHNYDSALUAUBYDSXTOUAUCUDYDYMTZYHY
      QYCUUDYFYOYGYPJUUDYEYNHNYDYMALUAUBYDYMXTOUAUCUDYDYRTZYHUUBYCUUEYFYTYGUUAJ
      UUEYEYSHNYDYRALUAUBYDYRXTOUAUCUDYDBTZYHYBYCUUFYFXSYGYAJUUFYEXRHNYDBALUAUB
      YDBXTOUAUCUDXNYLXQXNAUEFZYLAUFZUUGYJHYKJUUGYJHSNMHUUGYISHNAUGUBHUHUIUJUUG
      HHYKJHUKULUUGXTUEFZYKHTHUEFZUUGUUIUHHAUMZUNXTUOUPUQURUPUSYMGFZYCYQUUBUULX
      NXQYQUUBPZXNUULXQUUMPXNUULXQYQUUBXNUULQZXQYQQZQZYOANMZYPXTLMZYTUUAJUUPUUQ
      YOXTLMZUURUUNUUQEFZUUOUUNYOEFZXNUUTUUNHEFZYNEFZUVAUKUULXNYMEFZUVCYMUTZAYM
      VAZVBHYNVCVDZXNUULVEYOAVCVFZUSUUNUUSEFZUUOUUNUVAXTEFZUVIUVGXNUVJUULUVBXNU
      VJUKHAVCUNZUSZYOXTVAVFUSUUNUUREFZUUOUUNYPEFZUVJUVMXNUVJUULUVNUVKXTYMVGVHZ
      UVLYPXTVAVFUSUUNUUQUUSJKUUOUUNUUQUUQYNALMZNMZUUSJUUNSUVPJKZUUQUVQJKZUUNSA
      ALMZYMLMZUVPJXNUVTEFZSUVTJKZQUVDSYMJKZQSUWAJKUULXNUWBUWCXNUWBAAVIVJAVKVLU
      ULUVDUWDUVEYMVMVLUVTYMVNVOUUNUUGUUGYMUEFZUWAUVPTXNUUGUULUUHUSZUWFUULUWEXN
      YMVPZVQAAYMVRVSVTUUNUUTUVPEFZUVRUVSWAUVHUULXNUVDUWHUVEXNUVDQUVCXNUWHUVFXN
      UVDVEYNAVAVFVBUUQUVPWBVFWCXNUUGUWEUVQUUSTUULUUHUWGUUGUWEQZUVQYOAUVPNMNMZU
      USUWIYOUEFZUUGUVPUEFZUVQUWJTUWIUUJYNUEFZUWKUHAYMWDZHYNUMVDUUGUWEVEZUWIUWM
      UUGUWLUWNUWOYNAWDVFYOAUVPWEVSUWIUWMUUGUUSUWJTUWNUWOYNAWFVFWGVOVTUSUUPUVAU
      VNUVJSXTJKZQYQUUSUURJKUUNUVAUUOUVGUSUUNUVNUUOUVOUSUUPUVJUWPUUNUVJUUOUVLUS
      XNXQUWPUULYQXNXQUWPXNXQHXPNMZXTJKZUWPXPEFXNUVBXQUWRWAHUKWHUKXPAHWIWJUWQSX
      TJHUHWKWLWMWNWOVLUUNXQYQWPYOYPXTWQWRWSUUNYTUUQTZUUOXNUUGUWEUWSUULUUHUWGUW
      IYTHYNANMZNMZUUQUWIYSUWTHNUWIYSYNAHLMZNMZUWTUUGUWEUUJYSUXCTUHAYMHWTXAUWIU
      XBAYNNUUGUXBATUWEAXBUSUBXCUBUWIUWMUUGUUQUXATZUWNUWOUUJUWMUUGUXDUHHYNAWEXD
      VFWGVOUSUUNUUAUURTZUUOXNUUIUULUXEXNUUJUUGUUIUHUUHUUKVDXTYMXEVHUSXFXGXHXIX
      JXKXLXHXM $.
      $( [25-Jul-2009] $) $( [21-Feb-2005] $)

    $( Bernoulli's inequality, due to Johan Bernoulli (1667-1748). $)
    bernneqOLD $p |- ( ( A e. RR /\ N e. NN0 /\ -u 1 <_ A ) ->
                 ( 1 + ( A x. N ) ) <_ ( ( 1 + A ) ^ N ) ) $=
      ( vj vk cr wcel cn0 c1 cneg cle wbr cmul co caddc cexp wi wa cv cc0 wceq
      opreq2 opreq2d breq12d imbi2d cc recn mul01 ax1cn addid1i syl6eq 1re
      leidi addcl mpan exp0 syl syl5breqr eqbrtrd adantr nn0re remulcl sylan2
      readdcl sylancr simpl syl11anc reexpcl sylan anidms anim12i msqge0
      nn0ge0 mulge0OLD nn0cn adantl mul32 syl111anc breqtrd wb addge01 mpbid
      mulcl addass muladd11 eqtr4d syl2an renegcli leadd2 mp3an13 negidi
      breq1i syl6bb biimpa ad2ant2r jca simprr lemul1a syl31anc letrd adddi
      mp3an3 mulid1 eqtrd mp3an1 expp1 3brtr4d exp43 com12 imp3a a2d nn0ind
      exp3a 3imp ) AEFZBGFZHIZAJKZHABLMZNMZHANMZBOMZJKZXOXNXQYBPXOXNXQYBXNXQQZH
      ACRZLMZNMZXTYDOMZJKZPYCHASLMZNMZXTSOMZJKZPYCHADRZLMZNMZXTYMOMZJKZPYCHAYMH
      NMZLMZNMZXTYROMZJKZPYCYBPCDBYDSTZYHYLYCUUCYFYJYGYKJUUCYEYIHNYDSALUAUBYDSX
      TOUAUCUDYDYMTZYHYQYCUUDYFYOYGYPJUUDYEYNHNYDYMALUAUBYDYMXTOUAUCUDYDYRTZYHU
      UBYCUUEYFYTYGUUAJUUEYEYSHNYDYRALUAUBYDYRXTOUAUCUDYDBTZYHYBYCUUFYFXSYGYAJU
      UFYEXRHNYDBALUAUBYDBXTOUAUCUDXNYLXQXNAUEFZYLAUFZUUGYJHYKJUUGYJHSNMHUUGYIS
      HNAUGUBHUHUIUJUUGHHYKJHUKULUUGXTUEFZYKHTHUEFZUUGUUIUHHAUMZUNXTUOUPUQURUPU
      SYMGFZYCYQUUBUULXNXQYQUUBPZXNUULXQUUMPXNUULXQYQUUBXNUULQZXQYQQZQZYOANMZYP
      XTLMZYTUUAJUUPUUQYOXTLMZUURUUNUUQEFZUUOUUNYOEFZXNUUTUUNHEFZYNEFZUVAUKUULX
      NYMEFZUVCYMUTZAYMVAZVBHYNVCVDZXNUULVEYOAVCVFZUSUUNUUSEFZUUOUUNUVAXTEFZUVI
      UVGXNUVJUULUVBXNUVJUKHAVCUNZUSZYOXTVAVFUSUUNUUREFZUUOUUNYPEFZUVJUVMXNUVJU
      ULUVNUVKXTYMVGVHZUVLYPXTVAVFUSUUNUUQUUSJKUUOUUNUUQUUQYNALMZNMZUUSJUUNSUVP
      JKZUUQUVQJKZUUNSAALMZYMLMZUVPJUUNUVTEFZUVDQSUVTJKZSYMJKZQSUWAJKXNUWBUULUV
      DXNUWBAAVAVIUVEVJXNUWCUULUWDAVKYMVLVJUVTYMVMVFUUNUUGUUGYMUEFZUWAUVPTXNUUG
      UULUUHUSZUWFUULUWEXNYMVNZVOAAYMVPVQVRUUNUUTUVPEFZUVRUVSVSUVHUULXNUVDUWHUV
      EXNUVDQUVCXNUWHUVFXNUVDVEYNAVAVFVBUUQUVPVTVFWAXNUUGUWEUVQUUSTUULUUHUWGUUG
      UWEQZUVQYOAUVPNMNMZUUSUWIYOUEFZUUGUVPUEFZUVQUWJTUWIUUJYNUEFZUWKUHAYMWBZHY
      NUMVDUUGUWEVEZUWIUWMUUGUWLUWNUWOYNAWBVFYOAUVPWCVQUWIUWMUUGUUSUWJTUWNUWOYN
      AWDVFWEWFVRUSUUPUVAUVNUVJSXTJKZQYQUUSUURJKUUNUVAUUOUVGUSUUNUVNUUOUVOUSUUP
      UVJUWPUUNUVJUUOUVLUSXNXQUWPUULYQXNXQUWPXNXQHXPNMZXTJKZUWPXPEFXNUVBXQUWRVS
      HUKWGUKXPAHWHWIUWQSXTJHUHWJWKWLWMWNWOUUNXQYQWPYOYPXTWQWRWSUUNYTUUQTZUUOXN
      UUGUWEUWSUULUUHUWGUWIYTHYNANMZNMZUUQUWIYSUWTHNUWIYSYNAHLMZNMZUWTUUGUWEUUJ
      YSUXCTUHAYMHWTXAUWIUXBAYNNUUGUXBATUWEAXBUSUBXCUBUWIUWMUUGUUQUXATZUWNUWOUU
      JUWMUUGUXDUHHYNAWCXDVFWEWFUSUUNUUAUURTZUUOXNUUIUULUXEXNUUJUUGUUIUHUUHUUKV
      DXTYMXEVHUSXFXGXHXIXJXKXLXHXM $.
      $( [11-Oct-2006] $) $( [21-Feb-2005] $)
  $}

  $( Variation of Bernoulli's inequality ~ bernneq . $)
  bernneq2 $p |- ( ( A e. RR /\ N e. NN0 /\ 0 <_ A ) ->
               ( ( ( A - 1 ) x. N ) + 1 ) <_ ( A ^ N ) ) $=
    ( cr wcel cn0 cc0 cle wbr w3a c1 cmin co cmul caddc cexp cneg peano2rem
    3ad2ant1 simp2 wa df-neg wb 0re 1re lesub1 mp3an13 biimpa syl5eqbr 3adant2
    bernneq syl111anc wceq cc ax1cn recnd nn0cn mulcl syl2an addcom sylancr
    3adant3 recn pncan3 opreq1d 3brtr3d ) ACDZBEDZFAGHZIZJAJKLZBMLZNLZJVJNLZBOL
    ZVKJNLZABOLZGVIVJCDZVGJPZVJGHZVLVNGHVFVGVQVHAQZRVFVGVHSVFVHVSVGVFVHTVRFJKLZ
    VJGJUAVFVHWAVJGHZFCDVFJCDVHWBUBUCUDFAJUEUFUGUHUIVJBUJUKVFVGVLVOULZVHVFVGTJU
    MDZVKUMDZWCUNVFVJUMDBUMDWEVGVFVJVTUOBUPVJBUQURJVKUSUTVAVFVGVNVPULVHVFVMABOV
    FWDAUMDVMAULUNAVBJAVCUTVDRVE $.
    $( [18-Oct-2007] $)

  ${
    $d j k A $.  $d j k B $.
    $( MAY BE REVISED - note that min w/ ad2antlr, ad2antrl make compressed
       proof longer $)
    $( Exponentiation with a mantissa greater than 1 has no upper bound. $)
    expnbnd $p |- ( ( A e. RR /\ B e. RR /\ 1 < B ) ->
          E. k e. NN A < ( B ^ k ) ) $=
      ( cr wcel c1 clt wbr w3a cv cexp co cn wrex wa 1nn wi 1re axlttrn mp3an2
      exp4b com34 3imp1 wceq cc recn exp1 syl 3ad2ant2 adantr breqtrrd opreq2
      breq2d rcla4ev sylancr cle cmin cdiv cfl cfv caddc cn0 cc0 wne peano2rem
      adantl wb posdif mpan biimpa gt0ne0 sylan syldan redivcl syl111anc
      adantll subge0 mpan2 biimparc jca divge0 syl2an flge0nn0 syl11anc
      nn0p1nn cmul simplr peano2nn0 nn0re remulcl peano2re simprl reexpcl
      flltp1 ltdivmulOLD syl31anc mpbid ltsubadd lt01 0re mp3an12 mpani ltle
      syld imp bernneq2 ltletrd exp43 com4l simp1 a1i pm2.61ltlei ) ADEZBDEZFBG
      HZIZABCJZKLZGHZCMNZAFXPAFGHZOZFMEABFKLZGHZXTPYBABYCGXMXNXOYAABGHZXMXNYAXO
      YEXMXNYAXOYEXMFDEZXNYAXOOYEQRAFBSTUAUBUCXPYCBUDZYAXNXMYGXOXNBUEEYGBUFBUGU
      HUIUJUKXSYDCFMXQFUDXRYCAGXQFBKULUMUNUOXMXNXOFAUPHZXTYHXMXNXOXTYHXMXNXOXTY
      HXMOZXNXOOZOZAFUQLZBFUQLZURLZUSUTZFVALZMEZABYPKLZGHZXTYKYOVBEZYQYKYNDEZVC
      YNUPHZYTXMYJUUAYHXMYJOYLDEZYMDEZYMVCVDZUUAXMUUCYJAVEZUJYJUUDXMXNUUDXOBVEZ
      UJZVFYJUUEXMXNXOVCYMGHZUUEXNXOUUIYFXNXOUUIVGRFBVHVIVJZXNUUDUUIUUEUUGYMVKV
      LVMVFYLYMVNVOVPZYIUUCVCYLUPHZOUUDUUIOUUBYJYIUUCUULXMUUCYHUUFVFZXMUULYHXMY
      FUULYHVGRAFVQVRVSVTYJUUDUUIUUHUUJVTYLYMWAWBYNWCWDZYOWEUHYKAYMYPWFLZFVALZY
      RYHXMYJWGZYKUUODEZUUPDEYKUUDYPDEZUURYJUUDYIUUHVFZYKYPVBEZUUSYKYTUVAUUNYOW
      HUHZYPWIUHZYMYPWJWDZUUOWKUHYKXNUVAYRDEYIXNXOWLZUVBBYPWMWDYKYLUUOGHZAUUPGH
      ZYKYNYPGHZUVFYKUUAUVHUUKYNWNUHYKUUCUUDUUSUUIUVHUVFVGYIUUCYJUUMUJUUTUVCYJU
      UIYIUUJVFYLYMYPWOWPWQYKXMUURUVFUVGVGZUUQUVDXMYFUURUVIRAFUUOWRTWDWQYKXNUVA
      VCBUPHZUUPYRUPHUVEUVBYJUVJYIXNXOUVJXNXOVCBGHZUVJXNVCFGHZXOUVKWSVCDEZYFXNU
      VLXOOUVKQWTRVCFBSXAXBUVMXNUVKUVJQWTVCBXCVIXDXEVFBYPXFVOXGXSYSCYPMXQYPUDXR
      YRAGXQYPBKULUMUNWDXHXIUCXMXNXOXJYFXPRXKXL $.
      $( [20-Oct-2007] $)

    $( The reciprocal of exponentiation with a mantissa greater than 1 has no
       lower bound. $)
    expnlbnd $p |- ( ( A e. RR+ /\ B e. RR /\ 1 < B ) ->
          E. k e. NN ( 1 / ( B ^ k ) ) < A ) $=
      ( crp wcel cr c1 clt wbr w3a cdiv co cv cexp cn wrex cc0 wne rpre rpne0
      rereccl syl11anc expnbnd syl3an1 wa wb elrp biimpi 3ad2ant1 adantr cn0
      nnnn0 reexpcl sylan2 adantlr simpll adantl lt01 wi 0re 1re lttr mp3an12
      mpani imp expgt0 syl111anc jca 3adantl1 ltrec1 rexbidva mpbid ) ADEZBFEZG
      BHIZJZGAKLZBCMZNLZHIZCOPZGVSKLAHIZCOPVMVQFEZVNVOWAVMAFEZAQRWCASATAUAUBVQB
      CUCUDVPVTWBCOVPVROEZUEWDQAHIUEZVSFEZQVSHIZUEZVTWBUFVPWFWEVMVNWFVOVMWFAUGU
      HUIUJVNVOWEWIVMVNVOUEZWEUEZWGWHVNWEWGVOWEVNVRUKEZWGVRULZBVRUMUNUOWKVNWLQB
      HIZWHVNVOWEUPWEWLWJWMUQWJWNWEVNVOWNVNQGHIZVOWNURQFEGFEVNWOVOUEWNUSUTVAQGB
      VBVCVDVEUJBVRVFVGVHVIAVSVJUBVKVL $.
      $( [18-Jul-2008] $)

    $( The reciprocal of exponentiation with a mantissa greater than 1 has no
       lower bound. $)
    expnlbnd2 $p |- ( ( A e. RR+ /\ B e. RR /\ 1 < B ) ->
          E. j e. NN A. k e. NN ( j <_ k -> ( 1 / ( B ^ k ) ) < A ) ) $=
      ( crp wcel cr c1 clt wbr w3a cv cexp co cdiv cn wrex cle wi wral
      expnlbnd wa cn0 simpll ad2antrr nnnn0 adantl ad2antlr 1re ltle mpan imp
      anim1i expwordi syl31anc ex cc0 wb reexpcl sylan2 adantlr adantr lt01
      0re lttr mp3an12 mpani expgt0 syl111anc lerec syl22anc sylibd 3adantl1
      imp31 wne cc recn gt0ne0 syldan expne0i rereccl syl11anc rpre 3ad2ant1
      lelttr mpand com23 r19.21adva reximdva mpd ) AEFZBGFZHBIJZKZHBCLZMNZONZAI
      JZCPQWODLZRJZHBWSMNZONZAIJZSZDPTZCPQABCUAWNWRXECPWNWOPFZUBZWRXDDPXGWSPFZU
      BZWTWRXCXIWTWRXCSXIWTUBXBWQRJZWRXCXGXHWTXJWLWMXFXHWTXJSZSWKWLWMUBZXFUBZXH
      XKXMXHUBZWTWPXARJZXJXNWTXOXNWTUBWLWOUCFZWSUCFZHBRJZWTUBXOXMWLXHWTWLWMXFUD
      ZUEXMXPXHWTXFXPXLWOUFZUGZUEXHXQXMWTWSUFZUHXNXRWTXLXRXFXHWLWMXRHGFZWLWMXRS
      UIHBUJUKULUEUMBWOWSUNUOUPXNWPGFZUQWPIJZXAGFZUQXAIJZXOXJURXMYDXHWLXFYDWMXF
      WLXPYDXTBWOUSUTVAZVBXMYEXHXMWLXPUQBIJZYEXSYAXLYIXFWLWMYIWLUQHIJZWMYIVCUQG
      FYCWLYJWMUBYISVDUIUQHBVEVFVGULZVBBWOVHVIVBXLXHYFXFWLXHYFWMXHWLXQYFYBBWSUS
      UTVAZVAXLXHYGXFXLXHUBZWLXQYIYGWLWMXHUDXHXQXLYBUGZXLYIXHYKVBBWSVHVIVAWPXAV
      JVKVLUPVMVNXIXJWRUBXCSZWTXIXBGFZWQGFZAGFZYOWNXHYPXFWLWMXHYPWKYMYFXAUQVOZY
      PYLYMBVPFZBUQVOZXQYSWLYTWMXHBVQZUEXLUUAXHWLWMYIUUAYKBVRVSZVBYNBWSVTVIXAWA
      WBVMVAXGYQXHWLWMXFYQWKXMYDWPUQVOZYQYHXMYTUUAXPUUDWLYTWMXFUUBUEXLUUAXFUUCV
      BYABWOVTVIWPWAWBVMVBWNYRXFXHWKWLYRWMAWCWDUEXBWQAWEVIVBWFUPWGWHWIWJ $.
      $( [18-Jul-2008] $)
  $}

  $( Two ways to express the ` K ` th digit in the decimal (when base
     ` B = 10 ` ) expansion of a number ` A ` . ` K = 1 ` corresponds to the
     first digit after the decimal point. $)
  digit2 $p |- ( ( A e. RR /\ B e. NN /\ K e. NN )
    -> ( ( |_ ` ( ( B ^ K ) x. A ) ) mod B ) = ( ( |_ ` ( ( B ^ K ) x.
      A ) ) - ( B x. ( |_ ` ( ( B ^ ( K - 1 ) ) x. A ) ) ) ) ) $=
    ( cr wcel cn w3a cexp co cmul cfl cfv cmo cdiv cmin c1 crp wceq wa cn0
    nnre nnnn0 reexpcl syl2an remulcl sylan 3impa 3comr reflcl syl nnrp
    3ad2ant2 modval syl11anc simp2 fldiv cc cc0 wne nncn expcl 3adant1 recn
    3ad2ant1 nnne0 jca div23 syl111anc expm1 3expa opreq1d eqtr4d fveq2d eqtrd
    opreq2d ) ADEZBFEZCFEZGZBCHIZAJIZKLZBMIZWBBWBBNIKLZJIZOIZWBBBCPOIHIZAJIZKLZ
    JIZOIVSWBDEZBQEZWCWFRVSWADEZWKVQVRVPWMVQVRVPWMVQVRSVTDEZVPWMVQBDECTEZWNVRBU
    ACUBZBCUCUDVTAUEUFUGUHZWAUIUJVQVPWLVRBUKULWBBUMUNVSWEWJWBOVSWDWIBJVSWDWABNI
    ZKLZWIVSWMVQWDWSRWQVPVQVRUOWABUPUNVSWRWHKVSWRVTBNIZAJIZWHVSVTUQEZAUQEZBUQEZ
    BURUSZSZWRXARVQVRXBVPVQXDWOXBVRBUTZWPBCVAUDVBVPVQXCVRAVCVDVQVPXFVRVQXDXEXGB
    VEVFZULVTABVGVHVSWGWTAJVQVRWGWTRZVPVQXFVRXIXHXDXEVRXIBCVIVJUFVBVKVLVMVNVOVO
    VN $.
    $( [25-Dec-2008] $)

  $( Two ways to express the ` K ` th digit in the decimal expansion of a
     number ` A ` (when base ` B = 10 ` ). ` K = 1 ` corresponds to the first
     digit after the decimal point. $)
  digit1 $p |- ( ( A e. RR /\ B e. NN /\ K e. NN ) -> ( ( |_ ` ( ( B ^ K ) x.
        A ) ) mod B ) = ( ( ( |_ ` ( ( B ^ K ) x. A ) ) mod ( B ^ K ) )
        - ( ( B x. ( |_ ` ( ( B ^ ( K - 1 ) ) x. A ) ) ) mod ( B ^ K ) ) ) ) $=
    ( cn wcel cr cexp co cmul cfl cfv cmo c1 cmin wceq wa digit2 3coml 3expa
    opreq1d crp cc0 cle wbr clt cn0 nnre nnnn0 reexpcl syl2an remulcl sylan
    reflcl syl nnrp ad2antrr modcl syl11anc nnexpcl sylan2 adantr modge0 modlt
    cc nncn exp1 1nn0 a1i adantl nnge1 anim12i expwordi syl31anc eqbrtrrd
    ltletrd modid syl22anc simpll nnm1nn0 modmulnn syl111anc expm1t expcl
    simpl mulcom eqtrd opreq2d recn mulass fveq2d opreq12d 3brtr4d wb
    modsubdir mpbid 3eqtr3d 3impa 3comr ) BDEZCDEZAFEZBCGHZAIHZJKZBLHZXDXBLHZBB
    CMNHZGHZAIHZJKZIHZXBLHZNHZOZWSWTXAXNWSWTPZXAPZXEXBLHZXDXKNHZXBLHZXEXMXPXEXR
    XBLWSWTXAXEXROZXAWSWTXTABCQRSTXPXEFEZXBUAEZUBXEUCUDZXEXBUEUDXQXEOXPXDFEZBUA
    EZYAXPXCFEZYDXOXBFEZXAYFWSBFEZCUFEZYGWTBUGZCUHZBCUIUJZXBAUKULXCUMUNZWSYEWTX
    ABUOUPZXDBUQURZXOYBXAXOXBDEZYBWTWSYIYPYKBCUSUTXBUOUNVAZXPYDYEYCYMYNXDBVBURX
    PXEBXBYOWSYHWTXAYJUPZXOYGXAYLVAXPYDYEXEBUEUDYMYNXDBVCURXOBXBUCUDXAXOBMGHZBX
    BUCWSYSBOZWTWSBVDEZYTBVEZBVFUNVAXOYHMUFEZYIMBUCUDZMCUCUDZPYSXBUCUDWSYHWTYJV
    AUUCXOVGVHWTYIWSYKVIWSUUDWTUUEBVJCVJVKBMCVLVMVNVAVOXEXBVPVQXPXLXFUCUDZXSXMO
    ZXPXKBXHIHZLHZBXIIHZJKZUUHLHZXLXFUCXPWSXIFEZXHDEZUUIUULUCUDWSWTXAVRXOXHFEZX
    AUUMWSYHXGUFEZUUOWTYJCVSZBXGUIUJXHAUKULZXOUUNXAWTWSUUPUUNUUQBXGUSUTVAXIXHBV
    TWAXPXBUUHXKLXOXBUUHOZXAWSUUAWTUUSUUBUUAWTPZXBXHBIHZUUHBCWBUUTXHVDEZUUAUVAU
    UHOWTUUAUUPUVBUUQBXGWCZUTUUAWTWDXHBWEURWFULVAZWGXPXDUUKXBUUHLXPXCUUJJXPXCUU
    HAIHZUUJXPXBUUHAIUVDTXPUUAUVBAVDEZUVEUUJOWSUUAWTXAUUBUPXOUVBXAWSUUAUUPUVBWT
    UUBUUQUVCUJVAXAUVFXOAWHVIBXHAWIWAWFWJUVDWKWLXPYDXKFEZYBUUFUUGWMYMXPYHXJFEZU
    VGYRXPUUMUVHUURXIUMUNBXJUKURYQXDXKXBWNWAWOWPWQWR $.
    $( [3-Jan-2009] $)

$(
  @( Two ways to express the ` K ` th digit in the decimal expansion of a
     number ` A ` (when base ` B = 10 ` ). ` K = 1 ` corresponds to the first
     digit after the decimal point. @)
  digit3 @p |- ( ( A e. RR /\ B e. NN /\ K e. NN ) -> ( ( B ^ K ) x. ( ( A mod
 ( 1 / ( B ^ K ) ) ) - ( A mod ( 1 / ( B ^ ( K - 1 ) ) ) ) ) ) = ( ( |_ ` ( ( B
   ^ K ) x. A ) ) - ( B x. ( |_ ` ( ( B ^ ( K - 1 ) ) x. A ) ) ) ) ) @=
    ( cr wcel cn w3a cexp co cmul cfl cfv c1 cmin cdiv cmo opreq12d cc wceq
    subdi wa crp cn0 rpexpcl nnrp nnnn0 syl2anOLD rpre syl recnd 3adant1 mulcl
    nnm1nn0 rpreccl rpcn remulcl reexpcl nnre sylan2OLD 3impb reflcl sylancOLD cc0
    wne ax1ne0 redivcl mp3an3 1re sylanclOLD syl3anc eqcomd eqtrd eqid a1i
    nnncan1 recn 3ad2ant1 modval ax1cn pm3.2i divdiv2 mp3an2 expcl sylanOLD
    expne0i 3expa rpcnne0 jca fveq2d opreq2d adantlr div1 ) ADEZBFEZCFEZGZBCHIZ
    AJIKLBBCMNIZHIZAJIKLJINIZWQAMWQOIZPIZAMWSOIZPIZNIZJIZWPWTWQXCAWSJIZKLZJIZXA
    AWQJIZMOIZKLZJIZNIZJIZXFWPWTWQXIJIWQXMJINIZXO????????QWPXOXPWQREZXIREZXMREZ
    XOXPSWPWQXIXMT??????WNWOUAZWQUBEZWQDEZBUBEZCUCEZYAWNWOBCUDBUEZCUFZUGZWQUHUI
    ZUJUKXCREZXHREXRWPXCXHULWNWOYIWMXTXCUBEZYIXTWSUBEZYJYCWRUCEZYKWNWOBWRUDYECU
    MZUGWSUNUIZXCUOUIUKWPXHWPXGDEZXHDEWMWNWOYOWMWSDEZYOXTAWSUPBDEYLYPWNWOBWRUQB
    URYMUGZUSUTXGVAUIUJVBZWPXMXADEZXLDEZXMDEWPXAXLUPWNWOYSWMXTXAUBEZYSXTYAUUAYG
    WQUNUIZXAUHUIUKWPXKDEZYTXJDEZMDEZUUCWPUUDUUEMVCVDZUUCVEXJMVFVGWMWNWOUUDWMYB
    UUDXTAWQUPYHUSUTVHVIXKVAUIVBUJZVJVKVLWPWQWQXNXEJWQWQSWPWQVMVNWPXNAXMNIZAXIN
    IZNIZXEWPUUJXNAREZXSXRUUJXNSWPAXMXIVOWMWNUUKWOAVPZVQUUGYRVJVKWPXEUUJWMWNWOX
    EUUJSWMXTUAZXBUUHXDUUINUUMXBAXAAXAOIZKLZJIZNIZUUHWMUUAXBUUQSXTAXAVRUUBUSUUM
    UUPXMANUUMUUOXLXAJUUMUUNXKKUUKXQWQVCVDZUAZUUNXKSZWMXTUUKMREZUUFUAZUUSUUTUVA
    UUFVSVEVTZAMWQWAWBUULYCYDUUSWNWOYCYDUAXQUURBREZYDXQYCBCWCBUOWDUVDBVCVDZUAZY
    DUURYCUVDUVEYDUURBCWEWFBWGZWDWHYEYFUGUGWIWJWJVLUUMXDAXCAXCOIZKLZJIZNIZUUIWM
    YJXDUVKSXTAXCVRYNUSUUMUVJXIANUUMUVIXHXCJUUMUVHXGKUUMUVHXGMOIZXGUUKWSREZWSVC
    VDZUAZUVHUVLSZWMXTUUKUVBUVOUVPUVCAMWSWAWBUULUVFYLUVOWNWOUVFYLUAUVMUVNUVDYLU
    VMUVEBWRWCWKUVDUVEYLUVNBWRWEWFWHWNYCUVFYEUVGUIYMUGUGUUMXGREZUVLXGSUUKUVMUVQ
    WMXTAWSULUULXTWSYQUJUGXGWLUIVLWIWJWJVLQUTVKVLQVLVK @.
    @( [ ?] @) @( [27-Dec-2008] @)
$)


