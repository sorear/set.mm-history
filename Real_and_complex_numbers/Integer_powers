$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Integer powers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ^ $. $( Exponentiation. $)

  $( Extend class notation to include exponentiation of a complex number to an
     integer power. $)
  cexp $a class ^ $.

  ${
    $d x y z $.
    $( Define exponentiation to nonnegative integer powers.  This definition is
       not meant to be used directly; instead, ~ exp0t and ~ expp1t provide a
       the standard recursive definition.  The up-arrow notation is used by
       Donald Knuth for iterated exponentiation (_Science_ 194, 1235-1242,
       1976) and is convenient for us since we don't have superscripts.  See
       ~ expnnvalt for a description of how the recursive sequence builder is
       used.  10-Jun-2005: The definition was extended to include zero
       exponents, so that ` 0 ^ 0 = 1 ` per the convention of Definition
       10-4.1 of [Gleason] p. 134.  (Based on definition contributed by Raph
       Levien, 15-Oct-2004.) $)
    df-exp $a |- ^ = { <. <. x , y >. , z >. | ( ( x e. CC /\ y e. NN0 ) /\
          z = if ( y = 0 , 1 , ( ( x. seq1 ( NN X. { x } ) ) ` y ) ) ) }  $.

    $(
    Raph Levien's original definition from set.mm of 15-Oct-2004:
    df-exp $a |- ^ = { <. <. x , y >. , z >. | ( ( x e. CC /\ y e. NN0 ) /\
          z = ( ( rec ( { <. w , v >. | v = ( w x. x ) } , 1 ) o.
        `' ( rec ( { <. w , v >. | v = ( w + 1 ) } , 0 ) |` om ) ) ` y ) ) } $.

    Old NN-based version from set.mm of 21-May-2005:
    df-exp $a |- ^ = { <. <. x , y >. , z >. | ( ( x e. CC /\ y e. NN ) /\
          z = ( ( x. seq1 ( NN X. { x } ) ) ` y ) ) }  $.
    dfexp2 $p |- ^ = { <. <. x , y >. , z >. | ( ( x e. CC /\ y e. NN0 ) /\
        z = ( ( x. seq1 ( { <. 0 , 1 >. } u. ( NN X. { x } ) ) ) ` y ) ) }  $=
          z = ( ( x. seq0 ( NN0 X. { if ( y = 0 , 1 , x ) } ) ) ` y ) ) }  $=
    $)


  $}

  ${
    $d x y z A $.  $d x y z N $.
    $( Value of exponentiation to nonnegative integer powers. $)
    expvalt $p |- ( ( A e. CC /\ N e. NN0 ) -> ( A ^ N ) =
                   if ( N = 0 , 1 , ( ( x. seq1 ( NN X. { A } ) ) ` N ) ) ) $=
      ( vx vy vz cc cn0 cv cc0 wceq c1 cmul cn csn cxp cseq1 co cfv cif cexp
      1nn0 elisseti fvex ifex sneq xpeq2 syl opreq2d fveq1d ifeq2d fveq2 eqeq1
      ifbid eqtrd df-exp oprabval2 ) CDEABFGDHZIJZKUQLMCHZNZOZPQZRZSBIJZKBLMANZ
      OZPQZRZSZTURKUQVGRZSZVDKVHKGUAUBBVGUCUDUSAJZURVCVJKVLUQVBVGVLVAVFLPVLUTVE
      JVAVFJUSAUEUTVEMUFUGUHUIUJUQBJZVKURKVHSVIVMURVJVHKUQBVGUKUJVMURVDKVHUQBIU
      LUMUNCDEUOUP $.
      $( [11-Jun-2005] $) $( [20-May-2004] $)

    $( Value of a complex number raised to the 0th power.  Note that under our
       definition, ` 0 ^ 0 = 1 ` , following the convention used by Gleason.
       Part of Definition 10-4.1 of [Gleason] p. 134. $)
    exp0t $p |- ( A e. CC -> ( A ^ 0 ) = 1 ) $=
      ( cc wcel cc0 cexp co wceq c1 cmul cn csn cxp cseq1 cfv cif cn0 0nn0
      expvalt mpan2 eqid iftrue ax-mp syl6eq ) ABCZADEFZDDGZHDIJAKLMFNZOZHUDDPC
      UEUHGQADRSUFUHHGDTUFHUGUAUBUC $.
      $( [11-Jun-2005] $) $( [20-May-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( Value of exponentiation to natural number powers.  ` NN X. { A }  `
       is the constant function with value ` A ` . The ` seq1 ` operation
       produces the sequence ` A ` , ` A x. A ` , ` ( A x. A ) x. A ` ,...
       that we evaluate at index ` B ` . $)
    expnnvalt $p |- ( ( A e. CC /\ B e. NN ) -> ( A ^ B ) =
                   ( ( x. seq1 ( NN X. { A } ) ) ` B ) ) $=
      ( cc wcel cn wa cexp co cc0 wceq c1 cmul csn cxp cseq1 cfv cif cn0
      expvalt nnnn0t sylan2 wn wne nnne0t df-ne sylib iffalse syl adantl eqtrd
      ) ACDZBEDZFABGHZBIJZKBLEAMNOHPZQZUOUKBRDUMUPJULABSBTUAULUPUOJZUKULUNUBZUQ
      ULBIUCURBUDBIUEUFUNKUOUGUHUIUJ $.
      $( [22-Oct-2004] $) $( [19-Oct-2004] $)
  $}

  $( Value of a complex number raised to the first power. $)
  exp1t $p |- ( A e. CC -> ( A ^ 1 ) = A ) $=
    ( cc wcel c1 cexp co cmul cn csn cxp cseq1 cfv wceq 1nn expnnvalt mpan2 wf
    fconstg fvconst syl mulex nnex snex xpex seq11 syl5eq eqtrd ) ABCZADEFZDGHA
    IZJZKFLZAUHDHCZUIULMNADOPUHDUKLZAULUHHUJUKQZUNAMZHABRUOUMUPNHADUKSPTGUKUAHU
    JUBAUCUDUEUFUG $.
    $( [22-Oct-2004] $) $( [20-Oct-2004] $)

  $( Value of a complex number raised to a nonnegative integer power plus one.
     Part of Definition 10-4.1 of [Gleason] p. 134. $)
  expp1t $p |- ( ( A e. CC /\ N e. NN0 ) -> ( A ^ ( N + 1 ) ) =
               ( ( A ^ N ) x. A ) ) $=
    ( cc wcel cn0 c1 caddc co cexp cmul wceq cn cc0 wo wa csn cxp cseq1 cfv
    mulex nnex snex xpex seq1p1 adantl wf fvconst fconstg peano2nn syl2an
    opreq2d eqtrd expnnvalt sylan2 opreq1d 3eqtr4d ex opreq1 opreq2 eqeq12d
    mulid2t exp0t exp1t ax1cn addid2 opreq2i syl5eq 3eqtr4rd syl5bir com12
    jaod elnn0 syl5ib imp ) ACDZBEDZABFGHZIHZABIHZAJHZKZVOBLDZBMKZNWAVPVOWBWAWC
    VOWBWAVOWBOZVQJLAPZQZRHZSZBWGSZAJHZVRVTWDWHWIVQWFSZJHZWJWBWHWLKVOBJWFTLWEUA
    AUBUCUDUEWDWKAWIJLWEWFUFVQLDZWKAKVOWBLAVQWFUGLACUHBUIZUJUKULVOWMVRWHKWBAVQU
    MWNUNWDVSWIAJABUMUOUPUQWCVOWAWCWAAMFGHZIHZAMIHZAJHZKVOWCVRWPVTWRWCVQWOAIBMF
    GURUKWCVSWQAJBMAIUSUOUTVOFAJHAWRWPAVAVOWQFAJAVBUOVOAFIHAWPAVCWOFAIFVDVEVFVG
    VHVIVJVKBVLVMVN $.
    $( [27-Jul-2006] $) $( [20-May-2005] $)

  ${
    $d x y z w A $.  $d z B $.  $d x y z w F $.
    expcllem.1 $e |- F (_ CC $.
    expcllem.2 $e |- ( ( x e. F /\ y e. F ) -> ( x x. y ) e. F ) $.
    expcllem.3 $e |- 1 e. F $.
    $( Lemma for proving nonnegative integer exponentiation closure laws. $)
    expcllem $p |- ( ( A e. F /\ B e. NN0 ) -> ( A ^ B ) e. F ) $=
      ( vz vw wcel cn cc0 wceq wo cexp co cn0 cv wi c1 caddc opreq2 eleq1d
      imbi2d cc sseli exp1t syl ibir wa cmul opreq1 vtocl2ga ancoms adantlr wb
      expp1t nnnn0t sylan2 sylan adantr mpbird exp31 com12 a2d nnind impcom
      exp0t sylan9eqr syl6eqel jaodan elnn0 sylan2b ) CEKZDLKZDMNZOCDPQZEKZDRKV
      OVPVSVQVPVOVSVOCISZPQZEKZTVOCUAPQZEKZTVOCJSZPQZEKZTVOCWEUAUBQZPQZEKZTVOVS
      TIJDVTUANZWBWDVOWKWAWCEVTUACPUCUDUEVTWENZWBWGVOWLWAWFEVTWECPUCUDUEVTWHNZW
      BWJVOWMWAWIEVTWHCPUCUDUEVTDNZWBVSVOWNWAVREVTDCPUCUDUEVOWDVOWCCEVOCUFKZWCC
      NEUFCFUGZCUHUIUDUJWELKZVOWGWJVOWQWGWJTVOWQWGWJVOWQUKZWGUKWJWFCULQZEKZVOWG
      WTWQWGVOWTASZBSZULQZEKWFXBULQZEKWTABWFCEEXAWFNXCXDEXAWFXBULUMUDXBCNXDWSEX
      BCWFULUCUDGUNUOUPWRWJWTUQWGWRWIWSEWOWQWIWSNZVOWOWERKXEWQCWEURWEUSUTWPVAUD
      VBVCVDVEVFVGVHVOVQUKVRUAEVQVOVRCMPQZUADMCPUCVOWOXFUANWPCVIUIVJHVKVLDVMVN
      $.
      $( [15-Dec-2005] $) $( [14-Dec-2005] $)
  $}

  ${
    $d x y A $.  $d x y N $.
    $( Closure of exponentiation of nonnegative integers. $)
    nnexpclt $p |- ( ( A e. NN /\ N e. NN0 ) -> ( A ^ N ) e. NN ) $=
      ( vx vy cn nnsscn cv nnmulclt 1nn expcllem ) CDABEFCGDGHIJ $.
      $( [28-Jan-2006] $) $( [16-Dec-2005] $)

    $( Closure of exponentiation of nonnegative integers. $)
    nn0expclt $p |- ( ( A e. NN0 /\ N e. NN0 ) -> ( A ^ N ) e. NN0 ) $=
      ( vx vy cn0 nn0sscn cv nn0mulclt 1nn0 expcllem ) CDABEFCGDGHIJ $.
      $( [15-Dec-2005] $) $( [14-Dec-2005] $)

    $( Closure of exponentiation of integers. $)
    zexpclt $p |- ( ( A e. ZZ /\ N e. NN0 ) -> ( A ^ N ) e. ZZ ) $=
      ( vx vy cz zsscn cv zmulclt 1z expcllem ) CDABEFCGDGHIJ $.
      $( [3-May-2006] $) $( [16-Dec-2005] $)

    $( Closure of exponentiation of rationals. $)
    qexpclt $p |- ( ( A e. QQ /\ N e. NN0 ) -> ( A ^ N ) e. QQ ) $=
      ( vx vy cq qsscn cv qmulclt c1 cz wcel 1z zqt ax-mp expcllem ) CDABEFCGDG
      HIJKIEKLIMNO $.
      $( [16-Mar-2006] $) $( [16-Dec-2005] $)

    $( Closure of exponentiation of reals. $)
    reexpclt $p |- ( ( A e. RR /\ N e. NN0 ) -> ( A ^ N ) e. RR ) $=
      ( vx vy cr axresscn cv axmulrcl 1re expcllem ) CDABEFCGDGHIJ $.
      $( [15-Dec-2005] $) $( [14-Dec-2005] $)

    $( Closure law for nonnegative integer exponentiation. $)
    expclt $p |- ( ( A e. CC /\ N e. NN0 ) -> ( A ^ N ) e. CC ) $=
      ( vx vy cc ssid cv axmulcl ax1cn expcllem ) CDABEEFCGDGHIJ $.
      $( [25-Jul-2005] $) $( [26-May-2005] $)
  $}

  $( Exponentiation in terms of predecessor exponent. $)
  expm1t $p |- ( ( A e. CC /\ N e. NN ) -> ( A ^ N ) =
               ( ( A ^ ( N - 1 ) ) x. A ) ) $=
    ( cc wcel cn wa c1 cmin co caddc cexp cmul wceq nncnt ax1cn npcant mpan2
    syl opreq2d adantl cn0 expp1t nnm1nn0t sylan2 eqtr3d ) ACDZBEDZFABGHIZGJIZK
    IZABKIZAUHKIALIZUGUJUKMUFUGUIBAKUGBCDZUIBMZBNUMGCDUNOBGPQRSTUFUHUADUJULMUGA
    UHUBBUCUDUE $.
    $( [26-Jan-2006] $) $( [19-Dec-2005] $)

  ${
    $d j k N $.
    $( Value of one raised to a nonnegative integer power. $)
    1expt $p |- ( N e. NN0 -> ( 1 ^ N ) = 1 ) $=
      ( vj vk c1 cv cexp co wceq cc0 caddc opreq2 eqeq1d cc wcel ax1cn exp0t
      ax-mp cn0 cmul expp1t mpan opreq1 mulid1 syl6eq syl5bir nn0ind ) DBEZFGZD
      HDIFGZDHZDCEZFGZDHZDUKDJGZFGZDHZDAFGZDHBCAUGIHUHUIDUGIDFKLUGUKHUHULDUGUKD
      FKLUGUNHUHUODUGUNDFKLUGAHUHUQDUGADFKLDMNZUJODPQUKRNZUPULDSGZDHUMUSUOUTDUR
      USUOUTHODUKTUALUMUTDDSGDULDDSUBDOUCUDUEUF $.
      $( [16-Dec-2005] $) $( [15-Dec-2005] $)
  $}

  ${
    $d j k A $.  $d j k N $.
    $( Natural number exponentiation is 0 iff its mantissa is 0. $)
    expeq0t $p |- ( ( A e. CC /\ N e. NN ) -> ( ( A ^ N ) = 0 <-> A = 0 ) ) $=
      ( vj vk cn wcel cc cexp co cc0 wceq wb cv wi c1 caddc opreq2 eqeq1d
      bibi1d imbi2d exp1t wa wo cn0 cmul expp1t mul0ort expclt pm3.26 sylanc
      bitrd nnnn0t sylan2 bi1 idd jaod olc jctir bi sylibr sylan9bb exp31
      com12 a2d nnind impcom ) BEFAGFZABHIZJKZAJKZLZVGACMZHIZJKZVJLZNVGAOHIZJKZ
      VJLZNVGADMZHIZJKZVJLZNVGAVSOPIZHIZJKZVJLZNVGVKNCDBVLOKZVOVRVGWGVNVQVJWGVM
      VPJVLOAHQRSTVLVSKZVOWBVGWHVNWAVJWHVMVTJVLVSAHQRSTVLWCKZVOWFVGWIVNWEVJWIVM
      WDJVLWCAHQRSTVLBKZVOVKVGWJVNVIVJWJVMVHJVLBAHQRSTVGVPAJAUARVSEFZVGWBWFVGWK
      WBWFNVGWKWBWFVGWKUBWEWAVJUCZWBVJVGVSUDFZWEWLLWKVGWMUBZWEVTAUEIZJKZWLWNWDW
      OJAVSUFRVTGFVGWPWLLWNVTAUGAVSUHVGWMUIUJUKVSULUMWBWLVJNZVJWLNZUBWLVJLWBWQW
      RWBWAVJVJWAVJUNWBVJUOUPVJWAUQURWLVJUSUTVAVBVCVDVEVF $.
      $( [27-Jul-2006] $) $( [23-Feb-2005] $)

    $( Natural number exponentiation is nonzero iff its mantissa is nonzero. $)
    expne0t $p |- ( ( A e. CC /\ N e. NN ) ->
                  ( A =/= 0 <-> ( A ^ N ) =/= 0 ) ) $=
      ( cc wcel cn wa cc0 cexp co wceq expeq0t bicomd eqneqd ) ACDBEDFZAGABHIZG
      NOGJAGJABKLM $.
      $( [8-May-2005] $) $( [6-May-2005] $)

    $( Nonnegative integer exponentiation is nonzero if its mantissa is
       nonzero. $)
    expne0it $p |- ( ( A e. CC /\ N e. NN0 /\ A =/= 0 ) -> ( A ^ N ) =/= 0 ) $=
      ( cc wcel cn0 cc0 wne cexp co cn wceq wo wi wa expne0t biimpd ex c1
      ax1ne0 opreq2 exp0t sylan9eqr neeq1d mpbiri a1dd jaod elnn0 syl5ib 3imp
      ) ACDZBEDZAFGZABHIZFGZUJBJDZBFKZLULUNMZUKUJUOUQUPUJUOUQUJUONULUNABOPQUJUP
      UNULUJUPUNUJUPNZUNRFGSURUMRFUPUJUMAFHIRBFAHTAUAUBUCUDQUEUFBUGUHUI $.
      $( [27-Sep-2006] $) $( [2-Aug-2006] $)

    $( Nonnegative integer exponentiation with a positive mantissa is
       positive. $)
    expgt0t $p |- ( ( A e. RR /\ N e. NN0 /\ 0 < A ) -> 0 < ( A ^ N ) ) $=
      ( vj vk cr wcel cn0 cc0 clt wbr cexp co wi wa cv c1 caddc wceq opreq2
      breq2d imbi2d cc recnt exp0t syl lt01 syl5breqr adantr cmul axmulgt0
      reexpclt pm3.26 sylanc expp1t sylan sylibrd exp4b com12 com34 imp3a a2d
      nn0ind exp3a 3imp ) AEFZBGFZHAIJZHABKLZIJZVFVEVGVIMVFVEVGVIVEVGNZHACOZKLZ
      IJZMVJHAHKLZIJZMVJHADOZKLZIJZMVJHAVPPQLZKLZIJZMVJVIMCDBVKHRZVMVOVJWBVLVNH
      IVKHAKSTUAVKVPRZVMVRVJWCVLVQHIVKVPAKSTUAVKVSRZVMWAVJWDVLVTHIVKVSAKSTUAVKB
      RZVMVIVJWEVLVHHIVKBAKSTUAVEVOVGVEPVNHIVEAUBFZVNPRAUCZAUDUEUFUGUHVPGFZVJVR
      WAWHVEVGVRWAMWHVEVRVGWAVEWHVRVGWAMMVEWHVRVGWAVEWHNZVRVGNZHVQAUILZIJZWAVQE
      FVEWJWLMWIVQAUJAVPUKVEWHULUMWIVTWKHIWFWHVTWKRVEAVPUNWGUOTUPUQURUSUTVAVBVC
      URVD $.
      $( [7-Apr-2006] $) $( [16-Dec-2005] $)
  $}

  $( Value of zero raised to a natural number power. $)
  0expt $p |- ( N e. NN -> ( 0 ^ N ) = 0 ) $=
    ( cn wcel cc0 cexp co wceq eqid cc wb 0cn expeq0t mpan mpbiri ) ABCZDAEFDGZ
    DDGZDHDICOPQJKDALMN $.
    $( [30-Oct-2004] $) $( [19-Aug-2004] $)

  $( Nonnegative integer exponentiation with a nonnegative mantissa is
     nonnegative. $)
  expge0t $p |- ( ( A e. RR /\ N e. NN0 /\ 0 <_ A ) -> 0 <_ ( A ^ N ) ) $=
    ( cr wcel cn0 cc0 cle wbr cexp co cn wceq wo wi wa clt expgt0t nnnn0t
    syl3an2 3expa ex opreq1 eqeq2d 0expt eqcomd syl5bi com12 adantl orim12d wb
    0re leloet mpan adantr reexpclt sylan2 syl 3imtr4d opreq2 breq2d c1 cc
    recnt exp0t 1re lt01 ltlei syl5breqr syl5bir a1dd jaod elnn0 syl5ib 3imp )
    ACDZBEDZFAGHZFABIJZGHZVOBKDZBFLZMVQVSNZVPVOVTWBWAVOVTWBVOVTOZFAPHZFALZMZFVR
    PHZFVRLZMZVQVSWCWDWGWEWHWCWDWGVOVTWDWGVOVPWDWGVTABQBRZSTUAVTWEWHNVOWEVTWHWE
    FFBIJZLWHVTWEWKVRFFABIUBUCVTWKFBUDUEUFUGUHUIVOVQWFUJZVTFCDZVOWLUKFAULUMUNWC
    VRCDZVSWIUJZVOVPWNVTABUOWJUPWMWNWOUKFVRULUMUQURUAVOWAVSVQWAVOVSWAVSFAFIJZGH
    VOWAVRWPFGBFAIUSUTVOVAWPFGVOAVBDWPVALAVCAVDUQFVAUKVEVFVGVHVIUGVJVKBVLVMVN
    $.
    $( [28-Jul-2006] $) $( [16-Dec-2005] $)

  ${
    $d x y A $.  $d x N $.
    $( Natural number exponentiation with a mantissa greater than 1 is
       greater than 1. $)
    expgt1t $p |- ( ( A e. RR /\ N e. NN /\ 1 < A ) -> 1 < ( A ^ N ) ) $=
      ( vx vy cr wcel cn c1 clt wbr cexp co wi wa cv caddc wceq opreq2 breq2d
      imbi2d cc recnt exp1t syl biimpar cn0 cmul mulgt1t reexpclt pm3.26 jca
      sylan ex expp1t sylibrd nnnn0t sylan2 exp4b com12 com34 imp3a a2d nnind
      exp3a 3imp ) AEFZBGFZHAIJZHABKLZIJZVGVFVHVJMVGVFVHVJVFVHNZHACOZKLZIJZMVKH
      AHKLZIJZMVKHADOZKLZIJZMVKHAVQHPLZKLZIJZMVKVJMCDBVLHQZVNVPVKWCVMVOHIVLHAKR
      STVLVQQZVNVSVKWDVMVRHIVLVQAKRSTVLVTQZVNWBVKWEVMWAHIVLVTAKRSTVLBQZVNVJVKWF
      VMVIHIVLBAKRSTVFVPVHVFVOAHIVFAUAFZVOAQAUBZAUCUDSUEVQGFZVKVSWBWIVFVHVSWBMW
      IVFVSVHWBVFWIVSVHWBMMVFWIVSVHWBVFVQUFFZVSVHNZWBMWIVFWJNZWKHVRAUGLZIJZWBWL
      WKWNVREFZVFNWKWNWLVRAUHWLWOVFAVQUIVFWJUJUKULUMWLWAWMHIWGWJWAWMQVFAVQUNWHU
      LSUOVQUPUQURUSUTVAVBVCVDUSVE $.
      $( [27-Jul-2006] $) $( [13-Feb-2005] $)
  $}

  $( Natural number exponentiation with a mantissa greater than or equal to 1
     is greater than or equal to 1. $)
  expge1t $p |- ( ( A e. RR /\ N e. NN /\ 1 <_ A ) -> 1 <_ ( A ^ N ) ) $=
    ( cr wcel cn c1 cle wbr cexp co wa clt wceq wo expgt1t 3expa ex wi opreq1
    eqeq2d cn0 nnnn0t 1expt syl eqcomd syl5bi com12 adantl orim12d wb 1re
    leloet mpan adantr reexpclt sylan2 3imtr4d 3impia ) ACDZBEDZFAGHZFABIJZGHZU
    SUTKZFALHZFAMZNZFVBLHZFVBMZNZVAVCVDVEVHVFVIVDVEVHUSUTVEVHABOPQUTVFVIRUSVFUT
    VIVFFFBIJZMVIUTVFVKVBFFABISTUTVKFUTBUADZVKFMBUBZBUCUDUEUFUGUHUIUSVAVGUJZUTF
    CDZUSVNUKFAULUMUNVDVBCDZVCVJUJZUSVLVPUTABUOVMUPVOVPVQUKFVBULUMUDUQUR $.
    $( [19-Jul-2005] $) $( [21-Feb-2005] $)

  ${
    $d j k A $.  $d j k B $.  $d j k M $.  $d j N $.
    $( Natural number exponentiation of a product.  Proposition 10-4.2(c) of
       [Gleason] p. 135, restricted to nonnegative integer exponents. $)
    mulexpt $p |- ( ( A e. CC /\ B e. CC /\ N e. NN0 ) ->
                  ( ( A x. B ) ^ N ) = ( ( A ^ N ) x. ( B ^ N ) ) ) $=
      ( vj vk cc wcel cn0 cmul co cexp wceq wa cv wi cc0 c1 caddc opreq2
      opreq12d eqeq12d imbi2d axmulcl exp0t syl opreqan12d ax1cn mulid1 syl6eq
      eqtr4d expp1t sylan adantr opreq1 adantl mul4t expclt anim12i anandirs
      pm3.26 sylanc adantlr adantll 3eqtrd exp31 com12 a2d nn0ind exp3a com3l
      3imp ) AFGZBFGZCHGZABIJZCKJZACKJZBCKJZIJZLZVNVLVMVTVNVLVMVTVLVMMZVODNZKJZ
      AWBKJZBWBKJZIJZLZOWAVOPKJZAPKJZBPKJZIJZLZOWAVOENZKJZAWMKJZBWMKJZIJZLZOWAV
      OWMQRJZKJZAWSKJZBWSKJZIJZLZOWAVTODECWBPLZWGWLWAXEWCWHWFWKWBPVOKSXEWDWIWEW
      JIWBPAKSWBPBKSTUAUBWBWMLZWGWRWAXFWCWNWFWQWBWMVOKSXFWDWOWEWPIWBWMAKSWBWMBK
      STUAUBWBWSLZWGXDWAXGWCWTWFXCWBWSVOKSXGWDXAWEXBIWBWSAKSWBWSBKSTUAUBWBCLZWG
      VTWAXHWCVPWFVSWBCVOKSXHWDVQWEVRIWBCAKSWBCBKSTUAUBWAWHQWKWAVOFGZWHQLABUCZV
      OUDUEWAWKQQIJQVLVMWIQWJQIAUDBUDUFQUGUHUIUJWMHGZWAWRXDWAXKWRXDOWAXKWRXDWAX
      KMZWRMWTWNVOIJZWQVOIJZXCXLWTXMLZWRXIXKXOWAVOWMUKXJULUMWRXMXNLXLWNWQVOIUNU
      OXLXNXCLWRXLXNWOAIJZWPBIJZIJZXCWOFGZWPFGZMZWAXNXRLXLWOWPABUPVLVMXKYAVLXKM
      XSVMXKMXTAWMUQBWMUQURUSWAXKUTVAXLXAXPXBXQIVLXKXAXPLVMAWMUKVBVMXKXBXQLVLBW
      MUKVCTUJUMVDVEVFVGVHVIVJVK $.
      $( [16-Feb-2005] $) $( [13-Feb-2005] $)

    $( Nonnegative integer exponentiation of a reciprocal. $)
    recexpt $p |- ( ( A e. CC /\ N e. NN0 /\ A =/= 0 ) ->
                  ( ( 1 / A ) ^ N ) = ( 1 / ( A ^ N ) ) ) $=
      ( vj vk cc wcel cn0 cc0 wne c1 cdiv co cexp wceq wi wa cv caddc opreq2
      opreq2d eqeq12d imbi2d recclt exp0t syl ax1cn div1 syl6eq adantr eqtr4d
      cmul opreq1 ad2antll expp1t sylan an1rs adantrr divmuldivt expclt jctil
      pm3.26 jca expne0it 3expa pm3.27 sylanc mulid1 opreq1i syl6req eqtrd
      3eqtr4d exp43 com12 imp3a a2d nn0ind exp3a 3imp ) AEFZBGFZAHIZJAKLZBMLZJA
      BMLZKLZNZVTVSWAWFOVTVSWAWFVSWAPZWBCQZMLZJAWHMLZKLZNZOWGWBHMLZJAHMLZKLZNZO
      WGWBDQZMLZJAWQMLZKLZNZOWGWBWQJRLZMLZJAXBMLZKLZNZOWGWFOCDBWHHNZWLWPWGXGWIW
      MWKWOWHHWBMSXGWJWNJKWHHAMSTUAUBWHWQNZWLXAWGXHWIWRWKWTWHWQWBMSXHWJWSJKWHWQ
      AMSTUAUBWHXBNZWLXFWGXIWIXCWKXEWHXBWBMSXIWJXDJKWHXBAMSTUAUBWHBNZWLWFWGXJWI
      WCWKWEWHBWBMSXJWJWDJKWHBAMSTUAUBWGWMJWOWGWBEFZWMJNAUCZWBUDUEVSWOJNWAVSWOJ
      JKLJVSWNJJKAUDTJUFUGUHUIUJWQGFZWGXAXFXMVSWAXAXFOZVSXMWAXNOVSXMWAXAXFVSXMP
      ZWAXAPPWRWBUKLZWTWBUKLZXCXEXAXPXQNXOWAWRWTWBUKULUMXOWAXCXPNZXAVSWAXMXRXKX
      MXRWGWBWQUNXLUOUPUQXOWAXEXQNXAXOWAPZXEJWSAUKLZKLZXQXOXEYANWAXOXDXTJKAWQUN
      TUIXSXQJJUKLZXTKLZYAJEFZWSEFZPZYDVSPZPZWSHIZWAPXQYCNXSJWSJAURXOYHWAXOYFYG
      XOYEYDAWQUSUFUTXOVSYDVSXMVAUFUTVBUIXSYIWAVSXMWAYIAWQVCVDXOWAVEVBVFYBJXTKJ
      UFVGVHVIVJUQVKVLVMVNVOVPVQVMVR $.
      $( [10-Oct-2006] $) $( [2-Aug-2006] $)

    $( Sum of exponents law for nonnegative integer exponentiation.
       Proposition 10-4.2(a) of [Gleason] p. 135. $)
    expaddt $p |- ( ( A e. CC /\ M e. NN0 /\ N e. NN0 ) ->
                  ( A ^ ( M + N ) ) = ( ( A ^ M ) x. ( A ^ N ) ) ) $=
      ( vj vk cc wcel cn0 caddc co cexp cmul wceq wa cv wi cc0 c1 opreq2
      opreq2d eqeq12d imbi2d expclt ax1id syl exp0t adantr nn0cnt ax0id adantl
      3eqtr4rd ax1cn axaddass mp3an3 syl2an adantll expp1t simpll nn0addclt
      sylanc eqtr3d adantlr axmulass syl3anc eqtr4d opreq1 syl5bir expcom a2d
      nn0ind exp3a com3l 3imp ) AFGZBHGZCHGZABCIJZKJZABKJZACKJZLJZMZVPVNVOWBVPV
      NVOWBVNVONZABDOZIJZKJZVSAWDKJZLJZMZPWCABQIJZKJZVSAQKJZLJZMZPWCABEOZIJZKJZ
      VSAWOKJZLJZMZPWCABWORIJZIJZKJZVSAXAKJZLJZMZPWCWBPDECWDQMZWIWNWCXGWFWKWHWM
      XGWEWJAKWDQBISTXGWGWLVSLWDQAKSTUAUBWDWOMZWIWTWCXHWFWQWHWSXHWEWPAKWDWOBIST
      XHWGWRVSLWDWOAKSTUAUBWDXAMZWIXFWCXIWFXCWHXEXIWEXBAKWDXABISTXIWGXDVSLWDXAA
      KSTUAUBWDCMZWIWBWCXJWFVRWHWAXJWEVQAKWDCBISTXJWGVTVSLWDCAKSTUAUBWCVSRLJZVS
      WMWKWCVSFGZXKVSMABUCZVSUDUEWCWLRVSLVNWLRMVOAUFUGTWCWJBAKVOWJBMZVNVOBFGZXN
      BUHZBUIUEUJTUKWOHGZWCWTXFWCXQWTXFPWCXQNZXFWQALJZWSALJZMWTXRXCXSXEXTXRAWPR
      IJZKJZXCXSXRYAXBAKVOXQYAXBMZVNXOWOFGZYCVOXQXOYDRFGYCULBWORUMUNXPWOUHUOUPT
      VNWPHGZYBXSMXRAWPUQVNVOXQURZVOXQYEVNBWOUSUPUTVAXRXEVSWRALJZLJZXTXRXDYGVSL
      VNXQXDYGMVOAWOUQVBTXLWRFGZVNXTYHMXRVSWRAVCWCXLXQXMUGVNXQYIVOAWOUCVBYFVDVE
      UAWQWSALVFVGVHVIVJVKVLVM $.
      $( [2-Dec-2004] $) $( [30-Nov-2004] $)

    $( Product of exponents law for natural number exponentiation.  Proposition
       10-4.2(b) of [Gleason] p. 135, restricted to nonnegative integer
       exponents. $)
    expmult $p |- ( ( A e. CC /\ M e. NN0 /\ N e. NN0 ) ->
               ( A ^ ( M x. N ) ) = ( ( A ^ M ) ^ N ) ) $=
      ( vj vk cc wcel cn0 cmul co cexp wceq wa cv wi cc0 c1 caddc opreq2
      opreq2d eqeq12d imbi2d nn0cnt mul01t syl exp0t sylan9eqr expclt eqtr4d
      ax1cn axdistr mp3an3 ax1id adantr eqtrd syl2an adantll expaddt simpll
      nn0mulclt simplr syl3anc expp1t sylan opreq1 syl5bir expcom a2d nn0ind
      exp3a com3l 3imp ) AFGZBHGZCHGZABCIJZKJZABKJZCKJZLZVOVMVNVTVOVMVNVTVMVNMZ
      ABDNZIJZKJZVRWBKJZLZOWAABPIJZKJZVRPKJZLZOWAABENZIJZKJZVRWKKJZLZOWAABWKQRJ
      ZIJZKJZVRWPKJZLZOWAVTODECWBPLZWFWJWAXAWDWHWEWIXAWCWGAKWBPBISTWBPVRKSUAUBW
      BWKLZWFWOWAXBWDWMWEWNXBWCWLAKWBWKBISTWBWKVRKSUAUBWBWPLZWFWTWAXCWDWRWEWSXC
      WCWQAKWBWPBISTWBWPVRKSUAUBWBCLZWFVTWAXDWDVQWEVSXDWCVPAKWBCBISTWBCVRKSUAUB
      WAWHQWIVNVMWHAPKJQVNWGPAKVNBFGZWGPLBUCZBUDUETAUFUGWAVRFGZWIQLABUHZVRUFUEU
      IWKHGZWAWOWTWAXIWOWTOWAXIMZWTWMVRIJZWNVRIJZLWOXJWRXKWSXLXJWRAWLBRJZKJZXKX
      JWQXMAKVNXIWQXMLZVMXEWKFGZXOVNXIXEXPMZWQWLBQIJZRJZXMXEXPQFGWQXSLUJBWKQUKU
      LXQXRBWLRXEXRBLXPBUMUNTUOXFWKUCUPUQTVMWLHGZVNXNXKLXJAWLBURVMVNXIUSVNXIXTV
      MBWKUTUQVMVNXIVAVBUOXGXIWSXLLWAVRWKVCXHVDUAWMWNVRIVEVFVGVHVIVJVKVL $.
      $( [3-Feb-2006] $) $( [4-Jan-2006] $)
  $}

  $( Exponent subtraction law for nonnegative integer exponentiation. $)
  expsubt $p |- ( ( ( A e. CC /\ M e. NN0 /\ N e. NN0 ) /\
  ( A =/= 0 /\ N <_ M ) ) -> ( A ^ ( M - N ) ) = ( ( A ^ M ) / ( A ^ N ) ) ) $=
    ( cc wcel cn0 w3a cc0 wne cle wbr wa cexp co cdiv cmin wceq cmul caddc
    expaddt 3simp1 adantr 3simp3 wb nn0subt ancoms biimpa 3adantl1 syl3anc
    pncan3t nn0cnt syl2an opreq2d 3adant1 eqtr3d adantrl divmult expclt
    3adant3 3adant2 sylanc 3jca expne0it 3expa 3adantl2 adantrr mpbird eqcomd
    ) ADEZBFEZCFEZGZAHIZCBJKZLZLZABMNZACMNZONZABCPNZMNZVPVSWAQZVRWARNZVQQZVLVNW
    DVMVLVNLZACVTSNZMNZWCVQVIVKVTFEZWGWCQWEACVTTVLVIVNVIVJVKUAUBZVLVKVNVIVJVKUC
    UBVJVKVNWHVIVJVKLZVNWHVKVJVNWHUDCBUEUFUGUHZUIVLWGVQQZVNVJVKWLVIWJWFBAMVKVJW
    FBQZCDEBDEWMVKVJCBUJCUKBUKULUFUMUNUBUOUPVQDEZVRDEZWADEZGVRHIZWBWDUDVPVQVRWA
    UQVPWNWOWPVLWNVOVIVJWNVKABURUSUBVLWOVOVIVKWOVJACURUTUBVLVNWPVMVIWHWPWEAVTUR
    WIWKVAUPVBVLVMWQVNVIVKVMWQVJVIVKVMWQACVCVDVEVFVAVGVH $.
    $( [10-Oct-2006] $) $( [2-Aug-2006] $)

  $( Nonnegative integer exponentiation of a quotient. $)
  divexpt $p |- ( ( ( A e. CC /\ B e. CC /\ N e. NN0 ) /\ B =/= 0 ) ->
                ( ( A / B ) ^ N ) = ( ( A ^ N ) / ( B ^ N ) ) ) $=
    ( cc wcel cn0 cc0 wne cdiv co cexp wceq wa c1 cmul divrect 3expb opreq1d
    adantr mulexpt 3expa recclt sylanl2 recexpt an1rs adantll opreq2d expclt
    adantlr adantlrr expne0it syl3anc eqtr4d 3eqtrd exp42 com34 3imp1 ) ADEZBDE
    ZCFEZBGHZABIJZCKJZACKJZBCKJZIJZLZURUSVAUTVGURUSVAUTVGURUSVAMZMZUTMZVCANBIJZ
    OJZCKJZVDVKCKJZOJZVFVIVCVMLUTVIVBVLCKURUSVAVBVLLABPQRSURVKDEZUTVMVOLZVHURVP
    UTVQAVKCTUABUBUCVJVOVDNVEIJZOJZVFVJVNVRVDOVHUTVNVRLZURUSUTVAVTUSUTVAVTBCUDU
    AUEUFUGVDDEZVEDEZVEGHZVFVSLVJVDVEPURUTWAVHACUHUIURUSUTWBVAUSUTWBURBCUHUFUJV
    HUTWCURUSUTVAWCUSUTVAWCBCUKUAUEUFULUMUNUOUPUQ $.
    $( [?] $) $( [2-Aug-2006] $)

  $( Ordering relationship for exponentiation. $)
  expordit $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             ( 1 < A /\ M < N ) ) -> ( A ^ M ) < ( A ^ N ) ) $=
    ( cr wcel cn0 w3a c1 clt wbr wa cexp co cmul cmin cn expgt1t 3simp1 adantr
    wb cz znnsubt nn0zt syl2an 3adant1 biimpa adantrl simprl syl3anc cc0
    ltmul1t 1re a1i reexpclt nnnn0t syl sylanc 3adant3 3jca wi lt01 0re
    axlttrn mp3an12 mpani expgt0t 3expa ex syld imp adantrr mpbid wceq cc
    expclt recnt sylan mulid2t caddc recnd 3simp2 expaddt syl3an2 npcant
    nn0cnt ancoms opreq2d eqtr3d 3brtr3d ) ADEZBFEZCFEZGZHAIJZBCIJZKZKZHABLMZNM
    ZACBOMZLMZWRNMZWRACLMZIWQHXAIJZWSXBIJZWJWTPEZWNXDWQAWTQWMWJWPWJWKWLRZSWMWOX
    FWNWMWOXFWKWLWOXFTZWJBUAECUAEXHWKWLBCUBBUCCUCUDUEUFZUGWMWNWOUHUIHDEZXADEZWR
    DEZGUJWRIJZXDXETWQHXAWRUKWQXJXKXLXJWQULUMWMWOXKWNWJWTFEZXKWMWOKZAWTUNWMWJWO
    XGSXOXFXNXIWTUOZUPUQUGWMXLWPWJWKXLWLABUNURSUSWMWNXMWOWMWNXMWJWKWNXMUTWLWJWK
    KZWNUJAIJZXMWJWNXRUTWKWJUJHIJZWNXRVAUJDEXJWJXSWNKXRUTVBULUJHAVCVDVESXQXRXMW
    JWKXRXMABVFVGVHVIURVJVKUQVLWMWSWRVMZWPWJWKXTWLXQWRVNEZXTAVNEZWKYAWJABVOAVPV
    QWRVRUPURSWQAWTBVSMZLMZXBXCWQYBXFWKGZYDXBVMZWMWOYEWNXOYBXFWKWMYBWOWMAXGVTSX
    IWMWKWOWJWKWLWASUSUGYBXNWKYFXFAWTBWBXPWCUPWMYDXCVMWPWMYCCALWKWLYCCVMZWJWLWK
    YGCVNEBVNEYGWLWKCBWDCWEBWEUDWFUEWGSWHWI $.
    $( [6-Oct-2006] $) $( [2-Aug-2006] $)

  $( Cancellation law for exponentiation. $)
  expcant $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             1 < A ) -> ( ( A ^ M ) = ( A ^ N ) <-> M = N ) ) $=
    ( cr wcel cn0 w3a c1 clt wbr wa cexp co wceq wo wn wi expordit exp32 imp
    3com23 orim12d wb lttri2t nn0ret syl2an 3adant1 adantr reexpclt 3impdi
    3imtr4d a3d opreq2 impbid1 ) ADEZBFEZCFEZGZHAIJZKZABLMZACLMZNZBCNZUTVDVCUTB
    CIJZCBIJZOZVAVBIJZVBVAIJZOZVDPZVCPZUTVEVHVFVIURUSVEVHQURUSVEVHABCRSTURUSVFV
    IQZUOUQUPUSVMQUOUQUPGUSVFVIACBRSUATUBURVKVGUCZUSUPUQVNUOBDECDEVNUPUQBCUDBUE
    CUEUFUGUHURVLVJUCZUSUOUPUQVOVADEVBDEVOUOUPKUOUQKVAVBUDABUIACUIUFUJUHUKULBCA
    LUMUN $.
    $( [8-Oct-2006] $) $( [2-Aug-2006] $)

  $( Ordering law for exponentiation. $)
  expordt $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             1 < A ) -> ( M < N <-> ( A ^ M ) < ( A ^ N ) ) ) $=
    ( cr wcel cn0 w3a c1 clt wbr wa cexp co wi expordit exp32 imp wceq wo wn
    expcant biimprd 3com23 orim12d con3d wb axlttri reexpclt syl2an 3impdi
    adantr nn0ret 3adant1 3imtr4d impbid ) ADEZBFEZCFEZGZHAIJZKZBCIJZABLMZACLMZ
    IJZUSUTVBVENUSUTVBVEABCOPQVAVCVDRZVDVCIJZSZTZBCRZCBIJZSZTZVEVBVAVLVHVAVJVFV
    KVGVAVFVJABCUAUBUSUTVKVGNZUPURUQUTVNNUPURUQGUTVKVGACBOPUCQUDUEUSVEVIUFZUTUP
    UQURVOVCDEVDDEVOUPUQKUPURKVCVDUGABUHACUHUIUJUKUSVBVMUFZUTUQURVPUPBDECDEVPUQ
    URBCUGBULCULUIUMUKUNUO $.
    $( [9-Oct-2006] $) $( [2-Aug-2006] $)

  $( Weak ordering relationship for exponentiation. $)
  expwordit $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             ( 1 <_ A /\ M <_ N ) ) -> ( A ^ M ) <_ ( A ^ N ) ) $=
    ( cr wcel cn0 w3a c1 cle wbr cexp co clt wceq wo wi wb 1re leloet mpan
    3ad2ant1 wa expordt biimpd opreq2 a1i orim12d nn0ret syl2an 3adant1 adantr
    reexpclt 3adant3 3adant2 sylanc 3imtr4d ex opreq1 breq12d leid 1expt
    breqan12d mpbiri syl5bi com12 a1dd jaod sylbid imp32 ) ADEZBFEZCFEZGZHAIJZB
    CIJZABKLZACKLZIJZVMVNHAMJZHANZOZVOVRPZVJVKVNWAQZVLHDEVJWCRHASTUAVMVSWBVTVMV
    SWBVMVSUBZBCMJZBCNZOZVPVQMJZVPVQNZOZVOVRWDWEWHWFWIWDWEWHABCUCUDWFWIPWDBCAKU
    EUFUGVMVOWGQZVSVKVLWKVJBDECDEWKVKVLBCSBUHCUHUIUJUKVMVRWJQZVSVPDEZVQDEZWLVMV
    PVQSVJVKWMVLABULUMVJVLWNVKACULUNUOUKUPUQVMVTVRVOVKVLVTVRPVJVTVKVLUBZVRVTHBK
    LZHCKLZIJZVRWOVTWPVPWQVQIHABKURHACKURUSWOWRHHIJHRUTVKVLWPHWQHIBVACVAVBVCVDV
    EUJVFVGVHVI $.
    $( [9-Oct-2006] $) $( [14-Dec-2005] $)

  $( The power of a positive number smaller than 1 decreases as its exponent
     increases. $)
  explt1t $p |- ( ( ( A e. RR /\ M e. NN0 /\ N e. NN0 ) /\
             ( 0 < A /\ A < 1 ) ) -> ( M < N <-> ( A ^ N ) < ( A ^ M ) ) ) $=
    ( cr wcel cn0 w3a cc0 clt wbr c1 wa cdiv co cexp wb expordt wne gt0ne0t
    rerecclt syldan expcom idd 3anim123d impcom adantrr reclt1t biimpa anasss
    adantlr 3adantl3 sylanc ltrect an4s reexpclt adantr 3adantl2 jca expgt0t
    3expa cc wceq recexpt breq12d recnt id 3anim123i bitr4d ) ADEZBFEZCFEZGZHAI
    JZAKIJZLZLZBCIJZKAMNZBONZVRCONZIJZACONZABONZIJZVRDEZVJVKGZKVRIJZVQWAPVPVRBC
    QVLVMWFVNVMVLWFVMVIWEVJVJVKVKVIVMWEVIVMAHRZWEASZATUAUBVMVJUCVMVKUCUDUEUFVIV
    JVOWGVKVIVOWGVJVIVMVNWGVIVMLVNWGAUGUHUIUJUKULVLVMWDWAPVNVLVMLZWDKWCMNZKWBMN
    ZIJZWAWBDEZWCDEZLHWBIJZHWCIJZLWDWMPZWJWNWPWOWQWRWBWCUMUNWJWNWOVIVKVMWNVJVIV
    KLWNVMACUOUPUQVIVJVMWOVKVIVJLWOVMABUOUPUKURWJWPWQVIVKVMWPVJVIVKVMWPACUSUTUQ
    VIVJVMWQVKVIVJVMWQABUSUTUKURULAVAEZVJVKGZWHWAWMPWJWTWHLVSWKVTWLIWSVJWHVSWKV
    BZVKWSVJWHXAABVCUTUKWSVKWHVTWLVBZVJWSVKWHXBACVCUTUQVDVLWTVMVIWSVJVJVKVKAVEV
    JVFVKVFVGUPVIVJVMWHVKVIVMWHVJWIUJUKULVHUFVH $.
    $( [?] $) $( [2-Aug-2006] $)

  ${
    $d j k A $.  $d j k B $.  $d j k N $.
    $( Weak mantissa ordering relationship for exponentiation. $)
    expmwordit $p |- ( ( ( A e. RR /\ B e. RR /\ N e. NN0 ) /\
               ( 0 <_ A /\ A <_ B ) ) -> ( A ^ N ) <_ ( B ^ N ) ) $=
      ( vj vk cr wcel cn0 cc0 cle wbr wa cexp co wi cv c1 caddc cmul lemul12it
      reexpclt adantlr adantr adantll jca expge0t 3expa an1rs adantllr
      adantlrr anim1i simpll sylanc wceq cc expp1t recnt sylan 3brtr4d ex
      expcom a2d exp0t 1re leid syl6eqbr adantl breqtrrd syl2an opreq2 breq12d
      imbi2d nn0indALT exp4c com3l 3imp1 ) AFGZBFGZCHGZIAJKZABJKZLZACMNZBCMNZJK
      ZVSVQVRWBWEOVSVQVRWBWEVQVRLZWBLZADPZMNZBWHMNZJKZOWGAIMNZBIMNZJKZOWGAEPZMN
      ZBWOMNZJKZOWGAWOQRNZMNZBWSMNZJKZOWGWEODECWOHGZWGWRXBWGXCWRXBOWGXCLZWRXBXD
      WRLZWPASNZWQBSNZWTXAJWPFGZWQFGZLZIWPJKZWRLZLWGXFXGJKXEWPWQABTXEXJXLXEXHXI
      XDXHWRWFXCXHWBVQXCXHVRAWOUAUBUBUCXDXIWRWFXCXIWBVRXCXIVQBWOUAUDUBUCUEXDXKW
      RWFVTXCXKWAVQVTXCXKVRVQXCVTXKVQXCVTXKAWOUFUGUHUIUJUKUEWGXCWRULUMXDWTXFUNZ
      WRWFXCXMWBVQXCXMVRAUOGZXCXMVQAWOUPAUQZURUBUBUCXDXAXGUNZWRWFXCXPWBVRXCXPVQ
      BUOGZXCXPVRBWOUPBUQZURUDUBUCUSUTVAVBWFWNWBXNXQWNVQVRXNXQLZWLQWMJXSWLQQJXN
      WLQUNXQAVCUCQVDVEVFXQWMQUNXNBVCVGVHXOXRVIUCWHIUNZWKWNWGXTWIWLWJWMJWHIAMVJ
      WHIBMVJVKVLWHWOUNZWKWRWGYAWIWPWJWQJWHWOAMVJWHWOBMVJVKVLWHWSUNZWKXBWGYBWIW
      TWJXAJWHWSAMVJWHWSBMVJVKVLWHCUNZWKWEWGYCWIWCWJWDJWHCAMVJWHCBMVJVKVLVMVNVO
      VP $.
      $( [24-Jan-2006] $) $( [18-Dec-2005] $)
  $}

  $( An upper bound on ` A ^ N ` when ` 2 <_ A ` . $)
  expbndt $p |- ( ( A e. RR /\ N e. NN0 /\ 2 <_ A ) ->
                  ( A ^ N ) <_ ( ( 2 ^ N ) x. ( ( A - 1 ) ^ N ) ) ) $=
    ( cr wcel cn0 c2 cle wbr w3a cexp co c1 cmin cmul cc0 wa expmwordit 3simp1
    peano2rem 2re axmulrcl mpan syl 3ad2ant1 3simp2 3jca 0re 2pos ltlei wi
    letrt mp3an12 mpani imp caddc wb resubclt mpan2 leadd2t mp3an1 mpdan
    biimpa wceq cc recnt 2cn npcant adantr ax1cn subdit mp3an13 2timest mulid1
    a1i opreq12d addsubt mp3an3 anidms 3eqtrrd 3brtr3d jca 3adant2 sylanc
    mulexpt recnd sylan 3adant3 breqtrd ) ACDZBEDZFAGHZIZABJKZFALMKZNKZBJKZFBJK
    WNBJKNKZGWIWOCDZWJIOAGHZAWOGHZPZWMWPGHWLAWOBQWLWIWRWJWIWJWKRWIWJWRWKWIWNCDZ
    WRASZFCDZXBWRTFWNUAUBUCUDWIWJWKUEUFWIWKXAWJWIWKPZWSWTWIWKWSWIOFGHZWKWSOFUGT
    UHUIOCDXDWIXFWKPWSUJUGTOFAUKULUMUNXEAFMKZFUOKZXGAUOKZAWOGWIWKXHXIGHZWIXGCDZ
    WKXJUPZWIXDXKTAFUQURXDWIXKXLTFAXGUSUTVAVBWIXHAVCZWKWIAVDDZXMAVEZXNFVDDZXMVF
    AFVGURUCVHWIXIWOVCZWKWIXNXQXOXNWOFANKZFLNKZMKZAAUOKZFMKZXIXPXNLVDDWOXTVCVFV
    IFALVJVKXNXRYAXSFMAVLXSFVCXNFVFVMVNVOXNYBXIVCZXNXNXPYCVFAAFVPVQVRVSUCVHVTWA
    WBWCWIWJWPWQVCZWKWNVDDZWJYDWIXPYEWJYDVFFWNBWDUTWIWNXCWEWFWGWH $.
    $( [24-Jan-2006] $) $( [19-Dec-2005] $)

  $( Value of the square of a complex number.  (Contributed by Raph Levien,
     10-Apr-04.) $)
  sqvalt $p |- ( A e. CC -> ( A ^ 2 ) = ( A x. A ) ) $=
    ( cc wcel c2 cexp co c1 cmul caddc cn0 wceq 1nn0 expp1t mpan2 df-2 opreq2i
    syl5eq exp1t opreq1d eqtrd ) ABCZADEFZAGEFZAHFZAAHFUAAGGIFZEFZUDUBUAGJCUFUD
    KLAGMNDUEAEOPQUAUCAAHARST $.
    $( [28-Jul-2006] $) $( [20-May-2004] $)

  $( The square of the negative of a number.) $)
  sqnegt $p |- ( A e. CC -> ( -u A ^ 2 ) = ( A ^ 2 ) ) $=
    ( cc wcel cneg cmul co c2 cexp wceq mul2negt anidms negclt sqvalt syl
    3eqtr4d ) ABCZADZQEFZAAEFZQGHFZAGHFPRSIAAJKPQBCTRIALQMNAMO $.
    $( [18-Feb-2007] $) $( [15-Jan-2006] $)

  $( Closure of square. $)
  sqclt $p |- ( A e. CC -> ( A ^ 2 ) e. CC ) $=
    ( cc wcel c2 cexp co cmul sqvalt axmulcl anidms eqeltrd ) ABCZADEFAAGFZBAHL
    MBCAAIJK $.
    $( [10-Aug-1999] $)

  ${
    sqval.1 $e |- A e. CC $.
    $( Value of square.  Inference version. $)
    sqval $p |- ( A ^ 2 ) = ( A x. A ) $=
      ( cc wcel c2 cexp co cmul wceq sqvalt ax-mp ) ACDAEFGAAHGIBAJK $.
      $( [1-Aug-1999] $)

    $( Closure of square. $)
    sqcl $p |- ( A ^ 2 ) e. CC $=
      ( cc wcel c2 cexp co sqclt ax-mp ) ACDAEFGCDBAHI $.
      $( [2-Aug-1999] $)

    $( A number is zero iff its square is zero. $)
    sq00 $p |- ( ( A ^ 2 ) = 0 <-> A = 0 ) $=
      ( c2 cexp co cc0 wceq cmul sqval eqeq1i msq0 bitr ) ACDEZFGAAHEZFGAFGMNFA
      BIJABKL $.
      $( [2-Oct-1999] $)

    ${
      sqmul.2 $e |- B e. CC $.
      $( Distribution of square over multiplication. $)
      sqmul $p |- ( ( A x. B ) ^ 2 ) = ( ( A ^ 2 ) x. ( B ^ 2 ) ) $=
        ( cc wcel c2 cn0 cmul co cexp wceq 2nn0 mulexpt mp3an ) AEFBEFGHFABIJGK
        JAGKJBGKJIJLCDMABGNO $.
        $( [31-Jul-2006] $) $( [3-Sep-1999] $)

      ${
        sqdiv.3 $e |- B =/= 0 $.
        $( Distribution of square over division. $)
        sqdiv $p |- ( ( A / B ) ^ 2 ) = ( ( A ^ 2 ) / ( B ^ 2 ) ) $=
          ( cdiv co cmul c2 cexp divmuldiv divcl sqval opreq12i 3eqtr4 ) ABFGZP
          HGAAHGZBBHGZFGPIJGAIJGZBIJGZFGABABCDCDEEKPABCDELMSQTRFACMBDMNO $.
          $( [20-Aug-2001] $)
      $}
    $}

    ${
      sqreci.1 $e |- A =/= 0 $.
      $( Square of reciprocal. $)
      sqreci $p |- ( ( 1 / A ) ^ 2 ) = ( 1 / ( A ^ 2 ) ) $=
        ( c1 cdiv co cmul c2 cexp ax1cn divmuldiv mulid1 opreq1i eqtr reccl
        sqval opreq2i 3eqtr4 ) DAEFZSGFZDAAGFZEFZSHIFDAHIFZEFTDDGFZUAEFUBDADAJB
        JBCCKUDDUAEDJLMNSABCOPUCUADEABPQR $.
        $( [17-Sep-1999] $)
    $}
  $}

  $( A number is zero iff its square is zero. $)
  sq0t $p |- ( A e. CC -> ( ( A ^ 2 ) = 0 <-> A = 0 ) ) $=
    ( cc wcel c2 cexp co cc0 wceq wb cif opreq1 eqeq1d eqeq1 bibi12d 0cn
    elimel sq00 dedth ) ABCZADEFZGHZAGHZISAGJZDEFZGHZUCGHZIAGAUCHZUAUEUBUFUGTUD
    GAUCDEKLAUCGMNUCAGBOPQR $.
    $( [12-Apr-2006] $) $( [11-Mar-2006] $)

  $( A number is nonzero iff its square is nonzero. $)
  sqne0t $p |- ( A e. CC -> ( ( A ^ 2 ) =/= 0 <-> A =/= 0 ) ) $=
    ( cc wcel c2 cexp co cc0 sq0t eqneqd ) ABCADEFGAGAHI $.
    $( [14-Apr-2006] $) $( [11-Mar-2006] $)

  $( Closure of the square of a real number. $)
  resqclt $p |- ( A e. RR -> ( A ^ 2 ) e. RR ) $=
    ( cr wcel c2 cn0 cexp co 2nn0 reexpclt mpan2 ) ABCDECADFGBCHADIJ $.
    $( [29-Jul-2006] $) $( [18-Oct-1999] $)

  ${
    resqcl.1 $e |- A e. RR $.
    $( Closure of square in reals. $)
    resqcl $p |- ( A ^ 2 ) e. RR $=
      ( cr wcel c2 cexp co resqclt ax-mp ) ACDAEFGCDBAHI $.
      $( [2-Aug-1999] $)

    ${
      lt2sq.2 $e |- B e. RR $.
      $( Two nonnegative reals compare the same as their squares. $)
      lt2sq $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( A < B <-> ( A ^ 2 ) < ( B ^ 2 ) ) ) $=
        ( cc0 cle wbr wa clt cmul co c2 cexp lt2msq recn sqval breq12i syl6bbr
        ) EAFGEBFGHABIGAAJKZBBJKZIGALMKZBLMKZIGABCDNUASUBTIAACOPBBDOPQR $.
        $( [12-Sep-1999] $)

      $( Two nonnegative reals compare the same as their squares. $)
      le2sq $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( A <_ B <-> ( A ^ 2 ) <_ ( B ^ 2 ) ) ) $=
        ( cc0 cle wbr wa cmul co c2 cexp le2msq recn sqval breq12i syl6bbr ) EA
        FGEBFGHABFGAAIJZBBIJZFGAKLJZBKLJZFGABCDMTRUASFAACNOBBDNOPQ $.
        $( [12-Sep-1999] $)

      $( The square function is one-to-one for nonnegative reals. $)
      sq11 $p |-  ( ( 0 <_ A /\ 0 <_ B ) ->
                ( ( A ^ 2 ) = ( B ^ 2 ) <-> A = B ) ) $=
        ( cc0 cle wbr wa cmul co wceq c2 cexp msq11 recn sqval eqeq12i syl5bb
        ) EAFGEBFGHAAIJZBBIJZKABKALMJZBLMJZKABCDNUASUBTAACOPBBDOPQR $.
        $( [27-Oct-1999] $)
    $}

    $( The square of a non-zero real is positive. $)
    sqgt0 $p |- ( A =/= 0 -> 0 < ( A ^ 2 ) ) $=
      ( cc0 wceq wn cmul co clt wbr wne c2 cexp msqgt0 df-ne recn sqval breq2i
      3imtr4 ) ACDECAAFGZHIACJCAKLGZHIABMACNTSCHAABOPQR $.
      $( [17-Sep-1999] $)

    $( A square of a real is nonnegative. $)
    sqge0 $p |- 0 <_ ( A ^ 2 ) $=
      ( cc0 cmul co c2 cexp cle msqge0 recn sqval breqtrr ) CAADEAFGEHABIAABJKL
      $.
      $( [3-Aug-1999] $)
  $}

  $( The square function is one-to-one for nonnegative reals. $)
  sq11t $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
               ( ( A ^ 2 ) = ( B ^ 2 ) <-> A = B ) ) $=
    ( cr wcel cc0 cle wbr c2 cexp co wceq wb wa wi cif breq2 anbi1d opreq1
    eqeq1d eqeq1 bibi12d imbi12d anbi2d eqeq2d eqeq2 0re elimel sq11 dedth2h
    imp an4s ) ACDZBCDZEAFGZEBFGZAHIJZBHIJZKZABKZLZULUMMUNUOMZUTULUMVAUTNEULAEO
    ZFGZUOMZVBHIJZUQKZVBBKZLZNVCEUMBEOZFGZMZVEVIHIJZKZVBVIKZLZNABEEAVBKZVAVDUTV
    HVPUNVCUOAVBEFPQVPURVFUSVGVPUPVEUQAVBHIRSAVBBTUAUBBVIKZVDVKVHVOVQUOVJVCBVIE
    FPUCVQVFVMVGVNVQUQVLVEBVIHIRUDBVIVBUEUAUBVBVIAECUFUGBECUFUGUHUIUJUK $.
    $( [13-Mar-2006] $)  $( [8-Apr-2001] $)

  $( Two nonnegative numbers compare the same as their squares. $)
  lt2sqt $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
              ( A < B <-> ( A ^ 2 ) < ( B ^ 2 ) ) ) $=
    ( cr wcel cc0 cle wbr wa clt cmul co c2 cexp lt2msqt wb cc sqvalt
    breqan12d recnt syl2an ad2ant2r bitr4d ) ACDZEAFGZHBCDZEBFGZHHABIGAAJKZBBJK
    ZIGZALMKZBLMKZIGZABNUCUEULUIOZUDUFAPDZBPDZUMUCUEUNUOUJUGUKUHIAQBQRASBSTUAUB
    $.
    $( [11-Apr-2006] $) $( [24-Feb-2006] $)

  $( Two nonnegative numbers compare the same as their squares. $)
  lt2sqtOLD $p |- ( ( A e. RR /\ B e. RR ) -> ( ( 0 <_ A /\ 0 <_ B ) ->
              ( A < B <-> ( A ^ 2 ) < ( B ^ 2 ) ) ) ) $=
    ( cr wcel cc0 cle wbr wa clt c2 cexp co wb wi cif wceq breq2 anbi1d breq1
    opreq1 breq1d bibi12d imbi12d anbi2d breq2d 0re elimel lt2sq dedth2h ) ACDZ
    BCDZEAFGZEBFGZHZABIGZAJKLZBJKLZIGZMZNEUJAEOZFGZUMHZUTBIGZUTJKLZUQIGZMZNVAEU
    KBEOZFGZHZUTVGIGZVDVGJKLZIGZMZNABEEAUTPZUNVBUSVFVNULVAUMAUTEFQRVNUOVCURVEAU
    TBISVNUPVDUQIAUTJKTUAUBUCBVGPZVBVIVFVMVOUMVHVABVGEFQUDVOVCVJVEVLBVGUTIQVOUQ
    VKVDIBVGJKTUEUBUCUTVGAECUFUGBECUFUGUHUI $.
    $( [26-Sep-1999] $)

  $( Two nonnegative numbers compare the same as their squares. $)
  le2sqt $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
              ( A <_ B <-> ( A ^ 2 ) <_ ( B ^ 2 ) ) ) $=
    ( cr wcel cc0 cle wbr wa cmul co c2 cexp le2msqt wb cc sqvalt breqan12d
    recnt syl2an ad2ant2r bitr4d ) ACDZEAFGZHBCDZEBFGZHHABFGAAIJZBBIJZFGZAKLJZB
    KLJZFGZABMUBUDUKUHNZUCUEAODZBODZULUBUDUMUNUIUFUJUGFAPBPQARBRSTUA $.
    $( [26-Jul-2006] $) $( [18-Oct-1999] $)

  $( A square of a real is nonnegative. $)
  sqge0t $p |- ( A e. RR -> 0 <_ ( A ^ 2 ) ) $=
    ( cr wcel cc0 c2 cexp co cle wbr cif wceq opreq1 breq2d 0re elimel sqge0
    dedth ) ABCZDAEFGZHIDRADJZEFGZHIADATKSUADHATEFLMTADBNOPQ $.
    $( [18-Oct-1999] $)

  ${
    sumsqne0.1 $e |- A e. RR $.
    sumsqne0.2 $e |- B e. RR $.
    $( The sum of two squares is nonzero iff one of its terms is nonzero. $)
    sumsqne0 $p |- ( ( A =/= 0 \/ B =/= 0 ) <->
                   ( ( A ^ 2 ) + ( B ^ 2 ) ) =/= 0 ) $=
      ( cc0 wne wo c2 cexp co caddc clt wbr sqgt0 cle sqge0 cr wcel wb resqcl
      addge01t mp2an mpbi 0re readdcl ltletr mpan2 gt0ne0 3syl addge02t jaoi
      wceq wn wa opreq1 opreqan12d cn 2nn 0expt ax-mp opreq12i 0cn addid1 eqtr
      syl6eq con3i df-ne orbi12i ianor bitr4 3imtr4 impbi ) AEFZBEFZGZAHIJZBHIJ
      ZKJZEFZVMVSVNVMEVPLMZEVRLMZVSACNVTVPVROMZWAEVQOMZWBBDPVPQRZVQQRZWCWBSACTZ
      BDTZVPVQUAUBUCEVPVRUDWFVPVQWFWGUEZUFUGVRWHUHZUIVNEVQLMZWAVSBDNWJVQVROMZWA
      EVPOMZWKACPWEWDWLWKSWGWFVQVPUJUBUCEVQVRUDWGWHUFUGWIUIUKVREULZUMAEULZBEULZ
      UNZUMZVSVOWPWMWPVREHIJZWRKJZEWNWOVPWRVQWRKAEHIUOBEHIUOUPWSEEKJEWREWREKHUQ
      RWREULURHUSUTZWTVAEVBVCVDVEVFVREVGVOWNUMZWOUMZGWQVMXAVNXBAEVGBEVGVHWNWOVI
      VJVKVL $.
      $( [25-Jul-2006] $) $( [29-Apr-2005] $)
  $}

  $( The square of 0 is 0. $)
  sq0 $p |- ( 0 ^ 2 ) = 0 $=
    ( cc0 c2 cexp co wceq eqid 0cn sq00 mpbir ) ABCDAEAAEAFAGHI $.
    $( [7-Jun-2006] $) $( [6-Jun-2006] $)

  $( If a number is zero, its square is zero.  (Contributed by FL,
     10-Dec-2006.) $)
  sq0i $p |- ( A = 0 -> ( A ^ 2 ) = 0 ) $=
    ( cc0 wceq c2 cexp co opreq1 sq0 syl6eq ) ABCADEFBDEFBABDEGHI $.
    $( [10-Dec-2006] $) $( [10-Dec-2006] $)

  $( The square of 1 is 1. $)
  sq1 $p |- ( 1 ^ 2 ) = 1 $=
    ( c1 c2 cexp co cmul ax1cn sqval mulid1 eqtr ) ABCDAAEDAAFGAFHI $.
    $( [22-Aug-1999] $)

  $( The square of 2 is 4. $)
  sq2 $p |- ( 2 ^ 2 ) = 4 $=
    ( c2 cexp co cmul c4 2cn sqval 2t2e4 eqtr ) AABCAADCEAFGHI $.
    $( [22-Aug-1999] $)

  $( The square of 3 is 9. $)
  sq3 $p |- ( 3 ^ 2 ) = 9 $=
    ( c3 c2 cexp co cmul c9 3re recn sqval 3t3e9 eqtr ) ABCDAAEDFAAGHIJK $.
    $( [16-Jun-2006] $) $( [26-Apr-2006] $)

  $( The cube of 2 is 8. $)
  cu2 $p |- ( 2 ^ 3 ) = 8 $=
    ( c2 c3 cexp co c1 caddc cmul c8 df-3 opreq2i cc wcel cn0 wceq 2cn 2nn0
    expp1t mp2an c4 sq2 opreq1i 4t2e8 eqtr 3eqtr ) ABCDAAEFDZCDZAACDZAGDZHBUEAC
    IJAKLAMLUFUHNOPAAQRUHSAGDHUGSAGTUAUBUCUD $.
    $( [28-Jul-2006] $) $( [2-Aug-2004] $)

  ${
    binom2.1 $e |- A e. CC $.
    binom2.2 $e |- B e. CC $.
    $( The square of a binomial. $)
    binom2 $p |- ( ( A + B ) ^ 2 ) = ( ( ( A ^ 2 ) + ( 2 x.
                ( A x. B ) ) ) + ( B ^ 2 ) ) $=
      ( caddc co cmul c2 cexp addcl adddi adddir mulcom opreq2i eqtr opreq12i
      mulcl addass opreq1i eqtr3 3eqtr sqval 2times 3eqtr4 ) ABEFZUEGFZAAGFZABG
      FZUHEFZEFZBBGFZEFZUEHIFAHIFZHUHGFZEFZBHIFZEFUFUEAGFZUEBGFZEFUGUHEFZUHUKEF
      ZEFZULUEABABCDJZCDKUQUSURUTEUQUGBAGFZEFUSABACDCLVCUHUGEBADCMNOABBCDDLPUSU
      HEFZUKEFVAULUSUHUKUGUHAACCQZABCDQZJVFBBDDQRVDUJUKEUGUHUHVEVFVFRSTUAUEVBUB
      UOUJUPUKEUMUGUNUIEACUBUHVFUCPBDUBPUD $.
      $( [11-Aug-1999] $)

    $( Product of sum and difference binomials. $)
    binom2a $p |- ( ( A + B ) x. ( A - B ) ) = ( ( A ^ 2 ) - ( B ^ 2 ) ) $=
      ( caddc co cmin cmul c2 cexp subcl adddir subdi sqval opreq1i eqtr4
      mulcom opreq12i sqcl mulcl addsubass addcom pncan3 eqtr eqtr3 3eqtr ) ABE
      FABGFZHFAUGHFZBUGHFZEFAIJFZABHFZGFZUKBIJFZGFZEFZUJUMGFZABUGCDABCDKLUHULUI
      UNEUHAAHFZUKGFULAABCCDMUJUQUKGACNOPUIBAHFZBBHFZGFUNBABDCDMUKURUMUSGABCDQB
      DNRPRULUKEFZUMGFUOUPULUKUMUJUKACSZABCDTZKZVBBDSUAUTUJUMGUTUKULEFUJULUKVCV
      BUBUKUJVBVAUCUDOUEUF $.
      $( [17-Jun-2005] $) $( [7-Feb-2005] $)

    $( The squares of two complex numbers are equal iff one number equals the
       other or its negative.  Lemma 15-4.7 of [Gleason] p. 311 and its
       converse. $)
    sqeqor $p |- ( ( A ^ 2 ) = ( B ^ 2 ) <-> ( A = B \/ A = -u B ) ) $=
      ( c2 cexp co wceq caddc cc0 cmin wo cneg cmul binom2a eqeq1i addcl subcl
      mul0or sqcl subeq0 3bitr3r orcom subneg negcl bitr3 orbi12i 3bitr ) AEFGZ
      BEFGZHZABIGZJHZABKGZJHZLZUOUMLABHZABMZHZLULUNNGZJHUIUJKGZJHUPUKUTVAJABCDO
      PULUNABCDQABCDRSUIUJACTBDTUAUBUMUOUCUOUQUMUSABCDUAUMAURKGZJHUSVBULJABCDUD
      PAURCBDUEUAUFUGUH $.
      $( [26-Apr-2006] $) $( [15-Jan-2006] $)

    $( Solutions to the difference of squares set equal to zero. $)
    sqdif0 $p |- ( ( ( A ^ 2 ) - ( B ^ 2 ) ) = 0 <-> ( A = B \/ A = -u B ) ) $=
      ( c2 cexp co cmin cc0 wceq cneg wo sqcl subeq0 sqeqor bitr ) AEFGZBEFGZHG
      IJQRJABJABKJLQRACMBDMNABCDOP $.
      $( [?] $) $( [25-Apr-2006] $)
  $}

  $( The square of a binomial.  (Contributed by FL, 10-Dec-2006.) $)
  binom2t $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + B ) ^ 2 ) =
     ( ( ( A ^ 2 ) + ( 2 x. ( A x. B ) ) ) + ( B ^ 2 ) ) ) $=
    ( cc wcel caddc co c2 cexp cmul wceq cc0 cif opreq1 opreq1d opreq2d
    opreq12d eqeq12d opreq2 0cn elimel binom2 dedth2h ) ACDZBCDZABEFZGHFZAGHFZG
    ABIFZIFZEFZBGHFZEFZJUCAKLZBEFZGHFZUMGHFZGUMBIFZIFZEFZUKEFZJUMUDBKLZEFZGHFZU
    PGUMVAIFZIFZEFZVAGHFZEFZJABKKAUMJZUFUOULUTVIUEUNGHAUMBEMNVIUJUSUKEVIUGUPUIU
    REAUMGHMVIUHUQGIAUMBIMOPNQBVAJZUOVCUTVHVJUNVBGHBVAUMERNVJUSVFUKVGEVJURVEUPE
    VJUQVDGIBVAUMIROOBVAGHMPQUMVAAKCSTBKCSTUAUB $.
    $( [10-Dec-2006] $) $( [27-Nov-2006] $)

  $( If a complex number equals its square, it must be 0 or 1. $)
  sq01t $p |- ( A e. CC -> ( ( A ^ 2 ) = A <-> ( A = 0 \/ A = 1 ) ) ) $=
    ( cc wcel c2 cexp co wceq cc0 c1 wo wa wne wn wi cmul wb sqvalt ax1id
    eqcomd eqeq12d adantr ax1cn mulcant mp3anl3 anabsan bitrd biimpd ex com23
    imp df-ne syl5ibr orrd sq0 opreq1 id 3eqtr4a sq1 jaoi impbid1 ) ABCZADEFZAG
    ZAHGZAIGZJZVAVCVFVAVCKZVDVEVGAHLZVEVDMVAVCVHVENVAVHVCVEVAVHVCVENVAVHKZVCVEV
    IVCAAOFZAIOFZGZVEVAVCVLPVHVAVBVJAVKAQVAVKAARSTUAVAVHVLVEPZVAVAIBCVHVMUBAAIU
    CUDUEUFUGUHUIUJAHUKULUMUHVDVCVEVDHDEFHVBAUNAHDEUOVDUPUQVEIDEFIVBAURAIDEUOVE
    UPUQUSUT $.
    $( [9-Jun-2006] $) $( [6-Jun-2006] $)

  ${
    $d j k A $.  $d j k N $.
    $( Bernoulli's inequality, due to Johan Bernoulli (1667-1748). $)
    bernneq $p |- ( ( A e. RR /\ N e. NN0 /\ -u 1 <_ A ) ->
                 ( 1 + ( A x. N ) ) <_ ( ( 1 + A ) ^ N ) ) $=
      ( vj vk cr wcel cn0 c1 cneg cle wbr cmul co caddc cexp wi wa cv cc0 wceq
      opreq2 opreq2d breq12d imbi2d cc recnt mul01t ax1cn addid1 syl6eq
      axaddcl mpan exp0t syl 1re leid syl5breqr eqbrtrd adantr axaddrcl
      axmulrcl nn0ret sylan2 pm3.26 sylanc reexpclt sylan mulge0t anidms
      anim12i c2 sqge0t sqvalt breqtrd nn0ge0t mul23t nn0cnt adantl syl3anc wb
      addge01t mpbid axaddass axmulcl muladd11t eqtr4d syl2an w3a lemul1it
      ad2antrr 3jca renegcl leadd2t mp3an13 negid breq1i syl6bb biimpa
      ad2ant2r simprr jca letrd axdistr mp3an3 ax1id eqtrd mp3an1 expp1t
      3brtr4d exp43 com12 imp3a a2d nn0ind exp3a 3imp ) AEFZBGFZHIZAJKZHABLMZNM
      ZHANMZBOMZJKZXRXQXTYEPXRXQXTYEXQXTQZHACRZLMZNMZYCYGOMZJKZPYFHASLMZNMZYCSO
      MZJKZPYFHADRZLMZNMZYCYPOMZJKZPYFHAYPHNMZLMZNMZYCUUAOMZJKZPYFYEPCDBYGSTZYK
      YOYFUUFYIYMYJYNJUUFYHYLHNYGSALUAUBYGSYCOUAUCUDYGYPTZYKYTYFUUGYIYRYJYSJUUG
      YHYQHNYGYPALUAUBYGYPYCOUAUCUDYGUUATZYKUUEYFUUHYIUUCYJUUDJUUHYHUUBHNYGUUAA
      LUAUBYGUUAYCOUAUCUDYGBTZYKYEYFUUIYIYBYJYDJUUIYHYAHNYGBALUAUBYGBYCOUAUCUDX
      QYOXTXQAUEFZYOAUFZUUJYMHYNJUUJYMHSNMHUUJYLSHNAUGUBHUHUIUJUUJHYNHJUUJYCUEF
      ZYNHTHUEFZUUJUULUHHAUKULZYCUMUNHUOUPUQURUNUSYPGFZYFYTUUEUUOXQXTYTUUEPZXQU
      UOXTUUPPXQUUOXTYTUUEXQUUOQZXTYTQZQZYRANMZYSYCLMZUUCUUDJUUSUUTYRYCLMZUVAUU
      QUUTEFZUURYREFZXQUVCUUQYRAUTUUQYQEFZUVDXQYPEFZUVEUUOAYPVAZYPVBZVCHEFZUVEU
      VDUOHYQUTULUNZXQUUOVDVEZUSUUQUVBEFZUURUVDYCEFZUVLUUQYRYCVAUVJXQUVMUUOUVIX
      QUVMUOHAUTULZUSZVEUSUUQUVAEFZUURYSEFZUVMUVPUUQYSYCVAUVMUUOUVQXQYCYPVFUVNV
      GZUVOVEUSUUQUUTUVBJKUURUUQUUTUUTYQALMZNMZUVBJUUQSUVSJKZUUTUVTJKZUUQSAALMZ
      YPLMZUVSJUWCEFZUVFQSUWCJKZSYPJKZQSUWDJKUUQUWCYPVHXQUWEUUOUVFXQUWEAAVAVIUV
      HVJXQUWFUUOUWGXQSAVKOMZUWCJAVLXQUUJUWHUWCTUUKAVMUNVNYPVOVJVEUUJUUJYPUEFZU
      WDUVSTUUQAAYPVPXQUUJUUOUUKUSZUWJUUOUWIXQYPVQZVRVSVNUVCUVSEFZUWAUWBVTUUQUU
      TUVSWAUVKXQUVFUWLUUOUVEXQUWLXQUVFQYQAVAUVGXQUVFVDVEUVHVCVEWBUUJUWIUVTUVBT
      XQUUOUUJUWIQZUVTYRAUVSNMNMZUVBYRUEFZUUJUVSUEFZUVTUWNTUWMYRAUVSWCUWMYQUEFZ
      UWOAYPWDZUUMUWQUWOUHHYQUKULUNUUJUWIVDZUWQUUJUWPUWMYQAWDUWRUWSVEVSUWQUUJUV
      BUWNTUWMYQAWEUWRUWSVEWFUUKUWKWGVNUSUVDUVQUVMWHSYCJKZYTQUVBUVAJKUUSYRYSYCW
      IUUSUVDUVQUVMUUQUVDUURUVJUSUUQUVQUURUVRUSXQUVMUUOUURUVNWJWKUUSUWTYTXQXTUW
      TUUOYTXQXTUWTXQXTHXSNMZYCJKZUWTXSEFXQUVIXTUXBVTHUOWLUOXSAHWMWNUXASYCJHUHW
      OWPWQWRWSUUQXTYTWTXAVEXBUUQUUCUUTTZUURUUJUWIUXCXQUUOUWMUUCHYQANMZNMZUUTUW
      MUUBUXDHNUWMUUBYQAHLMZNMZUXDUUJUWIUUMUUBUXGTUHAYPHXCXDUWMUXFAYQNUUJUXFATU
      WIAXEUSUBXFUBUWQUUJUUTUXETZUWMUUMUWQUUJUXHUHHYQAWCXGUWRUWSVEWFUUKUWKWGUSU
      UQUUDUVATZUURUULUUOUXIXQYCYPXHXQUUJUULUUKUUNUNVGUSXIXJXKXLXMXNXOXKXP $.
      $( [11-Oct-2006] $) $( [21-Feb-2005] $)
  $}

