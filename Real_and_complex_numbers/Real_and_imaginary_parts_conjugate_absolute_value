$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Real and imaginary parts; conjugate; absolute value
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Re $. $( Real part of a complex number. $)
  $c Im $. $( Imaginary part of a complex number. $)
  $c * $. $( Complex conjugate function. $)
  $c abs $. $( Absolute value function. $)

  $( Extend class notation to include real part of a complex number. $)
  cre $a class Re $.

  $( Extend class notation to include imaginary part of a complex number. $)
  cim $a class Im $.

  $( Extend class notation to include complex conjugate function. $)
  ccj $a class * $.

  $( Extend class notation to include a function for the absolute value
     (modulus) of a complex number. $)
  cabs $a class abs $.

  ${
    $d x y z w $.
    $( Define a function whose value is the real part of a complex number.  See
       ~ reval for its value, ~ recli for its closure, and ~ replim for its use
       in decomposing a complex number. $)
    df-re $a |- Re = { <. x , y >. | ( x e. CC /\
                 y = U. { z e. RR | E. w e. RR x = ( z + ( _i x. w ) ) } ) } $.

    $( Define a function whose value is the imaginary part of a complex
       number.  See ~ imval for its value, ~ imcli for its closure, and
       ~ replim for its use in decomposing a complex number. $)
    df-im $a |- Im = { <. x , y >. | ( x e. CC /\
                 y = U. { w e. RR | E. z e. RR x = ( z + ( _i x. w ) ) } ) } $.

    $( Define the complex conjugate function.  See ~ cjcli for its closure and
       ~ cjval for its value. $)
    df-cj $a |- * = { <. x , y >. | ( x e. CC /\
                        y = ( ( Re ` x ) - ( _i x. ( Im ` x ) ) ) ) } $.

    $( Define the function for the absolute value (modulus) of a complex
       number.  See ~ abscli for its closure and ~ absval or ~ absval2i for its
       value. $)
    df-abs $a |- abs = { <. x , y >. | ( x e. CC /\ y = ( sqr `
                 ( x x. ( * ` x ) ) ) ) } $.
  $}

  ${
    $d x y z w A $.
    $( The value of the real part of a complex number. $)
    reval $p |- ( A e. CC ->
        ( Re ` A ) = U. { x e. RR | E. y e. RR A = ( x + ( _i x. y ) ) } ) $=
      ( vz vw cv ci cmul co caddc wceq cr wrex crab cuni cc cre eqeq1 rexbidv
      rabbidv unieqd df-re reex rabex uniex fvopab4 ) DECDFZAFGBFHIJIZKZBLMZALN
      ZOCUHKZBLMZALNZOPQUGCKZUKUNUOUJUMALUOUIULBLUGCUHRSTUADEABUBUNUMALUCUDUEUF
      $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)

    $( The value of the imaginary part of a complex number. $)
    imval $p |- ( A e. CC ->
        ( Im ` A ) = U. { y e. RR | E. x e. RR A = ( x + ( _i x. y ) ) } ) $=
      ( vz vw cv ci cmul co caddc wceq cr wrex crab cuni cc cim eqeq1 rexbidv
      rabbidv unieqd df-im reex rabex uniex fvopab4 ) DECDFZAFGBFHIJIZKZALMZBLN
      ZOCUHKZALMZBLNZOPQUGCKZUKUNUOUJUMBLUOUIULALUGCUHRSTUADEABUBUNUMBLUCUDUEUF
      $.
      $( [11-Mar-2008] $) $( [9-May-1999] $)

    $( The real part of a complex number is real. $)
    recl $p |- ( A e. CC -> ( Re ` A ) e. RR ) $=
      ( vx vy cc wcel cre cfv cv ci cmul co caddc wceq cr wrex crab cuni reval
      wreu creur reucl syl eqeltrd ) ADEZAFGABHICHJKLKMCNOZBNPQZNBCARUDUEBNSUFN
      EBCATUEBNUAUBUC $.
      $( [9-May-1999] $)

    $( The imaginary part of a complex number is real. $)
    imcl $p |- ( A e. CC -> ( Im ` A ) e. RR ) $=
      ( vy vx cc wcel cim cfv cv ci cmul co caddc wceq cr wrex crab cuni imval
      wreu creui reucl syl eqeltrd ) ADEZAFGABHICHJKLKMBNOZCNPQZNBCARUDUECNSUFN
      EBCATUECNUAUBUC $.
      $( [9-May-1999] $)

    $( Domain and codomain of the real part function.  (Contributed by Paul
       Chapman, 22-Oct-2007.) $)
    ref $p |- Re : CC --> RR $=
      ( vx vy vz vw cc cr cre wf wfn cv cfv wcel wral ffnfv ci cmul co caddc
      wceq wrex crab cuni reex rabex uniex df-re fnopab2 recl rgen mpbir2an )
      EFGHGEIAJZGKFLZAEMAEFGNABEUKCJODJPQRQSDFTZCFUAZUBGUNUMCFUCUDUEABCDUFUGULA
      EUKUHUIUJ $.
      $( [22-Oct-2007] $)

    $( Domain and codomain of the imaginary part function.  (Contributed by
       Paul Chapman, 22-Oct-2007.) $)
    imf $p |- Im : CC --> RR $=
      ( vx vy vw vz cc cr cim wf wfn cv cfv wcel wral ffnfv ci cmul co caddc
      wceq wrex crab cuni reex rabex uniex df-im fnopab2 imcl rgen mpbir2an )
      EFGHGEIAJZGKFLZAEMAEFGNABEUKCJODJPQRQSCFTZDFUAZUBGUNUMDFUCUDUEABCDUFUGULA
      EUKUHUIUJ $.
      $( [22-Oct-2007] $)

    $( Reconstruct a complex number from its real and imaginary parts. $)
    replim $p |- ( A e. CC -> A = ( ( Re ` A ) + ( _i x. ( Im ` A ) ) ) ) $=
      ( vx vy cc wcel cv cr wa ci cim cfv cmul co caddc wceq cre wex wrex eqid
      wb imcl wi opreq2 opreq2d eqeq2d rexbidv eqeq2 bibi12d imbi2d crab cuni
      wreu creui reuuni1 sylan2 imval eqeq1d adantl bitr4d ex vtoclga mpcom
      mpbiri df-rex sylib recl opreq1 creur reval an4 2exbii eeanv bitri
      sylanbrc simprrl a1d ancld ancrd anim12d imp cru syl eqtr2 syl5bi impr
      simpld opreq1d eqtrd 19.23advv mpd ) ADEZBFZGEZCFZGEZHZAWLIAJKZLMZNMZOZAA
      PKZIWNLMZNMZOZHZHZCQBQZAXAWRNMZOZWKWMWTHZBQZWOXDHZCQZXGWKWTBGRZXKWKXNWQWQ
      OZWQSWQGEZWKXNXOTZAUAZWKAWLXBNMZOZBGRZWQWNOZTZUBWKXQUBCWQGWNWQOZYCXQWKYDY
      AXNYBXOYDXTWTBGYDXSWSAYDXBWRWLNWNWQILUCUDUEUFWNWQWQUGUHUIWOWKYCWOWKHYAYAC
      GUJUKZWNOZYBWKWOYACGULYAYFTBCAUMYACGUNUOWKYBYFTWOWKWQYEWNBCAUPUQURUSUTVAV
      BVCWTBGVDVEWKXDCGRZXMWKYGXAXAOZXASXAGEZWKYGYHTZAVFZWKXTCGRZXAWLOZTZUBWKYJ
      UBBXAGWLXAOZYNYJWKYOYLYGYMYHYOXTXDCGYOXSXCAWLXAXBNVGUEUFWLXAXAUGUHUIWMWKY
      NWMWKHYLYLBGUJUKZWLOZYMWKWMYLBGULYLYQTBCAVHYLBGUNUOWKYMYQTWMWKXAYPWLBCAVI
      UQURUSUTVAVBVCXDCGVDVEXGXJXLHZCQBQXKXMHXFYRBCWMWOWTXDVJVKXJXLBCVLVMVNWKXF
      XIBCWKXFXIWKXFHZAWSXHWKWPWTXDVOYSWLXAWRNYSYOYBWKWPXEYOYBHZWKWPHZWSXCOZYTX
      EUUAWMXPHZYIWOHZHZUUBYTTWKWPUUEWKWMUUCWOUUDWKWMXPWKXPWMXRVPVQWKWOYIWKYIWO
      YKVPVRVSVTWLWQXAWNWAWBAWSXCWCWDWEWFWGWHUTWIWJ $.
      $( [11-Mar-2008] $) $( [10-May-1999] $)

    $( The absolute value (modulus) of a complex number.  Proposition 10-3.7(a)
       of [Gleason] p. 133. $)
    absval $p |- ( A e. CC -> ( abs ` A ) = ( sqr ` ( A x. ( * ` A ) ) ) ) $=
      ( vx vy cv ccj cfv cmul co csqr cc cabs wceq id fveq2 opreq12d fveq2d
      df-abs fvex fvopab4 ) BCABDZTEFZGHZIFAAEFZGHZIFJKTALZUBUDIUETAUAUCGUEMTAE
      NOPBCQUDIRS $.
      $( [27-Jul-1999] $)
  $}

  ${
    $d x y A $.
    $( Value of the conjugate of a complex number.  The value is the real part
       minus ` _i ` times the imaginary part.  Definition 10-3.2 of [Gleason]
       p. 132. $)
    cjval $p |- ( A e. CC ->
                      ( * ` A ) = ( ( Re ` A ) - ( _i x. ( Im ` A ) ) ) ) $=
      ( vx vy cv cre cfv ci cim cmul co cmin cc ccj wceq fveq2 opreq2d
      opreq12d df-cj oprex fvopab4 ) BCABDZEFZGUAHFZIJZKJAEFZGAHFZIJZKJLMUAANZU
      BUEUDUGKUAAEOUHUCUFGIUAAHOPQBCRUEUGKST $.
      $( [11-Mar-2008] $) $( [10-May-1999] $)
  $}

  $( The conjugate of a complex number is a complex number (closure law). $)
  cjcl $p |- ( A e. CC -> ( * ` A ) e. CC ) $=
    ( cc wcel ccj cfv cre ci cim cmul co cmin cjval recl recnd axicn imcl
    mulcl sylancr subcl syl11anc eqeltrd ) ABCZADEAFEZGAHEZIJZKJZBALUBUCBCUEBCZ
    UFBCUBUCAMNUBGBCUDBCUGOUBUDAPNGUDQRUCUESTUA $.
    $( [10-May-1999] $)

  ${
    recl.1 $e |- A e. CC $.
    $( The real part of a complex number is real (closure law). $)
    recli $p |- ( Re ` A ) e. RR $=
      ( cc wcel cre cfv cr recl ax-mp ) ACDAEFGDBAHI $.
      $( [11-May-1999] $)

    $( The imaginary part of a complex number is real (closure law). $)
    imcli $p |- ( Im ` A ) e. RR $=
      ( cc wcel cim cfv cr imcl ax-mp ) ACDAEFGDBAHI $.
      $( [11-May-1999] $)

    $( Closure law for complex conjugate. $)
    cjcli $p |- ( * ` A ) e. CC $=
      ( cc wcel ccj cfv cjcl ax-mp ) ACDAEFCDBAGH $.
      $( [11-May-1999] $)

    $( Construct a complex number from its real and imaginary parts. $)
    replimi $p |- A = ( ( Re ` A ) + ( _i x. ( Im ` A ) ) ) $=
      ( cc wcel cre cfv ci cim cmul co caddc wceq replim ax-mp ) ACDAAEFGAHFIJK
      JLBAMN $.
      $( [11-Mar-2008] $) $( [1-Oct-1999] $)
  $}

  $( The real part of a complex number representation.  Definition 10-3.1 of
     [Gleason] p. 132. $)
  crre $p |- ( ( A e. RR /\ B e. RR ) -> ( Re ` ( A + ( _i x. B ) ) ) = A ) $=
    ( cr wcel wa ci cmul co caddc cre cfv wceq cim cc recn axicn mulcl sylancr
    addcl syl2an replim syl eqcomd wb recl imcl jca cru mpancom mpbid simpld )
    ACDZBCDZEZAFBGHZIHZJKZALZUPMKZBLZUNUQFUSGHIHZUPLZURUTEZUNUPVAUNUPNDZUPVALUL
    ANDUONDZVDUMAOUMFNDBNDVEPBOFBQRAUOSTZUPUAUBUCUQCDZUSCDZEUNVBVCUDUNVGVHUNVDV
    GVFUPUEUBUNVDVHVFUPUFUBUGUQUSABUHUIUJUK $.
    $( [11-Mar-2005] $) $( [12-May-2005] $)

  $( The real part of a complex number representation.  Definition 10-3.1 of
     [Gleason] p. 132. $)
  crim $p |- ( ( A e. RR /\ B e. RR ) -> ( Im ` ( A + ( _i x. B ) ) ) = B ) $=
    ( cr wcel wa ci cmul co caddc cre cfv wceq cim cc recn axicn mulcl sylancr
    addcl syl2an replim syl eqcomd wb recl imcl jca cru mpancom mpbid simprd )
    ACDZBCDZEZAFBGHZIHZJKZALZUPMKZBLZUNUQFUSGHIHZUPLZURUTEZUNUPVAUNUPNDZUPVALUL
    ANDUONDZVDUMAOUMFNDBNDVEPBOFBQRAUOSTZUPUAUBUCUQCDZUSCDZEUNVBVCUDUNVGVHUNVDV
    GVFUPUEUBUNVDVHVFUPUFUBUGUQUSABUHUIUJUK $.
    $( [11-Mar-2005] $) $( [12-May-2005] $)

  ${
    crre.1 $e |- A e. RR $.
    crre.2 $e |- B e. RR $.
    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crrei $p |- ( Re ` ( A + ( _i x. B ) ) ) = A $=
      ( cr wcel ci cmul co caddc cre cfv wceq crre mp2an ) AEFBEFAGBHIJIKLAMCDA
      BNO $.
      $( [11-Mar-2005] $) $( [10-May-1999] $)

    $( The imaginary part of a complex number representation.  Definition
       10-3.1 of [Gleason] p. 132. $)
    crimi $p |- ( Im ` ( A + ( _i x. B ) ) ) = B $=
      ( cr wcel ci cmul co caddc cim cfv wceq crim mp2an ) AEFBEFAGBHIJIKLBMCDA
      BNO $.
      $( [11-Mar-2005] $) $( [10-May-1999] $)
  $}

  $( The imaginary part of a complex number in terms of the real part
     function. $)
  imre $p |- ( A e. CC -> ( Im ` A ) = ( Re ` ( -u _i x. A ) ) ) $=
    ( cc wcel ci cneg cmul co cre cfv cim caddc replim opreq2d wceq recl recnd
    axicn imcl mulcl sylancr negcli adddi mp3an1 syl11anc mulneg12 mulass
    mp3an12 syl c1 mulid2 mulneg1i ixi negeqi ax1cn negnegi eqtri opreq1i
    syl5eq eqtr3d opreq12d negcl addcom eqtrd 3eqtrd fveq2d cr renegcl crre
    eqtr2d ) ABCZDEZAFGZHIAJIZDAHIZEZFGZKGZHIZVMVJVLVQHVJVLVKVNDVMFGZKGZFGZVKVN
    FGZVKVSFGZKGZVQVJAVTVKFALMVJVNBCZVSBCZWAWDNZVJVNAOZPZVJDBCZVMBCZWFQVJVMARZP
    ZDVMSTVKBCZWEWFWGDQUAZVKVNVSUBUCUDVJWDVPVMKGZVQVJWBVPWCVMKVJWJWEWBVPNQWIDVN
    UETVJVKDFGZVMFGZWCVMVJWKWRWCNZWMWNWJWKWSWOQVKDVMUFUGUHVJUIVMFGZVMWRVJWKWTVM
    NWMVMUJUHWQUIVMFWQDDFGZEZUIDDQQUKXBUIEZEUIXAXCULUMUIUNUOUPUPUQURUSUTVJVPBCZ
    WKWPVQNVJWJVOBCZXDQVJWEXEWIVNVAUHDVOSTWMVPVMVBUDVCVDVEVJVMVFCVOVFCZVRVMNWLV
    JVNVFCXFWHVNVGUHVMVOVHUDVI $.
    $( [30-Jul-2008] $) $( [12-May-2005] $)

  $( The imaginary part of a real number is 0. $)
  reim0 $p |- ( A e. RR -> ( Im ` A ) = 0 ) $=
    ( cr wcel cim cfv ci cmul co cc0 wceq cc recn imcl syl cre cmin caddc
    replim eqcomd wb recl axicn recnd mulcl sylancr subadd syl111anc mpbird
    resubcl mpdan eqeltrrd rimul syl11anc ) ABCZADEZBCZFUOGHZBCUOIJUNAKCZUPALZA
    MNZUNAAOEZPHZUQBUNVBUQJZVAUQQHZAJZUNAVDUNURAVDJUSARNSUNURVAKCZUQKCZVCVETUSU
    NVABCZVFUNURVHUSAUANZVALNUNFKCUOKCVGUBUNUOUTUCFUOUDUEAVAUQUFUGUHUNVHVBBCVIA
    VAUIUJUKUOULUM $.
    $( [18-Mar-2005] $)

  $( A number is real iff its imaginary part is 0. $)
  reim0b $p |- ( A e. CC -> ( A e. RR <-> ( Im ` A ) = 0 ) ) $=
    ( cc wcel cr cim cfv cc0 wceq reim0 wa cre ci cmul co caddc replim adantr
    opreq2 axicn mul01i syl6eq opreq2d recl recnd addid1 syl sylan9eqr eqtrd
    eqeltrd ex impbid2 ) ABCZADCZAEFZGHZAIULUOUMULUOJZAAKFZDUPAUQLUNMNZONZUQULA
    USHUOAPQUOULUSUQGONZUQUOURGUQOUOURLGMNGUNGLMRLSTUAUBULUQBCUTUQHULUQAUCZUDUQ
    UEUFUGUHULUQDCUOVAQUIUJUK $.
    $( [24-Jul-2008] $) $( [26-Sep-2005] $)

  $( A number is real iff it equals its real part.  Proposition 10-3.4(f) of
     [Gleason] p. 133. $)
  rereb $p |- ( A e. CC -> ( A e. RR <-> ( Re ` A ) = A ) ) $=
    ( cc wcel cr cre cfv wceq wa ci cim cmul co caddc cc0 replim adantr reim0
    opreq2d axicn mul01i syl6eq adantl recl recnd addid1 syl 3eqtrrd simpr
    eqeltrrd impbida ) ABCZADCZAEFZAGZUKULHZAUMIAJFZKLZMLZUMNMLZUMUKAURGULAOPUO
    UQNUMMULUQNGUKULUQINKLNULUPNIKAQRISTUAUBRUKUSUMGZULUKUMBCUTUKUMAUCZUDUMUEUF
    PUGUKUNHUMADUKUNUHUKUMDCUNVAPUIUJ $.
    $( [20-Aug-2008] $)

  $( A product with a nonzero real multiplier is real iff the multiplicand is
     real. $)
  mulre $p |- ( ( A e. CC /\ B e. RR /\ B =/= 0 )
         -> ( A e. RR <-> ( B x. A ) e. RR ) ) $=
    ( cc wcel cr cc0 wne w3a cre cfv wceq cmul co wb rereb 3ad2ant1 wa recl
    recnd simp1 recn anim1i 3adant1 mulcan syl111anc ci cim caddc adantr
    adantl axicn imcl mulcl sylancr adddi replim opreq2d mul12 mp3an1 syl2an
    3eqtr4d fveq2d remulcl sylan2 crre syl11anc eqtr2d eqeq1d sylan syl bitr4d
    ancoms 3adant3 3bitr2d ) ACDZBEDZBFGZHZAEDZAIJZAKZBVTLMZBALMZKZWCEDZVOVPVSW
    ANVQAOPVRVTCDZVOBCDZVQQZWDWANVOVPWFVQVOVTARZSZPVOVPVQTVPVQWHVOVPWGVQBUAZUBU
    CVTABUDUEVOVPWDWENZVQVPVOWLVPVOQZWDWCIJZWCKZWEWMWBWNWCWMWNWBUFBAUGJZLMZLMZU
    HMZIJZWBWMWCWSIWMBVTUFWPLMZUHMZLMZWBBXALMZUHMZWCWSWMWGWFXACDZXCXEKVPWGVOWKU
    IVOWFVPWJUJVOXFVPVOUFCDZWPCDZXFUKVOWPAULZSZUFWPUMUNUJBVTXAUOUEWMAXBBLVOAXBK
    VPAUPUJUQWMWRXDWBUHVPWGXHWRXDKZVOWKXJXGWGXHXKUKUFBWPURUSUTUQVAVBWMWBEDZWQED
    ZWTWBKVOVPVTEDXLWIBVTVCVDVOVPWPEDXMXIBWPVCVDWBWQVEVFVGVHWMWCCDZWEWONVPWGVOX
    NWKBAUMVIWCOVJVKVLVMVN $.
    $( [21-Aug-2008] $)

  ${
    cjcji.1 $e |- A e. CC $.
    $( The conjugate of the conjugate is the original complex number.
       Proposition 10-3.4(e) of [Gleason] p. 133. $)
    cjcji $p |- ( * ` ( * ` A ) ) = A $=
      ( ccj cfv cre ci cim cmul co cmin cneg caddc recli recni axicn imcli
      mulcli negsubi mulneg2i opreq2i cc wcel wceq cjval ax-mp 3eqtr4ri fveq2i
      renegcli crrei eqtri crimi 3eqtri opreq12i cjcli replimi subnegi eqtr4i
      3eqtr4i ) ACDZEDZFUSGDZHIZJIZAEDZFAGDZHIZKZJIZUSCDZAUTVDVBVGJUTVDFVEKZHIZ
      LIZEDVDUSVLEVDVGLIVDVFJIZVLUSVDVFVDABMZNZFVEOVEABPZNZQZRVKVGVDLFVEOVQSZTA
      UAUBUSVMUCBAUDUEUFZUGVDVJVNVEVPUHZUIUJVBFVLGDZHIVKVGVAWBFHUSVLGVTUGTWBVJF
      HVDVJVNWAUKTVSULUMUSUAUBVIVCUCABUNUSUDUEAVDVFLIVHABUOVDVFVOVRUPUQUR $.
      $( [11-May-1999] $)

    $( A number is real iff its imaginary part is 0. $)
    reim0bi $p |- ( A e. RR <-> ( Im ` A ) = 0 ) $=
      ( cc wcel cr cim cfv cc0 wceq wb reim0b ax-mp ) ACDAEDAFGHIJBAKL $.
      $( [29-May-1999] $)

    $( A real number equals its real part.  Proposition 10-3.4(f) of [Gleason]
       p. 133. $)
    rerebi $p |- ( A e. RR <-> ( Re ` A ) = A ) $=
      ( cc wcel cr cre cfv wceq wb rereb ax-mp ) ACDAEDAFGAHIBAJK $.
      $( [24-Jul-2008] $) $( [27-Oct-1999] $)

    $( A number is real iff it equals its complex conjugate.  Proposition
       10-3.4(f) of [Gleason] p. 133. $)
    cjrebi $p |- ( A e. RR <-> ( * ` A ) = A ) $=
      ( cr wcel cim cfv cc0 wceq ccj reim0bi cneg imcli recni eqnegi ci cmul
      co axicn mulneg2i eqeq2i negcli ine0 mulcani cre caddc replimi cmin cc
      cjval ax-mp recli mulcli negsubi eqtr4i eqeq12i addcani bitr2i 3bitr3i
      bitr3i eqcom 3bitri ) ACDAEFZGHZAAIFZHZVDAHABJVCVBVBKZHZVEVBVBABLMZNOVBPQ
      ZOVFPQZHVIVIKZHZVGVEVJVKVIOVBRVHSTVBVFOVHVBVHUARUBUCVEAUDFZVIUEQZVMVKUEQZ
      HVLAVNVDVOABUFVDVMVIUGQZVOAUHDVDVPHBAUIUJVMVIVMABUKMZOVBRVHULZUMUNUOVMVIV
      KVQVRVIVRUAUPUQURUSAVDUTVA $.
      $( [11-Oct-1999] $)

    $( Real part of a complex conjugate. $)
    recji $p |- ( Re ` ( * ` A ) ) = ( Re ` A ) $=
      ( ccj cfv cre ci cim cneg cmul co caddc cmin recli recni axicn imcli
      mulcli negsubi mulneg2i opreq2i cc wcel wceq cjval ax-mp 3eqtr4ri fveq2i
      renegcli crrei eqtri ) ACDZEDAEDZFAGDZHZIJZKJZEDULUKUPEULFUMIJZHZKJULUQLJ
      ZUPUKULUQULABMZNFUMOUMABPZNZQRUOURULKFUMOVBSTAUAUBUKUSUCBAUDUEUFUGULUNUTU
      MVAUHUIUJ $.
      $( [2-Oct-1999] $)

    $( Imaginary part of a complex conjugate. $)
    imcji $p |- ( Im ` ( * ` A ) ) = -u ( Im ` A ) $=
      ( ccj cfv cim cre ci cneg cmul co caddc cmin recli recni axicn imcli
      mulcli negsubi mulneg2i opreq2i cc wcel wceq cjval ax-mp 3eqtr4ri fveq2i
      renegcli crimi eqtri ) ACDZEDAFDZGAEDZHZIJZKJZEDUNUKUPEULGUMIJZHZKJULUQLJ
      ZUPUKULUQULABMZNGUMOUMABPZNZQRUOURULKGUMOVBSTAUAUBUKUSUCBAUDUEUFUGULUNUTU
      MVAUHUIUJ $.
      $( [2-Oct-1999] $)

    ${
      readdi.2 $e |- B e. CC $.
      $( Real part distributes over addition. $)
      readdi $p |- ( Re ` ( A + B ) ) = ( ( Re ` A ) + ( Re ` B ) ) $=
        ( caddc co cre cfv ci cim cmul recli recni axicn imcli mulcli add4i
        replimi opreq12i adddii opreq2i 3eqtr4i fveq2i readdcli crrei eqtri )
        ABEFZGHAGHZBGHZEFZIAJHZBJHZEFZKFZEFZGHUJUGUOGUHIUKKFZEFZUIIULKFZEFZEFUJ
        UPUREFZEFUGUOUHUPUIURUHACLZMIUKNUKACOZMZPUIBDLZMIULNULBDOZMZPQAUQBUSEAC
        RBDRSUNUTUJEIUKULNVCVFTUAUBUCUJUMUHUIVAVDUDUKULVBVEUDUEUF $.
        $( [28-Jul-1999] $)

      $( Imaginary part distributes over addition. $)
      imaddi $p |- ( Im ` ( A + B ) ) = ( ( Im ` A ) + ( Im ` B ) ) $=
        ( caddc co cim cfv cre ci cmul recli recni axicn imcli mulcli add4i
        replimi opreq12i adddii opreq2i 3eqtr4i fveq2i readdcli crimi eqtri )
        ABEFZGHAIHZBIHZEFZJAGHZBGHZEFZKFZEFZGHUMUGUOGUHJUKKFZEFZUIJULKFZEFZEFUJ
        UPUREFZEFUGUOUHUPUIURUHACLZMJUKNUKACOZMZPUIBDLZMJULNULBDOZMZPQAUQBUSEAC
        RBDRSUNUTUJEJUKULNVCVFTUAUBUCUJUMUHUIVAVDUDUKULVBVEUDUEUF $.
        $( [28-Jul-1999] $)

      $( Real part of a product. $)
      remuli $p |- ( Re ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Re ` B ) ) - ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( cmul co cre cfv cim cmin ci caddc replimi opreq12i recli recni imcli
        crmuli eqtri fveq2i remulcli resubcli readdcli crrei ) ABEFZGHAGHZBGHZE
        FZAIHZBIHZEFZJFZKUFUJEFZUIUGEFZLFZEFLFZGHULUEUPGUEUFKUIEFLFZUGKUJEFLFZE
        FUPAUQBUREACMBDMNUFUIUGUJUFACOZPUIACQZPUGBDOZPUJBDQZPRSTULUOUHUKUFUGUSV
        AUAUIUJUTVBUAUBUMUNUFUJUSVBUAUIUGUTVAUAUCUDS $.
        $( [28-Jul-1999] $)

      $( Imaginary part of a product. $)
      immuli $p |- ( Im ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Im ` B ) ) + ( ( Im ` A ) x. ( Re ` B ) ) ) $=
        ( cmul co cim cfv cre cmin ci caddc replimi opreq12i recli recni imcli
        crmuli eqtri fveq2i remulcli resubcli readdcli crimi ) ABEFZGHAIHZBIHZE
        FZAGHZBGHZEFZJFZKUFUJEFZUIUGEFZLFZEFLFZGHUOUEUPGUEUFKUIEFLFZUGKUJEFLFZE
        FUPAUQBUREACMBDMNUFUIUGUJUFACOZPUIACQZPUGBDOZPUJBDQZPRSTULUOUHUKUFUGUSV
        AUAUIUJUTVBUAUBUMUNUFUJUSVBUAUIUGUTVAUAUCUDS $.
        $( [28-Jul-1999] $)

      $( Complex conjugate distributes over addition.  Proposition 10-3.4(a) of
         [Gleason] p. 133. $)
      cjaddi $p |- ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) $=
        ( caddc co cre cfv ci cim cmul cmin ccj readdi imaddi opreq2i axicn
        imcli recni adddii eqtri opreq12i recli mulcli addsub4i cc wcel wceq
        addcli cjval ax-mp 3eqtr4i ) ABEFZGHZIUMJHZKFZLFZAGHZIAJHZKFZLFZBGHZIBJ
        HZKFZLFZEFZUMMHZAMHZBMHZEFUQURVBEFZUTVDEFZLFVFUNVJUPVKLABCDNUPIUSVCEFZK
        FVKUOVLIKABCDOPIUSVCQUSACRSZVCBDRSZTUAUBURVBUTVDURACUCSVBBDUCSIUSQVMUDI
        VCQVNUDUEUAUMUFUGVGUQUHABCDUIUMUJUKVHVAVIVEEAUFUGVHVAUHCAUJUKBUFUGVIVEU
        HDBUJUKUBUL $.
        $( [28-Jul-1999] $)

      $( Complex conjugate distributes over multiplication.  Proposition
         10-3.4(c) of [Gleason] p. 133. $)
      cjmuli $p |- ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) $=
        ( cmul co cre cfv ci cim cmin cneg caddc ccj recli recni mulcli imcli
        subcli axicn addcli negsubi mul2negi eqcomi opreq2i mulneg2i negdii
        mulneg1i opreq12i eqtr4i eqtr3i remuli immuli negcli crmuli 3eqtr4i cc
        wcel wceq cjval ax-mp 3eqtr4ri ) ABEFZGHZIVCJHZEFZKFZAGHZIAJHZLZEFZMFZB
        GHZIBJHZLZEFZMFZEFZVCNHZANHZBNHZEFVHVMEFZVIVNEFZKFZIVHVNEFZVIVMEFZMFZEF
        ZKFZWBVJVOEFZKFZIVHVOEFZVJVMEFZMFZEFZMFZVGVRWDWHLZMFWIWPWDWHWBWCVHVMVHA
        COPZVMBDOPZQVIVNVIACRPZVNBDRPZQSIWGTWEWFVHVNWRXAQZVIVMWTWSQZUAZQUBWDWKW
        QWOMWCWJWBKWJWCVIVNWTXAUCUDUEIWGLZEFWQWOIWGTXDUFXEWNIEXEWELZWFLZMFWNWEW
        FXBXCUGWLXFWMXGMVHVNWRXAUFVIVMWTWSUHUIUJUEUKUIUKVDWDVFWHKABCDULVEWGIEAB
        CDUMUEUIVHVJVMVOWRVIWTUNWSVNXAUNUOUPVCUQURVSVGUSABCDQVCUTVAVTVLWAVQEVHI
        VIEFZLZMFVHXHKFZVLVTVHXHWRIVITWTQUBVKXIVHMIVITWTUFUEAUQURVTXJUSCAUTVAVB
        VMIVNEFZLZMFVMXKKFZVQWAVMXKWSIVNTXAQUBVPXLVMMIVNTXAUFUEBUQURWAXMUSDBUTV
        AVBUIUP $.
        $( [28-Jul-1999] $)

      $( Standard inner product on complex numbers. $)
      ipcni $p |- ( Re ` ( A x. ( * ` B ) ) ) =
             ( ( ( Re ` A ) x. ( Re ` B ) ) + ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( ccj cfv cmul co cre cim cmin cneg caddc cjcli remuli recli remulcli
        recni imcli negsubi recji opreq2i wceq imcji negcon2i mpbi mulneg2i
        eqtr2i opreq12i 3eqtr2i ) ABEFZGHIFAIFZUKIFZGHZAJFZUKJFZGHZKHUNUQLZMHUL
        BIFZGHZUOBJFZGHZMHAUKCBDNZOUNUQUNULUMACPUKVCPQRUQUOUPACSZUKVCSZQRTUNUTU
        RVBMUMUSULGBDUAUBVBUOUPLZGHURVAVFUOGUPVALUCVAVFUCBDUDUPVAUPVERZVABDSRUE
        UFUBUOUPUOVDRVGUGUHUIUJ $.
        $( [2-Oct-1999] $)
    $}

    $( A complex number times its conjugate is real. $)
    cjmulrcli $p |- ( A x. ( * ` A ) ) e. RR $=
      ( ccj cfv cmul co cr wcel wceq cjcji opreq2i cjcli cjmuli mulcomi
      3eqtr4i mulcli cjrebi mpbir ) AACDZEFZGHTCDZTISSCDZEFSAEFUATUBASEABJKASBA
      BLZMASBUCNOTASBUCPQR $.
      $( [11-May-1999] $)

    $( A complex number times its conjugate. $)
    cjmulvali $p |- ( A x. ( * ` A ) ) =
                   ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) $=
      ( cre cfv c2 cexp co cim caddc cmul ccj recli recni sqvali imcli
      opreq12i ipcni cr wcel wceq cjmulrcli rerebi mpbi 3eqtr2ri ) ACDZEFGZAHDZ
      EFGZIGUEUEJGZUGUGJGZIGAAKDJGZCDZUKUFUIUHUJIUEUEABLMNUGUGABOMNPAABBQUKRSUL
      UKTABUAZUKUKUMMUBUCUD $.
      $( [2-Oct-1999] $)

    $( A complex number times its conjugate is nonnegative. $)
    cjmulge0i $p |- 0 <_ ( A x. ( * ` A ) ) $=
      ( cc0 cre cfv c2 cexp co cim caddc ccj cmul cle wbr recli sqge0i imcli
      resqcli addge0i mp2an cjmulvali breqtrri ) CADEZFGHZAIEZFGHZJHZAAKELHMCUD
      MNCUFMNCUGMNUCABOZPUEABQZPUDUFUCUHRUEUIRSTABUAUB $.
      $( [28-May-1999] $)

    $( Real part of negative. $)
    renegi $p |- ( Re ` -u A ) = -u ( Re ` A ) $=
      ( cneg cre cfv ci cim cmul co caddc recli recni axicn imcli mulcli
      negdii replimi negeqi mulneg2i opreq2i 3eqtr4i fveq2i renegcli crrei
      eqtri ) ACZDEADEZCZFAGEZCZHIZJIZDEUHUFULDUGFUIHIZJIZCUHUMCZJIUFULUGUMUGAB
      KZLFUIMUIABNZLZOPAUNABQRUKUOUHJFUIMURSTUAUBUHUJUGUPUCUIUQUCUDUE $.
      $( [24-Jul-2008] $) $( [2-Aug-1999] $)

    $( The negative of a real is real. $)
    negrebi $p |- ( -u A e. RR <-> A e. RR ) $=
      ( cneg cr wcel cre cfv wceq negcli rerebi renegi eqeq1i recli recni
      neg11i 3bitri eleq1 mpbii sylbi renegcl impbii ) ACZDEZADEZUCAFGZAHZUDUCU
      BFGZUBHUECZUBHUFUBABIJUGUHUBABKLUEAUEABMZNBOPUFUEDEUDUIUEADQRSATUA $.
      $( [11-Aug-1999] $)

    $( Imaginary part of negative. $)
    imnegi $p |- ( Im ` -u A ) = -u ( Im ` A ) $=
      ( cneg cim cfv cre ci cmul co caddc recli recni axicn imcli mulcli
      negdii replimi negeqi mulneg2i opreq2i 3eqtr4i fveq2i renegcli crimi
      eqtri ) ACZDEAFEZCZGADEZCZHIZJIZDEUJUFULDUGGUIHIZJIZCUHUMCZJIUFULUGUMUGAB
      KZLGUIMUIABNZLZOPAUNABQRUKUOUHJGUIMURSTUAUBUHUJUGUPUCUIUQUCUDUE $.
      $( [24-Jul-2008] $) $( [2-Aug-1999] $)

    $( Complex conjugate of negative. $)
    cjnegi $p |- ( * ` -u A ) = -u ( * ` A ) $=
      ( cneg cre cfv ci cim cmul co cmin ccj caddc recli recni negcli axicn
      imcli mulcli negsubi negdii renegi imnegi opreq2i mulneg2i eqtri
      opreq12i 3eqtr4ri negeqi cc wcel wceq cjval ax-mp 3eqtr4i ) ACZDEZFUOGEZH
      IZJIZADEZFAGEZHIZJIZCZUOKEZAKEZCUSUTVBCZLIZCZVDUTCZVGCLIVJVGJIVIUSVJVGUTU
      TABMNZOVBFVAPVAABQNZRZOZSUTVGVKVNTUPVJURVGJABUAURFVACZHIVGUQVOFHABUBUCFVA
      PVLUDUEUFUGVHVCUTVBVKVMSUHUEUOUIUJVEUSUKABOUOULUMVFVCAUIUJVFVCUKBAULUMUHU
      N $.
      $( [2-Aug-1999] $)

    $( A number plus its conjugate is twice its real part.  Compare Proposition
       10-3.4(h) of [Gleason] p. 133. $)
    addcji $p |- ( A + ( * ` A ) ) = ( 2 x. ( Re ` A ) ) $=
      ( cre cfv ci cim cmul co caddc cneg ccj c2 cc0 recli recni axicn imcli
      mulcli negcli add4i negidi opreq2i addcli addid1i 3eqtri replimi cmin cc
      wcel wceq cjval ax-mp negsubi eqtr4i opreq12i 2timesi 3eqtr4i ) ACDZEAFDZ
      GHZIHZURUTJZIHZIHZURURIHZAAKDZIHLURGHVDVEUTVBIHZIHVEMIHVEURUTURVBURABNOZE
      USPUSABQORZVHUTVISTVGMVEIUTVIUAUBVEURURVHVHUCUDUEAVAVFVCIABUFVFURUTUGHZVC
      AUHUIVFVJUJBAUKULURUTVHVIUMUNUOURVHUPUQ $.
      $( [2-Oct-1999] $)
  $}

  $( A real number equals its real part.  One direction of Proposition
     10-3.4(f) of [Gleason] p. 133.  (Contributed by Paul Chapman,
     7-Sep-2007.) $)
  rere $p |- ( A e. RR -> ( Re ` A ) = A ) $=
    ( cr wcel cre cfv wceq cc wb recn rereb syl ibi ) ABCZADEAFZMAGCMNHAIAJKL
    $.
    $( [7-Sep-2007] $)

  $( A number is real iff it equals its complex conjugate.  Proposition
     10-3.4(f) of [Gleason] p. 133. $)
  cjreb $p |- ( A e. CC -> ( A e. RR <-> ( * ` A ) = A ) ) $=
    ( cc wcel cr ccj cfv wceq wb cc0 cif eleq1 fveq2 id eqeq12d bibi12d 0cn
    elimel cjrebi dedth ) ABCZADCZAEFZAGZHTAIJZDCZUDEFZUDGZHAIAUDGZUAUEUCUGAUDD
    KUHUBUFAUDAUDELUHMNOUDAIBPQRS $.
    $( [2-Jul-2005] $)

  $( A complex number times its conjugate is real. $)
  cjmulrcl $p |- ( A e. CC -> ( A x. ( * ` A ) ) e. RR ) $=
    ( cc wcel ccj cfv cmul co cr cc0 cif wceq id fveq2 opreq12d eleq1d 0cn
    elimel cjmulrcli dedth ) ABCZAADEZFGZHCTAIJZUCDEZFGZHCAIAUCKZUBUEHUFAUCUAUD
    FUFLAUCDMNOUCAIBPQRS $.
    $( [26-Mar-2005] $)

  $( A complex number times its conjugate. $)
  cjmulval $p |- ( A e. CC -> ( A x. ( * ` A ) ) =
                 ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) ) $=
    ( cc wcel ccj cfv cmul co cre c2 cexp cim caddc wceq cif id fveq2 opreq12d
    opreq1d eqeq12d 2cn elimel cjmulvali dedth ) ABCZAADEZFGZAHEZIJGZAKEZIJGZLG
    ZMUDAINZULDEZFGZULHEZIJGZULKEZIJGZLGZMAIAULMZUFUNUKUSUTAULUEUMFUTOAULDPQUTU
    HUPUJURLUTUGUOIJAULHPRUTUIUQIJAULKPRQSULAIBTUAUBUC $.
    $( [1-Feb-2007] $)

  $( A complex number times its conjugate is nonnegative. $)
  cjmulge0 $p |- ( A e. CC -> 0 <_ ( A x. ( * ` A ) ) ) $=
    ( cc wcel cc0 ccj cfv cmul co cle wbr cif wceq id fveq2 opreq12d breq2d
    0cn elimel cjmulge0i dedth ) ABCZDAAEFZGHZIJDUAADKZUDEFZGHZIJADAUDLZUCUFDIU
    GAUDUBUEGUGMAUDENOPUDADBQRST $.
    $( [26-Mar-2005] $)

  $( Real part of negative. $)
  reneg $p |- ( A e. CC -> ( Re ` -u A ) = -u ( Re ` A ) ) $=
    ( cc wcel cneg cre cfv wceq cc0 cif negeq fveq2d fveq2 negeqd eqeq12d 0cn
    elimel renegi dedth ) ABCZADZEFZAEFZDZGSAHIZDZEFZUDEFZDZGAHAUDGZUAUFUCUHUIT
    UEEAUDJKUIUBUGAUDELMNUDAHBOPQR $.
    $( [17-Mar-2005] $)

  $( Real part distributes over addition. $)
  readd $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Re ` ( A + B ) ) = ( ( Re ` A ) + ( Re ` B ) ) ) $=
    ( cc wcel caddc co cre cfv wceq cc0 cif opreq1 fveq2d fveq2 opreq1d
    eqeq12d opreq2 opreq2d 0cn elimel readdi dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZ
    EFZIUCAJKZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEU
    KGAUJBELMUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOAJ
    CSTBJCSTUAUB $.
    $( [17-Mar-2005] $)

  $( Real part distributes over subtraction. $)
  resub $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Re ` ( A - B ) ) = ( ( Re ` A ) - ( Re ` B ) ) ) $=
    ( cc wcel wa cneg caddc co cre cfv cmin wceq negcl readd sylan2 reneg
    adantl opreq2d eqtrd negsub fveq2d recl recnd syl2an 3eqtr3d ) ACDZBCDZEZAB
    FZGHZIJZAIJZBIJZFZGHZABKHZIJULUMKHZUHUKULUIIJZGHZUOUGUFUICDUKUSLBMAUINOUHUR
    UNULGUGURUNLUFBPQRSUHUJUPIABTUAUFULCDUMCDUOUQLUGUFULAUBUCUGUMBUBUCULUMTUDUE
    $.
    $( [2-Aug-2006] $) $( [17-Mar-2005] $)

  $( The imaginary part of a negative number. $)
  imneg $p |- ( A e. CC -> ( Im ` -u A ) = -u ( Im ` A ) ) $=
    ( cc wcel cneg cim cfv wceq cc0 cif negeq fveq2d fveq2 negeqd eqeq12d 0cn
    elimel imnegi dedth ) ABCZADZEFZAEFZDZGSAHIZDZEFZUDEFZDZGAHAUDGZUAUFUCUHUIT
    UEEAUDJKUIUBUGAUDELMNUDAHBOPQR $.
    $( [18-Mar-2005] $)

  $( Imaginary part distributes over addition. $)
  imadd $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Im ` ( A + B ) ) = ( ( Im ` A ) + ( Im ` B ) ) ) $=
    ( cc wcel caddc co cim cfv wceq cc0 cif opreq1 fveq2d fveq2 opreq1d
    eqeq12d opreq2 opreq2d 0cn elimel imaddi dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZ
    EFZIUCAJKZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEU
    KGAUJBELMUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOAJ
    CSTBJCSTUAUB $.
    $( [18-Mar-2005] $)

  $( Imaginary part distributes over subtraction. $)
  imsub $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Im ` ( A - B ) ) = ( ( Im ` A ) - ( Im ` B ) ) ) $=
    ( cc wcel wa cneg caddc co cim cfv cmin wceq negcl imadd sylan2 imneg
    adantl opreq2d eqtrd negsub fveq2d imcl recnd syl2an 3eqtr3d ) ACDZBCDZEZAB
    FZGHZIJZAIJZBIJZFZGHZABKHZIJULUMKHZUHUKULUIIJZGHZUOUGUFUICDUKUSLBMAUINOUHUR
    UNULGUGURUNLUFBPQRSUHUJUPIABTUAUFULCDUMCDUOUQLUGUFULAUBUCUGUMBUBUCULUMTUDUE
    $.
    $( [1-Aug-2006] $) $( [18-Mar-2005] $)

  $( A real number equals its complex conjugate.  Proposition 10-3.4(f) of
     [Gleason] p. 133. $)
  cjre $p |- ( A e. RR -> ( * ` A ) = A ) $=
    ( cc wcel cr ccj cfv wceq recn cjreb biimpd mpcom ) ABCZADCZAEFAGZAHLMNAIJK
    $.
    $( [8-Oct-1999] $)

  $( The conjugate of the conjugate is the original complex number.
     Proposition 10-3.4(e) of [Gleason] p. 133. $)
  cjcj $p |- ( A e. CC -> ( * ` ( * ` A ) ) = A ) $=
    ( cc wcel ccj cfv wceq cc0 cif fveq2 fveq2d id eqeq12d 0cn elimel cjcji
    dedth ) ABCZADEZDEZAFQAGHZDEZDEZTFAGATFZSUBATUCRUADATDIJUCKLTAGBMNOP $.
    $( [29-Jul-1999] $)

  $( Complex conjugate distributes over addition.  Proposition 10-3.4(a) of
     [Gleason] p. 133. $)
  cjadd $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) ) $=
    ( cc wcel caddc co ccj cfv wceq cc0 cif opreq1 fveq2d fveq2 opreq1d
    eqeq12d opreq2 opreq2d 0cn elimel cjaddi dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZ
    EFZIUCAJKZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEU
    KGAUJBELMUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOAJ
    CSTBJCSTUAUB $.
    $( [31-Jul-1999] $)

  $( Complex conjugate distributes over multiplication.  Proposition 10-3.4(c)
     of [Gleason] p. 133. $)
  cjmul $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) ) $=
    ( cc wcel cmul co ccj cfv wceq cc0 cif opreq1 fveq2d fveq2 opreq1d eqeq12d
    opreq2 opreq2d 0cn elimel cjmuli dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZEFZIUCAJ
    KZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEUKGAUJBEL
    MUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOAJCSTBJCST
    UAUB $.
    $( [29-Jul-1999] $)

  $( Complex conjugate of negative. $)
  cjneg $p |- ( A e. CC -> ( * ` -u A ) = -u ( * ` A ) ) $=
    ( cc wcel cneg ccj cfv wceq cc0 cif negeq fveq2d fveq2 negeqd eqeq12d 0cn
    elimel cjnegi dedth ) ABCZADZEFZAEFZDZGSAHIZDZEFZUDEFZDZGAHAUDGZUAUFUCUHUIT
    UEEAUDJKUIUBUGAUDELMNUDAHBOPQR $.
    $( [27-Feb-2005] $)

  $( A number plus its conjugate is twice its real part.  Compare Proposition
     10-3.4(h) of [Gleason] p. 133. $)
  addcj $p |- ( A e. CC -> ( A + ( * ` A ) ) = ( 2 x. ( Re ` A ) ) ) $=
    ( cc wcel ccj cfv caddc co c2 cre cmul wceq cc0 cif id fveq2 opreq12d
    opreq2d eqeq12d 0cn elimel addcji dedth ) ABCZAADEZFGZHAIEZJGZKUCALMZUHDEZF
    GZHUHIEZJGZKALAUHKZUEUJUGULUMAUHUDUIFUMNAUHDOPUMUFUKHJAUHIOQRUHALBSTUAUB $.
    $( [21-Jan-2007] $)

  $( Complex conjugate distributes over subtraction. $)
  cjsub $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A - B ) ) = ( ( * ` A ) - ( * ` B ) ) ) $=
    ( cc wcel wa cneg caddc co ccj cfv cmin wceq negcl cjadd sylan2 negsub
    fveq2d cjneg adantl opreq2d cjcl syl2an eqtrd 3eqtr3d ) ACDZBCDZEZABFZGHZIJ
    ZAIJZUHIJZGHZABKHZIJUKBIJZKHZUFUEUHCDUJUMLBMAUHNOUGUIUNIABPQUGUMUKUOFZGHZUP
    UGULUQUKGUFULUQLUEBRSTUEUKCDUOCDURUPLUFAUABUAUKUOPUBUCUD $.
    $( [1-Aug-2006] $) $( [28-Apr-2005] $)

  ${
    $d j k A $.  $d j k N $.
    $( Complex conjugate of natural number exponentiation. $)
    cjexp $p |- ( ( A e. CC /\ N e. NN0 ) ->
               ( * ` ( A ^ N ) ) = ( ( * ` A ) ^ N ) ) $=
      ( vj vk cn0 wcel cc cexp co ccj cfv wceq cv wi cc0 c1 caddc opreq2
      fveq2d eqeq12d imbi2d exp0 cjcl cr 1re cjre ax-mp syl6eqr syl eqtr4d wa
      cmul expp1 expcl simpl cjmul syl11anc eqtrd adantr opreq1 sylan eqcomd
      sylan9eqr exp31 com12 a2d nn0ind impcom ) BEFAGFZABHIZJKZAJKZBHIZLZVIACMZ
      HIZJKZVLVOHIZLZNVIAOHIZJKZVLOHIZLZNVIADMZHIZJKZVLWDHIZLZNVIAWDPQIZHIZJKZV
      LWIHIZLZNVIVNNCDBVOOLZVSWCVIWNVQWAVRWBWNVPVTJVOOAHRSVOOVLHRTUAVOWDLZVSWHV
      IWOVQWFVRWGWOVPWEJVOWDAHRSVOWDVLHRTUAVOWILZVSWMVIWPVQWKVRWLWPVPWJJVOWIAHR
      SVOWIVLHRTUAVOBLZVSVNVIWQVQVKVRVMWQVPVJJVOBAHRSVOBVLHRTUAVIWAPJKZWBVIVTPJ
      AUBSVIVLGFZWBWRLAUCZWSWBPWRVLUBPUDFWRPLUEPUFUGUHUIUJWDEFZVIWHWMVIXAWHWMNV
      IXAWHWMVIXAUKZWHUKWKWFVLULIZWLXBWKXCLWHXBWKWEAULIZJKZXCXBWJXDJAWDUMSXBWEG
      FVIXEXCLAWDUNVIXAUOWEAUPUQURUSWHXBXCWGVLULIZWLWFWGVLULUTXBWLXFVIWSXAWLXFL
      WTVLWDUMVAVBVCURVDVEVFVGVH $.
      $( [7-Jun-2006] $)
  $}

  $( The real part of a number in terms of complex conjugate. $)
  recj $p |- ( A e. CC -> ( Re ` A ) = ( ( A + ( * ` A ) ) / 2 ) ) $=
    ( cc wcel cre cfv caddc co c2 cdiv ccj wceq recl recnd cc0 wne wa 2cn 2ne0
    pm3.2i divdir mp3an3 syl11anc ci cim cmul cmin cneg replim cjval opreq12d
    axicn imcl mulcl sylancr negsub opreq2d mpan negid 3syl negcl add4
    syl22anc jca addcl addid1 3eqtr3d 3eqtr2rd opreq1d 2halves syl 3eqtr3rd )
    ABCZADEZVMFGZHIGZVMHIGZVPFGZAAJEZFGZHIGVMVLVMBCZVTVOVQKZVLVMALMZWBVTVTHBCZH
    NOZPWAWCWDQRSVMVMHTUAUBVLVNVSHIVLVSVMUCAUDEZUEGZFGZVMWFUFGZFGWGVMWFUGZFGZFG
    ZVNVLAWGVRWHFAUHAUIUJVLWJWHWGFVLVTWFBCZWJWHKWBVLUCBCZWEBCZWLUKVLWEAULMZUCWE
    UMZUNZVMWFUOUBUPVLVNWFWIFGZFGZVNNFGZWKVNVLWRNVNFVLWNWLWRNKWOWMWNWLUKWPUQZWF
    URUSUPVLVTVTWLWIBCZWSWKKWBWBWQVLWNWLXBWOXAWFUTUSVMVMWFWIVAVBVLVTVTPVNBCWTVN
    KVLVTVTWBWBVCVMVMVDVNVEUSVFVGVHVLVTVQVMKWBVMVIVJVK $.
    $( [2-Aug-2006] $) $( [30-Apr-2005] $)

  $( The imaginary part of a number in terms of complex conjugate. $)
  imcj $p |- ( A e. CC -> ( Im ` A ) = ( ( A - ( * ` A ) ) / ( 2 x. _i ) ) ) $=
    ( cc wcel cim cfv c2 ci cmul co cdiv ccj cmin wceq imcl recnd cc0 wne 2cn
    axicn mulcli 2ne0 ine0 mulne0i divcan4 mp3an23 syl cre caddc recl mulcl
    sylancr addcl syl11anc subsub syl111anc replim cjval opreq12d 2times
    mulcom mpan2 mulass mp3an12 eqtrd pncan2 opreq1d 3eqtr4d 3eqtr4rd eqtr3d )
    ABCZADEZFGHIZHIZVLJIZVKAAKEZLIZVLJIVJVKBCZVNVKMZVJVKANOZVQVLBCZVLPQVRFGRSTZ
    FGRSUAUBUCVKVLUDUEUFVJVMVPVLJVJAUGEZGVKHIZUHIZWBWCLIZLIZWDWBLIZWCUHIZVPVMVJ
    WDBCZWBBCZWCBCZWFWHMVJWJWKWIVJWBAUIOZVJGBCZVQWKSVSGVKUJUKZWBWCULUMWLWNWDWBW
    CUNUOVJAWDVOWELAUPAUQURVJFWCHIZWCWCUHIZVMWHVJWKWOWPMWNWCUSUFVJVQVMWOMVSVQVM
    VLVKHIZWOVQVTVMWQMWAVKVLUTVAFBCWMVQWQWOMRSFGVKVBVCVDUFVJWGWCWCUHVJWJWKWGWCM
    WLWNWBWCVEUMVFVGVHVFVI $.
    $( [30-Apr-2005] $)

  $( The real part of zero. $)
  re0 $p |- ( Re ` 0 ) = 0 $=
    ( cc0 cr wcel cre cfv wceq 0re 0cn rerebi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The imaginary part of zero. $)
  im0 $p |- ( Im ` 0 ) = 0 $=
    ( cc0 cr wcel cim cfv wceq 0re 0cn reim0bi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The real part of one.  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  re1 $p |- ( Re ` 1 ) = 1 $=
    ( c1 cr wcel cre cfv wceq 1re ax1cn rerebi mpbi ) ABCADEAFGAHIJ $.
    $( [9-Jun-2006] $)

  $( The imaginary part of one.  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  im1 $p |- ( Im ` 1 ) = 0 $=
    ( c1 cr wcel cim cfv cc0 wceq 1re ax1cn reim0bi mpbi ) ABCADEFGHAIJK $.
    $( [9-Jun-2006] $)

  $( The real part of ` _i ` .  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  rei $p |- ( Re ` _i ) = 0 $=
    ( cc0 ci c1 cmul co caddc cre cfv axicn ax1cn mulcli addid2i fveq2i 0re
    1re crrei mulid1i 3eqtr3ri ) ABCDEZFEZGHSGHABGHTSGSBCIJKLMACNOPSBGBIQMR $.
    $( [15-Mar-2008] $) $( [9-Jun-2006] $)

  $( The imaginary part of ` _i ` .  (Contributed by Scott Fenton,
     9-Jun-2006.) $)
  imi $p |- ( Im ` _i ) = 1 $=
    ( ci c1 cmul co cim cfv cc0 caddc axicn ax1cn mulcli addid2i eqcomi fveq2i
    mulid1i 0re 1re crimi 3eqtr3i ) ABCDZEFGTHDZEFAEFBTUAEUATTABIJKLMNTAEAIONGB
    PQRS $.
    $( [15-Mar-2008] $) $( [9-Jun-2006] $)

  $( The conjugate of zero. $)
  cj0 $p |- ( * ` 0 ) = 0 $=
    ( cc0 cr wcel ccj cfv wceq 0re 0cn cjrebi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The complex conjugate of the imaginary unit. $)
  cji $p |- ( * ` _i ) = -u _i $=
    ( ci cre cfv cim cmul co cmin cc0 ccj cneg rei c1 imi opreq2i axicn
    mulid1i eqtri opreq12i cc wcel wceq cjval ax-mp df-neg 3eqtr4i ) ABCZAADCZE
    FZGFZHAGFAICZAJUFHUHAGKUHALEFAUGLAEMNAOPQRASTUJUIUAOAUBUCAUDUE $.
    $( [26-Mar-2005] $)

  $( The conjugate of a representation of a complex number in terms of real and
     imaginary parts. $)
  cjreim $p |- ( ( A e. RR /\ B e. RR ) -> ( * ` ( A + ( _i x. B ) ) ) =
                ( A - ( _i x. B ) ) ) $=
    ( cr wcel wa ci cmul co caddc ccj cfv cneg cmin cc wceq recn axicn mulcl
    sylancr cjadd syl2an cjre cjmul cji a1i opreq12d mulneg1 3eqtrd opreqan12d
    negsub ) ACDZBCDZEAFBGHZIHJKZAJKZUMJKZIHZAUMLZIHZAUMMHZUKANDZUMNDZUNUQOULAP
    ZULFNDZBNDZVBQBPZFBRSZAUMTUAUKULUOAUPURIAUBULUPFJKZBJKZGHZFLZBGHZURULVDVEUP
    VJOQVFFBUCSULVHVKVIBGVHVKOULUDUEBUBUFULVDVEVLUROQVFFBUGSUHUIUKVAVBUSUTOULVC
    VGAUMUJUAUH $.
    $( [1-Aug-2006] $) $( [1-Jul-2005] $)

  $( The conjugate of the representation of a complex number in terms of real
     and imaginary parts. $)
  cjreim2 $p |- ( ( A e. RR /\ B e. RR ) -> ( * ` ( A - ( _i x. B ) ) ) =
                ( A + ( _i x. B ) ) ) $=
    ( cr wcel wa ci cmul co cmin ccj cfv cneg caddc cc wceq recn axicn mulcl
    sylancr cjsub syl2an cjre cjmul cji a1i opreq12d mulneg1 3eqtrd opreqan12d
    subneg ) ACDZBCDZEAFBGHZIHJKZAJKZUMJKZIHZAUMLZIHZAUMMHZUKANDZUMNDZUNUQOULAP
    ZULFNDZBNDZVBQBPZFBRSZAUMTUAUKULUOAUPURIAUBULUPFJKZBJKZGHZFLZBGHZURULVDVEUP
    VJOQVFFBUCSULVHVKVIBGVHVKOULUDUEBUBUFULVDVEVLUROQVFFBUGSUHUIUKVAVBUSUTOULVC
    VGAUMUJUAUH $.
    $( [1-Jul-2005] $)

  $( Complex conjugate is a one-to-one function.  (Proof shortened by Eric
     Schmidt, 2-Jul-2009.  Previous version is ~ cj11OLD .) $)
  cj11 $p |- ( ( A e. CC /\ B e. CC ) ->
              ( ( * ` A ) = ( * ` B ) <-> A = B ) ) $=
    ( cc wcel wa ccj cfv wceq cjcj eqeqan12d fveq2 syl5bi impbid1 ) ACDZBCDZEZA
    FGZBFGZHZABHZPQFGZRFGZHTSNOUAAUBBAIBIJQRFKLABFKM $.
    $( [2-Jul-2009] $) $( [29-Apr-2005] $)

  $( Complex conjugate is a one-to-one function. $)
  cj11OLD $p |- ( ( A e. CC /\ B e. CC ) ->
              ( ( * ` A ) = ( * ` B ) <-> A = B ) ) $=
    ( cc wcel wa cre cfv ci cim cneg cmul co caddc wceq ccj wb imcl recnd
    neg11 syl2an anbi2d cr recl renegcl syl jca cru 3bitr4d cmin axicn mulcl
    sylancr negsub syl11anc mulneg2 opreq2d cjval 3eqtr4rd eqeqan12d replim )
    ACDZBCDZEZAFGZHAIGZJZKLZMLZBFGZHBIGZJZKLZMLZNZVDHVEKLZMLZVIHVJKLZMLZNZAOGZB
    OGZNABNVCVDVINZVFVKNZEZWBVEVJNZEZVNVSVCWCWEWBVAVECDZVJCDZWCWEPVBVAVEAQZRZVB
    VJBQZRZVEVJSTUAVAVDUBDZVFUBDZEVIUBDZVKUBDZEVNWDPVBVAWMWNAUCZVAVEUBDZWNWIVEU
    DUEUFVBWOWPBUCZVBVJUBDZWPWKVJUDUEUFVDVFVIVKUGTVAWMWREWOWTEVSWFPVBVAWMWRWQWI
    UFVBWOWTWSWKUFVDVEVIVJUGTUHVAVBVTVHWAVMVAVDVOJZMLZVDVOUILZVHVTVAVDCDVOCDZXB
    XCNVAVDWQRVAHCDZWGXDUJWJHVEUKULVDVOUMUNVAVGXAVDMVAXEWGVGXANUJWJHVEUOULUPAUQ
    URVBVIVQJZMLZVIVQUILZVMWAVBVICDVQCDZXGXHNVBVIWSRVBXEWHXIUJWLHVJUKULVIVQUMUN
    VBVLXFVIMVBXEWHVLXFNUJWLHVJUOULUPBUQURUSVAVBAVPBVRAUTBUTUSUH $.
    $( [1-Aug-2006] $) $( [29-Apr-2005] $)

  $( A number is nonzero iff its complex conjugate is nonzero. $)
  cjne0 $p |- ( A e. CC -> ( A =/= 0 <-> ( * ` A ) =/= 0 ) ) $=
    ( cc wcel cc0 ccj cfv wceq wb 0cn cj11 mpan2 cj0 eqeq2i syl5rbbr necon3bid
    ) ABCZADAEFZDPQDEFZGZADGZQDGPDBCSTHIADJKRDQLMNO $.
    $( [29-Apr-2005] $)

  $( Absolute value of negative. $)
  absneg $p |- ( A e. CC -> ( abs ` -u A ) = ( abs ` A ) ) $=
    ( cc wcel cneg ccj cfv cmul co csqr cabs cjneg opreq2d wceq cjcl mul2neg
    mpdan eqtrd fveq2d negcl absval syl 3eqtr4d ) ABCZADZUDEFZGHZIFZAAEFZGHZIFU
    DJFZAJFUCUFUIIUCUFUDUHDZGHZUIUCUEUKUDGAKLUCUHBCULUIMANAUHOPQRUCUDBCUJUGMASU
    DTUAATUB $.
    $( [27-Feb-2005] $)

  $( Real closure of absolute value. $)
  abscl $p |- ( A e. CC -> ( abs ` A ) e. RR ) $=
    ( cc wcel cabs cfv ccj cmul co csqr cr absval cc0 cle wbr cjmulrcl
    cjmulge0 sqrcl syl11anc eqeltrd ) ABCZADEAAFEGHZIEZJAKTUAJCLUAMNUBJCAOAPUAQ
    RS $.
    $( [3-Oct-1999] $)

  $( The absolute value of a number and its conjugate are the same.
     Proposition 10-3.7(b) of [Gleason] p. 133. $)
  abscj $p |- ( A e. CC -> ( abs ` ( * ` A ) ) = ( abs ` A ) ) $=
    ( cc wcel ccj cfv cmul co csqr cabs wceq cjcl mulcom mpdan cjcj opreq2d
    eqtr4d fveq2d absval syl 3eqtr4rd ) ABCZAADEZFGZHEUBUBDEZFGZHEZAIEUBIEZUAUC
    UEHUAUCUBAFGZUEUAUBBCZUCUHJAKZAUBLMUAUDAUBFANOPQARUAUIUGUFJUJUBRST $.
    $( [28-Apr-2005] $)

  $( Square of value of absolute value function. $)
  absvalsq $p |- ( A e. CC -> ( ( abs ` A ) ^ 2 ) = ( A x. ( * ` A ) ) ) $=
    ( cc wcel cabs cfv c2 cexp co ccj cmul csqr absval opreq1d cr cc0 cle wbr
    wceq cjmulrcl cjmulge0 sqsqr syl11anc eqtrd ) ABCZADEZFGHAAIEJHZKEZFGHZUFUD
    UEUGFGALMUDUFNCOUFPQUHUFRASATUFUAUBUC $.
    $( [16-Jan-2006] $)

  $( Square of value of absolute value function. $)
  absvalsq2 $p |- ( A e. CC -> ( ( abs ` A ) ^ 2 ) =
                    ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) ) $=
    ( cc wcel cabs cfv c2 cexp co ccj cmul cre cim caddc absvalsq cjmulval
    eqtrd ) ABCADEFGHAAIEJHAKEFGHALEFGHMHANAOP $.
    $( [1-Feb-2007] $)

  ${
    absvalsqi.1 $e |- A e. CC $.
    $( Square of value of absolute value function. $)
    absvalsqi $p |- ( ( abs ` A ) ^ 2 ) = ( A x. ( * ` A ) ) $=
      ( cc wcel cabs cfv c2 cexp co ccj cmul wceq absvalsq ax-mp ) ACDAEFGHIAAJ
      FKILBAMN $.
      $( [2-Oct-1999] $)

    $( Square of value of absolute value function. $)
    absvalsq2i $p |- ( ( abs ` A ) ^ 2 ) =
                    ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) $=
      ( cc wcel cabs cfv c2 cexp co cre cim caddc wceq absvalsq2 ax-mp ) ACDAEF
      GHIAJFGHIAKFGHILIMBANO $.
      $( [2-Oct-1999] $)

    $( Real closure of absolute value. $)
    abscli $p |- ( abs ` A ) e. RR $=
      ( cc wcel cabs cfv cr abscl ax-mp ) ACDAEFGDBAHI $.
      $( [2-Aug-1999] $)

    $( Absolute value is nonnegative. $)
    absge0i $p |- 0 <_ ( abs ` A ) $=
      ( cc0 ccj cfv cmul co csqr cabs cle wbr cjmulge0i cjmulrcli sqrge0i
      ax-mp cc wcel wceq absval breqtrri ) CAADEFGZHEZAIEZJCUAJKCUBJKABLUAABMNO
      APQUCUBRBASOT $.
      $( [2-Aug-1999] $)

    $( Value of absolute value function.  Definition 10.36 of [Gleason]
       p. 133. $)
    absval2i $p |- ( abs ` A ) =
                 ( sqr ` ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) ) $=
      ( cabs cfv ccj cmul co csqr cre c2 cexp cim caddc cc wcel wceq absval
      ax-mp cjmulvali fveq2i eqtri ) ACDZAAEDFGZHDZAIDJKGALDJKGMGZHDANOUBUDPBAQ
      RUCUEHABSTUA $.
      $( [2-Oct-1999] $)

    $( The absolute value of a number is zero iff the number is zero.
       Proposition 10-3.7(c) of [Gleason] p. 133. $)
    abs00i $p |- ( ( abs ` A ) = 0 <-> A = 0 ) $=
      ( cabs cfv cc0 wceq cre c2 cexp co cim caddc csqr absval2i eqeq1i cr
      wcel cle wbr wb recli resqcli imcli readdcli sqge0i addge0i mp2an sqr00
      wa add20i recni sqeq0i anbi12i ci cmul opreq1 opreq2 opreq2d sylan9eq
      axicn 0cn mulcli addid2i mul01i eqtri syl6eq replimi syl5eq fveq2 re0
      im0 jca impbii 3bitri ) ACDZEFAGDZHIJZAKDZHIJZLJZMDZEFZVTEFZAEFZVOWAEABNO
      VTPQEVTRSZWBWCTVQVSVPABUAZUBZVRABUCZUBZUDEVQRSZEVSRSZWEVPWFUEZVRWHUEZVQVS
      WGWIUFUGVTUHUGWCVQEFZVSEFZUIZVPEFZVREFZUIZWDWJWKWCWPTWLWMVQVSWGWIUJUGWNWQ
      WOWRVPVPWFUKULVRVRWHUKULUMWSWDWSVPUNVRUOJZLJZEAWSXAEUNEUOJZLJZEWQWRXAEWTL
      JXCVPEWTLUPWRWTXBELVREUNUOUQURUSXCXBEXBUNEUTVAVBVCUNUTVDVEVFABVGVHWDWQWRW
      DVPEGDEAEGVIVJVFWDVREKDEAEKVIVKVFVLVMVNVN $.
      $( [24-Jul-2008] $) $( [28-Jul-1999] $)

    $( The absolute value of a nonzero number is positive.  Remark in [Apostol]
       p. 363. $)
    absgt0i $p |- ( A =/= 0 <-> 0 < ( abs ` A ) ) $=
      ( cc0 cabs cfv clt wbr cle wne wa 0re abscli ltleni absge0i biantrur
      abs00i necon3bii 3bitr2ri ) CADEZFGCSHGZSCIZJUAACICSKABLMTUAABNOSCACABPQR
      $.
      $( [6-Mar-2005] $) $( [1-Oct-1999] $)

    $( Absolute value of negative. $)
    absnegi $p |- ( abs ` -u A ) = ( abs ` A ) $=
      ( cc wcel cneg cabs cfv wceq absneg ax-mp ) ACDAEFGAFGHBAIJ $.
      $( [2-Aug-1999] $)

    $( The absolute value of a number and its conjugate are the same.
       Proposition 10-3.7(b) of [Gleason] p. 133. $)
    abscji $p |- ( abs ` ( * ` A ) ) = ( abs ` A ) $=
      ( cc wcel ccj cfv cabs wceq abscj ax-mp ) ACDAEFGFAGFHBAIJ $.
      $( [27-Mar-2005] $) $( [2-Oct-1999] $)

    ${
      abssub.2 $e |- B e. CC $.
      $( Swapping order of subtraction doesn't change the absolute value.
         Example of [Apostol] p. 363. $)
      abssubi $p |- ( abs ` ( A - B ) ) = ( abs ` ( B - A ) ) $=
        ( cmin co cneg cabs cfv negsubdi2i fveq2i subcli absnegi eqtr3i ) BAEFZ
        GZHIABEFZHIOHIPQHBADCJKOBADCLMN $.
        $( [1-Oct-1999] $)

      $( Absolute value distributes over multiplication.  Proposition 10-3.7(f)
         of [Gleason] p. 133. $)
      absmuli $p |- ( abs ` ( A x. B ) ) = ( ( abs ` A ) x. ( abs ` B ) ) $=
        ( cmul co ccj cfv csqr cabs cjmuli opreq2i cjcli mul4i eqtri fveq2i
        cjmulrcli cjmulge0i sqrmulii cc wcel wceq mulcli absval ax-mp opreq12i
        3eqtr4i ) ABEFZUHGHZEFZIHZAAGHZEFZIHZBBGHZEFZIHZEFZUHJHZAJHZBJHZEFUKUMU
        PEFZIHURUJVBIUJUHULUOEFZEFVBUIVCUHEABCDKLABULUOCDACMBDMNOPUMUPACQBDQACR
        BDRSOUHTUAUSUKUBABCDUCUHUDUEUTUNVAUQEATUAUTUNUBCAUDUEBTUAVAUQUBDBUDUEUF
        UG $.
        $( [1-Oct-1999] $)
    $}
  $}

  $( Square of absolute value of sum.  Proposition 10-3.7(g) of [Gleason]
     p. 133. $)
  sqabsadd $p |- ( ( A e. CC /\ B e. CC ) -> ( ( abs ` ( A + B ) ) ^ 2 ) =
                  ( ( ( ( abs ` A ) ^ 2 ) + ( ( abs ` B ) ^ 2 ) )
                    + ( 2 x. ( Re ` ( A x. ( * ` B ) ) ) ) ) ) $=
    ( cc wcel wa caddc co ccj cfv cmul cabs c2 cexp cre cjadd opreq2d wceq
    cjcl anim12i muladd mpdan eqtrd addcl absvalsq syl mulcom opreqan12d mulcl
    sylan2 addcj cjmul cjcj adantl eqtr3d opreq12d 3eqtr4d ) ACDZBCDZEZABFGZUTH
    IZJGZAAHIZJGZBHIZBJGZFGZAVEJGZVCBJGZFGZFGZUTKILMGZAKILMGZBKILMGZFGZLVHNIJGZ
    FGUSVBUTVCVEFGZJGZVKUSVAVQUTJABOPUSVCCDZVECDZEVRVKQUQVSURVTARBRZSABVCVETUAU
    BUSUTCDVLVBQABUCUTUDUEUSVOVGVPVJFUQURVMVDVNVFFAUDURVNBVEJGZVFBUDURVTWBVFQWA
    BVEUFUAUBUGUSVHVHHIZFGZVPVJUSVHCDZWDVPQURUQVTWEWAAVEUHUIVHUJUEUSWCVIVHFUSWC
    VCVEHIZJGZVIURUQVTWCWGQWAAVEUKUIUSWFBVCJURWFBQUQBULUMPUBPUNUOUP $.
    $( [21-Jan-2007] $)

  $( Square of absolute value of difference. $)
  sqabssub $p |- ( ( A e. CC /\ B e. CC ) -> ( ( abs ` ( A - B ) ) ^ 2 ) =
                  ( ( ( ( abs ` A ) ^ 2 ) + ( ( abs ` B ) ^ 2 ) )
                    - ( 2 x. ( Re ` ( A x. ( * ` B ) ) ) ) ) ) $=
    ( cc wcel wa cmin co ccj cfv cmul caddc cabs c2 cexp cre cjsub opreq2d
    wceq cjcl anim12i mulsub mpdan eqtrd subcl absvalsq syl mulcom opreqan12d
    mulcl sylan2 addcj cjmul cjcj adantl eqtr3d opreq12d 3eqtr4d ) ACDZBCDZEZAB
    FGZVAHIZJGZAAHIZJGZBHIZBJGZKGZAVFJGZVDBJGZKGZFGZVALIMNGZALIMNGZBLIMNGZKGZMV
    IOIJGZFGUTVCVAVDVFFGZJGZVLUTVBVRVAJABPQUTVDCDZVFCDZEVSVLRURVTUSWAASBSZTABVD
    VFUAUBUCUTVACDVMVCRABUDVAUEUFUTVPVHVQVKFURUSVNVEVOVGKAUEUSVOBVFJGZVGBUEUSWA
    WCVGRWBBVFUGUBUCUHUTVIVIHIZKGZVQVKUTVICDZWEVQRUSURWAWFWBAVFUIUJVIUKUFUTWDVJ
    VIKUTWDVDVFHIZJGZVJUSURWAWDWHRWBAVFULUJUTWGBVDJUSWGBRURBUMUNQUCQUOUPUQ $.
    $( [21-Jan-2007] $)

  ${
    sqabsaddi.1 $e |- A e. CC $.
    sqabsaddi.2 $e |- B e. CC $.
    $( Square of absolute value of sum.  Proposition 10-3.7(g) of [Gleason]
       p. 133. $)
    sqabsaddi $p |- ( ( abs ` ( A + B ) ) ^ 2 ) =
                 ( ( ( ( abs ` A ) ^ 2 ) + ( ( abs ` B ) ^ 2 ) ) +
                 ( 2 x. ( Re ` ( A x. ( * ` B ) ) ) ) ) $=
      ( cc wcel caddc co cabs cfv c2 cexp ccj cmul cre wceq sqabsadd mp2an ) AE
      FBEFABGHIJKLHAIJKLHBIJKLHGHKABMJNHOJNHGHPCDABQR $.
      $( [2-Oct-1999] $)

    $( Square of absolute value of difference.  (Contributed by Steve
       Rodriguez, 20-Jan-2007.) $)
    sqabssubi $p |- ( ( abs ` ( A - B ) ) ^ 2 ) =
                    ( ( ( ( abs ` A ) ^ 2 ) + ( ( abs ` B ) ^ 2 ) )
                      - ( 2 x. ( Re ` ( A x. ( * ` B ) ) ) ) ) $=
      ( cc wcel cmin co cabs cfv c2 cexp caddc ccj cmul cre wceq sqabssub
      mp2an ) AEFBEFABGHIJKLHAIJKLHBIJKLHMHKABNJOHPJOHGHQCDABRS $.
      $( [19-Jan-2007] $)
  $}

  $( Value of absolute value function.  Definition 10.36 of [Gleason]
     p. 133. $)
  absval2 $p |- ( A e. CC -> ( abs ` A ) =
               ( sqr ` ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) ) ) $=
    ( cc wcel cabs cfv cre c2 cexp co cim caddc csqr wceq cc0 cif fveq2
    opreq1d opreq12d fveq2d eqeq12d 0cn elimel absval2i dedth ) ABCZADEZAFEZGHI
    ZAJEZGHIZKIZLEZMUEANOZDEZUMFEZGHIZUMJEZGHIZKIZLEZMANAUMMZUFUNULUTAUMDPVAUKU
    SLVAUHUPUJURKVAUGUOGHAUMFPQVAUIUQGHAUMJPQRSTUMANBUAUBUCUD $.
    $( [17-Mar-2005] $)

  $( The absolute value of a number is zero iff the number is zero.
     Proposition 10-3.7(c) of [Gleason] p. 133. $)
  abs00 $p |- ( A e. CC -> ( ( abs ` A ) = 0 <-> A = 0 ) ) $=
    ( cc wcel cabs cfv cc0 wceq wb cif fveq2 eqeq1d eqeq1 bibi12d 0cn elimel
    abs00i dedth ) ABCZADEZFGZAFGZHRAFIZDEZFGZUBFGZHAFAUBGZTUDUAUEUFSUCFAUBDJKA
    UBFLMUBAFBNOPQ $.
    $( [26-Sep-2005] $)

  $( Absolute value is nonnegative. $)
  absge0 $p |- ( A e. CC -> 0 <_ ( abs ` A ) ) $=
    ( cc wcel cc0 cabs cfv cle wbr cif wceq fveq2 breq2d 0cn elimel absge0i
    dedth ) ABCZDAEFZGHDQADIZEFZGHADASJRTDGASEKLSADBMNOP $.
    $( [20-Nov-2004] $)

  $( The absolute value of a nonzero number is a positive real.  (Contributed
     by FL, 27-Dec-2007.) $)
  absrpcl $p |- ( ( A e. CC /\ A =/= 0 ) -> ( abs ` A ) e. RR+ ) $=
    ( cc wcel cc0 wne wa cabs cfv cr clt wbr crp abscl adantr cle wi absge0
    0re w3a wb leltne syl3an2 wceq abs00 3ad2ant2 necon3bid bitrd biimprd 3exp
    com23 ax-mp mpcom imp elrp sylanbrc ) ABCZADEZFAGHZICZDURJKZURLCUPUSUQAMZNU
    PUQUTDUROKZUPUQUTPZAQDICZVBUPVCPPRVDUPVBVCVDUPVBVCVDUPVBSZUTUQVEUTURDEZUQVD
    USVBUTVFTUPDURUAVAUBVEURDADUPVDURDUCADUCTVBAUDUEUFUGUHUIUJUKULUMURUNUO $.
    $( [27-Dec-2007] $)

  $( Square of the absolute value of a number that has been decomposed into
     real and imaginary parts. $)
  absreimsq $p |- ( ( A e. RR /\ B e. RR ) ->
         ( ( abs ` ( A + ( _i x. B ) ) ) ^ 2 ) = ( ( A ^ 2 ) + ( B ^ 2 ) ) ) $=
    ( cr wcel wa ci cmul co caddc cabs cfv c2 cexp cre cim cc wceq recn axicn
    mulcl sylancr addcl syl2an absvalsq2 syl crre opreq1d crim opreq12d eqtrd
    ) ACDZBCDZEZAFBGHZIHZJKLMHZUONKZLMHZUOOKZLMHZIHZALMHZBLMHZIHUMUOPDZUPVAQUKA
    PDUNPDZVDULARULFPDBPDVESBRFBTUAAUNUBUCUOUDUEUMURVBUTVCIUMUQALMABUFUGUMUSBLM
    ABUHUGUIUJ $.
    $( [15-Mar-2008] $) $( [1-Feb-2007] $)

  $( Absolute value of a number that has been decomposed into real and
     imaginary parts. $)
  absreim $p |- ( ( A e. RR /\ B e. RR ) -> ( abs ` ( A + ( _i x. B ) ) ) =
               ( sqr ` ( ( A ^ 2 ) + ( B ^ 2 ) ) ) ) $=
    ( cr wcel wa ci cmul co caddc cabs cfv c2 cexp csqr cc0 cle wbr wceq cc
    recn axicn mulcl sylancr addcl syl2an abscl syl absge0 sqrsq syl11anc
    absreimsq fveq2d eqtr3d ) ACDZBCDZEZAFBGHZIHZJKZLMHZNKZUSALMHBLMHIHZNKUPUSC
    DZOUSPQZVAUSRUPURSDZVCUNASDUQSDZVEUOATUOFSDBSDVFUABTFBUBUCAUQUDUEZURUFUGUPV
    EVDVGURUHUGUSUIUJUPUTVBNABUKULUM $.
    $( [14-Jan-2006] $)

  $( Absolute value distributes over multiplication.  Proposition 10-3.7(f) of
     [Gleason] p. 133. $)
  absmul $p |- ( ( A e. CC /\ B e. CC ) ->
                ( abs ` ( A x. B ) ) = ( ( abs ` A ) x. ( abs ` B ) ) ) $=
    ( cc wcel cmul co cabs cfv wceq cc0 cif opreq1 fveq2d fveq2 opreq1d
    eqeq12d opreq2 opreq2d 0cn elimel absmuli dedth2h ) ACDZBCDZABEFZGHZAGHZBGH
    ZEFZIUCAJKZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUE
    UKGAUJBELMUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOA
    JCSTBJCSTUAUB $.
    $( [11-Oct-1999] $)

  ${
    absdivzi.1 $e |- A e. CC $.
    absdivzi.2 $e |- B e. CC $.
    $( Absolute value distributes over division. $)
    absdivzi $p |- ( B =/= 0 ->
                  ( abs ` ( A / B ) ) = ( ( abs ` A ) / ( abs ` B ) ) ) $=
      ( cc0 wne cabs cfv cdiv co wceq cmul cc wcel divclzi absmul sylancr
      divcan2zi fveq2d eqtr3d wb cr abscl recn syl abs00i necon3bii biimpri
      abscli recni wa divmul mp3an1 mpanr1 syl11anc mpbird eqcomd ) BEFZAGHZBGH
      ZIJZABIJZGHZURVAVCKZUTVCLJZUSKZURBVBLJZGHZVEUSURBMNVBMNZVHVEKDABCDOZBVBPQ
      URVGAGABCDRSTURVCMNZUTEFZVDVFUAZURVIVKVJVIVCUBNVKVBUCVCUDUEUEVLURUTEBEBDU
      FUGUHVKUTMNZVLVMUTBDUIUJUSMNVKVNVLUKVMUSACUIUJUSVCUTULUMUNUOUPUQ $.
      $( [26-Mar-2005] $)
  $}


  $( Absolute value distributes over division. $)
  absdiv $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                ( abs ` ( A / B ) ) = ( ( abs ` A ) / ( abs ` B ) ) ) $=
    ( cc wcel cc0 wne cdiv co cabs cfv wceq wi cif opreq1 fveq2d fveq2 opreq1d
    eqeq12d imbi2d neeq1 opreq2 opreq2d imbi12d 0cn elimel absdivzi dedth2h
    3impia ) ACDZBCDZBEFZABGHZIJZAIJZBIJZGHZKZUIUJUKUQLUKUIAEMZBGHZIJZURIJZUOGH
    ZKZLUJBEMZEFZURVDGHZIJZVAVDIJZGHZKZLABEEAURKZUQVCUKVKUMUTUPVBVKULUSIAURBGNO
    VKUNVAUOGAURIPQRSBVDKZUKVEVCVJBVDETVLUTVGVBVIVLUSVFIBVDURGUAOVLUOVHVAGBVDIP
    UBRUCURVDAECUDUEBECUDUEUFUGUH $.
    $( [27-Apr-2005] $)

  ${
    absidi.1 $e |- A e. RR $.
    $( A nonnegative number is its own absolute value. $)
    absidi $p |- ( 0 <_ A -> ( abs ` A ) = A ) $=
      ( cc0 cle wbr cmul co csqr cfv cabs sqrmsqi ccj cc wcel wceq recni
      absval ax-mp cr cjrebi mpbi opreq2i fveq2i eqtri syl5eq ) CADEAAFGZHIZAAJ
      IZABKUHAALIZFGZHIZUGAMNUHUKOABPZAQRUJUFHUIAAFASNUIAOBAULTUAUBUCUDUE $.
      $( [2-Aug-1999] $)
  $}

  $( A nonnegative number is its own absolute value. $)
  absid $p |- ( ( A e. RR /\ 0 <_ A ) -> ( abs ` A ) = A ) $=
    ( cr wcel cc0 cle wbr cabs cfv wceq wi cif breq2 fveq2 id eqeq12d imbi12d
    0re elimel absidi dedth imp ) ABCZDAEFZAGHZAIZUBUCUEJDUBADKZEFZUFGHZUFIZJAD
    AUFIZUCUGUEUIAUFDELUJUDUHAUFAUFGMUJNOPUFADBQRSTUA $.
    $( [28-Feb-2005] $) $( [11-Oct-1999] $)

  $( A negative number is the negative of its own absolute value. $)
  absnid $p |- ( ( A e. RR /\ A <_ 0 ) -> ( abs ` A ) = -u A ) $=
    ( cr wcel cc0 cle wbr cabs cfv cneg wceq le0neg1 wa cc recn absneg syl
    adantr renegcl absid sylan eqtr3d ex sylbid imp ) ABCZADEFZAGHZAIZJZUEUFDUH
    EFZUIAKUEUJUIUEUJLUHGHZUGUHUEUKUGJZUJUEAMCULANAOPQUEUHBCUJUKUHJARUHSTUAUBUC
    UD $.
    $( [27-Feb-2005] $)

  $( A real number is less than or equal to its absolute value. $)
  leabs $p |- ( A e. RR -> A <_ ( abs ` A ) ) $=
    ( cr wcel cabs cfv cle wbr cc0 0re a1i id wceq absid eqcom eqle sylan2b
    syldan cc recn absge0 syl wa wi abscl letr mp3an2 mpdan mpan2d imp lecasei
    ) ABCZAADEZFGZHAHBCZUKIJUKKUKHAFGULALZUMAMUOUKAULLUMULANAULOPQUKAHFGZUMUKUP
    HULFGZUMUKARCZUQASZATUAUKULBCZUPUQUBUMUCZUKURUTUSAUDUAUKUNUTVAIAHULUEUFUGUH
    UIUJ $.
    $( [27-Feb-2005] $)

  $( The absolute value of a real number is either that number or its
     negative. $)
  absor $p |- ( A e. RR -> ( ( abs ` A ) = A \/ ( abs ` A ) = -u A ) ) $=
    ( cr wcel cc0 cle wbr wo cabs cfv wceq cneg 0re letric mpan absid ex
    absnid orim12d mpd ) ABCZDAEFZADEFZGZAHIZAJZUDAKJZGDBCTUCLDAMNTUAUEUBUFTUAU
    EAOPTUBUFAQPRS $.
    $( [27-Feb-2005] $)

  $( Absolute value of a real number. $)
  absre $p |- ( A e. RR -> ( abs ` A ) = ( sqr ` ( A ^ 2 ) ) ) $=
    ( cr wcel cabs cfv ccj cmul co csqr c2 cexp cc wceq recn absval syl sqval
    cjre opreq2d eqtr4d fveq2d ) ABCZADEZAAFEZGHZIEZAJKHZIEUBALCZUCUFMANZAOPUBU
    GUEIUBUGAAGHZUEUBUHUGUJMUIAQPUBUDAAGARSTUAT $.
    $( [17-Mar-2005] $)

  $( Square of the absolute value of a real number. $)
  absresq $p |- ( A e. RR -> ( ( abs ` A ) ^ 2 ) = ( A ^ 2 ) ) $=
    ( cr wcel ccj cfv cmul co cabs c2 cexp cjre opreq2d cc wceq recn absvalsq
    syl sqval 3eqtr4d ) ABCZAADEZFGZAAFGZAHEIJGZAIJGZTUAAAFAKLTAMCZUDUBNAOZAPQT
    UFUEUCNUGARQS $.
    $( [16-Jan-2006] $)

  ${
    $d j k A $.  $d j k N $.
    $( Absolute value of natural number exponentiation. $)
    absexp $p |- ( ( A e. CC /\ N e. NN0 ) ->
                  ( abs ` ( A ^ N ) ) = ( ( abs ` A ) ^ N ) ) $=
      ( vj vk cn0 wcel cc cexp co cabs cfv wceq cv wi cc0 c1 caddc opreq2
      fveq2d eqeq12d imbi2d cle wbr 0re 1re lt01 ltleii absidi ax-mp exp0
      abscl recnd syl 3eqtr4a wa cmul opreq1 adantl expp1 expcl simpl absmul
      syl11anc eqtrd adantr sylan 3eqtr4d exp31 com12 a2d nn0ind impcom ) BEFAG
      FZABHIZJKZAJKZBHIZLZVMACMZHIZJKZVPVSHIZLZNVMAOHIZJKZVPOHIZLZNVMADMZHIZJKZ
      VPWHHIZLZNVMAWHPQIZHIZJKZVPWMHIZLZNVMVRNCDBVSOLZWCWGVMWRWAWEWBWFWRVTWDJVS
      OAHRSVSOVPHRTUAVSWHLZWCWLVMWSWAWJWBWKWSVTWIJVSWHAHRSVSWHVPHRTUAVSWMLZWCWQ
      VMWTWAWOWBWPWTVTWNJVSWMAHRSVSWMVPHRTUAVSBLZWCVRVMXAWAVOWBVQXAVTVNJVSBAHRS
      VSBVPHRTUAVMPJKZPWEWFOPUBUCXBPLOPUDUEUFUGPUEUHUIVMWDPJAUJSVMVPGFZWFPLVMVP
      AUKULZVPUJUMUNWHEFZVMWLWQVMXEWLWQNVMXEWLWQVMXEUOZWLUOWJVPUPIZWKVPUPIZWOWP
      WLXGXHLXFWJWKVPUPUQURXFWOXGLWLXFWOWIAUPIZJKZXGXFWNXIJAWHUSSXFWIGFVMXJXGLA
      WHUTVMXEVAWIAVBVCVDVEXFWPXHLZWLVMXCXEXKXDVPWHUSVFVEVGVHVIVJVKVL $.
      $( [5-Jan-2006] $)
  $}

  $( The squares of two reals are equal iff their absolute values are equal. $)
  sqabs $p |- ( ( A e. RR /\ B e. RR ) -> ( ( A ^ 2 ) = ( B ^ 2 ) <->
               ( abs ` A ) = ( abs ` B ) ) ) $=
    ( cr wcel wa c2 cexp co wceq cabs cfv cc0 cle wbr resqcl sqge0 absid
    syl11anc cc cn0 recn 2nn0 absexp sylancl eqtr3d eqeqan12d wb abscl absge0
    jca sq11 syl2an bitrd ) ACDZBCDZEAFGHZBFGHZIAJKZFGHZBJKZFGHZIZURUTIZUNUOUPU
    SUQVAUNUPJKZUPUSUNUPCDLUPMNVDUPIAOAPUPQRUNASDZFTDZVDUSIAUAZUBAFUCUDUEUOUQJK
    ZUQVAUOUQCDLUQMNVHUQIBOBPUQQRUOBSDZVFVHVAIBUAZUBBFUCUDUEUFUNVEVIVBVCUGZUOVG
    VJVEURCDZLURMNZEUTCDZLUTMNZEVKVIVEVLVMAUHAUIUJVIVNVOBUHBUIUJURUTUKULULUM $.
    $( [6-Mar-2009] $)

  $( The absolute value of a complex number is greater than or equal to the
     absolute value of its real part. $)
  absrele $p |- ( A e. CC -> ( abs ` ( Re ` A ) ) <_ ( abs ` A ) ) $=
    ( cc wcel cre cfv c2 cexp co csqr cim caddc cabs cle wbr cc0 cr imcl sqge0
    syl wb recl resqcl addge01 syl11anc mpbid readdcl addge0 syl22anc sqrle
    wceq absre absval2 3brtr4d ) ABCZADEZFGHZIEZUPAJEZFGHZKHZIEZUOLEZALEMUNUPUT
    MNZUQVAMNZUNOUSMNZVCUNURPCZVEAQZURRSZUNUPPCZUSPCZVEVCTUNUOPCZVIAUAZUOUBSZUN
    VFVJVGURUBSZUPUSUCUDUEUNVIUTPCZOUPMNZOUTMNZVCVDTVMUNVIVJVOVMVNUPUSUFUDUNVKV
    PVLUORSZUNVIVJVPVEVQVMVNVRVHUPUSUGUHUPUTUIUHUEUNVKVBUQUJVLUOUKSAULUM $.
    $( [25-Jul-2006] $) $( [1-Apr-2005] $)

  $( The absolute value of a complex number is greater than or equal to the
     absolute value of its imaginary part. $)
  absimle $p |- ( A e. CC -> ( abs ` ( Im ` A ) ) <_ ( abs ` A ) ) $=
    ( cc wcel cim cfv c2 cexp co csqr cre caddc cabs cle wbr cr cc0 imcl
    resqcl syl recl sqge0 addge02 biimp3a syl111anc wb readdcl syl11anc addge0
    syl22anc sqrle mpbid wceq absre absval2 3brtr4d ) ABCZADEZFGHZIEZAJEZFGHZUR
    KHZIEZUQLEZALEMUPURVBMNZUSVCMNZUPUROCZVAOCZPVAMNZVEUPUQOCZVGAQZUQRSZUPUTOCZ
    VHATZUTRSZUPVMVIVNUTUASZVGVHVIVEURVAUBUCUDUPVGVBOCZPURMNZPVBMNZVEVFUEVLUPVH
    VGVQVOVLVAURUFUGUPVJVRVKUQUASZUPVHVGVIVRVSVOVLVPVTVAURUHUIURVBUJUIUKUPVJVDU
    SULVKUQUMSAUNUO $.
    $( [25-Jul-2006] $) $( [17-Mar-2005] $)

  ${
    absnidi.1 $e |- A e. RR $.
    $( A negative number is the negative of its own absolute value. $)
    absnidi $p |- ( A <_ 0 -> ( abs ` A ) = -u A ) $=
      ( cr wcel cc0 cle wbr cabs cfv cneg wceq absnid mpan ) ACDAEFGAHIAJKBALM
      $.
      $( [2-Aug-1999] $)

    $( A real number is less than or equal to its absolute value. $)
    leabsi $p |- A <_ ( abs ` A ) $=
      ( cc0 cle wbr wo cabs cfv 0re letrii wceq absidi recni abscli eqlei
      eqcoms syl absge0i letri mpan2 jaoi ax-mp ) CADEZACDEZFAAGHZDEZCAIBJUCUFU
      DUCUEAKUFABLUFAUEAUEBAABMZNZOPQUDCUEDEUFAUGRACUEBIUHSTUAUB $.
      $( [2-Aug-1999] $)

    $( The absolute value of a real number is either that number or its
       negative. $)
    absori $p |- ( ( abs ` A ) = A \/ ( abs ` A ) = -u A ) $=
      ( cr wcel cabs cfv wceq cneg wo absor ax-mp ) ACDAEFZAGLAHGIBAJK $.
      $( [30-Sep-1999] $)

    $( Absolute value of a real number. $)
    absrei $p |- ( abs ` A ) = ( sqr ` ( A ^ 2 ) ) $=
      ( cr wcel cabs cfv c2 cexp co csqr wceq absre ax-mp ) ACDAEFAGHIJFKBALM
      $.
      $( [3-Aug-1999] $)
  $}

  ${
    abslti.1 $e |- A e. RR $.
    abslti.2 $e |- B e. RR $.
    $( Absolute value and 'less than' relation. $)
    abslti $p |- ( ( abs ` A ) < B <-> ( -u B < A /\ A < B ) ) $=
      ( cabs cfv clt wbr cneg wa cle renegcli leabsi recni absnegi breqtri
      abscli lelttri mpan jca wceq wo absori breq1 biimprcd jaao mpi ancoms
      impbii ltnegcon1i anbi1i bitri ) AEFZBGHZAIZBGHZABGHZJZBIAGHZUQJUNURUNUPU
      QUOUMKHUNUPUOUOEFUMKUOACLZMAACNZOPUOUMBUTAVAQZDRSAUMKHUNUQACMAUMBCVBDRSTU
      QUPUNUQUPJUMAUAZUMUOUAZUBUNACUCUQVCUNUPVDVCUNUQUMABGUDUEVDUNUPUMUOBGUDUEU
      FUGUHUIUPUSUQABCDUJUKUL $.
      $( [6-Apr-2005] $)

    $( Absolute value and 'less than or equal to' relation. $)
    abslei $p |- ( ( abs ` A ) <_ B <-> ( -u B <_ A /\ A <_ B ) ) $=
      ( cabs cfv cle wbr cneg wa renegcli leabsi recni absnegi breqtri abscli
      letri mpan jca wceq wo absori breq1 biimprcd jaao mpi ancoms impbii
      lenegcon1i anbi1i bitri ) AEFZBGHZAIZBGHZABGHZJZBIAGHZUPJUMUQUMUOUPUNULGH
      UMUOUNUNEFULGUNACKZLAACMZNOUNULBUSAUTPZDQRAULGHUMUPACLAULBCVADQRSUPUOUMUP
      UOJULATZULUNTZUAUMACUBUPVBUMUOVCVBUMUPULABGUCUDVCUMUOULUNBGUCUDUEUFUGUHUO
      URUPABCDUIUJUK $.
      $( [6-Apr-2005] $)
  $}

  $( The absolute value of 0. $)
  abs0 $p |- ( abs ` 0 ) = 0 $=
    ( cc0 cabs cfv wceq eqid 0cn abs00i mpbir ) ABCADAADAEAFGH $.
    $( [26-Mar-2005] $)

  $( The absolute value of the imaginary unit. $)
  absi $p |- ( abs ` _i ) = 1 $=
    ( ci cabs cfv ccj cmul co csqr c1 cc wcel wceq axicn absval ax-mp cneg
    mulneg2i cji opreq2i ixi mulcli ax1cn negcon2i mpbi 3eqtr4i fveq2i sqr1
    3eqtri ) ABCZAADCZEFZGCZHGCHAIJUHUKKLAMNUJHGAAOZEFAAEFZOZUJHAALLPUIULAEQRUM
    HOKHUNKSUMHAALLTUAUBUCUDUEUFUG $.
    $( [26-Mar-2005] $)

  $( The absolute value of an integer is a nonnegative integer. $)
  nn0abscl $p |- ( A e. ZZ -> ( abs ` A ) e. NN0 ) $=
    ( cz wcel cabs cfv cc0 cle wbr cn0 wceq cneg wo cr zre absor syl wi eleq1
    biimprd znegcl syl5bir jaoi mpcom cc zcn absge0 elnn0z sylanbrc ) ABCZADEZB
    CZFUJGHZUJICUJAJZUJAKZJZLZUIUKUIAMCUPANAOPUMUIUKQUOUMUKUIUJABRSUOUKUNBCUIUJ
    UNBRATUAUBUCUIAUDCULAUEAUFPUJUGUH $.
    $( [27-Feb-2005] $)

  $( Absolute value and 'less than' relation. $)
  abslt $p |- ( ( A e. RR /\ B e. RR ) ->
               ( ( abs ` A ) < B <-> ( -u B < A /\ A < B ) ) ) $=
    ( cr wcel cabs cfv clt wbr cneg wa wb cc0 cif wceq fveq2 breq1d breq2
    breq1 anbi12d bibi12d negeq 0re elimel abslti dedth2h ) ACDZBCDZAEFZBGHZBIZ
    AGHZABGHZJZKUFALMZEFZBGHZUJUNGHZUNBGHZJZKUOUGBLMZGHZUTIZUNGHZUNUTGHZJZKABLL
    AUNNZUIUPUMUSVFUHUOBGAUNEOPVFUKUQULURAUNUJGQAUNBGRSTBUTNZUPVAUSVEBUTUOGQVGU
    QVCURVDVGUJVBUNGBUTUAPBUTUNGQSTUNUTALCUBUCBLCUBUCUDUE $.
    $( [6-Apr-2005] $)

  $( Absolute value and 'less than or equal to' relation. $)
  absle $p |- ( ( A e. RR /\ B e. RR ) ->
               ( ( abs ` A ) <_ B <-> ( -u B <_ A /\ A <_ B ) ) ) $=
    ( cr wcel cabs cfv cle wbr cneg wa wb cc0 cif wceq fveq2 breq1d breq2
    breq1 anbi12d bibi12d negeq 0re elimel abslei dedth2h ) ACDZBCDZAEFZBGHZBIZ
    AGHZABGHZJZKUFALMZEFZBGHZUJUNGHZUNBGHZJZKUOUGBLMZGHZUTIZUNGHZUNUTGHZJZKABLL
    AUNNZUIUPUMUSVFUHUOBGAUNEOPVFUKUQULURAUNUJGQAUNBGRSTBUTNZUPVAUSVEBUTUOGQVGU
    QVCURVDVGUJVBUNGBUTUAPBUTUNGQSTUNUTALCUBUCBLCUBUCUDUE $.
    $( [6-Apr-2005] $)

  $( If the absolute value of a complex number is less than a real, its
     difference from the real is nonzero. $)
  abssubne0 $p |- ( ( A e. CC /\ B e. RR /\ ( abs ` A ) < B ) ->
                   ( B - A ) =/= 0 ) $=
    ( cc wcel cr cabs cfv clt wbr w3a cmin co cc0 wne wceq wn ltne abscl
    syl3an1 df-ne sylib wi wa cneg ltnr intnand wb abslt anidms mtbird pm2.21d
    imp a1i fveq2 breq1d anbi2d eqeq2d 3imtr4d com12 3adant1 mtod sylibr recn
    subeq0 sylan ancoms eqcom syl6bb necon3bid 3adant3 mpbird ) ACDZBEDZAFGZBHI
    ZJZBAKLZMNZABNZVPABOZPVSVPVTBVNOZVPBVNNZWAPVNEDVMVOWBVLVNBQARSBVNTUAVMVOVTW
    AUBVLVTVMVOUCZWAVTVMBFGZBHIZUCZBWDOZWCWAWFWGUBVTVMWEWGVMWEWGVMWEBUDBHIZBBHI
    ZUCZVMWIWHBUEUFVMWEWJUGBBUHUIUJUKULUMVTVOWEVMVTVNWDBHABFUNZUOUPVTVNWDBWKUQU
    RUSUTVAABTVBVLVMVRVSUGVOVLVMUCZVQMABWLVQMOZBAOZVTVMVLWMWNUGZVMBCDVLWOBVCBAV
    DVEVFBAVGVHVIVJVK $.
    $( [2-Nov-2007] $)

  $( The absolute value of a difference and 'less than' relation.  (Contributed
     by Paul Chapman, 18-Sep-2007.) $)
  absdiflt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                    ( ( abs ` ( A - B ) ) < C <->
                      ( ( B - C ) < A /\ A < ( B + C ) ) ) ) $=
    ( cr wcel w3a cmin co cabs cfv clt wbr cneg wa caddc wb resubcl abslt
    sylan 3impa ltaddsub2 renegcl syl3an2 3comr wceq cc recn negsub syl2an
    3adant1 breq1d bitr3d ltsubadd2 anbi12d bitrd ) ADEZBDEZCDEZFZABGHZIJCKLZCM
    ZUTKLZUTCKLZNZBCGHZAKLZABCOHKLZNUPUQURVAVEPZUPUQNUTDEURVIABQUTCRSTUSVCVGVDV
    HUSBVBOHZAKLZVCVGUQURUPVKVCPZUQVBDEUPVLURBVBAUACUBUCUDUSVJVFAKUQURVJVFUEZUP
    UQBUFECUFEVMURBUGCUGBCUHUIUJUKULABCUMUNUO $.
    $( [18-Sep-2007] $)

  $( The absolute value of a difference and 'less than or equal to' relation.
     (Contributed by Paul Chapman, 18-Sep-2007.) $)
  absdifle $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                    ( ( abs ` ( A - B ) ) <_ C <->
                      ( ( B - C ) <_ A /\ A <_ ( B + C ) ) ) ) $=
    ( cr wcel w3a cmin co cabs cfv cle wbr cneg wa caddc wb resubcl absle
    sylan 3impa leaddsub2 renegcl syl3an2 3comr wceq cc recn negsub syl2an
    3adant1 breq1d bitr3d lesubadd2 anbi12d bitrd ) ADEZBDEZCDEZFZABGHZIJCKLZCM
    ZUTKLZUTCKLZNZBCGHZAKLZABCOHKLZNUPUQURVAVEPZUPUQNUTDEURVIABQUTCRSTUSVCVGVDV
    HUSBVBOHZAKLZVCVGUQURUPVKVCPZUQVBDEUPVLURBVBAUACUBUCUDUSVJVFAKUQURVJVFUEZUP
    UQBUFECUFEVMURBUGCUGBCUHUIUJUKULABCUMUNUO $.
    $( [18-Sep-2007] $)

  $( Comparison to a nonnegative number based on comparison to squares. $)
  lenegsq $p |- ( ( A e. RR /\ B e. RR /\ 0 <_ B ) ->
              ( ( A <_ B /\ -u A <_ B ) <-> ( A ^ 2 ) <_ ( B ^ 2 ) ) ) $=
    ( cr wcel cc0 cle wbr cneg wa c2 cexp co wb cabs cfv cc recn abscl absge0
    jca syl le2sq sylan absle lenegcon1 anbi1d ancom syl5rbbr bitrd adantrr
    absresq breq1d adantr 3bitr3d 3impb ) ACDZBCDZEBFGZABFGZAHBFGZIZAJKLZBJKLZF
    GZMUPUQURIZIANOZBFGZVFJKLZVCFGZVAVDUPVFCDZEVFFGZIZVEVGVIMUPAPDZVLAQVMVJVKAR
    ASTUAVFBUBUCUPUQVGVAMURUPUQIZVGBHAFGZUSIZVAABUDVNUTUSIVPVAVNUTVOUSABUEUFUTU
    SUGUHUIUJUPVIVDMVEUPVHVBVCFAUKULUMUNUO $.
    $( [16-Jan-2006] $)

  $( The real part of a number is less than or equal to its absolute value.
     Proposition 10-3.7(d) of [Gleason] p. 133. $)
  releabs $p |- ( A e. CC -> ( Re ` A ) <_ ( abs ` A ) ) $=
    ( cc wcel cre cfv cabs recl cr recn abscl 3syl cle wbr leabs syl absrele
    letrd ) ABCZADEZSFEZAFEAGZRSHCZSBCTHCUASISJKAJRUBSTLMUASNOAPQ $.
    $( [1-Apr-2005] $)

  ${
    recvalzi.1 $e |- A e. CC $.
    $( Reciprocal expressed with a real denominator. $)
    recvalzi $p |- ( A =/= 0 ->
                  ( 1 / A ) = ( ( * ` A ) / ( ( abs ` A ) ^ 2 ) ) ) $=
      ( cc0 wne c1 cabs cfv c2 cexp co ccj cdiv wceq cmul cjcli mulcomi
      absvalsqi eqtr4i wb cc wcel cjne0 ax-mp abscli resqcli recni divmulzi
      sylbi mpbiri opreq2d clt wbr gt0ne0i absgt0i sqeq0i necon3bii 3imtr4i
      biimpi wa recdiv mpanr1 mpanl1 syl11anc eqtr3d ) ACDZEAFGZHIJZAKGZLJZLJZE
      ALJVHVGLJZVEVIAELVEVIAMZVHANJZVGMZVMAVHNJVGVHAABOZBPABQRVEVHCDZVLVNSATUAV
      EVPSBAUBUCZVGVHAVGVFABUDZUEUFZVOBUGUHUIUJVEVGCDZVPVJVKMZCVFUKULVFCDVEVTVF
      VRUMABUNVGCVFCVFVFVRUFUOUPUQVEVPVQURVGTUAZVTVPWAVSWBVTUSVHTUAVPWAVOVGVHUT
      VAVBVCVD $.
      $( [29-Apr-2005] $)
  $}

  ${
    cjdiv.1 $e |- A e. CC $.
    cjdiv.2 $e |- B e. CC $.
    $( Complex conjugate distributes over division. $)
    cjdivi $p |- ( B =/= 0 ->
                    ( * ` ( A / B ) ) = ( ( * ` A ) / ( * ` B ) ) ) $=
      ( cc0 wne cdiv co ccj cfv c1 cmul divreczi fveq2d cc wcel wceq recclzi
      cjmul sylancr wb cjne0 ax-mp cjcli sylbi cabs c2 cexp cr clt wbr abscli
      gt0ne0i absgt0i recni sqeq0i necon3bii 3imtr4i resqcli rerecclzi cjre
      3syl opreq2d syl abscji opreq1i opreq2i syl5eq 3eqtr4rd recvalzi eqtrd
      3eqtr4d eqtr2d 3eqtrd ) BEFZABGHZIJAKBGHZLHZIJZAIJZVQIJZLHZVTBIJZGHZVOVPV
      RIABCDMNVOAOPVQOPVSWBQCBDRAVQSTVOWDVTKWCGHZLHZWBVOWCEFZWDWFQBOPVOWGUADBUB
      UCZVTWCACUDBDUDZMUEVOWEWAVTLVOWCIJZWCUFJZUGUHHZGHZWCKBUFJZUGUHHZGHZLHZIJZ
      WEWAVOWJWPIJZLHZWJWPLHZWRWMVOWSWPWJLVOWOEFZWPUIPWSWPQEWNUJUKWNEFVOXBWNBDU
      LZUMBDUNWOEWNEWNWNXCUOUPUQURZWOWNXCUSZUTWPVAVBVCVOWCOPWPOPZWRWTQWIVOXBXFX
      DWOWOXEUOZRVDWCWPSTVOWJWOGHZXAWMVOXBXHXAQXDWJWOWCWIUDXGMVDWLWOWJGWKWNUGUH
      BDVEVFVGVHVIVOWGWEWMQWHWCWIVJUEVOVQWQIVOVQWCWOGHZWQBDVJVOXBXIWQQXDWCWOWIX
      GMVDVKNVLVCVMVN $.
      $( [13-Dec-2005] $) $( [29-Apr-2005] $)
  $}

  $( Complex conjugate distributes over division. $)
  cjdiv $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                   ( * ` ( A / B ) ) = ( ( * ` A ) / ( * ` B ) ) ) $=
    ( cc wcel cc0 wne cdiv co ccj cfv wceq wi cif opreq1 fveq2d fveq2 opreq1d
    eqeq12d imbi2d neeq1 opreq2 opreq2d imbi12d 0cn elimel cjdivi dedth2h
    3impia ) ACDZBCDZBEFZABGHZIJZAIJZBIJZGHZKZUIUJUKUQLUKUIAEMZBGHZIJZURIJZUOGH
    ZKZLUJBEMZEFZURVDGHZIJZVAVDIJZGHZKZLABEEAURKZUQVCUKVKUMUTUPVBVKULUSIAURBGNO
    VKUNVAUOGAURIPQRSBVDKZUKVEVCVJBVDETVLUTVGVBVIVLUSVFIBVDURGUAOVLUOVHVAGBVDIP
    UBRUCURVDAECUDUEBECUDUEUFUGUH $.
    $( [3-Feb-2006] $) $( [29-Apr-2005] $)

  ${
    releabs.1 $e |- A e. CC $.
    $( The real part of a number is less than or equal to its absolute value.
       Proposition 10-3.7(d) of [Gleason] p. 133. $)
    releabsi $p |- ( Re ` A ) <_ ( abs ` A ) $=
      ( cc wcel cre cfv cabs cle wbr releabs ax-mp ) ACDAEFAGFHIBAJK $.
      $( [2-Oct-1999] $)

    ${
      abstri.2 $e |- B e. CC $.
      $( Triangle inequality for absolute value.  Proposition 10-3.7(h) of
         [Gleason] p. 133. $)
      abstrii $p |- ( abs ` ( A + B ) ) <_ ( ( abs ` A ) + ( abs ` B ) ) $=
        ( caddc co cabs cfv cle wbr c2 cexp ccj cmul cre cjcli mulcli releabsi
        absmuli abscji opreq2i eqtri breqtri cc0 clt wb 2pos recli abscli
        remulcli 2re lemul2i ax-mp mpbi resqcli readdcli leadd2i sqabsaddi
        recni binom2i sqcli add32i 3brtr4i addcli absge0i addge0i mp2an le2sqi
        mpbir ) ABEFZGHZAGHZBGHZEFZIJZVKKLFZVNKLFZIJZVLKLFZVMKLFZEFZKABMHZNFZOH
        ZNFZEFZWAKVLVMNFZNFZEFZVPVQIWEWHIJZWFWIIJWDWGIJZWJWDWCGHZWGIWCAWBCBDPZQ
        ZRWLVLWBGHZNFWGAWBCWMSWOVMVLNBDTUAUBUCUDKUEJWKWJUFUGWDWGKWCWNUHZVLVMACU
        IZBDUIZUJZUKULUMUNWEWHWAKWDUKWPUJKWGUKWSUJZVSVTVLWQUOVMWRUOUPUQUNABCDUR
        VQVSWHEFVTEFWIVLVMVLWQUSZVMWRUSZUTVSWHVTVLXAVAWHWTUSVMXBVAVBUBVCUDVKIJU
        DVNIJZVOVRUFVJABCDVDZVEUDVLIJUDVMIJXCACVEBDVEVLVMWQWRVFVGVKVNVJXDUIVLVM
        WQWRUPVHVGVI $.
        $( [2-Oct-1999] $)
    $}
  $}

  $( The absolute value function is idempotent. $)
  absidm $p |- ( A e. CC -> ( abs ` ( abs ` A ) ) = ( abs ` A ) ) $=
    ( cc wcel cabs cfv cr cc0 cle wbr wceq abscl absge0 absid syl11anc ) ABCADE
    ZFCGOHIODEOJAKALOMN $.
    $( [20-Jul-2005] $) $( [20-Nov-2004] $)

  $( The absolute value of a nonzero number is positive. $)
  absgt0 $p |- ( A e. CC -> ( A =/= 0 <-> 0 < ( abs ` A ) ) ) $=
    ( cc wcel cc0 wne cabs cfv clt wbr wb cif wceq neeq1 fveq2 breq2d bibi12d
    0cn elimel absgt0i dedth ) ABCZADEZDAFGZHIZJUAADKZDEZDUEFGZHIZJADAUELZUBUFU
    DUHAUEDMUIUCUGDHAUEFNOPUEADBQRST $.
    $( [6-Mar-2005] $) $( [1-Oct-1999] $)

  $( The absolute value of a nonzero integer is a positive integer.
     (Contributed by Paul Chapman, 21-Mar-2011.)  (The proof was shortened by
     Andrew Salmon, 25-May-2011.) $)
  nnabscl $p |- ( ( N e. ZZ /\ N =/= 0 ) -> ( abs ` N ) e. NN ) $=
    ( cz wcel cc0 wne wa cabs cfv clt wbr cn cn0 nn0abscl nn0z syl adantr cc
    wb zcn absgt0 biimpa elnnz sylanbrc ) ABCZADEZFAGHZBCZDUFIJZUFKCUDUGUEUDUFL
    CUGAMUFNOPUDUEUHUDAQCUEUHRASATOUAUFUBUC $.
    $( [25-May-2011] $) $( [21-Mar-2011] $)

  $( Swapping order of subtraction doesn't change the absolute value. $)
  abssub $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( abs ` ( A - B ) ) = ( abs ` ( B - A ) ) ) $=
    ( cc wcel cmin co cabs cfv wceq cc0 cif opreq1 fveq2d opreq2 eqeq12d 0cn
    elimel abssubi dedth2h ) ACDZBCDZABEFZGHZBAEFZGHZITAJKZBEFZGHZBUFEFZGHZIUFU
    ABJKZEFZGHZUKUFEFZGHZIABJJAUFIZUCUHUEUJUPUBUGGAUFBELMUPUDUIGAUFBENMOBUKIZUH
    UMUJUOUQUGULGBUKUFENMUQUIUNGBUKUFELMOUFUKAJCPQBJCPQRS $.
    $( [1-Oct-1999] $)

  $( Absolute value of a nonnegative difference. $)
  abssubge0 $p |- ( ( A e. RR /\ B e. RR /\ A <_ B )
                      -> ( abs ` ( B - A ) ) = ( B - A ) ) $=
    ( cr wcel cle wbr cmin co cabs cfv wceq w3a cc0 resubcl 3adant3 subge0
    biimp3ar absid syl11anc 3com12 ) BCDZACDZABEFZBAGHZIJUDKZUAUBUCLUDCDZMUDEFZ
    UEUAUBUFUCBANOUAUBUGUCBAPQUDRST $.
    $( [14-Feb-2008] $)

  $( Absolute value of a nonpositive difference.  (Contributed by FL,
     3-Jan-2008.) $)
  abssuble0 $p |- ( ( A e. RR /\ B e. RR /\ A <_ B )
                      -> ( abs ` ( A - B ) ) = ( B - A ) ) $=
    ( cr wcel cle wbr w3a cmin co cabs cfv wceq cc recn abssub syl2an 3adant3
    abssubge0 eqtrd ) ACDZBCDZABEFZGABHIJKZBAHIZJKZUDTUAUCUELZUBTAMDBMDUFUAANBN
    ABOPQABRS $.
    $( [3-Jan-2008] $)

  $( The maximum of two numbers using absolute value. $)
  absmax $p |- ( ( A e. RR /\ B e. RR ) -> if ( A <_ B , B , A )
              = ( ( ( A + B ) + ( abs ` ( A - B ) ) ) / 2 ) ) $=
    ( cr wcel wa cle wbr cif caddc co cmin cabs cfv c2 cdiv wceq clt cmul cc
    recn cc0 wne 2cn 2ne0 divcan3 mp3an23 syl ad2antlr ltle imp abssubge0
    3expa syldan opreq2d simpr simpl ppncan syl111anc 2times adantl eqtr4d
    syl2an adantr eqtrd opreq1d wn ltnle biimpa iffalse 3eqtr4rd ancom1s
    abssuble0 addcom 3eqtr4d iftrue pm2.61ltlei ) ACDZBCDZEZABFGZBAHZABIJZABKJZ
    LMZIJZNOJZPZBAVRVQBAQGZWGVRVQEZWHEZNARJZNOJZAWFWAVQWLAPZVRWHVQASDZWMATZWNNS
    DZNUAUBZWMUCUDANUEUFUGUHWJWEWKNOWJWEWBWCIJZWKWJWDWCWBIWIWHBAFGZWDWCPZWIWHWS
    BAUIUJVRVQWSWTBAUKULUMUNWIWRWKPZWHVRBSDZWNXAVQBTZWOXBWNEZWRAAIJZWKXDWNXBWNW
    RXEPXBWNUOZXBWNUPXFABAUQURWNWKXEPXBAUSUTVAVBVCVDVEWJVTVFZWAAPWIWHXGBAVGVHVT
    BAVIUGVJVKVSVTEZNBRJZNOJZBWFWAVRXJBPZVQVTVRXBXKXCXBWPWQXKUCUDBNUEUFUGUHXHWE
    XINOXHWEWBBAKJZIJZXIXHWDXLWBIVQVRVTWDXLPABVLULUNVSXMXIPZVTVQWNXBXNVRWOXCWNX
    BEZBAIJZXLIJZBBIJZXMXIXOXBWNXBXQXRPWNXBUOZWNXBUPXSBABUQURXOWBXPXLIABVMVEXBX
    IXRPWNBUSUTVNVBVCVDVEVTWABPVSVTBAVOUTVJVQVRUOVQVRUPVP $.
    $( [7-Aug-2008] $)

  $( Triangle inequality for absolute value.  Proposition 10-3.7(h) of
     [Gleason] p. 133. $)
  abstri $p |- ( ( A e. CC /\ B e. CC ) ->
                ( abs ` ( A + B ) ) <_ ( ( abs ` A ) + ( abs ` B ) ) ) $=
    ( cc wcel caddc co cabs cfv cle wbr cc0 cif wceq opreq1 fveq2d fveq2
    opreq1d breq12d opreq2 opreq2d 0cn elimel abstrii dedth2h ) ACDZBCDZABEFZGH
    ZAGHZBGHZEFZIJUEAKLZBEFZGHZULGHZUJEFZIJULUFBKLZEFZGHZUOUQGHZEFZIJABKKAULMZU
    HUNUKUPIVBUGUMGAULBENOVBUIUOUJEAULGPQRBUQMZUNUSUPVAIVCUMURGBUQULESOVCUJUTUO
    EBUQGPTRULUQAKCUAUBBKCUAUBUCUD $.
    $( [7-Mar-2005] $)

  $( Absolute value of differences around common element.  (Contributed by FL,
     9-Oct-2006.) $)
  abs3dif $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
            ( abs ` ( A - B ) ) <_ ( ( abs ` ( A - C ) ) +
                      ( abs ` ( C - B ) ) ) ) $=
    ( cc wcel w3a cmin co caddc cabs cfv cle wceq npncan 3com23 fveq2d wbr
    subcl 3adant2 ancoms 3adant1 abstri syl11anc eqbrtrrd ) ADEZBDEZCDEZFZACGHZ
    CBGHZIHZJKZABGHZJKUIJKUJJKIHZLUHUKUMJUEUGUFUKUMMACBNOPUHUIDEZUJDEZULUNLQUEU
    GUOUFACRSUFUGUPUEUGUFUPCBRTUAUIUJUBUCUD $.
    $( [9-Oct-2006] $)

  ${
    abs3dif.1 $e |- A e. CC $.
    abs3dif.2 $e |- B e. CC $.
    abs3dif.3 $e |- C e. CC $.
    $( Absolute value of differences around common element. $)
    abs3difi $p |- ( abs ` ( A - B ) ) <_ ( ( abs ` ( A - C ) ) +
                  ( abs ` ( C - B ) ) ) $=
      ( cc wcel cmin co cabs cfv caddc cle wbr abs3dif mp3an ) AGHBGHCGHABIJKLA
      CIJKLCBIJKLMJNODEFABCPQ $.
      $( [2-Oct-1999] $)
    ${
      abs3lem.4 $e |- D e. RR $.
      $( Lemma involving absolute value of differences. $)
      abs3lemi $p |- ( ( ( abs ` ( A - C ) ) < ( D / 2 ) /\
                         ( abs ` ( C - B ) ) < ( D / 2 ) ) ->
                         ( abs ` ( A - B ) ) < D ) $=
        ( cmin co cabs cfv c2 cdiv clt wbr wa caddc cle abs3difi subcli abscli
        2re 2ne0 redivcli lt2addi readdcli lelttri sylancr cmul recni 2timesi
        2cn divcan2i eqtr3i syl6breq ) ACIJZKLZDMNJZOPCBIJZKLZUSOPQZABIJZKLZUSU
        SRJZDOVBVDURVARJZSPVFVEOPVDVEOPABCEFGTURVAUSUSUQACEGUAUBZUTCBGFUAUBZDMH
        UCUDUEZVIUFVDVFVEVCABEFUAUBURVAVGVHUGUSUSVIVIUGUHUIMUSUJJVEDUSUSVIUKULD
        MDHUKUMUDUNUOUP $.
        $( [2-Oct-1999] $)
    $}
  $}

  $( Difference of absolute values.  (Contributed by Paul Chapman,
     7-Sep-2007.) $)
  abs2dif $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( ( abs ` A ) - ( abs ` B ) ) <_ ( abs ` ( A - B ) ) ) $=
    ( cc wcel wa cc0 cmin co cabs cfv cle subid1 fveq2d opreqan12d wbr caddc
    0cn abs3dif mp3an2 cr w3a wb subcl mpan2 abscl syl anim12i df-3an sylanbrc
    lesubadd mpbird eqbrtrrd ) ACDZBCDZEZAFGHZIJZBFGHZIJZGHZAIJZBIJZGHABGHZIJZK
    UMUNUQVAUSVBGUMUPAIALMUNURBIBLMNUOUTVDKOZUQVDUSPHKOZUMFCDZUNVFQAFBRSUOUQTDZ
    USTDZVDTDZUAZVEVFUBUOVHVIEVJVKUMVHUNVIUMUPCDZVHUMVGVLQAFUCUDUPUEUFUNURCDZVI
    UNVGVMQBFUCUDURUEUFUGUOVCCDVJABUCVCUEUFVHVIVJUHUIUQUSVDUJUFUKUL $.
    $( [7-Sep-2007] $)

  $( Absolute value of difference of absolute values.  (Contributed by Paul
     Chapman, 7-Sep-2007.) $)
  abs2difabs $p |- ( ( A e. CC /\ B e. CC ) ->
            ( abs ` ( ( abs ` A ) - ( abs ` B ) ) ) <_ ( abs ` ( A - B ) ) ) $=
    ( cc wcel wa cabs cfv cmin co cle wbr cneg cr wb abscl resubcl syl2an
    subcl syl absle syl11anc lenegcon1 anbi1d bitr4d abs2dif ancoms wceq recnd
    negsubdi2 abssub 3brtr4d mpbir2and ) ACDZBCDZEZAFGZBFGZHIZFGABHIZFGZJKZURLZ
    UTJKZURUTJKZUOVAUTLURJKZVDEZVCVDEUOURMDZUTMDZVAVFNUMUPMDUQMDVGUNAOZBOZUPUQP
    QZUOUSCDVHABRUSOSZURUTTUAUOVCVEVDUOVGVHVCVENVKVLURUTUBUAUCUDUOUQUPHIZBAHIFG
    ZVBUTJUNUMVMVNJKBAUEUFUMUPCDUQCDVBVMUGUNUMUPVIUHUNUQVJUHUPUQUIQABUJUKABUEUL
    $.
    $( [7-Sep-2007] $)

  ${
    $d x A $.
    abs1m.1 $e |- A e. CC $.
    $( For any complex number, there exists a unit-magnitude multiplier that
       produces its absolute value.  Part of proof of Theorem 13-2.12 of
       [Gleason] p. 195. $)
    abs1mi $p |- E. x e. CC ( ( abs ` x ) = 1 /\ ( abs ` A ) = ( x x. A ) ) $=
      ( cv cabs cfv c1 wceq cmul co wa cc wrex cc0 wcel ax1cn abs00i biimpri
      opreq2 0cn mulid2i syl6eq eqtr4d cle wbr 0re 1re lt01 ltleii absidi
      ax-mp jctil fveq2 eqeq1d opreq1 eqeq2d anbi12d rcla4ev sylancr wne ccj
      cdiv necon3bii cjcli abscli recni divclzi absdivzi divid mpan abscji
      absidm opreq12i syl5eq eqtrd c2 cexp absvalsqi sqvali mulcomi 3eqtr3i
      mulcli divmulzi mpbiri div23 mp3an12 eqtr3d jca32 sylbir syl pm2.61ine )
      ADZEFZGHZBEFZWLBIJZHZKZALMZBNBNHZGLOGEFZGHZWOGBIJZHZKZWSPWTXDXBWTWONXCWON
      HWTBCQZRWTXCGNIJNBNGISNTUAUBUCNGUDUEXBNGUFUGUHUIGUGUJUKULWRXEAGLWLGHZWNXB
      WQXDXGWMXAGWLGEUMUNXGWPXCWOWLGBIUOUPUQURUSBNUTZBVAFZWOVBJZLOZXJEFZGHZWOXJ
      BIJZHZKZKZWSXHWONUTZXQWONBNXFVCXRXKXMXOXIWOBCVDZWOBCVEVFZVGXRXLXIEFZWOEFZ
      VBJZGXIWOXSXTVHXRWOWOVBJZGYCWOLOZXRYDGHXTWOVIVJYAWOYBWOVBBCVKBLOZYBWOHCBV
      LUKVMVNVOXRXIBIJZWOVBJZWOXNXRYHWOHWOWOIJZYGHWOVPVQJBXIIJYIYGBCVRWOXTVSBXI
      CXSVTWAYGWOWOXIBXSCWBXTXTWCWDYEXRYHXNHZXTXILOYFYEXRKYJXSCXIBWOWEWFVJWGWHW
      IWRXPAXJLWLXJHZWNXMWQXOYKWMXLGWLXJEUMUNYKWPXNWOWLXJBIUOUPUQURWJWK $.
      $( [26-Mar-2005] $)
  $}

  ${
    $d x y A $.  $d x B $.
    $( Cancellation law involving the real part of a complex number. $)
    recan $p |- ( ( A e. CC /\ B e. CC ) -> ( A. x e. CC
                 ( Re ` ( x x. A ) ) = ( Re ` ( x x. B ) ) <-> A = B ) ) $=
      ( cc wcel wa cv cmul co cre cfv wceq wral c1 ci cneg caddc cim replim
      mulid2 eqcomd fveq2d imre opreq2d opreq12d eqtrd eqeqan12d wi ax1cn
      opreq1 eqeq12d rcla4v ax-mp axicn negcli syl5bir opreq2 a1d r19.21aiv
      impbid1 ) BDEZCDEZFZAGZBHIZJKZVDCHIZJKZLZADMZBCLZVCVKNBHIZJKZOOPZBHIZJKZH
      IZQIZNCHIZJKZOVNCHIZJKZHIZQIZLVJVAVBBVRCWDVABBJKZOBRKZHIZQIVRBSVAWEVMWGVQ
      QVABVLJVAVLBBTUAUBVAWFVPOHBUCUDUEUFVBCCJKZOCRKZHIZQIWDCSVBWHVTWJWCQVBCVSJ
      VBVSCCTUAUBVBWIWBOHCUCUDUEUFUGVJVMVTVQWCQNDEVJVMVTLZUHUIVIWKANDVDNLZVFVMV
      HVTWLVEVLJVDNBHUJUBWLVGVSJVDNCHUJUBUKULUMVJVPWBOHVNDEVJVPWBLZUHOUNUOVIWMA
      VNDVDVNLZVFVPVHWBWNVEVOJVDVNBHUJUBWNVGWAJVDVNCHUJUBUKULUMUDUEUPVKVIADVKVI
      VDDEVKVEVGJBCVDHUQUBURUSUT $.
      $( [24-Jul-2008] $) $( [12-May-2005] $)

    $( Mapping domain and codomain of the absolute value function. $)
    absf $p |- abs : CC --> RR $=
      ( vx vy cc cr cv ccj cfv cmul co csqr cabs df-abs wcel absval abscl
      eqeltrrd fopab ) ABCDAEZRFGHIJGZKABLRCMRKGSDRNROPQ $.
      $( [30-Aug-2007] $)
  $}

  $( Lemma involving absolute value of differences. $)
  abs3lem $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\
                   D e. RR ) ) -> (
          ( ( abs ` ( A - C ) ) < ( D / 2 ) /\
            ( abs ` ( C - B ) ) < ( D / 2 ) ) ->
            ( abs ` ( A - B ) ) < D ) ) $=
    ( cc wcel cr cmin co cabs cfv c2 cdiv clt wbr wa wi cc0 cif wceq opreq1
    fveq2d breq1d anbi1d imbi12d opreq2 anbi2d anbi12d imbi1d breq2d breq2 0cn
    elimel 0re abs3lemi dedth4h ) AEFZBEFZCEFZDGFZACHIZJKZDLMIZNOZCBHIZJKZVCNOZ
    PZABHIZJKZDNOZQUQARSZCHIZJKZVCNOZVGPZVLBHIZJKZDNOZQVOCURBRSZHIZJKZVCNOZPZVL
    VTHIZJKZDNOZQVLUSCRSZHIZJKZVCNOZWHVTHIZJKZVCNOZPZWGQWJUTDRSZLMIZNOZWMWQNOZP
    ZWFWPNOZQABCDRRRRAVLTZVHVPVKVSXBVDVOVGXBVBVNVCNXBVAVMJAVLCHUAUBUCUDXBVJVRDN
    XBVIVQJAVLBHUAUBUCUEBVTTZVPWDVSWGXCVGWCVOXCVFWBVCNXCVEWAJBVTCHUFUBUCUGXCVRW
    FDNXCVQWEJBVTVLHUFUBUCUECWHTZWDWOWGXDVOWKWCWNXDVNWJVCNXDVMWIJCWHVLHUFUBUCXD
    WBWMVCNXDWAWLJCWHVTHUAUBUCUHUIDWPTZWOWTWGXAXEWKWRWNWSXEVCWQWJNDWPLMUAZUJXEV
    CWQWMNXFUJUHDWPWFNUKUEVLVTWHWPAREULUMBREULUMCREULUMDRGUNUMUOUP $.
    $( [2-Oct-1999] $)

  ${
    abslem2.1 $e |- A e. CC $.
    ${
      abslem2i.2 $e |- A =/= 0 $.
      $( Lemma involving absolute values. $)
      abslem2i $p |- ( ( ( * ` ( A / ( abs ` A ) ) ) x. A ) +
      ( ( A / ( abs ` A ) ) x. ( * ` A ) ) ) = ( 2 x. ( abs ` A ) ) $=
        ( c2 cabs cfv cmul co caddc cdiv ccj abscli recni 2timesi cr wcel wceq
        cjrebi mpbi cexp sqvali absvalsqi eqtr3i opreq1i cc0 wne abs00i
        necon3bii mpbir divcan4i cjcli div23i 3eqtr3i fveq2i divcli cjmuli
        cjcji opreq2i 3eqtri opreq12i eqtr2i ) DAEFZGHVBVBIHAVBJHZKFZAGHZVCAKFZ
        GHZIHVBVBABLZMZNVBVEVBVGIVBVGKFZVDVFKFZGHVEVBKFZVBVJVBOPVLVBQVHVBVIRSVB
        VGKVBVBGHZVBJHAVFGHZVBJHVBVGVMVNVBJVBDTHVMVNVBVIUAABUBUCUDVBVBVIVIVBUEU
        FAUEUFCVBUEAUEABUGUHUIZUJAVFVBBABUKZVIVOULUMZUNUCVCVFAVBBVIVOUOVPUPVKAV
        DGABUQURUSVQUTVA $.
        $( [11-Oct-1999] $)
    $}

    $( Lemma involving absolute values. $)
    abslem2 $p |- ( A =/= 0 -> ( ( ( * ` ( A / ( abs ` A ) ) ) x. A ) +
      ( ( A / ( abs ` A ) ) x. ( * ` A ) ) ) = ( 2 x. ( abs ` A ) ) ) $=
      ( cc0 wne cabs cfv cdiv co ccj cmul caddc c2 wceq c1 cif id fveq2
      opreq12d fveq2d opreq2d eqeq12d cc ax1cn keepel elimne0 abslem2i dedth )
      ACDZAAEFZGHZIFZAJHZUJAIFZJHZKHZLUIJHZMUHANOZUQEFZGHZIFZUQJHZUSUQIFZJHZKHZ
      LURJHZMANAUQMZUOVDUPVEVFULVAUNVCKVFUKUTAUQJVFUJUSIVFAUQUIURGVFPZAUQEQZRZS
      VGRVFUJUSUMVBJVIAUQIQRRVFUIURLJVHTUAUQUHANUBBUCUDAUEUFUG $.
      $( [11-Oct-1999] $)
  $}

  ${
    $d x y z A $.  $d x y z w F $.
    seq1bnd.1 $e |- F : NN --> CC $.
    $( An initial segment of an infinite sequence of complex numbers is
       bounded. $)
    seq1bndi $p |- ( A e. NN -> E. x e. RR A. y e. NN ( y <_ A ->
                 ( abs ` ( F ` y ) ) < x ) ) $=
      ( vz vw cv cle wbr cfv cabs clt wi cn wral cr wrex c1 caddc co wceq
      breq2 imbi1d rexralbidv wcel cc wf 1nn ffvelrn mp2an abscli 1re readdcli
      nnle1eq1 fveq2 fveq2d ltp1i syl6eqbr syl6bi rgen imbi2d ralbidv rcla4ev
      wa cif wo wb nnre peano2nn syl leloe syl2an nnleltp1 orbi1d bitr4d
      ancoms adantlr adantr max1ALT ad2antlr ffvelrni abscl adantl simplr
      peano2re 3syl ifcl sylan ltletr syl111anc mpan2d imim2d imp breq1d ltp1
      syl5cbir ad2antrr max2 sylan2 syld jaod sylbid ex ralimdva jctild syl6
      r19.23adva cbvrexv syl5ib nnind ) BHZFHZIJZXLDKZLKZAHZMJZNZBOPAQRXLSIJZXR
      NZBOPZAQRZXLGHZIJZXRNZBOPZAQRZXLYDSTUAZIJZXRNZBOPZAQRZXLCIJZXRNZBOPAQRFGC
      XMSUBZXSYAABQOYPXNXTXRXMSXLIUCUDUEXMYDUBZXSYFABQOYQXNYEXRXMYDXLIUCUDUEXMY
      IUBZXSYKABQOYRXNYJXRXMYIXLIUCUDUEXMCUBZXSYOABQOYSXNYNXRXMCXLIUCUDUESDKZLK
      ZSTUAZQUFXTXPUUBMJZNZBOPZYCUUASYTOUGDUHSOUFYTUGUFEUIOUGSDUJUKULZUMUNUUDBO
      XLOUFZXTXLSUBZUUCXLUOUUHXPUUAUUBMUUHXOYTLXLSDUPUQUUAUUFURUSUTVAYBUUEAUUBQ
      XQUUBUBZYAUUDBOUUIXRUUCXTXQUUBXPMUCVBVCVDUKYDOUFZYEXPXMMJZNZBOPZFQRYMYHUU
      JUUMYMFQUUJXMQUFZVEZUUMXMYIDKZLKZSTUAZIJZUURXMVFZQUFZYJXPUUTMJZNZBOPZVEYM
      UUOUUMUVDUVAUUOUULUVCBOUUOUUGVEZUULUVCUVEUULVEZYJYEXLYIUBZVGZUVBUVEYJUVHV
      HZUULUUJUUGUVIUUNUUGUUJUVIUUGUUJVEZYJXLYIMJZUVGVGZUVHUUGXLQUFYIQUFZYJUVLV
      HUUJXLVIUUJYIOUFZUVMYDVJZYIVIVKXLYIVLVMUVJYEUVKUVGXLYDVNVOVPVQVRVSUVFYEUV
      BUVGUVEUULYEUVBNUVEUUKUVBYEUVEUUKXMUUTIJZUVBUUNUVPUUJUUGXMUURVTWAUVEXPQUF
      ZUUNUVAUUKUVPVEUVBNUUGUVQUUOUUGXOUGUFUVQOUGXLDEWBXOWCVKWDZUUJUUNUUGWEUUOU
      VAUUGUUJUURQUFZUUNUVAUUJUUPUGUFZUUQQUFZUVSUUJUVNUVTUVOOUGYIDEWBVKZUUPWCZU
      UQWFWGZUUSUURXMQWHWIZVSZXPXMUUTWJWKWLWMWNUVEUVGUVBNUULUVEUVGXPUURMJZUVBUU
      JUVGUWGNUUNUUGUVGUWGUUQUURMJZUUJUVGXPUUQUURMUVGXOUUPLXLYIDUPUQWOUUJUVTUWA
      UWHUWBUWCUUQWPWGWQWRUVEUWGUURUUTIJZUVBUUOUWIUUGUUNUUJUWIUUJUUNUVSUWIUWDXM
      UURWSWTVQVSUVEUVQUVSUVAUWGUWIVEUVBNUVRUUJUVSUUNUUGUWDWRUWFXPUURUUTWJWKWLX
      AVSXBXCXDXEUWEXFYLUVDAUUTQXQUUTUBZYKUVCBOUWJXRUVBYJXQUUTXPMUCVBVCVDXGXHYG
      UUMAFQXQXMUBZYFUULBOUWKXRUUKYEXQXMXPMUCVBVCXIXJXK $.
      $( [3-Apr-2005] $)
  $}

  ${
    $d y A $.  $d x y z w v B $.  $d y z w v F $.
    seq1ub.1 $e |- F : NN --> RR $.
    $( Lemma for ~ seq1ubi . $)
    seq1ublem $p |- ( B e. NN -> ( ran ( F |` { x e. NN | x <_ B } ) C_ RR /\
        ran ( F |` { x e. NN | x <_ B } ) =/= (/) /\
        E. y e. RR A. z e. ran ( F |` { x e. NN | x <_ B } ) z <_ y ) ) $=
      ( vw vv cn wcel cv cle wbr crab cres crn cr wss c0 wne wral wrex wf
      ssrab2 fssres mp2an frn ax-mp a1i c1 cfv wfn ffn fnssres wa nnge1 1nn
      jctil breq1 elrab sylibr fnfvelrn sylancr ne0i syl cabs clt wi cc
      axresscn fss seq1bndi wceq fveq2 fveq2d breq1d imbi12d rcla4cv imp32
      adantll adantr ffvelrni leabs recn abscl ancli lelttr 3expa sylan mpand
      ltle syld ancoms adantrr wb fvres sylbir adantl sylibrd biimpd sylan9
      adantllr mpd exp31 syl5ib r19.23adv fvelrnb ex r19.21adv reximia 3jca )
      DIJZEAKZDLMZAINZOZPZQRZXQSTZCKZBKZLMZCXQUAZBQUBZXRXLXOQXPUCZXRIQEUCZXOIRZ
      YEFXNAIUDZIQXOEUEUFXOQXPUGUHUIXLUJXPUKZXQJZXSXLXPXOULZUJXOJZYJEIULZYGYKYF
      YMFIQEUMUHYHIXOEUNUFZXLUJIJZUJDLMZUOYLXLYPYODUPUQURXNYPAUJIXMUJDLUSUTVAXO
      UJXPVBVCXQYIVDVEXLGKZDLMZYQEUKZVFUKZYAVGMZVHZGIUAZBQUBYDBGDEYFQVIRIVIEUCF
      VJIQVIEVKUFVLUUCYCBQYAQJZUUCYBCXQUUDUUCXTXQJZYBVHUUDUUCUOZHKZXPUKZXTVMZHX
      OUBZYBUUEUUFUUIYBHXOUUFUUGIJZUUGDLMZUOZUUIYBVHUUGXOJZUUFUUMUUIYBUUFUUMUOZ
      UUIUOUUGEUKZVFUKZYAVGMZYBUUOUURUUIUUCUUMUURUUDUUCUUKUULUURUUBUULUURVHGUUG
      IYQUUGVMZYRUULUUAUURYQUUGDLUSUUSYTUUQYAVGUUSYSUUPVFYQUUGEVNVOVPVQVRVSVTWA
      UUDUUMUUIUURYBVHUUCUUDUUMUOZUURUUHYALMZUUIYBUUTUURUUPYALMZUVAUUDUUKUURUVB
      VHZUULUUKUUDUVCUUKUUPQJZUUDUVCIQUUGEFWBUVDUUDUOZUURUUPYAVGMZUVBUVEUUPUUQL
      MZUURUVFUVDUVGUUDUUPWCWAUVDUVDUUQQJZUOUUDUVGUURUOUVFVHZUVDUVHUVDUUPVIJUVH
      UUPWDUUPWEVEWFUVDUVHUUDUVIUUPUUQYAWGWHWIWJUUPYAWKWLWIWMWNUUMUVAUVBWOUUDUU
      MUUHUUPYALUUMUUNUUHUUPVMXNUULAUUGIXMUUGDLUSUTZUUGXOEWPWQVPWRWSUUIUVAYBUUH
      XTYALUSWTXAXBXCXDUVJXEXFYKUUEUUJWOYNHXOXTXPXGUHXEXHXIXJVEXK $.
      $( [17-Jun-2007] $) $( [20-Apr-2005] $)

    $( An upper bound for an initial segment of a sequence of reals. $)
    seq1ubi $p |- ( ( A e. NN /\ B e. NN /\ A <_ B ) ->
           ( F ` A ) <_ sup ( ran ( F |` { x e. NN | x <_ B } ) , RR , < ) ) $=
      ( vz vy cn wcel cle wbr w3a cv crab cres crn cr wss c0 wne wral wrex cfv
      clt csup seq1ublem 3ad2ant2 wa wceq breq1 cbvrabv elrab2 fvres sylbir
      wfn wf ffn ax-mp ssrab2 fnssres mp2an biimpri fnfvelrn sylancr eqeltrrd
      3adant2 suprub syl11anc ) BHIZCHIZBCJKZLDAMZCJKZAHNZOZPZQRVPSTFMGMZJKFVPU
      AGQUBLZBDUCZVPIZVSVPQUDUEJKVJVIVRVKAGFCDEUFUGVIVKVTVJVIVKUHZBVOUCZVSVPWAB
      VNIZWBVSUIVQCJKZVKGBHVNVQBCJUJVMWDAGHVLVQCJUJUKULZBVNDUMUNWAVOVNUOZWCWBVP
      IDHUOZVNHRWFHQDUPWGEHQDUQURVMAHUSHVNDUTVAWCWAWEVBVNBVOVCVDVEVFGFVPVSVGVH
      $.
      $( [20-Apr-2005] $)
  $}

  ${
    $d x y z $.  $d y z R $.
    cau2.1 $e |- F : NN --> CC $.
    cau2.2 $e |- ( x e. RR -> ( 0 < x -> 0 R x ) ) $.
    $( Two ways to express that a sequence meets the Cauchy criterion.  Remark
       in [Gleason] p. 181. ` R ` can be either ` < ` or ` <_ ` . $)
    cau2i $p |- ( A. x e. RR ( 0 < x -> E. y e. NN A. z e. NN ( y < z ->
                      ( abs ` ( ( F ` z ) - ( F ` y ) ) ) R x ) ) <->
                    A. x e. RR ( 0 < x -> E. y e. NN A. z e. NN ( y <_ z ->
                      ( abs ` ( ( F ` z ) - ( F ` y ) ) ) R x ) ) ) $=
      ( cc0 cv clt wbr cfv cmin co cabs wi cn wral wrex cle cr wcel wa wceq wo
      imp fveq2 opreq2d cc ffvelrni subid syl sylan9eqr fveq2d abs0 syl6eq
      breq1d biimprcd exp3a adantlr biantrud jaob syl6bbr wb nnre leloe syl2an
      adantll imbi1d bitr4d ralbidva rexbidva pm5.74da ralbiia ) HAIZJKZBIZCIZJ
      KZVRELZVQELZMNZOLZVODKZPZCQRZBQSZPVPVQVRTKZWDPZCQRZBQSZPAUAVOUAUBZVPWGWKW
      LVPUCZWFWJBQWMVQQUBZUCZWEWICQWOVRQUBZUCZWEVSVQVRUDZUEZWDPZWIWQWEWEWRWDPZU
      CWTWQXAWEWMWPXAWNWMWPXAWMHVODKZWPXAPWLVPXBGUFXBWPWRWDWPWRUCZWDXBXCWCHVODX
      CWCHOLHXCWBHOWRWPWBVTVTMNZHWRWAVTVTMVQVREUGUHWPVTUIUBXDHUDQUIVREFUJVTUKUL
      UMUNUOUPUQURUSULUFUTVAVSWDWRVBVCWQWHWSWDWNWPWHWSVDZWMWNVQUAUBVRUAUBXEWPVQ
      VEVRVEVQVRVFVGVHVIVJVKVLVMVN $.
      $( [10-Apr-2005] $)
  $}

  ${
    $d m ph $.  $d j m W $.  $d j k m Z $.
    cau3i.1 $e |- Z C_ ZZ $.
    $( A relationship used to derive two ways to express a Cauchy sequence. $)
    cau3ii $p |- ( E. m e. Z A. j e. Z A. k e. W ( ( m <_ j /\ m <_ k ) ->
                ph ) -> E. j e. Z A. k e. W ( j <_ k -> ph ) ) $=
      ( cv cle wbr wa wi wral wrex wsbc wcel ax-17 hbs1 hbim wceq breq2 anbi1d
      sbequ12 imbi12d rcla4 cr cz zssre sstri sseli leid syl biantrurd imbi1d
      sylibrd ralimdv ralcom syl5ib reximia hbral breq1 ralbidv cbvrex sylibr
      ) DHZBHZIJZVECHZIJZKZALZCEMBFMZDFNVIABVEOZLZCEMZDFNVFVHIJZALZCEMZBFNVLVOD
      FVEFPZVKBFMZCEMVOVLVSVTVNCEVSVTVEVEIJZVIKZVMLZVNVKWCBVEFWBVMBWBBQABDRZSVF
      VETZVJWBAVMWEVGWAVIVFVEVEIUAUBABDUCZUDUEVSVIWBVMVSWAVIVSVEUFPWAFUFVEFUGUF
      GUHUIUJVEUKULUMUNUOUPVKBCFEUQURUSVRVOBDFVRDQVNBCEVHEPBQVIVMBVIBQWDSUTWEVQ
      VNCEWEVPVIAVMVFVEVHIVAWFUDVBVCVD $.
      $( [17-Dec-2006] $)
  $}

  ${
    $d j k m n x y R $.  $d j k m n x y W $.  $d j k m n x y Z $.
    $d j n y ch $.  $d j k m y et $.  $d m n y ph $.  $d k ps $.  $d x th $.
    $d x ta $.
    cau3ir.1 $e |- ( k = m -> ( ph <-> ps ) ) $.
    cau3ir.2 $e |- ( j = k -> ( ps <-> ch ) ) $.
    cau3ir.3 $e |- ( x = ( y / 2 ) -> ( ( ph /\ ps ) <-> th ) ) $.
    cau3ir.4 $e |- ( x = y -> ( ch <-> ta ) ) $.
    cau3ir.5 $e |- ( ( ( et /\ y e. RR ) /\ ( j e. Z /\ k e. W /\ m e. W ) ) ->
                   ( th -> ta ) ) $.
    $( A relationship used to derive two ways to express a Cauchy sequence.
       Normally ` Z ` and ` W ` are subsets of ` ZZ ` , and ` R ` is ` <_ ` or
       ` < ` . ` ph ` is ` ph ( j , k , x ) ` . $)
    cau3iri $p |- ( et -> ( A. x e. RR ( 0 < x -> E. j e. Z A. k e. W ( j R k
       -> ph ) ) -> A. x e. RR ( 0 < x -> E. m e. Z A. j e. W A. k e. W
          ( ( m R j /\ m R k ) -> ph ) ) ) ) $=
      ( vn cc0 cv clt wbr wi wral wrex cr wa wcel raaanv wceq breq2 imbi12d
      cbvralv anbi2i anidm 3bitr2ri prth ralimi sylbi reximi imim2i c2 cdiv co
      wb halfpos2 adantl rehalfcl imbi2d 2ralbidv rexbidv rcla4cva sylan2
      sylbid sylan 3exp2 imp41 imim2d ralimdva reximdva adantrl syld exp32
      r19.21adv breq1 anbi12d imbi1d cbvrexv anbi2d ralbii anbi1d ralbidv
      bitr2i rexbii 3bitri imbi2i bitr3i syl6ib ) FUAGUBZUCUDZJUBZKUBZIUDZAUEZK
      MUFZJNUGZUEZGUHUFZUAHUBZUCUDZXEXCLUBZIUDZUIZEUEZLMUFZKMUFZJNUGZUEZHUHUFZX
      BXMXCIUDZXMXDIUDZUIZAUEZKMUFJMUFZLNUGZUEZGUHUFZFXJXTHUHFXJXKUHUJZXTFXJYJU
      IZUIXLXODUEZLMUFZKMUFZJNUGZXSYKXLYOUEZFXJXBXOABUIZUEZLMUFZKMUFZJNUGZUEZGU
      HUFZYJYPXIUUBGUHXHUUAXBXGYTJNXGXFXNBUEZUIZLMUFZKMUFZYTUUGXGUUDLMUFZUIXGXG
      UIXGXFUUDKLMUKXGUUHXGXFUUDKLMXDXMULZXEXNABXDXMXCIUMOUNUOUPXGUQURUUFYSKMUU
      EYRLMXEAXNBUSUTUTVAVBVCUTUUCYJUIXLUAXKVDVEVFZUCUDZYOYJXLUUKVGUUCXKVHVIYJU
      UCUUJUHUJUUKYOUEZXKVJUUBUULGUUJUHXAUUJULZXBUUKUUAYOXAUUJUAUCUMUUMYTYNJNUU
      MYRYLKLMMUUMYQDXOQVKVLVMUNVNVOVPVQVIFYJYOXSUEXJFYJUIZYNXRJNUUNXCNUJZUIZYM
      XQKMUUPXDMUJZUIZYLXPLMUURXMMUJZUIDEXOUUNUUOUUQUUSDEUEZUUNUUOUUQUUSUUTSVRV
      SVTWAWAWBWCWDWEWFYAXBXOCUEZLMUFKMUFZJNUGZUEZGUHUFYIUVDXTGHUHXAXKULZXBXLUV
      CXSXAXKUAUCUMUVEUVBXRJNUVEUVAXPKLMMUVECEXORVKVLVMUNUOUVDYHGUHUVCYGXBUVCTU
      BZXDIUDZUVFXMIUDZUIZCUEZLMUFZKMUFZTNUGUVFXCIUDZUVGUIZAUEZKMUFZJMUFZTNUGYG
      UVBUVLJTNXCUVFULZUVAUVJKLMMUVRXOUVICUVRXEUVGXNUVHXCUVFXDIWGXCUVFXMIWGWHWI
      VLWJUVLUVQTNUVQUVMUVHUIZBUEZLMUFZJMUFUVLUVPUWAJMUVOUVTKLMUUIUVNUVSABUUIUV
      GUVHUVMXDXMUVFIUMWKOUNUOWLUWAUVKJKMXCXDULZUVTUVJLMUWBUVSUVIBCUWBUVMUVGUVH
      XCXDUVFIUMWMPUNWNUOWOWPUVQYFTLNUVFXMULZUVOYEJKMMUWCUVNYDAUWCUVMYBUVGYCUVF
      XMXCIWGUVFXMXDIWGWHWIVLWJWQWRWLWSWT $.
      $( [17-Dec-2006] $)
  $}

  ${
    $d j k m n x y R $.  $d j k m n x y Z $.  $d j n y ch $.  $d j k m y et $.
    $d m n y ph $.  $d k ps $.  $d x th $.  $d x ta $.
    cau3.1 $e |- ( k = m -> ( ph <-> ps ) ) $.
    cau3.2 $e |- ( j = k -> ( ps <-> ch ) ) $.
    cau3.3 $e |- ( x = ( y / 2 ) -> ( ( ph /\ ps ) <-> th ) ) $.
    cau3.4 $e |- ( x = y -> ( ch <-> ta ) ) $.
    cau3.5 $e |- ( ( ( et /\ y e. RR ) /\ ( j e. Z /\ k e. Z /\ m e. Z ) ) ->
                   ( th -> ta ) ) $.
    cau3.6 $e |- Z C_ ZZ $.
    $( A relationship used to derive two ways to express a Cauchy sequence.
       ` ph ` is ` ph ( j , k , x ) ` . $)
    cau3i $p |- ( et ->
         ( A. x e. RR ( 0 < x -> E. j e. Z A. k e. Z
        ( j <_ k -> ph ) ) <-> A. x e. RR ( 0 < x ->
       E. m e. Z A. j e. Z A. k e. Z ( ( m <_ j /\ m <_ k ) -> ph ) ) ) ) $=
      ( cc0 cv clt wbr cle wi wral wrex cr wa cau3iri cau3ii imim2i ralimi
      impbid1 ) FSGTUAUBZITZJTZUCUBAUDJLUEILUFZUDZGUGUEUNKTZUOUCUBUSUPUCUBUHAUD
      JLUEILUEKLUFZUDZGUGUEABCDEFGHUCIJKLLMNOPQUIVAURGUGUTUQUNAIJKLLRUJUKULUM
      $.
      $( [17-Dec-2006] $)
  $}

  ${
    $d j k m n M $.  $d m Z $.  $d m n ph $.
    cau5i.1 $e |- M e. ZZ $.
    cau5i.1a $e |- Z = ( ZZ>= ` M ) $.
    cau5i.2 $e |- W C_ ZZ $.
    cau5i.3 $e |- U C_ ZZ $.
    $( A relationship used to derive two ways to express a Cauchy sequence.
       ` ph ` is ` ph ( j , k ) ` . $)
    cau5ii $p |- ( E. m e. ZZ A. j e. ZZ A. k e. ZZ ( ( m <_ j /\ m <_ k ) ->
            ph ) -> E. m e. Z A. j e. W A. k e. U
                ( ( m <_ j /\ m <_ k ) -> ph ) ) $=
      ( vn cv cle wbr wa wi cz wral wrex cuz cfv wcel zre cr zrei a1i letr
      mp3an2 sylan2 im2anan9 anandis sylan anandi syl5ib expdimp an32s anassrs
      imim1d ralimdva uzid ax-mp wceq breq1 anbi12d imbi1d 2ralbidv rcla4ev
      mpan syl6 eluz1i ex sylbir lecasei r19.23aiv sseli imim1i ralimi2
      imim12i reximi syl cbvrexv wb rexeq 3imtr4i ) MNZCNZOPZWGDNZOPZQZARZDSTZC
      STZMSUAZENZWHOPZWQWJOPZQZARZDBTZCGTZEFUBUCZUAZXADSTZCSTZESUAXCEHUAZWPXGEX
      DUAZXEWOXIMSWGSUDZWOXIRZWGFWGUEZFUFUDZXJFIUGZUHXJWGFOPZQZWOFWHOPZFWJOPZQZ
      ARZDSTZCSTZXIXPWNYACSXPWHSUDZQZWMXTDSYDWJSUDZQXSWLAXPYCYEXSWLRZXJYCYEQZXO
      YFXJYGQZXOXSWLYHXOXQQZXOXRQZQZWLXOXSQXJWGUFUDZYGYKWLRZXLYLYCYEYMYLYCQYIWI
      YLYEQYJWKYCYLWHUFUDZYIWIRZWHUEYLXMYNYOXNWGFWHUIUJUKYEYLWJUFUDZYJWKRZWJUEY
      LXMYPYQXNWGFWJUIUJUKULUMUNXOXQXRUOUPUQURUSUTVAVAFXDUDZYBXIFSUDYRIFVBVCXGY
      BEFXDWQFVDZXAXTCDSSYSWTXSAYSWRXQWSXRWQFWHOVEWQFWJOVEVFVGVHVIVJVKXJFWGOPQW
      GXDUDZXKFWGIVLYTWOXIXGWOEWGXDWQWGVDZXAWMCDSSUUAWTWLAUUAWRWIWSWKWQWGWHOVEW
      QWGWJOVEVFVGVHZVIVMVNVOVPXGXCEXDXFXBCSGWHGUDYCXFXBGSWHKVQXAXADSBWJBUDYEXA
      BSWJLVQVRVSVTVSWAWBXGWOEMSUUBWCHXDVDXHXEWDJXCEHXDWEVCWF $.
      $( [17-Dec-2006] $)
  $}

  ${
    $d j k m M $.  $d j k m Z $.  $d m ph $.
    cau4i.1 $e |- M e. ZZ $.
    cau4i.2 $e |- Z = ( ZZ>= ` M ) $.
    $( A relationship used to derive two ways to express a Cauchy sequence.
       ` ph ` is ` ph ( j , k ) ` . $)
    cau4ii $p |- ( E. j e. Z A. k e. Z ( j <_ k -> ph ) ->
                  E. j e. ZZ A. k e. ZZ ( j <_ k -> ph ) ) $=
      ( cv cle wbr wi wral wrex wa cz cuz cfv wceq wb rexeq ax-mp wcel rexuz
      bitri cr zre zrei letr mp3an1 syl2an exp4b com23 imp31 ancrd imim1d
      impexp syl5ibr ralimdva raleqi raluz syl5ib expimpd reximia sylbi ) BHZCH
      ZIJZAKZCELZBEMZDVEIJZVINZBOMZVHCOLZBOMVJVIBDPQZMZVMEVORVJVPSGVIBEVOTUADOU
      BZVPVMSFVIBDUCUAUDVLVNBOVEOUBZVKVIVNVRVKNZDVFIJZVHKZCOLZVNVIVSWAVHCOVSVFO
      UBZNZVTVGNZAKVHWAWDVGWEAWDVGVTVRVKWCVGVTKZVRWCVKWFVRWCVKVGVTVRVEUEUBZVFUE
      UBZVKVGNVTKZWCVEUFVFUFDUEUBWGWHWIDFUGDVEVFUHUIUJUKULUMUNUOVTVGAUPUQURVIVH
      CVOLZWBVHCEVOGUSVQWJWBSFVHCDUTUAUDVAVBVCVD $.
      $( [18-Dec-2006] $)

    $( A relationship used to derive two ways to express a Cauchy sequence.
       ` ph ` is ` ph ( j , k ) ` . $)
    cau5i $p |- ( E. m e. ZZ A. j e. ZZ A. k e. ZZ ( ( m <_ j /\ m <_ k ) ->
     ph ) <-> E. m e. Z A. j e. Z A. k e. Z ( ( m <_ j /\ m <_ k ) -> ph ) ) $=
      ( cv cle wbr wa wi cz wral wrex cuz cfv uzssz eqsstri cau5ii wceq wb
      rexeq ax-mp wcel rexuz bitri cr zre anim12i zrei letr mp3an1 im2anan9
      ancom syl6ib anandis anandi syl5ib syl2an exp4b com23 imp31 anassrs
      ancrd imim1d impexp syl5ibr ralimdva raleqi raluz r19.21v 3bitr3i ralbii
      3bitri expimpd reximia sylbi impbii ) DIZBIZJKZWACIZJKZLZAMZCNOZBNOZDNPZW
      GCFOZBFOZDFPZAFBCDEFFGHFEQRZNHESTZWOUAWMEWAJKZWLLZDNPZWJWMWLDWNPZWRFWNUBW
      MWSUCHWLDFWNUDUEENUFZWSWRUCGWLDEUGUEUHWQWIDNWANUFZWPWLWIXAWPLZEWDJKZEWBJK
      ZWGMZMZCNOZBNOZWIWLXBXGWHBNXBWBNUFZLZXFWGCNXJWDNUFZLZXCXDLZWFLZAMZWGXFXLW
      FXNAXLWFXMXBXIXKWFXMMZXAWPXIXKLZXPXAXQWPXPXAXQWPWFXMXAWAUIUFZWBUIUFZWDUIU
      FZLZWPWFLZXMMXQWAUJXIXSXKXTWBUJWDUJUKXRYALWPWCLZWPWELZLZXMYBXRXSXTYEXMMXR
      XSLZXRXTLZLYEXDXCLXMYFYCXDYGYDXCEUIUFZXRXSYCXDMEGULZEWAWBUMUNYHXRXTYDXCMY
      IEWAWDUMUNUOXDXCUPUQURWPWCWEUSUTVAVBVCVDVEVFVGXOXMWGMXFXMWFAVHXCXDWGVHUHV
      IVJVJWLWKBWNOZXDWKMZBNOZXHWKBFWNHVKWTYJYLUCGWKBEVLUEYKXGBNXECFOXECWNOZYKX
      GXECFWNHVKXDWGCFVMWTYMXGUCGXECEVLUEVNVOVPUTVQVRVSVT $.
      $( [18-Dec-2006] $)
  $}

  ${
    $d j k $.  $d k Z $.
    cvg1i.1 $e |- Z = ( ZZ>= ` M ) $.
    $( A relationship used to derive two ways to express that a sequence
       converges.  Unlike ~ cvg1 , ` j ` may be free in ` ph ` , so this can
       also be used for Cauchy sequences. $)
    cvg1i $p |- ( E. j e. Z A. k e. Z ( j <_ k -> ph ) ->
                  E. j e. Z A. k e. Z ( j < k -> ph ) ) $=
      ( cv cle wbr wi wral clt wcel wa cr cuz cfv eleq2i cz eluzelz zre syl
      sylbi ltle syl2an imim1d ralimdva reximia ) BGZCGZHIZAJZCEKUIUJLIZAJZCEKB
      EUIEMZULUNCEUOUJEMZNUMUKAUOUIOMZUJOMZUMUKJUPUOUIDPQZMZUQEUSUIFRUTUISMUQDU
      ITUIUAUBUCUPUJUSMZUREUSUJFRVAUJSMURDUJTUJUAUBUCUIUJUDUEUFUGUH $.
      $( [20-Dec-2006] $)
  $}

  ${
    $d k M $.  $d j k m Z $.  $d j m ph $.
    cvg1.1 $e |- Z = ( ZZ>= ` M ) $.
    $( A relationship used to derive two ways to express that a sequence
       converges. ` ph ` is ` ph ( k ) ` . $)
    cvg1 $p |- ( E. j e. Z A. k e. Z ( j < k -> ph ) <->
               E. j e. Z A. k e. Z ( j <_ k -> ph ) ) $=
      ( vm cv clt wbr wi wral wrex cle wcel c1 caddc co wa cuz cfv eleq2i cz
      wb eluzelz sylbi zltp1le syl2an imbi1d ralbidva biimpd peano2uz
      syl6eleqr jctild wceq breq1 ralbidv rcla4ev syl6 r19.23aiv cbvrexv sylib
      cvg1i impbii ) BHZCHZIJZAKZCELZBEMZVEVFNJZAKZCELZBEMZVJGHZVFNJZAKZCELZGEM
      ZVNVIVSBEVEEOZVIVEPQRZEOZWAVFNJZAKZCELZSZVSVTVEDTUAZOZVIWFKEWGVEFUBWHVIWE
      WBWHVIWEWHVHWDCEWHVFEOZSVGWCAWHVEUCOVFUCOZVGWCUDWIDVEUEWIVFWGOWJEWGVFFUBD
      VFUEUFVEVFUGUHUIUJUKWHWAWGEDVEULFUMUNUFVRWEGWAEVOWAUOZVQWDCEWKVPWCAVOWAVF
      NUPUIUQURUSUTVRVMGBEVOVEUOZVQVLCEWLVPVKAVOVEVFNUPUIUQVAVBABCDEFVCVD $.
      $( [20-Dec-2006] $)
  $}

  ${
    $d x y z w $.  $d x w A $.  $d x z w F $.  $d x w G $.  $d x w ph $.
    cvg2.1 $e |- ( ( y e. F /\ z e. G ) -> A e. RR ) $.
    $( Two ways to express that a sequence converges or is Cauchy. $)
    cvg2i $p |- ( A. x e. RR ( 0 < x ->
                 E. y e. F A. z e. G ( ph -> A < x ) ) <->
         A. x e. RR ( 0 < x -> E. y e. F A. z e. G ( ph -> A <_ x ) ) ) $=
      ( vw cc0 cv clt wbr wi wral wrex cr cle wcel wa ltle ancoms sylan2
      anassrs imim2d ralimdva reximdva ralimia c2 cdiv co wceq breq2 imbi2d
      rexralbidv imbi12d rcla4cv wb rehalfcl adantr halfpos2 biimpa biimt
      syl11anc impexp syl6bb halfpos simpr ad2antrr simpll lelttr syl111anc
      mpan2d sylbird com12 exp3a syl r19.21aiv cbvralv sylib impbii ) JBKZLMZAE
      WBLMZNZDGOZCFPZNZBQOZWCAEWBRMZNZDGOZCFPZNZBQOZWHWNBQWBQSZWGWMWCWPWFWLCFWP
      CKFSZTZWEWKDGWRDKGSZTWDWJAWPWQWSWDWJNZWQWSTZWPEQSZWTHXBWPWTEWBUAUBUCUDUEU
      FUGUEUHWOJIKZLMZAEXCLMZNZDGOZCFPZNZIQOWIWOXIIQWOXCUIUJUKZQSZJXJLMZAEXJRMZ
      NZDGOZCFPZNZNZXCQSZXINWNXQBXJQWBXJULZWCXLWMXPWBXJJLUMXTWKXNCDFGXTWJXMAWBX
      JERUMUNUOUPUQXRXSXDXHXSXDTZXRXHYAXRXPXHYAXPXKXLTZXPNZXRYAXKXLXPYCURXSXKXD
      XCUSZUTXSXDXLXCVAVBYBXPVCVDXKXLXPVEVFYAXOXGCFYAWQTZXNXFDGYEWSTXMXEAYAWQWS
      XMXENZXAYAXBYFHYAXBTZXMXJXCLMZXEYAYHXBXSXDYHXCVGVBUTYGXBXKXSXMYHTXENYAXBV
      HXSXKXDXBYDVIXSXDXBVJEXJXCVKVLVMUCUDUEUFUGVNVOVPVQVRXIWHIBQXCWBULZXDWCXHW
      GXCWBJLUMYIXFWECDFGYIXEWDAXCWBELUMUNUOUPVSVTWA $.
      $( [10-Apr-2005] $)
  $}

  ${
    $d j k M $.  $d k m N $.  $d k W $.  $d j k m Z $.  $d j m ph $.
    cvg3.1 $e |- M e. ZZ $.
    cvg3.2 $e |- ( ZZ>= ` M ) C_ Z $.
    cvg3.3 $e |- Z C_ ZZ $.
    cvg3.4 $e |- N e. ZZ $.
    cvg3.5 $e |- ( ZZ>= ` N ) C_ W $.
    cvg3.6 $e |- W C_ ZZ $.
    $( A relationship used to derive two ways to express convergence. ` ph ` is
       ` ph ( k ) ` . $)
    cvg3i $p |- ( E. j e. ZZ A. k e. ZZ ( j <_ k -> ph ) <->
               E. j e. Z A. k e. W ( j <_ k -> ph ) ) $=
      ( vm cv cle wbr wi cz wral wrex wceq breq1 imbi1d ralbidv cbvrexv wcel
      cr zrei a1i zre wa cuz cfv eluz1i sseli sylbir rcla4ev sylan ex letr
      mp3an2 syl2an expdimp an32s imim1d ralimdva uzid ax-mp sselii mpan syl6
      lecasei r19.23aiv wss ssralv reximi syl wo letric sylancr mp3an1 ancrd
      adantr jctild a1i12 ancld jcad jaodan wb raluz impexp ralbii bitr4i
      sylib syl5 mpdan impbii bitri ) BOZCOZPQZARZCSTZBSUANOZXAPQZARZCSTZNSUAZX
      CCFTZBGUAZXDXHBNSWTXEUBZXCXGCSXLXBXFAWTXEXAPUCUDUEZUFXIXKXIXDBGUAZXKXHXNN
      SXESUGZXHXNRDXEDUHUGZXODHUIZUJXEUKZXODXEPQULZXHXNXSXEGUGZXHXNXSXEDUMUNZUG
      XTDXEHUOYAGXEIUPUQXDXHBXEGXMURUSUTXOXEDPQZULZXHDXAPQZARZCSTZXNYCXGYECSYCX
      ASUGZULYDXFAXOYGYBYDXFRXOYGULYBYDXFXOXEUHUGZXAUHUGZYBYDULXFRZYGXRXAUKZYHX
      PYIYJXQXEDXAVAVBVCVDVEVFVGDGUGYFXNYAGDIDSUGDYAUGHDVHVIVJXDYFBDGWTDUBZXCYE
      CSYLXBYDAWTDXAPUCUDUEURVKVLVMVNXDXJBGFSVOXDXJRMXCCFSVPVIVQVRXJXIBGWTGUGZE
      WTPQZWTEPQZVSZXJXIRYMEUHUGZWTUHUGZYPEKUIZYMWTSUGZYRGSWTJUPZWTUKVRZEWTVTWA
      YMYPULEXAPQZXBULZARZCSTZXIXJYMYNUUFXIRYOYMYNULZUUFYTXDULXIUUGUUFXDYTUUGUU
      EXCCSUUGYGULZXBUUDAUUHXBUUCYMYGYNXBUUCRYMYGULZYNXBUUCYMYRYIYNXBULUUCRZYGU
      UBYKYQYRYIUUJYSEWTXAVAWBVCVDVEWCVFVGYMYTYNUUAWDWEXHXDNWTSXEWTUBZXGXCCSUUK
      XFXBAXEWTXAPUCUDUEURVLYMYOULZUUFESUGZUUCARZCSTZULXIUULUUFUUMUUOUULUUFUUMK
      WFUULUUEUUNCSUULYGULZUUCUUDAUUPUUCXBYMYGYOUUCXBRUUIYOUUCXBYMYRYIYOUUCULXB
      RZYGUUBYKYRYQYIUUQYSWTEXAVAVBVCVDVEWGVFVGWHXHUUONESXEEUBZXGUUNCSUURXFUUCA
      XEEXAPUCUDUEURVLWIXJXCCEUMUNZTZUUFUUSFVOXJUUTRLXCCUUSFVPVIUUTUUCXCRZCSTZU
      UFUUMUUTUVBWJKXCCEWKVIUUEUVACSUUCXBAWLWMWNWOWPWQVNWRWS $.
      $( [27-Dec-2006] $)
  $}

  ${
    $d j k m n $.  $d j m n ph $.  $d j m n ps $.
    $( Equivalence that lets us conjoin the properties of two independent
       converging sequences. ` k ` may be free in ` ph ` and ` ps ` .  Compare
       ~ r19.40 , where the implication holds in only one direction. $)
    cvganz $p |- ( ( E. j e. ZZ A. k e. ZZ ( j <_ k -> ph ) /\
                E. j e. ZZ A. k e. ZZ ( j <_ k -> ps ) ) <->
                E. j e. ZZ A. k e. ZZ ( j <_ k -> ( ph /\ ps ) ) ) $=
      ( vm vn cv cle wbr wi cn0 wral wrex wa cz wceq breq1 imbi1d ralbidv
      cbvrexv reeanv wcel caddc co cr nn0re nn0addge1 sylan adantr ad2antrr
      nn0addcl syl adantl letr syl111anc mpand nn0addge2 ancoms ad2antlr jcad
      prth syl9 ralimdva r19.26 syl5ibr jctild rcla4ev syl6 r19.23aivv sylbir
      syl2anb simpl imim2i ralimi reximi simpr jca impbii cc0 0z cuz cfv nn0uz
      eqimss2i nn0ssz cvg3i anbi12i 3bitr4i ) CGZDGZHIZAJZDKLZCKMZWKBJZDKLZCKMZ
      NZWKABNZJZDKLZCKMZWLDOLCOMZWODOLCOMZNWTDOLCOMWRXBWNEGZWJHIZAJZDKLZEKMZFGZ
      WJHIZBJZDKLZFKMZXBWQWMXHCEKWIXEPZWLXGDKXOWKXFAWIXEWJHQRSTWPXMCFKWIXJPZWOX
      LDKXPWKXKBWIXJWJHQRSTXIXNNXHXMNZFKMEKMXBXHXMEFKKUAXQXBEFKKXEKUBZXJKUBZNZX
      QXEXJUCUDZKUBZYAWJHIZWSJZDKLZNXBXTXQYEYBXTXGXLNZDKLYEXQXTYFYDDKXTWJKUBZNZ
      YCXFXKNYFWSYHYCXFXKYHXEYAHIZYCXFXTYIYGXRXEUEUBZXSYIXEUFZXEXJUGUHUIYHYJYAU
      EUBZWJUEUBZYIYCNXFJXRYJXSYGYKUJXTYLYGXTYBYLXEXJUKZYAUFULUIZYGYMXTWJUFUMZX
      EYAWJUNUOUPYHXJYAHIZYCXKXTYQYGXSXRYQXSXJUEUBZXRYQXJUFZXJXEUQUHURUIYHYRYLY
      MYQYCNXKJXSYRXRYGYSUSYOYPXJYAWJUNUOUPUTXFAXKBVAVBVCXGXLDKVDVEYNVFXAYECYAK
      WIYAPZWTYDDKYTWKYCWSWIYAWJHQRSVGVHVIVJVKXBWNWQXAWMCKWTWLDKWSAWKABVLVMVNVO
      XAWPCKWTWODKWSBWKABVPVMVNVOVQVRXCWNXDWQACDVSVSKKVTKVSWAWBWCWDZWEVTUUAWEWF
      BCDVSVSKKVTUUAWEVTUUAWEWFWGWSCDVSVSKKVTUUAWEVTUUAWEWFWH $.
      $( [18-Dec-2006] $)
  $}

  ${
    $d j k M $.  $d j k Z $.  $d j ph $.  $d j ps $.
    cvganuz.1 $e |- M e. ZZ $.
    cvganuz.2 $e |- Z = ( ZZ>= ` M ) $.
    $( Lemma that lets us combine the properties of two independent converging
       sequences. ` k ` may be free in ` ph ` and ` ps ` . $)
    cvganuzi $p |- ( ( E. j e. Z A. k e. Z ( j <_ k -> ph ) /\
                E. j e. Z A. k e. Z ( j <_ k -> ps ) ) <->
                E. j e. Z A. k e. Z ( j <_ k -> ( ph /\ ps ) ) ) $=
      ( cv cle wbr wi cz wral wrex wa cvganz cuz cfv eqimss2i uzssz eqsstri
      cvg3i anbi12i 3bitr3i ) CIDIJKZALZDMNCMOZUFBLZDMNCMOZPUFABPZLZDMNCMOUGDFN
      CFOZUIDFNCFOZPULDFNCFOABCDQUHUMUJUNACDEEFFGFERSZHTZFUOMHEUAUBZGUPUQUCBCDE
      EFFGUPUQGUPUQUCUDUKCDEEFFGUPUQGUPUQUCUE $.
      $( [18-Dec-2006] $)
  $}

  ${
    $d x y z w u t F $.
    caubnd.1 $e |- F : NN --> CC $.
    caubnd.2 $e |- A. z e. RR ( 0 < z -> E. w e. NN A. y e. NN ( w < y ->
                      ( abs ` ( ( F ` y ) - ( F ` w ) ) ) < z ) ) $.
    $( A Cauchy sequence of complex numbers is bounded. $)
    caubndi $p |- E. x e. RR A. y e. NN ( abs ` ( F ` y ) ) < x $=
      ( vu vt cv clt wbr cfv cabs wi cn wral cr wrex cmin co c1 cc0 lt01 wcel
      1re wceq breq2 imbi2d rexralbidv imbi12d rcla4v mp2 ax-mp caddc wa wb cc
      ffvelrni subcl syl2an abscl syl adantl ltadd1 mp3an2 syl11anc cle npcan
      fveq2d abstri eqbrtrrd adantr readdcl sylancr lelttr syl111anc mpand
      sylbid ancoms imim2d ralimdva jctild ralbidv rcla4ev syl6 reximia
      seq1bndi cif wo nnre lelttric adantlr max2 simpll ifcl ltletr mpan2d
      max1ALT ad2antlr simplr anim12d jaob syl6ibr adantlll mpid adantll
      r19.26 syl5ibr exp4b com23 imp r19.23adv exp31 mpd r19.23aiv ) DJZBJZKLZX
      REMZNMZHJZKLZOZBPQZHRSZDPSZYAAJZKLZBPQZARSZXSXTXQEMZTUAZNMZUBKLZOZBPQZDPS
      ZYGUCUBKLZYRUDUBRUEZUCCJZKLZXSYNUUAKLZOZBPQDPSZOZCRQYSYROZUFGUUFUUGCUBRUU
      AUBUGZUUBYSUUEYRUUAUBUCKUHUUHUUDYPDBPPUUHUUCYOXSUUAUBYNKUHUIUJUKULUMUNYQY
      FDPXQPUEZYQUBYLNMZUOUAZRUEZXSYAUUKKLZOZBPQZUPYFUUIYQUUOUULUUIYPUUNBPUUIXR
      PUEZUPYOUUMXSUUPUUIYOUUMOUUPUUIUPZYOYNUUJUOUAZUUKKLZUUMUUQYNRUEZUUJRUEZYO
      UUSUQZUUQYMURUEZUUTUUPXTURUEZYLURUEZUVCUUIPURXREFUSZPURXQEFUSZXTYLUTVAZYM
      VBVCZUUIUVAUUPUUIUVEUVAUVGYLVBVCZVDZUUTYTUVAUVBUFYNUBUUJVEVFVGUUQYAUURVHL
      ZUUSUUMUUQYMYLUOUAZNMZYAUURVHUUQUVMXTNUUPUVDUVEUVMXTUGUUIUVFUVGXTYLVIVAVJ
      UUQUVCUVEUVNUURVHLUVHUUIUVEUUPUVGVDYMYLVKVGVLUUQYARUEZUURRUEZUULUVLUUSUPU
      UMOUUPUVOUUIUUPUVDUVOUVFXTVBVCZVMUUQUUTUVAUVPUVIUVKYNUUJVNVGUUIUULUUPUUIY
      TUVAUULUFUVJUBUUJVNVOZVDYAUURUUKVPVQVRVSVTWAWBUVRWCYEUUOHUUKRYBUUKUGZYDUU
      NBPUVSYCUUMXSYBUUKYAKUHUIWDWEWFWGUNYFYKDPUUIXRXQVHLZYAIJZKLZOZBPQZIRSYFYK
      OZIBXQEFWHUUIUWDUWEIRUUIUWARUEZUWDUWEUUIUWFUPZUWDUPYEYKHRUWGUWDYBRUEZYEYK
      OZOUWGUWHUWDUWIUWGUWHUWDYEYKUWGUWHUPZUWCYDUPZBPQZYKUWDYEUPUWJUWLYBUWAVHLZ
      UWAYBWIZRUEZYAUWNKLZBPQZUPYKUWJUWLUWQUWOUWJUWKUWPBPUWJUUPUPUWKUVTXSWJZUWP
      UWGUUPUWRUWHUUIUUPUWRUWFUUPUUIUWRUUPXRRUEXQRUEUWRUUIXRWKXQWKXRXQWLVAVTWMW
      MUWFUWHUUPUWKUWRUWPOZOUUIUWFUWHUPZUUPUPZUWKUVTUWPOZXSUWPOZUPUWSUXAUWCUXBY
      DUXCUXAUWBUWPUVTUXAUWBUWAUWNVHLZUWPUWTUXDUUPUWHUWFUXDYBUWAWNVTVMUXAUVOUWF
      UWOUWBUXDUPUWPOUUPUVOUWTUVQVDZUWFUWHUUPWOUWTUWOUUPUWMUWAYBRWPZVMZYAUWAUWN
      WQVQWRWAUXAYCUWPXSUXAYCYBUWNVHLZUWPUWHUXHUWFUUPYBUWAWSWTUXAUVOUWHUWOYCUXH
      UPUWPOUXEUWFUWHUUPXAUXGYAYBUWNWQVQWRWAXBUVTUWPXSXCXDXEXFWBUWFUWHUWOUUIUXF
      XGWCYJUWQAUWNRYHUWNUGYIUWPBPYHUWNYAKUHWDWEWFUWCYDBPXHXIXJXKXLXMXNXMXOXPUN
      $.
      $( [4-Apr-2005] $)
  $}

  ${
    $d y z w $.  $d x y w $.  $d x F $.  $d x G $.
    caure.1 $e |- F : NN --> CC $.
    caure.2 $e |- A. z e. RR ( 0 < z -> E. w e. NN A. y e. NN ( w < y ->
                      ( abs ` ( ( F ` y ) - ( F ` w ) ) ) < z ) ) $.
    caure.3 $e |- G Fn NN $.
    caure.4 $e |- ( x e. NN -> ( G ` x ) = ( Re ` ( F ` x ) ) ) $.
    $( The real part of a complex Caucy sequence is a Cauchy sequence. $)
    caurei $p |- A. z e. RR ( 0 < z -> E. w e. NN A. y e. NN ( w < y ->
                      ( abs ` ( ( G ` y ) - ( G ` w ) ) ) < z ) ) $=
      ( cc0 cv clt wbr cfv cmin co cabs wi cn wral wrex cr wcel wa w3a cle cre
      wceq fveq2 fveq2d eqeq12d vtoclga opreqan12d cc ffvelrni resub syl2an
      eqtr4d subcl absrele syl eqbrtrd 3adant3 wf wfn ffnfv recl eqeltrd rgen
      mpbir2an resubcl recnd abscl simp3 lelttr syl111anc mpand 3com13 3expa
      imim2d ralimdva reximdva ralimia ax-mp ) KCLZMNZDLZBLZMNZWIEOZWHEOZPQZROZ
      WFMNZSZBTUAZDTUBZSZCUCUAWGWJWIFOZWHFOZPQZROZWFMNZSZBTUAZDTUBZSZCUCUAHWSXH
      CUCWFUCUDZWRXGWGXIWQXFDTXIWHTUDZUEZWPXEBTXKWITUDZUEWOXDWJXIXJXLWOXDSZXLXJ
      XIXMXLXJXIUFZXCWNUGNZWOXDXLXJXOXIXLXJUEZXCWMUHOZROZWNUGXPXBXQRXPXBWKUHOZW
      LUHOZPQZXQXLXJWTXSXAXTPALZFOZYBEOZUHOZUIZWTXSUIAWITYBWIUIZYCWTYEXSYBWIFUJ
      YGYDWKUHYBWIEUJUKULJUMYFXAXTUIAWHTYBWHUIZYCXAYEXTYBWHFUJYHYDWLUHYBWHEUJUK
      ULJUMUNXLWKUOUDZWLUOUDZXQYAUIXJTUOWIEGUPZTUOWHEGUPZWKWLUQURUSUKXPWMUOUDZX
      RWNUGNXLYIYJYMXJYKYLWKWLUTURZWMVAVBVCVDXNXCUCUDZWNUCUDZXIXOWOUEXDSXLXJYOX
      IXPXBUOUDYOXPXBXLWTUCUDXAUCUDXBUCUDXJTUCWIFTUCFVEFTVFYCUCUDZATUAATUCFVGIY
      QATYBTUDZYCYEUCJYRYDUOUDYEUCUDTUOYBEGUPYDVHVBVIVJVKZUPTUCWHFYSUPWTXAVLURV
      MXBVNVBVDXLXJYPXIXPYMYPYNWMVNVBVDXLXJXIVOXCWNWFVPVQVRVSVTWAWBWCWAWDWE $.
      $( [16-Apr-2005] $)
  $}

  ${
    $d y z w $.  $d x y w $.  $d x F $.  $d x G $.
    cauim.1 $e |- F : NN --> CC $.
    cauim.2 $e |- A. z e. RR ( 0 < z -> E. w e. NN A. y e. NN ( w < y ->
                      ( abs ` ( ( F ` y ) - ( F ` w ) ) ) < z ) ) $.
    cauim.3 $e |- G Fn NN $.
    cauim.4 $e |- ( x e. NN -> ( G ` x ) = ( Im ` ( F ` x ) ) ) $.
    $( The imaginary part of a complex Caucy sequence is a Cauchy sequence. $)
    cauimi $p |- A. z e. RR ( 0 < z -> E. w e. NN A. y e. NN ( w < y ->
                      ( abs ` ( ( G ` y ) - ( G ` w ) ) ) < z ) ) $=
      ( cc0 cv clt wbr cfv cmin co cabs wi cn wral wrex cr wcel wa w3a cle cim
      wceq fveq2 fveq2d eqeq12d vtoclga opreqan12d cc ffvelrni imsub syl2an
      eqtr4d subcl absimle syl eqbrtrd 3adant3 wf wfn ffnfv imcl eqeltrd rgen
      mpbir2an resubcl recnd abscl simp3 lelttr syl111anc mpand 3com13 3expa
      imim2d ralimdva reximdva ralimia ax-mp ) KCLZMNZDLZBLZMNZWIEOZWHEOZPQZROZ
      WFMNZSZBTUAZDTUBZSZCUCUAWGWJWIFOZWHFOZPQZROZWFMNZSZBTUAZDTUBZSZCUCUAHWSXH
      CUCWFUCUDZWRXGWGXIWQXFDTXIWHTUDZUEZWPXEBTXKWITUDZUEWOXDWJXIXJXLWOXDSZXLXJ
      XIXMXLXJXIUFZXCWNUGNZWOXDXLXJXOXIXLXJUEZXCWMUHOZROZWNUGXPXBXQRXPXBWKUHOZW
      LUHOZPQZXQXLXJWTXSXAXTPALZFOZYBEOZUHOZUIZWTXSUIAWITYBWIUIZYCWTYEXSYBWIFUJ
      YGYDWKUHYBWIEUJUKULJUMYFXAXTUIAWHTYBWHUIZYCXAYEXTYBWHFUJYHYDWLUHYBWHEUJUK
      ULJUMUNXLWKUOUDZWLUOUDZXQYAUIXJTUOWIEGUPZTUOWHEGUPZWKWLUQURUSUKXPWMUOUDZX
      RWNUGNXLYIYJYMXJYKYLWKWLUTURZWMVAVBVCVDXNXCUCUDZWNUCUDZXIXOWOUEXDSXLXJYOX
      IXPXBUOUDYOXPXBXLWTUCUDXAUCUDXBUCUDXJTUCWIFTUCFVEFTVFYCUCUDZATUAATUCFVGIY
      QATYBTUDZYCYEUCJYRYDUOUDYEUCUDTUOYBEGUPYDVHVBVIVJVKZUPTUCWHFYSUPWTXAVLURV
      MXBVNVBVDXLXJYPXIXPYMYPYNWMVNVBVDXLXJXIVOXCWNWFVPVQVRVSVTWAWBWCWAWDWE $.
      $( [16-Apr-2005] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d x y z F $.  $d x y z G $.
    ser1absdif.1 $e |- F : NN --> CC $.
    ser1absdif.2 $e |- G Fn NN $.
    ser1absdif.3 $e |- ( x e. NN -> ( G ` x ) = ( abs ` ( F ` x ) ) ) $.
    $( Lemma for ~ ser1absdifi . _Warning:  The HTML proof page is 1/2 megabyte
       in size_. $)
    ser1absdiflem $p |- ( ( A e. NN /\ B e. NN ) ->
       ( abs ` ( ( ( + seq1 F ) ` ( A + B ) ) - ( ( + seq1 F ) ` A ) ) ) <_
       ( ( ( + seq1 G ) ` ( A + B ) ) - ( ( + seq1 G ) ` A ) ) ) $=
      ( vy vz cn wcel caddc co cseq1 cfv cmin cabs cle wbr cv wi c1 wceq
      opreq2 fveq2d opreq1d breq12d imbi2d cr cc peano2nn ffvelrni syl abscl
      leid addex wf cvv nnex fex mp2an seq1p1 ser1cl1i pncan2 syl11anc eqtrd
      wfn fnex wss wral ffnfv eqeltrd rgen mpbir2an axresscn fss fveq2 eqeq12d
      vtoclga recnd 3eqtrd 3brtr4d wa nnaddcl adantr subcl abstri addcl
      readdcl ser1recli resubcl letr syl111anc mpand wb leadd1 nncn ax1cn
      addass mp3an3 syl2an eqtr3d addsub opreq2d 3imtr4d expcom a2d nnind
      impcom ) CKLBKLZBCMNZMDONZPZBXMPZQNZRPZXLMEONZPZBXRPZQNZSTZXKBIUAZMNZXMPZ
      XOQNZRPZYDXRPZXTQNZSTZUBXKBUCMNZXMPZXOQNZRPZYKXRPZXTQNZSTZUBXKBJUAZMNZXMP
      ZXOQNZRPZYSXRPZXTQNZSTZUBXKBYRUCMNZMNZXMPZXOQNZRPZUUGXRPZXTQNZSTZUBXKYBUB
      IJCYCUCUDZYJYQXKUUNYGYNYIYPSUUNYFYMRUUNYEYLXOQUUNYDYKXMYCUCBMUEZUFUGUFUUN
      YHYOXTQUUNYDYKXRUUOUFUGUHUIYCYRUDZYJUUEXKUUPYGUUBYIUUDSUUPYFUUARUUPYEYTXO
      QUUPYDYSXMYCYRBMUEZUFUGUFUUPYHUUCXTQUUPYDYSXRUUQUFUGUHUIYCUUFUDZYJUUMXKUU
      RYGUUJYIUULSUURYFUUIRUURYEUUHXOQUURYDUUGXMYCUUFBMUEZUFUGUFUURYHUUKXTQUURY
      DUUGXRUUSUFUGUHUIYCCUDZYJYBXKUUTYGXQYIYASUUTYFXPRUUTYEXNXOQUUTYDXLXMYCCBM
      UEZUFUGUFUUTYHXSXTQUUTYDXLXRUVAUFUGUHUIXKYKDPZRPZUVCYNYPSXKUVCUJLZUVCUVCS
      TXKUVBUKLZUVDXKYKKLZUVEBULZKUKYKDFUMZUNZUVBUOZUNUVCUPUNXKYMUVBRXKYMXOUVBM
      NZXOQNZUVBXKYLUVKXOQBMDUQKUKDURKUSLZDUSLFUTKUKUSDVAVBZVCUGXKXOUKLZUVEUVLU
      VBUDBDFVDZUVIXOUVBVEVFVGUFXKYPXTYKEPZMNZXTQNZUVQUVCXKYOUVRXTQBMEUQEKVHZUV
      MEUSLGUTKUSEVIVBZVCUGXKXTUKLZUVQUKLZUVSUVQUDBEKUJEURZUJUKVJKUKEURUWDUVTAU
      AZEPZUJLZAKVKAKUJEVLGUWGAKUWEKLZUWFUWEDPZRPZUJHUWHUWIUKLUWJUJLKUKUWEDFUMU
      WIUOUNVMVNVOZVPKUJUKEVQVBZVDZXKUVFUWCUVGUVFUVQUVFUVQUVCUJUWFUWJUDZUVQUVCU
      DZAYKKUWEYKUDZUWFUVQUWJUVCUWEYKEVRUWPUWIUVBRUWEYKDVRUFVSHVTZUVFUVEUVDUVHU
      VJUNVMWAUNXTUVQVEVFXKUVFUWOUVGUWQUNWBWCYRKLZXKUUEUUMXKUWRUUEUUMUBXKUWRWDZ
      UUBYSUCMNZDPZRPZMNZUUDUXBMNZSTZUUAUXAMNZRPZUXDSTZUUEUUMUWSUXGUXCSTZUXEUXH
      UWSUUAUKLZUXAUKLZUXIUWSYTUKLZUVOUXJUWSYSKLZUXLBYRWEZYSDFVDUNZXKUVOUWRUVPW
      FZYTXOWGVFZUWSUWTKLZUXKUWSUXMUXRUXNYSULUNZKUKUWTDFUMZUNZUUAUXAWHVFUWSUXGU
      JLZUXCUJLZUXDUJLZUXIUXEWDUXHUBUWSUXFUKLZUYBUWSUXJUXKUYEUXQUYAUUAUXAWIVFUX
      FUOUNUWSUUBUJLZUXBUJLZUYCUWSUXJUYFUXQUUAUOUNZUWSUXKUYGUYAUXAUOZUNZUUBUXBW
      JVFUWSUUDUJLZUYGUYDUWSUUCUJLZXTUJLZUYKUWSUXMUYLUXNYSEUWKWKUNXKUYMUWRBEUWK
      WKWFUUCXTWLVFZUYJUUDUXBWJVFUXGUXCUXDWMWNWOUWSUYFUYKUYGUUEUXEWPUYHUYNUYJUU
      BUUDUXBWQWNUWSUUJUXGUULUXDSUWSUUIUXFRUWSUUIYTUXAMNZXOQNZUXFUWSUUHUYOXOQUW
      SUWTXMPZUUHUYOUWSUWTUUGXMXKBUKLZYRUKLZUWTUUGUDZUWRBWRYRWRUYRUYSUCUKLUYTWS
      BYRUCWTXAXBZUFUWSUXMUYQUYOUDUXNYSMDUQUVNVCUNXCUGUWSUXLUXKUVOUYPUXFUDUXOUY
      AUXPYTUXAXOXDWNVGUFUWSUULUUCUWTEPZMNZXTQNZUUDVUBMNZUXDUWSUUKVUCXTQUWSUWTX
      RPZUUKVUCUWSUWTUUGXRVUAUFUWSUXMVUFVUCUDUXNYSMEUQUWAVCUNXCUGUWSUUCUKLZVUBU
      KLZUWBVUDVUEUDUWSUXMVUGUXNYSEUWLVDUNUWSUXRVUHUXSUXRVUBUXRVUBUXBUJUWNVUBUX
      BUDZAUWTKUWEUWTUDZUWFVUBUWJUXBUWEUWTEVRVUJUWIUXARUWEUWTDVRUFVSHVTZUXRUXKU
      YGUXTUYIUNVMWAUNXKUWBUWRUWMWFUUCVUBXTXDWNUWSVUBUXBUUDMUWSUXRVUIUXSVUKUNXE
      WBUHXFXGXHXIXJ $.
      $( [17-Apr-2005] $)

    $( Generalized triangle inequality: absolute value of a partial sum is less
       than or equal to sum of absolute values. $)
    ser1absdifi $p |- ( ( A e. NN /\ B e. NN /\ A < B ) ->
       ( abs ` ( ( ( + seq1 F ) ` B ) - ( ( + seq1 F ) ` A ) ) ) <_
       ( ( ( + seq1 G ) ` B ) - ( ( + seq1 G ) ` A ) ) ) $=
      ( cn wcel clt wbr w3a cmin co caddc cseq1 cfv cabs cle simp1 nnsub
      biimp3a ser1absdiflem syl11anc wceq wa cc nncn pncan3 syl2an fveq2d
      opreq1d 3adant3 3brtr3d ) BIJZCIJZBCKLZMZBCBNOZPOZPDQOZRZBVBRZNOZSRZVAPEQ
      OZRZBVGRZNOZCVBRZVDNOZSRZCVGRZVINOZTUSUPUTIJZVFVJTLUPUQURUAUPUQURVPBCUBUC
      ABUTDEFGHUDUEUPUQVFVMUFURUPUQUGZVEVLSVQVCVKVDNVQVACVBUPBUHJCUHJVACUFUQBUI
      CUIBCUJUKZULUMULUNUPUQVJVOUFURVQVHVNVINVQVACVGVRULUMUNUO $.
      $( [17-Apr-2005] $)
  $}


