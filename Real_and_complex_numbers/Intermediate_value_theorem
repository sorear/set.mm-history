$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Intermediate value theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d y H $.  $d w y z P $.  $d y R $.  $d w y z U $.  $d y W $.
    ivthlem1.1 $e |- U e. RR $.
    ivthlem1.2 $e |- P e. RR $.
    $( Lemma for ~ isupivthi . $)
    ivthlem1 $p |- ( ( Q e. RR -> ( ( abs ` ( P - Q ) ) < ( P - U ) -> U < Q )
         ) /\ ( Q e. RR -> ( ( abs ` ( P - Q ) ) < ( U - P ) -> Q < U ) ) ) $=
      ( cr wcel cmin co cabs cfv clt wbr wi cc wceq abssub recni recn sylancr
      breq1d wa caddc wb resubcli absdiflt mp3an23 pm3.26bda nncan mp2an
      syl5eqbrr ex sylbid pm3.27bda pncan3i syl6breq pm3.2i ) BFGZABHIJKZACHIZL
      MZCBLMZNNURUSCAHIZLMZBCLMZNNURVABAHIJKZUTLMZVBURUSVFUTLAOGZBOGUSVFPURABQA
      ERZBSTZUAURVGVBURVGUBAUTHIZBCLURVGVKBLMZBAUTUCILMZURAFGZUTFGVGVLVMUBUDEAC
      EDUEBAUTUFUGUHVHCOGVKCPVICDRZACUIUJUKULUMURVDVFVCLMZVEURUSVFVCLVJUAURVPVE
      URVPUBBAVCUCIZCLURVPAVCHIBLMZBVQLMZURVNVCFGVPVRVSUBUDECADEUEBAVCUFUGUNACV
      IVOUOUPULUMUQ $.
      $( [26-Nov-2007] $) $( [8-Oct-2007] $)

    ${
      ivthlem2.3 $e |- A. y e. RR+ E. z e. RR+ A. w e. R ( W < z -> H < y ) $.
      $( Lemma for ~ isupivthi . $)
      ivthlem2 $p |- ( U < P ->
            E. z e. RR+ A. w e. R ( W < z -> H < ( P - U ) ) ) $=
        ( clt wbr cmin co crp wcel cv wi wral wrex cc0 posdifi cr resubcli wa
        elrp biimpri mpan sylbi wceq breq2 imbi2d rexralbidv vtoclri syl ) FDLM
        ZDFNOZPQZHBRLMZGURLMZSZCETBPUAZUQUBURLMZUSFDIJUCURUDQZVDUSDFJIUEUSVEVDU
        FURUGUHUIUJUTGARZLMZSZCETBPUAVCAURPVFURUKZVHVBBCPEVIVGVAUTVFURGLULUMUNK
        UOUP $.
        $( [26-Nov-2007] $) $( [8-Oct-2007] $)
    $}
  $}

  ${
    ivthlem3.1 $e |- ( ph -> E. z e. RR+ A. w e. A ( ps -> th ) ) $.
    ivthlem3.2 $e |- ( w e. A -> ( th -> ta ) ) $.
    $( Lemma for ~ isupivthi . $)
    ivthlem3 $p |- ( ph -> E. z ( z e. RR+ /\ A. w e. A ( ps -> ta ) ) ) $=
      ( wi wral crp wrex cv wcel wa wex rgen imim1 r19.20sii mpi r19.22si
      df-rex sylib syl ) ABCJZFGKZELMZENLOBDJZFGKZPEQZHUHUJELMUKUGUJELUGCDJZFGK
      UJULFGIRUFULUIFGBCDSTUAUBUJELUCUDUE $.
      $( [26-Nov-2007] $) $( [8-Oct-2007] $)
  $}

  ${
    $d A c d v w x y z $.  $d B c d v w x y z $.  $d C d v w x y z $.
    $d F c d v w x y z $.  $d S v w x y $.  $d T v w x $.  $d U c d w y z $.
    ivthlem4.1 $e |- A e. RR $.
    ivthlem4.2 $e |- B e. RR $.
    ivthlem4.3 $e |- U e. RR $.
    ivthlem4.4 $e |- A < B $.
    ivthlem4.5 $e |- ( ( F ` A ) < U /\ U < ( F ` B ) ) $.
    ivthlem4.6 $e |- C = sup ( S , RR , < ) $.
    ivthlem4.7 $e |- S = { c e. ( A [,] B ) | ( F ` c ) <_ U } $.
    ivthlem4.8 $e |- F e. ( ( A [,] B ) -cn-> RR ) $.
    $( Lemma for ~ isupivthi . $)
    ivthlem4 $p |- ( ( S C_ RR /\ S =/= (/) /\
                        E. x e. RR A. y e. S y <_ x ) /\ A e. S ) $=
      ( cr wss c0 wne cv cle wbr wral wrex w3a wcel wa cicc co cfv crab ssrab2
      eqsstri wceq fveq2 breq1d elrab2 ltleii lbicc2 mp3an cc ccncf wi iccssre
      mp2an axresscn sstri cncffvelrn ax-mp clt pm3.26i mpbir2an iccsupr
      mp3an23 pm3.2i ) FRSFTUABUBAUBUCUDBFUEARUFUGZCFUHZCRUHZDRUHZVRJKVTWAUIFCD
      UJUKZSVSVRFIUBZHULZGUCUDZIWBUMWBPWEIWBUNUOVSCWBUHZCHULZGUCUDZWEWHICWBFWCC
      UPWDWGGUCWCCHUQURPUSVTWACDUCUDWFJKCDJKMUTCDVAVBZWGGWFWGRUHZWIWBVCSRVCSHWB
      RVDUKUHWFWJVEWBRVCVTWAWBRSJKCDVFVGVHVIVHQWBRCHVJVBVKLWGGVLUDGDHULVLUDNVMU
      TVNZABCDCFVOVPVGWKVQ $.
      $( [22-Jan-2008] $) $( [8-Oct-2007] $)

    $( Lemma for ~ isupivthi . $)
    ivthlem5 $p |- C e. ( A [,] B ) $=
      ( vx vy vv cicc co wcel cr cle wbr w3a clt csup wss c0 wne cv wral wrex
      ivthlem4 pm3.26i suprclii eqeltri wa suprub ax-mp breqtrri cfv crab
      ssrab2 eqsstri sseli wb elicc2 mp2an sylib 3simp3d rgen suprleubii
      eqbrtri 3pm3.2i mpbir ) CABSTZUAZCUBUAZACUCUDZCBUCUDZUEZVSVTWACDUBUFUGZUB
      MPQDDUBUHDUIUJQUKPUKUCUDQDULPUBUMUEZADUAZPQABCDEFGHIJKLMNOUNZUOZUPUQAWCCU
      CWDWEURAWCUCUDWFPQDAUSUTMVACWCBUCMBUBUAZRUKZBUCUDZRDULWCBUCUDIWJRDWIDUAZW
      IUBUAZAWIUCUDZWJWKWIVQUAZWLWMWJUEZDVQWIDGUKFVBEUCUDZGVQVCVQNWPGVQVDVEVFAU
      BUAZWHWNWOVGHIABWIVHVIVJVKVLPQRDBWGVMVIVNVOWQWHVRWBVGHIABCVHVIVP $.
      $( [22-Jan-2008] $) $( [8-Oct-2007] $)

    ${
      $d C w x $.  $d D w x $.  $d P x $.  $d ph x $.  $d ps x $.  $d ch x $.
      ivthlem6.9 $e |- ( ph <-> D e. RR+ ) $.
      ivthlem6.10 $e |- ( ps <-> A. w e. ( A [,] B )
                           ( ( abs ` ( C - w ) ) < D -> U < ( F ` w ) ) ) $.
      ivthlem6.11 $e |- ( ch <-> A. w e. ( A [,] B )
                           ( ( abs ` ( C - w ) ) < D -> ( F ` w ) < U ) ) $.
      ivthlem6.12 $e |- P = if ( B <_ ( C + D ) , B , ( C + D ) ) $.
      $( Lemma for ~ isupivthi : modus tollens case 1. $)
      ivthlem6 $p |- -. ( ph /\ ps ) $=
        ( vx vv vy wa wn wex wrex cmin co cv clt wbr wi wral wcel w3a cfv cle
        cicc wceq fveq2 breq1d elrab2 pm3.27bi cr wb lenlt pm3.26bi cc wss
        ccncf iccssre mp2an axresscn sstri cncffvelrn mp3an syl sylancl mpbid
        3ad2ant1 caddc csup c0 wne ivthlem4 pm3.26i suprubii syl6breqr adantr
        cabs cc0 crp rpgt0 sylbi ivthlem5 elicc2 mpbi 3simp1i recni subidi
        fveq2i abs0 eqtri syl5eqbr absdiflt a1i rpre syl3anc pm3.27d adantl
        lelttr mp3an2 sylib 3simp1d readdcl sylancr syl2an mp2and 3adant3
        opreq2 fveq2d breq2d imbi12d rcla4v syl5ib 3impia abssub biimpi recnd
        bitr3d imbi1d biimpd mpd syl3an1 mpan2d mtod 3expib con2d rgen
        suprlubii resubcl pm3.26d syl6breq sylanc 3simp2i r19.37zv ax-mp mpbir
        pm3.41 con3d r19.22si r19.29 pm4.83 rexbii rexex 19.9v ) ABUIZUJZUFUKZU
        VDUVDUFJULZUVEGHUMUNZUFUOZUPUQZUVDURZUVIUJZUVDURZUIZUFJULZUVFUVJUFJUSUV
        LUFJULZUVNUVJUFJUVHJUTZUVCUVIUVPABUVKUVPABVAZUVIKUVHLVBZUPUQZUVPAUVSUJZ
        BUVPUVRKVCUQZUVTUVPUVHEFVDUNZUTZUWAMUOZLVBZKVCUQUWAMUVHUWBJUWDUVHVEUWEU
        VRKVCUWDUVHLVFVGTVHZVIUVRVJUTZKVJUTUWAUVTVKUVPUVRKVLUVPUWCUWGUVPUWCUWAU
        WFVMZUWBVNVOVJVNVOLUWBVJVPUNUTUWCUWGURUWBVJVNEVJUTZFVJUTZUWBVJVONOEFVQV
        RVSVTVSUAUWBVJUVHLWAWBWCPWDWEWFUVQUVIUVHGHWGUNZUPUQZUVSUVPAUWLBUVPAUIUV
        HGVCUQZGUWKUPUQZUWLUVPUWMAUVPUVHJVJUPWHZGVCUGUHJUVHJVJVOZJWIWJZUHUOUGUO
        VCUQUHJUSUGVJULZVAEJUTUGUHEFGJKLMNOPQRSTUAWKWLZWMSWNWOAUWNUVPAUVGGUPUQZ
        UWNAGGUMUNZWPVBZHUPUQZUWTUWNUIZAWQHUXBUPAHWRUTZWQHUPUQUBHWSWTUXBWQWPVBW
        QUXAWQWPGGGVJUTZEGVCUQZGFVCUQZGUWBUTZUXFUXGUXHVAZEFGJKLMNOPQRSTUAXAUWIU
        WJUXIUXJVKNOEFGXBVRXCXDZXEZXFXGXHXIXJUXFUXFHVJUTZUXCUXDVKAGGHXKUXFAUXKX
        LZUXNAUXEUXMUBHXMWTZXNWEZXOXPUVHVJUTZUWKVJUTZUWMUWNUIUWLURZUVPAUXQUXFUX
        RUXSUXKUVHGUWKXQXRUVPUXQEUVHVCUQZUVHFVCUQZUVPUWCUXQUXTUYAVAZUWHUWIUWJUW
        CUYBVKNOEFUVHXBVRZXSXTUXFUXMUXRAGHYAUXKUXOYBYCYDYEUWCABUVIUWLUIZUVSURZU
        VPUWCABVAGUVHUMUNZWPVBZHUPUQZUVSURZUYEUWCABUYIUWCBUYIURAUWCGDUOZUMUNZWP
        VBZHUPUQZKUYJLVBZUPUQZURZDUWBUSUYIBUYPUYIDUVHUWBUYJUVHVEZUYMUYHUYOUVSUY
        QUYLUYGHUPUYQUYKUYFWPUYJUVHGUMYFYGVGUYQUYNUVRKUPUYJUVHLVFYHYIYJUCYKWOYL
        UWCAUYIUYEURBUWCAUIZUYIUYEUYRUYHUYDUVSUYRUVHGUMUNWPVBZHUPUQZUYHUYDUYRUY
        SUYGHUPUWCUYSUYGVEZAUVHVNUTGVNUTVUAUWCUVHGYMUWCUVHUWCUXQUXTUYAUWCUYBUYC
        YNXTZYOUXLWDWOVGUXQUXMUYTUYDVKZUWCAUXQUXFUXMVUCUXKUVHGHXKXRVUBUXOYCYPYQ
        YRYEYSUWHYTUUAUUBUUCUUDUUEAUVIURZUFJULZUVOVUEAUVIUFJULZURZUVGVJUTZUVGUW
        OUPUQVUFAUGUHUFJUVGUWSUUFUXFUXMVUHAGHUUGUXKUXOYBAUVGGUWOUPAUWTUWNUXPUUH
        SUUIUUJUWQVUEVUGVKUWPUWQUWRUWSUUKAUVIUFJUULUUMUUNVUDUVLUFJVUDUVCUVIABUV
        IUUOUUPUUQUUMUVJUVLUFJUURVRUVMUVDUFJUVIUVDUUSUUTXCUVDUFJUVAUUMUVDUFUVBX
        C $.
        $( [22-Jan-2008] $) $( [8-Oct-2007] $)

      $( Lemma for ~ isupivthi : modus tollens case 2. $)
      ivthlem7 $p |- -. ( ph /\ ch ) $=
        ( vx vv wa wn wex cicc co wrex cv clt wbr wi wral wcel w3a pm3.26 a1i
        cfv cle cmin caddc cabs wceq opreq2 fveq2d breq1d fveq2 imbi12d rcla4v
        syl5ib adantr 3impia cc abssub cr wb elicc2 mp2an biimpi 3simp1d recnd
        ivthlem5 mpbi 3simp1i recni sylancl absdiflt mp3an2 crp rpre sylbi
        syl2an bitr3d imbi1d biimpd 3adant3 mpd cc0 rpgt0 subidi fveq2i abs0
        eqtri syl5eqbr syl3anc mpbid pm3.26d adantl axlttrn resubcl sylancr
        ancoms mpand cif ltmin breq2i syl5bb readdcl jcab sylib pm3.27d
        syl2and ltle wss ccncf iccssre axresscn sstri cncffvelrn mp3an
        3ad2ant1 elrab2 biimpri ex 3syld csup c0 wne ivthlem4 pm3.26i suprubii
        syl6breqr lenlt pm3.26bi syl6 pm2.65d 3expib rgen cq qbtwnre ifcl
        syl5eqel pm3.27i ltleii ubicc2 ax-mp ltnsymi mtbiri impcom sylan nsyl3
        eqlelt 3simp3i mpbiran nsyl2 jca mpbird qssre ssrexv syl df-rex
        3simp2i lelttr mp3an12 mpani mpan syld min1 ltletr mp3an3 sylan2
        mpan2d mpan2 expcom imp3a pm3.27 jcad 19.22dv sylibr crab ssrab2
        eqsstri ssn0 r19.37zv mpbir con3 r19.22si r19.29 pm3.33 pm2.01d rexex
        19.9v ) ACUHZUIZUFUJZUXIUXIUFEFUKULZUMZUXJUXHGUFUNZUOUPZUXMIUOUPZUHZUIZ
        UQZUXQUXIUQZUHZUFUXKUMZUXLUXRUFUXKURUXSUFUXKUMZUYAUXRUFUXKUXMUXKUSZACUX
        QUYCACUTZUXPUXNUXPUXNUQUYDUXNUXOVAVBUYDUXPUXMJUSZUXNUIZUYDUXPUXMLVCZKUO
        UPZUYGKVDUPZUYEUYDGHVEULZUXMUOUPZUXMGHVFULZUOUPZUYHUXNUXOUYDGUXMVEULZVG
        VCZHUOUPZUYHUQZUYKUYMUHZUYHUQZUYCACUYQUYCCUYQUQAUYCGDUNZVEULZVGVCZHUOUP
        ZUYTLVCZKUOUPZUQZDUXKURZUYQCVUFUYQDUXMUXKUYTUXMVHZVUCUYPVUEUYHVUHVUBUYO
        HUOVUHVUAUYNVGUYTUXMGVEVIVJVKVUHVUDUYGKUOUYTUXMLVLVKVMVNUDVOVPVQUYCAUYQ
        UYSUQCUYCAUHZUYQUYSVUIUYPUYRUYHVUIUXMGVEULVGVCZHUOUPZUYPUYRVUIVUJUYOHUO
        UYCVUJUYOVHZAUXMVRUSGVRUSVULUYCUXMGVSUYCUXMUYCUXMVTUSZEUXMVDUPZUXMFVDUP
        ZUYCVUMVUNVUOUTZEVTUSZFVTUSZUYCVUPWANOEFUXMWBWCZWDWEZWFGGVTUSZEGVDUPZGF
        VDUPZGUXKUSZVVAVVBVVCUTZEFGJKLMNOPQRSTUAWGZVUQVURVVDVVEWANOEFGWBWCWHZWI
        ZWJZWKVPVKVUMHVTUSZVUKUYRWAZUYCAVUMVVAVVJVVKVVHUXMGHWLWMVUTAHWNUSZVVJUB
        HWOWPZWQWRWSWTXAXBUYCAUXNUYKUQCVUIUYJGUOUPZUXNUYKAVVNUYCAVVNGUYLUOUPZAG
        GVEULZVGVCZHUOUPZVVNVVOUHZAXCHVVQUOAVVLXCHUOUPUBHXDWPVVQXCVGVCXCVVPXCVG
        GVVIXEXFXGXHXIZVVAVVAVVJVVRVVSWAAGGHWLVVAAVVHVBZVWAVVMXJXKZXLXMAUYCVVNU
        XNUHUYKUQZUYJVTUSZVUMVWCAUYCVWDVVAVUMVWCVVHUYJGUXMXNWMVVAVVJVWDAGHXOVVH
        VVMXPVUTWQXQXRXAUYCAUXOUYMUQZCVUIUXOUXMFUOUPZUQZVWEVUIUXOVWFUYMUHZUQVWG
        VWEUHVUIUXOVWHVUMUYLVTUSZUXOVWHWAZUYCAVUMVURVWIVWJOVUMVURVWIUTUXMFUYLVD
        UPZFUYLXSZUOUPVWHUXOUXMFUYLXTIVWLUXMUOUEYAYBWMVUTVVAVVJVWIAGHYCVVHVVMXP
        ZWQWTUXOVWFUYMYDYEYFXAYGUYCAUYHUYIUQZCUYGVTUSZKVTUSVWNUYCUYGKYHUXKVRYIZ
        VTVRYIZLUXKVTYJULUSZUYCVWOUQUXKVTVRVUQVURUXKVTYINOEFYKWCYLYMZYLUAUXKVTU
        XMLYNYOPWKYPUYCAUYIUYEUQCUYCUYIUYEUYEUYCUYIUHMUNZLVCZKVDUPZUYIMUXMUXKJV
        WTUXMVHVXAUYGKVDVWTUXMLVLVKTYQZYRYSYPYTUYEUXMGVDUPZUYFUYEUXMJVTUOUUAGVD
        UGDJUXMJVTYIZJUUBUUCZUYTUGUNVDUPDJURUGVTUMZUTEJUSUGDEFGJKLMNOPQRSTUAUUD
        UUEZUUFSUUGVUMVVAVXDUYFWAUYEUXMGUUHUYEVUMVUNVUOUYEUYCVUPUYEUYCUYIVXCUUI
        VUSYEWEVVHWKXKUUJUUKUULUUMUXHUXPUQZUFUXKUMZUYBVXJUXHUXPUFUXKUMZUQZUXHUY
        CUXPUHZUFUJZVXKUXHVUMUXPUHZUFUJZVXNUXHUXPUFVTUMZVXPUXHUXPUFUUNUMZVXQVVA
        IVTUSZGIUOUPVXRUXHUFGIUUOVVAUXHVVHVBAVXSCAVWLVTIVURVWIVWLVTUSAVWKFUYLVT
        UUPOVWMXPUEUUQZVPUXHGVWLIUOUXHGVWLUOUPZGFUOUPZVVOUHZUXHVYBVVOUXHGFVHZVY
        BVYDGLVCZKUOUPZUXHVYDVYFFLVCZKUOUPZKVYGUOUPZVYHUIELVCKUOUPVYIRUURKVYGPF
        UXKUSZVYGVTUSZVUQVUREFVDUPVYJNOEFNOQUUSEFUUTYOVWPVWQVWRVYJVYKUQVWSYLUAU
        XKVTFLYNYOUVAUVBUVAVYDVYEVYGKUOGFLVLVKUVCVVRCVYFACVVRVYFCVUGVVRVYFUQZUD
        VVDVUGVYLUQVVFVUFVYLDGUXKUYTGVHZVUCVVRVUEVYFVYMVUBVVQHUOVYMVUAVVPVGUYTG
        GVEVIVJVKVYMVUDVYEKUOUYTGLVLVKVMVNUVAWPUVDVVTUVEUVFVYDVYBUIZVYDVVCVYNVV
        AVURVYDVVCVYNUHWAVVHOGFUVGWCVVAVVBVVCVVGUVHUVIYRUVJAVVOCAVVNVVOVWBYFVPU
        VKAVYAVYCWAZCVVAVURVWIVYOAGFUYLXTVWAVURAOVBVWMXJVPUVLUEUUGXJUUNVTYIVXRV
        XQUQUVMUXPUFUUNVTUVNUVAUVOUXPUFVTUVPYEUXHVXOVXMUFAVXOVXMUQCAVXOUYCUXPAV
        UMUXPUYCVUMAUXPUYCUQVUMAUHZVUNVUOUYCUXNUXOVUMVUNVUOUHUYCUQAVUMVUNVUOUYC
        UYCVUPVUSYRUULVPVUMUXNVUNUQAVUMUXNEUXMUOUPZVUNVUMVVBUXNVYQVVAVVBVVCVVGU
        VQVUQVVAVUMVVBUXNUHVYQUQNVVHEGUXMUVRUVSUVTVUQVUMVYQVUNUQNEUXMYHUWAUWBVP
        VYPUXOVWFVUOVYPUXOIFVDUPZVWFAVYRVUMAVWLFIVDVURVWIVWLFVDUPAFUYLUWCOVWMXP
        UEXIXMVUMVXSUXOVYRUHVWFUQZAVUMVXSVURVYSOUXMIFUWDUWEVXTUWFUWGVUMVWFVUOUQ
        ZAVUMVURVYTOUXMFYHUWHVPUWBYGUWIUWJVXOUXPUQAVUMUXPUWKVBUWLVPUWMXBUXPUFUX
        KUVPUWNUXKUUBUUCZVXJVXLWAJUXKYIVXFWUAJVXBMUXKUWOUXKTVXBMUXKUWPUWQVXEVXF
        VXGVXHUVQJUXKUWRWCUXHUXPUFUXKUWSUVAUWTVXIUXSUFUXKUXHUXPUXAUXBUVAUXRUXSU
        FUXKUXCWCUXTUXIUFUXKUXTUXHUXHUXQUXIUXDUXEUXBUVAUXIUFUXKUXFUVAUXIUFUXGWH
        $.
        $( [22-Jan-2008] $) $( [8-Oct-2007] $)
    $}

    $( Lemma for ~ isupivthi . $)
    ivthlem8 $p |- ( C e. ( A (,) B ) /\ ( F ` C ) = U ) $=
      ( vx vw vz vy cioo co wcel cfv wceq cr clt wbr cle cicc w3a ivthlem5 wb
      elicc2 mp2an mpbi 3simp1i wne pm3.26i ltleii lbicc2 mp3an wf cv cmin
      cabs wi wral crp wrex ccncf wa cc wss iccssre axresscn sstri elcncf
      ffvelrni ax-mp ltnei fveq2 wn lttri3i wex caddc cif biid eqid ivthlem7
      nex opreq1 fveq2d breq1d opreq1d imbi12d rexralbidv ralbidv pm3.27i
      vtoclri ivthlem2 ivthlem1 syl ivthlem3 mto ivthlem6 mpbir2an syl5eqr
      necon3i 3simp2i leltne mpbir ubicc2 syl6eq 3simp3i cxr elioo2 rexr
      syl2an biimpri pm3.2i ) CABTUAUBZCFUCZEUDZCUEUBZACUFUGZCBUFUGZYAYDACUHUGZ
      CBUHUGZCABUIUAZUBZYDYGYHUJZABCDEFGHIJKLMNOUKZAUEUBZBUEUBZYJYKULHIABCUMUNU
      OZUPZYECAUQZEAFUCZUQZYQYREUFUGZYSYTEBFUCZUFUGZLURYREAYIUBZYRUEUBYMYNABUHU
      GZUUCHIABHIKUSZABUTVAYIUEAFYIUEFVBZPVCZQVCZVDUAZVEUCZRVCZUFUGZUUGFUCZUUHF
      UCZVDUAZVEUCZSVCZUFUGZVFZQYIVGRVHVIZSVHVGZPYIVGZFYIUEVJUAUBZUUFUVBVKZOYIV
      LVMUEVLVMUVCUVDULYIUEVLYMYNYIUEVMHIABVNUNVOVPVOPSRQYIUEFVQUNUOZURZVRVSJVT
      VSCAEYRCAUDYBYRECAFWAYCYBEUFUGZWBEYBUFUGZWBYBEYJYBUEUBYLYIUECFUVFVRVSZJWC
      UVGUUKVHUBZCUUHVDUAZVEUCZUUKUFUGZUUNEUFUGZVFQYIVGZVKZRWDUVPRUVJUVMEUUNUFU
      GZVFQYIVGZUVOQABCUUKBCUUKWEUAZUHUGBUVSWFZDEFGHIJKLMNOUVJWGZUVRWGZUVOWGZUV
      TWHZWIWJUVGUVMYBUUNVDUAZVEUCZEYBVDUAUFUGZUVNRQYISRQEYIYBUWFUVLUVIJYJUVMUW
      FUUQUFUGZVFZQYIVGRVHVIZSVHVGZYLUVAUWKPCYIUUGCUDZUUTUWJSVHUWLUUSUWIRQVHYIU
      WLUULUVMUURUWHUWLUUJUVLUUKUFUWLUUIUVKVEUUGCUUHVDWKWLWMUWLUUPUWFUUQUFUWLUU
      OUWEVEUWLUUMYBUUNVDUUGCFWAWNWLWMWOWPWQUUFUVBUVEWRWSVSZWTUUHYIUBZUUNUEUBZU
      WGUVNVFZYIUEUUHFUVFVRZUWOUWFYBEVDUAUFUGZUVQVFZVFZUWOUWPVFZYBUUNEJUVIXAZWR
      XBXCXDUVHUVJUVRVKZRWDUXCRUVJUVRUVOQABCUUKUVTDEFGHIJKLMNOUWAUWBUWCUWDXEWJU
      VHUVMUWRUVQRQYISRQYBYIEUWFUVLJUVIUWMWTUWNUWOUWSUWQUWTUXAUXBURXBXCXDXFZXGX
      HVSYMYDYGYEYQULHYPYDYGYHYOXIACXJVAXKYFBCUQZUUAEUQZUXEUUBUXFYTUUBLWREUUAJB
      YIUBZUUAUEUBYMYNUUDUXGHIUUEABXLVAYIUEBFUVFVRVSVTVSBCUUAEBCUDUUAYBEBCFWAUX
      DXMXHVSYDYNYHYFUXEULYPIYDYGYHYOXNCBXJVAXKYAYDYEYFUJZYMYNYAUXHULZHIAXOUBBX
      OUBUXIYMYNABCXPAXQBXQXRUNXSVAUXDXT $.
      $( [22-Jan-2008] $) $( [8-Oct-2007] $)

    ${
      ivthlem9.9 $e |- T = { d e. ( A [,] B ) | ( F ` d ) = U } $.
      ivthlem9.10 $e |- ( F ` C ) = U $.
      $( Lemma for ~ isupivthi . $)
      ivthlem9 $p |- C = sup ( T , RR , < ) $=
        ( vv vw vx cr clt csup wceq cle wbr cicc co wcel wss iccssre mp2an
        ivthlem5 sselii c0 wne cv wral wrex w3a wa cfv crab ssrab2 eqsstri
        fveq2 eqeq1d elrab2 mpbir2an iccsupr mp3an23 suprclii letri3i suprubii
        ax-mp wi cc ccncf axresscn sstri cncffvelrn mp3an eqle ex syl breq1d
        ivthlem4 pm3.26i syl6breqr sylbir syld imp sylbi rgen suprleubii ) CEUC
        UDUEZUFCWRUGUHZWRCUGUHZCWRABUIUJZUCCAUCUKZBUCUKZXAUCULJKABUMUNZABCDFGHJ
        KLMNOPQUOZUPZTUAEXBXCEUCULEUQURUAUSTUSUGUHZUAEUTTUCVAVBZJKXBXCVCEXAULCE
        UKZXHEIUSZGVDZFUFZIXAVEXARXLIXAVFVGXICXAUKCGVDZFUFZXLXNICXAEXJCUFXKXMFX
        JCGVHVIRVJXESVKZTUAABCEVLVMUNZVNVOXIWSXOTUAECXPVPVQCUCUKUBUSZCUGUHZUBEU
        TWTXFXRUBEXQEUKXQXAUKZXQGVDZFUFZVCXRXLYAIXQXAEXJXQUFXKXTFXJXQGVHVIRVJXS
        YAXRXSYAXTFUGUHZXRXSXTUCUKZYAYBVRXAVSULUCVSULGXAUCVTUJUKXSYCVRXAUCVSXDW
        AWBWAQXAUCXQGWCWDYCYAYBXTFWEWFWGXSYBXRXSYBVCXQDUKZXRHUSZGVDZFUGUHYBHXQX
        ADYEXQUFYFXTFUGYEXQGVHWHPVJYDXQDUCUDUECUGTUADXQDUCULDUQURXGUADUTTUCVAVB
        ADUKTUAABCDFGHJKLMNOPQWIWJVPOWKWLWFWMWNWOWPTUAUBECXPWQUNVK $.
        $( [22-Jan-2008] $) $( [22-Jan-2008] $)
    $}
  $}

  ${
    isupivth.1 $e |- A e. RR $.
    isupivth.2 $e |- B e. RR $.
    isupivth.3 $e |- U e. RR $.
    isupivth.4 $e |- A < B $.
    isupivth.5 $e |- ( A [,] B ) C_ D $.
    isupivth.6 $e |- D C_ CC $.
    isupivth.7 $e |- F e. ( D -cn-> CC ) $.
    isupivth.8 $e |- ( x e. ( A [,] B ) -> ( F ` x ) e. RR ) $.
    isupivth.9 $e |- S = { x e. ( A [,] B ) | ( F ` x ) = U } $.
    ${
      $d A c x $.  $d B c x $.  $d F c x $.  $d U c x $.
      isupivth.10 $e |- ( ( F ` A ) < U /\ U < ( F ` B ) ) $.
      isupivth.11 $e |- C = sup ( S , RR , < ) $.
      $( The intermediate value theorem, increasing case with supremum
         solution.  (Contributed by Paul Chapman, 22-Jan-2008.) $)
      isupivthi $p |- ( C e. ( A (,) B ) /\ ( F ` C ) = U ) $=
        ( vc cioo co wcel cfv wceq cv cle wbr cicc crab cr clt csup cres wa
        ltleii lbicc2 mp3an fvres ax-mp breq1i ubicc2 breq2i anbi12i mpbir
        breq1d rabbii supeq1 eqcomi eqid cc ccncf wss wi ssid rescncf w3a wral
        sstri axresscn 3pm3.2i eqeltrd rgen cncffvrn mp2an eqeq1d eqtr4i
        ivthlem8 pm3.27i ivthlem9 pm3.26i eqeltri fveq2i ioossicc sselii
        3eqtr3i pm3.2i ) DBCUAUBZUCDHUDZGUEDTUFZHUDZGUGUHZTBCUIUBZUJZUKULUMZWRD
        FUKULUMXESBCXEWTHXCUNZUDZGUGUHZTXCUJZFGXFTAIJKLBXFUDZGULUHZGCXFUDZULUHZ
        UOBHUDZGULUHZGCHUDZULUHZUORXKXOXMXQXJXNGULBXCUCZXJXNUEBUKUCZCUKUCZBCUGU
        HZXRIJBCIJLUPZBCUQURBXCHUSUTVAXLXPGULCXCUCZXLXPUEXSXTYAYCIJYBBCVBURCXCH
        USUTVCVDVEZXIUKULUMZXEXIXDUEYEXEUEXHXBTXCWTXCUCXGXAGUGWTXCHUSVFVGUKXIXD
        ULVHUTVIZXIVJZXFXCVKVLUBUCZXFXCUKVLUBUCZHEVKVLUBUCZYHOEVKVMVKVKVMZXCEVM
        YJYHVNNVKVOZMEVKXCHVPURUTXCVKVMZYKUKVKVMZVQAUFZXFUDZUKUCZAXCVRYHYIVNYMY
        KYNXCEVKMNVSYLVTWAYQAXCYOXCUCZYPYOHUDZUKYOXCHUSZPWBWCAXCVKUKXFWDWEUTZFY
        SGUEZAXCUJYPGUEZAXCUJQUUCUUBAXCYRYPYSGYTWFVGWGXEWRUCZXEXFUDZGUEZBCXEXIG
        XFTIJKLYDYFYGUUAWHZWIZWJWGZUUDUUFUUGWKWLZDXFUDZUUEWSGDXEXFUUIWMDXCUCUUK
        WSUEWRXCDBCWNUUJWODXCHUSUTUUHWPWQ $.
        $( [22-Jan-2008] $) $( [22-Jan-2008] $)
    $}

    ${
      $d A a b x $.  $d B a b x $.  $d C a b $.  $d D a b x $.  $d F a b x $.
      $d G x $.  $d U x $.
      dsupivth.10 $e |- ( ( F ` B ) < U /\ U < ( F ` A ) ) $.
      dsupivth.11 $e |- C = sup ( S , RR , < ) $.

      ${
        dsupivthlem.12 $e |- G = { <. a , b >. |
                                   ( a e. D /\ b = -u ( F ` a ) ) } $.
        $( Lemma for ~ dsupivthi . $)
        dsupivthlem $p |- ( C e. ( A (,) B ) /\ ( F ` C ) = U ) $=
          ( cioo co wcel cfv wceq cneg renegcli negfcncfi cv cicc cr sseli
          fveq2 negeqd negex fvopab4 syl renegcl eqeltrd crab eqeq1d cc wb
          neg11 recnd recni sylancl bitr2d rabbii eqtri clt wbr cle ltleii
          lbicc2 mp3an sselii ax-mp pm3.27i wral rgen eleq1d rcla4v mp2 ltnegi
          mpbi eqbrtri pm3.26i ubicc2 breqtrri pm3.2i isupivthi ioossicc
          eqtr3i wss ccncf wi ssid cncffvelrn mpan2 3syl ) DBCUDUEZUFZDHUGZGUHZ
          XFDIUGZGUIZUHZABCDEFXJILMGNUJOPQEHIJKQRUCUKAULZBCUMUEZUFZXLIUGZXLHUGZ
          UIZUNXNXLEUFXOXQUHXMEXLPUOJKXLJULZHUGZUIZXQEIXRXLUHXSXPXRXLHUPUQUCXPU
          RUSUTZXNXPUNUFZXQUNUFSXPVAUTVBFXPGUHZAXMVCXOXJUHZAXMVCTYCYDAXMXNYDXQX
          JUHZYCXNXOXQXJYAVDXPVEUFGVEUFZYEYCVFXNXPGVGXNXPSVHGNVIZVJVKVLVMBIUGZX
          JVNVOXJCIUGZVNVOYHBHUGZUIZXJVNBEUFYHYKUHXMEBPBUNUFZCUNUFZBCVPVOZBXMUF
          ZLMBCLMOVQZBCVRVSZVTJKBXTYKEIXRBUHXSYJXRBHUPUQUCYJURUSWAGYJVNVOZYKXJV
          NVOCHUGZGVNVOZYRUAWBGYJNYOYBAXMWCZYJUNUFZYQYBAXMSWDZYBUUBABXMXLBUHXPY
          JUNXLBHUPWEWFWGWHWIWJXJYSUIZYIVNYTXJUUDVNVOYTYRUAWKYSGCXMUFZUUAYSUNUF
          ZYLYMYNUUELMYPBCWLVSZUUCYBUUFACXMXLCUHXPYSUNXLCHUPWEWFWGNWHWICEUFYIUU
          DUHXMECPUUGVTJKCXTUUDEIXRCUHXSYSXRCHUPUQUCYSURUSWAWMWNUBWOZWKZXGUIZXJ
          UHZXHXIUUJXJDXMUFZXIUUJUHZXEXMDBCWPUUIVTZUULDEUFZUUMXMEDPUOZJKDXTUUJE
          IXRDUHXSXGXRDHUPUQUCXGURUSUTWAXFXKUUHWBWQUULUUKXHVFZUUNUULUUOXGVEUFZU
          UQUUPEVEWRVEVEWRHEVEWSUEUFUUOUURWTQVEXAREVEDHXBVSUURYFUUQYGXGGVGXCXDW
          AWIWN $.
          $( [22-Jan-2008] $) $( [22-Jan-2008] $)
      $}

      $( The intermediate value theorem, decreasing case with supremum
         solution.  (Contributed by Paul Chapman, 22-Jan-2008.) $)
      dsupivthi $p |- ( C e. ( A (,) B ) /\ ( F ` C ) = U ) $=
        ( va vb cv wcel cfv cneg wceq wa copab eqid dsupivthlem ) ABCDEFGHTUBZE
        UCUAUBUKHUDUEUFUGTUAUHZTUAIJKLMNOPQRSULUIUJ $.
        $( [22-Jan-2008] $) $( [22-Jan-2008] $)
    $}
  $}


