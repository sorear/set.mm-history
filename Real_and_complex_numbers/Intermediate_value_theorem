$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Intermediate value theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d c x y z B $.  $d c x y z D $.  $d c x y z F $.  $d c x y z ph $.
    $d c x y A $.  $d x y z C $.  $d x y z S $.  $d c x y z U $.
    ivth.1 $e |- ( ph -> A e. RR ) $.
    ivth.2 $e |- ( ph -> B e. RR ) $.
    ivth.3 $e |- ( ph -> U e. RR ) $.
    ivth.4 $e |- ( ph -> A < B ) $.
    ivth.5 $e |- ( ph -> ( A [,] B ) C_ D ) $.
    ivth.6 $e |- ( ph -> D C_ CC ) $.
    ivth.7 $e |- ( ph -> F e. ( D -cn-> CC ) ) $.
    ivth.8 $e |- ( ( ph /\ x e. ( A [,] B ) ) -> ( F ` x ) e. RR ) $.
    ${
      ivth.9 $e |- ( ph -> ( ( F ` A ) < U /\ U < ( F ` B ) ) ) $.
      ${
        ivth.10 $e |- S = { x e. ( A [,] B ) | ( F ` x ) <_ U } $.
        ivth.11 $e |- C = sup ( S , RR , < ) $.
        $( Lemma for ~ ivth , the intermediate value theorem, increasing case
           with supremum solution. $)
        ivthlem $p |- ( ph -> ( C e. ( A (,) B ) /\ ( F ` C ) = U ) ) $=
          ( vz vy cioo co wcel cfv wceq cr clt wbr w3a csup wss c0 wne cle wral
          cv wrex cicc crab ssrab2 eqsstri iccssre syl2anc syl5ss wi mpd lbicc2
          ltle syl3anc simpld fveq2 eleq1d rcla4v sylc breq1d elrab2 sseli rexr
          syl cxr syl5 breq2 rcla4ev 3jca wn wa wb suprub syl6breqr elicc2 cmin
          mpbird cabs crp cc adantr sseldd difrp biimpa cncfi syl32anc ad2antrr
          ssralv caddc adantl rpre readdcl mpan2d breq2d notbid necon2ad jctild
          a1i imp ltlen sylibrd eqbrtrd jca r19.29 pm3.45 simprr simprl simplll
          mpbid resubcl absdiflt recnd ex lenlt syld sylbid pm2.21d com23 imp3a
          expr rexlimdva pm2.01d ad2antll sylanbrc ne0i iccleub 3expia ralrimiv
          ralrimiva ralbidv suprcl syl5eqel suprleub syl22anc lttri3 ccncf ssid
          syl5eqbr c2 cdiv cif rphalfcl ifcl simprd ubicc2 lttr ltnr syl5ibrcom
          ltaddrp ifboth letrd min1 abssubge0 rphalflt ltadd2 lelttrd ltsubadd2
          min2 oveq1 fveq2d anbi12d pncan3 3imtr4d sylibd adantld simpr ltsubrp
          baib mt2d syl6breq suprlub syl12anc ad2antrl abssuble0 jca32 reximdv2
          ltsub23 nncan simprbi adantrd mpbir2and breqtrrd syl5ibcom elioo2 ) A
          ECDUCUDUEZEIUFZHUGZAUXBEUHUEZCEUIUJZEDUIUJZUKZAUXEUXFUXGAEGUHUIULZUHT
          AGUHUMZGUNUOZUAURZBURZUPUJZUAGUQZBUHUSZUKZUXIUHUEAUXJUXKUXPAGCDUTUDZU
          HGUXMIUFZHUPUJZBUXRVAUXRSUXTBUXRVBVCZACUHUEZDUHUEZUXRUHUMZJKCDVDVEZVF
          ZACGUEZUXKACUXRUEZCIUFZHUPUJZUYGAUYBUYCCDUPUJZUYHJKACDUIUJZUYKMAUYBUY
          CUYLUYKVGJKCDVJVEVHZCDVIVKZAUYIHUIUJZUYJAUYOHDIUFZUIUJZRVLZAUYIUHUEZH
          UHUEZUYOUYJVGAUYHUXSUHUEZBUXRUQZUYSUYNAVUABUXRQUUFZVUAUYSBCUXRUXMCUGZ
          UXSUYIUHUXMCIVMZVNVOVPLUYIHVJVEVHUXTUYJBCUXRGVUDUXSUYIHUPVUEVQSVRUUAZ
          GCUUBWAZAUYCUXLDUPUJZUAGUQZUXPKAVUHUAGUXLGUEUXLUXRUEZAVUHGUXRUXLUYAVS
          ACWBUEZDWBUEZVUJVUHVGAUYBVUKJCVTWAZAUYCVULKDVTWAZVUKVULVUJVUHCDUXLUUC
          UUDVEWCUUEZUXOVUIBDUHUXMDUGZUXNVUHUAGUXMDUXLUPWDUUGWEVEWFZBUAGUUHWAUU
          IZAUYIUXCUIUJZUXFAUYIHUXCUIUYRAUXDUXCHUIUJZWGZHUXCUIUJZWGZAUXCUHUEZUY
          TUXDVVAVVCWHWIAEUXRUEZVUBVVDAVVEUXECEUPUJZEDUPUJZUKZAUXEVVFVVGVURACUX
          IEUPAUXQUYGCUXIUPUJVUQVUFBUAGCWJVETWKZAEUXIDUPTAUXJUXKUYCVUIUXIDUPUJU
          YFVUGKVUOUAGDUUJUUKUUOZWFAUYBUYCVVEVVHWIJKCDEWLVEWNZVUCVUAVVDBEUXRUXM
          EUGUXSUXCUHUXMEIVMVNVOVPZLUXCHUULVEAVUTAVUTVVAAVUTWHZUBURZEWMUDZWOUFZ
          UXLUIUJZVVNIUFZUXCWMUDWOUFZHUXCWMUDZUIUJZVGZUBFUQZUAWPUSZVVAVVMFWQUMZ
          WQWQUMZIFWQUUMUDUEZEFUEZVVTWPUEZVWDAVWEVUTOWRVWFVVMWQUUNZXOAVWGVUTPWR
          AVWHVUTAUXRFENVVKWSZWRAVUTVWIAVVDUYTVUTVWIWIVVLLUXCHWTVEXAUAUBFWQEVVT
          IXBXCVVMVWCVVAUAWPVVMUXLWPUEZWHZVWCVWBUBUXRUQZVVAAVWCVWNVGZVUTVWLAUXR
          FUMZVWONVWBUBUXRFXEWAXDVWMVWNVVQEVVNUIUJZWHZUBUXRUSZVVAVWMDEUXLUUPUUQ
          UDZXFUDZUPUJZDVXAUURZUXRUEZVXCEWMUDZWOUFZUXLUIUJZEVXCUIUJZWHZVWSVWMVX
          DVXCUHUEZCVXCUPUJZVXCDUPUJZUKZVWMVXJVXKVXLVWMUYCVXAUHUEZVXJAUYCVUTVWL
          KXDZVWMUXEVWTUHUEZVXNAUXEVUTVWLVURXDZVWMVWTWPUEZVXPVWLVXRVVMUXLUUSXGZ
          VWTXHWAZEVWTXIVEZVXBDVXAUHUUTVEZVWMCEVXCAUYBVUTVWLJXDVXQVYBAVVFVUTVWL
          VVIXDVWMVXHEVXCUPUJZVWMUXGEVXAUIUJZVXHVWMUXCUYPUIUJZUXGVVMVYEVWLAVUTV
          YEAVUTUYQVYEAUYOUYQRUVAZAVVDUYTUYPUHUEZVUTUYQWHVYEVGVVLLADUXRUEZVUBVY
          GAUYBUYCUYKVYHJKUYMCDUVBVKVUCVUAVYGBDUXRVUPUXSUYPUHUXMDIVMVNVOVPUXCHU
          YPUVCVKXJXPWRAVYEUXGVGVUTVWLAVYEVVGDEUOZWHZUXGAVYEVYIVVGAVYEDEAVYEWGD
          EUGZUXCUXCUIUJZWGZAVVDVYMVVLUXCUVDWAZVYKVYEVYLVYKUYPUXCUXCUIDEIVMXKXL
          UVEXMVVJXNAUXEUYCUXGVYJWIVURKEDXQVEXRZXDVHVWMUXEVXRVYDVXQVXSEVWTUVFVE
          VXBUXGVYDVXHDVXADVXCEUIWDVXAVXCEUIWDUVGVEZVWMUXEVXJVXHVYCVGVXQVYBEVXC
          VJVEVHZUVHVWMUYCVXNVXLVXOVYADVXAUVIVEWFAVXDVXMWIZVUTVWLAUYBUYCVYRJKCD
          VXCWLVEXDWNVWMVXGVXHVWMVXFVXEUXLUIVWMUXEVXJVYCVXFVXEUGVXQVYBVYQEVXCUV
          JVKVWMVXEUXLUIUJZVXCEUXLXFUDZUIUJZVWMVXCVXAVYTVYBVYAVWMUXEUXLUHUEZVYT
          UHUEVXQVWLWUBVVMUXLXHZXGZEUXLXIVEVWMUYCVXNVXCVXAUPUJVXOVYADVXAUVOVEVW
          MVWTUXLUIUJZVXAVYTUIUJZVWLWUEVVMUXLUVKXGVWMVXPWUBUXEWUEWUFWIVXTWUDVXQ
          VWTUXLEUVLVKYFUVMVWMVXJUXEWUBVYSWUAWIVYBVXQWUDVXCEUXLUVNVKWNXSVYPXTVW
          RVXIUBVXCUXRVVNVXCUGZVVQVXGVWQVXHWUGVVPVXFUXLUIWUGVVOVXEWOVVNVXCEWMUV
          PUVQVQVVNVXCEUIWDUVRWEVEVWNVWSWHVWBVWRWHZUBUXRUSVWMVVAVWBVWRUBUXRYAVW
          MWUHVVAUBUXRWUHVWAVWQWHZVWMVVNUXRUEZWHZVVAVWBVWRWUIVVQVWAVWQYBXPWUKVW
          AVWQVVAWUKVWQVWAVVAVWMWUJVWQVWAVVAVGVWMWUJVWQWHZWHZVWAVVAWUMVWAVWQVWM
          WUJVWQYCWUMVWAUXCVVTWMUDVVRUIUJZVVRUXCVVTXFUDZUIUJZWHZVWQWGZWUMVVRUHU
          EZVVDVVTUHUEZVWAWUQWIWUMWUJVUBWUSVWMWUJVWQYDZWUMAVUBAVUTVWLWULYEZVUCW
          AVUAWUSBVVNUXRUXMVVNUGZUXSVVRUHUXMVVNIVMZVNVOZVPZWUMAVVDWVBVVLWAZWUMU
          YTVVDWUTWUMAUYTWVBLWAZWVGHUXCYGVEVVRUXCVVTYHVKWUMWUPWURWUNWUMWUPVVNGU
          EZWURWUMVVRHUIUJZVVRHUPUJZWUPWVIWUMWUSUYTWVJWVKVGWVFWVHVVRHVJVEWUMWUO
          HVVRUIWUMUXCWQUEZHWQUEZWUOHUGWUMUXCWVGYIWUMHWVHYIUXCHUVSVEXKWUMWUJWVI
          WVKWIWVAWVIWUJWVKUXTWVKBVVNUXRGWVCUXSVVRHUPWVDVQSVRZUWEWAUVTWUMWVIVVN
          EUPUJZWURWUMAWVIWVOVGZWVBAUXQWVPVUQUXQWVIWVOUXQWVIWHVVNUXIEUPBUAGVVNW
          JZTWKYJWAWAWUMVVNUHUEZUXEWVOWURWIWUMUXRUHVVNWUMAUYDWVBUYEWAWVAWSWUMAU
          XEWVBVURWAVVNEYKVEUWAYLUWBYMUWFYNYQYOYPWCYRWCXJYLYRVHYJYSAVVBAVVBVVCA
          VVBWHZVVQVVSUXCHWMUDZUIUJZVGZUBFUQZUAWPUSZVVCWVSVWEVWFVWGVWHWVTWPUEZW
          WDAVWEVVBOWRVWFWVSVWJXOAVWGVVBPWRAVWHVVBVWKWRAVVBWWEAUYTVVDVVBWWEWILV
          VLHUXCWTVEXAUAUBFWQEWVTIXBXCWVSWWCVVCUAWPWVSVWLWHZWWCWWBUBUXRUQZVVCAW
          WCWWGVGZVVBVWLAVWPWWHNWWBUBUXRFXEWAXDWWFWWGVVQWVIWHZUBUXRUSZVVCWWFEUX
          LWMUDZVVNUIUJZUBGUSZWWJWWFUXQWWKUHUEZWWKUXIUIUJWWMAUXQVVBVWLVUQXDWWFU
          XEWUBWWNAUXEVVBVWLVURXDZVWLWUBWVSWUCXGZEUXLYGVEWWFWWKEUXIUIWWFUXEVWLW
          WKEUIUJWWOWVSVWLUWCEUXLUWDVETUWGBUAUBGWWKUWHUWIWWFWWLWWIUBGUXRWWFWVIW
          WLWHZWUJWWIWHWWFWWQWHZWUJVVQWVIWVIWUJWWFWWLGUXRVVNUYAVSZUWJZWWRVVPEVV
          NWMUDZUXLUIWWRWVRUXEWVOVVPWXAUGWWRUXRUHVVNWWRAUYDAVVBVWLWWQYEZUYEWAWW
          TWSZWWRAUXEWXBVURWAZWWRVVNUXIEUPWWRUXQWVIVVNUXIUPUJWWRAUXQWXBVUQWAWWF
          WVIWWLYDZWVQVETWKVVNEUWKVKWWRWWLWXAUXLUIUJZWWFWVIWWLYCWWRUXEWUBWVRWWL
          WXFWIWXDWWFWUBWWQWWPWRWXCEUXLVVNUWNVKYFXSWXEUWLYJUWMVHWWGWWJWHWWBWWIW
          HZUBUXRUSWWFVVCWWBWWIUBUXRYAWWFWXGVVCUBUXRWXGWWAWVIWHZWWFWUJWHVVCWWBW
          WIWXHVVQWWAWVIYBXPWWFWXHVVCVGWUJWWFWWAWVIVVCWWFWVIWWAVVCWVSVWLWVIWWAV
          VCVGWVSVWLWVIWHZWHZWWAUXCWVTWMUDZVVRUIUJZVVRUXCWVTXFUDUIUJZWHZVVCWXJW
          USVVDWVTUHUEZWWAWXNWIWXJWUJVUBWUSWVIWUJWVSVWLWWSYTAVUBVVBWXIVUCXDWVEV
          PZAVVDVVBWXIVVLXDZWXJVVDUYTWXOWXQAUYTVVBWXILXDZUXCHYGVEVVRUXCWVTYHVKW
          XJWXLVVCWXMWXJWXLHVVRUIUJZVVCWXJWXKHVVRUIWXJWVLWVMWXKHUGWXJUXCWXQYIWX
          JHWXRYIUXCHUWOVEVQWXJWXSVVCWXJWVKWXSWGZWVIWVKWVSVWLWVIWUJWVKWVNUWPYTW
          XJWUSUYTWVKWXTWIWXPWXRVVRHYKVEYFYNYMUWQYMYQYOYPWRWCYRWCXJYLYRVHYJYSUW
          RZUWSAVUSVVFECUOZWHZUXFAVUSWYBVVFAVUSECAVYMECUGZVUSWGVYNWYDVYLVUSWYDU
          XCUYIUXCUIECIVMVQXLUWTXMVVIXNAUYBUXEUXFWYCWIJVURCEXQVEXRVHAVYEUXGAUXC
          HUYPUIWYAVYFXSVYOVHWFAVUKVULUXBUXHWIVUMVUNCDEUXAVEWNWYAXT $.
          $( [30-Apr-2014] $)
      $}

      $( The intermediate value theorem, increasing case.  (Contributed by Paul
         Chapman, 22-Jan-2008.)  (The proof was shortened by Mario Carneiro,
         30-Apr-2014.) $)
      ivth $p |- ( ph -> E. c e. ( A (,) B ) ( F ` c ) = U ) $=
        ( vy cfv wceq cv cle wbr cicc co crab cr clt csup cioo wcel wrex breq1d
        wa fveq2 cbvrabv eqid ivthlem eqeq1d rcla4ev syl ) ARUAZGSZFUBUCZRCDUDU
        EZUFZUGUHUIZCDUJUEZUKVGGSZFTZUNHUAZGSZFTZHVHULABCDVGEVFFGIJKLMNOPQVDBUA
        ZGSZFUBUCRBVEVBVNTVCVOFUBVBVNGUOUMUPVGUQURVMVJHVGVHVKVGTVLVIFVKVGGUOUSU
        TVA $.
        $( [30-Apr-2014] $) $( [22-Jan-2008] $)
    $}

    ${
      ivth2.9 $e |- ( ph -> ( ( F ` B ) < U /\ U < ( F ` A ) ) ) $.
      $( The intermediate value theorem, decreasing case.  (Contributed by Paul
         Chapman, 22-Jan-2008.)  (Revised by Mario Carneiro, 30-Apr-2014.) $)
      ivth2 $p |- ( ph -> E. c e. ( A (,) B ) ( F ` c ) = U ) $=
        ( wceq cr wcel vy cv cfv cneg cmpt cioo co wrex renegcl syl cc wss eqid
        ccncf negfcncf syl2anc cicc wa sseld imp fveq2 negeqd negex eqeltrd clt
        fvmpt wbr cle wi ltle mpd lbicc2 syl3anc sseldd simprd ralrimiva eleq1d
        wb wral rcla4v sylc ltneg mpbid eqbrtrd simpld ubicc2 breqtrrd jca ivth
        ioossicc syl5ss eqeq1d ssid a1i cncff ffvelrn sylan syldan recnd adantr
        wf neg11 bitrd rexbidva ) AHUBZUAEUAUBZGUCZUDZUEZUCZFUDZRZHCDUFUGZUHXEG
        UCZFRZHXMUHABCDEXKXIHIJAFSTZXKSTKFUIUJLMNAEUKULZGEUKUNUGZTZXIXRTNOUAEGX
        IXIUMZUOUPABUBZCDUQUGZTZURZYAXIUCZYAGUCZUDZSYDYAETZYEYGRAYCYHAYBEYAMUSU
        TUAYAXHYGEXIXFYARXGYFXFYAGVAVBXTYFVCVFUJYDYFSTZYGSTPYFUIUJVDACXIUCZXKVE
        VGXKDXIUCZVEVGAYJCGUCZUDZXKVEACETYJYMRAYBECMACSTZDSTZCDVHVGZCYBTZIJACDV
        EVGZYPLAYNYOYRYPVIIJCDVJUPVKZCDVLVMZVNUACXHYMEXIXFCRXGYLXFCGVAVBXTYLVCV
        FUJAFYLVEVGZYMXKVEVGZADGUCZFVEVGZUUAQVOAXPYLSTZUUAUUBVRKAYQYIBYBVSZUUEY
        TAYIBYBPVPZYIUUEBCYBYACRYFYLSYACGVAVQVTWAFYLWBUPWCWDAXKUUCUDZYKVEAUUDXK
        UUHVEVGZAUUDUUAQWEAUUCSTZXPUUDUUIVRADYBTZUUFUUJAYNYOYPUUKIJYSCDWFVMZUUG
        YIUUJBDYBYADRYFUUCSYADGVAVQVTWAKUUCFWBUPWCADETYKUUHRAYBEDMUULVNUADXHUUH
        EXIXFDRXGUUCXFDGVAVBXTUUCVCVFUJWGWHWIAXLXOHXMAXEXMTZURZXLXNUDZXKRZXOUUN
        XJUUOXKUUNXEETZXJUUORAUUMUUQAXMEXEAXMYBECDWJMWKUSUTZUAXEXHUUOEXIXFXERXG
        XNXFXEGVAVBXTXNVCVFUJWLUUNXNUKTZFUKTZUUPXOVRAUUMUUQUUSUURAEUKGXAZUUQUUS
        AXQUKUKULZXSUVANUVBAUKWMWNOEUKGWOVMEUKXEGWPWQWRAUUTUUMAFKWSWTXNFXBUPXCX
        DWC $.
        $( [30-Apr-2014] $) $( [22-Jan-2008] $)
    $}

    ivthle2.9 $e |- ( ph -> ( ( F ` B ) <_ U /\ U <_ ( F ` A ) ) ) $.
    $( The intermediate value theorem with weak inequality, decreasing case.
       (Contributed by Mario Carneiro, 12-May-2014.) $)
    ivthle2 $p |- ( ph -> E. c e. ( A [,] B ) ( F ` c ) = U ) $=
      ( wceq cr wcel cfv clt wbr wo cv cicc co wrex simpld wb wral ltle syl2anc
      cle wi mpd ubicc2 syl3anc ralrimiva fveq2 eleq1d rcla4v sylc leloe simprd
      mpbid wa lbicc2 adantr cioo wss ioossicc ccncf adantlr simpr ivth2 ssrexv
      cc mpsyl anassrs eqcom eqeq2d syl5bb rcla4ev sylan jaodan mpdan eqeq1d )
      ADGUAZFUBUCZWIFRZUDZHUEZGUAZFRZHCDUFUGZUHZAWIFUNUCZWLAWRFCGUAZUNUCZQUIAWI
      STZFSTZWRWLUJADWPTZBUEZGUAZSTZBWPUKZXAACSTZDSTZCDUNUCZXCIJACDUBUCZXJLAXHX
      IXKXJUOIJCDULUMUPZCDUQURZAXFBWPPUSZXFXABDWPXDDRXEWISXDDGUTVAVBVCKWIFVDUMV
      FAWJWQWKAWJVGZFWSUBUCZFWSRZUDZWQAXRWJAWTXRAWRWTQVEAXBWSSTZWTXRUJKACWPTZXG
      XSAXHXIXJXTIJXLCDVHURZXNXFXSBCWPXDCRXEWSSXDCGUTVAVBVCFWSVDUMVFVIXOXPWQXQA
      WJXPWQCDVJUGZWPVKAWJXPVGZVGZWOHYBUHWQCDVLYDBCDEFGHAXHYCIVIAXIYCJVIAXBYCKV
      IAXKYCLVIAWPEVKYCMVIAEVRVKYCNVIAGEVRVMUGTYCOVIAXDWPTXFYCPVNAYCVOVPWOHYBWP
      VQVSVTAXQWQWJAXTXQWQYAWOXQHCWPWOFWNRWMCRZXQWNFWAYEWNWSFWMCGUTWBWCWDWEVNWF
      WGAXCWKWQXMWOWKHDWPWMDRWNWIFWMDGUTWHWDWEWFWG $.
      $( [30-Apr-2014] $) $( [22-Jan-2008] $)
  $}


