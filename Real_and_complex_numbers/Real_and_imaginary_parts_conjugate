$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Real and imaginary parts; conjugate
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Re $. $( Real part of a complex number. $)
  $c Im $. $( Imaginary part of a complex number. $)
  $c * $. $( Complex conjugate function. $)

  $( Extend class notation to include real part of a complex number. $)
  cre $a class Re $.

  $( Extend class notation to include imaginary part of a complex number. $)
  cim $a class Im $.

  $( Extend class notation to include complex conjugate function. $)
  ccj $a class * $.

  ${
    $d x y $.
    $( Define the complex conjugate function.  See ~ cjcli for its closure and
       ~ cjval for its value. $)
    df-cj $a |- * = ( x e. CC |-> ( iota_ y e. CC
                 ( ( x + y ) e. RR /\ ( _i x. ( x - y ) ) e. RR ) ) ) $.

    $( Define a function whose value is the real part of a complex number.  See
       ~ reval for its value, ~ recli for its closure, and ~ replim for its use
       in decomposing a complex number. $)
    df-re $a |- Re = ( x e. CC |-> ( ( x + ( * ` x ) ) / 2 ) ) $.

    $( Define a function whose value is the imaginary part of a complex
       number.  See ~ imval for its value, ~ imcli for its closure, and
       ~ replim for its use in decomposing a complex number. $)
    df-im $a |- Im = ( x e. CC |-> ( Re ` ( x / _i ) ) ) $.
  $}

  ${
    $d x y A $.
    $( The value of the conjugate of a complex number. $)
    cjval $p |- ( A e. CC -> ( * ` A ) = ( iota_ x e. CC
          ( ( A + x ) e. RR /\ ( _i x. ( A - x ) ) e. RR ) ) ) $=
      ( vy cv caddc co cr wcel ci cmin cmul crio ccj wceq opreq1 eleq1d opreq2d
      wa cc anbi12d riotabidv df-cj riotaex fvmpt ) CBCDZADZEFZGHZIUEUFJFZKFZGH
      ZRZASLBUFEFZGHZIBUFJFZKFZGHZRZASLSMUEBNZULURASUSUHUNUKUQUSUGUMGUEBUFEOPUS
      UJUPGUSUIUOIKUEBUFJOQPTUACAUBURASUCUD $.
      $( [6-Nov-2013] $)

    $( The defining property of the complex conjugate. $)
    cjth $p |- ( A e. CC ->
          ( ( A + ( * ` A ) ) e. RR /\ ( _i x. ( A - ( * ` A ) ) ) e. RR ) ) $=
      ( vx cc wcel cv caddc co cr ci cmin cmul wa ccj cfv wsbc crio wceq opreq2
      syl eleq1d wreu cju riota4 cjval dfsbcq mpbird fvex opreq2d anbi12d sbcie
      wb sylib ) ACDZABEZFGZHDZIAUNJGZKGZHDZLZBAMNZOZAVAFGZHDZIAVAJGZKGZHDZLZUM
      VBUTBUTBCPZOZUMUTBCUAVJBAUBUTBCUCSUMVAVIQVBVJUKBAUDUTBVAVIUESUFUTVHBVAAMU
      GUNVAQZUPVDUSVGVKUOVCHUNVAAFRTVKURVFHVKUQVEIKUNVAAJRUHTUIUJUL $.
      $( [6-Nov-2013] $)

    $( Domain and codomain of the conjugate function. $)
    cjf $p |- * : CC --> CC $=
      ( vx vy cv caddc co cr wcel ci cmin cmul wa cc crio wral ccj wreu riotacl
      wf cju syl rgen df-cj fmpt mpbi ) ACZBCZDEFGHUEUFIEJEFGKZBLMZLGZALNLLORUI
      ALUELGUGBLPUIBUESUGBLQTUAALLUHOABUBUCUD $.
      $( [6-Nov-2013] $)

    $( The conjugate of a complex number is a complex number (closure law). $)
    cjcl $p |- ( A e. CC -> ( * ` A ) e. CC ) $=
      ( cc ccj cjf ffvelrni ) BBACDE $.
      $( [6-Nov-2013] $) $( [10-May-1999] $)

    $( The value of the real part of a complex number. $)
    reval $p |- ( A e. CC -> ( Re ` A ) = ( ( A + ( * ` A ) ) / 2 ) ) $=
      ( vx cv ccj cfv caddc co c2 cdiv cc cre fveq2 opreq12 mpdan opreq1d df-re
      wceq oprex fvmpt ) BABCZTDEZFGZHIGAADEZFGZHIGJKTAQZUBUDHIUEUAUCQUBUDQTADL
      TAUAUCFMNOBPUDHIRS $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( The value of the imaginary part of a complex number. $)
    imval $p |- ( A e. CC -> ( Im ` A ) = ( Re ` ( A / _i ) ) ) $=
      ( vx cv ci cdiv co cre cfv cc cim wceq opreq1 fveq2d df-im fvex fvmpt ) B
      ABCZDEFZGHADEFZGHIJQAKRSGQADELMBNSGOP $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( The imaginary part of a complex number in terms of the real part
       function. $)
    imre $p |- ( A e. CC -> ( Im ` A ) = ( Re ` ( -u _i x. A ) ) ) $=
      ( cc wcel cim cfv ci cdiv co cre cneg cmul imval cc0 wne wceq ax-icn ine0
      c1 divrec2 mp3an23 irec opreq1i syl6eq fveq2d eqtrd ) ABCZADEAFGHZIEFJZAK
      HZIEALUFUGUIIUFUGRFGHZAKHZUIUFFBCFMNUGUKOPQAFSTUJUHAKUAUBUCUDUE $.
      $( [6-Nov-2013] $) $( [12-May-2005] $)

    $( The real part of a complex number is real. $)
    recl $p |- ( A e. CC -> ( Re ` A ) e. RR ) $=
      ( cc wcel cre cfv ccj caddc co c2 cdiv cr reval cmin cmul simpld rehalfcl
      ci cjth syl eqeltrd ) ABCZADEAAFEZGHZIJHZKALUAUCKCZUDKCUAUEQAUBMHNHKCAROU
      CPST $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( The imaginary part of a complex number is real. $)
    imcl $p |- ( A e. CC -> ( Im ` A ) e. RR ) $=
      ( cc wcel cim cfv ci cneg cmul co cre cr imre ax-icn negcli ax-mulcl mpan
      recl syl eqeltrd ) ABCZADEFGZAHIZJEZKALTUBBCZUCKCUABCTUDFMNUAAOPUBQRS $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( Domain and codomain of the real part function.  (Contributed by Paul
       Chapman, 22-Oct-2007.) $)
    ref $p |- Re : CC --> RR $=
      ( vx cv ccj cfv caddc co c2 cdiv cr wcel cc wral reval recl eqeltrrd rgen
      cre wf df-re fmpt mpbi ) ABZUBCDEFGHFZIJZAKLKIQRUDAKUBKJUBQDUCIUBMUBNOPAK
      IUCQASTUA $.
      $( [6-Nov-2013] $) $( [22-Oct-2007] $)

    $( Domain and codomain of the imaginary part function.  (Contributed by
       Paul Chapman, 22-Oct-2007.) $)
    imf $p |- Im : CC --> RR $=
      ( vx cv ci cdiv co cre cfv cr wcel cc wral imval imcl eqeltrrd rgen df-im
      cim wf fmpt mpbi ) ABZCDEFGZHIZAJKJHQRUCAJUAJIUAQGUBHUALUAMNOAJHUBQAPST
      $.
      $( [6-Nov-2013] $) $( [22-Oct-2007] $)

    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crre $p |- ( ( A e. RR /\ B e. RR ) -> ( Re ` ( A + ( _i x. B ) ) ) = A )
        $=
      ( cr wcel ci cmul co caddc cdiv wceq ax-icn sylancr syl cmin syl11anc a1i
      c2 cc syl111anc opreq1d wa cre cfv ccj recn mulcl addcl syl2an reval recl
      cc0 eqeltrrd simpl resubcl adantl cjcl subcl halfcl subdi adantr ax-mulcl
      pnpcan pnpcan2 eqtr4d ax-addcl addsub subsub 3eqtr4d opreq2d wne 2cn 2ne0
      2times divsubdir syl112anc 3eqtr3d divcan3 ax-mulass divass opreq12d cneg
      c1 ixi renegcli eqeltri simpr ax-mulrcl cjth simprd rehalfcl 3syl eqeltrd
      1re rimul wb subeq0 mpbid eqtrd ) ACDZBCDZUAZAEBFGZHGZUBUCZXCXCUDUCZHGZQI
      GZAXAXCRDZXDXGJWSARDZXBRDZXHWTAUEZWTERDZBRDZXJKBUEZEBUFZLAXBUGUHZXCUIMZXA
      XGANGZUKJZXGAJZXAXRCDZEXRFGZCDXSXAXGCDWSYAXAXDXGCXQXAXHXDCDXPXCUJMULWSWTU
      MXGAUNOXAYBEEFGZBFGZEXCXENGZFGZQIGZNGZCXAEXBYEQIGZNGZFGZEXBFGZEYIFGZNGZYB
      YHXAXLXJYIRDZYKYNJXLXAKPZXAXLXMXJYPWTXMWSXNUOZXOOXAYERDZYOXAXHXERDZYRXPXA
      XHYSXPXCUPMZXCXEUQOZYEURMEXBYIUSSXAXRYJEFXAXGQAFGZQIGZNGZQXBFGZQIGZYINGZX
      RYJXAXFUUBNGZQIGZUUEYENGZQIGZUUDUUGXAUUHUUJQIXAXFAAHGZNGZXBXBHGZYENGZUUHU
      UJXAXCUULNGZXEHGZUUNXCNGZXEHGZUUMUUOXAUUPUURXEHXAUUPXBANGZUURXAXIXJXIUUPU
      UTJWSXIWTXKUTZXAXLXMXJKYQEBVALZUVAAXBAVBSXAXJXIXJUURUUTJUVBUVAUVBXBAXBVCS
      VDTXAXHYSUULRDZUUMUUQJXPYTXAXIXIUVCUVAUVAAAVEOXCXEUULVFSXAUUNRDZXHYSUUOUU
      SJXAXJXJUVDUVBUVBXBXBVEOXPYTUUNXCXEVGSVHXAUUBUULXFNXAXIUUBUULJUVAAVMMVIXA
      UUEUUNYENXAXJUUEUUNJUVBXBVMMTVHTXAXFRDZUUBRDZQRDZQUKVJZUUIUUDJXAXHYSUVEXP
      YTXCXEVEOZXAUVGXIUVFVKUVAQAVALUVGXAVKPZUVHXAVLPZXFUUBQVNVOXAUUERDZYRUVGUV
      HUUKUUGJXAUVGXJUVLVKUVBQXBVALUUAUVJUVKUUEYEQVNVOVPXAUUCAXGNXAXIUVGUVHUUCA
      JUVAUVJUVKAQVQSVIXAUUFXBYINXAXJUVGUVHUUFXBJUVBUVJUVKXBQVQSTVPVIXAYDYLYGYM
      NXAXLXLXMYDYLJYPYPYQEEBVRSXAXLYRUVGUVHYGYMJYPUUAUVJUVKEYEQVSVOVTVHXAYDCDZ
      YGCDZYHCDXAYCCDWTUVMYCWBWACWCWBWMWDWEWSWTWFYCBWGLXAXHYFCDZUVNXPXHXFCDUVOX
      CWHWIYFWJWKYDYGUNOWLXRWNOXAXGRDZXIXSXTWOXAUVEUVPUVIXFURMUVAXGAWPOWQWR $.
      $( [7-Nov-2013] $) $( [12-May-2005] $)

    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crim $p |- ( ( A e. RR /\ B e. RR ) -> ( Im ` ( A + ( _i x. B ) ) ) = B )
        $=
      ( cr wcel wa ci cmul co caddc cfv cdiv cre cneg wceq recn ax-icn ax-mulcl
      cc syl2an ine0 cim sylancr ax-addcl imval syl mpan cc0 wne divdir mpanr12
      3expa sylan2 c1 divrec2 mp3an23 irec opreq1i a1i mulneg12 divcan3 opreq12
      3eqtrd negcl addcom sylan 3eqtrrd fveq2d id renegcl crre syl2anr 3eqtr2d
      ) ACDZBCDZEZAFBGHZIHZUAJZVQFKHZLJZBFAMZGHZIHZLJZBVOVQRDZVRVTNVMARDZVPRDZW
      EVNAOZVNFRDZBRDZWGPBOZFBQZUBAVPUCSVQUDUEVOWCVSLVMWFWJWCVSNVNWHWKWFWJEVSAF
      KHZVPFKHZIHZWBBIHZWCWJWFWGVSWONZWIWJWGPWLUFWFWGEWIFUGUHZWQPTWFWGWIWREWQAV
      PFUIUKUJULWFWMWBNWNBNZWOWPNWJWFWMUMFKHZAGHZFMZAGHZWBWFWIWRWMXANPTAFUNUOXA
      XCNWFWTXBAGUPUQURWIWFXCWBNPFAUSUFVBWJWIWRWSPTBFUTUOWMWBWNBIVASWFWBRDZWJWP
      WCNWFWIWARDXDPAVCFWAQUBWBBVDVEVFSVGVNVNWACDWDBNVMVNVHAVIBWAVJVKVL $.
      $( [7-Nov-2013] $) $( [12-May-2005] $)

    $( Reconstruct a complex number from its real and imaginary parts. $)
    replim $p |- ( A e. CC -> A = ( ( Re ` A ) + ( _i x. ( Im ` A ) ) ) ) $=
      ( vx vy cc wcel cv ci cmul co caddc wceq wrex cre cfv cim ax-cnre opreq2d
      cr opreq12d fveq2 wa crre crim eqcomd id eqeq12d syl5ibrcom rexlimivv syl
      ) ADEABFZGCFZHIZJIZKZCRLBRLAAMNZGAONZHIZJIZKZBCAPUNUSBCRRUJREUKREUAZUSUNU
      MUMMNZGUMONZHIZJIZKUTVDUMUTVAUJVCULJUJUKUBUTVBUKGHUJUKUCQSUDUNAUMURVDUNUE
      UNUOVAUQVCJAUMMTUNUPVBGHAUMOTQSUFUGUHUI $.
      $( [7-Nov-2013] $) $( [10-May-1999] $)

    $( Value of the conjugate of a complex number.  The value is the real part
       minus ` _i ` times the imaginary part.  Definition 10-3.2 of [Gleason]
       p. 132. $)
    remim $p |- ( A e. CC ->
                      ( * ` A ) = ( ( Re ` A ) - ( _i x. ( Im ` A ) ) ) ) $=
      ( vx vy cc wcel cfv cv caddc co cr ci cmin cmul wa recnd ax-icn syl111anc
      wceq eqtrd syl11anc ccj crio cre cim replim opreq1d recl ax-mulcl sylancr
      cjval imcl ppncan ax-addrcl eqeltrd pnncan a1i ax-distr 3eqtr4d ax-mulass
      opreq2d mp3an12 syl eqcomd c1 cneg ixi 1re renegcli eqeltri ax-mulrcl jca
      wreu wb subcl cju ax-17 a17d opreq2 eleq1d anbi12d riota2f mpbid ) ADEZAU
      AFABGZHIZJEZKAWDLIZMIZJEZNZBDUBZAUCFZKAUDFZMIZLIZBAUJWCAWOHIZJEZKAWOLIZMI
      ZJEZNZWKWORZWCWQWTWCWPWLWLHIZJWCWPWLWNHIZWOHIZXCWCAXDWOHAUEZUFWCWLDEZWNDE
      ZXGXEXCRWCWLAUGZOZWCKDEZWMDEZXHPWCWMAUKZOZKWMUHUIZXJWLWNWLULQSWCWLJEZXPXC
      JEXIXIWLWLUMTUNWCWSKKMIZWMWMHIZMIZJWCWSKKXRMIZMIZXSWCWRXTKMWCXDWOLIZWNWNH
      IZWRXTWCXGXHXHYBYCRXJXOXOWLWNWNUOQWCAXDWOLXFUFWCXKXLXLXTYCRXKWCPUPXNXNKWM
      WMUQQURUTWCXSYAWCXRDEZXSYARZWCXRWCWMJEZYFXRJEZXMXMWMWMUMTZOXKXKYDYEPPKKXR
      USVAVBVCSWCXQJEYGXSJEXQVDVEJVFVDVGVHVIYHXQXRVJUIUNVKWCWODEZWJBDVLXAXBVMWC
      XGXHYIXJXOWLWNVNTBAVOWJXABCDWOCGWOEBVPYIXABVQWDWORZWFWQWIWTYJWEWPJWDWOAHV
      RVSYJWHWSJYJWGWRKMWDWOALVRUTVSVTWATWBS $.
      $( [7-Nov-2013] $) $( [10-May-1999] $)
  $}

  ${
    recl.1 $e |- A e. CC $.
    $( The real part of a complex number is real (closure law). $)
    recli $p |- ( Re ` A ) e. RR $=
      ( cc wcel cre cfv cr recl ax-mp ) ACDAEFGDBAHI $.
      $( [11-May-1999] $)

    $( The imaginary part of a complex number is real (closure law). $)
    imcli $p |- ( Im ` A ) e. RR $=
      ( cc wcel cim cfv cr imcl ax-mp ) ACDAEFGDBAHI $.
      $( [11-May-1999] $)

    $( Closure law for complex conjugate. $)
    cjcli $p |- ( * ` A ) e. CC $=
      ( cc wcel ccj cfv cjcl ax-mp ) ACDAEFCDBAGH $.
      $( [11-May-1999] $)

    $( Construct a complex number from its real and imaginary parts. $)
    replimi $p |- A = ( ( Re ` A ) + ( _i x. ( Im ` A ) ) ) $=
      ( cc wcel cre cfv ci cim cmul co caddc wceq replim ax-mp ) ACDAAEFGAHFIJK
      JLBAMN $.
      $( [11-Mar-2008] $) $( [1-Oct-1999] $)
  $}

  ${
    crre.1 $e |- A e. RR $.
    crre.2 $e |- B e. RR $.
    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crrei $p |- ( Re ` ( A + ( _i x. B ) ) ) = A $=
      ( cr wcel ci cmul co caddc cre cfv wceq crre mp2an ) AEFBEFAGBHIJIKLAMCDA
      BNO $.
      $( [11-Mar-2005] $) $( [10-May-1999] $)

    $( The imaginary part of a complex number representation.  Definition
       10-3.1 of [Gleason] p. 132. $)
    crimi $p |- ( Im ` ( A + ( _i x. B ) ) ) = B $=
      ( cr wcel ci cmul co caddc cim cfv wceq crim mp2an ) AEFBEFAGBHIJIKLBMCDA
      BNO $.
      $( [11-Mar-2005] $) $( [10-May-1999] $)
  $}

  $( The imaginary part of a real number is 0. $)
  reim0 $p |- ( A e. RR -> ( Im ` A ) = 0 ) $=
    ( cr wcel ci cc0 cmul co caddc cim cfv cc wceq ax-icn mul01i opreq2i addid1
    recn syl5eq syl fveq2d 0re crim mpan2 eqtr3d ) ABCZADEFGZHGZIJZAIJEUEUGAIUE
    AKCZUGALAQUIUGAEHGAUFEAHDMNOAPRSTUEEBCUHELUAAEUBUCUD $.
    $( [7-Nov-2013] $) $( [18-Mar-2005] $)

  $( A number is real iff its imaginary part is 0. $)
  reim0b $p |- ( A e. CC -> ( A e. RR <-> ( Im ` A ) = 0 ) ) $=
    ( cc wcel cr cim cfv cc0 wceq reim0 wa cre ci co caddc replim adantr opreq2
    cmul ax-icn mul01i syl6eq opreq2d recl recnd addid1 sylan9eqr eqtrd eqeltrd
    syl ex impbid2 ) ABCZADCZAEFZGHZAIULUOUMULUOJZAAKFZDUPAUQLUNRMZNMZUQULAUSHU
    OAOPUOULUSUQGNMZUQUOURGUQNUOURLGRMGUNGLRQLSTUAUBULUQBCUTUQHULUQAUCZUDUQUEUI
    UFUGULUQDCUOVAPUHUJUK $.
    $( [24-Jul-2008] $) $( [26-Sep-2005] $)

  $( A number is real iff it equals its real part.  Proposition 10-3.4(f) of
     [Gleason] p. 133. $)
  rereb $p |- ( A e. CC -> ( A e. RR <-> ( Re ` A ) = A ) ) $=
    ( cc wcel cr cre cfv wceq wa ci cim cmul co caddc cc0 replim adantr opreq2d
    reim0 ax-icn mul01i syl6eq adantl recnd addid1 syl 3eqtrrd eqeltrrd impbida
    recl simpr ) ABCZADCZAEFZAGZUKULHZAUMIAJFZKLZMLZUMNMLZUMUKAURGULAOPUOUQNUMM
    ULUQNGUKULUQINKLNULUPNIKARQISTUAUBQUKUSUMGZULUKUMBCUTUKUMAUIZUCUMUDUEPUFUKU
    NHUMADUKUNUJUKUMDCUNVAPUGUH $.
    $( [20-Aug-2008] $)

  $( A product with a nonzero real multiplier is real iff the multiplicand is
     real. $)
  mulre $p |- ( ( A e. CC /\ B e. RR /\ B =/= 0 )
         -> ( A e. RR <-> ( B x. A ) e. RR ) ) $=
    ( cc wcel cr cre wceq cmul co wb rereb 3ad2ant1 wa recnd syl111anc ci caddc
    cfv adantl ax-icn cc0 wne recl simp1 recn anim1i 3adant1 mulcan adantr imcl
    w3a mulcl sylancr adddi replim opreq2d mp3an1 syl2an 3eqtr4d fveq2d remulcl
    cim mul12 sylan2 syl11anc eqtr2d eqeq1d sylan bitr4d ancoms 3adant3 3bitr2d
    crre syl ) ACDZBEDZBUAUBZUKZAEDZAFRZAGZBVTHIZBAHIZGZWCEDZVOVPVSWAJVQAKLVRVT
    CDZVOBCDZVQMZWDWAJVOVPWFVQVOVTAUCZNZLVOVPVQUDVPVQWHVOVPWGVQBUEZUFUGVTABUHOV
    OVPWDWEJZVQVPVOWLVPVOMZWDWCFRZWCGZWEWMWBWNWCWMWNWBPBAVBRZHIZHIZQIZFRZWBWMWC
    WSFWMBVTPWPHIZQIZHIZWBBXAHIZQIZWCWSWMWGWFXACDZXCXEGVPWGVOWKUIVOWFVPWJSVOXFV
    PVOPCDZWPCDZXFTVOWPAUJZNZPWPULUMSBVTXAUNOWMAXBBHVOAXBGVPAUOSUPWMWRXDWBQVPWG
    XHWRXDGZVOWKXJXGWGXHXKTPBWPVCUQURUPUSUTWMWBEDZWQEDZWTWBGVOVPVTEDXLWIBVTVAVD
    VOVPWPEDXMXIBWPVAVDWBWQVMVEVFVGWMWCCDZWEWOJVPWGVOXNWKBAULVHWCKVNVIVJVKVL $.
    $( [21-Aug-2008] $)

  ${
    cjcji.1 $e |- A e. CC $.
    $( The conjugate of the conjugate is the original complex number.
       Proposition 10-3.4(e) of [Gleason] p. 133. $)
    cjcji $p |- ( * ` ( * ` A ) ) = A $=
      ( ccj cfv cre ci cim cmul co cmin cneg caddc recni ax-icn opreq2i cc wcel
      wceq remim ax-mp recli imcli mulcli negsubi mulneg2i 3eqtr4ri crrei eqtri
      fveq2i renegcli crimi 3eqtri opreq12i replimi subnegi eqtr4i 3eqtr4i
      cjcli ) ACDZEDZFUSGDZHIZJIZAEDZFAGDZHIZKZJIZUSCDZAUTVDVBVGJUTVDFVEKZHIZLI
      ZEDVDUSVLEVDVGLIVDVFJIZVLUSVDVFVDABUAZMZFVENVEABUBZMZUCZUDVKVGVDLFVENVQUE
      ZOAPQUSVMRBASTUFZUIVDVJVNVEVPUJZUGUHVBFVLGDZHIVKVGVAWBFHUSVLGVTUIOWBVJFHV
      DVJVNWAUKOVSULUMUSPQVIVCRABURUSSTAVDVFLIVHABUNVDVFVOVRUOUPUQ $.
      $( [11-May-1999] $)

    $( A number is real iff its imaginary part is 0. $)
    reim0bi $p |- ( A e. RR <-> ( Im ` A ) = 0 ) $=
      ( cc wcel cr cim cfv cc0 wceq wb reim0b ax-mp ) ACDAEDAFGHIJBAKL $.
      $( [29-May-1999] $)

    $( A real number equals its real part.  Proposition 10-3.4(f) of [Gleason]
       p. 133. $)
    rerebi $p |- ( A e. RR <-> ( Re ` A ) = A ) $=
      ( cc wcel cr cre cfv wceq wb rereb ax-mp ) ACDAEDAFGAHIBAJK $.
      $( [24-Jul-2008] $) $( [27-Oct-1999] $)

    $( A number is real iff it equals its complex conjugate.  Proposition
       10-3.4(f) of [Gleason] p. 133. $)
    cjrebi $p |- ( A e. RR <-> ( * ` A ) = A ) $=
      ( cr wcel cim cfv cc0 wceq ccj reim0bi cneg imcli recni eqnegi ci cmul co
      ax-icn negcli caddc mulneg2i eqeq2i ine0 mulcani replimi cmin remim ax-mp
      cre cc mulcli negsubi eqtr4i eqeq12i addcani bitr2i 3bitr3i bitr3i 3bitri
      recli eqcom ) ACDAEFZGHZAAIFZHZVDAHABJVCVBVBKZHZVEVBVBABLMZNOVBPQZOVFPQZH
      VIVIKZHZVGVEVJVKVIOVBRVHUAUBVBVFOVHVBVHSRUCUDVEAUIFZVITQZVMVKTQZHVLAVNVDV
      OABUEVDVMVIUFQZVOAUJDVDVPHBAUGUHVMVIVMABUTMZOVBRVHUKZULUMUNVMVIVKVQVRVIVR
      SUOUPUQURAVDVAUS $.
      $( [11-Oct-1999] $)

    $( Real part of a complex conjugate. $)
    recji $p |- ( Re ` ( * ` A ) ) = ( Re ` A ) $=
      ( ccj cfv cre ci cim cneg cmul caddc cmin recli recni ax-icn imcli mulcli
      co negsubi mulneg2i opreq2i wcel wceq remim ax-mp 3eqtr4ri renegcli crrei
      cc fveq2i eqtri ) ACDZEDAEDZFAGDZHZIQZJQZEDULUKUPEULFUMIQZHZJQULUQKQZUPUK
      ULUQULABLZMFUMNUMABOZMZPRUOURULJFUMNVBSTAUHUAUKUSUBBAUCUDUEUIULUNUTUMVAUF
      UGUJ $.
      $( [2-Oct-1999] $)

    $( Imaginary part of a complex conjugate. $)
    imcji $p |- ( Im ` ( * ` A ) ) = -u ( Im ` A ) $=
      ( ccj cfv cim cre ci cneg cmul caddc cmin recli recni ax-icn imcli mulcli
      co negsubi mulneg2i opreq2i wcel wceq remim ax-mp 3eqtr4ri renegcli crimi
      cc fveq2i eqtri ) ACDZEDAFDZGAEDZHZIQZJQZEDUNUKUPEULGUMIQZHZJQULUQKQZUPUK
      ULUQULABLZMGUMNUMABOZMZPRUOURULJGUMNVBSTAUHUAUKUSUBBAUCUDUEUIULUNUTUMVAUF
      UGUJ $.
      $( [2-Oct-1999] $)

    ${
      readdi.2 $e |- B e. CC $.
      $( Real part distributes over addition. $)
      readdi $p |- ( Re ` ( A + B ) ) = ( ( Re ` A ) + ( Re ` B ) ) $=
        ( caddc co cre cfv ci cim recli recni ax-icn imcli mulcli add4i replimi
        cmul opreq12i readdcli adddii opreq2i 3eqtr4i fveq2i crrei eqtri ) ABEF
        ZGHAGHZBGHZEFZIAJHZBJHZEFZRFZEFZGHUJUGUOGUHIUKRFZEFZUIIULRFZEFZEFUJUPUR
        EFZEFUGUOUHUPUIURUHACKZLIUKMUKACNZLZOUIBDKZLIULMULBDNZLZOPAUQBUSEACQBDQ
        SUNUTUJEIUKULMVCVFUAUBUCUDUJUMUHUIVAVDTUKULVBVETUEUF $.
        $( [28-Jul-1999] $)

      $( Imaginary part distributes over addition. $)
      imaddi $p |- ( Im ` ( A + B ) ) = ( ( Im ` A ) + ( Im ` B ) ) $=
        ( caddc co cim cfv cre ci recli recni ax-icn imcli mulcli add4i replimi
        cmul opreq12i readdcli adddii opreq2i 3eqtr4i fveq2i crimi eqtri ) ABEF
        ZGHAIHZBIHZEFZJAGHZBGHZEFZRFZEFZGHUMUGUOGUHJUKRFZEFZUIJULRFZEFZEFUJUPUR
        EFZEFUGUOUHUPUIURUHACKZLJUKMUKACNZLZOUIBDKZLJULMULBDNZLZOPAUQBUSEACQBDQ
        SUNUTUJEJUKULMVCVFUAUBUCUDUJUMUHUIVAVDTUKULVBVETUEUF $.
        $( [28-Jul-1999] $)

      $( Real part of a product. $)
      remuli $p |- ( Re ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Re ` B ) ) - ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( cmul co cre cfv cim cmin ci caddc replimi opreq12i recli recni crmuli
        imcli eqtri remulcli fveq2i resubcli readdcli crrei ) ABEFZGHAGHZBGHZEF
        ZAIHZBIHZEFZJFZKUFUJEFZUIUGEFZLFZEFLFZGHULUEUPGUEUFKUIEFLFZUGKUJEFLFZEF
        UPAUQBUREACMBDMNUFUIUGUJUFACOZPUIACRZPUGBDOZPUJBDRZPQSUAULUOUHUKUFUGUSV
        ATUIUJUTVBTUBUMUNUFUJUSVBTUIUGUTVATUCUDS $.
        $( [28-Jul-1999] $)

      $( Imaginary part of a product. $)
      immuli $p |- ( Im ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Im ` B ) ) + ( ( Im ` A ) x. ( Re ` B ) ) ) $=
        ( cmul co cim cfv cre cmin ci caddc replimi opreq12i recli recni crmuli
        imcli eqtri remulcli fveq2i resubcli readdcli crimi ) ABEFZGHAIHZBIHZEF
        ZAGHZBGHZEFZJFZKUFUJEFZUIUGEFZLFZEFLFZGHUOUEUPGUEUFKUIEFLFZUGKUJEFLFZEF
        UPAUQBUREACMBDMNUFUIUGUJUFACOZPUIACRZPUGBDOZPUJBDRZPQSUAULUOUHUKUFUGUSV
        ATUIUJUTVBTUBUMUNUFUJUSVBTUIUGUTVATUCUDS $.
        $( [28-Jul-1999] $)

      $( Complex conjugate distributes over addition.  Proposition 10-3.4(a) of
         [Gleason] p. 133. $)
      cjaddi $p |- ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) $=
        ( caddc co cre cfv ci cim cmul cmin ccj ax-icn recni cc wcel wceq remim
        ax-mp readdi imaddi opreq2i imcli adddii eqtri opreq12i mulcli addsub4i
        recli addcli 3eqtr4i ) ABEFZGHZIUMJHZKFZLFZAGHZIAJHZKFZLFZBGHZIBJHZKFZL
        FZEFZUMMHZAMHZBMHZEFUQURVBEFZUTVDEFZLFVFUNVJUPVKLABCDUAUPIUSVCEFZKFVKUO
        VLIKABCDUBUCIUSVCNUSACUDOZVCBDUDOZUEUFUGURVBUTVDURACUJOVBBDUJOIUSNVMUHI
        VCNVNUHUIUFUMPQVGUQRABCDUKUMSTVHVAVIVEEAPQVHVARCASTBPQVIVERDBSTUGUL $.
        $( [28-Jul-1999] $)

      $( Complex conjugate distributes over multiplication.  Proposition
         10-3.4(c) of [Gleason] p. 133. $)
      cjmuli $p |- ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) $=
        ( cmul co cre cfv cim cmin cneg caddc ccj recni mulcli opreq2i mulneg2i
        ci ax-icn opreq12i recli subcli addcli negsubi mul2negi eqcomi mulneg1i
        imcli negdii eqtr4i eqtr3i remuli immuli negcli crmuli 3eqtr4i cc remim
        wcel wceq ax-mp 3eqtr4ri ) ABEFZGHZRVCIHZEFZJFZAGHZRAIHZKZEFZLFZBGHZRBI
        HZKZEFZLFZEFZVCMHZAMHZBMHZEFVHVMEFZVIVNEFZJFZRVHVNEFZVIVMEFZLFZEFZJFZWB
        VJVOEFZJFZRVHVOEFZVJVMEFZLFZEFZLFZVGVRWDWHKZLFWIWPWDWHWBWCVHVMVHACUANZV
        MBDUANZOVIVNVIACUHNZVNBDUHNZOUBRWGSWEWFVHVNWRXAOZVIVMWTWSOZUCZOUDWDWKWQ
        WOLWCWJWBJWJWCVIVNWTXAUEUFPRWGKZEFWQWORWGSXDQXEWNREXEWEKZWFKZLFWNWEWFXB
        XCUIWLXFWMXGLVHVNWRXAQVIVMWTWSUGTUJPUKTUKVDWDVFWHJABCDULVEWGREABCDUMPTV
        HVJVMVOWRVIWTUNWSVNXAUNUOUPVCUQUSVSVGUTABCDOVCURVAVTVLWAVQEVHRVIEFZKZLF
        VHXHJFZVLVTVHXHWRRVISWTOUDVKXIVHLRVISWTQPAUQUSVTXJUTCAURVAVBVMRVNEFZKZL
        FVMXKJFZVQWAVMXKWSRVNSXAOUDVPXLVMLRVNSXAQPBUQUSWAXMUTDBURVAVBTUP $.
        $( [28-Jul-1999] $)

      $( Standard inner product on complex numbers. $)
      ipcni $p |- ( Re ` ( A x. ( * ` B ) ) ) =
             ( ( ( Re ` A ) x. ( Re ` B ) ) + ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( ccj cfv cmul cre cim cmin cneg caddc cjcli recli remulcli recni imcli
        co opreq2i wceq remuli negsubi recji imcji negcon2i mpbi eqtr2i 3eqtr2i
        mulneg2i opreq12i ) ABEFZGRHFAHFZUKHFZGRZAIFZUKIFZGRZJRUNUQKZLRULBHFZGR
        ZUOBIFZGRZLRAUKCBDMZUAUNUQUNULUMACNUKVCNOPUQUOUPACQZUKVCQZOPUBUNUTURVBL
        UMUSULGBDUCSVBUOUPKZGRURVAVFUOGUPVAKTVAVFTBDUDUPVAUPVEPZVABDQPUEUFSUOUP
        UOVDPVGUIUGUJUH $.
        $( [2-Oct-1999] $)
    $}

    $( A complex number times its conjugate is real. $)
    cjmulrcli $p |- ( A x. ( * ` A ) ) e. RR $=
      ( ccj cfv cmul co cr wcel wceq cjcji opreq2i cjcli cjmuli mulcomi 3eqtr4i
      mulcli cjrebi mpbir ) AACDZEFZGHTCDZTISSCDZEFSAEFUATUBASEABJKASBABLZMASBU
      CNOTASBUCPQR $.
      $( [11-May-1999] $)

    $( A complex number times its conjugate. $)
    cjmulvali $p |- ( A x. ( * ` A ) ) =
                   ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) $=
      ( cre cfv c2 cexp co cim caddc cmul ccj recli recni sqvali imcli opreq12i
      ipcni cr wcel wceq cjmulrcli rerebi mpbi 3eqtr2ri ) ACDZEFGZAHDZEFGZIGUEU
      EJGZUGUGJGZIGAAKDJGZCDZUKUFUIUHUJIUEUEABLMNUGUGABOMNPAABBQUKRSULUKTABUAZU
      KUKUMMUBUCUD $.
      $( [2-Oct-1999] $)

    $( A complex number times its conjugate is nonnegative. $)
    cjmulge0i $p |- 0 <_ ( A x. ( * ` A ) ) $=
      ( cc0 cre cfv c2 cexp co cim caddc ccj cle wbr recli sqge0i imcli resqcli
      cmul addge0i mp2an cjmulvali breqtrri ) CADEZFGHZAIEZFGHZJHZAAKERHLCUDLMC
      UFLMCUGLMUCABNZOUEABPZOUDUFUCUHQUEUIQSTABUAUB $.
      $( [28-May-1999] $)

    $( Real part of negative. $)
    renegi $p |- ( Re ` -u A ) = -u ( Re ` A ) $=
      ( cneg cre cfv ci cim cmul caddc recli recni ax-icn mulcli negdii replimi
      co imcli negeqi mulneg2i renegcli opreq2i 3eqtr4i fveq2i crrei eqtri ) AC
      ZDEADEZCZFAGEZCZHPZIPZDEUHUFULDUGFUIHPZIPZCUHUMCZIPUFULUGUMUGABJZKFUILUIA
      BQZKZMNAUNABORUKUOUHIFUILURSUAUBUCUHUJUGUPTUIUQTUDUE $.
      $( [24-Jul-2008] $) $( [2-Aug-1999] $)

    $( The negative of a real is real. $)
    negrebi $p |- ( -u A e. RR <-> A e. RR ) $=
      ( cneg wcel cre cfv negcli rerebi renegi eqeq1i recli recni neg11i 3bitri
      cr wceq eleq1 mpbii sylbi renegcl impbii ) ACZODZAODZUCAEFZAPZUDUCUBEFZUB
      PUECZUBPUFUBABGHUGUHUBABIJUEAUEABKZLBMNUFUEODUDUIUEAOQRSATUA $.
      $( [11-Aug-1999] $)

    $( Imaginary part of negative. $)
    imnegi $p |- ( Im ` -u A ) = -u ( Im ` A ) $=
      ( cneg cim cfv cre ci cmul caddc recli recni ax-icn mulcli negdii replimi
      co imcli negeqi mulneg2i renegcli opreq2i 3eqtr4i fveq2i crimi eqtri ) AC
      ZDEAFEZCZGADEZCZHPZIPZDEUJUFULDUGGUIHPZIPZCUHUMCZIPUFULUGUMUGABJZKGUILUIA
      BQZKZMNAUNABORUKUOUHIGUILURSUAUBUCUHUJUGUPTUIUQTUDUE $.
      $( [24-Jul-2008] $) $( [2-Aug-1999] $)

    $( Complex conjugate of negative. $)
    cjnegi $p |- ( * ` -u A ) = -u ( * ` A ) $=
      ( cneg cre cfv ci cim cmul co ccj caddc recni negcli ax-icn negsubi eqtri
      cmin negeqi cc wcel mulcli negdii renegi imnegi opreq2i mulneg2i opreq12i
      recli imcli 3eqtr4ri wceq remim ax-mp 3eqtr4i ) ACZDEZFUOGEZHIZQIZADEZFAG
      EZHIZQIZCZUOJEZAJEZCUSUTVBCZKIZCZVDUTCZVGCKIVJVGQIVIUSVJVGUTUTABUHLZMVBFV
      ANVAABUILZUAZMZOUTVGVKVNUBUPVJURVGQABUCURFVACZHIVGUQVOFHABUDUEFVANVLUFPUG
      UJVHVCUTVBVKVMORPUOSTVEUSUKABMUOULUMVFVCASTVFVCUKBAULUMRUN $.
      $( [2-Aug-1999] $)

    $( A number plus its conjugate is twice its real part.  Compare Proposition
       10-3.4(h) of [Gleason] p. 133. $)
    addcji $p |- ( A + ( * ` A ) ) = ( 2 x. ( Re ` A ) ) $=
      ( cre cfv ci cim cmul co caddc ccj c2 cc0 recli recni ax-icn imcli mulcli
      cneg negcli add4i negidi opreq2i addcli addid1i replimi cmin cc wcel wceq
      3eqtri remim ax-mp negsubi eqtr4i opreq12i 2timesi 3eqtr4i ) ACDZEAFDZGHZ
      IHZURUTRZIHZIHZURURIHZAAJDZIHKURGHVDVEUTVBIHZIHVELIHVEURUTURVBURABMNZEUSO
      USABPNQZVHUTVISTVGLVEIUTVIUAUBVEURURVHVHUCUDUJAVAVFVCIABUEVFURUTUFHZVCAUG
      UHVFVJUIBAUKULURUTVHVIUMUNUOURVHUPUQ $.
      $( [2-Oct-1999] $)
  $}

  $( A real number equals its real part.  One direction of Proposition
     10-3.4(f) of [Gleason] p. 133.  (Contributed by Paul Chapman,
     7-Sep-2007.) $)
  rere $p |- ( A e. RR -> ( Re ` A ) = A ) $=
    ( cr wcel cre cfv wceq cc wb recn rereb syl ibi ) ABCZADEAFZMAGCMNHAIAJKL
    $.
    $( [7-Sep-2007] $)

  $( A number is real iff it equals its complex conjugate.  Proposition
     10-3.4(f) of [Gleason] p. 133. $)
  cjreb $p |- ( A e. CC -> ( A e. RR <-> ( * ` A ) = A ) ) $=
    ( cc wcel cr ccj cfv wceq wb cc0 cif eleq1 fveq2 eqeq12d bibi12d 0cn elimel
    id cjrebi dedth ) ABCZADCZAEFZAGZHTAIJZDCZUDEFZUDGZHAIAUDGZUAUEUCUGAUDDKUHU
    BUFAUDAUDELUHQMNUDAIBOPRS $.
    $( [2-Jul-2005] $)

  $( A complex number times its conjugate is real. $)
  cjmulrcl $p |- ( A e. CC -> ( A x. ( * ` A ) ) e. RR ) $=
    ( cc wcel ccj cfv cmul co cr cc0 cif id fveq2 opreq12d eleq1d 0cn cjmulrcli
    wceq elimel dedth ) ABCZAADEZFGZHCTAIJZUCDEZFGZHCAIAUCQZUBUEHUFAUCUAUDFUFKA
    UCDLMNUCAIBORPS $.
    $( [26-Mar-2005] $)

  $( A complex number times its conjugate. $)
  cjmulval $p |- ( A e. CC -> ( A x. ( * ` A ) ) =
                 ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) ) $=
    ( cc wcel ccj cfv cmul co cre c2 cexp cim caddc wceq fveq2 opreq12d opreq1d
    cif id eqeq12d 2cn elimel cjmulvali dedth ) ABCZAADEZFGZAHEZIJGZAKEZIJGZLGZ
    MUDAIQZULDEZFGZULHEZIJGZULKEZIJGZLGZMAIAULMZUFUNUKUSUTAULUEUMFUTRAULDNOUTUH
    UPUJURLUTUGUOIJAULHNPUTUIUQIJAULKNPOSULAIBTUAUBUC $.
    $( [1-Feb-2007] $)

  $( A complex number times its conjugate is nonnegative. $)
  cjmulge0 $p |- ( A e. CC -> 0 <_ ( A x. ( * ` A ) ) ) $=
    ( cc wcel cc0 ccj cfv cmul co cle wbr cif wceq id fveq2 opreq12d breq2d 0cn
    elimel cjmulge0i dedth ) ABCZDAAEFZGHZIJDUAADKZUDEFZGHZIJADAUDLZUCUFDIUGAUD
    UBUEGUGMAUDENOPUDADBQRST $.
    $( [26-Mar-2005] $)

  $( Real part of negative. $)
  reneg $p |- ( A e. CC -> ( Re ` -u A ) = -u ( Re ` A ) ) $=
    ( cc wcel cneg cre cfv cc0 cif negeq fveq2d fveq2 negeqd eqeq12d 0cn elimel
    wceq renegi dedth ) ABCZADZEFZAEFZDZPSAGHZDZEFZUDEFZDZPAGAUDPZUAUFUCUHUITUE
    EAUDIJUIUBUGAUDEKLMUDAGBNOQR $.
    $( [17-Mar-2005] $)

  $( Real part distributes over addition. $)
  readd $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Re ` ( A + B ) ) = ( ( Re ` A ) + ( Re ` B ) ) ) $=
    ( cc wcel caddc co cre cfv wceq cc0 cif opreq1 fveq2d fveq2 opreq1d eqeq12d
    opreq2 opreq2d 0cn elimel readdi dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZEFZIUCAJ
    KZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEUKGAUJBEL
    MUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOAJCSTBJCST
    UAUB $.
    $( [17-Mar-2005] $)

  $( Real part distributes over subtraction. $)
  resub $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Re ` ( A - B ) ) = ( ( Re ` A ) - ( Re ` B ) ) ) $=
    ( cc wcel wa cneg caddc cre cfv cmin negcl readd sylan2 reneg adantl negsub
    co wceq recl recnd opreq2d eqtrd fveq2d syl2an 3eqtr3d ) ACDZBCDZEZABFZGQZH
    IZAHIZBHIZFZGQZABJQZHIULUMJQZUHUKULUIHIZGQZUOUGUFUICDUKUSRBKAUILMUHURUNULGU
    GURUNRUFBNOUAUBUHUJUPHABPUCUFULCDUMCDUOUQRUGUFULASTUGUMBSTULUMPUDUE $.
    $( [2-Aug-2006] $) $( [17-Mar-2005] $)

  $( The imaginary part of a negative number. $)
  imneg $p |- ( A e. CC -> ( Im ` -u A ) = -u ( Im ` A ) ) $=
    ( cc wcel cneg cim cfv cc0 cif negeq fveq2d fveq2 negeqd eqeq12d 0cn elimel
    wceq imnegi dedth ) ABCZADZEFZAEFZDZPSAGHZDZEFZUDEFZDZPAGAUDPZUAUFUCUHUITUE
    EAUDIJUIUBUGAUDEKLMUDAGBNOQR $.
    $( [18-Mar-2005] $)

  $( Imaginary part distributes over addition. $)
  imadd $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Im ` ( A + B ) ) = ( ( Im ` A ) + ( Im ` B ) ) ) $=
    ( cc wcel caddc co cim cfv wceq cc0 cif opreq1 fveq2d fveq2 opreq1d eqeq12d
    opreq2 opreq2d 0cn elimel imaddi dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZEFZIUCAJ
    KZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEUKGAUJBEL
    MUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOAJCSTBJCST
    UAUB $.
    $( [18-Mar-2005] $)

  $( Imaginary part distributes over subtraction. $)
  imsub $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Im ` ( A - B ) ) = ( ( Im ` A ) - ( Im ` B ) ) ) $=
    ( cc wcel wa cneg caddc cim cfv cmin negcl imadd sylan2 imneg adantl negsub
    co wceq imcl recnd opreq2d eqtrd fveq2d syl2an 3eqtr3d ) ACDZBCDZEZABFZGQZH
    IZAHIZBHIZFZGQZABJQZHIULUMJQZUHUKULUIHIZGQZUOUGUFUICDUKUSRBKAUILMUHURUNULGU
    GURUNRUFBNOUAUBUHUJUPHABPUCUFULCDUMCDUOUQRUGUFULASTUGUMBSTULUMPUDUE $.
    $( [1-Aug-2006] $) $( [18-Mar-2005] $)

  $( A real number equals its complex conjugate.  Proposition 10-3.4(f) of
     [Gleason] p. 133. $)
  cjre $p |- ( A e. RR -> ( * ` A ) = A ) $=
    ( cc wcel cr ccj cfv wceq recn cjreb biimpd mpcom ) ABCZADCZAEFAGZAHLMNAIJK
    $.
    $( [8-Oct-1999] $)

  $( The conjugate of the conjugate is the original complex number.
     Proposition 10-3.4(e) of [Gleason] p. 133. $)
  cjcj $p |- ( A e. CC -> ( * ` ( * ` A ) ) = A ) $=
    ( cc wcel ccj cfv wceq cc0 cif fveq2 fveq2d eqeq12d 0cn elimel cjcji dedth
    id ) ABCZADEZDEZAFQAGHZDEZDEZTFAGATFZSUBATUCRUADATDIJUCPKTAGBLMNO $.
    $( [29-Jul-1999] $)

  $( Complex conjugate distributes over addition.  Proposition 10-3.4(a) of
     [Gleason] p. 133. $)
  cjadd $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) ) $=
    ( cc wcel caddc co ccj cfv wceq cc0 cif opreq1 fveq2d fveq2 opreq1d eqeq12d
    opreq2 opreq2d 0cn elimel cjaddi dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZEFZIUCAJ
    KZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEUKGAUJBEL
    MUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOAJCSTBJCST
    UAUB $.
    $( [31-Jul-1999] $)

  $( Complex conjugate distributes over multiplication.  Proposition 10-3.4(c)
     of [Gleason] p. 133. $)
  cjmul $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) ) $=
    ( cc wcel cmul co ccj cfv wceq cc0 cif opreq1 fveq2d opreq1d eqeq12d opreq2
    fveq2 opreq2d 0cn elimel cjmuli dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZEFZIUCAJK
    ZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEUKGAUJBELM
    UTUGUMUHEAUJGQNOBUOIZULUQUNUSVAUKUPGBUOUJEPMVAUHURUMEBUOGQROUJUOAJCSTBJCSTU
    AUB $.
    $( [29-Jul-1999] $)

  $( Complex conjugate of negative. $)
  cjneg $p |- ( A e. CC -> ( * ` -u A ) = -u ( * ` A ) ) $=
    ( cc wcel cneg ccj cfv cc0 cif negeq fveq2d fveq2 negeqd eqeq12d 0cn elimel
    wceq cjnegi dedth ) ABCZADZEFZAEFZDZPSAGHZDZEFZUDEFZDZPAGAUDPZUAUFUCUHUITUE
    EAUDIJUIUBUGAUDEKLMUDAGBNOQR $.
    $( [27-Feb-2005] $)

  $( A number plus its conjugate is twice its real part.  Compare Proposition
     10-3.4(h) of [Gleason] p. 133. $)
  addcj $p |- ( A e. CC -> ( A + ( * ` A ) ) = ( 2 x. ( Re ` A ) ) ) $=
    ( cc wcel ccj cfv caddc co c2 cre cmul wceq cc0 id opreq12d opreq2d eqeq12d
    cif fveq2 0cn elimel addcji dedth ) ABCZAADEZFGZHAIEZJGZKUCALQZUHDEZFGZHUHI
    EZJGZKALAUHKZUEUJUGULUMAUHUDUIFUMMAUHDRNUMUFUKHJAUHIROPUHALBSTUAUB $.
    $( [21-Jan-2007] $)

  $( Complex conjugate distributes over subtraction. $)
  cjsub $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A - B ) ) = ( ( * ` A ) - ( * ` B ) ) ) $=
    ( cc wcel wa cneg caddc ccj cfv cmin negcl cjadd sylan2 negsub fveq2d cjneg
    co wceq adantl cjcl opreq2d syl2an eqtrd 3eqtr3d ) ACDZBCDZEZABFZGQZHIZAHIZ
    UHHIZGQZABJQZHIUKBHIZJQZUFUEUHCDUJUMRBKAUHLMUGUIUNHABNOUGUMUKUOFZGQZUPUGULU
    QUKGUFULUQRUEBPSUAUEUKCDUOCDURUPRUFATBTUKUONUBUCUD $.
    $( [1-Aug-2006] $) $( [28-Apr-2005] $)

  ${
    $d j k A $.  $d j k N $.
    $( Complex conjugate of natural number exponentiation. $)
    cjexp $p |- ( ( A e. CC /\ N e. NN0 ) ->
               ( * ` ( A ^ N ) ) = ( ( * ` A ) ^ N ) ) $=
      ( vj vk cn0 wcel cc cexp co ccj cfv wceq cc0 opreq2 fveq2d eqeq12d imbi2d
      wi c1 cmul cv caddc exp0 cjcl cr 1re ax-mp syl6eqr syl eqtr4d expp1 expcl
      cjre wa simpl cjmul syl11anc eqtrd adantr opreq1 sylan eqcomd exp31 com12
      sylan9eqr a2d nn0ind impcom ) BEFAGFZABHIZJKZAJKZBHIZLZVIACUAZHIZJKZVLVOH
      IZLZRVIAMHIZJKZVLMHIZLZRVIADUAZHIZJKZVLWDHIZLZRVIAWDSUBIZHIZJKZVLWIHIZLZR
      VIVNRCDBVOMLZVSWCVIWNVQWAVRWBWNVPVTJVOMAHNOVOMVLHNPQVOWDLZVSWHVIWOVQWFVRW
      GWOVPWEJVOWDAHNOVOWDVLHNPQVOWILZVSWMVIWPVQWKVRWLWPVPWJJVOWIAHNOVOWIVLHNPQ
      VOBLZVSVNVIWQVQVKVRVMWQVPVJJVOBAHNOVOBVLHNPQVIWASJKZWBVIVTSJAUCOVIVLGFZWB
      WRLAUDZWSWBSWRVLUCSUEFWRSLUFSUMUGUHUIUJWDEFZVIWHWMVIXAWHWMRVIXAWHWMVIXAUN
      ZWHUNWKWFVLTIZWLXBWKXCLWHXBWKWEATIZJKZXCXBWJXDJAWDUKOXBWEGFVIXEXCLAWDULVI
      XAUOWEAUPUQURUSWHXBXCWGVLTIZWLWFWGVLTUTXBWLXFVIWSXAWLXFLWTVLWDUKVAVBVEURV
      CVDVFVGVH $.
      $( [7-Jun-2006] $)
  $}

  $( The real part of a number in terms of complex conjugate. $)
  recj $p |- ( A e. CC -> ( Re ` A ) = ( ( A + ( * ` A ) ) / 2 ) ) $=
    ( cc wcel cre cfv caddc co c2 cdiv ccj wceq recnd cc0 wa syl11anc ci ax-icn
    recl opreq2d 3syl wne 2ne0 pm3.2i divdir mp3an3 cmul cmin cneg replim remim
    2cn opreq12d imcl mulcl sylancr negsub mpan negid negcl add4 syl22anc addcl
    cim jca addid1 3eqtr3d 3eqtr2rd opreq1d 2halves syl 3eqtr3rd ) ABCZADEZVMFG
    ZHIGZVMHIGZVPFGZAAJEZFGZHIGVMVLVMBCZVTVOVQKZVLVMARLZWBVTVTHBCZHMUAZNWAWCWDU
    KUBUCVMVMHUDUEOVLVNVSHIVLVSVMPAVCEZUFGZFGZVMWFUGGZFGWGVMWFUHZFGZFGZVNVLAWGV
    RWHFAUIAUJULVLWJWHWGFVLVTWFBCZWJWHKWBVLPBCZWEBCZWLQVLWEAUMLZPWEUNZUOZVMWFUP
    OSVLVNWFWIFGZFGZVNMFGZWKVNVLWRMVNFVLWNWLWRMKWOWMWNWLQWPUQZWFURTSVLVTVTWLWIB
    CZWSWKKWBWBWQVLWNWLXBWOXAWFUSTVMVMWFWIUTVAVLVTVTNVNBCWTVNKVLVTVTWBWBVDVMVMV
    BVNVETVFVGVHVLVTVQVMKWBVMVIVJVK $.
    $( [2-Aug-2006] $) $( [30-Apr-2005] $)

  $( The imaginary part of a number in terms of complex conjugate. $)
  imcj $p |- ( A e. CC -> ( Im ` A ) = ( ( A - ( * ` A ) ) / ( 2 x. _i ) ) ) $=
    ( cc wcel cim cfv c2 ci cmul co cdiv ccj cmin recnd 2cn ax-icn syl syl11anc
    wceq caddc opreq1d imcl cc0 wne 2ne0 ine0 mulne0i divcan4 mp3an23 cre mulcl
    mulcli recl sylancr addcl subsub syl111anc replim remim 2times mulcom mpan2
    opreq12d mulass mp3an12 eqtrd pncan2 3eqtr4d 3eqtr4rd eqtr3d ) ABCZADEZFGHI
    ZHIZVLJIZVKAAKEZLIZVLJIVJVKBCZVNVKRZVJVKAUAMZVQVLBCZVLUBUCVRFGNOUKZFGNOUDUE
    UFVKVLUGUHPVJVMVPVLJVJAUIEZGVKHIZSIZWBWCLIZLIZWDWBLIZWCSIZVPVMVJWDBCZWBBCZW
    CBCZWFWHRVJWJWKWIVJWBAULMZVJGBCZVQWKOVSGVKUJUMZWBWCUNQWLWNWDWBWCUOUPVJAWDVO
    WELAUQAURVBVJFWCHIZWCWCSIZVMWHVJWKWOWPRWNWCUSPVJVQVMWORVSVQVMVLVKHIZWOVQVTV
    MWQRWAVKVLUTVAFBCWMVQWQWORNOFGVKVCVDVEPVJWGWCWCSVJWJWKWGWCRWLWNWBWCVFQTVGVH
    TVI $.
    $( [30-Apr-2005] $)

  $( The real part of zero. $)
  re0 $p |- ( Re ` 0 ) = 0 $=
    ( cc0 cr wcel cre cfv wceq 0re 0cn rerebi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The imaginary part of zero. $)
  im0 $p |- ( Im ` 0 ) = 0 $=
    ( cc0 cr wcel cim cfv wceq 0re 0cn reim0bi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The real part of one.  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  re1 $p |- ( Re ` 1 ) = 1 $=
    ( c1 cr wcel cre cfv wceq 1re ax-1cn rerebi mpbi ) ABCADEAFGAHIJ $.
    $( [9-Jun-2006] $)

  $( The imaginary part of one.  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  im1 $p |- ( Im ` 1 ) = 0 $=
    ( c1 cr wcel cim cfv cc0 wceq 1re ax-1cn reim0bi mpbi ) ABCADEFGHAIJK $.
    $( [9-Jun-2006] $)

  $( The real part of ` _i ` .  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  rei $p |- ( Re ` _i ) = 0 $=
    ( cc0 ci c1 cmul co caddc cre cfv ax-icn ax-1cn mulcli addid2i fveq2i crrei
    0re 1re mulid1i 3eqtr3ri ) ABCDEZFEZGHSGHABGHTSGSBCIJKLMACOPNSBGBIQMR $.
    $( [15-Mar-2008] $) $( [9-Jun-2006] $)

  $( The imaginary part of ` _i ` .  (Contributed by Scott Fenton,
     9-Jun-2006.) $)
  imi $p |- ( Im ` _i ) = 1 $=
    ( ci c1 cmul cim cfv cc0 ax-icn ax-1cn mulcli addid2i eqcomi fveq2i mulid1i
    co caddc 0re 1re crimi 3eqtr3i ) ABCNZDEFTONZDEADEBTUADUATTABGHIJKLTADAGMLF
    BPQRS $.
    $( [15-Mar-2008] $) $( [9-Jun-2006] $)

  $( The conjugate of zero. $)
  cj0 $p |- ( * ` 0 ) = 0 $=
    ( cc0 cr wcel ccj cfv wceq 0re 0cn cjrebi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The complex conjugate of the imaginary unit. $)
  cji $p |- ( * ` _i ) = -u _i $=
    ( ci cre cfv cim cmul co cmin cc0 ccj cneg rei c1 imi opreq2i mulid1i eqtri
    ax-icn opreq12i cc wcel wceq remim ax-mp df-neg 3eqtr4i ) ABCZAADCZEFZGFZHA
    GFAICZAJUFHUHAGKUHALEFAUGLAEMNAQOPRASTUJUIUAQAUBUCAUDUE $.
    $( [26-Mar-2005] $)

  $( The conjugate of a representation of a complex number in terms of real and
     imaginary parts. $)
  cjreim $p |- ( ( A e. RR /\ B e. RR ) -> ( * ` ( A + ( _i x. B ) ) ) =
                ( A - ( _i x. B ) ) ) $=
    ( cr wcel wa ci cmul co caddc ccj cfv cneg cc wceq recn ax-icn sylancr cjre
    syl2an 3eqtrd cmin mulcl cjadd cjmul cji opreq12d mulneg1 opreqan12d negsub
    a1i ) ACDZBCDZEAFBGHZIHJKZAJKZUMJKZIHZAUMLZIHZAUMUAHZUKAMDZUMMDZUNUQNULAOZU
    LFMDZBMDZVBPBOZFBUBQZAUMUCSUKULUOAUPURIARULUPFJKZBJKZGHZFLZBGHZURULVDVEUPVJ
    NPVFFBUDQULVHVKVIBGVHVKNULUEUJBRUFULVDVEVLURNPVFFBUGQTUHUKVAVBUSUTNULVCVGAU
    MUIST $.
    $( [1-Aug-2006] $) $( [1-Jul-2005] $)

  $( The conjugate of the representation of a complex number in terms of real
     and imaginary parts. $)
  cjreim2 $p |- ( ( A e. RR /\ B e. RR ) -> ( * ` ( A - ( _i x. B ) ) ) =
                ( A + ( _i x. B ) ) ) $=
    ( cr wcel wa ci cmul cmin ccj cfv cneg wceq recn ax-icn sylancr syl2an cjre
    co cc 3eqtrd caddc mulcl cjsub cjmul cji opreq12d mulneg1 opreqan12d subneg
    a1i ) ACDZBCDZEAFBGRZHRIJZAIJZUMIJZHRZAUMKZHRZAUMUARZUKASDZUMSDZUNUQLULAMZU
    LFSDZBSDZVBNBMZFBUBOZAUMUCPUKULUOAUPURHAQULUPFIJZBIJZGRZFKZBGRZURULVDVEUPVJ
    LNVFFBUDOULVHVKVIBGVHVKLULUEUJBQUFULVDVEVLURLNVFFBUGOTUHUKVAVBUSUTLULVCVGAU
    MUIPT $.
    $( [1-Jul-2005] $)

  $( Complex conjugate is a one-to-one function.  (Proof shortened by Eric
     Schmidt, 2-Jul-2009.  Previous version is ~ cj11OLD .) $)
  cj11 $p |- ( ( A e. CC /\ B e. CC ) ->
              ( ( * ` A ) = ( * ` B ) <-> A = B ) ) $=
    ( cc wcel wa ccj cfv wceq fveq2 cjcj eqeqan12d syl5ib impbid1 ) ACDZBCDZEZA
    FGZBFGZHZABHZSQFGZRFGZHPTQRFINOUAAUBBAJBJKLABFIM $.
    $( [2-Jul-2009] $) $( [29-Apr-2005] $)

  $( Complex conjugate is a one-to-one function. $)
  cj11OLD $p |- ( ( A e. CC /\ B e. CC ) ->
              ( ( * ` A ) = ( * ` B ) <-> A = B ) ) $=
    ( cc wcel wa cre cfv ci cneg cmul co caddc wceq wb recnd syl2an jca sylancr
    cr ax-icn cim ccj imcl neg11 anbi2d recl renegcl syl cru 3bitr4d cmin mulcl
    negsub syl11anc mulneg2 opreq2d remim 3eqtr4rd eqeqan12d replim ) ACDZBCDZE
    ZAFGZHAUAGZIZJKZLKZBFGZHBUAGZIZJKZLKZMZVDHVEJKZLKZVIHVJJKZLKZMZAUBGZBUBGZMA
    BMVCVDVIMZVFVKMZEZWBVEVJMZEZVNVSVCWCWEWBVAVECDZVJCDZWCWENVBVAVEAUCZOZVBVJBU
    CZOZVEVJUDPUEVAVDSDZVFSDZEVISDZVKSDZEVNWDNVBVAWMWNAUFZVAVESDZWNWIVEUGUHQVBW
    OWPBUFZVBVJSDZWPWKVJUGUHQVDVFVIVKUIPVAWMWREWOWTEVSWFNVBVAWMWRWQWIQVBWOWTWSW
    KQVDVEVIVJUIPUJVAVBVTVHWAVMVAVDVOIZLKZVDVOUKKZVHVTVAVDCDVOCDZXBXCMVAVDWQOVA
    HCDZWGXDTWJHVEULRVDVOUMUNVAVGXAVDLVAXEWGVGXAMTWJHVEUORUPAUQURVBVIVQIZLKZVIV
    QUKKZVMWAVBVICDVQCDZXGXHMVBVIWSOVBXEWHXITWLHVJULRVIVQUMUNVBVLXFVILVBXEWHVLX
    FMTWLHVJUORUPBUQURUSVAVBAVPBVRAUTBUTUSUJ $.
    $( [1-Aug-2006] $) $( [29-Apr-2005] $)

  $( A number is nonzero iff its complex conjugate is nonzero. $)
  cjne0 $p |- ( A e. CC -> ( A =/= 0 <-> ( * ` A ) =/= 0 ) ) $=
    ( cc wcel cc0 ccj cfv wceq cj0 eqeq2i wb 0cn cj11 mpan2 syl5rbbr necon3bid
    ) ABCZADAEFZDQDGQDEFZGZPADGZRDQHIPDBCSTJKADLMNO $.
    $( [29-Apr-2005] $)

  ${
    cjdiv.1 $e |- A e. CC $.
    cjdiv.2 $e |- B e. CC $.
    $( Complex conjugate distributes over division. $)
    cjdivi $p |- ( B =/= 0 ->
                    ( * ` ( A / B ) ) = ( ( * ` A ) / ( * ` B ) ) ) $=
      ( cc0 wne ccj cfv cdiv co wceq cmul cc divclzi cjmul sylancr divcan2zi wb
      wcel cjcli fveq2d eqtr3d cjcl syl 0cn cj11 mp2an cj0 eqeq2i bitr3i biimpi
      necon3bii wa divmul mp3an1 mpanr1 syl11anc mpbird eqcomd ) BEFZAGHZBGHZIJ
      ZABIJZGHZUTVCVEKZVBVELJZVAKZUTBVDLJZGHZVGVAUTBMSZVDMSZVJVGKDABCDNZBVDOPUT
      VIAGABCDQUAUBUTVEMSZVBEFZVFVHRZUTVLVNVMVDUCUDUTVOBEVBEBEKZVBEGHZKZVBEKVKE
      MSVSVQRDUEBEUFUGVREVBUHUIUJULUKVNVBMSZVOVPBDTVAMSVNVTVOUMVPACTVAVEVBUNUOU
      PUQURUS $.
      $( [6-Sep-2013] $) $( [29-Apr-2005] $)
  $}

  $( Complex conjugate distributes over division. $)
  cjdiv $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                   ( * ` ( A / B ) ) = ( ( * ` A ) / ( * ` B ) ) ) $=
    ( cc wcel cc0 wne cdiv co ccj cfv wceq wi cif opreq1 fveq2d opreq1d eqeq12d
    fveq2 0cn elimel imbi2d neeq1 opreq2 opreq2d imbi12d cjdivi dedth2h 3impia
    ) ACDZBCDZBEFZABGHZIJZAIJZBIJZGHZKZUIUJUKUQLUKUIAEMZBGHZIJZURIJZUOGHZKZLUJB
    EMZEFZURVDGHZIJZVAVDIJZGHZKZLABEEAURKZUQVCUKVKUMUTUPVBVKULUSIAURBGNOVKUNVAU
    OGAURIRPQUABVDKZUKVEVCVJBVDEUBVLUTVGVBVIVLUSVFIBVDURGUCOVLUOVHVAGBVDIRUDQUE
    URVDAECSTBECSTUFUGUH $.
    $( [3-Feb-2006] $) $( [29-Apr-2005] $)

