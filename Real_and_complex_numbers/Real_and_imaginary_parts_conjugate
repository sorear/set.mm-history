$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Real and imaginary parts; conjugate
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Re $. $( Real part of a complex number. $)
  $c Im $. $( Imaginary part of a complex number. $)
  $c * $. $( Complex conjugate function. $)

  $( Extend class notation to include real part of a complex number. $)
  cre $a class Re $.

  $( Extend class notation to include imaginary part of a complex number. $)
  cim $a class Im $.

  $( Extend class notation to include complex conjugate function. $)
  ccj $a class * $.

  ${
    $d x y $.
    $( Define the complex conjugate function.  See ~ cjcli for its closure and
       ~ cjval for its value. $)
    df-cj $a |- * = ( x e. CC |-> ( iota_ y e. CC
                 ( ( x + y ) e. RR /\ ( _i x. ( x - y ) ) e. RR ) ) ) $.

    $( Define a function whose value is the real part of a complex number.  See
       ~ reval for its value, ~ recli for its closure, and ~ replim for its use
       in decomposing a complex number. $)
    df-re $a |- Re = ( x e. CC |-> ( ( x + ( * ` x ) ) / 2 ) ) $.

    $( Define a function whose value is the imaginary part of a complex
       number.  See ~ imval for its value, ~ imcli for its closure, and
       ~ replim for its use in decomposing a complex number. $)
    df-im $a |- Im = ( x e. CC |-> ( Re ` ( x / _i ) ) ) $.
  $}

  ${
    $d x y A $.
    $( The value of the conjugate of a complex number. $)
    cjval $p |- ( A e. CC -> ( * ` A ) = ( iota_ x e. CC
          ( ( A + x ) e. RR /\ ( _i x. ( A - x ) ) e. RR ) ) ) $=
      ( vy cv caddc co cr wcel ci cmin cmul wa cc crio ccj wceq opreq1 eleq1d
      opreq2d anbi12d riotabidv df-cj riotaex fvmpt ) CBCDZADZEFZGHZIUEUFJFZKFZ
      GHZLZAMNBUFEFZGHZIBUFJFZKFZGHZLZAMNMOUEBPZULURAMUSUHUNUKUQUSUGUMGUEBUFEQR
      USUJUPGUSUIUOIKUEBUFJQSRTUACAUBURAMUCUD $.
      $( [6-Nov-2013] $)

    $( The defining property of the complex conjugate. $)
    cjth $p |- ( A e. CC ->
          ( ( A + ( * ` A ) ) e. RR /\ ( _i x. ( A - ( * ` A ) ) ) e. RR ) ) $=
      ( vx cc wcel cv caddc co cr ci cmin cmul wa ccj cfv wsbc crio wreu cju
      riota4 syl wceq wb cjval dfsbcq mpbird fvex opreq2 eleq1d opreq2d
      anbi12d sbcie sylib ) ACDZABEZFGZHDZIAUNJGZKGZHDZLZBAMNZOZAVAFGZHDZIAVAJG
      ZKGZHDZLZUMVBUTBUTBCPZOZUMUTBCQVJBARUTBCSTUMVAVIUAVBVJUBBAUCUTBVAVIUDTUEU
      TVHBVAAMUFUNVAUAZUPVDUSVGVKUOVCHUNVAAFUGUHVKURVFHVKUQVEIKUNVAAJUGUIUHUJUK
      UL $.
      $( [6-Nov-2013] $)

    $( Domain and codomain of the conjugate function. $)
    cjf $p |- * : CC --> CC $=
      ( vx vy cv caddc co cr wcel ci cmin cmul wa cc crio wral ccj wf wreu cju
      riotacl syl rgen df-cj fmpt mpbi ) ACZBCZDEFGHUEUFIEJEFGKZBLMZLGZALNLLOPU
      IALUELGUGBLQUIBUERUGBLSTUAALLUHOABUBUCUD $.
      $( [6-Nov-2013] $)

    $( The conjugate of a complex number is a complex number (closure law). $)
    cjcl $p |- ( A e. CC -> ( * ` A ) e. CC ) $=
      ( cc ccj cjf ffvelrni ) BBACDE $.
      $( [6-Nov-2013] $) $( [10-May-1999] $)

    $( The value of the real part of a complex number. $)
    reval $p |- ( A e. CC -> ( Re ` A ) = ( ( A + ( * ` A ) ) / 2 ) ) $=
      ( vx cv ccj cfv caddc co c2 cdiv cc cre wceq fveq2 opreq12 mpdan opreq1d
      df-re oprex fvmpt ) BABCZTDEZFGZHIGAADEZFGZHIGJKTALZUBUDHIUEUAUCLUBUDLTAD
      MTAUAUCFNOPBQUDHIRS $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( The value of the imaginary part of a complex number. $)
    imval $p |- ( A e. CC -> ( Im ` A ) = ( Re ` ( A / _i ) ) ) $=
      ( vx cv ci cdiv co cre cfv cc cim wceq opreq1 fveq2d df-im fvex fvmpt )
      BABCZDEFZGHADEFZGHIJQAKRSGQADELMBNSGOP $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( The imaginary part of a complex number in terms of the real part
       function. $)
    imre $p |- ( A e. CC -> ( Im ` A ) = ( Re ` ( -u _i x. A ) ) ) $=
      ( cc wcel cim cfv ci cdiv co cre cneg cmul imval c1 cc0 wne wceq ax-icn
      ine0 divrec2 mp3an23 irec opreq1i syl6eq fveq2d eqtrd ) ABCZADEAFGHZIEFJZ
      AKHZIEALUFUGUIIUFUGMFGHZAKHZUIUFFBCFNOUGUKPQRAFSTUJUHAKUAUBUCUDUE $.
      $( [6-Nov-2013] $) $( [12-May-2005] $)

    $( The real part of a complex number is real. $)
    recl $p |- ( A e. CC -> ( Re ` A ) e. RR ) $=
      ( cc wcel cre cfv ccj caddc co c2 cdiv cr reval ci cmin cmul cjth simpld
      rehalfcl syl eqeltrd ) ABCZADEAAFEZGHZIJHZKALUAUCKCZUDKCUAUEMAUBNHOHKCAPQ
      UCRST $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( The imaginary part of a complex number is real. $)
    imcl $p |- ( A e. CC -> ( Im ` A ) e. RR ) $=
      ( cc wcel cim cfv ci cneg cmul co cre cr imre ax-icn negcli ax-mulcl
      mpan recl syl eqeltrd ) ABCZADEFGZAHIZJEZKALTUBBCZUCKCUABCTUDFMNUAAOPUBQR
      S $.
      $( [6-Nov-2013] $) $( [9-May-1999] $)

    $( Domain and codomain of the real part function.  (Contributed by Paul
       Chapman, 22-Oct-2007.) $)
    ref $p |- Re : CC --> RR $=
      ( vx cv ccj cfv caddc co c2 cdiv cr wcel cc wral cre wf reval recl
      eqeltrrd rgen df-re fmpt mpbi ) ABZUBCDEFGHFZIJZAKLKIMNUDAKUBKJUBMDUCIUBO
      UBPQRAKIUCMASTUA $.
      $( [6-Nov-2013] $) $( [22-Oct-2007] $)

    $( Domain and codomain of the imaginary part function.  (Contributed by
       Paul Chapman, 22-Oct-2007.) $)
    imf $p |- Im : CC --> RR $=
      ( vx cv ci cdiv co cre cfv cr wcel cc wral cim wf imval imcl eqeltrrd
      rgen df-im fmpt mpbi ) ABZCDEFGZHIZAJKJHLMUCAJUAJIUALGUBHUANUAOPQAJHUBLAR
      ST $.
      $( [6-Nov-2013] $) $( [22-Oct-2007] $)

    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crre $p |- ( ( A e. RR /\ B e. RR ) -> ( Re ` ( A + ( _i x. B ) ) ) = A )
        $=
      ( cr wcel wa ci cmul co caddc cre cfv ccj c2 cdiv cc wceq recn ax-icn
      mulcl sylancr addcl syl2an reval syl cmin cc0 recl eqeltrrd simpl
      resubcl syl11anc a1i adantl cjcl subcl halfcl subdi syl111anc adantr
      ax-mulcl pnpcan pnpcan2 eqtr4d opreq1d ax-addcl addsub subsub 3eqtr4d
      2times opreq2d wne 2cn 2ne0 divsubdir syl112anc 3eqtr3d divcan3
      ax-mulass divass opreq12d c1 cneg ixi 1re renegcli eqeltri simpr
      ax-mulrcl cjth simprd rehalfcl 3syl eqeltrd rimul wb subeq0 mpbid eqtrd
      ) ACDZBCDZEZAFBGHZIHZJKZXCXCLKZIHZMNHZAXAXCODZXDXGPWSAODZXBODZXHWTAQZWTFO
      DZBODZXJRBQZFBSZTAXBUAUBZXCUCUDZXAXGAUEHZUFPZXGAPZXAXRCDZFXRGHZCDXSXAXGCD
      WSYAXAXDXGCXQXAXHXDCDXPXCUGUDUHWSWTUIXGAUJUKXAYBFFGHZBGHZFXCXEUEHZGHZMNHZ
      UEHZCXAFXBYEMNHZUEHZGHZFXBGHZFYIGHZUEHZYBYHXAXLXJYIODZYKYNPXLXARULZXAXLXM
      XJYPWTXMWSXNUMZXOUKXAYEODZYOXAXHXEODZYRXPXAXHYSXPXCUNUDZXCXEUOUKZYEUPUDFX
      BYIUQURXAXRYJFGXAXGMAGHZMNHZUEHZMXBGHZMNHZYIUEHZXRYJXAXFUUBUEHZMNHZUUEYEU
      EHZMNHZUUDUUGXAUUHUUJMNXAXFAAIHZUEHZXBXBIHZYEUEHZUUHUUJXAXCUULUEHZXEIHZUU
      NXCUEHZXEIHZUUMUUOXAUUPUURXEIXAUUPXBAUEHZUURXAXIXJXIUUPUUTPWSXIWTXKUSZXAX
      LXMXJRYQFBUTTZUVAAXBAVAURXAXJXIXJUURUUTPUVBUVAUVBXBAXBVBURVCVDXAXHYSUULOD
      ZUUMUUQPXPYTXAXIXIUVCUVAUVAAAVEUKXCXEUULVFURXAUUNODZXHYSUUOUUSPXAXJXJUVDU
      VBUVBXBXBVEUKXPYTUUNXCXEVGURVHXAUUBUULXFUEXAXIUUBUULPUVAAVIUDVJXAUUEUUNYE
      UEXAXJUUEUUNPUVBXBVIUDVDVHVDXAXFODZUUBODZMODZMUFVKZUUIUUDPXAXHYSUVEXPYTXC
      XEVEUKZXAUVGXIUVFVLUVAMAUTTUVGXAVLULZUVHXAVMULZXFUUBMVNVOXAUUEODZYRUVGUVH
      UUKUUGPXAUVGXJUVLVLUVBMXBUTTUUAUVJUVKUUEYEMVNVOVPXAUUCAXGUEXAXIUVGUVHUUCA
      PUVAUVJUVKAMVQURVJXAUUFXBYIUEXAXJUVGUVHUUFXBPUVBUVJUVKXBMVQURVDVPVJXAYDYL
      YGYMUEXAXLXLXMYDYLPYPYPYQFFBVRURXAXLYRUVGUVHYGYMPYPUUAUVJUVKFYEMVSVOVTVHX
      AYDCDZYGCDZYHCDXAYCCDWTUVMYCWAWBCWCWAWDWEWFWSWTWGYCBWHTXAXHYFCDZUVNXPXHXF
      CDUVOXCWIWJYFWKWLYDYGUJUKWMXRWNUKXAXGODZXIXSXTWOXAUVEUVPUVIXFUPUDUVAXGAWP
      UKWQWR $.
      $( [7-Nov-2013] $) $( [12-May-2005] $)

    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crim $p |- ( ( A e. RR /\ B e. RR ) -> ( Im ` ( A + ( _i x. B ) ) ) = B )
        $=
      ( cr wcel wa ci cmul co caddc cim cfv cdiv cre cneg cc wceq recn ax-icn
      ax-mulcl sylancr ax-addcl syl2an imval syl mpan cc0 wne ine0 divdir
      3expa mpanr12 sylan2 c1 divrec2 mp3an23 irec opreq1i a1i mulneg12 3eqtrd
      divcan3 opreq12 negcl addcom sylan 3eqtrrd fveq2d id renegcl crre
      syl2anc 3eqtr2d ) ACDZBCDZEZAFBGHZIHZJKZVQFLHZMKZBFANZGHZIHZMKZBVOVQODZVR
      VTPVMAODZVPODZWEVNAQZVNFODZBODZWGRBQZFBSZTAVPUAUBVQUCUDVOWCVSMVMWFWJWCVSP
      VNWHWKWFWJEVSAFLHZVPFLHZIHZWBBIHZWCWJWFWGVSWOPZWIWJWGRWLUEWFWGEWIFUFUGZWQ
      RUHWFWGWIWREWQAVPFUIUJUKULWFWMWBPWNBPZWOWPPWJWFWMUMFLHZAGHZFNZAGHZWBWFWIW
      RWMXAPRUHAFUNUOXAXCPWFWTXBAGUPUQURWIWFXCWBPRFAUSUEUTWJWIWRWSRUHBFVAUOWMWB
      WNBIVBUBWFWBODZWJWPWCPWFWIWAODXDRAVCFWASTWBBVDVEVFUBVGVNVNWACDWDBPVMVNVHA
      VIBWAVJVKVL $.
      $( [7-Nov-2013] $) $( [12-May-2005] $)

    $( Reconstruct a complex number from its real and imaginary parts. $)
    replim $p |- ( A e. CC -> A = ( ( Re ` A ) + ( _i x. ( Im ` A ) ) ) ) $=
      ( vx vy cc wcel cv ci cmul co caddc wceq cr wrex cre cfv cim ax-cnre wa
      crre crim opreq2d opreq12d eqcomd id fveq2 eqeq12d syl5ibrcom r19.23aivv
      syl ) ADEABFZGCFZHIZJIZKZCLMBLMAANOZGAPOZHIZJIZKZBCAQUNUSBCLLUJLEUKLERZUS
      UNUMUMNOZGUMPOZHIZJIZKUTVDUMUTVAUJVCULJUJUKSUTVBUKGHUJUKTUAUBUCUNAUMURVDU
      NUDUNUOVAUQVCJAUMNUEUNUPVBGHAUMPUEUAUBUFUGUHUI $.
      $( [7-Nov-2013] $) $( [10-May-1999] $)

    $( Value of the conjugate of a complex number.  The value is the real part
       minus ` _i ` times the imaginary part.  Definition 10-3.2 of [Gleason]
       p. 132. $)
    remim $p |- ( A e. CC ->
                      ( * ` A ) = ( ( Re ` A ) - ( _i x. ( Im ` A ) ) ) ) $=
      ( vx vy cc wcel ccj cfv cv caddc co cr ci cmin cmul wa crio cre cim
      cjval wceq replim opreq1d recl recnd ax-icn imcl ax-mulcl sylancr ppncan
      syl111anc eqtrd ax-addrcl syl11anc eqeltrd pnncan a1i ax-distr 3eqtr4d
      opreq2d ax-mulass mp3an12 syl eqcomd c1 cneg ixi 1re renegcli eqeltri
      ax-mulrcl jca wreu wb subcl cju ax-17 a17d opreq2 eleq1d anbi12d riota2f
      mpbid ) ADEZAFGABHZIJZKEZLAWDMJZNJZKEZOZBDPZAQGZLARGZNJZMJZBASWCAWOIJZKEZ
      LAWOMJZNJZKEZOZWKWOTZWCWQWTWCWPWLWLIJZKWCWPWLWNIJZWOIJZXCWCAXDWOIAUAZUBWC
      WLDEZWNDEZXGXEXCTWCWLAUCZUDZWCLDEZWMDEZXHUEWCWMAUFZUDZLWMUGUHZXJWLWNWLUIU
      JUKWCWLKEZXPXCKEXIXIWLWLULUMUNWCWSLLNJZWMWMIJZNJZKWCWSLLXRNJZNJZXSWCWRXTL
      NWCXDWOMJZWNWNIJZWRXTWCXGXHXHYBYCTXJXOXOWLWNWNUOUJWCAXDWOMXFUBWCXKXLXLXTY
      CTXKWCUEUPXNXNLWMWMUQUJURUSWCXSYAWCXRDEZXSYATZWCXRWCWMKEZYFXRKEZXMXMWMWMU
      LUMZUDXKXKYDYEUEUELLXRUTVAVBVCUKWCXQKEYGXSKEXQVDVEKVFVDVGVHVIYHXQXRVJUHUN
      VKWCWODEZWJBDVLXAXBVMWCXGXHYIXJXOWLWNVNUMBAVOWJXABCDWOCHWOEBVPYIXABVQWDWO
      TZWFWQWIWTYJWEWPKWDWOAIVRVSYJWHWSKYJWGWRLNWDWOAMVRUSVSVTWAUMWBUK $.
      $( [7-Nov-2013] $) $( [10-May-1999] $)
  $}

  ${
    recl.1 $e |- A e. CC $.
    $( The real part of a complex number is real (closure law). $)
    recli $p |- ( Re ` A ) e. RR $=
      ( cc wcel cre cfv cr recl ax-mp ) ACDAEFGDBAHI $.
      $( [11-May-1999] $)

    $( The imaginary part of a complex number is real (closure law). $)
    imcli $p |- ( Im ` A ) e. RR $=
      ( cc wcel cim cfv cr imcl ax-mp ) ACDAEFGDBAHI $.
      $( [11-May-1999] $)

    $( Closure law for complex conjugate. $)
    cjcli $p |- ( * ` A ) e. CC $=
      ( cc wcel ccj cfv cjcl ax-mp ) ACDAEFCDBAGH $.
      $( [11-May-1999] $)

    $( Construct a complex number from its real and imaginary parts. $)
    replimi $p |- A = ( ( Re ` A ) + ( _i x. ( Im ` A ) ) ) $=
      ( cc wcel cre cfv ci cim cmul co caddc wceq replim ax-mp ) ACDAAEFGAHFIJK
      JLBAMN $.
      $( [11-Mar-2008] $) $( [1-Oct-1999] $)
  $}

  ${
    crre.1 $e |- A e. RR $.
    crre.2 $e |- B e. RR $.
    $( The real part of a complex number representation.  Definition 10-3.1 of
       [Gleason] p. 132. $)
    crrei $p |- ( Re ` ( A + ( _i x. B ) ) ) = A $=
      ( cr wcel ci cmul co caddc cre cfv wceq crre mp2an ) AEFBEFAGBHIJIKLAMCDA
      BNO $.
      $( [11-Mar-2005] $) $( [10-May-1999] $)

    $( The imaginary part of a complex number representation.  Definition
       10-3.1 of [Gleason] p. 132. $)
    crimi $p |- ( Im ` ( A + ( _i x. B ) ) ) = B $=
      ( cr wcel ci cmul co caddc cim cfv wceq crim mp2an ) AEFBEFAGBHIJIKLBMCDA
      BNO $.
      $( [11-Mar-2005] $) $( [10-May-1999] $)
  $}

  $( The imaginary part of a real number is 0. $)
  reim0 $p |- ( A e. RR -> ( Im ` A ) = 0 ) $=
    ( cr wcel ci cc0 cmul co caddc cim cfv cc wceq recn ax-icn mul01i opreq2i
    addid1 syl5eq syl fveq2d 0re crim mpan2 eqtr3d ) ABCZADEFGZHGZIJZAIJEUEUGAI
    UEAKCZUGALAMUIUGAEHGAUFEAHDNOPAQRSTUEEBCUHELUAAEUBUCUD $.
    $( [7-Nov-2013] $) $( [18-Mar-2005] $)

  $( A number is real iff its imaginary part is 0. $)
  reim0b $p |- ( A e. CC -> ( A e. RR <-> ( Im ` A ) = 0 ) ) $=
    ( cc wcel cr cim cfv cc0 wceq reim0 wa cre ci cmul co caddc replim adantr
    opreq2 ax-icn mul01i syl6eq opreq2d recl recnd addid1 syl sylan9eqr eqtrd
    eqeltrd ex impbid2 ) ABCZADCZAEFZGHZAIULUOUMULUOJZAAKFZDUPAUQLUNMNZONZUQULA
    USHUOAPQUOULUSUQGONZUQUOURGUQOUOURLGMNGUNGLMRLSTUAUBULUQBCUTUQHULUQAUCZUDUQ
    UEUFUGUHULUQDCUOVAQUIUJUK $.
    $( [24-Jul-2008] $) $( [26-Sep-2005] $)

  $( A number is real iff it equals its real part.  Proposition 10-3.4(f) of
     [Gleason] p. 133. $)
  rereb $p |- ( A e. CC -> ( A e. RR <-> ( Re ` A ) = A ) ) $=
    ( cc wcel cr cre cfv wceq wa ci cim cmul co caddc cc0 replim adantr reim0
    opreq2d ax-icn mul01i syl6eq adantl recl recnd addid1 syl 3eqtrrd simpr
    eqeltrrd impbida ) ABCZADCZAEFZAGZUKULHZAUMIAJFZKLZMLZUMNMLZUMUKAURGULAOPUO
    UQNUMMULUQNGUKULUQINKLNULUPNIKAQRISTUAUBRUKUSUMGZULUKUMBCUTUKUMAUCZUDUMUEUF
    PUGUKUNHUMADUKUNUHUKUMDCUNVAPUIUJ $.
    $( [20-Aug-2008] $)

  $( A product with a nonzero real multiplier is real iff the multiplicand is
     real. $)
  mulre $p |- ( ( A e. CC /\ B e. RR /\ B =/= 0 )
         -> ( A e. RR <-> ( B x. A ) e. RR ) ) $=
    ( cc wcel cr cc0 wne w3a cre cfv wceq cmul co wb rereb 3ad2ant1 wa recl
    recnd simp1 recn anim1i 3adant1 mulcan syl111anc ci cim caddc adantr
    adantl ax-icn imcl mulcl sylancr adddi replim opreq2d mul12 mp3an1 syl2an
    3eqtr4d fveq2d remulcl sylan2 crre syl11anc eqtr2d eqeq1d sylan syl bitr4d
    ancoms 3adant3 3bitr2d ) ACDZBEDZBFGZHZAEDZAIJZAKZBVTLMZBALMZKZWCEDZVOVPVSW
    ANVQAOPVRVTCDZVOBCDZVQQZWDWANVOVPWFVQVOVTARZSZPVOVPVQTVPVQWHVOVPWGVQBUAZUBU
    CVTABUDUEVOVPWDWENZVQVPVOWLVPVOQZWDWCIJZWCKZWEWMWBWNWCWMWNWBUFBAUGJZLMZLMZU
    HMZIJZWBWMWCWSIWMBVTUFWPLMZUHMZLMZWBBXALMZUHMZWCWSWMWGWFXACDZXCXEKVPWGVOWKU
    IVOWFVPWJUJVOXFVPVOUFCDZWPCDZXFUKVOWPAULZSZUFWPUMUNUJBVTXAUOUEWMAXBBLVOAXBK
    VPAUPUJUQWMWRXDWBUHVPWGXHWRXDKZVOWKXJXGWGXHXKUKUFBWPURUSUTUQVAVBWMWBEDZWQED
    ZWTWBKVOVPVTEDXLWIBVTVCVDVOVPWPEDXMXIBWPVCVDWBWQVEVFVGVHWMWCCDZWEWONVPWGVOX
    NWKBAUMVIWCOVJVKVLVMVN $.
    $( [21-Aug-2008] $)

  ${
    cjcji.1 $e |- A e. CC $.
    $( The conjugate of the conjugate is the original complex number.
       Proposition 10-3.4(e) of [Gleason] p. 133. $)
    cjcji $p |- ( * ` ( * ` A ) ) = A $=
      ( ccj cfv cre ci cim cmul co cmin cneg caddc recli recni ax-icn imcli
      mulcli negsubi mulneg2i opreq2i cc wcel wceq remim ax-mp 3eqtr4ri fveq2i
      renegcli crrei eqtri crimi 3eqtri opreq12i cjcli replimi subnegi eqtr4i
      3eqtr4i ) ACDZEDZFUSGDZHIZJIZAEDZFAGDZHIZKZJIZUSCDZAUTVDVBVGJUTVDFVEKZHIZ
      LIZEDVDUSVLEVDVGLIVDVFJIZVLUSVDVFVDABMZNZFVEOVEABPZNZQZRVKVGVDLFVEOVQSZTA
      UAUBUSVMUCBAUDUEUFZUGVDVJVNVEVPUHZUIUJVBFVLGDZHIVKVGVAWBFHUSVLGVTUGTWBVJF
      HVDVJVNWAUKTVSULUMUSUAUBVIVCUCABUNUSUDUEAVDVFLIVHABUOVDVFVOVRUPUQUR $.
      $( [11-May-1999] $)

    $( A number is real iff its imaginary part is 0. $)
    reim0bi $p |- ( A e. RR <-> ( Im ` A ) = 0 ) $=
      ( cc wcel cr cim cfv cc0 wceq wb reim0b ax-mp ) ACDAEDAFGHIJBAKL $.
      $( [29-May-1999] $)

    $( A real number equals its real part.  Proposition 10-3.4(f) of [Gleason]
       p. 133. $)
    rerebi $p |- ( A e. RR <-> ( Re ` A ) = A ) $=
      ( cc wcel cr cre cfv wceq wb rereb ax-mp ) ACDAEDAFGAHIBAJK $.
      $( [24-Jul-2008] $) $( [27-Oct-1999] $)

    $( A number is real iff it equals its complex conjugate.  Proposition
       10-3.4(f) of [Gleason] p. 133. $)
    cjrebi $p |- ( A e. RR <-> ( * ` A ) = A ) $=
      ( cr wcel cim cfv cc0 wceq ccj reim0bi cneg imcli recni eqnegi ci cmul
      co ax-icn mulneg2i eqeq2i negcli ine0 mulcani cre caddc replimi cmin cc
      remim ax-mp recli mulcli negsubi eqtr4i eqeq12i addcani bitr2i 3bitr3i
      bitr3i eqcom 3bitri ) ACDAEFZGHZAAIFZHZVDAHABJVCVBVBKZHZVEVBVBABLMZNOVBPQ
      ZOVFPQZHVIVIKZHZVGVEVJVKVIOVBRVHSTVBVFOVHVBVHUARUBUCVEAUDFZVIUEQZVMVKUEQZ
      HVLAVNVDVOABUFVDVMVIUGQZVOAUHDVDVPHBAUIUJVMVIVMABUKMZOVBRVHULZUMUNUOVMVIV
      KVQVRVIVRUAUPUQURUSAVDUTVA $.
      $( [11-Oct-1999] $)

    $( Real part of a complex conjugate. $)
    recji $p |- ( Re ` ( * ` A ) ) = ( Re ` A ) $=
      ( ccj cfv cre ci cim cneg cmul co caddc cmin recli recni ax-icn imcli
      mulcli negsubi mulneg2i opreq2i cc wcel wceq remim ax-mp 3eqtr4ri fveq2i
      renegcli crrei eqtri ) ACDZEDAEDZFAGDZHZIJZKJZEDULUKUPEULFUMIJZHZKJULUQLJ
      ZUPUKULUQULABMZNFUMOUMABPZNZQRUOURULKFUMOVBSTAUAUBUKUSUCBAUDUEUFUGULUNUTU
      MVAUHUIUJ $.
      $( [2-Oct-1999] $)

    $( Imaginary part of a complex conjugate. $)
    imcji $p |- ( Im ` ( * ` A ) ) = -u ( Im ` A ) $=
      ( ccj cfv cim cre ci cneg cmul co caddc cmin recli recni ax-icn imcli
      mulcli negsubi mulneg2i opreq2i cc wcel wceq remim ax-mp 3eqtr4ri fveq2i
      renegcli crimi eqtri ) ACDZEDAFDZGAEDZHZIJZKJZEDUNUKUPEULGUMIJZHZKJULUQLJ
      ZUPUKULUQULABMZNGUMOUMABPZNZQRUOURULKGUMOVBSTAUAUBUKUSUCBAUDUEUFUGULUNUTU
      MVAUHUIUJ $.
      $( [2-Oct-1999] $)

    ${
      readdi.2 $e |- B e. CC $.
      $( Real part distributes over addition. $)
      readdi $p |- ( Re ` ( A + B ) ) = ( ( Re ` A ) + ( Re ` B ) ) $=
        ( caddc co cre cfv ci cim cmul recli recni ax-icn imcli mulcli add4i
        replimi opreq12i adddii opreq2i 3eqtr4i fveq2i readdcli crrei eqtri )
        ABEFZGHAGHZBGHZEFZIAJHZBJHZEFZKFZEFZGHUJUGUOGUHIUKKFZEFZUIIULKFZEFZEFUJ
        UPUREFZEFUGUOUHUPUIURUHACLZMIUKNUKACOZMZPUIBDLZMIULNULBDOZMZPQAUQBUSEAC
        RBDRSUNUTUJEIUKULNVCVFTUAUBUCUJUMUHUIVAVDUDUKULVBVEUDUEUF $.
        $( [28-Jul-1999] $)

      $( Imaginary part distributes over addition. $)
      imaddi $p |- ( Im ` ( A + B ) ) = ( ( Im ` A ) + ( Im ` B ) ) $=
        ( caddc co cim cfv cre ci cmul recli recni ax-icn imcli mulcli add4i
        replimi opreq12i adddii opreq2i 3eqtr4i fveq2i readdcli crimi eqtri )
        ABEFZGHAIHZBIHZEFZJAGHZBGHZEFZKFZEFZGHUMUGUOGUHJUKKFZEFZUIJULKFZEFZEFUJ
        UPUREFZEFUGUOUHUPUIURUHACLZMJUKNUKACOZMZPUIBDLZMJULNULBDOZMZPQAUQBUSEAC
        RBDRSUNUTUJEJUKULNVCVFTUAUBUCUJUMUHUIVAVDUDUKULVBVEUDUEUF $.
        $( [28-Jul-1999] $)

      $( Real part of a product. $)
      remuli $p |- ( Re ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Re ` B ) ) - ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( cmul co cre cfv cim cmin ci caddc replimi opreq12i recli recni imcli
        crmuli eqtri fveq2i remulcli resubcli readdcli crrei ) ABEFZGHAGHZBGHZE
        FZAIHZBIHZEFZJFZKUFUJEFZUIUGEFZLFZEFLFZGHULUEUPGUEUFKUIEFLFZUGKUJEFLFZE
        FUPAUQBUREACMBDMNUFUIUGUJUFACOZPUIACQZPUGBDOZPUJBDQZPRSTULUOUHUKUFUGUSV
        AUAUIUJUTVBUAUBUMUNUFUJUSVBUAUIUGUTVAUAUCUDS $.
        $( [28-Jul-1999] $)

      $( Imaginary part of a product. $)
      immuli $p |- ( Im ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Im ` B ) ) + ( ( Im ` A ) x. ( Re ` B ) ) ) $=
        ( cmul co cim cfv cre cmin ci caddc replimi opreq12i recli recni imcli
        crmuli eqtri fveq2i remulcli resubcli readdcli crimi ) ABEFZGHAIHZBIHZE
        FZAGHZBGHZEFZJFZKUFUJEFZUIUGEFZLFZEFLFZGHUOUEUPGUEUFKUIEFLFZUGKUJEFLFZE
        FUPAUQBUREACMBDMNUFUIUGUJUFACOZPUIACQZPUGBDOZPUJBDQZPRSTULUOUHUKUFUGUSV
        AUAUIUJUTVBUAUBUMUNUFUJUSVBUAUIUGUTVAUAUCUDS $.
        $( [28-Jul-1999] $)

      $( Complex conjugate distributes over addition.  Proposition 10-3.4(a) of
         [Gleason] p. 133. $)
      cjaddi $p |- ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) $=
        ( caddc co cre cfv ci cim cmul cmin ccj readdi imaddi opreq2i ax-icn
        imcli recni adddii eqtri opreq12i recli mulcli addsub4i cc wcel wceq
        addcli remim ax-mp 3eqtr4i ) ABEFZGHZIUMJHZKFZLFZAGHZIAJHZKFZLFZBGHZIBJ
        HZKFZLFZEFZUMMHZAMHZBMHZEFUQURVBEFZUTVDEFZLFVFUNVJUPVKLABCDNUPIUSVCEFZK
        FVKUOVLIKABCDOPIUSVCQUSACRSZVCBDRSZTUAUBURVBUTVDURACUCSVBBDUCSIUSQVMUDI
        VCQVNUDUEUAUMUFUGVGUQUHABCDUIUMUJUKVHVAVIVEEAUFUGVHVAUHCAUJUKBUFUGVIVEU
        HDBUJUKUBUL $.
        $( [28-Jul-1999] $)

      $( Complex conjugate distributes over multiplication.  Proposition
         10-3.4(c) of [Gleason] p. 133. $)
      cjmuli $p |- ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) $=
        ( cmul co cre cfv ci cim cmin cneg caddc ccj recli recni mulcli imcli
        subcli ax-icn addcli negsubi mul2negi eqcomi opreq2i mulneg2i negdii
        mulneg1i opreq12i eqtr4i eqtr3i remuli immuli negcli crmuli 3eqtr4i cc
        wcel wceq remim ax-mp 3eqtr4ri ) ABEFZGHZIVCJHZEFZKFZAGHZIAJHZLZEFZMFZB
        GHZIBJHZLZEFZMFZEFZVCNHZANHZBNHZEFVHVMEFZVIVNEFZKFZIVHVNEFZVIVMEFZMFZEF
        ZKFZWBVJVOEFZKFZIVHVOEFZVJVMEFZMFZEFZMFZVGVRWDWHLZMFWIWPWDWHWBWCVHVMVHA
        COPZVMBDOPZQVIVNVIACRPZVNBDRPZQSIWGTWEWFVHVNWRXAQZVIVMWTWSQZUAZQUBWDWKW
        QWOMWCWJWBKWJWCVIVNWTXAUCUDUEIWGLZEFWQWOIWGTXDUFXEWNIEXEWELZWFLZMFWNWEW
        FXBXCUGWLXFWMXGMVHVNWRXAUFVIVMWTWSUHUIUJUEUKUIUKVDWDVFWHKABCDULVEWGIEAB
        CDUMUEUIVHVJVMVOWRVIWTUNWSVNXAUNUOUPVCUQURVSVGUSABCDQVCUTVAVTVLWAVQEVHI
        VIEFZLZMFVHXHKFZVLVTVHXHWRIVITWTQUBVKXIVHMIVITWTUFUEAUQURVTXJUSCAUTVAVB
        VMIVNEFZLZMFVMXKKFZVQWAVMXKWSIVNTXAQUBVPXLVMMIVNTXAUFUEBUQURWAXMUSDBUTV
        AVBUIUP $.
        $( [28-Jul-1999] $)

      $( Standard inner product on complex numbers. $)
      ipcni $p |- ( Re ` ( A x. ( * ` B ) ) ) =
             ( ( ( Re ` A ) x. ( Re ` B ) ) + ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( ccj cfv cmul co cre cim cmin cneg caddc cjcli remuli recli remulcli
        recni imcli negsubi recji opreq2i wceq imcji negcon2i mpbi mulneg2i
        eqtr2i opreq12i 3eqtr2i ) ABEFZGHIFAIFZUKIFZGHZAJFZUKJFZGHZKHUNUQLZMHUL
        BIFZGHZUOBJFZGHZMHAUKCBDNZOUNUQUNULUMACPUKVCPQRUQUOUPACSZUKVCSZQRTUNUTU
        RVBMUMUSULGBDUAUBVBUOUPLZGHURVAVFUOGUPVALUCVAVFUCBDUDUPVAUPVERZVABDSRUE
        UFUBUOUPUOVDRVGUGUHUIUJ $.
        $( [2-Oct-1999] $)
    $}

    $( A complex number times its conjugate is real. $)
    cjmulrcli $p |- ( A x. ( * ` A ) ) e. RR $=
      ( ccj cfv cmul co cr wcel wceq cjcji opreq2i cjcli cjmuli mulcomi
      3eqtr4i mulcli cjrebi mpbir ) AACDZEFZGHTCDZTISSCDZEFSAEFUATUBASEABJKASBA
      BLZMASBUCNOTASBUCPQR $.
      $( [11-May-1999] $)

    $( A complex number times its conjugate. $)
    cjmulvali $p |- ( A x. ( * ` A ) ) =
                   ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) $=
      ( cre cfv c2 cexp co cim caddc cmul ccj recli recni sqvali imcli
      opreq12i ipcni cr wcel wceq cjmulrcli rerebi mpbi 3eqtr2ri ) ACDZEFGZAHDZ
      EFGZIGUEUEJGZUGUGJGZIGAAKDJGZCDZUKUFUIUHUJIUEUEABLMNUGUGABOMNPAABBQUKRSUL
      UKTABUAZUKUKUMMUBUCUD $.
      $( [2-Oct-1999] $)

    $( A complex number times its conjugate is nonnegative. $)
    cjmulge0i $p |- 0 <_ ( A x. ( * ` A ) ) $=
      ( cc0 cre cfv c2 cexp co cim caddc ccj cmul cle wbr recli sqge0i imcli
      resqcli addge0i mp2an cjmulvali breqtrri ) CADEZFGHZAIEZFGHZJHZAAKELHMCUD
      MNCUFMNCUGMNUCABOZPUEABQZPUDUFUCUHRUEUIRSTABUAUB $.
      $( [28-May-1999] $)

    $( Real part of negative. $)
    renegi $p |- ( Re ` -u A ) = -u ( Re ` A ) $=
      ( cneg cre cfv ci cim cmul co caddc recli recni ax-icn imcli mulcli
      negdii replimi negeqi mulneg2i opreq2i 3eqtr4i fveq2i renegcli crrei
      eqtri ) ACZDEADEZCZFAGEZCZHIZJIZDEUHUFULDUGFUIHIZJIZCUHUMCZJIUFULUGUMUGAB
      KZLFUIMUIABNZLZOPAUNABQRUKUOUHJFUIMURSTUAUBUHUJUGUPUCUIUQUCUDUE $.
      $( [24-Jul-2008] $) $( [2-Aug-1999] $)

    $( The negative of a real is real. $)
    negrebi $p |- ( -u A e. RR <-> A e. RR ) $=
      ( cneg cr wcel cre cfv wceq negcli rerebi renegi eqeq1i recli recni
      neg11i 3bitri eleq1 mpbii sylbi renegcl impbii ) ACZDEZADEZUCAFGZAHZUDUCU
      BFGZUBHUECZUBHUFUBABIJUGUHUBABKLUEAUEABMZNBOPUFUEDEUDUIUEADQRSATUA $.
      $( [11-Aug-1999] $)

    $( Imaginary part of negative. $)
    imnegi $p |- ( Im ` -u A ) = -u ( Im ` A ) $=
      ( cneg cim cfv cre ci cmul co caddc recli recni ax-icn imcli mulcli
      negdii replimi negeqi mulneg2i opreq2i 3eqtr4i fveq2i renegcli crimi
      eqtri ) ACZDEAFEZCZGADEZCZHIZJIZDEUJUFULDUGGUIHIZJIZCUHUMCZJIUFULUGUMUGAB
      KZLGUIMUIABNZLZOPAUNABQRUKUOUHJGUIMURSTUAUBUHUJUGUPUCUIUQUCUDUE $.
      $( [24-Jul-2008] $) $( [2-Aug-1999] $)

    $( Complex conjugate of negative. $)
    cjnegi $p |- ( * ` -u A ) = -u ( * ` A ) $=
      ( cneg cre cfv ci cim cmul co cmin ccj caddc recli recni negcli ax-icn
      imcli mulcli negsubi negdii renegi imnegi opreq2i mulneg2i eqtri
      opreq12i 3eqtr4ri negeqi cc wcel wceq remim ax-mp 3eqtr4i ) ACZDEZFUOGEZH
      IZJIZADEZFAGEZHIZJIZCZUOKEZAKEZCUSUTVBCZLIZCZVDUTCZVGCLIVJVGJIVIUSVJVGUTU
      TABMNZOVBFVAPVAABQNZRZOZSUTVGVKVNTUPVJURVGJABUAURFVACZHIVGUQVOFHABUBUCFVA
      PVLUDUEUFUGVHVCUTVBVKVMSUHUEUOUIUJVEUSUKABOUOULUMVFVCAUIUJVFVCUKBAULUMUHU
      N $.
      $( [2-Aug-1999] $)

    $( A number plus its conjugate is twice its real part.  Compare Proposition
       10-3.4(h) of [Gleason] p. 133. $)
    addcji $p |- ( A + ( * ` A ) ) = ( 2 x. ( Re ` A ) ) $=
      ( cre cfv ci cim cmul co caddc cneg ccj c2 cc0 recli recni ax-icn imcli
      mulcli negcli add4i negidi opreq2i addcli addid1i 3eqtri replimi cmin cc
      wcel wceq remim ax-mp negsubi eqtr4i opreq12i 2timesi 3eqtr4i ) ACDZEAFDZ
      GHZIHZURUTJZIHZIHZURURIHZAAKDZIHLURGHVDVEUTVBIHZIHVEMIHVEURUTURVBURABNOZE
      USPUSABQORZVHUTVISTVGMVEIUTVIUAUBVEURURVHVHUCUDUEAVAVFVCIABUFVFURUTUGHZVC
      AUHUIVFVJUJBAUKULURUTVHVIUMUNUOURVHUPUQ $.
      $( [2-Oct-1999] $)
  $}

  $( A real number equals its real part.  One direction of Proposition
     10-3.4(f) of [Gleason] p. 133.  (Contributed by Paul Chapman,
     7-Sep-2007.) $)
  rere $p |- ( A e. RR -> ( Re ` A ) = A ) $=
    ( cr wcel cre cfv wceq cc wb recn rereb syl ibi ) ABCZADEAFZMAGCMNHAIAJKL
    $.
    $( [7-Sep-2007] $)

  $( A number is real iff it equals its complex conjugate.  Proposition
     10-3.4(f) of [Gleason] p. 133. $)
  cjreb $p |- ( A e. CC -> ( A e. RR <-> ( * ` A ) = A ) ) $=
    ( cc wcel cr ccj cfv wceq wb cc0 cif eleq1 fveq2 id eqeq12d bibi12d 0cn
    elimel cjrebi dedth ) ABCZADCZAEFZAGZHTAIJZDCZUDEFZUDGZHAIAUDGZUAUEUCUGAUDD
    KUHUBUFAUDAUDELUHMNOUDAIBPQRS $.
    $( [2-Jul-2005] $)

  $( A complex number times its conjugate is real. $)
  cjmulrcl $p |- ( A e. CC -> ( A x. ( * ` A ) ) e. RR ) $=
    ( cc wcel ccj cfv cmul co cr cc0 cif wceq id fveq2 opreq12d eleq1d 0cn
    elimel cjmulrcli dedth ) ABCZAADEZFGZHCTAIJZUCDEZFGZHCAIAUCKZUBUEHUFAUCUAUD
    FUFLAUCDMNOUCAIBPQRS $.
    $( [26-Mar-2005] $)

  $( A complex number times its conjugate. $)
  cjmulval $p |- ( A e. CC -> ( A x. ( * ` A ) ) =
                 ( ( ( Re ` A ) ^ 2 ) + ( ( Im ` A ) ^ 2 ) ) ) $=
    ( cc wcel ccj cfv cmul co cre c2 cexp cim caddc wceq cif id fveq2 opreq12d
    opreq1d eqeq12d 2cn elimel cjmulvali dedth ) ABCZAADEZFGZAHEZIJGZAKEZIJGZLG
    ZMUDAINZULDEZFGZULHEZIJGZULKEZIJGZLGZMAIAULMZUFUNUKUSUTAULUEUMFUTOAULDPQUTU
    HUPUJURLUTUGUOIJAULHPRUTUIUQIJAULKPRQSULAIBTUAUBUC $.
    $( [1-Feb-2007] $)

  $( A complex number times its conjugate is nonnegative. $)
  cjmulge0 $p |- ( A e. CC -> 0 <_ ( A x. ( * ` A ) ) ) $=
    ( cc wcel cc0 ccj cfv cmul co cle wbr cif wceq id fveq2 opreq12d breq2d
    0cn elimel cjmulge0i dedth ) ABCZDAAEFZGHZIJDUAADKZUDEFZGHZIJADAUDLZUCUFDIU
    GAUDUBUEGUGMAUDENOPUDADBQRST $.
    $( [26-Mar-2005] $)

  $( Real part of negative. $)
  reneg $p |- ( A e. CC -> ( Re ` -u A ) = -u ( Re ` A ) ) $=
    ( cc wcel cneg cre cfv wceq cc0 cif negeq fveq2d fveq2 negeqd eqeq12d 0cn
    elimel renegi dedth ) ABCZADZEFZAEFZDZGSAHIZDZEFZUDEFZDZGAHAUDGZUAUFUCUHUIT
    UEEAUDJKUIUBUGAUDELMNUDAHBOPQR $.
    $( [17-Mar-2005] $)

  $( Real part distributes over addition. $)
  readd $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Re ` ( A + B ) ) = ( ( Re ` A ) + ( Re ` B ) ) ) $=
    ( cc wcel caddc co cre cfv wceq cc0 cif opreq1 fveq2d fveq2 opreq1d
    eqeq12d opreq2 opreq2d 0cn elimel readdi dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZ
    EFZIUCAJKZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEU
    KGAUJBELMUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOAJ
    CSTBJCSTUAUB $.
    $( [17-Mar-2005] $)

  $( Real part distributes over subtraction. $)
  resub $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Re ` ( A - B ) ) = ( ( Re ` A ) - ( Re ` B ) ) ) $=
    ( cc wcel wa cneg caddc co cre cfv cmin wceq negcl readd sylan2 reneg
    adantl opreq2d eqtrd negsub fveq2d recl recnd syl2an 3eqtr3d ) ACDZBCDZEZAB
    FZGHZIJZAIJZBIJZFZGHZABKHZIJULUMKHZUHUKULUIIJZGHZUOUGUFUICDUKUSLBMAUINOUHUR
    UNULGUGURUNLUFBPQRSUHUJUPIABTUAUFULCDUMCDUOUQLUGUFULAUBUCUGUMBUBUCULUMTUDUE
    $.
    $( [2-Aug-2006] $) $( [17-Mar-2005] $)

  $( The imaginary part of a negative number. $)
  imneg $p |- ( A e. CC -> ( Im ` -u A ) = -u ( Im ` A ) ) $=
    ( cc wcel cneg cim cfv wceq cc0 cif negeq fveq2d fveq2 negeqd eqeq12d 0cn
    elimel imnegi dedth ) ABCZADZEFZAEFZDZGSAHIZDZEFZUDEFZDZGAHAUDGZUAUFUCUHUIT
    UEEAUDJKUIUBUGAUDELMNUDAHBOPQR $.
    $( [18-Mar-2005] $)

  $( Imaginary part distributes over addition. $)
  imadd $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Im ` ( A + B ) ) = ( ( Im ` A ) + ( Im ` B ) ) ) $=
    ( cc wcel caddc co cim cfv wceq cc0 cif opreq1 fveq2d fveq2 opreq1d
    eqeq12d opreq2 opreq2d 0cn elimel imaddi dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZ
    EFZIUCAJKZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEU
    KGAUJBELMUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOAJ
    CSTBJCSTUAUB $.
    $( [18-Mar-2005] $)

  $( Imaginary part distributes over subtraction. $)
  imsub $p |- ( ( A e. CC /\ B e. CC ) ->
              ( Im ` ( A - B ) ) = ( ( Im ` A ) - ( Im ` B ) ) ) $=
    ( cc wcel wa cneg caddc co cim cfv cmin wceq negcl imadd sylan2 imneg
    adantl opreq2d eqtrd negsub fveq2d imcl recnd syl2an 3eqtr3d ) ACDZBCDZEZAB
    FZGHZIJZAIJZBIJZFZGHZABKHZIJULUMKHZUHUKULUIIJZGHZUOUGUFUICDUKUSLBMAUINOUHUR
    UNULGUGURUNLUFBPQRSUHUJUPIABTUAUFULCDUMCDUOUQLUGUFULAUBUCUGUMBUBUCULUMTUDUE
    $.
    $( [1-Aug-2006] $) $( [18-Mar-2005] $)

  $( A real number equals its complex conjugate.  Proposition 10-3.4(f) of
     [Gleason] p. 133. $)
  cjre $p |- ( A e. RR -> ( * ` A ) = A ) $=
    ( cc wcel cr ccj cfv wceq recn cjreb biimpd mpcom ) ABCZADCZAEFAGZAHLMNAIJK
    $.
    $( [8-Oct-1999] $)

  $( The conjugate of the conjugate is the original complex number.
     Proposition 10-3.4(e) of [Gleason] p. 133. $)
  cjcj $p |- ( A e. CC -> ( * ` ( * ` A ) ) = A ) $=
    ( cc wcel ccj cfv wceq cc0 cif fveq2 fveq2d id eqeq12d 0cn elimel cjcji
    dedth ) ABCZADEZDEZAFQAGHZDEZDEZTFAGATFZSUBATUCRUADATDIJUCKLTAGBMNOP $.
    $( [29-Jul-1999] $)

  $( Complex conjugate distributes over addition.  Proposition 10-3.4(a) of
     [Gleason] p. 133. $)
  cjadd $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) ) $=
    ( cc wcel caddc co ccj cfv wceq cc0 cif opreq1 fveq2d fveq2 opreq1d
    eqeq12d opreq2 opreq2d 0cn elimel cjaddi dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZ
    EFZIUCAJKZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEU
    KGAUJBELMUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOAJ
    CSTBJCSTUAUB $.
    $( [31-Jul-1999] $)

  $( Complex conjugate distributes over multiplication.  Proposition 10-3.4(c)
     of [Gleason] p. 133. $)
  cjmul $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) ) $=
    ( cc wcel cmul co ccj cfv wceq cc0 cif opreq1 fveq2d fveq2 opreq1d eqeq12d
    opreq2 opreq2d 0cn elimel cjmuli dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZEFZIUCAJ
    KZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEUKGAUJBEL
    MUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOAJCSTBJCST
    UAUB $.
    $( [29-Jul-1999] $)

  $( Complex conjugate of negative. $)
  cjneg $p |- ( A e. CC -> ( * ` -u A ) = -u ( * ` A ) ) $=
    ( cc wcel cneg ccj cfv wceq cc0 cif negeq fveq2d fveq2 negeqd eqeq12d 0cn
    elimel cjnegi dedth ) ABCZADZEFZAEFZDZGSAHIZDZEFZUDEFZDZGAHAUDGZUAUFUCUHUIT
    UEEAUDJKUIUBUGAUDELMNUDAHBOPQR $.
    $( [27-Feb-2005] $)

  $( A number plus its conjugate is twice its real part.  Compare Proposition
     10-3.4(h) of [Gleason] p. 133. $)
  addcj $p |- ( A e. CC -> ( A + ( * ` A ) ) = ( 2 x. ( Re ` A ) ) ) $=
    ( cc wcel ccj cfv caddc co c2 cre cmul wceq cc0 cif id fveq2 opreq12d
    opreq2d eqeq12d 0cn elimel addcji dedth ) ABCZAADEZFGZHAIEZJGZKUCALMZUHDEZF
    GZHUHIEZJGZKALAUHKZUEUJUGULUMAUHUDUIFUMNAUHDOPUMUFUKHJAUHIOQRUHALBSTUAUB $.
    $( [21-Jan-2007] $)

  $( Complex conjugate distributes over subtraction. $)
  cjsub $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A - B ) ) = ( ( * ` A ) - ( * ` B ) ) ) $=
    ( cc wcel wa cneg caddc co ccj cfv cmin wceq negcl cjadd sylan2 negsub
    fveq2d cjneg adantl opreq2d cjcl syl2an eqtrd 3eqtr3d ) ACDZBCDZEZABFZGHZIJ
    ZAIJZUHIJZGHZABKHZIJUKBIJZKHZUFUEUHCDUJUMLBMAUHNOUGUIUNIABPQUGUMUKUOFZGHZUP
    UGULUQUKGUFULUQLUEBRSTUEUKCDUOCDURUPLUFAUABUAUKUOPUBUCUD $.
    $( [1-Aug-2006] $) $( [28-Apr-2005] $)

  ${
    $d j k A $.  $d j k N $.
    $( Complex conjugate of natural number exponentiation. $)
    cjexp $p |- ( ( A e. CC /\ N e. NN0 ) ->
               ( * ` ( A ^ N ) ) = ( ( * ` A ) ^ N ) ) $=
      ( vj vk cn0 wcel cc cexp co ccj cfv wceq cv wi cc0 c1 caddc opreq2
      fveq2d eqeq12d imbi2d exp0 cjcl cr 1re cjre ax-mp syl6eqr syl eqtr4d wa
      cmul expp1 expcl simpl cjmul syl11anc eqtrd adantr opreq1 sylan eqcomd
      sylan9eqr exp31 com12 a2d nn0ind impcom ) BEFAGFZABHIZJKZAJKZBHIZLZVIACMZ
      HIZJKZVLVOHIZLZNVIAOHIZJKZVLOHIZLZNVIADMZHIZJKZVLWDHIZLZNVIAWDPQIZHIZJKZV
      LWIHIZLZNVIVNNCDBVOOLZVSWCVIWNVQWAVRWBWNVPVTJVOOAHRSVOOVLHRTUAVOWDLZVSWHV
      IWOVQWFVRWGWOVPWEJVOWDAHRSVOWDVLHRTUAVOWILZVSWMVIWPVQWKVRWLWPVPWJJVOWIAHR
      SVOWIVLHRTUAVOBLZVSVNVIWQVQVKVRVMWQVPVJJVOBAHRSVOBVLHRTUAVIWAPJKZWBVIVTPJ
      AUBSVIVLGFZWBWRLAUCZWSWBPWRVLUBPUDFWRPLUEPUFUGUHUIUJWDEFZVIWHWMVIXAWHWMNV
      IXAWHWMVIXAUKZWHUKWKWFVLULIZWLXBWKXCLWHXBWKWEAULIZJKZXCXBWJXDJAWDUMSXBWEG
      FVIXEXCLAWDUNVIXAUOWEAUPUQURUSWHXBXCWGVLULIZWLWFWGVLULUTXBWLXFVIWSXAWLXFL
      WTVLWDUMVAVBVCURVDVEVFVGVH $.
      $( [7-Jun-2006] $)
  $}

  $( The real part of a number in terms of complex conjugate. $)
  recj $p |- ( A e. CC -> ( Re ` A ) = ( ( A + ( * ` A ) ) / 2 ) ) $=
    ( cc wcel cre cfv caddc co c2 cdiv ccj wceq recl recnd cc0 wne wa 2cn 2ne0
    pm3.2i divdir mp3an3 syl11anc ci cim cmul cmin cneg replim remim opreq12d
    ax-icn imcl mulcl sylancr negsub opreq2d mpan negid 3syl negcl add4
    syl22anc jca addcl addid1 3eqtr3d 3eqtr2rd opreq1d 2halves syl 3eqtr3rd )
    ABCZADEZVMFGZHIGZVMHIGZVPFGZAAJEZFGZHIGVMVLVMBCZVTVOVQKZVLVMALMZWBVTVTHBCZH
    NOZPWAWCWDQRSVMVMHTUAUBVLVNVSHIVLVSVMUCAUDEZUEGZFGZVMWFUFGZFGWGVMWFUGZFGZFG
    ZVNVLAWGVRWHFAUHAUIUJVLWJWHWGFVLVTWFBCZWJWHKWBVLUCBCZWEBCZWLUKVLWEAULMZUCWE
    UMZUNZVMWFUOUBUPVLVNWFWIFGZFGZVNNFGZWKVNVLWRNVNFVLWNWLWRNKWOWMWNWLUKWPUQZWF
    URUSUPVLVTVTWLWIBCZWSWKKWBWBWQVLWNWLXBWOXAWFUTUSVMVMWFWIVAVBVLVTVTPVNBCWTVN
    KVLVTVTWBWBVCVMVMVDVNVEUSVFVGVHVLVTVQVMKWBVMVIVJVK $.
    $( [2-Aug-2006] $) $( [30-Apr-2005] $)

  $( The imaginary part of a number in terms of complex conjugate. $)
  imcj $p |- ( A e. CC -> ( Im ` A ) = ( ( A - ( * ` A ) ) / ( 2 x. _i ) ) ) $=
    ( cc wcel cim cfv c2 ci cmul co cdiv ccj cmin wceq imcl recnd cc0 wne 2cn
    ax-icn mulcli 2ne0 ine0 mulne0i divcan4 mp3an23 syl cre caddc recl mulcl
    sylancr addcl syl11anc subsub syl111anc replim remim opreq12d 2times
    mulcom mpan2 mulass mp3an12 eqtrd pncan2 opreq1d 3eqtr4d 3eqtr4rd eqtr3d )
    ABCZADEZFGHIZHIZVLJIZVKAAKEZLIZVLJIVJVKBCZVNVKMZVJVKANOZVQVLBCZVLPQVRFGRSTZ
    FGRSUAUBUCVKVLUDUEUFVJVMVPVLJVJAUGEZGVKHIZUHIZWBWCLIZLIZWDWBLIZWCUHIZVPVMVJ
    WDBCZWBBCZWCBCZWFWHMVJWJWKWIVJWBAUIOZVJGBCZVQWKSVSGVKUJUKZWBWCULUMWLWNWDWBW
    CUNUOVJAWDVOWELAUPAUQURVJFWCHIZWCWCUHIZVMWHVJWKWOWPMWNWCUSUFVJVQVMWOMVSVQVM
    VLVKHIZWOVQVTVMWQMWAVKVLUTVAFBCWMVQWQWOMRSFGVKVBVCVDUFVJWGWCWCUHVJWJWKWGWCM
    WLWNWBWCVEUMVFVGVHVFVI $.
    $( [30-Apr-2005] $)

  $( The real part of zero. $)
  re0 $p |- ( Re ` 0 ) = 0 $=
    ( cc0 cr wcel cre cfv wceq 0re 0cn rerebi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The imaginary part of zero. $)
  im0 $p |- ( Im ` 0 ) = 0 $=
    ( cc0 cr wcel cim cfv wceq 0re 0cn reim0bi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The real part of one.  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  re1 $p |- ( Re ` 1 ) = 1 $=
    ( c1 cr wcel cre cfv wceq 1re ax-1cn rerebi mpbi ) ABCADEAFGAHIJ $.
    $( [9-Jun-2006] $)

  $( The imaginary part of one.  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  im1 $p |- ( Im ` 1 ) = 0 $=
    ( c1 cr wcel cim cfv cc0 wceq 1re ax-1cn reim0bi mpbi ) ABCADEFGHAIJK $.
    $( [9-Jun-2006] $)

  $( The real part of ` _i ` .  (Contributed by Scott Fenton, 9-Jun-2006.) $)
  rei $p |- ( Re ` _i ) = 0 $=
    ( cc0 ci c1 cmul co caddc cre cfv ax-icn ax-1cn mulcli addid2i fveq2i 0re
    1re crrei mulid1i 3eqtr3ri ) ABCDEZFEZGHSGHABGHTSGSBCIJKLMACNOPSBGBIQMR $.
    $( [15-Mar-2008] $) $( [9-Jun-2006] $)

  $( The imaginary part of ` _i ` .  (Contributed by Scott Fenton,
     9-Jun-2006.) $)
  imi $p |- ( Im ` _i ) = 1 $=
    ( ci c1 cmul co cim cfv cc0 caddc ax-icn ax-1cn mulcli addid2i eqcomi
    fveq2i mulid1i 0re 1re crimi 3eqtr3i ) ABCDZEFGTHDZEFAEFBTUAEUATTABIJKLMNTA
    EAIONGBPQRS $.
    $( [15-Mar-2008] $) $( [9-Jun-2006] $)

  $( The conjugate of zero. $)
  cj0 $p |- ( * ` 0 ) = 0 $=
    ( cc0 cr wcel ccj cfv wceq 0re 0cn cjrebi mpbi ) ABCADEAFGAHIJ $.
    $( [27-Jul-1999] $)

  $( The complex conjugate of the imaginary unit. $)
  cji $p |- ( * ` _i ) = -u _i $=
    ( ci cre cfv cim cmul co cmin cc0 ccj cneg rei c1 imi opreq2i ax-icn
    mulid1i eqtri opreq12i cc wcel wceq remim ax-mp df-neg 3eqtr4i ) ABCZAADCZE
    FZGFZHAGFAICZAJUFHUHAGKUHALEFAUGLAEMNAOPQRASTUJUIUAOAUBUCAUDUE $.
    $( [26-Mar-2005] $)

  $( The conjugate of a representation of a complex number in terms of real and
     imaginary parts. $)
  cjreim $p |- ( ( A e. RR /\ B e. RR ) -> ( * ` ( A + ( _i x. B ) ) ) =
                ( A - ( _i x. B ) ) ) $=
    ( cr wcel wa ci cmul co caddc ccj cfv cneg cmin cc wceq recn ax-icn mulcl
    sylancr cjadd syl2an cjre cjmul cji a1i opreq12d mulneg1 3eqtrd opreqan12d
    negsub ) ACDZBCDZEAFBGHZIHJKZAJKZUMJKZIHZAUMLZIHZAUMMHZUKANDZUMNDZUNUQOULAP
    ZULFNDZBNDZVBQBPZFBRSZAUMTUAUKULUOAUPURIAUBULUPFJKZBJKZGHZFLZBGHZURULVDVEUP
    VJOQVFFBUCSULVHVKVIBGVHVKOULUDUEBUBUFULVDVEVLUROQVFFBUGSUHUIUKVAVBUSUTOULVC
    VGAUMUJUAUH $.
    $( [1-Aug-2006] $) $( [1-Jul-2005] $)

  $( The conjugate of the representation of a complex number in terms of real
     and imaginary parts. $)
  cjreim2 $p |- ( ( A e. RR /\ B e. RR ) -> ( * ` ( A - ( _i x. B ) ) ) =
                ( A + ( _i x. B ) ) ) $=
    ( cr wcel wa ci cmul co cmin ccj cfv cneg caddc cc wceq recn ax-icn mulcl
    sylancr cjsub syl2an cjre cjmul cji a1i opreq12d mulneg1 3eqtrd opreqan12d
    subneg ) ACDZBCDZEAFBGHZIHJKZAJKZUMJKZIHZAUMLZIHZAUMMHZUKANDZUMNDZUNUQOULAP
    ZULFNDZBNDZVBQBPZFBRSZAUMTUAUKULUOAUPURIAUBULUPFJKZBJKZGHZFLZBGHZURULVDVEUP
    VJOQVFFBUCSULVHVKVIBGVHVKOULUDUEBUBUFULVDVEVLUROQVFFBUGSUHUIUKVAVBUSUTOULVC
    VGAUMUJUAUH $.
    $( [1-Jul-2005] $)

  $( Complex conjugate is a one-to-one function.  (Proof shortened by Eric
     Schmidt, 2-Jul-2009.  Previous version is ~ cj11OLD .) $)
  cj11 $p |- ( ( A e. CC /\ B e. CC ) ->
              ( ( * ` A ) = ( * ` B ) <-> A = B ) ) $=
    ( cc wcel wa ccj cfv wceq fveq2 cjcj eqeqan12d syl5ib impbid1 ) ACDZBCDZEZA
    FGZBFGZHZABHZSQFGZRFGZHPTQRFINOUAAUBBAJBJKLABFIM $.
    $( [2-Jul-2009] $) $( [29-Apr-2005] $)

  $( Complex conjugate is a one-to-one function. $)
  cj11OLD $p |- ( ( A e. CC /\ B e. CC ) ->
              ( ( * ` A ) = ( * ` B ) <-> A = B ) ) $=
    ( cc wcel wa cre cfv ci cim cneg cmul co caddc wceq ccj wb imcl recnd
    neg11 syl2an anbi2d cr recl renegcl syl jca cru 3bitr4d cmin ax-icn mulcl
    sylancr negsub syl11anc mulneg2 opreq2d remim 3eqtr4rd eqeqan12d replim )
    ACDZBCDZEZAFGZHAIGZJZKLZMLZBFGZHBIGZJZKLZMLZNZVDHVEKLZMLZVIHVJKLZMLZNZAOGZB
    OGZNABNVCVDVINZVFVKNZEZWBVEVJNZEZVNVSVCWCWEWBVAVECDZVJCDZWCWEPVBVAVEAQZRZVB
    VJBQZRZVEVJSTUAVAVDUBDZVFUBDZEVIUBDZVKUBDZEVNWDPVBVAWMWNAUCZVAVEUBDZWNWIVEU
    DUEUFVBWOWPBUCZVBVJUBDZWPWKVJUDUEUFVDVFVIVKUGTVAWMWREWOWTEVSWFPVBVAWMWRWQWI
    UFVBWOWTWSWKUFVDVEVIVJUGTUHVAVBVTVHWAVMVAVDVOJZMLZVDVOUILZVHVTVAVDCDVOCDZXB
    XCNVAVDWQRVAHCDZWGXDUJWJHVEUKULVDVOUMUNVAVGXAVDMVAXEWGVGXANUJWJHVEUOULUPAUQ
    URVBVIVQJZMLZVIVQUILZVMWAVBVICDVQCDZXGXHNVBVIWSRVBXEWHXIUJWLHVJUKULVIVQUMUN
    VBVLXFVIMVBXEWHVLXFNUJWLHVJUOULUPBUQURUSVAVBAVPBVRAUTBUTUSUH $.
    $( [1-Aug-2006] $) $( [29-Apr-2005] $)

  $( A number is nonzero iff its complex conjugate is nonzero. $)
  cjne0 $p |- ( A e. CC -> ( A =/= 0 <-> ( * ` A ) =/= 0 ) ) $=
    ( cc wcel cc0 ccj cfv wceq cj0 eqeq2i wb 0cn cj11 mpan2 syl5rbbr necon3bid
    ) ABCZADAEFZDQDGQDEFZGZPADGZRDQHIPDBCSTJKADLMNO $.
    $( [29-Apr-2005] $)

  ${
    cjdiv.1 $e |- A e. CC $.
    cjdiv.2 $e |- B e. CC $.
    $( Complex conjugate distributes over division. $)
    cjdivi $p |- ( B =/= 0 ->
                    ( * ` ( A / B ) ) = ( ( * ` A ) / ( * ` B ) ) ) $=
      ( cc0 wne ccj cfv cdiv co wceq cmul cc wcel divclzi cjmul sylancr
      divcan2zi fveq2d eqtr3d wb cjcl syl 0cn cj11 mp2an cj0 eqeq2i bitr3i
      necon3bii biimpi cjcli wa divmul mp3an1 mpanr1 syl11anc mpbird eqcomd )
      BEFZAGHZBGHZIJZABIJZGHZUTVCVEKZVBVELJZVAKZUTBVDLJZGHZVGVAUTBMNZVDMNZVJVGK
      DABCDOZBVDPQUTVIAGABCDRSTUTVEMNZVBEFZVFVHUAZUTVLVNVMVDUBUCUTVOBEVBEBEKZVB
      EGHZKZVBEKVKEMNVSVQUADUDBEUEUFVREVBUGUHUIUJUKVNVBMNZVOVPBDULVAMNVNVTVOUMV
      PACULVAVEVBUNUOUPUQURUS $.
      $( [6-Sep-2013] $) $( [29-Apr-2005] $)
  $}

  $( Complex conjugate distributes over division. $)
  cjdiv $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                   ( * ` ( A / B ) ) = ( ( * ` A ) / ( * ` B ) ) ) $=
    ( cc wcel cc0 wne cdiv co ccj cfv wceq wi cif opreq1 fveq2d fveq2 opreq1d
    eqeq12d imbi2d neeq1 opreq2 opreq2d imbi12d 0cn elimel cjdivi dedth2h
    3impia ) ACDZBCDZBEFZABGHZIJZAIJZBIJZGHZKZUIUJUKUQLUKUIAEMZBGHZIJZURIJZUOGH
    ZKZLUJBEMZEFZURVDGHZIJZVAVDIJZGHZKZLABEEAURKZUQVCUKVKUMUTUPVBVKULUSIAURBGNO
    VKUNVAUOGAURIPQRSBVDKZUKVEVCVJBVDETVLUTVGVBVIVLUSVFIBVDURGUAOVLUOVHVAGBVDIP
    UBRUCURVDAECUDUEBECUDUEUFUGUH $.
    $( [3-Feb-2006] $) $( [29-Apr-2005] $)

