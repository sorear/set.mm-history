$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Reciprocals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( ` i ` times itself is minus 1. $)
  ixi $p |- ( i x. i ) = -u 1 $=
    ( ci cmul co cc0 caddc c1 cneg axicn mulcl ax1cn negcl addass axi2m1
    opreq1i negid opreq2i 3eqtr3r addid1 addid2 3eqtr3 ) AABCZDECZDFGZECZUAUCUA
    FECZUCECUAFUCECZECUDUBUAFUCAAHHIZJFJKZLUEDUCEMNUFDUAEFJOPQUAUGRUCUHST $.
    $( [6-May-1999] $)

  $( Lemma for ~ recext . $)
  recextlem1 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + ( i x. B ) ) x.
      ( A - ( i x. B ) ) ) = ( ( A x. A ) + ( B x. B ) ) ) $=
    ( cc wcel wa ci cmul co caddc cmin cneg wceq adddirt pm3.26 axicn axmulcl
    mpan adantl subclt sylan2 syl3anc subdit axmulcom c1 mulm1t syl ixi
    opreq1i syl5req pm3.2i mul4t eqtrd anidms opreq12d eqtr4d npncant adantr
    negclt subnegt syl2an 3eqtrd ) ACDZBCDZEZAFBGHZIHAVEJHZGHZAVFGHZVEVFGHZIHZA
    AGHZAVEGHZJHZVLBBGHZKZJHZIHZVKVNIHZVBVECDZVFCDZVGVJLVDAVEVFMVBVCNZVCVSVBFCD
    ZVCVSOFBPQZRZVBVSVTVCAVESWCTUAVDVHVMVIVPIVBVBVSVHVMLVDAAVEUBWAWAWDUAVDVIVEA
    GHZVEVEGHZJHZVPVSVBVSVIWGLVDVEAVEUBWDWAWDUAVDVLWEVOWFJVBVSVLWELVCAVEUCWCTVC
    VOWFLZVBVCWHVCVCEZVOFFGHZVNGHZWFWIUDKZVNGHZVOWKWIVNCDZWMVOLBBPZVNUEUFWJWLVN
    GUGUHUIWBWBEWIWKWFLWBWBOOUJFFBBUKQULUMRUNUOUNVDVQVKVOJHZVRVKCDZVLCDZVOCDZVQ
    WPLVDVKVLVOUPVBWQVCVBWQAAPUMZUQVBVSWRVCAVEPWCTVCWSVBVCWSWIWNWSWOVNURUFUMRUA
    WQWNWPVRLVBVCVKVNUSWTVCWNWOUMUTULVA $.
    $( [11-Mar-2008] $) $( [23-May-2007] $)

  $( Lemma for ~ recext . $)
  recextlem2 $p |- ( ( A e. RR /\ B e. RR /\ ( A + ( i x. B ) ) =/= 0 ) ->
                        ( ( A x. A ) + ( B x. B ) ) =/= 0 ) $=
    ( cmul co caddc cr wcel cc0 clt wbr wne ci w3a gt0ne0t axaddrcl axmulrcl
    anidms syl2an 3adant3 wa wo cle addgtge0t anim12i adantr msqgt0t msqge0t
    an1rs sylanc addgegt0t anassrs jaodan wceq wn opreq12 opreq2 axicn mul01
    syl6eq sylan2 0cn addid1 necon3ai neorian sylibr 3impa ) AACDZBBCDZEDZFGZHV
    IIJZVIHKAFGZBFGZALBCDZEDZHKZMVINVLVMVJVPVGFGZVHFGZVJVLVMVGVHOVLVQAAPQZVMVRB
    BPQZRSVLVMVPVKVLVMTZAHKZBHKZUAZVKVPWAWBVKWCVQVRTZHVGIJZHVHUBJZTZVKWAWBTVGVH
    UCWAWEWBVLVQVMVRVSVTUDZUEVLWBVMWHVLWBTWFVMWGAUFBUGUDUHUIWEHVGUBJZHVHIJZTZVK
    WAWCTVGVHUJWAWEWCWIUEVLVMWCWLVLWJVMWCTWKAUGBUFUDUKUIULVPAHUMZBHUMZTZUNWDWOV
    OHWOVOHHEDZHWMVNHUMVOWPUMWNAHVNHEUOWNVNLHCDHBHLCUPLUQURUSUTHVAVBUSVCAHBHVDV
    EUTVFUI $.
    $( [11-Mar-2008] $) $( [23-May-2007] $)

  ${
    $d x y a b A $.
    $( Existence of reciprocal of nonzero complex number.  (Contributed by Eric
       Schmidt, 22-May-2007.) $)
    recext $p |- ( ( A e. CC /\ A =/= 0 ) -> E. x e. CC ( A x. x ) = 1 ) $=
      ( va vb vy cc wcel cc0 wne cv cmul co c1 wceq wrex ci caddc cr wi axcnre
      wa recextlem2 3expia axrrecex axaddrcl axmulrcl anidms syl2an sylan cmin
      opreq2 eqeq1d rcla4ev axmulcl subclt axicn mpan sylan2 adantr axmulass
      axaddcl pm3.27 syl3anc recextlem1 opreq1d eqtr3d id sylan9eq sylanc
      exp31 recnt syl5 r19.23adv mpd ex syld wb neeq1 adantl opreq1 rexbidv
      3imtr4d r19.23aivv syl imp ) BFGZBHIZBAJZKLZMNZAFOZWFBCJZPDJZKLZQLZNZDROC
      ROWGWKSZCDBTWPWQCDRRWLRGZWMRGZUAZWPWQWTWPUAWOHIZWOWHKLZMNZAFOZWGWKWTXAXDS
      WPWTXAWLWLKLZWMWMKLZQLZHIZXDWRWSXAXHWLWMUBUCWTXHXDWTXHUAXGEJZKLZMNZEROZXD
      XGRGZXHXLWTEXGUDXERGZXFRGZXMWRWSXEXFUEWRXNWLWLUFUGWSXOWMWMUFUGUHUIWTXLXDS
      ZXHWLFGZWMFGZXPWRWSXQXRUAZXKXDERXSXIFGZXKXDSXIRGXSXTXKXDWLWNUJLZXIKLZFGZW
      OYBKLZMNZXDXSXTUAZXKUAXCYEAYBFWHYBNXBYDMWHYBWOKUKULUMYFYCXKYAFGZXTYCXSYAX
      IUNXQWNFGZYGXRWLWNUOPFGXRYHUPPWMUNUQZURZUIUSYFXKYDXJMYFWOYAKLZXIKLZYDXJWO
      FGZYGXTYLYDNYFWOYAXIUTXSYMXTXQYHYMXRWLWNVAYIURUSXSYGXTYJUSXSXTVBVCYFYKXGX
      IKXSYKXGNXTWLWMVDUSVEVFXKVGVHVIVJXIVKVLVMWLVKWMVKUHUSVNVOVPUSWPWGXAVQWTBW
      OHVRVSWPWKXDVQWTWPWJXCAFWPWIXBMBWOWHKVTULWAVSWBVOWCWDWE $.
      $( [11-Mar-2008] $) $( [23-May-2007] $)
  $}

  ${
    $d x A $.
    recex.1 $e |- A e. CC $.
    recex.2 $e |- A =/= 0 $.
    $( Existence of reciprocals. $)
    recex $p |- E. x e. CC ( A x. x ) = 1 $=
      ( cc wcel cc0 wne cv cmul co c1 wceq wrex recext mp2an ) BEFBGHBAIJKLMAEN
      CDABOP $.
      $( [26-Jan-1995] $)
  $}

  ${
      $d x A $.  $d x B $.  $d x C $.
    mulcan.1 $e |- A e. CC $.
    mulcan.2 $e |- B e. CC $.
    mulcan.3 $e |- C e. CC $.
    mulcan.4 $e |- A =/= 0 $.
    $( Cancellation law for multiplication.  Theorem I.7 of [Apostol]
       p. 18. $)
    mulcan $p |- ( ( A x. B ) = ( A x. C ) <-> B = C ) $=
      ( vx cmul co wceq cv c1 cc wrex wi recex wcel wa wb axmulass mp3an3
      eqeq12d mpan2 opreq2 syl5bir adantr axmulcom mpan eqeq1d opreq1 mulid2
      syl6eq syl6bi imp sylibd r19.23aiva ax-mp impbi ) ABIJZACIJZKZBCKZAHLZIJZ
      MKZHNOVBVCPZHADGQVFVGHNVDNRZVFSVBVDAIJZBIJZVICIJZKZVCVHVBVLPVFVHVLVDUTIJZ
      VDVAIJZKZVBVHANRZVLVOTDVHVPSVJVMVKVNVHVPBNRVJVMKEVDABUAUBVHVPCNRVKVNKFVDA
      CUAUBUCUDUTVAVDIUEUFUGVHVFVLVCTZVHVFVIMKZVQVHVEVIMVPVHVEVIKDAVDUHUIUJVRVJ
      BVKCVRVJMBIJBVIMBIUKBEULUMVRVKMCIJCVIMCIUKCFULUMUCUNUOUPUQURBCAIUEUS $.
      $( [26-Jan-1995] $)
  $}

  ${
    mulcant2.1 $e |- A =/= 0 $.
    $( Cancellation law for multiplication (theorem form).  Theorem I.7
       of [Apostol] p. 18.  Illustrates use of ~ keephyp . $)
    mulcant2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( A x. B ) = ( A x. C ) <-> B = C ) ) $=
      ( cc wcel cmul co wceq wb c1 cif opreq1 eqeq12d bibi1d opreq2 eqeq1d
      eqeq1 bibi12d eqeq2d eqeq2 ax1cn elimel cc0 wne neeq1 ax1ne0 keephyp
      mulcan dedth3h ) AEFZBEFZCEFZABGHZACGHZIZBCIZJUKAKLZBGHZURCGHZIZUQJABCKKK
      URULBKLZGHZUTIZVBCIZJVCURUMCKLZGHZIZVBVFIZJAURIZUPVAUQVJUNUSUOUTAURBGMAUR
      CGMNOBVBIZVAVDUQVEVKUSVCUTBVBURGPQBVBCRSCVFIZVDVHVEVIVLUTVGVCCVFURGPTCVFV
      BUASURVBVFAKEUBUCBKEUBUCCKEUBUCUKAUDUEKUDUEURUDUEAKAURUDUFKURUDUFDUGUHUIU
      J $.
      $( [29-Jan-1995] $)
  $}

  $( Cancellation law for multiplication (full theorem form).  Theorem I.7
     of [Apostol] p. 18.  Illustrates use of ~ dedth and ~ elimne0 . $)
  mulcant $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ A =/= 0 ) ->
                 ( ( A x. B ) = ( A x. C ) <-> B = C ) ) $=
    ( cc0 wne cc wcel w3a cmul co wceq wb wi c1 cif eleq1 3anbi1d opreq1
    eqeq12d bibi1d imbi12d elimne0 mulcant2 dedth impcom ) ADEZAFGZBFGZCFGZHZAB
    IJZACIJZKZBCKZLZUFUJUOMUFANOZFGZUHUIHZUPBIJZUPCIJZKZUNLZMANAUPKZUJURUOVBVCU
    GUQUHUIAUPFPQVCUMVAUNVCUKUSULUTAUPBIRAUPCIRSTUAUPBCAUBUCUDUE $.
    $( [29-Jan-1995] $)

  $( Cancellation law for multiplication. $)
  mulcan2t $p |- ( ( ( A e. CC /\ B e. CC /\ C e. CC ) /\ C =/= 0 ) ->
                 ( ( A x. C ) = ( B x. C ) <-> A = B ) ) $=
    ( cc wcel w3a cc0 wne wa cmul co wceq wb axmulcom 3adant2 3adant1 eqeq12d
    adantr wi mulcant ex 3coml imp bitrd ) ADEZBDEZCDEZFZCGHZIACJKZBCJKZLZCAJKZ
    CBJKZLZABLZUHULUOMUIUHUJUMUKUNUEUGUJUMLUFACNOUFUGUKUNLUEBCNPQRUHUIUOUPMZUGU
    EUFUIUQSUGUEUFFUIUQCABTUAUBUCUD $.
    $( [23-Jan-2005] $) $( [21-Jan-2005] $)

  ${
    mul0or.1 $e |- A e. CC $.
    ${
      mul0or.2 $e |- B e. CC $.
      $( If a product is zero, one of its factors must be zero.  Theorem I.11
         of [Apostol] p. 18. $)
      mul0or $p |- ( ( A x. B ) = 0 <-> ( A = 0 \/ B = 0 ) ) $=
        ( cmul co cc0 wceq wo wn wne wi df-ne cc wcel w3a wb 0cn 3pm3.2i
        mulcant mpan mul01 eqeq2i syl5bbr biimpd sylbir com12 orrd opreq1
        mul02 syl6eq opreq2 jaoi impbi ) ABEFZGHZAGHZBGHZIUPUQURUQJZUPURUSAGKZU
        PURLAGMUTUPURUTUOAGEFZHZURUPANOZBNOZGNOZPUTVBURQVCVDVECDRSABGTUAVAGUOAC
        UBZUCUDUEUFUGUHUQUPURUQUOGBEFGAGBEUIBDUJUKURUOVAGBGAEULVFUKUMUN $.
        $( [7-Oct-1999] $)
    $}

    $( A number is zero iff its square is zero (where square is represented
       using multiplication). $)
    msq0 $p |- ( ( A x. A ) = 0 <-> A = 0 ) $=
      ( cmul co cc0 wceq wo mul0or oridm bitr ) AACDEFAEFZKGKAABBHKIJ $.
      $( [28-Jul-1999] $)
  $}

  $( If a product is zero, one of its factors must be zero.  Theorem I.11
     of [Apostol] p. 18. $)
  mul0ort $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( ( A x. B ) = 0 <-> ( A = 0 \/ B = 0 ) ) ) $=
    ( cc wcel cmul co cc0 wceq wo wb cif opreq1 eqeq1d eqeq1 orbi1d bibi12d
    opreq2 orbi2d 0cn elimel mul0or dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZIZJUCAGKZ
    BEFZGHZUJGHZUHIZJUJUDBGKZEFZGHZUMUOGHZIZJABGGAUJHZUFULUIUNUTUEUKGAUJBELMUTU
    GUMUHAUJGNOPBUOHZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMBUOGNRPUJUOAGCSTBGCSTUAUB
    $.
    $( [9-Oct-1999] $)

  $( The product of two non-zero numbers is non-zero. $)
  muln0bt $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A =/= 0 /\ B =/= 0 ) <->
               ( A x. B ) =/= 0 ) ) $=
    ( cc wcel wa cc0 wceq wo wn cmul co wne mul0ort negbid bicomd neanior
    df-ne 3bitr4g ) ACDBCDEZAFGBFGHZIZABJKZFGZIZAFLBFLEUBFLSUDUASUCTABMNOAFBFPU
    BFQR $.
    $( [2-Aug-2004] $) $( [1-Aug-2004] $)

  $( The product of two non-zero numbers is non-zero. $)
  muln0t $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) ) ->
               ( A x. B ) =/= 0 ) $=
    ( cc wcel cc0 wne cmul co wa muln0bt biimpa an4s ) ACDZBCDZAEFZBEFZABGHEFZM
    NIOPIQABJKL $.
    $( [30-Dec-2007] $) $( [30-Dec-2007] $)

  ${
    muln0.1 $e |- A e. CC $.
    muln0.2 $e |- B e. CC $.
    muln0.3 $e |- A =/= 0 $.
    muln0.4 $e |- B =/= 0 $.
    $( The product of two non-zero numbers is non-zero. $)
    muln0 $p |- ( A x. B ) =/= 0 $=
      ( cc wcel cc0 wne wa cmul co pm3.2i muln0t mp2an ) AGHZAIJZKBGHZBIJZKABLM
      IJQRCENSTDFNABOP $.
      $( [15-Feb-1995] $)
  $}

  $( Property of numbers whose product equals their sum.  Equation 5 of
     [Kreyszig] p. 12. $)
  muleqaddt $p |- ( ( A e. CC /\ B e. CC ) ->
             ( ( A x. B ) = ( A + B ) <-> ( ( A - 1 ) x. ( B - 1 ) ) = 1 ) ) $=
    ( cc wcel wa c1 cmin co cmul wceq caddc cc0 ax1cn mulsubt mpanr2 mpanl2
    mulid1 opreq2i a1i ax1id opreqan12d opreq12d addsubt mp3an2 axmulcl
    axaddcl sylanc 3eqtrd eqeq1d wb subclt 0cn addcan2t mp3an23 syl addid2
    eqeq2i syl5rbbr subeq0t 3bitr2rd ) ACDZBCDZEZAFGHBFGHIHZFJABIHZABKHZGHZFKHZ
    FJZVGLJZVEVFJZVCVDVHFVCVDVEFFIHZKHZAFIHZBFIHZKHZGHZVEFKHZVFGHZVHVAFCDZVBVDV
    QJZMVAVTEVBVTWAMAFBFNOPVCVMVRVPVFGVMVRJVCVLFVEKFMQRSVAVBVNAVOBKATBTUAUBVECD
    ZVFCDZVSVHJZVCWBVTWCWDMVEFVFUCUDABUEZABUFZUGUHUIVCVHLFKHZJZVJVIVCVGCDZWHVJU
    JZWBWCWIVCVEVFUKWEWFUGWILCDVTWJULMVGLFUMUNUOWGFVHFMUPUQURWBWCVJVKUJVCVEVFUS
    WEWFUGUT $.
    $( [13-Nov-2006] $) $( [13-Nov-2006] $)

  ${
    $d x y A $.  $d x y B $.
    receu.1 $e |- A e. CC $.
    receu.2 $e |- B e. CC $.
    receu.3 $e |- A =/= 0 $.
    $( Existential uniqueness of reciprocals.  Theorem I.8 of [Apostol]
       p. 18. $)
    receu $p |- E! x e. CC ( A x. x ) = B $=
      ( vy cv cmul co wceq cc wreu wrex wa wi wral opreq2 eqeq1d reu4 c1 recex
      wcel axmulcl mpan2 risset sylib axmulass mp3an13 eqcomd sylan9eqr opreq1
      mulid2 syl6eq exp32 impcom a1d r19.21aiv ex r19.22 syl6 mpid r19.23aiv
      ax-mp w3a mulcant2 eqtr3t syl5bi mp3an1 rgen2a mpbir2an ) BAHZIJZCKZALMVN
      ALNZVNBGHZIJZCKZOZVLVPKZPZGLQALQVNVRAGLVTVMVQCVLVPBIRSTVQUAKZGLNVOGBDFUBW
      BVOGLVPLUCZWBVLVPCIJZKZALNZVOWCWDLUCZWFWCCLUCZWGEVPCUDUEAWDLUFUGWCWBWEVNP
      ZALQZWFVOPWCWBWJWCWBOZWIALWKWIVLLUCZWBWCWIWBWCWEVNWCWEOWBVMVQCIJZCWEWCVMB
      WDIJZWMVLWDBIRWCWMWNBLUCZWCWHWMWNKDEBVPCUHUIUJUKWBWMUACIJCVQUACIULCEUMUNU
      KUOUPUQURUSWEVNALUTVAVBVCVDWAAGLWOWLWCWADWOWLWCVEVMVQKVTVSBVLVPFVFVMVQCVG
      VHVIVJVK $.
      $( [29-Jan-1995] $)
  $}

  ${
    $d x y $.
    $( Multiplication maps nonzero complex numbers to nonzero complex numbers.
      (Contributed by Steve Rodriguez, 23-Feb-2007.) $)
    mulnzcnopr $p |- ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) :
                             ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) -->
                             ( CC \ { 0 } ) $=
      ( vx vy cc cc0 csn cdif cxp cmul cres wf wfn cv co wcel wral ffnoprval
      wss wa axmulopr mpbi pm3.26i difss ssxp mp2an fnssres oprvalres wne
      axmulcl ad2ant2r muln0t jca eldifsn syl2anb sylibr eqeltrd rgen2a
      mpbir2an ) CDEZFZUSGZUSHUTIZJVAUTKZALZBLZVAMZUSNZBUSOAUSOABUSUSUSVAPHCCGZ
      KZUTVGQZVBVHVCVDHMZCNZBCOACOZVGCHJVHVLRSABCCCHPTUAUSCQZVMVICURUBZVNUSCUSC
      UCUDVGUTHUEUDVFABUSVCUSNZVDUSNZRZVEVJUSVCVDUSUSHUFVQVKVJDUGZRZVJUSNVCCNZV
      CDUGZRZVDCNZVDDUGZRZVSVOVPWBWERVKVRVTWCVKWAWDVCVDUHUIVCVDUJUKVCCDULVDCDUL
      UMVJCDULUNUOUPUQ $.
      $( [24-Feb-2007] $) $( [17-Feb-2007] $)
 $}


