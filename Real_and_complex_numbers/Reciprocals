$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Reciprocals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( ` _i ` times itself is minus 1.  (The proof was shortened by Andrew
     Salmon, 19-Nov-2011.) $)
  ixi $p |- ( _i x. _i ) = -u 1 $=
    ( c1 cneg cc0 cmin co ci cmul df-neg wceq caddc axi2m1 0cn ax1cn axicn
    mulcli subadd2i mpbir eqtr2i ) ABCADEZFFGEZAHSTITAJECIKCATLMFFNNOPQR $.
    $( [19-Nov-2011] $) $( [6-May-1999] $)

  $( Lemma for ~ recex . $)
  recextlem1 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + ( _i x. B ) ) x.
      ( A - ( _i x. B ) ) ) = ( ( A x. A ) + ( B x. B ) ) ) $=
    ( cc wcel wa ci cmul co caddc cmin cneg wceq simpl axicn mulcl mpan adantl
    subcl sylan2 adddir syl111anc subdi mulcom c1 mulm1 syl ixi opreq1i
    syl5req pm3.2i mul4 eqtrd anidms opreq12d eqtr4d adantr negcl npncan
    subneg syl2an 3eqtrd ) ACDZBCDZEZAFBGHZIHAVEJHZGHZAVFGHZVEVFGHZIHZAAGHZAVEG
    HZJHZVLBBGHZKZJHZIHZVKVNIHZVDVBVECDZVFCDZVGVJLVBVCMZVCVSVBFCDZVCVSNFBOPZQZV
    BVSVTVCAVERWCSAVEVFTUAVDVHVMVIVPIVDVBVBVSVHVMLWAWAWDAAVEUBUAVDVIVEAGHZVEVEG
    HZJHZVPVDVSVBVSVIWGLWDWAWDVEAVEUBUAVDVLWEVOWFJVBVSVLWELVCAVEUCWCSVCVOWFLZVB
    VCWHVCVCEZVOFFGHZVNGHZWFWIUDKZVNGHZVOWKWIVNCDZWMVOLBBOZVNUEUFWJWLVNGUGUHUIW
    BWBEWIWKWFLWBWBNNUJFFBBUKPULUMQUNUOUNVDVQVKVOJHZVRVDVKCDZVLCDZVOCDZVQWPLVBW
    QVCVBWQAAOUMZUPVBVSWRVCAVEOWCSVCWSVBVCWSWIWNWSWOVNUQUFUMQVKVLVOURUAWQWNWPVR
    LVBVCVKVNUSWTVCWNWOUMUTULVA $.
    $( [11-Mar-2008] $) $( [23-May-2007] $)

  $( Lemma for ~ recex . $)
  recextlem2 $p |- ( ( A e. RR /\ B e. RR /\ ( A + ( _i x. B ) ) =/= 0 ) ->
                        ( ( A x. A ) + ( B x. B ) ) =/= 0 ) $=
    ( cr wcel ci cmul co caddc cc0 wne w3a clt wbr readdcl remulcl anidms
    syl2an 3adant3 wa wo cle anim12i adantr msqgt0 msqge0 an1rs addgtge0
    syl11anc anassrs addgegt0 jaodan wceq wn opreq12 opreq2 axicn mul01i
    syl6eq sylan2 0cn addid1i necon3ai neorian sylibr 3impa gt0ne0 ) ACDZBCDZAE
    BFGZHGZIJZKAAFGZBBFGZHGZCDZIVNLMZVNIJVGVHVOVKVLCDZVMCDZVOVGVHVLVMNVGVQAAOPZ
    VHVRBBOPZQRVGVHVKVPVGVHSZAIJZBIJZTZVPVKWAWBVPWCWAWBSVQVRSZIVLLMZIVMUAMZSZVP
    WAWEWBVGVQVHVRVSVTUBZUCVGWBVHWHVGWBSWFVHWGAUDBUEUBUFVLVMUGUHWAWCSWEIVLUAMZI
    VMLMZSZVPWAWEWCWIUCVGVHWCWLVGWJVHWCSWKAUEBUDUBUIVLVMUJUHUKVKAIULZBIULZSZUMW
    DWOVJIWOVJIIHGZIWMVIIULVJWPULWNAIVIIHUNWNVIEIFGIBIEFUOEUPUQURUSIUTVAURVBAIB
    IVCVDUSVEVNVFUH $.
    $( [11-Mar-2008] $) $( [23-May-2007] $)

  ${
    $d x y a b A $.
    $( Existence of reciprocal of nonzero complex number.  (Contributed by Eric
       Schmidt, 22-May-2007.) $)
    recex $p |- ( ( A e. CC /\ A =/= 0 ) -> E. x e. CC ( A x. x ) = 1 ) $=
      ( va vb vy cc wcel cc0 wne cv cmul co c1 wceq wrex ci caddc cr wi axcnre
      wa recextlem2 3expia axrrecex readdcl remulcl anidms syl2an sylan cmin
      mulcl subcl axicn mpan sylan2 adantr addcl simpr mulass syl111anc
      recextlem1 opreq1d eqtr3d id sylan9eq opreq2 eqeq1d rcla4ev syl11anc
      exp31 recn syl5 r19.23adv mpd ex syld wb neeq1 adantl opreq1 rexbidv
      3imtr4d r19.23aivv syl imp ) BFGZBHIZBAJZKLZMNZAFOZWFBCJZPDJZKLZQLZNZDROC
      ROWGWKSZCDBTWPWQCDRRWLRGZWMRGZUAZWPWQWTWPUAWOHIZWOWHKLZMNZAFOZWGWKWTXAXDS
      WPWTXAWLWLKLZWMWMKLZQLZHIZXDWRWSXAXHWLWMUBUCWTXHXDWTXHUAXGEJZKLZMNZEROZXD
      XGRGZXHXLWTEXGUDXERGZXFRGZXMWRWSXEXFUEWRXNWLWLUFUGWSXOWMWMUFUGUHUIWTXLXDS
      ZXHWLFGZWMFGZXPWRWSXQXRUAZXKXDERXSXIFGZXKXDSXIRGXSXTXKXDXSXTUAZXKUAWLWNUJ
      LZXIKLZFGZWOYCKLZMNZXDYAYDXKYBFGZXTYDXSYBXIUKXQWNFGZYGXRWLWNULPFGXRYHUMPW
      MUKUNZUOZUIUPYAXKYEXJMYAWOYBKLZXIKLZYEXJYAWOFGZYGXTYLYENXSYMXTXQYHYMXRWLW
      NUQYIUOUPXSYGXTYJUPXSXTURWOYBXIUSUTYAYKXGXIKXSYKXGNXTWLWMVAUPVBVCXKVDVEXC
      YFAYCFWHYCNXBYEMWHYCWOKVFVGVHVIVJXIVKVLVMWLVKWMVKUHUPVNVOVPUPWPWGXAVQWTBW
      OHVRVSWPWKXDVQWTWPWJXCAFWPWIXBMBWOWHKVTVGWAVSWBVOWCWDWE $.
      $( [11-Mar-2008] $) $( [23-May-2007] $)
  $}

  ${
    $d x A $.
    recex.1 $e |- A e. CC $.
    recex.2 $e |- A =/= 0 $.
    $( Existence of reciprocals. $)
    recexi $p |- E. x e. CC ( A x. x ) = 1 $=
      ( cc wcel cc0 wne cv cmul co c1 wceq wrex recex mp2an ) BEFBGHBAIJKLMAENC
      DABOP $.
      $( [26-Jan-1995] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    mulcan.1 $e |- A e. CC $.
    mulcan.2 $e |- B e. CC $.
    mulcan.3 $e |- C e. CC $.
    mulcan.4 $e |- C =/= 0 $.
    $( Cancellation law for multiplication.  Theorem I.7 of [Apostol] p. 18. $)
    mulcani $p |- ( ( C x. A ) = ( C x. B ) <-> A = B ) $=
      ( vx cmul co wceq cv c1 cc wrex wi recexi wcel wa wb mulass mp3an3
      eqeq12d mpan2 opreq2 syl5bir adantr mulcom mpan eqeq1d opreq1 mulid2i
      syl6eq syl6bi imp sylibd r19.23aiva ax-mp impbii ) CAIJZCBIJZKZABKZCHLZIJ
      ZMKZHNOVBVCPZHCFGQVFVGHNVDNRZVFSVBVDCIJZAIJZVIBIJZKZVCVHVBVLPVFVHVLVDUTIJ
      ZVDVAIJZKZVBVHCNRZVLVOTFVHVPSVJVMVKVNVHVPANRVJVMKDVDCAUAUBVHVPBNRVKVNKEVD
      CBUAUBUCUDUTVAVDIUEUFUGVHVFVLVCTZVHVFVIMKZVQVHVEVIMVPVHVEVIKFCVDUHUIUJVRV
      JAVKBVRVJMAIJAVIMAIUKADULUMVRVKMBIJBVIMBIUKBEULUMUCUNUOUPUQURABCIUEUS $.
      $( [24-Aug-2008] $) $( [26-Jan-1995] $)
  $}

  ${
    mulcant2.1 $e |- C =/= 0 $.
    $( Cancellation law for multiplication.  Theorem I.7 of [Apostol] p. 18.
       Illustrates use of ~ keephyp . $)
    mulcant2i $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( C x. A ) = ( C x. B ) <-> A = B ) ) $=
      ( cc wcel cmul co wceq wb c1 cif opreq2 eqeq1d eqeq1 bibi12d eqeq2d
      eqeq2 opreq1 eqeq12d bibi1d ax1cn elimel cc0 wne neeq1 ax1ne0 keephyp
      mulcani dedth3h ) AEFZBEFZCEFZCAGHZCBGHZIZABIZJCUKAKLZGHZUOIZURBIZJUSCULB
      KLZGHZIZURVBIZJUMCKLZURGHZVFVBGHZIZVEJABCKKKAURIZUPUTUQVAVJUNUSUOAURCGMNA
      URBOPBVBIZUTVDVAVEVKUOVCUSBVBCGMQBVBURRPCVFIZVDVIVEVLUSVGVCVHCVFURGSCVFVB
      GSTUAURVBVFAKEUBUCBKEUBUCCKEUBUCUMCUDUEKUDUEVFUDUECKCVFUDUFKVFUDUFDUGUHUI
      UJ $.
      $( [25-Aug-2008] $) $( [29-Jan-1995] $)
  $}

  $( Cancellation law for multiplication (full theorem form).  Theorem I.7 of
     [Apostol] p. 18.  Illustrates use of ~ dedth and ~ elimne0 . $)
  mulcan $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
                 ( ( C x. A ) = ( C x. B ) <-> A = B ) ) $=
    ( cc wcel cc0 wne wa cmul co wceq wb w3a wi c1 cif eleq1 3anbi3d opreq1
    eqeq12d bibi1d imbi12d elimne0 mulcant2i dedth 3expd com4l imp4a 3imp ) ADE
    ZBDEZCDEZCFGZHCAIJZCBIJZKZABKZLZUJUKULUMURUMUJUKULURUMUJUKULURUMUJUKULMZURN
    UJUKUMCOPZDEZMZUTAIJZUTBIJZKZUQLZNCOCUTKZUSVBURVFVGULVAUJUKCUTDQRVGUPVEUQVG
    UNVCUOVDCUTAISCUTBISTUAUBABUTCUCUDUEUFUGUHUI $.
    $( [26-Aug-2008] $) $( [29-Jan-1995] $)

  $( Cancellation law for multiplication. $)
  mulcan2 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
                 ( ( A x. C ) = ( B x. C ) <-> A = B ) ) $=
    ( cc wcel cc0 wne wa w3a cmul co wceq wb mulcom 3adant2 3adant1 eqeq12d
    3adant3r mulcan bitrd ) ADEZBDEZCDEZCFGZHIACJKZBCJKZLZCAJKZCBJKZLZABLUAUBUC
    UGUJMUDUAUBUCIUEUHUFUIUAUCUEUHLUBACNOUBUCUFUILUABCNPQRABCST $.
    $( [26-Aug-2008] $) $( [21-Jan-2005] $)

  ${
    mul0or.1 $e |- A e. CC $.
    ${
      mul0or.2 $e |- B e. CC $.
      $( If a product is zero, one of its factors must be zero.  Theorem I.11
         of [Apostol] p. 18. $)
      mul0ori $p |- ( ( A x. B ) = 0 <-> ( A = 0 \/ B = 0 ) ) $=
        ( cmul co cc0 wceq wo wn wne wi df-ne cc wcel wb wa 0cn mulcan mp3an12
        mpan mul01i eqeq2i syl5bbr biimpd sylbir com12 orrd opreq1 mul02i
        syl6eq opreq2 jaoi impbii ) ABEFZGHZAGHZBGHZIUPUQURUQJZUPURUSAGKZUPURLA
        GMUTUPURUTUOAGEFZHZURUPANOZUTVBURPZCBNOGNOVCUTQVDDRBGASTUAVAGUOACUBZUCU
        DUEUFUGUHUQUPURUQUOGBEFGAGBEUIBDUJUKURUOVAGBGAEULVEUKUMUN $.
        $( [7-Oct-1999] $)
    $}

    $( A number is zero iff its square is zero (where square is represented
       using multiplication). $)
    msq0i $p |- ( ( A x. A ) = 0 <-> A = 0 ) $=
      ( cmul co cc0 wceq wo mul0ori oridm bitri ) AACDEFAEFZKGKAABBHKIJ $.
      $( [28-Jul-1999] $)
  $}

  $( If a product is zero, one of its factors must be zero.  Theorem I.11 of
     [Apostol] p. 18. $)
  mul0or $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( ( A x. B ) = 0 <-> ( A = 0 \/ B = 0 ) ) ) $=
    ( cc wcel cmul co cc0 wceq wo wb cif opreq1 eqeq1d eqeq1 orbi1d bibi12d
    opreq2 orbi2d 0cn elimel mul0ori dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZIZJUCAGK
    ZBEFZGHZUJGHZUHIZJUJUDBGKZEFZGHZUMUOGHZIZJABGGAUJHZUFULUIUNUTUEUKGAUJBELMUT
    UGUMUHAUJGNOPBUOHZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMBUOGNRPUJUOAGCSTBGCSTUAUB
    $.
    $( [9-Oct-1999] $)

  $( The product of two nonzero numbers is nonzero.  (The proof was shortened
     by Andrew Salmon, 19-Nov-2011.) $)
  mulne0b $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A =/= 0 /\ B =/= 0 ) <->
               ( A x. B ) =/= 0 ) ) $=
    ( cc wcel wa cmul co cc0 wne wceq wo wn mul0or necon3abid neanior syl6rbbr
    ) ACDBCDEZABFGZHIAHJBHJKZLAHIBHIEQSRHABMNAHBHOP $.
    $( [19-Nov-2011] $) $( [1-Aug-2004] $)

  $( The product of two nonzero numbers is nonzero. $)
  mulne0 $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) ) ->
               ( A x. B ) =/= 0 ) $=
    ( cc wcel cc0 wne cmul co wa mulne0b biimpa an4s ) ACDZBCDZAEFZBEFZABGHEFZM
    NIOPIQABJKL $.
    $( [30-Dec-2007] $)

  ${
    muln0.1 $e |- A e. CC $.
    muln0.2 $e |- B e. CC $.
    muln0.3 $e |- A =/= 0 $.
    muln0.4 $e |- B =/= 0 $.
    $( The product of two nonzero numbers is nonzero. $)
    mulne0i $p |- ( A x. B ) =/= 0 $=
      ( cc wcel cc0 wne cmul co mulne0 mp4an ) AGHAIJBGHBIJABKLIJCEDFABMN $.
      $( [15-Feb-1995] $)
  $}

  $( Property of numbers whose product equals their sum.  Equation 5 of
     [Kreyszig] p. 12. $)
  muleqadd $p |- ( ( A e. CC /\ B e. CC ) ->
             ( ( A x. B ) = ( A + B ) <-> ( ( A - 1 ) x. ( B - 1 ) ) = 1 ) ) $=
    ( cc wcel wa c1 cmin co cmul wceq caddc cc0 ax1cn mulsub mpanr2 mpanl2
    mulid1i opreq2i a1i ax1id opreqan12d opreq12d mulcl addcl addsub mp3an2
    syl11anc 3eqtrd eqeq1d wb subcl 0cn addcan2 mp3an23 syl addid2i eqeq2i
    syl5rbbr subeq0 3bitr2rd ) ACDZBCDZEZAFGHBFGHIHZFJABIHZABKHZGHZFKHZFJZVGLJZ
    VEVFJZVCVDVHFVCVDVEFFIHZKHZAFIHZBFIHZKHZGHZVEFKHZVFGHZVHVAFCDZVBVDVQJZMVAVT
    EVBVTWAMAFBFNOPVCVMVRVPVFGVMVRJVCVLFVEKFMQRSVAVBVNAVOBKATBTUAUBVCVECDZVFCDZ
    VSVHJZABUCZABUDZWBVTWCWDMVEFVFUEUFUGUHUIVCVHLFKHZJZVJVIVCVGCDZWHVJUJZVCWBWC
    WIWEWFVEVFUKUGWILCDVTWJULMVGLFUMUNUOWGFVHFMUPUQURVCWBWCVJVKUJWEWFVEVFUSUGUT
    $.
    $( [13-Nov-2006] $)

  ${
    $d x y A $.  $d x y B $.
    receu.1 $e |- A e. CC $.
    receu.2 $e |- B e. CC $.
    receu.3 $e |- A =/= 0 $.
    $( Existential uniqueness of reciprocals.  Theorem I.8 of [Apostol]
       p. 18. $)
    receui $p |- E! x e. CC ( A x. x ) = B $=
      ( vy cv cmul co wceq cc wreu wrex wa wi wral opreq2 eqeq1d reu4 c1
      recexi wcel mulcl mpan2 risset sylib mulass mp3an13 eqcomd sylan9eqr
      opreq1 mulid2i syl6eq exp32 impcom a1d r19.21aiv ex rexim syl6 mpid
      r19.23aiv ax-mp w3a mulcant2i eqtr3 syl5bi mp3an3 rgen2a mpbir2an ) BAHZI
      JZCKZALMVNALNZVNBGHZIJZCKZOZVLVPKZPZGLQALQVNVRAGLVTVMVQCVLVPBIRSTVQUAKZGL
      NVOGBDFUBWBVOGLVPLUCZWBVLVPCIJZKZALNZVOWCWDLUCZWFWCCLUCZWGEVPCUDUEAWDLUFU
      GWCWBWEVNPZALQZWFVOPWCWBWJWCWBOZWIALWKWIVLLUCZWBWCWIWBWCWEVNWCWEOWBVMVQCI
      JZCWEWCVMBWDIJZWMVLWDBIRWCWMWNBLUCZWCWHWMWNKDEBVPCUHUIUJUKWBWMUACIJCVQUAC
      IULCEUMUNUKUOUPUQURUSWEVNALUTVAVBVCVDWAAGLWLWCWOWADWLWCWOVEVMVQKVTVSVLVPB
      FVFVMVQCVGVHVIVJVK $.
      $( [29-Jan-1995] $)
  $}

  ${
    $d x y $.
    $( Multiplication maps nonzero complex numbers to nonzero complex numbers.
       (Contributed by Steve Rodriguez, 23-Feb-2007.) $)
    mulnzcnopr $p |- ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) :
                             ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) -->
                             ( CC \ { 0 } ) $=
      ( vx vy cc cc0 csn cdif cxp cmul cres wf wfn cv co wcel wral ffnoprv wss
      wa axmulopr mpbi simpli difss xpss12 mp2an fnssres oprvres wne mulcl
      ad2ant2r mulne0 jca eldifsn syl2anb sylibr eqeltrd rgen2a mpbir2an ) CDEZ
      FZUSGZUSHUTIZJVAUTKZALZBLZVAMZUSNZBUSOAUSOABUSUSUSVAPHCCGZKZUTVGQZVBVHVCV
      DHMZCNZBCOACOZVGCHJVHVLRSABCCCHPTUAUSCQZVMVICURUBZVNUSCUSCUCUDVGUTHUEUDVF
      ABUSVCUSNZVDUSNZRZVEVJUSVCVDUSUSHUFVQVKVJDUGZRZVJUSNVCCNZVCDUGZRZVDCNZVDD
      UGZRZVSVOVPWBWERVKVRVTWCVKWAWDVCVDUHUIVCVDUJUKVCCDULVDCDULUMVJCDULUNUOUPU
      Q $.
      $( [17-Feb-2007] $)
  $}


