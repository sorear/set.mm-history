$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Reciprocals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( ` _i ` times itself is minus 1.  (The proof was shortened by Andrew
     Salmon, 19-Nov-2011.) $)
  ixi $p |- ( _i x. _i ) = -u 1 $=
    ( c1 cneg cc0 cmin co ci cmul df-neg caddc ax-i2m1 0cn ax-1cn ax-icn mulcli
    wceq subadd2i mpbir eqtr2i ) ABCADEZFFGEZAHSTOTAIECOJCATKLFFMMNPQR $.
    $( [19-Nov-2011] $) $( [6-May-1999] $)

  $( Lemma for ~ recex . $)
  recextlem1 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A + ( _i x. B ) ) x.
      ( A - ( _i x. B ) ) ) = ( ( A x. A ) + ( B x. B ) ) ) $=
    ( cc wcel wa ci cmul co caddc cmin cneg wceq ax-icn adantl sylan2 syl111anc
    mulcl mpan subdi anidms simpl subcl adddir mulcom ixi opreq1i mulm1 syl5req
    c1 syl pm3.2i mul4 eqtrd opreq12d eqtr4d adantr npncan subneg syl2an 3eqtrd
    negcl ) ACDZBCDZEZAFBGHZIHAVEJHZGHZAVFGHZVEVFGHZIHZAAGHZAVEGHZJHZVLBBGHZKZJ
    HZIHZVKVNIHZVDVBVECDZVFCDZVGVJLVBVCUAZVCVSVBFCDZVCVSMFBQRZNZVCVBVSVTWCAVEUB
    OAVEVFUCPVDVHVMVIVPIVDVBVBVSVHVMLWAWAWDAAVESPVDVIVEAGHZVEVEGHZJHZVPVDVSVBVS
    VIWGLWDWAWDVEAVESPVDVLWEVOWFJVCVBVSVLWELWCAVEUDOVCVOWFLZVBVCWHVCVCEZVOFFGHZ
    VNGHZWFWIWKUIKZVNGHZVOWJWLVNGUEUFWIVNCDZWMVOLBBQZVNUGUJUHWBWBEWIWKWFLWBWBMM
    UKFFBBULRUMTNUNUOUNVDVQVKVOJHZVRVDVKCDZVLCDZVOCDZVQWPLVBWQVCVBWQAAQTZUPVCVB
    VSWRWCAVEQOVCWSVBVCWSWIWNWSWOVNVAUJTNVKVLVOUQPVBWQWNWPVRLVCWTVCWNWOTVKVNURU
    SUMUT $.
    $( [11-Mar-2008] $) $( [23-May-2007] $)

  $( Lemma for ~ recex . $)
  recextlem2 $p |- ( ( A e. RR /\ B e. RR /\ ( A + ( _i x. B ) ) =/= 0 ) ->
                        ( ( A x. A ) + ( B x. B ) ) =/= 0 ) $=
    ( cr wcel ci cmul co caddc cc0 wne clt remulcl anidms wa wceq syl6eq sylan2
    wbr anim12i syl11anc w3a readdcl syl2an 3adant3 wo wn opreq2 ax-icn opreq12
    mul01i 0cn addid1i necon3ai neorian sylibr cle adantr msqgt0 an32s addgtge0
    msqge0 anassrs addgegt0 jaodan 3impa gt0ne0 ) ACDZBCDZAEBFGZHGZIJZUAAAFGZBB
    FGZHGZCDZIVNKRZVNIJVGVHVOVKVGVLCDZVMCDZVOVHVGVQAALMZVHVRBBLMZVLVMUBUCUDVGVH
    VKVPVKVGVHNZAIJZBIJZUEZVPVKAIOZBIOZNZUFWDWGVJIWGVJIIHGZIWFWEVIIOVJWHOWFVIEI
    FGIBIEFUGEUHUJPAIVIIHUIQIUKULPUMAIBIUNUOWAWBVPWCWAWBNVQVRNZIVLKRZIVMUPRZNZV
    PWAWIWBVGVQVHVRVSVTSZUQVGWBVHWLVGWBNWJVHWKAURBVASUSVLVMUTTWAWCNWIIVLUPRZIVM
    KRZNZVPWAWIWCWMUQVGVHWCWPVGWNVHWCNWOAVABURSVBVLVMVCTVDQVEVNVFT $.
    $( [11-Mar-2008] $) $( [23-May-2007] $)

  ${
    $d x y a b A $.
    $( Existence of reciprocal of nonzero complex number.  (Contributed by Eric
       Schmidt, 22-May-2007.) $)
    recex $p |- ( ( A e. CC /\ A =/= 0 ) -> E. x e. CC ( A x. x ) = 1 ) $=
      ( va vb vy cc wcel cc0 wne cv cmul co c1 wceq wrex ci cr wi wa adantr mpd
      caddc ax-cnre recextlem2 3expia remulcl anidms syl2an ax-rrecex recn cmin
      readdcl sylan ax-icn mulcl mpan subcl sylan2 addcl simpr mulass syl111anc
      recextlem1 opreq1d eqtr3d id sylan9eq opreq2 eqeq1d rcla4ev syl11anc syl5
      exp31 rexlimdv ex syld wb adantl opreq1 rexbidv 3imtr4d rexlimivv syl imp
      neeq1 ) BFGZBHIZBAJZKLZMNZAFOZWFBCJZPDJZKLZUBLZNZDQOCQOWGWKRZCDBUCWPWQCDQ
      QWLQGZWMQGZSZWPWQWTWPSWOHIZWOWHKLZMNZAFOZWGWKWTXAXDRWPWTXAWLWLKLZWMWMKLZU
      BLZHIZXDWRWSXAXHWLWMUDUEWTXHXDWTXHSXGEJZKLZMNZEQOZXDWTXGQGZXHXLWRXEQGZXFQ
      GZXMWSWRXNWLWLUFUGWSXOWMWMUFUGXEXFULUHEXGUIUMWTXLXDRZXHWRWLFGZWMFGZXPWSWL
      UJWMUJXQXRSZXKXDEQXIQGXIFGZXSXKXDRXIUJXSXTXKXDXSXTSZXKSWLWNUKLZXIKLZFGZWO
      YCKLZMNZXDYAYDXKXSYBFGZXTYDXRXQWNFGZYGPFGXRYHUNPWMUOUPZWLWNUQURZYBXIUOUMT
      YAXKYEXJMYAWOYBKLZXIKLZYEXJYAWOFGZYGXTYLYENXSYMXTXRXQYHYMYIWLWNUSURTXSYGX
      TYJTXSXTUTWOYBXIVAVBYAYKXGXIKXSYKXGNXTWLWMVCTVDVEXKVFVGXCYFAYCFWHYCNXBYEM
      WHYCWOKVHVIVJVKVMVLVNUHTUAVOVPTWPWGXAVQWTBWOHWEVRWPWKXDVQWTWPWJXCAFWPWIXB
      MBWOWHKVSVIVTVRWAVOWBWCWD $.
      $( [11-Mar-2008] $) $( [23-May-2007] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    mulcan.1 $e |- A e. CC $.
    mulcan.2 $e |- B e. CC $.
    mulcan.3 $e |- C e. CC $.
    mulcan.4 $e |- C =/= 0 $.
    $( Cancellation law for multiplication.  Theorem I.7 of [Apostol] p. 18. $)
    mulcani $p |- ( ( C x. A ) = ( C x. B ) <-> A = B ) $=
      ( vx cmul co wceq c1 cc wi wcel wa opreq2 wb mulass mp3an3 wrex cc0 recex
      cv mp2an eqeq12d mpan2 syl5ibr adantr mulcom eqeq1d opreq1 mulid2i syl6eq
      wne mpan syl6bi imp sylibd rexlimiva ax-mp impbii ) CAIJZCBIJZKZABKZCHUDZ
      IJZLKZHMUAZVEVFNZCMOZCUBUOVJFGHCUCUEVIVKHMVGMOZVIPVEVGCIJZAIJZVNBIJZKZVFV
      MVEVQNVIVEVQVMVGVCIJZVGVDIJZKZVCVDVGIQVMVLVQVTRFVMVLPVOVRVPVSVMVLAMOVOVRK
      DVGCASTVMVLBMOVPVSKEVGCBSTUFUGUHUIVMVIVQVFRZVMVIVNLKZWAVMVHVNLVLVMVHVNKFC
      VGUJUPUKWBVOAVPBWBVOLAIJAVNLAIULADUMUNWBVPLBIJBVNLBIULBEUMUNUFUQURUSUTVAA
      BCIQVB $.
      $( [24-Aug-2008] $) $( [26-Jan-1995] $)
  $}

  ${
    mulcant2.1 $e |- C =/= 0 $.
    $( Cancellation law for multiplication.  Theorem I.7 of [Apostol] p. 18.
       Illustrates use of ~ keephyp . $)
    mulcant2i $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( C x. A ) = ( C x. B ) <-> A = B ) ) $=
      ( cc wcel cmul co wceq wb cif opreq2 bibi12d opreq1 ax-1cn elimel cc0 wne
      c1 neeq1 eqeq1d eqeq1 eqeq2d eqeq2 eqeq12d bibi1d ax-1ne0 keephyp mulcani
      dedth3h ) AEFZBEFZCEFZCAGHZCBGHZIZABIZJCUKASKZGHZUOIZURBIZJUSCULBSKZGHZIZ
      URVBIZJUMCSKZURGHZVFVBGHZIZVEJABCSSSAURIZUPUTUQVAVJUNUSUOAURCGLUAAURBUBMB
      VBIZUTVDVAVEVKUOVCUSBVBCGLUCBVBURUDMCVFIZVDVIVEVLUSVGVCVHCVFURGNCVFVBGNUE
      UFURVBVFASEOPBSEOPCSEOPUMCQRSQRVFQRCSCVFQTSVFQTDUGUHUIUJ $.
      $( [25-Aug-2008] $) $( [29-Jan-1995] $)
  $}

  $( Cancellation law for multiplication (full theorem form).  Theorem I.7 of
     [Apostol] p. 18.  Illustrates use of ~ dedth and ~ elimne0 . $)
  mulcan $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
                 ( ( C x. A ) = ( C x. B ) <-> A = B ) ) $=
    ( cc wcel cc0 wne wa cmul co wceq wb w3a wi c1 eleq1 3anbi3d opreq1 eqeq12d
    cif bibi1d imbi12d elimne0 mulcant2i dedth 3expd com4l imp4a 3imp ) ADEZBDE
    ZCDEZCFGZHCAIJZCBIJZKZABKZLZUJUKULUMURUMUJUKULURUMUJUKULURUMUJUKULMZURNUJUK
    UMCOTZDEZMZUTAIJZUTBIJZKZUQLZNCOCUTKZUSVBURVFVGULVAUJUKCUTDPQVGUPVEUQVGUNVC
    UOVDCUTAIRCUTBIRSUAUBABUTCUCUDUEUFUGUHUI $.
    $( [26-Aug-2008] $) $( [29-Jan-1995] $)

  $( Cancellation law for multiplication. $)
  mulcan2 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
                 ( ( A x. C ) = ( B x. C ) <-> A = B ) ) $=
    ( cc wcel cc0 wne wa w3a cmul co wb mulcom 3adant2 3adant1 eqeq12d 3adant3r
    wceq mulcan bitrd ) ADEZBDEZCDEZCFGZHIACJKZBCJKZRZCAJKZCBJKZRZABRUAUBUCUGUJ
    LUDUAUBUCIUEUHUFUIUAUCUEUHRUBACMNUBUCUFUIRUABCMOPQABCST $.
    $( [26-Aug-2008] $) $( [21-Jan-2005] $)

  ${
    mul0or.1 $e |- A e. CC $.
    ${
      mul0or.2 $e |- B e. CC $.
      $( If a product is zero, one of its factors must be zero.  Theorem I.11
         of [Apostol] p. 18. $)
      mul0ori $p |- ( ( A x. B ) = 0 <-> ( A = 0 \/ B = 0 ) ) $=
        ( cmul co cc0 wceq wo wn wne wi df-ne mul01i eqeq2i cc wcel wb syl6eq
        wa mulcan mp3an12 mpan syl5bbr biimpd sylbir com12 opreq1 mul02i opreq2
        0cn orrd jaoi impbii ) ABEFZGHZAGHZBGHZIUPUQURUQJZUPURUSAGKZUPURLAGMUTU
        PURUPUOAGEFZHZUTURVAGUOACNZOAPQZUTVBURRZCBPQGPQVDUTTVEDUKBGAUAUBUCUDUEU
        FUGULUQUPURUQUOGBEFGAGBEUHBDUISURUOVAGBGAEUJVCSUMUN $.
        $( [7-Oct-1999] $)
    $}

    $( A number is zero iff its square is zero (where square is represented
       using multiplication). $)
    msq0i $p |- ( ( A x. A ) = 0 <-> A = 0 ) $=
      ( cmul co cc0 wceq wo mul0ori oridm bitri ) AACDEFAEFZKGKAABBHKIJ $.
      $( [28-Jul-1999] $)
  $}

  $( If a product is zero, one of its factors must be zero.  Theorem I.11 of
     [Apostol] p. 18. $)
  mul0or $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( ( A x. B ) = 0 <-> ( A = 0 \/ B = 0 ) ) ) $=
    ( cc wcel cmul co cc0 wceq wo cif opreq1 eqeq1d eqeq1 orbi1d bibi12d opreq2
    wb orbi2d 0cn elimel mul0ori dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZIZQUCAGJZBEF
    ZGHZUJGHZUHIZQUJUDBGJZEFZGHZUMUOGHZIZQABGGAUJHZUFULUIUNUTUEUKGAUJBEKLUTUGUM
    UHAUJGMNOBUOHZULUQUNUSVAUKUPGBUOUJEPLVAUHURUMBUOGMROUJUOAGCSTBGCSTUAUB $.
    $( [9-Oct-1999] $)

  $( The product of two nonzero numbers is nonzero.  (The proof was shortened
     by Andrew Salmon, 19-Nov-2011.) $)
  mulne0b $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A =/= 0 /\ B =/= 0 ) <->
               ( A x. B ) =/= 0 ) ) $=
    ( cc wcel wa cmul co cc0 wne wceq wo wn mul0or necon3abid neanior syl6rbbr
    ) ACDBCDEZABFGZHIAHJBHJKZLAHIBHIEQSRHABMNAHBHOP $.
    $( [19-Nov-2011] $) $( [1-Aug-2004] $)

  $( The product of two nonzero numbers is nonzero. $)
  mulne0 $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) ) ->
               ( A x. B ) =/= 0 ) $=
    ( cc wcel cc0 wne cmul co wa mulne0b biimpa an4s ) ACDZBCDZAEFZBEFZABGHEFZM
    NIOPIQABJKL $.
    $( [30-Dec-2007] $)

  ${
    muln0.1 $e |- A e. CC $.
    muln0.2 $e |- B e. CC $.
    muln0.3 $e |- A =/= 0 $.
    muln0.4 $e |- B =/= 0 $.
    $( The product of two nonzero numbers is nonzero. $)
    mulne0i $p |- ( A x. B ) =/= 0 $=
      ( cc wcel cc0 wne cmul co mulne0 mp4an ) AGHAIJBGHBIJABKLIJCEDFABMN $.
      $( [15-Feb-1995] $)
  $}

  $( Property of numbers whose product equals their sum.  Equation 5 of
     [Kreyszig] p. 12. $)
  muleqadd $p |- ( ( A e. CC /\ B e. CC ) ->
             ( ( A x. B ) = ( A + B ) <-> ( ( A - 1 ) x. ( B - 1 ) ) = 1 ) ) $=
    ( cc wcel wa c1 cmin co cmul wceq caddc ax-1cn mulsub mpanr2 mpanl2 mulid1i
    cc0 mulid1 syl11anc wb opreq2i a1i opreqan12d opreq12d addsub mp3an2 3eqtrd
    mulcl addcl eqeq1d addid2i eqeq2i subcl 0cn addcan2 mp3an23 syl5rbbr subeq0
    syl 3bitr2rd ) ACDZBCDZEZAFGHBFGHIHZFJABIHZABKHZGHZFKHZFJZVGQJZVEVFJZVCVDVH
    FVCVDVEFFIHZKHZAFIHZBFIHZKHZGHZVEFKHZVFGHZVHVAFCDZVBVDVQJZLVAVTEVBVTWALAFBF
    MNOVCVMVRVPVFGVMVRJVCVLFVEKFLPUAUBVAVBVNAVOBKARBRUCUDVCVECDZVFCDZVSVHJZABUH
    ZABUIZWBVTWCWDLVEFVFUEUFSUGUJVIVHQFKHZJZVCVJWGFVHFLUKULVCVGCDZWHVJTZVCWBWCW
    IWEWFVEVFUMSWIQCDVTWJUNLVGQFUOUPUSUQVCWBWCVJVKTWEWFVEVFURSUT $.
    $( [13-Nov-2006] $)

  ${
    $d x y A $.  $d x y B $.
    $( Existential uniqueness of reciprocals.  Theorem I.8 of [Apostol] p. 18.
       (Revised by Mario Carneiro, 17-Feb-2014.) $)
    receu $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                  E! x e. CC ( B x. x ) = A ) $=
      ( vy cc wcel w3a cv cmul co wceq wrex wa wi wral c1 3adant1 opreq2 eqeq1d
      syl11anc cc0 wreu recex simprl simpll mulcl opreq1 ad2antll simplr mulass
      wne syl111anc mulid2 syl 3eqtr3d rcla4ev exp32 rexlimdv 3adant3 mpd eqtr3
      mulcan syl5ib 3expa expcom ralrimivv reu4 sylanbrc ) BEFZCEFZCUAUKZGZCAHZ
      IJZBKZAELZVOCDHZIJZBKZMZVMVQKZNZDEOAEOVOAEUBVLVRPKZDELZVPVJVKWDVIDCUCQVIV
      JWDVPNVKVIVJMZWCVPDEWEVQEFZWCVPWEWFWCMZMZVQBIJZEFZCWIIJZBKZVPWHWFVIWJWEWF
      WCUDZVIVJWGUEZVQBUFTWHVRBIJZPBIJZWKBWCWOWPKWEWFVRPBIUGUHWHVJWFVIWOWKKVIVJ
      WGUIWMWNCVQBUJULWHVIWPBKWNBUMUNUOVOWLAWIEVMWIKVNWKBVMWICIRSUPTUQURUSUTVLW
      BADEEVJVKVMEFZWFMZWBNVIWRVJVKMZWBWQWFWSWBVTVNVRKWQWFWSGWAVNVRBVAVMVQCVBVC
      VDVEQVFVOVSADEWAVNVRBVMVQCIRSVGVH $.
      $( [17-Feb-2014] $) $( [29-Jan-1995] $)
  $}

  ${
    $d x y $.
    $( Multiplication maps nonzero complex numbers to nonzero complex numbers.
       (Contributed by Steve Rodriguez, 23-Feb-2007.) $)
    mulnzcnopr $p |- ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) :
                             ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) -->
                             ( CC \ { 0 } ) $=
      ( vx vy cc cc0 csn cdif cxp cmul wf wfn cv co wcel wral ffnoprv wss mp2an
      wa wne eldifsn cres ax-mulopr mpbi simpli xpss12 fnssres oprvres ad2ant2r
      difss mulcl mulne0 jca syl2anb sylibr eqeltrd rgen2a mpbir2an ) CDEZFZUSG
      ZUSHUTUAZIVAUTJZAKZBKZVALZUSMZBUSNAUSNABUSUSUSVAOHCCGZJZUTVGPZVBVHVCVDHLZ
      CMZBCNACNZVGCHIVHVLRUBABCCCHOUCUDUSCPZVMVICURUIZVNUSCUSCUEQVGUTHUFQVFABUS
      VCUSMZVDUSMZRZVEVJUSVCVDUSUSHUGVQVKVJDSZRZVJUSMVOVCCMZVCDSZRZVDCMZVDDSZRZ
      VSVPVCCDTVDCDTWBWERVKVRVTWCVKWAWDVCVDUJUHVCVDUKULUMVJCDTUNUOUPUQ $.
      $( [17-Feb-2007] $)
  $}


