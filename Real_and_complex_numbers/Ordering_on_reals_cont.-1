$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordering on reals (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    lt_2.1 $e |- A e. RR $.

    ${
      lt_2.2 $e |- B e. RR $.

      ${
        lt_2.3 $e |- C e. RR $.
        $( Addition to both sides of 'less than'.  Theorem I.18 of [Apostol]
           p. 20. $)
        ltadd1i $p |- ( A < B <-> ( A + C ) < ( B + C ) ) $=
          ( clt wbr caddc co ltadd2i recni addcomi breq12i bitri ) ABGHCAIJZCBI
          JZGHACIJZBCIJZGHABCDEFKPRQSGCACFLZADLMCBTBELMNO $.
          $( [21-Jan-1997] $)

        $( Addition to both sides of 'less than or equal to'. $)
        leadd1i $p |- ( A <_ B <-> ( A + C ) <_ ( B + C ) ) $=
          ( clt wbr wn caddc co cle ltadd1i notbii lenlti readdcli 3bitr4i ) BA
          GHZIBCJKZACJKZGHZIABLHTSLHRUABACEDFMNABDEOTSACDFPBCEFPOQ $.
          $( [11-Aug-1999] $)

        $( Addition to both sides of 'less than or equal to'. $)
        leadd2i $p |- ( A <_ B <-> ( C + A ) <_ ( C + B ) ) $=
          ( cle wbr caddc co leadd1i recni addcomi breq12i bitri ) ABGHACIJZBCI
          JZGHCAIJZCBIJZGHABCDEFKPRQSGACADLCFLZMBCBELTMNO $.
          $( [11-Aug-1999] $)

        $( 'Less than' relationship between subtraction and addition.  (The
           proof was shortened by Andrew Salmon, 19-Nov-2011.) $)
        ltsubaddi $p |- ( ( A - B ) < C <-> A < ( C + B ) ) $=
          ( cmin co clt wbr caddc resubcli ltadd1i wcel wceq recni npcan breq1i
          cc mp2an bitri ) ABGHZCIJUBBKHZCBKHZIJAUDIJUBCBABDELFEMUCAUDIASNBSNUC
          AOADPBEPABQTRUA $.
          $( [19-Nov-2011] $) $( [21-Jan-1997] $)

        $( 'Less than or equal to' relationship between subtraction and
           addition.  (The proof was shortened by Andrew Salmon,
           19-Nov-2011.) $)
        lesubaddi $p |- ( ( A - B ) <_ C <-> A <_ ( C + B ) ) $=
          ( cmin co cle wbr caddc resubcli leadd1i wcel wceq recni npcan breq1i
          cc mp2an bitri ) ABGHZCIJUBBKHZCBKHZIJAUDIJUBCBABDELFEMUCAUDIASNBSNUC
          AOADPBEPABQTRUA $.
          $( [19-Nov-2011] $) $( [30-Sep-1999] $)

        ${
          lt.4 $e |- D e. RR $.
          $( Adding both side of two inequalities.  Theorem I.25 of [Apostol]
             p. 20. $)
          lt2addi $p |- ( ( A < C /\ B < D ) -> ( A + B ) < ( C + D ) ) $=
            ( clt wbr caddc co ltadd1i ltadd2i readdcli lttri syl2anb ) ACIJABK
            LZCBKLZIJSCDKLZIJRTIJBDIJACBEGFMBDCFHGNRSTABEFOCBGFOCDGHOPQ $.
            $( [14-May-1999] $)

          $( Adding both side of two inequalities. $)
          le2addi $p |- ( ( A <_ C /\ B <_ D ) -> ( A + B ) <_ ( C + D ) ) $=
            ( cle wbr caddc co leadd1i leadd2i readdcli letri syl2anb ) ACIJABK
            LZCBKLZIJSCDKLZIJRTIJBDIJACBEGFMBDCFHGNRSTABEFOCBGFOCDGHOPQ $.
            $( [16-Sep-1999] $)
        $}
      $}

      $( Addition of 2 positive numbers is positive.  (The proof was shortened
         by Andrew Salmon, 19-Nov-2011.) $)
      addgt0i $p |- ( ( 0 < A /\ 0 < B ) -> 0 < ( A + B ) ) $=
        ( cc0 clt wbr wa caddc co 0cn addid1i 0re lt2addi syl5eqbrr ) EAFGEBFGH
        EEEIJABIJFEKLEEABMMCDNO $.
        $( [19-Nov-2011] $) $( [16-May-1999] $)

      $( Addition of 2 nonnegative numbers is nonnegative.  (The proof was
         shortened by Andrew Salmon, 19-Nov-2011.) $)
      addge0i $p |- ( ( 0 <_ A /\ 0 <_ B ) -> 0 <_ ( A + B ) ) $=
        ( cc0 cle wbr wa caddc co 0cn addid1i 0re le2addi syl5eqbrr ) EAFGEBFGH
        EEEIJABIJFEKLEEABMMCDNO $.
        $( [19-Nov-2011] $) $( [28-May-1999] $)

      $( Addition of nonnegative and positive numbers is positive.  (The proof
         was shortened by Andrew Salmon, 19-Nov-2011.) $)
      addgegt0i $p |- ( ( 0 <_ A /\ 0 < B ) -> 0 < ( A + B ) ) $=
        ( cc0 cle wbr clt wceq wo caddc 0re leloei addgt0i recni addid2i opreq1
        co syl5eqr breq2d biimpa jaoian sylanb ) EAFGEAHGZEAIZJEBHGZEABKRZHGZEA
        LCMUDUFUHUEABCDNUEUFUHUEBUGEHUEBEBKRUGBBDOPEABKQSTUAUBUC $.
        $( [19-Nov-2011] $) $( [25-Sep-1999] $)

      ${
        addgt0i.3 $e |- 0 < A $.
        addgt0i.4 $e |- 0 < B $.
        $( Addition of 2 positive numbers is positive. $)
        addgt0ii $p |- 0 < ( A + B ) $=
          ( cc0 clt wbr caddc co addgt0i mp2an ) GAHIGBHIGABJKHIEFABCDLM $.
          $( [18-May-1999] $)
      $}

      $( Two nonnegative numbers are zero iff their sum is zero. $)
      add20i $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
        ( ( A + B ) = 0 <-> ( A = 0 /\ B = 0 ) ) ) $=
        ( cc0 cle wbr wa caddc co wceq clt wo 0re leloei wn recni opreq1 eqeq1d
        syl5eqr wi readdcli lttri3i simprbi addgt0i nsyl3 pm2.21d addid2i eqcom
        biimprd biimpi jctild addid1i opreq2 jctird ccase2 syl2anb 0cn sylan9eq
        syl6eq impbid1 ) EAFGZEBFGZHABIJZEKZAEKZBEKZHZVBEALGZEAKZMEBLGZEBKZMVEV
        HUAZVCEANCOEBNDOVIVKVJVLVMVIVKHZVEVHVEEVDLGZVNVEVDELGPVOPVDEABCDUBNUCUD
        ABCDUEUFUGVJVEVGVFVJVGVEVJBVDEVJBEBIJZVDBBDQUHEABIRTSUJVJVFEAUIUKULVLVE
        VFVGVLVFVEVLAVDEVLAAEIJVDAACQUMEBAIUNTSUJVLVGEBUIUKUOUPUQVFVGVDVPEAEBIR
        VGVPEEIJEBEEIUNEURUMUTUSVA $.
        $( [28-Jul-1999] $)

      $( Negative of both sides of 'less than'.  Theorem I.23 of [Apostol]
         p. 20. $)
      ltnegi $p |- ( A < B <-> -u B < -u A ) $=
        ( clt wbr cneg caddc renegcli readdcli ltadd1i cc0 recni negidi opreq1i
        co addassi addid2i 3eqtr3i add12i opreq2i addid1i 3eqtri breq12i bitri
        ) ABEFAAGZBGZHPZHPZBUHHPZEFUGUFEFABUHCDUFUGACIZBDIZJKUIUGUJUFEAUFHPZUGH
        PLUGHPUIUGUMLUGHAACMZNOAUFUGUNUFUKMZUGULMZQUGUPRSUJUFBUGHPZHPUFLHPUFBUF
        UGBDMZUOUPTUQLUFHBURNUAUFUOUBUCUDUE $.
        $( [21-Jan-1997] $)

      $( Negative of both sides of 'less than or equal to'. $)
      lenegi $p |- ( A <_ B <-> -u B <_ -u A ) $=
        ( clt wbr wceq cneg cle ltnegi recni neg11i eqcom bitr2i orbi12i leloei
        wo renegcli 3bitr4i ) ABEFZABGZQBHZAHZEFZUBUCGZQABIFUBUCIFTUDUAUEABCDJU
        EBAGUABABDKACKLBAMNOABCDPUBUCBDRACRPS $.
        $( [1-Aug-1999] $)

      $( Contraposition of negative in 'less than'. $)
      ltnegcon2i $p |- ( A < -u B <-> B < -u A ) $=
        ( cneg clt wbr renegcli ltnegi recni negnegi breq1i bitri ) ABEZFGNEZAE
        ZFGBPFGANCBDHIOBPFBBDJKLM $.
        $( [14-May-1999] $)

      $( The product of two nonnegative numbers is nonnegative. $)
      mulge0i $p |- ( ( 0 <_ A /\ 0 <_ B ) -> 0 <_ ( A x. B ) ) $=
        ( cc0 cle wbr clt wceq wo cmul co 0re leloei mulgt0i remulcli syl recni
        wa syl5eqr ltlei mul02i opreq1 eqlei mul01i opreq2 ccase2 syl2anb ) EAF
        GEAHGZEAIZJEBHGZEBIZJEABKLZFGZEBFGEAMCNEBMDNUIUKUJULUNUIUKSEUMHGUNABCDO
        EUMMABCDPZUAQUJEUMIZUNUJEEBKLUMBBDRUBEABKUCTEUMMUOUDZQULUPUNULEAEKLUMAA
        CRUEEBAKUFTUQQUGUH $.
        $( [30-Jul-1999] $)
    $}

    $( 'Less than or equal to' is reflexive. $)
    leidi $p |- A <_ A $=
      ( cr wcel cle wbr leid ax-mp ) ACDAAEFBAGH $.
      $( [18-Aug-1999] $)

    $( Positive means nonzero (useful for ordering theorems involving
       division). $)
    gt0ne0i $p |- ( 0 < A -> A =/= 0 ) $=
      ( cc0 0re ltnei ) CADBE $.
      $( [16-Sep-1999] $)

    ${
      lesub0.1 $e |- B e. RR $.
      $( Lemma to show a nonnegative number is zero.  (The proof was shortened
         by Andrew Salmon, 19-Nov-2011.) $)
      lesub0i $p |- ( ( 0 <_ A /\ B <_ ( B - A ) ) <-> A = 0 ) $=
        ( cmin co cle wbr cc0 wa wceq caddc recni pncan3i addid2i eqtr4i breq2i
        resubcli 0re 3bitr4i leadd2i leadd1i anbi1i ancom letri3i ) BBAEFZGHZIA
        GHZJAIGHZUHJUHUGJAIKUGUIUHABLFZAUFLFZGHUJIBLFZGHUGUIUKULUJGUKBULABACMBD
        MZNBUMOPQBUFADBADCRCUAAIBCSDUBTUCUHUGUDAICSUET $.
        $( [19-Nov-2011] $) $( [8-Oct-1999] $)
    $}

    $( A nonzero square is positive.  Theorem I.20 of [Apostol] p. 20. $)
    msqgt0i $p |- ( A =/= 0 -> 0 < ( A x. A ) ) $=
      ( cc0 wne clt wbr wo cmul co 0re lttri2i cneg ltnegi neg0 breq1i renegcli
      bitri mulgt0i anidms sylbi recni mul2negi syl6breq jaoi ) ACDACEFZCAEFZGC
      AAHIZEFZACBJKUEUHUFUECALZEFZUHUECLZUIEFUJACBJMUKCUIENOQUJCUIUIHIZUGEUJCUL
      EFUIUIABPZUMRSAAABUAZUNUBUCTUFUHAABBRSUDT $.
      $( [23-May-2007] $) $( [17-Jan-1997] $)

    $( A square is nonnegative.  (The proof was shortened by Andrew Salmon,
       19-Nov-2011.) $)
    msqge0i $p |- 0 <_ ( A x. A ) $=
      ( cc0 cmul co cle wbr wceq 0re leidi opreq2 recni mul01i syl6eq syl5breqr
      wne clt msqgt0i remulcli ltlei syl pm2.61ine ) CAADEZFGZACACHZCCUCFCIJUEU
      CACDECACADKAABLMNOACPCUCQGUDABRCUCIAABBSTUAUB $.
      $( [19-Nov-2011] $) $( [14-May-1999] $)
  $}

  $( Two nonnegative numbers are zero iff their sum is zero.  (Contributed by
     Jeff Madsen, 2-Sep-2009.) $)
  add20 $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) )
                        -> ( ( A + B ) = 0 <-> ( A = 0 /\ B = 0 ) ) ) $=
    ( cr wcel cc0 cle wbr caddc co wceq wa wb wi cif breq2 anbi1d eqeq1 bibi12d
    eqeq1d imbi12d opreq1 anbi2d opreq2 0re elimel add20i dedth2h imp an4s ) AC
    DZBCDZEAFGZEBFGZABHIZEJZAEJZBEJZKZLZUJUKKULUMKZUSUJUKUTUSMEUJAENZFGZUMKZVAB
    HIZEJZVAEJZUQKZLZMVBEUKBENZFGZKZVAVIHIZEJZVFVIEJZKZLZMABEEAVAJZUTVCUSVHVQUL
    VBUMAVAEFOPVQUOVEURVGVQUNVDEAVABHUASVQUPVFUQAVAEQPRTBVIJZVCVKVHVPVRUMVJVBBV
    IEFOUBVRVEVMVGVOVRVDVLEBVIVAHUCSVRUQVNVFBVIEQUBRTVAVIAECUDUEBECUDUEUFUGUHUI
    $.
    $( [2-Sep-2009] $)

  $( A nonzero square is positive.  Theorem I.20 of [Apostol] p. 20. $)
  msqgt0 $p |- ( ( A e. RR /\ A =/= 0 ) -> 0 < ( A x. A ) ) $=
    ( cr wcel cc0 wne cmul co clt wbr wi cif wceq neeq1 opreq12d breq2d imbi12d
    id 0re elimel msqgt0i dedth imp ) ABCZADEZDAAFGZHIZUCUDUFJUCADKZDEZDUGUGFGZ
    HIZJADAUGLZUDUHUFUJAUGDMUKUEUIDHUKAUGAUGFUKQZULNOPUGADBRSTUAUB $.
    $( [24-May-2007] $) $( [6-May-1999] $)

  $( A square is nonnegative. $)
  msqge0 $p |- ( A e. RR -> 0 <_ ( A x. A ) ) $=
    ( cr wcel cc0 cmul co cle wbr cif wceq id opreq12d breq2d 0re msqge0i dedth
    elimel ) ABCZDAAEFZGHDRADIZTEFZGHADATJZSUADGUBATATEUBKZUCLMTADBNQOP $.
    $( [23-May-2007] $)

  ${
    gt0ne0i.1 $e |- A e. RR $.
    gt0ne0i.2 $e |- 0 < A $.
    $( Positive implies nonzero. $)
    gt0ne0ii $p |- A =/= 0 $=
      ( cc0 clt wbr wne gt0ne0i ax-mp ) DAEFADGCABHI $.
      $( [15-May-1999] $)
  $}

  $( Positive implies nonzero. $)
  gt0ne0 $p |- ( ( A e. RR /\ 0 < A ) -> A =/= 0 ) $=
    ( cr wcel cc0 clt wbr wne wi cif wceq breq2 neeq1 imbi12d 0re gt0ne0i dedth
    elimel imp ) ABCZDAEFZADGZSTUAHDSADIZEFZUBDGZHADAUBJTUCUAUDAUBDEKAUBDLMUBAD
    BNQOPR $.
    $( [25-Feb-2005] $) $( [3-Oct-1999] $)

  $( Addition to both sides of 'less than'. $)
  ltadd1 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( A + C ) < ( B + C ) ) ) $=
    ( cr wcel clt wbr caddc co wb cc0 cif wceq opreq1 breq1d bibi12d opreq2 0re
    breq1 elimel breq2 breq2d breq12d bibi2d ltadd1i dedth3h ) ADEZBDEZCDEZABFG
    ZACHIZBCHIZFGZJUGAKLZBFGZUNCHIZULFGZJUNUHBKLZFGZUPURCHIZFGZJUSUNUICKLZHIZUR
    VBHIZFGZJABCKKKAUNMZUJUOUMUQAUNBFSVFUKUPULFAUNCHNOPBURMZUOUSUQVABURUNFUAVGU
    LUTUPFBURCHNUBPCVBMZVAVEUSVHUPVCUTVDFCVBUNHQCVBURHQUCUDUNURVBAKDRTBKDRTCKDR
    TUEUF $.
    $( [12-Nov-1999] $)

  $( Addition to both sides of 'less than'. $)
  ltadd2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( C + A ) < ( C + B ) ) ) $=
    ( cr wcel w3a clt wbr caddc co ltadd1 wceq cc addcom syl2an 3adant2 3adant1
    recn breq12d bitrd ) ADEZBDEZCDEZFZABGHACIJZBCIJZGHCAIJZCBIJZGHABCKUDUEUGUF
    UHGUAUCUEUGLZUBUAAMECMEZUIUCARCRZACNOPUBUCUFUHLZUAUBBMEUJULUCBRUKBCNOQST $.
    $( [12-Nov-1999] $)

  $( Addition to both sides of 'less than or equal to'. $)
  leadd1 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( A + C ) <_ ( B + C ) ) ) $=
    ( cr wcel cle wbr caddc co wb cc0 cif wceq opreq1 breq1d bibi12d opreq2 0re
    breq1 elimel breq2 breq2d breq12d bibi2d leadd1i dedth3h ) ADEZBDEZCDEZABFG
    ZACHIZBCHIZFGZJUGAKLZBFGZUNCHIZULFGZJUNUHBKLZFGZUPURCHIZFGZJUSUNUICKLZHIZUR
    VBHIZFGZJABCKKKAUNMZUJUOUMUQAUNBFSVFUKUPULFAUNCHNOPBURMZUOUSUQVABURUNFUAVGU
    LUTUPFBURCHNUBPCVBMZVAVEUSVHUPVCUTVDFCVBUNHQCVBURHQUCUDUNURVBAKDRTBKDRTCKDR
    TUEUF $.
    $( [18-Oct-1999] $)

  $( Addition to both sides of 'less than or equal to'. $)
  leadd2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( C + A ) <_ ( C + B ) ) ) $=
    ( cr wcel w3a cle wbr caddc co leadd1 wceq cc addcom syl2an 3adant2 3adant1
    recn breq12d bitrd ) ADEZBDEZCDEZFZABGHACIJZBCIJZGHCAIJZCBIJZGHABCKUDUEUGUF
    UHGUAUCUEUGLZUBUAAMECMEZUIUCARCRZACNOPUBUCUFUHLZUAUBBMEUJULUCBRUKBCNOQST $.
    $( [26-Oct-1999] $)

  $( 'Less than' relationship between subtraction and addition. $)
  ltsubadd $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( ( A - B ) < C <-> A < ( C + B ) ) ) $=
    ( cr wcel cmin co clt wbr caddc wb cc0 cif opreq1 breq1d bibi12d opreq2 0re
    wceq elimel breq1 breq2d breq2 ltsubaddi dedth3h ) ADEZBDEZCDEZABFGZCHIZACB
    JGZHIZKUFALMZBFGZCHIZUMUKHIZKUMUGBLMZFGZCHIZUMCUQJGZHIZKURUHCLMZHIZUMVBUQJG
    ZHIZKABCLLLAUMSZUJUOULUPVFUIUNCHAUMBFNOAUMUKHUAPBUQSZUOUSUPVAVGUNURCHBUQUMF
    QOVGUKUTUMHBUQCJQUBPCVBSZUSVCVAVECVBURHUCVHUTVDUMHCVBUQJNUBPUMUQVBALDRTBLDR
    TCLDRTUDUE $.
    $( [21-Jan-1997] $)

  $( 'Less than' relationship between subtraction and addition. $)
  ltsubadd2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) < C <-> A < ( B + C ) ) ) $=
    ( cr wcel w3a cmin co clt caddc ltsubadd wceq cc recn addcom syl2an 3adant1
    wbr breq2d bitr4d ) ADEZBDEZCDEZFZABGHCIRACBJHZIRABCJHZIRABCKUDUFUEAIUBUCUF
    UELZUAUBBMECMEUGUCBNCNBCOPQST $.
    $( [21-Jan-1997] $)

  $( 'Less than or equal to' relationship between subtraction and addition. $)
  lesubadd $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) <_ C <-> A <_ ( C + B ) ) ) $=
    ( cr wcel cmin co cle wbr caddc wb cc0 cif opreq1 breq1d bibi12d opreq2 0re
    wceq elimel breq1 breq2d breq2 lesubaddi dedth3h ) ADEZBDEZCDEZABFGZCHIZACB
    JGZHIZKUFALMZBFGZCHIZUMUKHIZKUMUGBLMZFGZCHIZUMCUQJGZHIZKURUHCLMZHIZUMVBUQJG
    ZHIZKABCLLLAUMSZUJUOULUPVFUIUNCHAUMBFNOAUMUKHUAPBUQSZUOUSUPVAVGUNURCHBUQUMF
    QOVGUKUTUMHBUQCJQUBPCVBSZUSVCVAVECVBURHUCVHUTVDUMHCVBUQJNUBPUMUQVBALDRTBLDR
    TCLDRTUDUE $.
    $( [17-Nov-2004] $)

  $( 'Less than or equal to' relationship between subtraction and addition. $)
  lesubadd2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) <_ C <-> A <_ ( B + C ) ) ) $=
    ( cr wcel w3a cmin co cle caddc lesubadd wceq cc recn addcom syl2an 3adant1
    wbr breq2d bitr4d ) ADEZBDEZCDEZFZABGHCIRACBJHZIRABCJHZIRABCKUDUFUEAIUBUCUF
    UELZUAUBBMECMEUGUCBNCNBCOPQST $.
    $( [10-Aug-1999] $)

  $( 'Less than' relationship between addition and subtraction. $)
  ltaddsub $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A + B ) < C <-> A < ( C - B ) ) ) $=
    ( cr wcel w3a caddc co clt wbr cmin cle wn wb lesubadd 3com13 resubcl lenlt
    wa sylan 3impa readdcl sylan2 3impb 3coml 3bitr3rd con4bid ) ADEZBDEZCDEZFZ
    ABGHZCIJZACBKHZIJZUKUNALJZCULLJZUOMZUMMZUJUIUHUPUQNCBAOPUJUIUHUPURNZUJUIUHU
    TUJUISUNDEUHUTCBQUNARTUAPUJUHUIUQUSNZUJUHUIVAUHUISUJULDEVAABUBCULRUCUDUEUFU
    G $.
    $( [17-Nov-2004] $)

  $( 'Less than' relationship between addition and subtraction. $)
  ltaddsub2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A + B ) < C <-> B < ( C - A ) ) ) $=
    ( cr wcel w3a caddc co clt wbr cmin wceq cc addcom syl2an 3adant3 breq1d wb
    recn ltaddsub 3com12 bitrd ) ADEZBDEZCDEZFZABGHZCIJBAGHZCIJZBCAKHIJZUFUGUHC
    IUCUDUGUHLZUEUCAMEBMEUKUDASBSABNOPQUDUCUEUIUJRBACTUAUB $.
    $( [17-Nov-2004] $)

  $( 'Less than or equal to' relationship between addition and subtraction. $)
  leaddsub $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A + B ) <_ C <-> A <_ ( C - B ) ) ) $=
    ( cr wcel w3a caddc co cle wbr cmin clt wn wb ltsubadd 3com13 resubcl ltnle
    wa sylan 3impa readdcl sylan2 3impb 3coml 3bitr3rd con4bid ) ADEZBDEZCDEZFZ
    ABGHZCIJZACBKHZIJZUKUNALJZCULLJZUOMZUMMZUJUIUHUPUQNCBAOPUJUIUHUPURNZUJUIUHU
    TUJUISUNDEUHUTCBQUNARTUAPUJUHUIUQUSNZUJUHUIVAUHUISUJULDEVAABUBCULRUCUDUEUFU
    G $.
    $( [6-Apr-2005] $)

  $( 'Less than or equal to' relationship between and addition and
     subtraction. $)
  leaddsub2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A + B ) <_ C <-> B <_ ( C - A ) ) ) $=
    ( cr wcel w3a caddc co cle wbr cmin wceq cc addcom syl2an 3adant3 breq1d wb
    recn leaddsub 3com12 bitrd ) ADEZBDEZCDEZFZABGHZCIJBAGHZCIJZBCAKHIJZUFUGUHC
    IUCUDUGUHLZUEUCAMEBMEUKUDASBSABNOPQUDUCUEUIUJRBACTUAUB $.
    $( [6-Apr-2005] $)

  $( Swap subtrahends in an inequality. $)
  suble $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) <_ C <-> ( A - C ) <_ B ) ) $=
    ( cr wcel w3a cmin co cle wbr caddc lesubadd wb lesubadd2 3com23 bitr4d ) A
    DEZBDEZCDEZFABGHCIJACBKHIJZACGHBIJZABCLQSRUATMACBNOP $.
    $( [29-Sep-2005] $)

  $( Swap subtrahends in an inequality.  (The proof was shortened by Andrew
     Salmon, 19-Nov-2011.) $)
  lesub $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( A <_ ( B - C ) <-> C <_ ( B - A ) ) ) $=
    ( cr wcel cmin co cle wbr wb w3a caddc leaddsub leaddsub2 bitr3d 3com23 ) A
    DEZCDEZBDEZABCFGHIZCBAFGHIZJQRSKACLGBHITUAACBMACBNOP $.
    $( [19-Nov-2011] $) $( [29-Sep-2005] $)

  ${
    ltsubadd.1 $e |- A e. RR $.
    ltsubadd.2 $e |- B e. RR $.
    ltsubadd.3 $e |- C e. RR $.
    $( 'Less than' relationship between subtraction and addition. $)
    ltsubadd2i $p |- ( ( A - B ) < C <-> A < ( B + C ) ) $=
      ( cr wcel cmin co clt wbr caddc wb ltsubadd2 mp3an ) AGHBGHCGHABIJCKLABCM
      JKLNDEFABCOP $.
      $( [21-Jan-1997] $)

    $( 'Less than or equal to' relationship between subtraction and
       addition. $)
    lesubadd2i $p |- ( ( A - B ) <_ C <-> A <_ ( B + C ) ) $=
      ( cr wcel cmin co cle wbr caddc wb lesubadd2 mp3an ) AGHBGHCGHABIJCKLABCM
      JKLNDEFABCOP $.
      $( [3-Aug-1999] $)

    $( 'Less than' relationship between subtraction and addition. $)
    ltaddsubi $p |- ( ( A + B ) < C <-> A < ( C - B ) ) $=
      ( cr wcel caddc co clt wbr cmin wb ltaddsub mp3an ) AGHBGHCGHABIJCKLACBMJ
      KLNDEFABCOP $.
      $( [14-May-1999] $)

    $( Multiplication of both sides of 'less than' by a positive number.
       Theorem I.19 of [Apostol] p. 20. $)
    ltmullem $p |- ( 0 < C -> ( A < B -> ( A x. C ) < ( B x. C ) ) ) $=
      ( cc0 clt wbr cmin cmul resubcli caddc recni addid2i breq1i 0re ltaddsubi
      co remulcli mulgt0i expcom bitr3i subdiri breq2i 3bitr2ri 3imtr4g ) GCHIZ
      GBAJSZHIZGUICKSZHIZABHIZACKSZBCKSZHIZUJUHULUICBAEDLFUAUBUMGAMSZBHIUJUQABH
      AADNZOPGABQDERUCULGUOUNJSZHIGUNMSZUOHIUPUKUSGHBACBENURCFNUDUEGUNUOQACDFTZ
      BCEFTRUTUNUOHUNUNVANOPUFUG $.
      $( [15-May-1999] $)
  $}

  $( 'Less than' relationship between subtraction and addition. $)
  ltsub23 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) < C <-> ( A - C ) < B ) ) $=
    ( cr wcel w3a cmin co clt wbr caddc ltsubadd wb ltsubadd2 3com23 bitr4d ) A
    DEZBDEZCDEZFABGHCIJACBKHIJZACGHBIJZABCLQSRUATMACBNOP $.
    $( [4-Oct-1999] $)

  $( 'Less than' relationship between subtraction and addition. $)
  ltsub13 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( A < ( B - C ) <-> C < ( B - A ) ) ) $=
    ( cr wcel cmin co clt wbr wb w3a caddc ltaddsub ltaddsub2 bitr3d 3com23 ) A
    DEZCDEZBDEZABCFGHIZCBAFGHIZJQRSKACLGBHITUAACBMACBNOP $.
    $( [17-Nov-2004] $)

  $( Adding both sides of two 'less than' relations.  Theorem I.25 of [Apostol]
     p. 20. $)
  lt2add $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A < C /\ B < D ) -> ( A + B ) < ( C + D ) ) ) $=
    ( cr wcel clt wbr wa caddc co cc0 cif wceq breq1 anbi1d imbi12d 0re elimel
    wi opreq1 breq1d anbi2d opreq2 breq2 breq2d lt2addi dedth4h ) AEFZBEFZCEFZD
    EFZACGHZBDGHZIZABJKZCDJKZGHZTUIALMZCGHZUNIZUSBJKZUQGHZTUTUJBLMZDGHZIZUSVDJK
    ZUQGHZTUSUKCLMZGHZVEIZVGVIDJKZGHZTVJVDULDLMZGHZIZVGVIVNJKZGHZTABCDLLLLAUSNZ
    UOVAURVCVSUMUTUNAUSCGOPVSUPVBUQGAUSBJUAUBQBVDNZVAVFVCVHVTUNVEUTBVDDGOUCVTVB
    VGUQGBVDUSJUDUBQCVINZVFVKVHVMWAUTVJVECVIUSGUEPWAUQVLVGGCVIDJUAUFQDVNNZVKVPV
    MVRWBVEVOVJDVNVDGUEUCWBVLVQVGGDVNVIJUDUFQUSVDVIVNALERSBLERSCLERSDLERSUGUH
    $.
    $( [15-Aug-1999] $)

  $( Adding both sides of two 'less than or equal to' relations. $)
  le2add $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A <_ C /\ B <_ D ) -> ( A + B ) <_ ( C + D ) ) ) $=
    ( cr wcel cle wbr wa caddc co cc0 cif wceq breq1 anbi1d imbi12d 0re elimel
    wi opreq1 breq1d anbi2d opreq2 breq2 breq2d le2addi dedth4h ) AEFZBEFZCEFZD
    EFZACGHZBDGHZIZABJKZCDJKZGHZTUIALMZCGHZUNIZUSBJKZUQGHZTUTUJBLMZDGHZIZUSVDJK
    ZUQGHZTUSUKCLMZGHZVEIZVGVIDJKZGHZTVJVDULDLMZGHZIZVGVIVNJKZGHZTABCDLLLLAUSNZ
    UOVAURVCVSUMUTUNAUSCGOPVSUPVBUQGAUSBJUAUBQBVDNZVAVFVCVHVTUNVEUTBVDDGOUCVTVB
    VGUQGBVDUSJUDUBQCVINZVFVKVHVMWAUTVJVECVIUSGUEPWAUQVLVGGCVIDJUAUFQDVNNZVKVPV
    MVRWBVEVOVJDVNVDGUEUCWBVLVQVGGDVNVIJUDUFQUSVDVIVNALERSBLERSCLERSDLERSUGUH
    $.
    $( [17-Apr-2005] $)

  $( Adding both sides of two orderings. $)
  ltleadd $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A < C /\ B <_ D ) -> ( A + B ) < ( C + D ) ) ) $=
    ( cr wcel wa clt wbr caddc co wb ltadd1 3com23 3expa adantrr leadd2 readdcl
    cle 3expb adantll wi adantr ancoms ad2ant2lr adantl ltletr syl111anc sylbid
    anbi12d ) AEFZBEFZGZCEFZDEFZGZGZACHIZBDSIZGABJKZCBJKZHIZVACDJKZSIZGZUTVCHIZ
    UQURVBUSVDUMUNURVBLZUOUKULUNVGUKUNULVGACBMNOPULUPUSVDLZUKULUNUOVHULUOUNVHBD
    CQNTUAUJUQUTEFZVAEFZVCEFZVEVFUBUMVIUPABRUCULUNVJUKUOUNULVJCBRUDUEUPVKUMCDRU
    FUTVAVCUGUHUI $.
    $( [23-Dec-2007] $)

  $( Adding both sides of two orderings. $)
  leltadd $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A <_ C /\ B < D ) -> ( A + B ) < ( C + D ) ) ) $=
    ( cr wcel wa cle wbr caddc co wi ltleadd ancomsd cc wceq recn addcom syl2an
    clt ancom2s ancom1s breqan12d sylibrd ) AEFZBEFZGZCEFZDEFZGZGACHIZBDTIZGZBA
    JKZDCJKZTIZABJKZCDJKZTIUFUEUJUMUPLZUFUEGZUIUHUSUTUIUHGGULUKUPBADCMNUAUBUGUJ
    UQUNURUOTUEAOFBOFUQUNPUFAQBQABRSUHCOFDOFURUOPUICQDQCDRSUCUD $.
    $( [15-Aug-2008] $)

  $( The sum of 2 positive numbers is positive.  (The proof was shortened by
     Andrew Salmon, 19-Nov-2011.) $)
  addgt0 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 0 < A /\ 0 < B ) ) ->
                0 < ( A + B ) ) $=
    ( cr wcel wa cc0 clt wbr caddc 0cn addid1i 0re lt2add mpanl12 imp syl5eqbrr
    co wi ) ACDBCDEZFAGHFBGHEZEFFFIQZABIQZGFJKSTUAUBGHZFCDZUDSTUCRLLFFABMNOP $.
    $( [19-Nov-2011] $) $( [1-Jun-2005] $)

  $( The sum of nonnegative and positive numbers is positive.  (The proof was
     shortened by Andrew Salmon, 19-Nov-2011.) $)
  addgegt0 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 0 <_ A /\ 0 < B ) ) ->
                  0 < ( A + B ) ) $=
    ( cr wcel wa cc0 cle wbr clt caddc co 0cn addid1i leltadd mpanl12 syl5eqbrr
    wi 0re imp ) ACDBCDEZFAGHFBIHEZEFFFJKZABJKZIFLMTUAUBUCIHZFCDZUETUAUDQRRFFAB
    NOSP $.
    $( [19-Nov-2011] $) $( [28-Dec-2005] $)

  $( The sum of nonnegative and positive numbers is positive.  (The proof was
     shortened by Andrew Salmon, 19-Nov-2011.) $)
  addgtge0 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 0 < A /\ 0 <_ B ) ) ->
                  0 < ( A + B ) ) $=
    ( cr wcel wa cc0 clt wbr cle caddc co 0cn addid1i ltleadd mpanl12 syl5eqbrr
    wi 0re imp ) ACDBCDEZFAGHFBIHEZEFFFJKZABJKZGFLMTUAUBUCGHZFCDZUETUAUDQRRFFAB
    NOSP $.
    $( [19-Nov-2011] $) $( [28-Dec-2005] $)

  $( The sum of 2 nonnegative numbers is nonnegative.  (The proof was shortened
     by Andrew Salmon, 19-Nov-2011.) $)
  addge0 $p |- ( ( ( A e. RR /\ B e. RR ) /\
               ( 0 <_ A /\ 0 <_ B ) ) -> 0 <_ ( A + B ) ) $=
    ( cr wcel wa cc0 cle wbr caddc 0cn addid1i 0re le2add mpanl12 imp syl5eqbrr
    co wi ) ACDBCDEZFAGHFBGHEZEFFFIQZABIQZGFJKSTUAUBGHZFCDZUDSTUCRLLFFABMNOP $.
    $( [19-Nov-2011] $) $( [17-Mar-2005] $)

  $( Adding a positive number to another number increases it. $)
  ltaddpos $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 < A <-> B < ( B + A ) ) ) $=
    ( cr wcel wa cc0 clt wbr caddc co wb 0re ltadd2 mp3an1 wceq recn addid1 syl
    cc adantl breq1d bitrd ) ACDZBCDZEZFAGHZBFIJZBAIJZGHZBUHGHFCDUCUDUFUIKLFABM
    NUEUGBUHGUDUGBOZUCUDBSDUJBPBQRTUAUB $.
    $( [17-Nov-2004] $)

  $( Adding a positive number to another number increases it. $)
  ltaddpos2 $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 < A <-> B < ( A + B ) ) ) $=
    ( cr wcel wa cc0 clt wbr caddc co ltaddpos wceq addcom syl2an breq2d bitr4d
    cc recn ) ACDZBCDZEZFAGHBBAIJZGHBABIJZGHABKUAUCUBBGSAQDBQDUCUBLTARBRABMNOP
    $.
    $( [8-Apr-2005] $)

  $( Subtracting a positive number from another number decreases it.  (The
     proof was shortened by Andrew Salmon, 19-Nov-2011.) $)
  ltsubpos $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 < A <-> ( B - A ) < B ) ) $=
    ( cr wcel wa cc0 clt wbr caddc co cmin ltaddpos wb ltsubadd 3anidm13 ancoms
    bitr4d ) ACDZBCDZEFAGHBBAIJGHZBAKJBGHZABLSRUATMZSRUBBABNOPQ $.
    $( [19-Nov-2011] $) $( [17-Nov-2004] $)

  $( Comparison of two numbers whose difference is positive. $)
  posdif $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> 0 < ( B - A ) ) ) $=
    ( cr wcel wa cc0 co clt wbr caddc wb resubcl ancoms simpl ltaddpos syl11anc
    cmin cc wceq recn pncan3 syl2an breq2d bitr2d ) ACDZBCDZEZFBAQGZHIZAAUHJGZH
    IZABHIUGUHCDZUEUIUKKUFUEULBALMUEUFNUHAOPUGUJBAHUEARDBRDUJBSUFATBTABUAUBUCUD
    $.
    $( [17-Nov-2004] $)

  $( Negative of both sides of 'less than'.  Theorem I.23 of [Apostol]
     p. 20. $)
  ltneg $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> -u B < -u A ) ) $=
    ( cr wcel clt wbr cneg cc0 cif wceq breq1 negeq breq2d bibi12d breq2 breq1d
    wb 0re elimel ltnegi dedth2h ) ACDZBCDZABEFZBGZAGZEFZQUBAHIZBEFZUEUHGZEFZQU
    HUCBHIZEFZULGZUJEFZQABHHAUHJZUDUIUGUKAUHBEKUPUFUJUEEAUHLMNBULJZUIUMUKUOBULU
    HEOUQUEUNUJEBULLPNUHULAHCRSBHCRSTUA $.
    $( [27-Aug-1999] $)

  $( Contraposition of negative in 'less than'. $)
  ltnegcon1 $p |- ( ( A e. RR /\ B e. RR ) -> ( -u A < B <-> -u B < A ) ) $=
    ( cr wcel wa cneg clt wbr wb renegcl ltneg sylan cc wceq recn negneg breq2d
    syl adantr bitrd ) ACDZBCDZEAFZBGHZBFZUCFZGHZUEAGHZUAUCCDUBUDUGIAJUCBKLUAUG
    UHIUBUAUFAUEGUAAMDUFANAOAPRQST $.
    $( [8-Nov-2004] $)

  $( Negative of both sides of 'less than or equal to'. $)
  leneg $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B <-> -u B <_ -u A ) ) $=
    ( cr wcel cle wbr cneg cc0 cif wceq breq1 negeq breq2d bibi12d breq2 breq1d
    wb 0re elimel lenegi dedth2h ) ACDZBCDZABEFZBGZAGZEFZQUBAHIZBEFZUEUHGZEFZQU
    HUCBHIZEFZULGZUJEFZQABHHAUHJZUDUIUGUKAUHBEKUPUFUJUEEAUHLMNBULJZUIUMUKUOBULU
    HEOUQUEUNUJEBULLPNUHULAHCRSBHCRSTUA $.
    $( [12-Sep-1999] $)

  $( Contraposition of negative in 'less than or equal to'. $)
  lenegcon1 $p |- ( ( A e. RR /\ B e. RR ) -> ( -u A <_ B <-> -u B <_ A ) ) $=
    ( cr wcel wa cneg cle wbr wb renegcl leneg sylan cc wceq recn negneg breq2d
    syl adantr bitrd ) ACDZBCDZEAFZBGHZBFZUCFZGHZUEAGHZUAUCCDUBUDUGIAJUCBKLUAUG
    UHIUBUAUFAUEGUAAMDUFANAOAPRQST $.
    $( [10-May-2004] $)

  $( Contraposition of negative in 'less than or equal to'. $)
  lenegcon2 $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ -u B <-> B <_ -u A ) ) $=
    ( cr wcel wa cneg cle wbr wb renegcl leneg sylan2 wceq cc negneg syl adantl
    recn breq1d bitrd ) ACDZBCDZEZABFZGHZUDFZAFZGHZBUGGHUBUAUDCDUEUHIBJAUDKLUCU
    FBUGGUBUFBMZUAUBBNDUIBRBOPQST $.
    $( [8-Oct-2005] $)

  $( Subtraction from both sides of 'less than or equal to'. $)
  lesub1 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( A - C ) <_ ( B - C ) ) ) $=
    ( cr wcel w3a cle cneg caddc co cmin wb renegcl leadd1 syl3an3 cc recn wceq
    wbr negsub 3adant2 3adant1 breq12d syl3an bitrd ) ADEZBDEZCDEZFABGSZACHZIJZ
    BUJIJZGSZACKJZBCKJZGSZUHUFUGUJDEUIUMLCMABUJNOUFAPEZUGBPEZUHCPEZUMUPLAQBQCQU
    QURUSFUKUNULUOGUQUSUKUNRURACTUAURUSULUORUQBCTUBUCUDUE $.
    $( [1-Aug-2006] $) $( [13-May-2004] $)

  $( Subtraction of both sides of 'less than or equal to'. $)
  lesub2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( C - B ) <_ ( C - A ) ) ) $=
    ( cr wcel w3a cle wbr cneg caddc co cmin wb leneg 3adant3 renegcl recn wceq
    cc negsub leadd2 syl3an2 syl3an1 3com12 3adant2 breq12d 3coml syl3an 3bitrd
    ) ADEZBDEZCDEZFABGHZBIZAIZGHZCUNJKZCUOJKZGHZCBLKZCALKZGHZUJUKUMUPMULABNOUKU
    JULUPUSMZUKUNDEZUJULVCBPUJVDUODEULVCAPUNUOCUAUBUCUDUJASEZUKBSEZULCSEZUSVBMZ
    AQBQCQVGVEVFVHVGVEVFFUQUTURVAGVGVFUQUTRVECBTUEVGVEURVARVFCATOUFUGUHUI $.
    $( [1-Aug-2006] $) $( [29-Sep-2005] $)

  $( Subtraction from both sides of 'less than'.  (Contributed by FL,
     3-Jan-2008.) $)
  ltsub1 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( A - C ) < ( B - C ) ) ) $=
    ( cr wcel w3a clt cneg caddc co cmin wb renegcl ltadd1 syl3an3 cc recn wceq
    wbr negsub 3adant2 3adant1 breq12d syl3an bitrd ) ADEZBDEZCDEZFABGSZACHZIJZ
    BUJIJZGSZACKJZBCKJZGSZUHUFUGUJDEUIUMLCMABUJNOUFAPEZUGBPEZUHCPEZUMUPLAQBQCQU
    QURUSFUKUNULUOGUQUSUKUNRURACTUAURUSULUORUQBCTUBUCUDUE $.
    $( [3-Jan-2008] $)

  $( Subtraction of both sides of 'less than'. $)
  ltsub2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( C - B ) < ( C - A ) ) ) $=
    ( cr wcel w3a clt wbr cneg caddc co cmin wb ltneg 3adant3 renegcl recn wceq
    cc negsub ltadd2 syl3an2 syl3an1 3com12 3adant2 breq12d 3coml syl3an 3bitrd
    ) ADEZBDEZCDEZFABGHZBIZAIZGHZCUNJKZCUOJKZGHZCBLKZCALKZGHZUJUKUMUPMULABNOUKU
    JULUPUSMZUKUNDEZUJULVCBPUJVDUODEULVCAPUNUOCUAUBUCUDUJASEZUKBSEZULCSEZUSVBMZ
    AQBQCQVGVEVFVHVGVEVFFUQUTURVAGVGVFUQUTRVECBTUEVGVEURVARVFCATOUFUGUHUI $.
    $( [1-Aug-2006] $) $( [29-Sep-2005] $)

  ${
    ltaddpos.1 $e |- A e. RR $.
    ltaddpos.2 $e |- B e. RR $.
    $( Adding a positive number to another number increases it. $)
    ltaddposi $p |- ( 0 < A <-> B < ( B + A ) ) $=
      ( cr wcel cc0 clt wbr caddc co wb ltaddpos mp2an ) AEFBEFGAHIBBAJKHILCDAB
      MN $.
      $( [25-Aug-1999] $)

    $( Comparison of two numbers whose difference is positive. $)
    posdifi $p |- ( A < B <-> 0 < ( B - A ) ) $=
      ( cr wcel clt wbr cc0 cmin co wb posdif mp2an ) AEFBEFABGHIBAJKGHLCDABMN
      $.
      $( [17-Nov-2004] $) $( [19-Aug-2001] $)

    $( Contraposition of negative in 'less than'. $)
    ltnegcon1i $p |- ( -u A < B <-> -u B < A ) $=
      ( cr wcel cneg clt wbr wb ltnegcon1 mp2an ) AEFBEFAGBHIBGAHIJCDABKL $.
      $( [14-May-1999] $)

    $( Contraposition of negative in 'less than or equal to'. $)
    lenegcon1i $p |- ( -u A <_ B <-> -u B <_ A ) $=
      ( cr wcel cneg cle wbr wb lenegcon1 mp2an ) AEFBEFAGBHIBGAHIJCDABKL $.
      $( [6-Apr-2005] $)
  $}

  $( Comparison of a number and its negative to zero.  Theorem I.23 of
     [Apostol] p. 20. $)
  lt0neg1 $p |- ( A e. RR -> ( A < 0 <-> 0 < -u A ) ) $=
    ( cr wcel cc0 clt wbr cneg wb 0re ltneg mpan2 neg0 breq1i syl6bb ) ABCZADEF
    ZDGZAGZEFZDREFODBCPSHIADJKQDRELMN $.
    $( [14-May-1999] $)

  $( Comparison of a number and its negative to zero. $)
  lt0neg2 $p |- ( A e. RR -> ( 0 < A <-> -u A < 0 ) ) $=
    ( cr wcel cc0 clt wbr cneg wb 0re ltneg mpan neg0 breq2i syl6bb ) ABCZDAEFZ
    AGZDGZEFZQDEFDBCOPSHIDAJKRDQELMN $.
    $( [10-May-2004] $)

  $( Comparison of a number and its negative to zero. $)
  le0neg1 $p |- ( A e. RR -> ( A <_ 0 <-> 0 <_ -u A ) ) $=
    ( cr wcel cc0 cle wbr cneg wb 0re leneg mpan2 neg0 breq1i syl6bb ) ABCZADEF
    ZDGZAGZEFZDREFODBCPSHIADJKQDRELMN $.
    $( [10-May-2004] $)

  $( Comparison of a number and its negative to zero. $)
  le0neg2 $p |- ( A e. RR -> ( 0 <_ A <-> -u A <_ 0 ) ) $=
    ( cr wcel cc0 cle wbr cneg wb 0re leneg mpan neg0 breq2i syl6bb ) ABCZDAEFZ
    AGZDGZEFZQDEFDBCOPSHIDAJKRDQELMN $.
    $( [24-Aug-1999] $)

  $( A number is less than or equal to itself plus a nonnegative number. $)
  addge01 $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 <_ B <-> A <_ ( A + B ) ) ) $=
    ( cr wcel wa cc0 cle wbr caddc co 0re leadd2 mp3an1 ancoms wceq recn addid1
    wb cc syl adantr breq1d bitrd ) ACDZBCDZEZFBGHZAFIJZABIJZGHZAUIGHUEUDUGUJRZ
    FCDUEUDUKKFBALMNUFUHAUIGUDUHAOZUEUDASDULAPAQTUAUBUC $.
    $( [21-Feb-2005] $)

  $( A number is less than or equal to itself plus a nonnegative number. $)
  addge02 $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 <_ B <-> A <_ ( B + A ) ) ) $=
    ( cr wcel wa cc0 cle wbr caddc co addge01 cc wceq recn addcom syl2an breq2d
    bitrd ) ACDZBCDZEZFBGHAABIJZGHABAIJZGHABKUAUBUCAGSALDBLDUBUCMTANBNABOPQR $.
    $( [27-Jul-2005] $)

  $( Nonnegative subtraction. $)
  subge0 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( 0 <_ ( A - B ) <-> B <_ A ) ) $=
    ( cr wcel wa cle wbr cneg caddc co cc0 wb simpr simpl renegcl adantl leadd1
    cmin cc recn syl111anc wceq negid negsub breq12d syl2an bitr2d ) ACDZBCDZEZ
    BAFGZBBHZIJZAULIJZFGZKABRJZFGZUJUIUHULCDZUKUOLUHUIMUHUINUIURUHBOPBAULQUAUHA
    SDZBSDZUOUQLUIATBTUSUTEUMKUNUPFUTUMKUBUSBUCPABUDUEUFUG $.
    $( [14-Mar-2005] $)

  $( Nonpositive subtraction. $)
  suble0 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( ( A - B ) <_ 0 <-> A <_ B ) ) $=
    ( cr wcel wa cmin co cc0 cle wbr cneg wb resubcl le0neg1 syl wceq negsubdi2
    cc recn syl2an breq2d subge0 ancoms 3bitrd ) ACDZBCDZEZABFGZHIJZHUHKZIJZHBA
    FGZIJZABIJZUGUHCDUIUKLABMUHNOUGUJULHIUEARDBRDUJULPUFASBSABQTUAUFUEUMUNLBAUB
    UCUD $.
    $( [20-Mar-2008] $)

  ${
    subge0.1 $e |- A e. RR $.
    subge0.2 $e |- B e. RR $.
    $( Nonnegative subtraction. $)
    subge0i $p |- ( 0 <_ ( A - B ) <-> B <_ A ) $=
      ( cr wcel cc0 cmin co cle wbr wb subge0 mp2an ) AEFBEFGABHIJKBAJKLCDABMN
      $.
      $( [13-Aug-2000] $)
  $}

  $( Nonnegative subtraction. $)
  subge02 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( 0 <_ B <-> ( A - B ) <_ A ) ) $=
    ( cr wcel wa cc0 cle wbr caddc co cmin addge01 wb lesubadd 3anidm13 bitr4d
    ) ACDZBCDZEFBGHAABIJGHZABKJAGHZABLQRTSMABANOP $.
    $( [27-Jul-2005] $)

  $( Lemma to show a nonnegative number is zero. $)
  lesub0 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( ( 0 <_ A /\ B <_ ( B - A ) ) <-> A = 0 ) ) $=
    ( cr wcel cc0 cle wbr cmin co wa wceq cif breq2 opreq2 breq2d anbi12d eqeq1
    wb 0re elimel bibi12d id opreq1 breq12d anbi2d bibi1d lesub0i dedth2h ) ACD
    ZBCDZEAFGZBBAHIZFGZJZAEKZREUIAELZFGZBBUPHIZFGZJZUPEKZRUQUJBELZVBUPHIZFGZJZV
    ARABEEAUPKZUNUTUOVAVFUKUQUMUSAUPEFMVFULURBFAUPBHNOPAUPEQUABVBKZUTVEVAVGUSVD
    UQVGBVBURVCFVGUBBVBUPHUCUDUEUFUPVBAECSTBECSTUGUH $.
    $( [8-Oct-1999] $)

  $( The product of two nonnegative numbers is nonnegative. $)
  mulge0 $p |- ( ( ( A e. RR /\ 0 <_ A ) /\
                ( B e. RR /\ 0 <_ B ) ) -> 0 <_ ( A x. B ) ) $=
    ( cr wcel cc0 cle wbr cmul co wa wi wceq breq2 anbi1d opreq1 breq2d imbi12d
    cif 0re elimel anbi2d opreq2 mulge0i dedth2h imp an4s ) ACDZBCDZEAFGZEBFGZE
    ABHIZFGZUGUHJUIUJJZULUGUHUMULKEUGAERZFGZUJJZEUNBHIZFGZKUOEUHBERZFGZJZEUNUSH
    IZFGZKABEEAUNLZUMUPULURVDUIUOUJAUNEFMNVDUKUQEFAUNBHOPQBUSLZUPVAURVCVEUJUTUO
    BUSEFMUAVEUQVBEFBUSUNHUBPQUNUSAECSTBECSTUCUDUEUF $.
    $( [16-Jun-2009] $) $( [8-Oct-1999] $)

  $( The product of two nonnegative numbers is nonnegative. $)
  mulge0OLD $p |- ( ( ( A e. RR /\ B e. RR ) /\
                ( 0 <_ A /\ 0 <_ B ) ) -> 0 <_ ( A x. B ) ) $=
    ( cr wcel wa cc0 cle wbr cmul co wi wceq breq2 anbi1d opreq1 breq2d imbi12d
    cif 0re elimel anbi2d opreq2 mulge0i dedth2h imp ) ACDZBCDZEFAGHZFBGHZEZFAB
    IJZGHZUFUGUJULKFUFAFRZGHZUIEZFUMBIJZGHZKUNFUGBFRZGHZEZFUMURIJZGHZKABFFAUMLZ
    UJUOULUQVCUHUNUIAUMFGMNVCUKUPFGAUMBIOPQBURLZUOUTUQVBVDUIUSUNBURFGMUAVDUPVAF
    GBURUMIUBPQUMURAFCSTBFCSTUCUDUE $.
    $( [17-Jul-2005] $) $( [8-Oct-1999] $)

  $( The product of two negative numbers is positive.  (Contributed by Jeffrey
     Hankins, 8-Jun-2009.) $)
  mullt0 $p |- ( ( ( A e. RR /\ A < 0 ) /\ ( B e. RR /\ B < 0 ) )
               -> 0 < ( A x. B ) ) $=
    ( cr wcel cc0 clt wbr wa cneg cmul renegcl adantr lt0neg1 biimpa jca mulgt0
    co syl2an cc recn wceq mul2neg ad2ant2r breqtrd ) ACDZAEFGZHZBCDZBEFGZHZHEA
    IZBIZJQZABJQZFUGUKCDZEUKFGZHULCDZEULFGZHEUMFGUJUGUOUPUEUOUFAKLUEUFUPAMNOUJU
    QURUHUQUIBKLUHUIURBMNOUKULPRUEUHUMUNUAZUFUIUEASDBSDUSUHATBTABUBRUCUD $.
    $( [8-Jun-2009] $)

  $( 0 is less than 1.  Theorem I.21 of [Apostol] p. 20. $)
  lt01 $p |- 0 < 1 $=
    ( cc0 c1 cmul clt wne wbr ax-1ne0 1re msqgt0i ax-mp ax-1cn mulid1i breqtri
    co ) ABBCNZBDBAEAODFGBHIJBKLM $.
    $( [17-Jan-1997] $)

