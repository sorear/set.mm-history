$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The modulo (remainder) operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c mod $.  $( Modulo operation. $)
  $c == $.  $( Equivalence symbol. $)

  $( Extend class notation with the modulo operation. $)
  cmo $a class mod $.

  ${
    $d x y $.
    $( Define the modulo (remainder) operation.  See ~ modval for its value. $)
    df-mod $a |- mod = ( x e. RR , y e. RR+ |->
          ( x - ( y x. ( |_ ` ( x / y ) ) ) ) ) $.
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The value of the modulo operation.  The modulo congruence notation of
       number theory, ` J == K ( ` modulo ` N ) ` , can be expressed in our
       notation as ` ( J mod N ) = ( K mod N ) ` .  Definition 1 in Knuth, _The
       Art of Computer Programming_, Vol.  I (1972), p. 38.  Knuth uses "mod"
       for the operation and "modulo" for the congruence.  Unlike Knuth, we
       restrict the second argument to positive reals to simplify certain
       theorems.  (This also gives us future flexibility to extend it to any
       one of several different conventions for a zero or negative second
       argument, should there be an advantage in doing so.)  (Revised by Mario
       Carneiro, 3-Nov-2013.) $)
    modval $p |- ( ( A e. RR /\ B e. RR+ )
           -> ( A mod B ) = ( A - ( B x. ( |_ ` ( A / B ) ) ) ) ) $=
      ( vx vy cr crp cv cdiv co cfl cfv cmul cmin opreq1 fveq2d opreq2d opreq12
      cmo wceq mpdan opreq2 df-mod oprex ovmpt2 ) CDABEFCGZDGZUEUFHIZJKZLIZMIZA
      BABHIZJKZLIZMIRAUFAUFHIZJKZLIZMIZUEASZUIUPSUJUQSURUHUOUFLURUGUNJUEAUFHNOP
      UEAUIUPMQTUFBSZUPUMAMUSUOULSUPUMSUSUNUKJUFBAHUAOUFBUOULLQTPCDUBAUMMUCUD
      $.
      $( [3-Nov-2013] $) $( [10-Nov-2008] $)
  $}

  $( Closure law for the modulo operation. $)
  modcl $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A mod B ) e. RR ) $=
    ( cr wcel crp wa cmo co cdiv cfl cfv cmul cmin modval rpre adantl rerpdivcl
    reflcl syl remulcl syl2anc resubcl syldan eqeltrd ) ACDZBEDZFZABGHABABIHZJK
    ZLHZMHZCABNUEUFUJCDZUKCDUGBCDZUICDZULUFUMUEBOPUGUHCDUNABQUHRSBUITUAAUJUBUCU
    D $.
    $( [10-Nov-2008] $)

  $( ` A mod B ` is zero iff ` A ` is evenly divisible by ` B ` .  (Contributed
     by Jeff Madsen, 2-Sep-2009.) $)
  mod0 $p |- ( ( A e. RR /\ B e. RR+ ) ->
                              ( ( A mod B ) = 0 <-> ( A / B ) e. ZZ ) ) $=
    ( cr wcel crp wa cmo co cc0 wceq cdiv cfl cfv cz cc wb adantl syl2anc recnd
    syl cmul cmin modval eqeq1d recn adantr rpre rerpdivcl reflcl remulcl eqcom
    subeq0 wne rpcnne0 divmul2 syl3anc syl5rbbr 3bitrd flidz bitrd ) ACDZBEDZFZ
    ABGHZIJZABKHZLMZVFJZVFNDZVCVEABVGUAHZUBHZIJZAVJJZVHVCVDVKIABUCUDVCAODZVJODV
    LVMPVAVNVBAUEUFZVCVJVCBCDZVGCDZVJCDVBVPVABUGQVCVFCDZVQABUHZVFUIZTBVGUJRSAVJ
    ULRVHVFVGJZVCVMVFVGUKVCVNVGODZBODBIUMFZWAVMPVOVCVRWBVSVRVGVTSTVBWCVABUNQAVG
    BUOUPUQURVCVRVHVIPVSVFUSTUT $.
    $( [2-Sep-2009] $)

  $( The modulo operation is nonnegative. $)
  modge0 $p |- ( ( A e. RR /\ B e. RR+ ) -> 0 <_ ( A mod B ) ) $=
    ( cr wcel crp wa cc0 cdiv co cfl cfv cmul cmin cmo cle wbr wb adantl mpbird
    syl rerpdivcl flle clt simpl rpregt0 lemuldiv2 syl3anc rpre remulcl syl2anc
    reflcl subge0 syldan modval breqtrrd ) ACDZBEDZFZGABABHIZJKZLIZMIZABNIOURGV
    BOPZVAAOPZURVDUTUSOPZURUSCDZVEABUAZUSUBTURUTCDZUPBCDZGBUCPFZVDVEQURVFVHVGUS
    UKTZUPUQUDUQVJUPBUERUTABUFUGSUPUQVACDZVCVDQURVIVHVLUQVIUPBUHRVKBUTUIUJAVAUL
    UMSABUNUO $.
    $( [10-Nov-2008] $)

  $( The modulo operation is less than its second argument. $)
  modlt $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A mod B ) < B ) $=
    ( cr wcel crp wa co cdiv cmin cmul clt cc cc0 wceq adantl recnd syl syl3anc
    wbr c1 cmo cfl cfv recn rpcnne0 divcan2 3expb syl2an opreq1d rpcn rerpdivcl
    reflcl subdi modval 3eqtr4rd fraclt1 divid breqtrrd wb resubcl syl2anc rpre
    wne rpregt0 ltmuldiv2 mpbird eqbrtrd ) ACDZBEDZFZABUAGZBABHGZVLUBUCZIGZJGZB
    KVJBVLJGZBVMJGZIGZAVQIGVOVKVJVPAVQIVHALDZBLDZBMVCZFZVPANZVIAUDBUEZVSVTWAWCA
    BUFUGUHUIVJVTVLLDVMLDVOVRNVIVTVHBUJOVJVLABUKZPVJVMVJVLCDZVMCDZWEVLULQZPBVLV
    MUMRABUNUOVJVOBKSZVNBBHGZKSZVJVNTWJKVJWFVNTKSWEVLUPQVIWJTNZVHVIWBWLWDBUQQOU
    RVJVNCDZBCDZWNMBKSFZWIWKUSVJWFWGWMWEWHVLVMUTVAVIWNVHBVBOVIWOVHBVDOVNBBVERVF
    VG $.
    $( [10-Nov-2008] $)

  $( The fractional part of a number is the number modulo 1. $)
  modfrac $p |- ( A e. RR -> ( A mod 1 ) = ( A - ( |_ ` A ) ) ) $=
    ( cr wcel c1 cmo co cdiv cfl cfv cmul cmin crp wceq 1rp modval mpan2 cc syl
    opreq2d eqtrd recn div1 fveq2d reflcl recnd mulid2 ) ABCZADEFZADADGFZHIZJFZ
    KFZAAHIZKFUGDLCUHULMNADOPUGUKUMAKUGUKDUMJFZUMUGUJUMDJUGUIAHUGAQCUIAMAUAAUBR
    UCSUGUMQCUNUMMUGUMAUDUEUMUFRTST $.
    $( [11-Nov-2008] $)

  $( The floor function expressed in terms of the modulo operation. $)
  flmod $p |- ( A e. RR -> ( |_ ` A ) = ( A - ( A mod 1 ) ) ) $=
    ( cr wcel c1 cmo co cmin cfl cfv modfrac opreq2d cc wceq reflcl recnd nncan
    recn syl2anc eqtr2d ) ABCZAADEFZGFAAAHIZGFZGFZUBTUAUCAGAJKTALCUBLCUDUBMAQTU
    BANOAUBPRS $.
    $( [11-Nov-2008] $)

  $( Break a number into its integer part and its fractional part. $)
  intfrac $p |- ( A e. RR -> A = ( ( |_ ` A ) + ( A mod 1 ) ) ) $=
    ( cr wcel cfl cfv c1 cmo co caddc cmin modfrac opreq2d cc wceq reflcl recnd
    recn pncan3 syl2anc eqtr2d ) ABCZADEZAFGHZIHUBAUBJHZIHZAUAUCUDUBIAKLUAUBMCA
    MCUEANUAUBAOPAQUBARST $.
    $( [31-Dec-2008] $)

  $( Move a nonnegative integer in and out of a floor. $)
  flmulnn0 $p |- ( ( N e. NN0 /\ A e. RR )
              -> ( N x. ( |_ ` A ) ) <_ ( |_ ` ( N x. A ) ) ) $=
    ( wcel cr wa cfl cfv cmul co c1 caddc cle cc0 wbr syl2an mpan2 jca cz wceq
    cc cn0 cmo nn0re reflcl remulcl crp 1rp modcl syl nn0ge0 modge0 mulge0 flge
    wb biimpa syl2anc addge01 biimp3a syl3anc intfrac opreq2d nn0cn recnd adddi
    0z adantl 3expb eqtrd fveq2d nn0z flcl zmulcl flzadd eqtr2d breqtrd ) BUACZ
    ADCZEZBAFGZHIZVTBAJUBIZHIZFGZKIZBAHIZFGZLVRVTDCZWCDCZMWCLNZVTWDLNZVPBDCZVSD
    CWGVQBUCZAUDZBVSUEOVRWBDCZWHVPWKWADCZWNVQWLVQJUFCZWOUGAJUHPZBWAUEOZWBUDUIVR
    WNMWBLNZWIWRVPWKMBLNZEWOMWALNZEWSVQVPWKWTWLBUJQVQWOXAWQVQWPXAUGAJUKPQBWAULO
    WNWSWIWNMRCWSWIUNVEWBMUMPUOUPWGWHWIWJVTWCUQURUSVRWFVTWBKIZFGZWDVRWEXBFVRWEB
    VSWAKIZHIZXBVRAXDBHVQAXDSVPAUTVFVAVPBTCZVSTCZWATCZEXEXBSZVQBVBVQXGXHVQVSWMV
    CVQWAWQVCQXFXGXHXIBVSWAVDVGOVHVIVRVTRCZWNXCWDSVPBRCVSRCXJVQBVJAVKBVSVLOWRWB
    VTVMUPVNVO $.
    $( [24-Jul-2009] $) $( [2-Jan-2009] $)

  $( Move a nonnegative integer in and out of a floor. $)
  flmulnn0OLD $p |- ( ( N e. NN0 /\ A e. RR )
              -> ( N x. ( |_ ` A ) ) <_ ( |_ ` ( N x. A ) ) ) $=
    ( wcel cr wa cfl cfv cmul co c1 caddc cle cc0 wbr syl2an mpan2 syl2anc wceq
    cz cc cn0 cmo nn0re reflcl remulcl crp 1rp modcl nn0ge0 modge0 mulge0OLD wb
    anim12i 0z flge biimpa addge01 biimp3a syl3anc intfrac adantl opreq2d nn0cn
    syl recnd adddi 3expb eqtrd fveq2d nn0z flcl zmulcl flzadd eqtr2d breqtrd
    jca ) BUACZADCZEZBAFGZHIZWABAJUBIZHIZFGZKIZBAHIZFGZLVSWADCZWDDCZMWDLNZWAWEL
    NZVQBDCZVTDCWHVRBUCZAUDZBVTUEOVSWCDCZWIVQWLWBDCZWOVRWMVRJUFCZWPUGAJUHPZBWBU
    EOZWCUDVDVSWOMWCLNZWJWSVSWLWPEMBLNZMWBLNZEWTVQWLVRWPWMWRUMVQXAVRXBBUIVRWQXB
    UGAJUJPUMBWBUKQWOWTWJWOMSCWTWJULUNWCMUOPUPQWHWIWJWKWAWDUQURUSVSWGWAWCKIZFGZ
    WEVSWFXCFVSWFBVTWBKIZHIZXCVSAXEBHVRAXERVQAUTVAVBVQBTCZVTTCZWBTCZEXFXCRZVRBV
    CVRXHXIVRVTWNVEVRWBWRVEVPXGXHXIXJBVTWBVFVGOVHVIVSWASCZWOXDWERVQBSCVTSCXKVRB
    VJAVKBVTVLOWSWCWAVMQVNVO $.
    $( [2-Jan-2009] $)

  $( Move a natural number in and out of a floor in the first argument of a
     modulo operation. $)
  modmulnn $p |- ( ( N e. NN /\ A e. RR /\ M e. NN ) -> ( ( N x. ( |_ ` A ) )
           mod ( N x. M ) ) <_ ( ( |_ ` ( N x. A ) ) mod ( N x. M ) ) ) $=
    ( cn wcel cr cfl cfv cmul co cdiv cmin cle 3adant3 syl 3adant2 syl2anc wceq
    wa cc w3a cmo wbr cn0 nnnn0 flmulnn0 sylan wb reflcl remulcl syl2an nnmulcl
    nnre crp nnrp rpre cc0 wne nnne0 mulne0 redivcl syl3anc lesub1 mpbid modval
    nncn jca fldiv recnd divcan5 syl3an fveq2d recn 3comr eqtrd opreq2d 3brtr4d
    3eqtr4rd ) CDEZAFEZBDEZUAZCAGHZIJZCBIJZWDWEKJZGHZIJZLJZCAIJZGHZWHLJZWDWEUBJ
    ZWKWEUBJZMWBWDWKMUCZWIWLMUCZVSVTWOWAVSCUDEVTWOCUEACUFUGNWBWDFEZWKFEZWHFEZWO
    WPUHVSVTWQWAVSCFEZWCFEWQVTCUMZAUIZCWCUJUKNZVSVTWRWAVSVTSWJFEZWRVSWTVTXDXACA
    UJUGZWJUIONZWBWEFEZWGFEZWSVSWAXGVTVSWASZWEUNEZXGXIWEDEZXJCBULZWEUOOZWEUPOPZ
    WBWFFEZXHWBWQXGWEUQURZXOXCXNVSWAXPVTVSCTEZCUQURZSZBTEZBUQURZSZXPWAVSXQXRCVF
    CUSVGZWAXTYABVFBUSVGZCBUTUKPWDWEVAVBWFUIOWEWGUJQWDWKWHVCVBVDWBWQXJWMWIRXCVS
    WAXJVTXMPZWDWEVEQWBWNWKWEWKWEKJGHZIJZLJZWLWBWRXJWNYHRXFYEWKWEVEQWBYGWHWKLWB
    YFWGWEIWBYFWJWEKJZGHZWGWBXDXKYFYJRVSVTXDWAXENVSWAXKVTXLPWJWEVHQVTWAVSYJWGRV
    TWAVSUAZWCBKJZGHZABKJZGHZWGYJVTWAYMYORVSABVHNYKWFYLGVTWCTEWAYBVSXSWFYLRVTWC
    XBVIYDYCWCBCVJVKVLYKYIYNGVTATEWAYBVSXSYIYNRAVMYDYCABCVJVKVLVRVNVOVPVPVOVQ
    $.
    $( [2-Jan-2009] $)

  $( Closure law for the modulo operation restricted to integers. $)
  zmodcl $p |- ( ( A e. ZZ /\ B e. NN ) -> ( A mod B ) e. NN0 ) $=
    ( cz wcel cn wa cmo co cc0 cle wbr cn0 cdiv cfl cfv cmul cmin cr crp syl2an
    wceq zre nnrp modval nnz adantl nndivre sylan flcl syl zmulcl zsubcl syldan
    syl2anc eqeltrd modge0 elnn0z sylanbrc ) ACDZBEDZFZABGHZCDIVBJKZVBLDVAVBABA
    BMHZNOZPHZQHZCUSARDZBSDZVBVGUAUTAUBZBUCZABUDTUSUTVFCDZVGCDVABCDZVECDZVLUTVM
    USBUEUFVAVDRDZVNUSVHUTVOVJABUGUHVDUIUJBVEUKUNAVFULUMUOUSVHVIVCUTVJVKABUPTVB
    UQUR $.
    $( [27-Nov-2008] $)

  $( Identity law for modulo. $)
  modid $p |- ( ( ( A e. RR /\ B e. RR+ ) /\ ( 0 <_ A /\ A < B ) )
               -> ( A mod B ) = A ) $=
    ( cr wcel wa cc0 cle wbr clt co cfl cfv cmul cmin wceq adantr cc syl c1 wb
    crp cmo cdiv modval caddc rerpdivcl recnd addid2 fveq2d cz 0z flbi2 sylancr
    rpregt0 divge0 sylan2 an32s adantrr simpr mulid1 breqtrrd ad2ant2l ad2antlr
    rpcn simpll 1re ltdivmul mp3an2 syl2anc mpbird mpbir2and eqtr3d mul01 eqtrd
    opreq2d recn subid1 ad2antrr 3eqtrd ) ACDZBUADZEZFAGHZABIHZEZEZABUBJZABABUC
    JZKLZMJZNJZAFNJZAWBWGWKOWEABUDPWFWJFANWFWJBFMJZFWFWIFBMWFFWHUEJZKLZWIFWFWHQ
    DZWOWIOWFWHWBWHCDZWEABUFPZUGWPWNWHKWHUHUIRWFWOFOZFWHGHZWHSIHZWFFUJDWQWSWTXA
    ETUKWRWHFULUMWBWCWTWDVTWCWAWTWAVTWCEBCDFBIHEZWTBUNZABUOUPUQURWFXAABSMJZIHZW
    AWDXEVTWCWAWDEABXDIWAWDUSWAXDBOZWDWABQDZXFBVDZBUTRPVAVBWFVTXBXAXETZVTWAWEVE
    WAXBVTWEXCVCVTSCDXBXIVFASBVGVHVIVJVKVLVOWAWMFOZVTWEWAXGXJXHBVMRVCVNVOVTWLAO
    ZWAWEVTAQDXKAVPAVQRVRVS $.
    $( [29-Dec-2008] $)

  $( Identity law for modulo. $)
  modid2 $p |- ( ( A e. RR /\ B e. RR+ )
     -> ( ( A mod B ) = A <-> ( 0 <_ A /\ A < B ) ) ) $=
    ( cr wcel crp wa cmo co wceq cc0 cle wbr clt modge0 modlt jca breq2 anbi12d
    breq1 syl5ibcom modid ex impbid ) ACDBEDFZABGHZAIZJAKLZABMLZFZUDJUEKLZUEBML
    ZFUFUIUDUJUKABNABOPUFUJUGUKUHUEAJKQUEABMSRTUDUIUFABUAUBUC $.
    $( [29-Dec-2008] $)

  $( Absorption law for modulo. $)
  modabs $p |- ( ( ( A e. RR /\ B e. RR+ /\ C e. RR+ ) /\ B <_ C )
           -> ( ( A mod B ) mod C ) = ( A mod B ) ) $=
    ( cr wcel crp w3a cle wbr wa cmo co cc0 clt wceq anim1i adantr 3adant3 rpre
    modcl 3impa modge0 3ad2ant2 3ad2ant3 modlt simpr ltletrd modid syl12anc ) A
    DEZBFEZCFEZGZBCHIZJZABKLZDEZULJZMUPHIZUPCNIUPCKLUPOUMURUNUJUKULURUJUKJUQULA
    BTZPUAQUMUSUNUJUKUSULABUBRQUOUPBCUMUQUNUJUKUQULUTRQUMBDEZUNUKUJVAULBSUCQUMC
    DEZUNULUJVBUKCSUDQUMUPBNIZUNUJUKVCULABUERQUMUNUFUGUPCUHUI $.
    $( [29-Dec-2008] $)

  $( Absorption law for modulo. $)
  modabs2 $p |- ( ( A e. RR /\ B e. RR+ )
           -> ( ( A mod B ) mod B ) = ( A mod B ) ) $=
    ( cr wcel crp wa cle wbr cmo co wceq rpre syl adantl wi w3a modabs 3anidm23
    leid ex mpd ) ACDZBEDZFBBGHZABIJZBIJUEKZUCUDUBUCBCDUDBLBSMNUBUCUDUFOUBUCUCP
    UDUFABBQTRUA $.
    $( [29-Dec-2008] $)

  $( The modulo operation is periodic. $)
  modcyc $p |- ( ( A e. RR /\ B e. RR+ /\ N e. ZZ )
        -> ( ( A + ( N x. B ) ) mod B ) = ( A mod B ) ) $=
    ( cr wcel cmul co caddc wceq cdiv cfl cfv cmin wa syl2an syl2anc cc 3adant1
    opreq2d 3adant2 cz crp cmo w3a zre rpre remulcl readdcl sylan2 3impb modval
    simp3 cc0 wne recn 3ad2ant1 recnd rpcnne0 3ad2ant3 divdir syl3anc zcn 3expb
    divcan4 eqtrd fveq2d rerpdivcl simp2 fladdz rpcn reflcl syl 3ad2ant2 mulcom
    adddi eqcomd 3eqtrd adantl mulcl pnpcan2 eqtr4d 3com23 ) ADEZCUAEZBUBEZACBF
    GZHGZBUCGZABUCGZIWCWDWEUDZWHABABJGZKLZFGZMGZWIWJWHWGBWGBJGZKLZFGZMGZWGWMWFH
    GZMGZWNWJWGDEZWEWHWRIWCWDWEXAWDWENZWCWFDEZXAWDCDEBDEXCWECUEBUFCBUGOZAWFUHUI
    UJWCWDWEULWGBUKPWJWQWSWGMWJWQBWLCHGZFGZWMBCFGZHGZWSWJWPXEBFWJWPWKCHGZKLZXEW
    JWOXIKWJWOWKWFBJGZHGZXIWJAQEZWFQEZBQEZBUMUNZNZWOXLIWCWDXMWEAUOUPZWDWEXNWCXB
    WFXDUQRZWEWCXQWDBURZUSAWFBUTVAWJXKCWKHWDWEXKCIZWCWDCQEZXQYAWECVBZXTYBXOXPYA
    CBVDVCORSVEVFWJWKDEZWDXJXEIWCWEYDWDABVGZTWCWDWEVHWKCVIPVESWJXOWLQEZYBXFXHIW
    EWCXOWDBVJZUSWCWEYFWDWCWENZYDYFYEYDWLWKVKUQVLZTWDWCYBWEYCVMBWLCVOVAWJXGWFWM
    HWJWFXGWDWEWFXGIZWCWDYBXOYJWEYCYGCBVNORVPSVQSWJXMWMQEZXNWTWNIXRWCWEYKWDYHXO
    YFYKWEXOWCYGVRYIBWLVSPTXSAWMWFVTVAVQWCWEWIWNIWDABUKTWAWB $.
    $( [10-Nov-2008] $)

  $( The modulo operation is periodic. $)
  modcyc2 $p |- ( ( A e. RR /\ B e. RR+ /\ N e. ZZ )
        -> ( ( A - ( B x. N ) ) mod B ) = ( A mod B ) ) $=
    ( cr wcel crp cz w3a cmul co cmin cmo cneg caddc cc wceq recn rpcn zcn wa
    mulneg1 ancoms mulcom negeqd eqtr4d mulcl negsub sylan2 3impb eqtr2d syl3an
    3adant1 opreq2d opreq1d znegcl modcyc syl3an3 eqtrd ) ADEZBFEZCGEZHZABCIJZK
    JZBLJACMZBIJZNJZBLJZABLJZVBVDVGBLUSAOEZUTBOEZVACOEZVDVGPAQBRCSVJVKVLHZVGAVC
    MZNJZVDVMVFVNANVKVLVFVNPVJVKVLTZVFCBIJZMZVNVLVKVFVRPCBUAUBVPVCVQBCUCUDUEULU
    MVJVKVLVOVDPZVPVJVCOEVSBCUFAVCUGUHUIUJUKUNVAUSUTVEGEVHVIPCUOABVEUPUQUR $.
    $( [12-Nov-2008] $)

  $( Addition property of the modulo operation. $)
  modadd1 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR+ )
       /\ ( A mod D ) = ( B mod D ) )
       -> ( ( A + C ) mod D ) = ( ( B + C ) mod D ) ) $=
    ( cr wcel wa cmo co wceq caddc cdiv cfl cfv cmin adantrl recn syl syl3anc
    cc crp cmul wb modval eqeqan12d anandirs opreq1 syl6bi adantr ad2antrl rpcn
    adantl rerpdivcl reflcl recnd mulcl syl2anc adantlr adantll eqeq12d sylibrd
    addsub cz readdcl adantrr simprr flcl modcyc2 syl5ib syld 3impia ) AEFZBEFZ
    GZCEFZDUAFZGZADHIZBDHIZJZACKIZDHIZBCKIZDHIZJZVNVQGZVTWADADLIZMNZUBIZOIZWCDB
    DLIZMNZUBIZOIZJZWEWFVTAWIOIZCKIZBWMOIZCKIZJZWOWFVTWPWRJZWTVNVPVTXAUCZVOVLVM
    VPXBVLVPGZVMVPGZVRWPVSWRADUDBDUDUEUFPWPWRCKUGUHWFWJWQWNWSVLVQWJWQJZVMVLVQGZ
    ATFZCTFZWITFZXEVLXGVQAQUIVOXHVLVPCQZUJVLVPXIVOXCDTFZWHTFZXIVPXKVLDUKZULXCWG
    EFZXLADUMZXNWHWGUNUORDWHUPUQPACWIVBSURVMVQWNWSJZVLVMVQGZBTFZXHWMTFZXPVMXRVQ
    BQUIVOXHVMVPXJUJVMVPXSVOXDXKWLTFZXSVPXKVMXMULXDWKEFZXTBDUMZYAWLWKUNUORDWLUP
    UQPBCWMVBSUSUTVAWOWJDHIZWNDHIZJWFWEWJWNDHUGWFYCWBYDWDVLVQYCWBJZVMXFWAEFZVPW
    HVCFZYEVLVOYFVPACVDVEVLVOVPVFVLVPYGVOXCXNYGXOWGVGRPWADWHVHSURVMVQYDWDJZVLXQ
    WCEFZVPWLVCFZYHVMVOYIVPBCVDVEVMVOVPVFVMVPYJVOXDYAYJYBWKVGRPWCDWLVHSUSUTVIVJ
    VK $.
    $( [12-Nov-2008] $)

  $( Multiplication property of the modulo operation.  Note that the multiplier
     ` C ` must be an integer. $)
  modmul1 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. ZZ /\ D e. RR+ )
       /\ ( A mod D ) = ( B mod D ) )
       -> ( ( A x. C ) mod D ) = ( ( B x. C ) mod D ) ) $=
    ( cr wcel wa cz cmo co wceq cmul cdiv cmin adantrl zcn syl syl3anc syl2anc
    cc crp cfl cfv wb modval eqeqan12d anandirs opreq1 syl6bi ad2antll ad2antrl
    rpcn rerpdivcl flcl mulass mul32 eqtr3d opreq2d adantr adantl subdir eqtr4d
    recn mulcl adantlr adantll eqeq12d sylibrd zre remulcl sylan2 simprr simprl
    adantrr zmulcl modcyc2 syl5ib syld 3impia ) AEFZBEFZGZCHFZDUAFZGZADIJZBDIJZ
    KZACLJZDIJZBCLJZDIJZKZWBWEGZWHWIDCADMJZUBUCZLJZLJZNJZWKDCBDMJZUBUCZLJZLJZNJ
    ZKZWMWNWHADWPLJZNJZCLJZBDXALJZNJZCLJZKZXEWNWHXGXJKZXLWBWDWHXMUDZWCVTWAWDXNV
    TWDGZWAWDGZWFXGWGXJADUEBDUEUFUGOXGXJCLUHUIWNWSXHXDXKVTWEWSXHKWAVTWEGZWSWIXF
    CLJZNJZXHXQWRXRWINXQDCLJZWPLJZWRXRXQDTFZCTFZWPTFZYAWRKWDYBVTWCDULZUJZWCYCVT
    WDCPZUKZVTWDYDWCXOWPHFZYDXOWOEFYIADUMWOUNQZWPPQZOZDCWPUORXQYBYCYDYAXRKYFYHY
    LDCWPUPRUQURXQATFZXFTFZYCXHXSKVTYMWEAVCUSVTWDYNWCXOYBYDYNWDYBVTYEUTYKDWPVDS
    OYHAXFCVARVBVEWAWEXDXKKVTWAWEGZXDWKXICLJZNJZXKYOXCYPWKNYOXTXALJZXCYPYOYBYCX
    ATFZYRXCKWDYBWAWCYEUJZWCYCWAWDYGUKZWAWDYSWCXPXAHFZYSXPWTEFUUBBDUMWTUNQZXAPQ
    ZOZDCXAUORYOYBYCYSYRYPKYTUUAUUEDCXAUPRUQURYOBTFZXITFZYCXKYQKWAUUFWEBVCUSWAW
    DUUGWCXPYBYSUUGWDYBWAYEUTUUDDXAVDSOUUABXICVARVBVFVGVHXEWSDIJZXDDIJZKWNWMWSX
    DDIUHWNUUHWJUUIWLVTWEUUHWJKZWAXQWIEFZWDWQHFZUUJVTWCUUKWDWCVTCEFZUUKCVIZACVJ
    VKVNVTWCWDVLXQWCYIUULVTWCWDVMVTWDYIWCYJOCWPVOSWIDWQVPRVEWAWEUUIWLKZVTYOWKEF
    ZWDXBHFZUUOWAWCUUPWDWCWAUUMUUPUUNBCVJVKVNWAWCWDVLYOWCUUBUUQWAWCWDVMWAWDUUBW
    CUUCOCXAVOSWKDXBVPRVFVGVQVRVS $.
    $( [12-Nov-2008] $)

  $( Distribute multiplication over a modulo operation. $)
  moddi $p |- ( ( A e. RR+ /\ B e. RR /\ C e. RR+ )
      -> ( A x. ( B mod C ) ) = ( ( A x. B ) mod ( A x. C ) ) ) $=
    ( crp wcel cr cdiv co cfl cfv cmul cmin cmo cc wceq rpcn wa 3adant1 syl3anc
    opreq2d w3a 3ad2ant1 recn 3ad2ant2 rpre adantl rerpdivcl reflcl syl remulcl
    syl2anc recnd subdi cc0 rpcnne0 3ad2ant3 divcan5 fveq2d mulass eqtr2d eqtrd
    wne modval sylan 3adant3 rpmulcl 3adant2 3eqtr4d ) ADEZBFEZCDEZUAZABCBCGHZI
    JZKHZLHZKHZABKHZACKHZVRVSGHZIJZKHZLHZABCMHZKHVRVSMHZVLVQVRAVOKHZLHZWCVLANEZ
    BNEZVONEZVQWGOVIVJWHVKAPUBZVJVIWIVKBUCUDZVJVKWJVIVJVKQZVOWMCFEZVNFEZVOFEVKW
    NVJCUEUFWMVMFEZWOBCUGZVMUHZUICVNUJUKULRABVOUMSVLWFWBVRLVLWBVSVNKHZWFVLWAVNV
    SKVLVTVMIVLWICNEZCUNVBQZWHAUNVBQZVTVMOWLVKVIXAVJCUOUPVIVJXBVKAUOUBBCAUQSURT
    VLWHWTVNNEZWSWFOWKVKVIWTVJCPUPVJVKXCVIWMWPXCWQWPVNWRULUIRACVNUSSUTTVAVLWDVP
    AKVJVKWDVPOVIBCVCRTVLVRFEZVSDEZWEWCOVIVJXDVKVIAFEVJXDAUEABUJVDVEVIVKXEVJACV
    FVGVRVSVCUKVH $.
    $( [11-Nov-2008] $)

  $( Distribute the modulo operation over a subtraction. $)
  modsubdir $p |- ( ( A e. RR /\ B e. RR /\ C e. RR+ ) -> ( ( B mod C )
  <_ ( A mod C ) <-> ( ( A - B ) mod C ) = ( ( A mod C ) - ( B mod C ) ) ) ) $=
    ( cr wcel cc0 cmo co cmin cle wbr wceq 3adant2 3adant1 syl2anc wa syl recnd
    cc adantr crp w3a wb modcl subge0 cdiv cfl cfv cz resubcl 3adant3 rerpdivcl
    cmul simp3 flcl zsubcl modcyc2 syl3anc recn 3ad2ant1 3ad2ant2 adantl reflcl
    rpre remulcl sub4 syl22anc 3ad2ant3 opreq2d modval opreq12d 3eqtr4d opreq1d
    subdi eqtr3d clt simpl3 simpr subge02 mpbid modlt lelttrd modid eqtrd sylan
    modge0 3impa breqtrd impbida bitr3d ) ADEZBDEZCUAEZUBZFACGHZBCGHZIHZJKZWPWO
    JKZABIHZCGHZWQLZWNWODEZWPDEZWRWSUCWKWMXCWLACUDMZWLWMXDWKBCUDNZWOWPUEOWNWRXB
    WNWRPZXAWQCGHZWQWNXAXHLWRWNWTCACUFHZUGUHZBCUFHZUGUHZIHZUMHZIHZCGHZXAXHWNWTD
    EZWMXMUIEZXPXALWKWLXQWMABUJZUKWKWLWMUNWNXJUIEZXLUIEZXRWKWMXTWLWKWMPZXIDEZXT
    ACULZXIUOQMWLWMYAWKWLWMPZXKDEZYABCULZXKUOQNXJXLUPOWTCXMUQURWNXOWQCGWNWTCXJU
    MHZCXLUMHZIHZIHZAYHIHZBYIIHZIHZXOWQWNASEZBSEZYHSEZYISEZYKYNLWKWLYOWMAUSUTWL
    WKYPWMBUSVAWKWMYQWLYBYHYBCDEZXJDEZYHDEWMYSWKCVDZVBYBYCYTYDXIVCQZCXJVEORMWLW
    MYRWKYEYIYEYSXLDEZYIDEWMYSWLUUAVBYEYFUUCYGXKVCQZCXLVEORNABYHYIVFVGWNXNYJWTI
    WNCSEXJSEZXLSEZXNYJLWNCWMWKYSWLUUAVHZRWKWMUUEWLYBXJUUBRMWLWMUUFWKYEXLUUDRNC
    XJXLVNURVIWNWOYLWPYMIWKWMWOYLLWLACVJMWLWMWPYMLWKBCVJNVKVLVMVOTXGWQDEZWMWRWQ
    CVPKZXHWQLWNUUHWRWNXCXDUUHXEXFWOWPUJOZTWKWLWMWRVQWNWRVRWNUUIWRWNWQWOCUUJXEU
    UGWNFWPJKZWQWOJKZWLWMUUKWKBCWFNWNXCXDUUKUULUCXEXFWOWPVSOVTWKWMWOCVPKWLACWAM
    WBTWQCWCVGWDWNXBPFXAWQJWNFXAJKZXBWKWLWMUUMWKWLPXQWMUUMXSWTCWFWEWGTWNXBVRWHW
    IWJ $.
    $( [30-Dec-2008] $)

  $( A number modulo an irrational multiple of it is nonzero. $)
  modirr $p |- ( ( A e. RR /\ B e. RR+ /\ ( A / B ) e. ( RR \ QQ ) )
      -> ( A mod B ) =/= 0 ) $=
    ( cr wcel crp cdiv co cq cdif cmo cc0 wne wa cc wb adantl syl syl2anc recnd
    wceq wn eldif cfl cmul cmin modval eqeq1d recn adantr rpre rerpdivcl reflcl
    remulcl subeq0 eqcom rpcnne0 divmul2 syl3anc syl5rbbr 3bitrd wi cz flidz zq
    cfv syl6bi sylbid necon3bd adantld syl5bi 3impia ) ACDZBEDZABFGZCHIDZABJGZK
    LZVOVNCDZVNHDZUAZMVLVMMZVQVNCHUBWAVTVQVRWAVSVPKWAVPKTZVNUCVEZVNTZVSWAWBABWC
    UDGZUEGZKTZAWETZWDWAVPWFKABUFUGWAANDZWENDWGWHOVLWIVMAUHUIZWAWEWABCDZWCCDZWE
    CDVMWKVLBUJPWAVRWLABUKZVNULZQBWCUMRSAWEUNRWDVNWCTZWAWHVNWCUOWAWIWCNDZBNDBKL
    MZWOWHOWJWAVRWPWMVRWCWNSQVMWQVLBUPPAWCBUQURUSUTWAVRWDVSVAWMVRWDVNVBDVSVNVCV
    NVDVFQVGVHVIVJVK $.
    $( [11-Nov-2008] $)

