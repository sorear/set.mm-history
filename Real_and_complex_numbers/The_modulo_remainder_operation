$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The modulo (remainder) operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c mod $.  $( Modulo operation. $)
  $c == $.  $( Equivalence symbol (not used yet except in comments). $)

  $( Extend class notation with the modulo operation. $)
  cmo $a class mod $.

  ${
    $d x y z $.
    $( Define the modulo (remainder) operation.  See ~ modval for its value. $)
    df-mod $a |- mod = { <. <. x , y >. , z >. | ( ( x e. RR /\ y e. RR+ )
          /\ z = ( x - ( y x. ( |_ ` ( x / y ) ) ) ) ) } $.
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( The value of the modulo operation.  The modulo congruence notation of
       number theory, ` J == K ( ` modulo ` N ) ` , can be expressed in our
       notation as ` ( J mod N ) = ( K mod N ) ` .  Definition 1 in Knuth, _The
       Art of Computer Programming_, Vol.  I (1972), p. 38.  Knuth uses "mod"
       for the operation and "modulo" for the congruence.  Unlike Knuth, we
       restrict the second argument to positive reals to simplify certain
       theorems.  (This also gives us future flexibility to extend it to any
       one of several different conventions for a zero or negative second
       argument, should there be an advantage in doing so.) $)
    modval $p |- ( ( A e. RR /\ B e. RR+ )
           -> ( A mod B ) = ( A - ( B x. ( |_ ` ( A / B ) ) ) ) ) $=
      ( vx vy vz cr crp cv cdiv co cfl cfv cmul cmin cmo oprex wceq id opreq1
      fveq2d opreq2d opreq12d opreq2 df-mod oprabval2 ) CDEABFGCHZDHZUFUGIJZKLZ
      MJZNJABABIJZKLZMJZNJOAUGAUGIJZKLZMJZNJAUMNPUFAQZUFAUJUPNUQRUQUIUOUGMUQUHU
      NKUFAUGISTUAUBUGBQZUPUMANURUGBUOULMURRURUNUKKUGBAIUCTUBUACDEUDUE $.
      $( [10-Nov-2008] $)
  $}

  $( Closure law for the modulo operation. $)
  modcl $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A mod B ) e. RR ) $=
    ( cr wcel crp wa cmo co cdiv cfl cfv cmul cmin modval rpre adantl
    rerpdivcl reflcl syl remulcl syl11anc resubcl syldan eqeltrd ) ACDZBEDZFZAB
    GHABABIHZJKZLHZMHZCABNUEUFUJCDZUKCDUGBCDZUICDZULUFUMUEBOPUGUHCDUNABQUHRSBUI
    TUAAUJUBUCUD $.
    $( [10-Nov-2008] $)

  $( The modulo operation is nonnegative. $)
  modge0 $p |- ( ( A e. RR /\ B e. RR+ ) -> 0 <_ ( A mod B ) ) $=
    ( cr wcel crp wa cc0 cdiv co cfl cfv cmul cmin cmo cle wbr rerpdivcl flle
    syl clt wb reflcl simpl rpregt0 adantl lemuldiv2 syl111anc mpbird rpre
    remulcl syl11anc subge0 syldan modval breqtrrd ) ACDZBEDZFZGABABHIZJKZLIZMI
    ZABNIOURGVBOPZVAAOPZURVDUTUSOPZURUSCDZVEABQZUSRSURUTCDZUPBCDZGBTPFZVDVEUAUR
    VFVHVGUSUBSZUPUQUCUQVJUPBUDUEUTABUFUGUHUPUQVACDZVCVDUAURVIVHVLUQVIUPBUIUEVK
    BUTUJUKAVAULUMUHABUNUO $.
    $( [10-Nov-2008] $)

  $( The modulo operation is less than its second argument. $)
  modlt $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A mod B ) < B ) $=
    ( cr wcel crp wa cmo co cdiv cfl cfv cmin cmul clt cc cc0 wne wceq recn
    rpcnne0 divcan2 3expb syl2an opreq1d rpcn adantl rerpdivcl recnd reflcl
    syl subdi syl111anc modval 3eqtr4rd wbr c1 fraclt1 divid breqtrrd wb
    resubcl syl11anc rpre rpregt0 ltmuldiv2 mpbird eqbrtrd ) ACDZBEDZFZABGHZBAB
    IHZVLJKZLHZMHZBNVJBVLMHZBVMMHZLHZAVQLHVOVKVJVPAVQLVHAODZBODZBPQZFZVPARZVIAS
    BTZVSVTWAWCABUAUBUCUDVJVTVLODVMODVOVRRVIVTVHBUEUFVJVLABUGZUHVJVMVJVLCDZVMCD
    ZWEVLUIUJZUHBVLVMUKULABUMUNVJVOBNUOZVNBBIHZNUOZVJVNUPWJNVJWFVNUPNUOWEVLUQUJ
    VIWJUPRZVHVIWBWLWDBURUJUFUSVJVNCDZBCDZWNPBNUOFZWIWKUTVJWFWGWMWEWHVLVMVAVBVI
    WNVHBVCUFVIWOVHBVDUFVNBBVEULVFVG $.
    $( [10-Nov-2008] $)

  $( The fractional part of a number is the number modulo 1. $)
  modfrac $p |- ( A e. RR -> ( A mod 1 ) = ( A - ( |_ ` A ) ) ) $=
    ( cr wcel c1 cmo co cdiv cfl cfv cmul cmin crp wceq 1rp modval mpan2 cc
    recn div1 syl fveq2d opreq2d reflcl recnd mulid2 eqtrd ) ABCZADEFZADADGFZHI
    ZJFZKFZAAHIZKFUGDLCUHULMNADOPUGUKUMAKUGUKDUMJFZUMUGUJUMDJUGUIAHUGAQCUIAMARA
    STUAUBUGUMQCUNUMMUGUMAUCUDUMUETUFUBUF $.
    $( [11-Nov-2008] $)

  $( The floor function expressed in terms of the modulo operation. $)
  flmod $p |- ( A e. RR -> ( |_ ` A ) = ( A - ( A mod 1 ) ) ) $=
    ( cr wcel c1 cmo co cmin cfl cfv modfrac opreq2d cc wceq recn reflcl recnd
    nncan syl11anc eqtr2d ) ABCZAADEFZGFAAAHIZGFZGFZUBTUAUCAGAJKTALCUBLCUDUBMAN
    TUBAOPAUBQRS $.
    $( [11-Nov-2008] $)

  $( Break a number into its integer part and its fractional part. $)
  intfrac $p |- ( A e. RR -> A = ( ( |_ ` A ) + ( A mod 1 ) ) ) $=
    ( cr wcel cfl cfv c1 cmo co caddc cmin modfrac opreq2d cc wceq reflcl
    recnd recn pncan3 syl11anc eqtr2d ) ABCZADEZAFGHZIHUBAUBJHZIHZAUAUCUDUBIAKL
    UAUBMCAMCUEANUAUBAOPAQUBARST $.
    $( [31-Dec-2008] $)

  $( Move a nonnegative integer in and out of a floor. $)
  flmulnn0 $p |- ( ( N e. NN0 /\ A e. RR )
              -> ( N x. ( |_ ` A ) ) <_ ( |_ ` ( N x. A ) ) ) $=
    ( cn0 wcel cr wa cfl cfv cmul co c1 cmo caddc cle cc0 wbr nn0re reflcl
    remulcl syl2an crp 1rp modcl mpan2 syl nn0ge0 jca modge0 mulge0 cz wb 0z
    flge biimpa syl11anc addge01 biimp3a syl111anc wceq intfrac adantl opreq2d
    cc nn0cn recnd adddi 3expb eqtrd fveq2d nn0z flcl zmulcl flzadd eqtr2d
    breqtrd ) BCDZAEDZFZBAGHZIJZVTBAKLJZIJZGHZMJZBAIJZGHZNVRVTEDZWCEDZOWCNPZVTW
    DNPZVPBEDZVSEDWGVQBQZARZBVSSTVRWBEDZWHVPWKWAEDZWNVQWLVQKUADZWOUBAKUCUDZBWAS
    TZWBRUEVRWNOWBNPZWIWRVPWKOBNPZFWOOWANPZFWSVQVPWKWTWLBUFUGVQWOXAWQVQWPXAUBAK
    UHUDUGBWAUITWNWSWIWNOUJDWSWIUKULWBOUMUDUNUOWGWHWIWJVTWCUPUQURVRWFVTWBMJZGHZ
    WDVRWEXBGVRWEBVSWAMJZIJZXBVRAXDBIVQAXDUSVPAUTVAVBVPBVCDZVSVCDZWAVCDZFXEXBUS
    ZVQBVDVQXGXHVQVSWMVEVQWAWQVEUGXFXGXHXIBVSWAVFVGTVHVIVRVTUJDZWNXCWDUSVPBUJDV
    SUJDXJVQBVJAVKBVSVLTWRWBVTVMUOVNVO $.
    $( [24-Jul-2009] $) $( [2-Jan-2009] $)

  $( Move a nonnegative integer in and out of a floor. $)
  flmulnn0OLD $p |- ( ( N e. NN0 /\ A e. RR )
              -> ( N x. ( |_ ` A ) ) <_ ( |_ ` ( N x. A ) ) ) $=
    ( cn0 wcel cr wa cfl cfv cmul co c1 cmo caddc cle cc0 wbr nn0re reflcl
    remulcl syl2an crp 1rp modcl mpan2 syl anim12i nn0ge0 modge0 mulge0OLD
    syl11anc cz wb 0z flge biimpa addge01 biimp3a syl111anc wceq intfrac
    adantl opreq2d cc nn0cn recnd jca adddi 3expb eqtrd fveq2d nn0z flcl
    zmulcl flzadd eqtr2d breqtrd ) BCDZAEDZFZBAGHZIJZWABAKLJZIJZGHZMJZBAIJZGHZN
    VSWAEDZWDEDZOWDNPZWAWENPZVQBEDZVTEDWHVRBQZARZBVTSTVSWCEDZWIVQWLWBEDZWOVRWMV
    RKUADZWPUBAKUCUDZBWBSTZWCRUEVSWOOWCNPZWJWSVSWLWPFOBNPZOWBNPZFWTVQWLVRWPWMWR
    UFVQXAVRXBBUGVRWQXBUBAKUHUDUFBWBUIUJWOWTWJWOOUKDWTWJULUMWCOUNUDUOUJWHWIWJWK
    WAWDUPUQURVSWGWAWCMJZGHZWEVSWFXCGVSWFBVTWBMJZIJZXCVSAXEBIVRAXEUSVQAUTVAVBVQ
    BVCDZVTVCDZWBVCDZFXFXCUSZVRBVDVRXHXIVRVTWNVEVRWBWRVEVFXGXHXIXJBVTWBVGVHTVIV
    JVSWAUKDZWOXDWEUSVQBUKDVTUKDXKVRBVKAVLBVTVMTWSWCWAVNUJVOVP $.
    $( [2-Jan-2009] $)

  $( Move a natural number in and out of a floor in the first argument of a
     modulo operation. $)
  modmulnn $p |- ( ( N e. NN /\ A e. RR /\ M e. NN ) -> ( ( N x. ( |_ ` A ) )
           mod ( N x. M ) ) <_ ( ( |_ ` ( N x. A ) ) mod ( N x. M ) ) ) $=
    ( cn wcel cr w3a cfl cfv cmul co cdiv cmin cmo cle wbr cn0 nnnn0 flmulnn0
    sylan 3adant3 wb nnre reflcl remulcl syl2an wa syl crp nnmulcl nnrp rpre
    3adant2 cc0 wne cc nncn nnne0 jca mulne0 redivcl syl111anc syl11anc lesub1
    mpbid wceq modval fldiv recnd divcan5 syl3an fveq2d recn 3eqtr4rd 3comr
    eqtrd opreq2d 3brtr4d ) CDEZAFEZBDEZGZCAHIZJKZCBJKZWDWELKZHIZJKZMKZCAJKZHIZ
    WHMKZWDWENKZWKWENKZOWBWDWKOPZWIWLOPZVSVTWOWAVSCQEVTWOCRACSTUAWBWDFEZWKFEZWH
    FEZWOWPUBVSVTWQWAVSCFEZWCFEWQVTCUCZAUDZCWCUEUFUAZVSVTWRWAVSVTUGWJFEZWRVSWTV
    TXDXACAUETZWJUDUHUAZWBWEFEZWGFEZWSVSWAXGVTVSWAUGZWEUIEZXGXIWEDEZXJCBUJZWEUK
    UHZWEULUHUMZWBWFFEZXHWBWQXGWEUNUOZXOXCXNVSWAXPVTVSCUPEZCUNUOZUGZBUPEZBUNUOZ
    UGZXPWAVSXQXRCUQCURUSZWAXTYABUQBURUSZCBUTUFUMWDWEVAVBWFUDUHWEWGUEVCWDWKWHVD
    VBVEWBWQXJWMWIVFXCVSWAXJVTXMUMZWDWEVGVCWBWNWKWEWKWELKHIZJKZMKZWLWBWRXJWNYHV
    FXFYEWKWEVGVCWBYGWHWKMWBYFWGWEJWBYFWJWELKZHIZWGWBXDXKYFYJVFVSVTXDWAXEUAVSWA
    XKVTXLUMWJWEVHVCVTWAVSYJWGVFVTWAVSGZWCBLKZHIZABLKZHIZWGYJVTWAYMYOVFVSABVHUA
    YKWFYLHVTWCUPEWAYBVSXSWFYLVFVTWCXBVIYDYCWCBCVJVKVLYKYIYNHVTAUPEWAYBVSXSYIYN
    VFAVMYDYCABCVJVKVLVNVOVPVQVQVPVR $.
    $( [2-Jan-2009] $)

  $( Closure law for the modulo operation restricted to integers. $)
  zmodcl $p |- ( ( A e. ZZ /\ B e. NN ) -> ( A mod B ) e. NN0 ) $=
    ( cz wcel cn wa cmo co cc0 cle wbr cn0 cdiv cfl cfv cmul cmin cr crp wceq
    zre nnrp modval syl2an nnz adantl nndivre sylan flcl syl zmulcl syl11anc
    zsubcl syldan eqeltrd modge0 elnn0z sylanbrc ) ACDZBEDZFZABGHZCDIVBJKZVBLDV
    AVBABABMHZNOZPHZQHZCUSARDZBSDZVBVGTUTAUAZBUBZABUCUDUSUTVFCDZVGCDVABCDZVECDZ
    VLUTVMUSBUEUFVAVDRDZVNUSVHUTVOVJABUGUHVDUIUJBVEUKULAVFUMUNUOUSVHVIVCUTVJVKA
    BUPUDVBUQUR $.
    $( [27-Nov-2008] $)

  $( Identity law for modulo. $)
  modid $p |- ( ( ( A e. RR /\ B e. RR+ ) /\ ( 0 <_ A /\ A < B ) )
               -> ( A mod B ) = A ) $=
    ( cr wcel crp wa cc0 cle wbr clt cmo co cdiv cfl cfv cmul cmin wceq modval
    adantr caddc cc rerpdivcl recnd addid2 fveq2d syl c1 cz wb 0z flbi2
    sylancr rpregt0 divge0 sylan2 an32s adantrr simpr rpcn mulid1 breqtrrd
    ad2ant2l simpll ad2antlr 1re ltdivmul mp3an2 syl11anc mpbird mpbir2and
    eqtr3d opreq2d mul01 eqtrd recn subid1 ad2antrr 3eqtrd ) ACDZBEDZFZGAHIZABJ
    IZFZFZABKLZABABMLZNOZPLZQLZAGQLZAWBWGWKRWEABSTWFWJGAQWFWJBGPLZGWFWIGBPWFGWH
    UALZNOZWIGWFWHUBDZWOWIRWFWHWBWHCDZWEABUCTZUDWPWNWHNWHUEUFUGWFWOGRZGWHHIZWHU
    HJIZWFGUIDWQWSWTXAFUJUKWRWHGULUMWBWCWTWDVTWCWAWTWAVTWCFBCDGBJIFZWTBUNZABUOU
    PUQURWFXAABUHPLZJIZWAWDXEVTWCWAWDFABXDJWAWDUSWAXDBRZWDWABUBDZXFBUTZBVAUGTVB
    VCWFVTXBXAXEUJZVTWAWEVDWAXBVTWEXCVEVTUHCDXBXIVFAUHBVGVHVIVJVKVLVMWAWMGRZVTW
    EWAXGXJXHBVNUGVEVOVMVTWLARZWAWEVTAUBDXKAVPAVQUGVRVS $.
    $( [29-Dec-2008] $)

  $( Identity law for modulo. $)
  modid2 $p |- ( ( A e. RR /\ B e. RR+ )
     -> ( ( A mod B ) = A <-> ( 0 <_ A /\ A < B ) ) ) $=
    ( cr wcel crp wa cmo co wceq cc0 cle wbr clt modge0 modlt jca breq2 breq1
    anbi12d syl5ibcom modid ex impbid ) ACDBEDFZABGHZAIZJAKLZABMLZFZUDJUEKLZUEB
    MLZFUFUIUDUJUKABNABOPUFUJUGUKUHUEAJKQUEABMRSTUDUIUFABUAUBUC $.
    $( [29-Dec-2008] $)

  $( Absorption law for modulo. $)
  modabs $p |- ( ( ( A e. RR /\ B e. RR+ /\ C e. RR+ ) /\ B <_ C )
           -> ( ( A mod B ) mod C ) = ( A mod B ) ) $=
    ( cr wcel crp w3a cle wbr wa cmo co cc0 clt wceq modcl anim1i 3impa adantr
    modge0 3adant3 rpre 3ad2ant2 3ad2ant3 modlt simpr ltletrd modid syl12anc )
    ADEZBFEZCFEZGZBCHIZJZABKLZDEZULJZMUPHIZUPCNIUPCKLUPOUMURUNUJUKULURUJUKJUQUL
    ABPZQRSUMUSUNUJUKUSULABTUASUOUPBCUMUQUNUJUKUQULUTUASUMBDEZUNUKUJVAULBUBUCSU
    MCDEZUNULUJVBUKCUBUDSUMUPBNIZUNUJUKVCULABUEUASUMUNUFUGUPCUHUI $.
    $( [29-Dec-2008] $)

  $( Absorption law for modulo. $)
  modabs2 $p |- ( ( A e. RR /\ B e. RR+ )
           -> ( ( A mod B ) mod B ) = ( A mod B ) ) $=
    ( cr wcel crp wa cle wbr cmo co wceq rpre leid syl adantl wi w3a modabs ex
    3anidm23 mpd ) ACDZBEDZFBBGHZABIJZBIJUEKZUCUDUBUCBCDUDBLBMNOUBUCUDUFPUBUCUC
    QUDUFABBRSTUA $.
    $( [29-Dec-2008] $)

  $( The modulo operation is periodic. $)
  modcyc $p |- ( ( A e. RR /\ B e. RR+ /\ N e. ZZ )
        -> ( ( A + ( N x. B ) ) mod B ) = ( A mod B ) ) $=
    ( cr wcel cz crp cmul co caddc cmo wceq w3a cdiv cfl cfv cmin wa zre rpre
    remulcl syl2an readdcl sylan2 3impb simp3 modval syl11anc cc cc0 wne recn
    3ad2ant1 recnd 3adant1 rpcnne0 3ad2ant3 divdir syl111anc zcn divcan4 3expb
    opreq2d eqtrd fveq2d rerpdivcl 3adant2 simp2 fladdz rpcn reflcl syl
    3ad2ant2 adddi mulcom eqcomd 3eqtrd adantl mulcl pnpcan2 eqtr4d 3com23 ) AD
    EZCFEZBGEZACBHIZJIZBKIZABKIZLWCWDWEMZWHABABNIZOPZHIZQIZWIWJWHWGBWGBNIZOPZHI
    ZQIZWGWMWFJIZQIZWNWJWGDEZWEWHWRLWCWDWEXAWDWERZWCWFDEZXAWDCDEBDEXCWECSBTCBUA
    UBZAWFUCUDUEWCWDWEUFWGBUGUHWJWQWSWGQWJWQBWLCJIZHIZWMBCHIZJIZWSWJWPXEBHWJWPW
    KCJIZOPZXEWJWOXIOWJWOWKWFBNIZJIZXIWJAUIEZWFUIEZBUIEZBUJUKZRZWOXLLWCWDXMWEAU
    LUMZWDWEXNWCXBWFXDUNUOZWEWCXQWDBUPZUQAWFBURUSWJXKCWKJWDWEXKCLZWCWDCUIEZXQYA
    WECUTZXTYBXOXPYACBVAVBUBUOVCVDVEWJWKDEZWDXJXELWCWEYDWDABVFZVGWCWDWEVHWKCVIU
    HVDVCWJXOWLUIEZYBXFXHLWEWCXOWDBVJZUQWCWEYFWDWCWERZYDYFYEYDWLWKVKUNVLZVGWDWC
    YBWEYCVMBWLCVNUSWJXGWFWMJWJWFXGWDWEWFXGLZWCWDYBXOYJWEYCYGCBVOUBUOVPVCVQVCWJ
    XMWMUIEZXNWTWNLXRWCWEYKWDYHXOYFYKWEXOWCYGVRYIBWLVSUHVGXSAWMWFVTUSVQWCWEWIWN
    LWDABUGVGWAWB $.
    $( [10-Nov-2008] $)

  $( The modulo operation is periodic. $)
  modcyc2 $p |- ( ( A e. RR /\ B e. RR+ /\ N e. ZZ )
        -> ( ( A - ( B x. N ) ) mod B ) = ( A mod B ) ) $=
    ( cr wcel crp cz w3a cmul co cmin cmo cneg caddc cc wceq recn rpcn zcn wa
    mulneg1 ancoms mulcom negeqd eqtr4d 3adant1 opreq2d mulcl negsub sylan2
    3impb eqtr2d syl3an opreq1d znegcl modcyc syl3an3 eqtrd ) ADEZBFEZCGEZHZABC
    IJZKJZBLJACMZBIJZNJZBLJZABLJZVBVDVGBLUSAOEZUTBOEZVACOEZVDVGPAQBRCSVJVKVLHZV
    GAVCMZNJZVDVMVFVNANVKVLVFVNPVJVKVLTZVFCBIJZMZVNVLVKVFVRPCBUAUBVPVCVQBCUCUDU
    EUFUGVJVKVLVOVDPZVPVJVCOEVSBCUHAVCUIUJUKULUMUNVAUSUTVEGEVHVIPCUOABVEUPUQUR
    $.
    $( [12-Nov-2008] $)

  $( Addition property of the modulo operation. $)
  modadd1 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR+ )
       /\ ( A mod D ) = ( B mod D ) )
       -> ( ( A + C ) mod D ) = ( ( B + C ) mod D ) ) $=
    ( cr wcel wa crp cmo co wceq caddc cdiv cfl cfv cmul cmin wb modval
    eqeqan12d anandirs adantrl opreq1 syl6bi cc recn adantr ad2antrl rpcn
    adantl rerpdivcl reflcl recnd syl mulcl syl11anc addsub syl111anc adantlr
    adantll eqeq12d sylibrd cz readdcl adantrr simprr flcl modcyc2 syl5ib syld
    3impia ) AEFZBEFZGZCEFZDHFZGZADIJZBDIJZKZACLJZDIJZBCLJZDIJZKZVNVQGZVTWADADM
    JZNOZPJZQJZWCDBDMJZNOZPJZQJZKZWEWFVTAWIQJZCLJZBWMQJZCLJZKZWOWFVTWPWRKZWTVNV
    PVTXARZVOVLVMVPXBVLVPGZVMVPGZVRWPVSWRADSBDSTUAUBWPWRCLUCUDWFWJWQWNWSVLVQWJW
    QKZVMVLVQGZAUEFZCUEFZWIUEFZXEVLXGVQAUFUGVOXHVLVPCUFZUHVLVPXIVOXCDUEFZWHUEFZ
    XIVPXKVLDUIZUJXCWGEFZXLADUKZXNWHWGULUMUNDWHUOUPUBACWIUQURUSVMVQWNWSKZVLVMVQ
    GZBUEFZXHWMUEFZXPVMXRVQBUFUGVOXHVMVPXJUHVMVPXSVOXDXKWLUEFZXSVPXKVMXMUJXDWKE
    FZXTBDUKZYAWLWKULUMUNDWLUOUPUBBCWMUQURUTVAVBWOWJDIJZWNDIJZKWFWEWJWNDIUCWFYC
    WBYDWDVLVQYCWBKZVMXFWAEFZVPWHVCFZYEVLVOYFVPACVDVEVLVOVPVFVLVPYGVOXCXNYGXOWG
    VGUNUBWADWHVHURUSVMVQYDWDKZVLXQWCEFZVPWLVCFZYHVMVOYIVPBCVDVEVMVOVPVFVMVPYJV
    OXDYAYJYBWKVGUNUBWCDWLVHURUTVAVIVJVK $.
    $( [12-Nov-2008] $)

  $( Multiplication property of the modulo operation.  Note that the multiplier
     ` C ` must be an integer. $)
  modmul1 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. ZZ /\ D e. RR+ )
       /\ ( A mod D ) = ( B mod D ) )
       -> ( ( A x. C ) mod D ) = ( ( B x. C ) mod D ) ) $=
    ( cr wcel wa cz crp cmo co wceq cmul cdiv cfl cfv cmin wb modval eqeqan12d
    anandirs adantrl opreq1 syl6bi cc rpcn ad2antll zcn ad2antrl rerpdivcl
    flcl syl mulass syl111anc mul32 eqtr3d opreq2d recn adantr adantl mulcl
    syl11anc subdir eqtr4d adantlr adantll eqeq12d sylibrd zre remulcl sylan2
    adantrr simprr simprl zmulcl modcyc2 syl5ib syld 3impia ) AEFZBEFZGZCHFZDIF
    ZGZADJKZBDJKZLZACMKZDJKZBCMKZDJKZLZWBWEGZWHWIDCADNKZOPZMKZMKZQKZWKDCBDNKZOP
    ZMKZMKZQKZLZWMWNWHADWPMKZQKZCMKZBDXAMKZQKZCMKZLZXEWNWHXGXJLZXLWBWDWHXMRZWCV
    TWAWDXNVTWDGZWAWDGZWFXGWGXJADSBDSTUAUBXGXJCMUCUDWNWSXHXDXKVTWEWSXHLWAVTWEGZ
    WSWIXFCMKZQKZXHXQWRXRWIQXQDCMKZWPMKZWRXRXQDUEFZCUEFZWPUEFZYAWRLWDYBVTWCDUFZ
    UGZWCYCVTWDCUHZUIZVTWDYDWCXOWPHFZYDXOWOEFYIADUJWOUKULZWPUHULZUBZDCWPUMUNXQY
    BYCYDYAXRLYFYHYLDCWPUOUNUPUQXQAUEFZXFUEFZYCXHXSLVTYMWEAURUSVTWDYNWCXOYBYDYN
    WDYBVTYEUTYKDWPVAVBUBYHAXFCVCUNVDVEWAWEXDXKLVTWAWEGZXDWKXICMKZQKZXKYOXCYPWK
    QYOXTXAMKZXCYPYOYBYCXAUEFZYRXCLWDYBWAWCYEUGZWCYCWAWDYGUIZWAWDYSWCXPXAHFZYSX
    PWTEFUUBBDUJWTUKULZXAUHULZUBZDCXAUMUNYOYBYCYSYRYPLYTUUAUUEDCXAUOUNUPUQYOBUE
    FZXIUEFZYCXKYQLWAUUFWEBURUSWAWDUUGWCXPYBYSUUGWDYBWAYEUTUUDDXAVAVBUBUUABXICV
    CUNVDVFVGVHXEWSDJKZXDDJKZLWNWMWSXDDJUCWNUUHWJUUIWLVTWEUUHWJLZWAXQWIEFZWDWQH
    FZUUJVTWCUUKWDWCVTCEFZUUKCVIZACVJVKVLVTWCWDVMXQWCYIUULVTWCWDVNVTWDYIWCYJUBC
    WPVOVBWIDWQVPUNVEWAWEUUIWLLZVTYOWKEFZWDXBHFZUUOWAWCUUPWDWCWAUUMUUPUUNBCVJVK
    VLWAWCWDVMYOWCUUBUUQWAWCWDVNWAWDUUBWCUUCUBCXAVOVBWKDXBVPUNVFVGVQVRVS $.
    $( [12-Nov-2008] $)

  $( Distribute multiplication over a modulo operation. $)
  moddi $p |- ( ( A e. RR+ /\ B e. RR /\ C e. RR+ )
      -> ( A x. ( B mod C ) ) = ( ( A x. B ) mod ( A x. C ) ) ) $=
    ( crp wcel cr w3a cdiv co cfl cfv cmul cmin cmo cc wceq rpcn 3ad2ant1 recn
    3ad2ant2 wa rpre adantl rerpdivcl reflcl syl remulcl syl11anc recnd
    3adant1 subdi syl111anc cc0 wne rpcnne0 3ad2ant3 divcan5 fveq2d opreq2d
    mulass eqtr2d eqtrd modval sylan 3adant3 rpmulcl 3adant2 3eqtr4d ) ADEZBFEZ
    CDEZGZABCBCHIZJKZLIZMIZLIZABLIZACLIZVRVSHIZJKZLIZMIZABCNIZLIVRVSNIZVLVQVRAV
    OLIZMIZWCVLAOEZBOEZVOOEZVQWGPVIVJWHVKAQRZVJVIWIVKBSTZVJVKWJVIVJVKUAZVOWMCFE
    ZVNFEZVOFEVKWNVJCUBUCWMVMFEZWOBCUDZVMUEZUFCVNUGUHUIUJABVOUKULVLWFWBVRMVLWBV
    SVNLIZWFVLWAVNVSLVLVTVMJVLWICOEZCUMUNUAZWHAUMUNUAZVTVMPWLVKVIXAVJCUOUPVIVJX
    BVKAUORBCAUQULURUSVLWHWTVNOEZWSWFPWKVKVIWTVJCQUPVJVKXCVIWMWPXCWQWPVNWRUIUFU
    JACVNUTULVAUSVBVLWDVPALVJVKWDVPPVIBCVCUJUSVLVRFEZVSDEZWEWCPVIVJXDVKVIAFEVJX
    DAUBABUGVDVEVIVKXEVJACVFVGVRVSVCUHVH $.
    $( [11-Nov-2008] $)

  $( Distribute the modulo operation over a subtraction. $)
  modsubdir $p |- ( ( A e. RR /\ B e. RR /\ C e. RR+ ) -> ( ( B mod C )
  <_ ( A mod C ) <-> ( ( A - B ) mod C ) = ( ( A mod C ) - ( B mod C ) ) ) ) $=
    ( cr wcel crp w3a cc0 cmo co cmin cle wbr wceq wb modcl 3adant2 3adant1
    subge0 syl11anc wa cdiv cfl cfv cmul cz resubcl 3adant3 simp3 rerpdivcl
    flcl syl zsubcl modcyc2 syl111anc cc recn 3ad2ant1 3ad2ant2 rpre adantl
    reflcl remulcl recnd sub4 syl22anc 3ad2ant3 subdi opreq2d modval opreq12d
    3eqtr4d opreq1d eqtr3d adantr clt simpl3 simpr modge0 subge02 mpbid modlt
    lelttrd modid eqtrd sylan 3impa breqtrd impbida bitr3d ) ADEZBDEZCFEZGZHACI
    JZBCIJZKJZLMZWPWOLMZABKJZCIJZWQNZWNWODEZWPDEZWRWSOWKWMXCWLACPQZWLWMXDWKBCPR
    ZWOWPSTWNWRXBWNWRUAZXAWQCIJZWQWNXAXHNWRWNWTCACUBJZUCUDZBCUBJZUCUDZKJZUEJZKJ
    ZCIJZXAXHWNWTDEZWMXMUFEZXPXANWKWLXQWMABUGZUHWKWLWMUIWNXJUFEZXLUFEZXRWKWMXTW
    LWKWMUAZXIDEZXTACUJZXIUKULQWLWMYAWKWLWMUAZXKDEZYABCUJZXKUKULRXJXLUMTWTCXMUN
    UOWNXOWQCIWNWTCXJUEJZCXLUEJZKJZKJZAYHKJZBYIKJZKJZXOWQWNAUPEZBUPEZYHUPEZYIUP
    EZYKYNNWKWLYOWMAUQURWLWKYPWMBUQUSWKWMYQWLYBYHYBCDEZXJDEZYHDEWMYSWKCUTZVAYBY
    CYTYDXIVBULZCXJVCTVDQWLWMYRWKYEYIYEYSXLDEZYIDEWMYSWLUUAVAYEYFUUCYGXKVBULZCX
    LVCTVDRABYHYIVEVFWNXNYJWTKWNCUPEXJUPEZXLUPEZXNYJNWNCWMWKYSWLUUAVGZVDWKWMUUE
    WLYBXJUUBVDQWLWMUUFWKYEXLUUDVDRCXJXLVHUOVIWNWOYLWPYMKWKWMWOYLNWLACVJQWLWMWP
    YMNWKBCVJRVKVLVMVNVOXGWQDEZWMWRWQCVPMZXHWQNWNUUHWRWNXCXDUUHXEXFWOWPUGTZVOWK
    WLWMWRVQWNWRVRWNUUIWRWNWQWOCUUJXEUUGWNHWPLMZWQWOLMZWLWMUUKWKBCVSRWNXCXDUUKU
    ULOXEXFWOWPVTTWAWKWMWOCVPMWLACWBQWCVOWQCWDVFWEWNXBUAHXAWQLWNHXALMZXBWKWLWMU
    UMWKWLUAXQWMUUMXSWTCVSWFWGVOWNXBVRWHWIWJ $.
    $( [30-Dec-2008] $)

  $( A number modulo an irrational multiple of it is nonzero. $)
  modirr $p |- ( ( A e. RR /\ B e. RR+ /\ ( A / B ) e. ( RR \ QQ ) )
      -> ( A mod B ) =/= 0 ) $=
    ( cr wcel crp cdiv co cq cdif cmo cc0 wne wn wa eldif wceq cfl cfv cmul
    cmin modval eqeq1d cc wb recn adantr rpre adantl rerpdivcl reflcl syl
    remulcl syl11anc recnd subeq0 eqcom rpcnne0 divmul2 syl111anc syl5rbbr
    3bitrd wi cz flidz zq syl6bi sylbid necon3bd adantld syl5bi 3impia ) ACDZBE
    DZABFGZCHIDZABJGZKLZVOVNCDZVNHDZMZNVLVMNZVQVNCHOWAVTVQVRWAVSVPKWAVPKPZVNQRZ
    VNPZVSWAWBABWCSGZTGZKPZAWEPZWDWAVPWFKABUAUBWAAUCDZWEUCDWGWHUDVLWIVMAUEUFZWA
    WEWABCDZWCCDZWECDVMWKVLBUGUHWAVRWLABUIZVNUJZUKBWCULUMUNAWEUOUMWDVNWCPZWAWHV
    NWCUPWAWIWCUCDZBUCDBKLNZWOWHUDWJWAVRWPWMVRWCWNUNUKVMWQVLBUQUHAWCBURUSUTVAWA
    VRWDVSVBWMVRWDVNVCDVSVNVDVNVEVFUKVGVHVIVJVK $.
    $( [11-Nov-2008] $)

