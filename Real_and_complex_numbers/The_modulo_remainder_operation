$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The modulo (remainder) operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c mod $.  $( Modulo operation. $)
  $c == $.  $( Equivalence symbol (not used yet except in comments). $)

  $( Extend class notation with the modulo operation. $)
  cmo $a class mod $.

  ${
    $d x y z $.
    $( Define the modulo (remainder) operation.  See ~ modval for its value. $)
    df-mod $a |- mod = { <. <. x , y >. , z >. | ( ( x e. RR /\ y e. RR+ )
          /\ z = ( x - ( y x. ( |_ ` ( x / y ) ) ) ) ) } $.
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( The value of the modulo operation.  The modulo congruence notation of
       number theory, ` J == K ( ` modulo ` N ) ` , can be expressed in our
       notation as ` ( J mod N ) = ( K mod N ) ` .  Definition 1 in Knuth, _The
       Art of Computer Programming_, Vol.  I (1972), p. 38.  Knuth uses "mod"
       for the operation and "modulo" for the congruence.  Unlike Knuth, we
       restrict the second argument to positive reals to simplify certain
       theorems.  (This also gives us future flexibility to extend it to any
       one of several different conventions for a zero or negative second
       argument, should there be an advantage in doing so.) $)
    modval $p |- ( ( A e. RR /\ B e. RR+ )
           -> ( A mod B ) = ( A - ( B x. ( |_ ` ( A / B ) ) ) ) ) $=
      ( vx vy vz cr crp cv cdiv co cfl cfv cmul cmin cmo oprex wceq id opreq1
      fveq2d opreq2d opreq12d opreq2 df-mod oprabval2 ) CDEABFGCHZDHZUFUGIJZKLZ
      MJZNJABABIJZKLZMJZNJOAUGAUGIJZKLZMJZNJAUMNPUFAQZUFAUJUPNUQRUQUIUOUGMUQUHU
      NKUFAUGISTUAUBUGBQZUPUMANURUGBUOULMURRURUNUKKUGBAIUCTUBUACDEUDUE $.
      $( [10-Nov-2008] $)
  $}

  $( Closure law for the modulo operation. $)
  modcl $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A mod B ) e. RR ) $=
    ( cr wcel crp wa cmo co cdiv cfl cfv cmul cmin modval remulcl rpre adantl
    rerpdivcl reflcl syl sylanc resubcl syldan eqeltrd ) ACDZBEDZFZABGHABABIHZJ
    KZLHZMHZCABNUEUFUJCDZUKCDBCDZUICDZULUGBUIOUFUMUEBPQUGUHCDUNABRUHSTUAAUJUBUC
    UD $.
    $( [10-Nov-2008] $)

  $( The modulo operation is nonnegative. $)
  modge0 $p |- ( ( A e. RR /\ B e. RR+ ) -> 0 <_ ( A mod B ) ) $=
    ( cr wcel crp wa cc0 cdiv co cfl cfv cmul cmin cmo cle wbr rerpdivcl flle
    syl clt wb lemuldiv2 reflcl pm3.26 rpregt0 adantl syl3anc mpbird remulcl
    rpre sylanc subge0 syldan modval breqtrrd ) ACDZBEDZFZGABABHIZJKZLIZMIZABNI
    OURGVBOPZVAAOPZURVDUTUSOPZURUSCDZVEABQZUSRSUTCDZUPBCDZGBTPFZVDVEUAURUTABUBU
    RVFVHVGUSUCSZUPUQUDUQVJUPBUEUFUGUHUPUQVACDZVCVDUAVIVHVLURBUTUIUQVIUPBUJUFVK
    UKAVAULUMUHABUNUO $.
    $( [10-Nov-2008] $)

  $( The modulo operation is less than its second argument. $)
  modlt $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A mod B ) < B ) $=
    ( cr wcel crp wa cmo co cdiv cfl cfv cmin cmul clt cc cc0 wne wceq divcan2
    3expb recn rpcnne0 syl2an opreq1d subdi rpcn adantl rerpdivcl recnd reflcl
    syl syl3anc modval 3eqtr4rd wbr c1 fraclt1 divid breqtrrd wb ltmuldiv2
    resubcl sylanc rpre rpregt0 mpbird eqbrtrd ) ACDZBEDZFZABGHZBABIHZVLJKZLHZM
    HZBNVJBVLMHZBVMMHZLHZAVQLHVOVKVJVPAVQLAODZBODZBPQZFZVPARZVHVIVSVTWAWCABSTAU
    ABUBZUCUDVTVLODVMODVOVRRVJBVLVMUEVIVTVHBUFUGVJVLABUHZUIVJVMVJVLCDZVMCDZWEVL
    UJUKZUIULABUMUNVJVOBNUOZVNBBIHZNUOZVJVNUPWJNVJWFVNUPNUOWEVLUQUKVIWJUPRZVHVI
    WBWLWDBURUKUGUSVNCDZBCDZWNPBNUOFZWIWKUTVJVNBBVAWFWGWMVJVLVMVBWEWHVCVIWNVHBV
    DUGVIWOVHBVEUGULVFVG $.
    $( [10-Nov-2008] $)

  $( The fractional part of a number is the number modulo 1. $)
  modfrac $p |- ( A e. RR -> ( A mod 1 ) = ( A - ( |_ ` A ) ) ) $=
    ( cr wcel c1 cmo co cdiv cfl cfv cmul cmin crp wceq 1rp modval mpan2 cc
    recn div1 syl fveq2d opreq2d reflcl recnd mulid2 eqtrd ) ABCZADEFZADADGFZHI
    ZJFZKFZAAHIZKFUGDLCUHULMNADOPUGUKUMAKUGUKDUMJFZUMUGUJUMDJUGUIAHUGAQCUIAMARA
    STUAUBUGUMQCUNUMMUGUMAUCUDUMUETUFUBUF $.
    $( [11-Nov-2008] $)

  $( The floor function expressed in terms of the modulo operation. $)
  flmod $p |- ( A e. RR -> ( |_ ` A ) = ( A - ( A mod 1 ) ) ) $=
    ( cr wcel c1 cmo co cmin cfl cfv modfrac opreq2d cc wceq nncan recn reflcl
    recnd sylanc eqtr2d ) ABCZAADEFZGFAAAHIZGFZGFZUBTUAUCAGAJKALCUBLCUDUBMTAUBN
    AOTUBAPQRS $.
    $( [11-Nov-2008] $)

  $( Break a number into its integer part and its fractional part. $)
  intfrac $p |- ( A e. RR -> A = ( ( |_ ` A ) + ( A mod 1 ) ) ) $=
    ( cr wcel cfl cfv c1 cmo co caddc cmin modfrac opreq2d cc wceq pncan3
    reflcl recnd recn sylanc eqtr2d ) ABCZADEZAFGHZIHUBAUBJHZIHZAUAUCUDUBIAKLUB
    MCAMCUEANUAUBAOUAUBAPQARST $.
    $( [31-Dec-2008] $)

  $( Move a nonnegative integer in and out of a floor. $)
  flmulnn0 $p |- ( ( N e. NN0 /\ A e. RR )
              -> ( N x. ( |_ ` A ) ) <_ ( |_ ` ( N x. A ) ) ) $=
    ( cn0 wcel cr wa cfl cfv cmul co c1 cmo caddc cle cc0 wbr addge01 biimp3a
    remulcl nn0re reflcl syl2an crp 1rp modcl mpan2 syl cz wb 0z flge biimpa
    mulge0 nn0ge0 jca modge0 sylanc syl3anc wceq intfrac adantl opreq2d cc
    adddi 3expb nn0cn recnd eqtrd fveq2d flzadd zmulcl nn0z flcl eqtr2d
    breqtrd ) BCDZAEDZFZBAGHZIJZVTBAKLJZIJZGHZMJZBAIJZGHZNVTEDZWCEDZOWCNPZVTWDN
    PZVRWGWHWIWJVTWCQRBEDZVSEDWGVPVQBVSSBTZAUAZUBVRWBEDZWHWKWAEDZWNVPVQBWASWLVQ
    KUCDZWOUDAKUEUFZUBZWBUAUGWNOWBNPZWIVRWNWSWIWNOUHDWSWIUIUJWBOUKUFULWRWKOBNPZ
    FWOOWANPZFWSVPVQBWAUMVPWKWTWLBUNUOVQWOXAWQVQWPXAUDAKUPUFUOUBUQURVRWFVTWBMJZ
    GHZWDVRWEXBGVRWEBVSWAMJZIJZXBVRAXDBIVQAXDUSVPAUTVAVBBVCDZVSVCDZWAVCDZFXEXBU
    SZVPVQXFXGXHXIBVSWAVDVEBVFVQXGXHVQVSWMVGVQWAWQVGUOUBVHVIVTUHDZWNXCWDUSVRWBV
    TVJBUHDVSUHDXJVPVQBVSVKBVLAVMUBWRUQVNVO $.
    $( [24-Jul-2009] $) $( [2-Jan-2009] $)

  $( Move a nonnegative integer in and out of a floor. $)
  flmulnn0OLD $p |- ( ( N e. NN0 /\ A e. RR )
              -> ( N x. ( |_ ` A ) ) <_ ( |_ ` ( N x. A ) ) ) $=
    ( cn0 wcel cr wa cfl cfv cmul co c1 cmo caddc cle cc0 wbr addge01 biimp3a
    remulcl nn0re reflcl syl2an crp 1rp modcl mpan2 syl cz wb 0z flge biimpa
    mulge0OLD anim12i nn0ge0 modge0 sylanc syl3anc wceq intfrac adantl opreq2d
    cc adddi 3expb nn0cn recnd jca eqtrd fveq2d flzadd zmulcl nn0z flcl eqtr2d
    breqtrd ) BCDZAEDZFZBAGHZIJZWABAKLJZIJZGHZMJZBAIJZGHZNWAEDZWDEDZOWDNPZWAWEN
    PZVSWHWIWJWKWAWDQRBEDZVTEDWHVQVRBVTSBTZAUAZUBVSWCEDZWIWLWBEDZWOVQVRBWBSWMVR
    KUCDZWPUDAKUEUFZUBZWCUAUGWOOWCNPZWJVSWOWTWJWOOUHDWTWJUIUJWCOUKUFULWSWLWPFOB
    NPZOWBNPZFWTVSBWBUMVQWLVRWPWMWRUNVQXAVRXBBUOVRWQXBUDAKUPUFUNUQUQURVSWGWAWCM
    JZGHZWEVSWFXCGVSWFBVTWBMJZIJZXCVSAXEBIVRAXEUSVQAUTVAVBBVCDZVTVCDZWBVCDZFXFX
    CUSZVQVRXGXHXIXJBVTWBVDVEBVFVRXHXIVRVTWNVGVRWBWRVGVHUBVIVJWAUHDZWOXDWEUSVSW
    CWAVKBUHDVTUHDXKVQVRBVTVLBVMAVNUBWSUQVOVP $.
    $( [2-Jan-2009] $)

  $( Move a natural number in and out of a floor in the first argument of a
     modulo operation. $)
  modmulnn $p |- ( ( N e. NN /\ A e. RR /\ M e. NN ) -> ( ( N x. ( |_ ` A ) )
           mod ( N x. M ) ) <_ ( ( |_ ` ( N x. A ) ) mod ( N x. M ) ) ) $=
    ( cn wcel cr w3a cfl cfv cmul co cdiv cmin cmo cle wbr cn0 flmulnn0 nnnn0
    sylan 3adant3 wb lesub1 remulcl nnre reflcl syl2an wa syl crp nnmulcl nnrp
    rpre 3adant2 cc0 wne redivcl cc mulne0 nncn nnne0 jca syl3anc sylanc mpbid
    wceq modval fldiv divcan5 recnd syl3an fveq2d recn 3eqtr4rd 3comr eqtrd
    opreq2d 3brtr4d ) CDEZAFEZBDEZGZCAHIZJKZCBJKZWDWELKZHIZJKZMKZCAJKZHIZWHMKZW
    DWENKZWKWENKZOWBWDWKOPZWIWLOPZVSVTWOWACQEVTWOVSACRCSTUAWDFEZWKFEZWHFEZWOWPU
    BWBWDWKWHUCVSVTWQWACFEZWCFEWQVSVTCWCUDCUEZAUFZUGUAZVSVTWRWAVSVTUHWJFEZWRWTV
    TXDVSCAUDXATZWJUFUIUAZWEFEZWGFEZWSWBWEWGUDVSWAXGVTVSWAUHZWEUJEZXGXIWEDEZXJC
    BUKZWEULUIZWEUMUIUNZWBWFFEZXHWQXGWEUOUPZXOWBWDWEUQXCXNVSWAXPVTCUREZCUOUPZUH
    ZBUREZBUOUPZUHZXPVSWACBUSVSXQXRCUTCVAVBZWAXTYABUTBVAVBZUGUNVCWFUFUIVDVCVEWQ
    XJWMWIVFWBWDWEVGXCVSWAXJVTXMUNZVDWBWNWKWEWKWELKHIZJKZMKZWLWRXJWNYHVFWBWKWEV
    GXFYEVDWBYGWHWKMWBYFWGWEJWBYFWJWELKZHIZWGXDXKYFYJVFWBWJWEVHVSVTXDWAXEUAVSWA
    XKVTXLUNVDVTWAVSYJWGVFVTWAVSGZWCBLKZHIZABLKZHIZWGYJVTWAYMYOVFVSABVHUAYKWFYL
    HWCUREYBXSWFYLVFVTWAVSWCBCVIVTWCXBVJYDYCVKVLYKYIYNHAUREYBXSYIYNVFVTWAVSABCV
    IAVMYDYCVKVLVNVOVPVQVQVPVR $.
    $( [2-Jan-2009] $)

  $( Closure law for the modulo operation restricted to integers. $)
  zmodcl $p |- ( ( A e. ZZ /\ B e. NN ) -> ( A mod B ) e. NN0 ) $=
    ( cmo co cn0 wcel cz cc0 cle wbr cn wa elnn0z cdiv cfl cfv cmul cmin cr
    crp wceq modval zre nnrp syl2an zmulcl nnz adantl nndivre sylan flcl syl
    sylanc zsubcl syldan eqeltrd modge0 sylanbrc ) ABCDZEFUSGFHUSIJZAGFZBKFZLZU
    SMVCUSABABNDZOPZQDZRDZGASFZBTFZUSVGUAVAVBABUBAUCZBUDZUEVAVBVFGFZVGGFBGFZVEG
    FZVLVCBVEUFVBVMVABUGUHVCVDSFZVNVHVBVOVAABUIVJUJVDUKULUMAVFUNUOUPVHVIUTVAVBA
    BUQVJVKUEUR $.
    $( [27-Nov-2008] $)

  $( Identity law for modulo. $)
  modid $p |- ( ( ( A e. RR /\ B e. RR+ ) /\ ( 0 <_ A /\ A < B ) )
               -> ( A mod B ) = A ) $=
    ( cr wcel crp wa cc0 cle wbr clt cmo co cdiv cfl cfv cmul cmin wceq modval
    adantr caddc cc rerpdivcl recnd addid2 fveq2d syl c1 cz wb flbi2 0z
    sylancr divge0 rpregt0 sylan2 an1rs adantrr pm3.27 rpcn mulid1 breqtrrd
    ad2ant2l 1re ltdivmul mp3an2 simpll ad2antlr sylanc mpbird mpbir2and
    eqtr3d opreq2d mul01 eqtrd recn subid1 ad2antrr 3eqtrd ) ACDZBEDZFZGAHIZABJ
    IZFZFZABKLZABABMLZNOZPLZQLZAGQLZAWBWGWKRWEABSTWFWJGAQWFWJBGPLZGWFWIGBPWFGWH
    UALZNOZWIGWFWHUBDZWOWIRWFWHWBWHCDZWEABUCTZUDWPWNWHNWHUEUFUGWFWOGRZGWHHIZWHU
    HJIZGUIDWQWSWTXAFUJWFWHGUKULWRUMWBWCWTWDVTWCWAWTVTWCFBCDGBJIFZWTWAABUNBUOZU
    PUQURWFXAABUHPLZJIZWAWDXEVTWCWAWDFABXDJWAWDUSWAXDBRZWDWABUBDZXFBUTZBVAUGTVB
    VCVTXBXAXEUJZWFVTUHCDXBXIVDAUHBVEVFVTWAWEVGWAXBVTWEXCVHVIVJVKVLVMWAWMGRZVTW
    EWAXGXJXHBVNUGVHVOVMVTWLARZWAWEVTAUBDXKAVPAVQUGVRVS $.
    $( [29-Dec-2008] $)

  $( Identity law for modulo. $)
  modid2 $p |- ( ( A e. RR /\ B e. RR+ )
     -> ( ( A mod B ) = A <-> ( 0 <_ A /\ A < B ) ) ) $=
    ( cr wcel crp wa cmo co wceq cc0 cle wbr clt breq2 breq1 anbi12d modge0
    modlt jca syl5cbi modid ex impbid ) ACDBEDFZABGHZAIZJAKLZABMLZFZUFJUEKLZUEB
    MLZFUIUDUFUJUGUKUHUEAJKNUEABMOPUDUJUKABQABRSTUDUIUFABUAUBUC $.
    $( [29-Dec-2008] $)

  $( Absorption law for modulo. $)
  modabs $p |- ( ( ( A e. RR /\ B e. RR+ /\ C e. RR+ ) /\ B <_ C )
           -> ( ( A mod B ) mod C ) = ( A mod B ) ) $=
    ( cr wcel crp w3a cle wbr wa cmo co cc0 clt wceq modcl anim1i 3impa adantr
    modge0 3adant3 rpre 3ad2ant2 3ad2ant3 modlt pm3.27 ltletrd modid syl12anc
    ) ADEZBFEZCFEZGZBCHIZJZABKLZDEZULJZMUPHIZUPCNIUPCKLUPOUMURUNUJUKULURUJUKJUQ
    ULABPZQRSUMUSUNUJUKUSULABTUASUOUPBCUMUQUNUJUKUQULUTUASUMBDEZUNUKUJVAULBUBUC
    SUMCDEZUNULUJVBUKCUBUDSUMUPBNIZUNUJUKVCULABUEUASUMUNUFUGUPCUHUI $.
    $( [29-Dec-2008] $)

  $( Absorption law for modulo. $)
  modabs2 $p |- ( ( A e. RR /\ B e. RR+ )
           -> ( ( A mod B ) mod B ) = ( A mod B ) ) $=
    ( cr wcel crp wa cle wbr cmo co wceq rpre leid syl adantl wi w3a modabs ex
    3anidm23 mpd ) ACDZBEDZFBBGHZABIJZBIJUEKZUCUDUBUCBCDUDBLBMNOUBUCUDUFPUBUCUC
    QUDUFABBRSTUA $.
    $( [29-Dec-2008] $)

  $( The modulo operation is periodic. $)
  modcyc $p |- ( ( A e. RR /\ B e. RR+ /\ N e. ZZ )
        -> ( ( A + ( N x. B ) ) mod B ) = ( A mod B ) ) $=
    ( cr wcel cz crp cmul co caddc cmo wceq w3a cdiv cfl cfv cmin modval wa
    readdcl remulcl zre rpre syl2an sylan2 3impb 3simp3 sylanc cc cc0 wne
    divdir recn 3ad2ant1 recnd 3adant1 rpcnne0 3ad2ant3 syl3anc divcan4 3expb
    zcn opreq2d eqtrd fveq2d fladdz rerpdivcl 3adant2 3simp2 adddi rpcn reflcl
    syl 3ad2ant2 mulcom eqcomd 3eqtrd pnpcan2 mulcl adantl eqtr4d 3com23 ) ADEZ
    CFEZBGEZACBHIZJIZBKIZABKIZLWCWDWEMZWHABABNIZOPZHIZQIZWIWJWHWGBWGBNIZOPZHIZQ
    IZWGWMWFJIZQIZWNWGDEZWEWHWRLWJWGBRWCWDWEXAWCWFDEZXAWDWESZAWFTCDEBDEXBWDWECB
    UACUBBUCUDZUEUFWCWDWEUGUHWJWQWSWGQWJWQBWLCJIZHIZWMBCHIZJIZWSWJWPXEBHWJWPWKC
    JIZOPZXEWJWOXIOWJWOWKWFBNIZJIZXIAUIEZWFUIEZBUIEZBUJUKZSZWOXLLWJAWFBULWCWDXM
    WEAUMUNZWDWEXNWCXCWFXDUOUPZWEWCXQWDBUQZURUSWJXKCWKJWDWEXKCLZWCCUIEZXQYAWDWE
    YBXOXPYACBUTVACVBZXTUDUPVCVDVEWKDEZWDXJXELWJWKCVFWCWEYDWDABVGZVHWCWDWEVIUHV
    DVCXOWLUIEZYBXFXHLWJBWLCVJWEWCXOWDBVKZURWCWEYFWDWCWESZYDYFYEYDWLWKVLUOVMZVH
    WDWCYBWEYCVNUSWJXGWFWMJWJWFXGWDWEWFXGLZWCYBXOYJWDWECBVOYCYGUDUPVPVCVQVCXMWM
    UIEZXNWTWNLWJAWMWFVRXRWCWEYKWDXOYFYKYHBWLVSWEXOWCYGVTYIUHVHXSUSVQWCWEWIWNLW
    DABRVHWAWB $.
    $( [10-Nov-2008] $)

  $( The modulo operation is periodic. $)
  modcyc2 $p |- ( ( A e. RR /\ B e. RR+ /\ N e. ZZ )
        -> ( ( A - ( B x. N ) ) mod B ) = ( A mod B ) ) $=
    ( cr wcel crp cz w3a cmul co cmin cmo cneg caddc cc wceq wa mulneg1 ancoms
    mulcom negeqd eqtr4d 3adant1 opreq2d negsub mulcl sylan2 3impb eqtr2d recn
    rpcn zcn syl3an opreq1d modcyc znegcl syl3an3 eqtrd ) ADEZBFEZCGEZHZABCIJZK
    JZBLJACMZBIJZNJZBLJZABLJZVBVDVGBLAOEZBOEZCOEZVDVGPUSUTVAVJVKVLHZVGAVCMZNJZV
    DVMVFVNANVKVLVFVNPVJVKVLQZVFCBIJZMZVNVLVKVFVRPCBRSVPVCVQBCTUAUBUCUDVJVKVLVO
    VDPZVJVCOEVSVPAVCUEBCUFUGUHUIAUJBUKCULUMUNUSUTVEGEVHVIPVAABVEUOCUPUQUR $.
    $( [12-Nov-2008] $)

  $( Addition property of the modulo operation. $)
  modadd1 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR+ )
       /\ ( A mod D ) = ( B mod D ) )
       -> ( ( A + C ) mod D ) = ( ( B + C ) mod D ) ) $=
    ( cr wcel wa crp cmo co wceq caddc cdiv cfl cfv cmul cmin wb modval
    eqeqan12d anandirs adantrl opreq1 syl6bi cc addsub recn adantr ad2antrl
    mulcl rpcn adantl rerpdivcl reflcl recnd syl sylanc syl3anc adantlr
    adantll eqeq12d sylibrd cz modcyc2 readdcl adantrr simprr flcl syl5bi syld
    3impia ) AEFZBEFZGZCEFZDHFZGZADIJZBDIJZKZACLJZDIJZBCLJZDIJZKZVNVQGZVTWADADM
    JZNOZPJZQJZWCDBDMJZNOZPJZQJZKZWEWFVTAWIQJZCLJZBWMQJZCLJZKZWOWFVTWPWRKZWTVNV
    PVTXARZVOVLVMVPXBVLVPGZVMVPGZVRWPVSWRADSBDSTUAUBWPWRCLUCUDWFWJWQWNWSVLVQWJW
    QKZVMAUEFZCUEFZWIUEFZXEVLVQGZACWIUFVLXFVQAUGUHVOXGVLVPCUGZUIVLVPXHVODUEFZWH
    UEFZXHXCDWHUJVPXKVLDUKZULXCWGEFZXLADUMZXNWHWGUNUOUPUQUBURUSVMVQWNWSKZVLBUEF
    ZXGWMUEFZXPVMVQGZBCWMUFVMXQVQBUGUHVOXGVMVPXJUIVMVPXRVOXKWLUEFZXRXDDWLUJVPXK
    VMXMULXDWKEFZXTBDUMZYAWLWKUNUOUPUQUBURUTVAVBWFWJDIJZWNDIJZKWEWOWFYCWBYDWDVL
    VQYCWBKZVMWAEFZVPWHVCFZYEXIWADWHVDVLVOYFVPACVEVFVLVOVPVGVLVPYGVOXCXNYGXOWGV
    HUPUBURUSVMVQYDWDKZVLWCEFZVPWLVCFZYHXSWCDWLVDVMVOYIVPBCVEVFVMVOVPVGVMVPYJVO
    XDYAYJYBWKVHUPUBURUTVAWJWNDIUCVIVJVK $.
    $( [12-Nov-2008] $)

  $( Multiplication property of the modulo operation.  Note that the multiplier
     ` C ` must be an integer. $)
  modmul1 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. ZZ /\ D e. RR+ )
       /\ ( A mod D ) = ( B mod D ) )
       -> ( ( A x. C ) mod D ) = ( ( B x. C ) mod D ) ) $=
    ( cr wcel wa cz crp cmo co wceq cmul cdiv cfl cfv cmin wb modval eqeqan12d
    anandirs adantrl opreq1 syl6bi cc mulass rpcn ad2antll zcn ad2antrl
    rerpdivcl flcl syl syl3anc mul23 eqtr3d opreq2d subdir recn adantr mulcl
    adantl sylanc eqtr4d adantlr adantll eqeq12d sylibrd modcyc2 remulcl zre
    sylan2 adantrr simprr zmulcl simprl syl5bi syld 3impia ) AEFZBEFZGZCHFZDIFZ
    GZADJKZBDJKZLZACMKZDJKZBCMKZDJKZLZWBWEGZWHWIDCADNKZOPZMKZMKZQKZWKDCBDNKZOPZ
    MKZMKZQKZLZWMWNWHADWPMKZQKZCMKZBDXAMKZQKZCMKZLZXEWNWHXGXJLZXLWBWDWHXMRZWCVT
    WAWDXNVTWDGZWAWDGZWFXGWGXJADSBDSTUAUBXGXJCMUCUDWNWSXHXDXKVTWEWSXHLWAVTWEGZW
    SWIXFCMKZQKZXHXQWRXRWIQXQDCMKZWPMKZWRXRDUEFZCUEFZWPUEFZYAWRLXQDCWPUFWDYBVTW
    CDUGZUHZWCYCVTWDCUIZUJZVTWDYDWCXOWPHFZYDXOWOEFYIADUKWOULUMZWPUIUMZUBZUNYBYC
    YDYAXRLXQDCWPUOYFYHYLUNUPUQAUEFZXFUEFZYCXHXSLXQAXFCURVTYMWEAUSUTVTWDYNWCYBY
    DYNXODWPVAWDYBVTYEVBYKVCUBYHUNVDVEWAWEXDXKLVTWAWEGZXDWKXICMKZQKZXKYOXCYPWKQ
    YOXTXAMKZXCYPYBYCXAUEFZYRXCLYODCXAUFWDYBWAWCYEUHZWCYCWAWDYGUJZWAWDYSWCXPXAH
    FZYSXPWTEFUUBBDUKWTULUMZXAUIUMZUBZUNYBYCYSYRYPLYODCXAUOYTUUAUUEUNUPUQBUEFZX
    IUEFZYCXKYQLYOBXICURWAUUFWEBUSUTWAWDUUGWCYBYSUUGXPDXAVAWDYBWAYEVBUUDVCUBUUA
    UNVDVFVGVHWNWSDJKZXDDJKZLWMXEWNUUHWJUUIWLVTWEUUHWJLZWAWIEFZWDWQHFZUUJXQWIDW
    QVIVTWCUUKWDVTCEFZUUKWCACVJCVKZVLVMVTWCWDVNWCYIUULXQCWPVOVTWCWDVPVTWDYIWCYJ
    UBVCUNVEWAWEUUIWLLZVTWKEFZWDXBHFZUUOYOWKDXBVIWAWCUUPWDWAUUMUUPWCBCVJUUNVLVM
    WAWCWDVNWCUUBUUQYOCXAVOWAWCWDVPWAWDUUBWCUUCUBVCUNVFVGWSXDDJUCVQVRVS $.
    $( [12-Nov-2008] $)

  $( Distribute multiplication over a modulo operation. $)
  moddi $p |- ( ( A e. RR+ /\ B e. RR /\ C e. RR+ )
      -> ( A x. ( B mod C ) ) = ( ( A x. B ) mod ( A x. C ) ) ) $=
    ( crp wcel cr w3a cdiv co cfl cfv cmul cmin cmo cc wceq subdi rpcn
    3ad2ant1 recn 3ad2ant2 wa remulcl rpre adantl rerpdivcl reflcl syl sylanc
    recnd 3adant1 syl3anc cc0 wne divcan5 rpcnne0 3ad2ant3 fveq2d opreq2d
    mulass eqtr2d eqtrd modval sylan 3adant3 rpmulcl 3adant2 3eqtr4d ) ADEZBFEZ
    CDEZGZABCBCHIZJKZLIZMIZLIZABLIZACLIZVRVSHIZJKZLIZMIZABCNIZLIVRVSNIZVLVQVRAV
    OLIZMIZWCAOEZBOEZVOOEZVQWGPVLABVOQVIVJWHVKARSZVJVIWIVKBTUAZVJVKWJVIVJVKUBZV
    OCFEZVNFEZVOFEWMCVNUCVKWNVJCUDUEWMVMFEZWOBCUFZVMUGZUHUIUJUKULVLWFWBVRMVLWBV
    SVNLIZWFVLWAVNVSLVLVTVMJWICOEZCUMUNUBZWHAUMUNUBZVTVMPVLBCAUOWLVKVIXAVJCUPUQ
    VIVJXBVKAUPSULURUSWHWTVNOEZWSWFPVLACVNUTWKVKVIWTVJCRUQVJVKXCVIWMWPXCWQWPVNW
    RUJUHUKULVAUSVBVLWDVPALVJVKWDVPPVIBCVCUKUSVRFEZVSDEZWEWCPVLVRVSVCVIVJXDVKAF
    EVJXDVIABUCAUDVDVEVIVKXEVJACVFVGUIVH $.
    $( [11-Nov-2008] $)

  $( Distribute the modulo operation over a subtraction. $)
  modsubdir $p |- ( ( A e. RR /\ B e. RR /\ C e. RR+ ) -> ( ( B mod C )
  <_ ( A mod C ) <-> ( ( A - B ) mod C ) = ( ( A mod C ) - ( B mod C ) ) ) ) $=
    ( cr wcel crp w3a cc0 cmo co cmin cle wbr wceq wb subge0 modcl 3adant2
    3adant1 sylanc wa cdiv cfl cfv cmul cz modcyc2 resubcl 3adant3 3simp3
    zsubcl rerpdivcl flcl syl syl3anc cc sub4 recn 3ad2ant1 3ad2ant2 remulcl
    rpre adantl reflcl recnd syl2anc subdi 3ad2ant3 opreq2d modval opreq12d
    3eqtr4d opreq1d eqtr3d adantr clt modid 3simpl3 pm3.27 modge0 subge02
    mpbid modlt lelttrd eqtrd sylan 3impa breqtrd impbida bitr3d ) ADEZBDEZCFEZ
    GZHACIJZBCIJZKJZLMZWPWOLMZABKJZCIJZWQNZWODEZWPDEZWRWSOWNWOWPPWKWMXCWLACQRZW
    LWMXDWKBCQSZTWNWRXBWNWRUAZXAWQCIJZWQWNXAXHNWRWNWTCACUBJZUCUDZBCUBJZUCUDZKJZ
    UEJZKJZCIJZXAXHWTDEZWMXMUFEZXPXANWNWTCXMUGWKWLXQWMABUHZUIWKWLWMUJXJUFEZXLUF
    EZXRWNXJXLUKWKWMXTWLWKWMUAZXIDEZXTACULZXIUMUNRWLWMYAWKWLWMUAZXKDEZYABCULZXK
    UMUNSTUOWNXOWQCIWNWTCXJUEJZCXLUEJZKJZKJZAYHKJZBYIKJZKJZXOWQAUPEZBUPEZYHUPEZ
    YIUPEZYKYNNWNABYHYIUQWKWLYOWMAURUSWLWKYPWMBURUTWKWMYQWLYBYHCDEZXJDEZYHDEYBC
    XJVAWMYSWKCVBZVCYBYCYTYDXIVDUNZTVERWLWMYRWKYEYIYSXLDEZYIDEYECXLVAWMYSWLUUAV
    CYEYFUUCYGXKVDUNZTVESVFWNXNYJWTKCUPEXJUPEZXLUPEZXNYJNWNCXJXLVGWNCWMWKYSWLUU
    AVHZVEWKWMUUEWLYBXJUUBVERWLWMUUFWKYEXLUUDVESUOVIWNWOYLWPYMKWKWMWOYLNWLACVJR
    WLWMWPYMNWKBCVJSVKVLVMVNVOWQDEZWMWRWQCVPMZXHWQNXGWQCVQWNUUHWRXCXDUUHWNWOWPU
    HXEXFTZVOWKWLWMWRVRWNWRVSWNUUIWRWNWQWOCUUJXEUUGWNHWPLMZWQWOLMZWLWMUUKWKBCVT
    SXCXDUUKUULOWNWOWPWAXEXFTWBWKWMWOCVPMWLACWCRWDVOVFWEWNXBUAHXAWQLWNHXALMZXBW
    KWLWMUUMXQWMUUMWKWLUAWTCVTXSWFWGVOWNXBVSWHWIWJ $.
    $( [30-Dec-2008] $)

  $( A number modulo an irrational multiple of it is nonzero. $)
  modirr $p |- ( ( A e. RR /\ B e. RR+ /\ ( A / B ) e. ( RR \ QQ ) )
      -> ( A mod B ) =/= 0 ) $=
    ( cr wcel crp cdiv co cq cdif cmo cc0 wne wa wn wceq cfl cfv cmul cmin
    modval eqeq1d cc wb subeq0 recn adantr remulcl rpre adantl rerpdivcl
    reflcl syl sylanc recnd divmul2 rpcnne0 syl3anc eqcom syl5rbbr 3bitrd wi
    cz flidz zq syl6bi sylbid necon3bd adantld eldif syl5ib 3impia ) ACDZBEDZAB
    FGZCHIDZABJGZKLZVLVMMZVNCDZVNHDZNZMVQVOVRWAVQVSVRVTVPKVRVPKOZVNPQZVNOZVTVRW
    BABWCRGZSGZKOZAWEOZWDVRVPWFKABTUAAUBDZWEUBDWGWHUCVRAWEUDVLWIVMAUEUFZVRWEBCD
    ZWCCDZWECDVRBWCUGVMWKVLBUHUIVRVSWLABUJZVNUKZULUMUNUMVRVNWCOZWHWDWIWCUBDZBUB
    DBKLMZWOWHUCVRAWCBUOWJVRVSWPWMVSWCWNUNULVMWQVLBUPUIUQVNWCURUSUTVRVSWDVTVAWM
    VSWDVNVBDVTVNVCVNVDVEULVFVGVHVNCHVIVJVK $.
    $( [11-Nov-2008] $)

