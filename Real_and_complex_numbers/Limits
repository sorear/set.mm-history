$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ~~> $.  $( Limit arrow. $)

  $( Extend class notation with convergence relation for limits. $)
  cli $a class ~~> $.

  ${
    $d x y z f $.
    $( Define the limit relation for complex number sequences.    See ~ clim
       for its relational expression.  Note that ` f : NN --> CC ` is an
       infinite sequence of complex numbers, i.e. a mapping from integers to
       complex numbers. $)
    df-clim $a |- ~~> = { <. f , w >. | ( ( f : NN --> CC /\ w e. CC ) /\
          A. x e. RR ( 0 < x ->
             E. y e. NN A. z e. NN ( y <_ z ->
                ( abs ` ( ( f ` z ) - w ) ) < x ) ) ) } $.
  $}

  ${
    $d x y z w f F $.  $d x y z w f A $.
    clim.1 $e |- F e. V $.
    clim.2 $e |- A e. V $.
    $( Express the predicate: The limit of complex number sequence ` F `
       is ` A ` , or ` F ` converges to ` A ` .  This means that for any
       real ` x ` , no matter how small, there always exists an integer
       ` y ` such that the absolute difference of any later complex number
       in the sequence and the limit is less than ` x ` . $)
    clim $p |- ( F ~~> A <-> ( ( F : NN --> CC /\ A e. CC ) /\
          A. x e. RR ( 0 < x ->
             E. y e. NN A. z e. NN ( y <_ z ->
                ( abs ` ( ( F ` z ) - A ) ) < x ) ) ) ) $=
      ( vf vw cn cc cv wf wcel wa cc0 clt wbr cle cfv cmin co cabs wi wral
      wrex cr cli wceq feq1 birand fveq1 opreq1d fveq2d breq1d bilimd biraldv
      birexdv bi2and eleq1 biland opreq2 df-clim brab ) JKHLZMZILZKNZOZPALZQRZB
      LCLZSRZVLVETZVGUAUBZUCTZVJQRZUDZCJUEZBJUFZUDZAUGUEZOJKEMZVHOZVKVMVLETZVGU
      AUBZUCTZVJQRZUDZCJUEZBJUFZUDZAUGUEZOWCDKNZOZVKVMWEDUAUBZUCTZVJQRZUDZCJUEZ
      BJUFZUDZAUGUEZOHIEDUHFGVEEUIZVIWDWBWMXDVFWCVHJKVEEUJUKXDWAWLAUGXDVTWKVKXD
      VSWJBJXDVRWICJXDVQWHVMXDVPWGVJQXDVOWFUCXDVNWEVGUAVLVEEULUMUNUOUPUQURUPUQU
      SVGDUIZWDWOWMXCXEVHWNWCVGDKUTVAXEWLXBAUGXEWKXAVKXEWJWTBJXEWIWSCJXEWHWRVMX
      EWGWQVJQXEWFWPUCVGDWEUAVBUNUOUPUQURUPUQUSABCIHVCVD $.
      $( [30-Sep-99] $)

    $( A sequence of complex numbers with a limit is a sequence. $)
    climseq $p |- ( F ~~> A -> F : NN --> CC ) $=
      ( vx vy vz cli wbr cn cc wf wcel wa cc0 cv clt cle cfv cmin co cabs wi
      wral wrex cr clim pm3.26bd pm3.26d ) BAHIZJKBLZAKMZUJUKULNOEPZQIFPGPZRIUN
      BSATUAUBSUMQIUCGJUDFJUEUCEUFUDEFGABCDUGUHUI $.
      $( [30-Sep-99] $)

    $( Closure of the limit of a sequence of complex numbers. $)
    climcn $p |- ( F ~~> A -> A e. CC ) $=
      ( vx vy vz cli wbr cn cc wf wcel wa cc0 cv clt cle cfv cmin co cabs wi
      wral wrex cr clim pm3.26bd pm3.27d ) BAHIZJKBLZAKMZUJUKULNOEPZQIFPGPZRIUN
      BSATUAUBSUMQIUCGJUDFJUEUCEUFUDEFGABCDUGUHUI $.
      $( [30-Sep-99] $)

    $( Convergence of a sequence of complex numbers. $)
    climconv $p |- ( F ~~> A -> A. x e. RR ( 0 < x ->
             E. y e. NN A. z e. NN ( y <_ z ->
                ( abs ` ( ( F ` z ) - A ) ) < x ) ) ) $=
      ( cli wbr cn cc wf wcel wa cc0 cv clt cle cfv cmin co cabs wi wral wrex
      cr clim pm3.27bd ) EDHIJKELDKMNOAPZQIBPCPZRIUJESDTUAUBSUIQIUCCJUDBJUEUCAU
      FUDABCDEFGUGUH $.
      $( [30-Sep-99] $)
  $}

  ${
    $d x y z w f F $.  $d x y z w f A $.
    $( The limit of a sequence of complex numbers. $)
    clim2 $p |- ( ( F : NN --> CC /\ A e. CC ) ->  ( F ~~> A <->
          A. x e. RR ( 0 < x ->
             E. y e. NN A. z e. NN ( y <_ z ->
                ( abs ` ( ( F ` z ) - A ) ) < x ) ) ) ) $=
      ( vf vw cn cc wf wcel wa cli wbr cc0 cv clt cle cfv cmin co cabs wi wral
      wrex cr cvv wb wceq feq1 birand fveq1 opreq1d fveq2d breq1d bilimd
      biraldv birexdv bi2and eleq1 biland opreq2 df-clim brabg wfn ffn nnex
      fnex ax-mp syl sylan ibar bitr4d ) HIEJZDIKZLZEDMNZVPOAPZQNZBPCPZRNZVTESZ
      DTUAZUBSZVRQNZUCZCHUDZBHUEZUCZAUFUDZLZWJEUGKZVOVQWKUHVNHIFPZJZGPZIKZLZVSW
      AVTWMSZWOTUAZUBSZVRQNZUCZCHUDZBHUEZUCZAUFUDZLVNWPLZVSWAWBWOTUAZUBSZVRQNZU
      CZCHUDZBHUEZUCZAUFUDZLWKFGEDUGIMWMEUIZWQXGXFXOXPWNVNWPHIWMEUJUKXPXEXNAUFX
      PXDXMVSXPXCXLBHXPXBXKCHXPXAXJWAXPWTXIVRQXPWSXHUBXPWRWBWOTVTWMEULUMUNUOUPU
      QURUPUQUSWODUIZXGVPXOWJXQWPVOVNWODIUTVAXQXNWIAUFXQXMWHVSXQXLWGBHXQXKWFCHX
      QXJWEWAXQXIWDVRQXQXHWCUBWODWBTVBUNUOUPUQURUPUQUSABCGFVCVDVNEHVEZWLHIEVFHU
      GKXRWLUCVGHUGEVHVIVJVKVPWJVLVM $.
      $( [30-Sep-99] $)
  $}

  ${
    $d x y z $.
    $( The zero sequence converges to zero. $)
    clim0 $p |- ( NN X. { 0 } ) ~~> 0 $=
      ( vx vy vz cn cc0 csn cxp cli wbr cc wf wcel wa cv clt cle cfv cmin co
      cabs wi wral wrex cr wss 0cn elisseti fconst snssi ax-mp fss mp2an
      pm3.2i c1 wceq fvconst mpan opreq1d subid syl6eq fveq2d cleqid abs00
      mpbir breq1d biimprd a1d com3r r19.21aiv 1nn jctil breq1 birimd biraldv
      rcla4ev syl a1i rgen nnex snex xpex clim ) DEFZGZEHIDJWDKZEJLZMZEANZOIZBN
      ZCNZPIZWKWDQZERSZTQZWHOIZUAZCDUBZBDUCZUAZAUDUBZMWGXAWEWFDWCWDKZWCJUEZWEDE
      EJUFUGZUHZWFXCUFEJUIUJDWCJWDUKULUFUMWTAUDWTWHUDLWIUNDLZUNWKPIZWPUAZCDUBZM
      WSWIXIXFWIXHCDWKDLZXGWIWPXJWIWPUAXGXJWPWIXJWOEWHOXJWOETQZEXJWNETXJWNEERSE
      XJWMEERXBXJWMEUOXEDEWKWDUPUQUREUFUSUTVAXKEUOEEUOEVBEUFVCVDUTVEVFVGVHVIVJV
      KWRXIBUNDWJUNUOZWQXHCDXLWLXGWPWJUNWKPVLVMVNVOVPVQVRUMABCEWDDWCVSEVTWAXDWB
      VD $.
      $( [2-Oct-99] $)
  $}

  ${
    $d x y z w v A $.  $d x y z w v B $.  $d x y z w v F $.
    climuni.1 $e |- A e. V $.
    climuni.2 $e |- B e. V $.
    climuni.3 $e |- F e. V $.
    ${
      climunii.3 $e |- ( F ~~> A /\ F ~~> B ) $.
      $( A sequence of complex numbers converges to at most one limit. $)
      climunii $p |- A = B $=
        ( vy vz vw vx cmin co cc0 wceq cabs cfv clt wbr wn cv cle wa cn wrex
        wral nn2get rgen2 c2 cdiv wi cli cc wcel pm3.26i climcn ax-mp pm3.27i
        subcl abscl 2re 2pos divgt0lem cr gt0ne0i divrcl breq2 bilimd biraldv
        birexdv bi2imd climconv vtoclri jca r19.26 ltnr wf climseq ffvrn mpan
        abssubt mpan2 syl breq1d birand pm3.2i abs3lemt sylbid mtoi con3 com12
        prth syl5 r19.20i sylbir r19.22si reeanv ralnex birex rexnal bitr
        3imtr3 3syl mt2 absgt0 bicon1 mpbi subeq0 ) ABLMZNOZABONXIPQZRSZTXJXLHU
        AZIUAZUBSZJUAZXNUBSZUCZIUDUEZJUDUFZHUDUFZXSHJUDIXMXPUGUHXLNXKUIUJMZRSZX
        OXNCQZALMPQZYBRSZUKZIUDUFZHUDUEZXQYDBLMPQZYBRSZUKZIUDUFZJUDUEZUCZYATZXK
        UIXIABCAULSZAUMUNZYQCBULSZGUOZACFDUPUQZYSBUMUNZYQYSGURZBCFEUPUQZUSZUTZV
        AVBVCYCYIYNYBVDUNZYCYIUKZXKUIUVFVAUIVAVBVEVFZNKUAZRSZXOYEUVJRSZUKZIUDUF
        ZHUDUEZUKZUVHKYBVDUVJYBOZUVKYCUVOYIUVJYBNRVGZUVQUVNYHHUDUVQUVMYGIUDUVQU
        VLYFXOUVJYBYERVGVHVIVJVKYQUVPKVDUFYTKHIACFDVLUQVMUQUVGYCYNUKZUVIUVKXQYJ
        UVJRSZUKZIUDUFZJUDUEZUKZUVSKYBVDUVQUVKYCUWCYNUVRUVQUWBYMJUDUVQUWAYLIUDU
        VQUVTYKXQUVJYBYJRVGVHVIVJVKYSUWDKVDUFUVCKJIBCFEVLUQVMUQVNYHYMUCZJUDUEZH
        UDUEXRTZIUDUFZJUDUEZHUDUEZYOYPUWFUWIHUDUWEUWHJUDUWEYGYLUCZIUDUFUWHYGYLI
        UDVOUWKUWGIUDXNUDUNZXRYFYKUCZUKZUWGUWKUWLUWMTZUWNUWGUKUWLUWMXKXKRSZXKUV
        FVPUWLUWMAYDLMPQZYBRSZYKUCZUWPUWLYFUWRYKUWLYEUWQYBRUWLYDUMUNZYEUWQOZUDU
        MCVQZUWLUWTYQUXBYTACFDVRUQUDUMXNCVSVTZUWTYRUXAUVAYDAWAWBWCWDWEUWLUWTUWS
        UWPUKZUXCUWTXKVDUNZUXDUVFYRUVBUCUWTUXEUCUXDYRUVBUVAUVDWFABYDXKWGVTWBWCW
        HWIUWNUWOUWGXRUWMWJWKWCXOYFXQYKWLWMWNWOWPWPYHYMHJUDUDWQUWJXTTZHUDUEYPUW
        IUXFHUDUWIXSTZJUDUEUXFUWHUXGJUDXRIUDWRWSXSJUDWTXAWSXTHUDWTXAXBXCXDXJXLX
        IUVEXEXFXGABUVAUVDXHXG $.
        $( [2-Oct-99] $)
    $}

    $( A complex number sequence converges to at most one limit. $)
    climuni $p |- ( ( F ~~> A /\ F ~~> B ) -> A = B ) $=
      ( cli wbr wa wceq cc0 cded cleq1 cleq2 cn csn cxp cvv cc 0cn elisseti
      keepel nnex snex xpex breq2 birand biland breq1 bi2and clim0 pm3.2i
      elimhyp3v climunii dedth2v ) CAGHZCBGHZIZABJURAKLZBJUSURBKLZJABKKAUSBMBUT
      USNUSUTURCOKPZQZLZURAKRDKSTUAZUBURBKREVDUBURCVBRFOVAUCKUDUEUBURCUSGHZUQIV
      ECUTGHZIVCUSGHZVCUTGHZIABCKKVBVBKGHZVIIVBUSGHZVIIVJVBUTGHZIAUSJUPVEUQAUSC
      GUFUGBUTJUQVFVEBUTCGUFUHCVCJVEVGVFVHCVCUSGUICVCUTGUIUJKUSJVIVJVIKUSVBGUFU
      GKUTJVIVKVJKUTVBGUFUHVBVCJVJVGVKVHVBVCUSGUIVBVCUTGUIUJVIVIUKUKULUMUNUO $.
      $( [2-Oct-99] $)
  $}

