$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ~~> $.  $( Limit arrow. $)

  $( Extend class notation with convergence relation for limits. $)
  cli $a class ~~> $.

  ${
    $d j k x y f $.
    $( Define the limit relation for complex number sequences.  See ~ clim for
       its relational expression. $)
    df-clim $a |- ~~> = { <. f , y >. | ( y e. CC /\
        A. x e. RR+ E. j e. ZZ A. k e. ( ZZ>= ` j )
          ( ( f ` k ) e. CC /\ ( abs ` ( ( f ` k ) - y ) ) < x ) ) } $.

    $( The limit relation is a relation. $)
    climrel $p |- Rel ~~> $=
      ( vy vk vf vx vj cli wrel cv cc wcel cfv cmin co cabs clt wbr wa cuz wral
      cz wrex crp copab relopab df-clim releqi mpbir ) FGAHZIJBHCHKZIJUIUHLMNKD
      HOPQBEHRKSETUADUBSQZCAUCZGUJCAUDFUKDACEBUEUFUG $.
      $( [5-Nov-2013] $) $( [28-Aug-2005] $)
  $}

  ${
    $d j k x y f F $.  $d j k x y f A $.
    $( Express the predicate:  The limit of complex number sequence ` F ` is
       ` A ` , or ` F ` converges to ` A ` .  This means that for any real
       ` x ` , no matter how small, there always exists an integer ` j ` such
       that the absolute difference of any later complex number in the sequence
       and the limit is less than ` x ` . $)
    clim $p |- ( ( F e. C /\ A e. D ) -> ( F ~~> A <-> ( A e. CC /\
      A. x e. RR+ E. j e. ZZ A. k e. ( ZZ>= ` j )
        ( ( F ` k ) e. CC /\ ( abs ` ( ( F ` k ) - A ) ) < x ) ) ) ) $=
      ( vy cv cc wcel cfv cmin cabs clt wa wral cz crp ralbidv wbr cuz wrex cli
      vf co wceq fveq1 eleq1d opreq1d fveq2d breq1d anbi12d anbi2d eleq1 opreq2
      rexbidv df-clim brabg ) HIZJKZFIZUEIZLZJKZVDUTMUFZNLZAIZOUAZPZFEIUBLZQZER
      UCZASQZPVAVBGLZJKZVOUTMUFZNLZVHOUAZPZFVKQZERUCZASQZPBJKZVPVOBMUFZNLZVHOUA
      ZPZFVKQZERUCZASQZPUEHGBCDUDVCGUGZVNWCVAWLVMWBASWLVLWAERWLVJVTFVKWLVEVPVIV
      SWLVDVOJVBVCGUHZUIWLVGVRVHOWLVFVQNWLVDVOUTMWMUJUKULUMTUQTUNUTBUGZVAWDWCWK
      UTBJUOWNWBWJASWNWAWIERWNVTWHFVKWNVSWGVPWNVRWFVHOWNVQWENUTBVOMUPUKULUNTUQT
      UMAHUEEFURUS $.
      $( [5-Nov-2013] $) $( [28-Aug-2005] $)

    $( Closure of the limit of a sequence of complex numbers. $)
    climcl $p |- ( ( A e. C /\ F ~~> A ) -> A e. CC ) $=
      ( vk vx vj wcel cli wbr wa cc cv cfv cmin co cabs clt cuz wral cvv cz crp
      wrex simpl wb climrel brrelexi clim sylan mpbid ancoms simpld ) ABGZCAHIZ
      JAKGZDLCMZKGUPANOPMELQIJDFLRMSFUAUCEUBSZUNUMUOUQJZUNUMJUNURUNUMUDUNCTGUMU
      NURUECAHUFUGEATBFDCUHUIUJUKUL $.
      $( [5-Nov-2013] $) $( [28-Aug-2005] $)
  $}

