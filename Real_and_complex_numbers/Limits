$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Limits
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ~~> $.  $( Limit arrow. $)

  $( Extend class notation with convergence relation for limits. $)
  cli $a class ~~> $.

  ${
    $d j k x y f $.
    $( Define the limit relation for complex number sequences.   See ~ clim
       for its relational expression. $)
    df-clim $a |- ~~> = { <. f , y >. | ( y e. CC /\
          A. x e. RR ( 0 < x ->
             E. j e. ZZ A. k e. ZZ ( j <_ k ->
              ( ( f ` k ) e. CC /\ ( abs ` ( ( f ` k ) - y ) ) < x ) ) ) ) } $.

    $( The limit relation is a relation. $)
    climrel $p |- Rel ~~> $=
      ( vy vx vj vk vf cli wrel cv cc wcel cc0 clt wbr cle cfv cmin co cabs wa
      wi cz wral wrex cr copab relopab df-clim releqi mpbir ) FGAHZIJKBHZLMCHDH
      ZNMULEHOZIJUMUJPQROUKLMSTDUAUBCUAUCTBUDUBSZEAUEZGUNEAUFFUOBAECDUGUHUI $.
      $( [21-Sep-2005] $) $( [28-Aug-2005] $)
  $}

  ${
    $d j k x y f F $.  $d j k x y f A $.  $d j k x B $.
    $( Express the predicate: The limit of complex number sequence ` F `
       is ` A ` , or ` F ` converges to ` A ` .  This means that for any
       real ` x ` , no matter how small, there always exists an integer
       ` j ` such that the absolute difference of any later complex number
       in the sequence and the limit is less than ` x ` . $)
    clim $p |- ( ( F e. C /\ A e. D ) -> ( F ~~> A <-> ( A e. CC /\
   A. x e. RR ( 0 < x -> E. j e. ZZ A. k e. ZZ ( j <_ k ->
    ( ( F ` k ) e. CC /\ ( abs ` ( ( F ` k ) - A ) ) < x ) ) ) ) ) ) $=
      ( vy vf cv cc wcel cc0 clt wbr cle cfv cmin co cabs wa wi cz wral wrex
      cr cli wceq fveq1 eleq1d opreq1d fveq2d breq1d anbi12d imbi2d rexralbidv
      ralbidv anbi2d eleq1 opreq2 df-clim brabg ) HJZKLZMAJZNOZEJFJZPOZVGIJZQZK
      LZVJVCRSZTQZVENOZUAZUBZFUCUDEUCUEZUBZAUFUDZUAVDVFVHVGGQZKLZVTVCRSZTQZVENO
      ZUAZUBZFUCUDEUCUEZUBZAUFUDZUABKLZVFVHWAVTBRSZTQZVENOZUAZUBZFUCUDEUCUEZUBZ
      AUFUDZUAIHGBCDUGVIGUHZVSWIVDWSVRWHAUFWSVQWGVFWSVPWFEFUCUCWSVOWEVHWSVKWAVN
      WDWSVJVTKVGVIGUIZUJWSVMWCVENWSVLWBTWSVJVTVCRWTUKULUMUNUOUPUOUQURVCBUHZVDW
      JWIWRVCBKUSXAWHWQAUFXAWGWPVFXAWFWOEFUCUCXAWEWNVHXAWDWMWAXAWCWLVENXAWBWKTV
      CBVTRUTULUMURUOUPUOUQUNAHIEFVAVB $.
      $( [18-Sep-2005] $) $( [28-Aug-2005] $)

    $( Closure of the limit of a sequence of complex numbers. $)
    climcl $p |- ( ( A e. C /\ F ~~> A ) -> A e. CC ) $=
      ( vx vj vk wcel cli wbr cc cvv wi wa cc0 cv clt cle cfv cmin co cabs cz
      wral wrex cr clim pm3.26bda ex wn climrel brrelexi con3i pm2.21d adantr
      pm2.61ian imp ) ABGZCAHIZAJGZCKGZUQURUSLZUTUQMZURUSVBURUSNDOZPIEOFOZQIVDC
      RZJGVEASTUARVCPIMLFUBUCEUBUDLDUEUCDAKBEFCUFUGUHUTUIZVAUQVFURUSURUTCAHUJUK
      ULUMUNUOUP $.
      $( [21-Sep-2005] $) $( [28-Aug-2005] $)
  $}

