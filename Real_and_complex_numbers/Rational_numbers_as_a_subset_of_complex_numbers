$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rational numbers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( Define the set of rationals.  Definition of rational numbers in
       [Apostol] p. 22. $)
    df-q $a |- QQ = { x | E. y e. ZZ E. z e. NN x = ( y / z ) } $.
  $}

  ${
    $d x y z A $.
    $( Membership in the set of rationals. $)
    elq $p |- ( A e. QQ <-> E. x e. ZZ E. y e. NN A = ( x / y ) ) $=
      ( vz cq wcel cv cdiv co wceq cn wrex cz cab df-q eleq2i cvv wi oprex
      eleq1 mpbiri a1i r19.23aiv cleq1 birexdv elab3g bitr ) CEFCDGZAGZBGZHIZJZ
      BKLZAMLZDNZFCUKJZBKLZAMLZEUOCDABOPUNURDCUQCQFZAMUQUSRUIMFUPUSBKUPUSRUJKFU
      PUSUKQFUIUJHSCUKQTUAUBUCUBUCUHCJZUMUQAMUTULUPBKUHCUKUDUEUEUFUG $.
      $( [8-Jan-02] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The ratio of an integer and a natural number is a rational number. $)
    znq $p |- ( ( A e. ZZ /\ B e. NN ) -> ( A / B ) e. QQ ) $=
      ( vx vy cz wcel cn wa cdiv co cv wceq wrex cq cleqid opreq1 cleq2d
      opreq2 rcla42ev mpan2 elq sylibr ) AEFBGFHZABIJZCKZDKZIJZLZDGMCEMZUDNFUCU
      DUDLZUIUDOUHUJUDAUFIJZLCDABEGUEALUGUKUDUEAUFIPQUFBLUKUDUDUFBAIRQSTCDUDUAU
      B $.
      $( [12-Jan-02] $)
  $}

  ${
    $d x y A $.
    $( A rational number is a real number. $)
    qret $p |- ( A e. QQ -> A e. RR ) $=
      ( vx vy cq wcel cv cdiv co wceq cn wrex cz cr elq wa eleq1 cc0 wne
      divrclt exp31 imp32 zret nnret nnne0t jca syl2an syl5bir com12
      r19.23aivv sylbi ) ADEABFZCFZGHZIZCJKBLKAMEZBCANUNUOBCLJUNUKLEZULJEZOZUOU
      NUOUMMEZURAUMMPUKMEZULMEZULQRZOUSUPUQUTVAVBUSUTVAVBUSUKULSTUAUKUBUQVAVBUL
      UCULUDUEUFUGUHUIUJ $.
      $( [14-Nov-02] $)

    $( An integer is a rational number. $)
    zqt $p |- ( A e. ZZ -> A e. QQ ) $=
      ( vx vy cv wceq cz wrex cdiv co cn wcel cq c1 cr cc zret recnt div1t
      3syl cleq2d cleqcom syl6rbbr 1nn opreq2 rcla4ev mpan syl6bi r19.22i
      risset elq 3imtr4 ) BDZAEZBFGAULCDZHIZEZCJGZBFGAFKALKUMUQBFULFKZUMAULMHIZ
      EZUQURUTAULEUMURUSULAURULNKULOKUSULEULPULQULRSTULAUAUBMJKUTUQUCUPUTCMJUNM
      EUOUSAUNMULHUDTUEUFUGUHBAFUIBCAUJUK $.
      $( [9-Jan-02] $)
  $}

  $( The integers are a subset of the rationals. $)
  zssq $p |- ZZ (_ QQ $=
    ( vx cz cq cv zqt ssriv ) ABCADEF $.
    $( [9-Jan-02] $)

  $( The nonnegative integers are a subset of the rationals. $)
  nn0ssq $p |- NN0 (_ QQ $=
    ( cn0 cz cq nn0ssz zssq sstri ) ABCDEF $.
    $( [15-Sep-04] $) $( [31-Jul-04] $)

  $( The natural numbers are a subset of the rationals. $)
  nnssq $p |- NN (_ QQ $=
    ( cn cz cq nnssz zssq sstri ) ABCDEF $.
    $( [?] $) $( [31-Jul-04] $)

  $( The rationals are a subset of the reals. $)
  qssre $p |- QQ (_ RR $=
    ( vx cq cr cv qret ssriv ) ABCADEF $.
    $( [9-Jan-02] $)

  $( The rationals are a subset of the complex numbers. $)
  qsscn $p |- QQ (_ CC $=
    ( cq cr cc qssre axresscn sstri ) ABCDEF $.
    $( [?] $) $( [2-Aug-04] $)

  $( A rational number is a complex number. $)
  qcnt $p |- ( A e. QQ -> A e. CC ) $=
    ( cq cc qsscn sseli ) BCADE $.
    $( [?] $) $( [2-Aug-04] $)

  $( The set of real numbers exists. $)
  reex $p |- RR e. V $=
    ( cr cc axcnex axresscn ssexi ) ABCDE $.
    $( [?] $) $( [30-Jul-04] $)

  $( The set of rational numbers exists. $)
  qex $p |- QQ e. V $=
    ( cq cr reex qssre ssexi ) ABCDE $.
    $( [?] $) $( [30-Jul-04] $)

  $( The set of integers exists. $)
  zex $p |- ZZ e. V $=
    ( cz cq qex zssq ssexi ) ABCDE $.
    $( [?] $) $( [30-Jul-04] $)

  ${
    $d x y z w v u A $.  $d x y z w v u B $.
    $( Closure of addition of rationals. $)
    qaddclt $p |- ( ( A e. QQ /\ B e. QQ ) -> ( A + B ) e. QQ ) $=
      ( vx vy vz vw vu vv cv cdiv co wceq cn wrex cz caddc cq wcel wa wi cmulc
      opreq1 cleq2d opreq2 rcla42ev elq sylibr zaddclt zmulclt nnzt sylan5
      adantrl adantlr sylan adantrr adantll sylan13 nnmulclt jca adantr
      opreq12 cc cc0 wne divadddivt zcnt nncnt im2an nnne0t adantl sylan9eqr
      an4s exp43 r19.23aivv r19.23advv imp syl2anb ) ACIZDIZJKZLZDMNCONZBEIZFIZ
      JKZLZFMNEONZABPKZQRZAQRBQRWBWGWIWBWFWIEFOMWAWCORZWDMRZSZWFWITTCDOMVRORZVS
      MRZSZWAWLWFWIWOWLWAWFWIVRWDUAKZVSWCUAKZPKZORZVSWDUAKZMRZSZWHWRWTJKZLZWIWO
      WLSZWAWFSZSXBXDSWHGIZHIZJKZLZHMNGONWIXJXDWHWRXHJKZLGHWRWTOMXGWRLXIXKWHXGW
      RXHJUBUCXHWTLXKXCWHXHWTWRJUDUCUEGHWHUFUGXEXBXFXEWSXAWPORZWQORZWSXEWPWQUHW
      MWLXLWNWMWKXLWJWMWDORXLWKVRWDUIWDUJUKULUMWNWLXMWMWNWJXMWKVSORWJXMWNVSWCUI
      VSUJUNUOUPUQWNWLXAWMWNWKXAWJVSWDURULUPUSUTXFXEWHVTWEPKZXCAVTBWEPVAVRVBRZV
      SVBRZSZWCVBRZWDVBRZSZSVSVCVDZWDVCVDZSXNXCLXEVRVSWCWDVEWOXQWLXTWMXOWNXPVRV
      FVSVGVHWJXRWKXSWCVFWDVGVHVHWOYAWLYBWNYAWMVSVIVJWKYBWJWDVIVJVHUQVKUQVLVMVN
      VOVPCDAUFEFBUFVQ $.
      $( [16-Sep-04] $) $( [1-Aug-04] $)

    $( Closure law for the negative of a rational. $)
    qnegclt $p |- ( A e. QQ -> -u A e. QQ ) $=
      ( vx vy vz vw cq wcel cv cdiv co wceq cn wrex cz cneg elq wa znegclt
      imran a1d cc cc0 wne divnegt anasss zcnt nncnt nnne0t jca syl2an cleq2d
      negeq syl5bi jcad opreq1 weq opreq2 rcla42ev sylibr syl6 r19.23aivv
      sylbi ) AFGABHZCHZIJZKZCLMBNMAOZFGZBCAPVFVHBCNLVCNGZVDLGZQZVFVCOZNGZVJQZV
      GVLVDIJZKZQZVHVKVFVNVPVKVNVFVIVMVJVCRSTVKVGVEOZKVPVFVKVRVOVGVCUAGZVDUAGZV
      DUBUCZQVRVOKZVIVJVSVTWAWBVCVDUDUEVCUFVJVTWAVDUGVDUHUIUJUKAVEULUMUNVQVGDHZ
      EHZIJZKZELMDNMVHWFVPVGVLWDIJZKDEVLVDNLWCVLKWEWGVGWCVLWDIUOUKECUPWGVOVGWDV
      DVLIUQUKURDEVGPUSUTVAVB $.
      $( [17-Sep-04] $) $( [2-Aug-04] $)

    $( Closure of multiplication of rationals. $)
    qmulclt $p |- ( ( A e. QQ /\ B e. QQ ) -> ( A x. B ) e. QQ ) $=
      ( vx vy vz vw vv vu cv cdiv co wceq cn wrex cz cmulc cq wcel wa wi
      opreq1 cleq2d opreq2 rcla42ev elq sylibr zmulclt nnmulclt im2an an4s
      adantr opreq12 cc cc0 wne divmuldivt zcnt nncnt nnne0t adantl sylan13
      sylan9eqr exp43 r19.23aivv r19.23advv imp syl2anb ) ACIZDIZJKZLZDMNCONZBE
      IZFIZJKZLZFMNEONZABPKZQRZAQRBQRVLVQVSVLVPVSEFOMVKVMORZVNMRZSZVPVSTTCDOMVH
      ORZVIMRZSZVKWBVPVSWEWBVKVPVSVHVMPKZORZVIVNPKZMRZSZVRWFWHJKZLZVSWEWBSZVKVP
      SZSWJWLSVRGIZHIZJKZLZHMNGONVSWRWLVRWFWPJKZLGHWFWHOMWOWFLWQWSVRWOWFWPJUAUB
      WPWHLWSWKVRWPWHWFJUCUBUDGHVRUEUFWMWJWNWCVTWDWAWJWCVTSWGWDWASWIVHVMUGVIVNU
      HUIUJUKWNWMVRVJVOPKZWKAVJBVOPULVHUMRZVIUMRZSZVMUMRZVNUMRZSZSVIUNUOZVNUNUO
      ZSWTWKLWMVHVIVMVNUPWEXCWBXFWCXAWDXBVHUQVIURUIVTXDWAXEVMUQVNURUIUIWEXGWBXH
      WDXGWCVIUSUTWAXHVTVNUSUTUIVAVBVAUJVCVDVEVFCDAUEEFBUEVG $.
      $( [?] $) $( [1-Aug-04] $)
  $}

  $( Closure of subtraction of rationals. $)
  qsubclt $p |- ( ( A e. QQ /\ B e. QQ ) -> ( A - B ) e. QQ ) $=
    ( cq wcel wa cmin co cneg caddc cc wceq subnegt qcnt syl2an qaddclt
    qnegclt sylan5 eqeltrd ) ACDZBCDZEABFGZABHZIGZCAJDBJDUAUCKSTABLAMBMNSUBCDUC
    CDTAUBOBPQR $.
    $( [?] $) $( [2-Aug-04] $)

  ${
    $d x y z w A $.
    $( Closure of reciprocal of rationals. $)
    qrecclt $p |- ( ( A e. QQ /\ A =/= 0 ) -> ( 1 / A ) e. QQ ) $=
      ( vx vy vz vw cq wcel cc0 wne c1 cdiv co cv wceq cn wrex cz wi elq wa
      cmulc neeq1 cc wb divneq0bt zcnt nncnt im2an sylan bicomd sylan9bbr
      zmulclt nnzt sylan5 adantr sqznn adantlr jca opreq2 dividt adantrr
      opreq1d w3a divdivdivt pm3.26 ancri pm3.27 3jca syl2an eqtr3d anassrs
      an1rs sylan9eqr exp sylbid anasss nnne0t ancli opreq1 cleq2d rcla42ev
      sylibr syl8 r19.23aivv sylbi imp ) AFGZAHIZJAKLZFGZWGABMZCMZKLZNZCOPBQPWH
      WJRZBCASWNWOBCQOWKQGZWLOGZTZWNWHWKWLUALZQGZWKWKUALZOGZTZWIWSXAKLZNZTZWJWP
      WQWLHIZTWNWHXFRZRZWQWPWQXGXIWRXGTZWNXHXJWNTZWHWKHIZXFWNWHWMHIZXJXLAWMHUBX
      JXLXMWKUCGZWLUCGZTZXGXLXMUDWRWKWLUEWPXNWQXOWKUFWLUGUHZUIUJUKXKXLXFXKXLTXC
      XEXJXLXCWNWRXLXCXGWRXLTWTXBWRWTXLWPWLQGWTWQWKWLULWLUMUNUOWPXLXBWQWKUPUQUR
      UQUQXJXLWNXEWNXJXLTWIJWMKLZXDAWMJKUSWRXLXGXRXDNZWRXLXGXSXPXLXGTZXSWRXPXTT
      ZWKWKKLZWMKLZXRXDYAYBJWMKXNXTYBJNZXOXNXLYDXGWKUTVAUQVBXNXNTZXPTXLXLXGVCYC
      XDNXPXTWKWKWKWLVDXPYEXPXNXNXNXOVEZYFURVFXTXLXLXGXLXGVEZYGXLXGVGVHVIVJXQUI
      VKVLVMVLURVNVOVNVPWQXGWLVQVRUNXFWIDMZEMZKLZNZEOPDQPWJYKXEWIWSYIKLZNDEWSXA
      QOYHWSNYJYLWIYHWSYIKVSVTYIXANYLXDWIYIXAWSKUSVTWADEWISWBWCWDWEWF $.
      $( [?] $) $( [3-Aug-04] $)
  $}

  ${
    $d x y z w A $.
    $( Closure of division of rationals. $)
    qdivclt $p |- ( ( ( A e. QQ /\ B e. QQ ) /\ B =/= 0 ) -> ( A / B ) e. QQ ) $=
      ( cq wcel wa cc0 wne cdiv co c1 cmulc cc wceq divrect qcnt im2an sylan
      qmulclt qrecclt sylan5 anassrs eqeltrd ) ACDZBCDZEZBFGZEABHIZAJBHIZKIZCAL
      DZBLDZEUFUGUIMUEABNUCUJUDUKAOBOPQUCUDUFUICDZUCUHCDULUDUFEAUHRBSTUAUB $.
      $( [?] $) $( [3-Aug-04] $)
  $}

  $( Reverse closure law for addition of rationals. $)
  qrevaddclt $p |- ( B e. QQ ->
                   ( ( A e. CC /\ ( A + B ) e. QQ ) <-> A e. QQ ) ) $=
    ( cq wcel cc caddc co wa wb cmin qsubclt ancoms adantlr wceq pncant qcnt
    sylan5 adantr eleq1d mpbid exp wi qaddclt impbid pm5.32d pm4.71ri syl6bbr
    ) BCDZAEDZABFGZCDZHUIACDZHULUHUIUKULUHUIUKULIUHUIHZUKULUMUKULUMUKHZUJBJGZCD
    ZULUHUKUPUIUKUHUPUJBKLMUNUOACUMUOANZUKUIUHUQUIBEDUQUHABOBPQLRSTUAUHULUKUBUI
    UHULUKULUHUKABUCLUARUDUAUEULUIAPUFUG $.
    $( [?] $) $( [2-Aug-04] $)

