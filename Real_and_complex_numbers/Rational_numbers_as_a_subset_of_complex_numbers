$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rational numbers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d m n x $.
    $( Define the set of rational numbers.  Definition of rationals in
       [Apostol] p. 22. $)
    df-q $a |- QQ = { x | E. m e. ZZ E. n e. NN x = ( m / n ) } $.
  $}

  ${
    $d x y z A $.
    $( Membership in the set of rationals. $)
    elq $p |- ( A e. QQ <-> E. x e. ZZ E. y e. NN A = ( x / y ) ) $=
      ( vz cq wcel cv cdiv co wceq cn wrex cz cab df-q eleq2i cvv wi oprex
      eleq1 mpbiri a1i r19.23aiv eqeq1 2rexbidv elab3 bitr ) CEFCDGZAGZBGZHIZJZ
      BKLAMLZDNZFCUKJZBKLZAMLZEUNCDABOPUMUQDCUPCQFZAMUPURRUIMFUOURBKUOURRUJKFUO
      URUKQFUIUJHSCUKQTUAUBUCUBUCUHCJULUOABMKUHCUKUDUEUFUG $.
      $( [8-Jan-2002] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The ratio of an integer and a natural number is a rational number. $)
    znq $p |- ( ( A e. ZZ /\ B e. NN ) -> ( A / B ) e. QQ ) $=
      ( vx vy cz wcel cn wa cdiv co cv wceq wrex cq eqid rcla4eopr mp3an3 elq
      sylibr ) AEFZBGFZHABIJZCKDKIJLDGMCEMZUBNFTUAUBUBLUCUBOCDEGABUBIPQCDUBRS
      $.
      $( [12-Jan-2002] $)
  $}

  ${
    $d x y A $.
    $( A rational number is a real number. $)
    qret $p |- ( A e. QQ -> A e. RR ) $=
      ( vx vy cq wcel cv cdiv co wceq cn wrex cz cr elq wa eleq1 cc0 wne
      redivclt 3expb zret nnret nnne0t jca syl2an syl5cbir r19.23aivv sylbi )
      ADEABFZCFZGHZIZCJKBLKAMEZBCANULUMBCLJULUMUKMEZUILEZUJJEZOAUKMPUIMEZUJMEZU
      JQRZOUNUOUPUQURUSUNUIUJSTUIUAUPURUSUJUBUJUCUDUEUFUGUH $.
      $( [14-Nov-2002] $)

    $( An integer is a rational number. $)
    zqt $p |- ( A e. ZZ -> A e. QQ ) $=
      ( vx vy cv wceq cz wrex cdiv co cn wcel cq c1 cr cc zret recnt div1t
      3syl eqeq2d eqcom syl6rbbr 1nn opreq2 rcla4ev mpan syl6bi r19.22i risset
      elq 3imtr4 ) BDZAEZBFGAULCDZHIZEZCJGZBFGAFKALKUMUQBFULFKZUMAULMHIZEZUQURU
      TAULEUMURUSULAURULNKULOKUSULEULPULQULRSTULAUAUBMJKUTUQUCUPUTCMJUNMEUOUSAU
      NMULHUDTUEUFUGUHBAFUIBCAUJUK $.
      $( [9-Jan-2002] $)
  $}

  $( The integers are a subset of the rationals. $)
  zssq $p |- ZZ (_ QQ $=
    ( vx cz cq cv zqt ssriv ) ABCADEF $.
    $( [9-Jan-2002] $)

  $( The nonnegative integers are a subset of the rationals. $)
  nn0ssq $p |- NN0 (_ QQ $=
    ( cn0 cz cq nn0ssz zssq sstri ) ABCDEF $.
    $( [15-Sep-2004] $) $( [31-Jul-2004] $)

  $( The natural numbers are a subset of the rationals. $)
  nnssq $p |- NN (_ QQ $=
    ( cn cz cq nnssz zssq sstri ) ABCDEF $.
    $( [21-Sep-2004] $) $( [31-Jul-2004] $)

  $( The rationals are a subset of the reals. $)
  qssre $p |- QQ (_ RR $=
    ( vx cq cr cv qret ssriv ) ABCADEF $.
    $( [9-Jan-2002] $)

  $( The rationals are a subset of the complex numbers. $)
  qsscn $p |- QQ (_ CC $=
    ( cq cr cc qssre axresscn sstri ) ABCDEF $.
    $( [21-Sep-2004] $) $( [2-Aug-2004] $)

  $( A natural number is rational. $)
  nnqt $p |- ( A e. NN -> A e. QQ ) $=
    ( cn cq nnssq sseli ) BCADE $.
    $( [10-Dec-2004] $) $( [17-Nov-2004] $)

  $( A rational number is a complex number. $)
  qcnt $p |- ( A e. QQ -> A e. CC ) $=
    ( cq cc qsscn sseli ) BCADE $.
    $( [25-Sep-2004] $) $( [2-Aug-2004] $)

  $( The set of rational numbers exists. $)
  qex $p |- QQ e. V $=
    ( cq cr reex qssre ssexi ) ABCDE $.
    $( [25-Sep-2004] $) $( [30-Jul-2004] $)

  ${
    $d x y z w v u A $.  $d x y z w v u B $.
    $( Closure of addition of rationals. $)
    qaddclt $p |- ( ( A e. QQ /\ B e. QQ ) -> ( A + B ) e. QQ ) $=
      ( vx vy vz vw vu vv cv cdiv co wceq cn wrex cz caddc cq wcel wa wi cmul
      w3a rcla4eopr elq sylibr zaddclt zmulclt nnzt sylan2 ad2ant2rl sylan
      ad2ant2lr sylanc adantr nnmulclt ad2ant2l opreq12 cc cc0 wne divadddivt
      zcnt nncnt anim12i nnne0t adantl sylan9eqr syl3anc an4s exp43 r19.23aivv
      r19.23advv imp syl2anb ) ACIZDIZJKZLZDMNCONZBEIZFIZJKZLZFMNEONZABPKZQRZAQ
      RBQRVSWDWFVSWCWFEFOMVRVTORZWAMRZSZWCWFTTCDOMVOORZVPMRZSZVRWIWCWFWLWIVRWCW
      FVOWAUAKZVPVTUAKZPKZORZVPWAUAKZMRZWEWOWQJKZLZWFWLWISZVRWCSZSWPWRWTUBWEGIH
      IJKLHMNGONWFGHOMWOWQWEJUCGHWEUDUEXAWPXBWMORZWNORZWPXAWMWNUFWJWHXCWKWGWJWA
      ORXCWHVOWAUGWAUHUIUJWKWGXDWJWHVPORWGXDWKVPVTUGVPUHUKULUMUNXAWRXBWKWHWRWJW
      GVPWAUOUPUNXBXAWEVQWBPKZWSAVQBWBPUQVOURRZVPURRZSZVTURRZWAURRZSZSVPUSUTZWA
      USUTZSXEWSLXAVOVPVTWAVAWLXHWIXKWJXFWKXGVOVBVPVCVDWGXIWHXJVTVBWAVCVDVDWLXL
      WIXMWKXLWJVPVEVFWHXMWGWAVEVFVDUMVGVHVIVJVKVLVMCDAUDEFBUDVN $.
      $( [16-Sep-2004] $) $( [1-Aug-2004] $)

    $( Closure law for the negative of a rational. $)
    qnegclt $p |- ( A e. QQ -> -u A e. QQ ) $=
      ( vx vy vz vw cq wcel cv cdiv co wceq cn wrex cz cneg elq wa cc cc0 wne
      divnegt 3expb zcnt nncnt nnne0t jca syl2an eqeq2d negeq syl5bi znegclt
      anim1i jctild rcla4eopr 3expa sylibr syl6 r19.23aivv sylbi ) AFGABHZCHZIJ
      ZKZCLMBNMAOZFGZBCAPVCVEBCNLUTNGZVALGZQZVCUTOZNGZVGQZVDVIVAIJZKZQZVEVHVCVM
      VKVHVDVBOZKVMVCVHVOVLVDUTRGZVARGZVASTZQVOVLKZVFVGVPVQVRVSUTVAUAUBUTUCVGVQ
      VRVAUDVAUEUFUGUHAVBUIUJVFVJVGUTUKULUMVNVDDHEHIJKELMDNMZVEVJVGVMVTDENLVIVA
      VDIUNUODEVDPUPUQURUS $.
      $( [17-Sep-2004] $) $( [2-Aug-2004] $)

    $( Closure of multiplication of rationals. $)
    qmulclt $p |- ( ( A e. QQ /\ B e. QQ ) -> ( A x. B ) e. QQ ) $=
      ( vx vy vz vw vv vu cv cdiv co wceq cn wrex cz cmul cq wcel wa wi
      rcla4eopr 3expa elq sylibr zmulclt nnmulclt anim12i an4s adantr opreq12
      cc cc0 wne divmuldivt zcnt nncnt nnne0t adantl sylanc sylan9eqr exp43
      r19.23aivv r19.23advv imp syl2anb ) ACIZDIZJKZLZDMNCONZBEIZFIZJKZLZFMNEON
      ZABPKZQRZAQRBQRVJVOVQVJVNVQEFOMVIVKORZVLMRZSZVNVQTTCDOMVFORZVGMRZSZVIVTVN
      VQWCVTVIVNVQVFVKPKZORZVGVLPKZMRZSZVPWDWFJKZLZVQWCVTSZVIVNSZSWHWJSVPGIHIJK
      LHMNGONZVQWEWGWJWMGHOMWDWFVPJUAUBGHVPUCUDWKWHWLWAVRWBVSWHWAVRSWEWBVSSWGVF
      VKUEVGVLUFUGUHUIWLWKVPVHVMPKZWIAVHBVMPUJVFUKRZVGUKRZSZVKUKRZVLUKRZSZSVGUL
      UMZVLULUMZSWNWILWKVFVGVKVLUNWCWQVTWTWAWOWBWPVFUOVGUPUGVRWRVSWSVKUOVLUPUGU
      GWCXAVTXBWBXAWAVGUQURVSXBVRVLUQURUGUSUTUSUHVAVBVCVDCDAUCEFBUCVE $.
      $( [25-Sep-2004] $) $( [1-Aug-2004] $)
  $}

  $( Closure of subtraction of rationals. $)
  qsubclt $p |- ( ( A e. QQ /\ B e. QQ ) -> ( A - B ) e. QQ ) $=
    ( cq wcel wa cneg caddc co cmin cc wceq negsubt qcnt syl2an qaddclt
    qnegclt sylan2 eqeltrrd ) ACDZBCDZEABFZGHZABIHZCAJDBJDUBUCKSTABLAMBMNSUACDU
    BCDTAUAOBPQR $.
    $( [2-Aug-2006] $) $( [2-Aug-2004] $)

  ${
    $d x y z w A $.
    $( Closure of reciprocal of rationals. $)
    qrecclt $p |- ( ( A e. QQ /\ A =/= 0 ) -> ( 1 / A ) e. QQ ) $=
      ( vx vy vz vw cq wcel cc0 wne c1 cdiv co cv wceq cn wrex cz wi elq wa
      cmul neeq1 cc wb divne0bt 3expa zcnt nncnt anim12i sylan bicomd
      sylan9bbr zmulclt nnzt sylan2 adantr msqznn adantlr jca opreq2 dividt
      ad2ant2r opreq1d w3a divdivdivt pm3.26 ancri id 3anidm12 syl2an eqtr3d
      anassrs an1rs sylan9eqr ex sylbid anasss nnne0t ancli rcla4eopr sylibr
      syl8 r19.23aivv sylbi imp ) AFGZAHIZJAKLZFGZWFABMZCMZKLZNZCOPBQPWGWIRZBCA
      SWMWNBCQOWJQGZWKOGZTZWMWGWJWKUALZQGZWJWJUALZOGZTZWHWRWTKLZNZTZWIWOWPWKHIZ
      TWMWGXERZRZWPWOWPXFXHWQXFTZWMXGXIWMTZWGWJHIZXEWMWGWLHIZXIXKAWLHUBXIXKXLWJ
      UCGZWKUCGZTZXFXKXLUDZWQXMXNXFXPWJWKUEUFWOXMWPXNWJUGWKUHUIZUJUKULXJXKXEXJX
      KTXBXDXIXKXBWMWQXKXBXFWQXKTWSXAWQWSXKWOWKQGWSWPWJWKUMWKUNUOUPWOXKXAWPWJUQ
      URUSURURXIXKWMXDWMXIXKTWHJWLKLZXCAWLJKUTWQXKXFXRXCNZWQXKXFXSXOXKXFTZXSWQX
      OXTTZWJWJKLZWLKLZXRXCYAYBJWLKXMXKYBJNXNXFWJVAVBVCXMXMTZXOTXKXKXFVDZYCXCNX
      OXTWJWJWJWKVEXOYDXOXMXMXMXNVFZYFUSVGXKXFYEYEVHVIVJVKXQUJVLVMVNVMUSVOVPVOV
      QWPXFWKVRVSUOXEWHDMEMKLNEOPDQPZWIWSXAXDYGDEQOWRWTWHKVTUFDEWHSWAWBWCWDWE
      $.
      $( [27-Sep-2004] $) $( [3-Aug-2004] $)
  $}

  ${
    $d x y z w A $.
    $( Closure of division of rationals. $)
    qdivclt $p |- ( ( ( A e. QQ /\ B e. QQ ) /\ B =/= 0 ) ->
                  ( A / B ) e. QQ ) $=
      ( cq wcel wa cc0 wne cdiv co c1 cmul cc wceq divrect 3expa qcnt anim12i
      sylan qmulclt qrecclt sylan2 anassrs eqeltrd ) ACDZBCDZEZBFGZEABHIZAJBHIZ
      KIZCALDZBLDZEUGUHUJMZUFUKULUGUMABNOUDUKUEULAPBPQRUDUEUGUJCDZUDUICDUNUEUGE
      AUISBTUAUBUC $.
      $( [27-Sep-2004] $) $( [3-Aug-2004] $)
  $}

  $( Reverse closure law for addition of rationals. $)
  qrevaddclt $p |- ( B e. QQ ->
                   ( ( A e. CC /\ ( A + B ) e. QQ ) <-> A e. QQ ) ) $=
    ( cq wcel cc caddc co wa cmin wceq pncant qcnt sylan2 ancoms adantr
    qsubclt adantlr eqeltrrd ex wi qaddclt expcom impbid pm5.32da pm4.71ri
    syl6bbr ) BCDZAEDZABFGZCDZHUHACDZHUKUGUHUJUKUGUHHZUJUKULUJUKULUJHUIBIGZACUL
    UMAJZUJUHUGUNUHBEDUNUGABKBLMNOUGUJUMCDZUHUJUGUOUIBPNQRSUGUKUJTUHUKUGUJABUAU
    BOUCUDUKUHALUEUF $.
    $( [27-Sep-2004] $) $( [2-Aug-2004] $)

  $( The reciprocal of a natural number is rational. $)
  nnrecqt $p |- ( A e. NN -> ( 1 / A ) e. QQ ) $=
    ( c1 cz wcel cn cdiv co cq 1z znq mpan ) BCDAEDBAFGHDIBAJK $.
    $( [10-Dec-2004] $) $( [17-Nov-2004] $)

  $( The reciprocal of a natural number is real. $)
  nnrecret $p |- ( A e. NN -> ( 1 / A ) e. RR ) $=
    ( cn wcel c1 cdiv co cq cr nnrecqt qret syl ) ABCDAEFZGCLHCAILJK $.
    $( [10-Feb-2008] $) $( [8-Feb-2008] $)

  ${
    $d x y z A $.  $d x y z B $.
    $( The rational numbers are dense in ` RR ` : any two real numbers have a
       rational between them.  Exercise 6 of [Apostol] p. 28. $)
    qbtwnre $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
                  E. x e. QQ ( A < x /\ x < B ) ) $=
      ( vy vz cr wcel clt wbr cv wa cq wrex c1 cdiv co cmin cn cc0 posdift wi
      nnreclt resubclt sylan ex ancoms sylbid cmul cle caddc cz wreu axmulrcl
      nnret peano2rem syl adantrl zbtwnre reurex 3syl wb wceq cc subdit nncnt
      recnt syl3an 3com23 3expb breq2d 1re ltdivmult mp3anl1 exp31 com23
      nngt0t sylc syl5 imp ltsub13t mp3an3 syl2an anandis 3bitr4d adantr
      anbi1d ltletrt 3expa adantrr jca ltmuldiv2t 3exp1 com4r mpcom adantrd
      imp31 sylibd axmulcl ax1cn npcant mpan2 w3a 3ad2ant2 syl3anl2 mpdan
      3coml bitr4d biimpd adantlrl anim12d zret sylan2 znq adantlr jctild
      breq2 breq1 anbi12d rcla4ev syl6 anass syl5ibr exp4b com34 r19.23adv mpd
      expcom syld 3impia ) BFGZCFGZBCHIZBAJZHIZYMCHIZKZALMZYJYKKZYLNDJZOPCBQPZH
      IZDRMZYQYRYLSYTHIZUUBBCTYKYJUUCUUBUAYKYJKZUUCUUBYTFGZUUCUUBUUDYTDUBCBUCZU
      DUEUFUGYRUUAYQDRYSRGZYRUUAYQUAZUUGYRKZYSCUHPZNQPZEJZUIIZUULUUKNUJPZHIZKZE
      UKMZUUHUUIUUKFGZUUPEUKULUUQUUGYKUURYJUUGYKKZUUJFGZUURYSFGZYKUUTUUGYSCUMZY
      SUNZUDUUJUOUPUQZEUUKURUUPEUKUSUTUUIUUPUUHEUKUUIUULUKGZUUAUUPYQUUIUVEUUAUU
      PYQUUIUVEKZUUAUUMKZUUOKZYQUUAUUPKUVFUVHUULYSOPZLGZBUVIHIZUVICHIZKZKYQUVFU
      VHUVMUVJUUIUULFGZUVHUVMUAUVEUUIUVNKZUVGUVKUUOUVLUVOUVGYSBUHPZUULHIZUVKUVO
      UVGUVPUUKHIZUUMKZUVQUVOUUAUVRUUMUUIUUAUVRVAUVNUUINYSYTUHPZHIZNUUJUVPQPZHI
      ZUUAUVRUUIUVTUWBNHUUGYJYKUVTUWBVBZUUGYKYJUWDYSVCGZCVCGZBVCGUWDUUGYKYJYSCB
      VDYSVEZCVFZBVFVGVHVIVJUUGYRUUAUWAVAZUUGUUEUWIYRUVASYSHIZUUEUWIUAUUGUVAUUE
      UWJUWIUVAUUEUWJUWINFGZUVAUUEUWJUWIVKNYSYTVLVMVNVOUVCYSVPZVQYKYJUUEUUFUFVR
      VSUVAYRUVRUWCVAZUUGUVAYJYKUWMUVPFGZUUTUWMUVAYJKUVAYKKUWNUUTUWKUWMVKUVPUUJ
      NVTWAYSBUMZUVBWBWCUVCUDWDWEWFUWNUURKUVNUVSUVQUAZUUIUWNUURUVNUWPUVPUUKUULW
      GWHUUIUWNUURUUGYJUWNYKUVAYJUWNUUGUWOUVCUDWIUVDWJUDUGUUGYRUVNUVQUVKVAZUUGY
      JUVNUWQUAZYKUWJUUGYJUWRUAUWLUUGYJUVNUWJUWQUUGUVAYJUVNUWJUWQUAUAUAUVCUVAYJ
      UVNUWJUWQYSBUULWKWLUPWMWNWOWPWQUUGYKUVNUUOUVLUAYJUUSUVNKZUUOUVLUWSUUOUULU
      UJHIZUVLUWSUUNUUJUULHUUSUUNUUJVBZUVNUUSUUJVCGZUXAUWEUWFUXBUUGYKYSCWRUWGUW
      HWBUXBNVCGUXAWSUUJNWTXAUPWEVJUUGYKUVNUVLUWTVAZUVNUUGYKUXCUVNUUGYKXBUWJUXC
      UUGUVNUWJYKUWLXCUVNUVAYKUWJUXCUUGUULYSCVLUVCXDXEXFWHXGXHXIXJUULXKXLUUGUVE
      UVJYRUVEUUGUVJUULYSXMUFXNXOYPUVMAUVILYMUVIVBYNUVKYOUVLYMUVIBHXPYMUVICHXQX
      RXSXTUUAUUMUUOYAYBYCYDYEYFYGYEYHYI $.
      $( [15-Feb-2007] $) $( [18-Nov-2004] $)

    $( The rational numbers are dense in ` RR* ` : any two extended real
       numbers have a rational between them. $)
    qbtwnxr $p |- ( ( A e. RR* /\ B e. RR* /\ A < B )
                   -> E. x e. QQ ( A < x /\ x < B ) ) $=
      ( cxr wcel clt wbr cv wa cq wrex cr cpnf wceq cmnf w3o wi qbtwnre 3expia
      c1 caddc co peano2re ltp1t mpd3an23 ltpnft syl adantr pnfxr xrlttrt
      mp3an3 qret rexrt adantl sylanc mpan2d anim2d r19.22dva mpd wb breq2
      anbi2d rexbidv mpbird a1d wn nltmnft negbid pm2.21d 3jaodan elxr sylan2b
      pnfnltt breq1 cmin peano2rem id ltm1t syl3anc mnfltt mnfxr mp3an1 syl2an
      mpand anim1d anbi1d cz 1z zqt ax-mp anbi12d rcla4ev mpan 1re mpbiri
      breq12 mtbiri 3jaoian sylanb 3impia ) BDEZCDEZBCFGZBAHZFGZXDCFGZIZAJKZBLE
      ZBMNZBONZPXBXCXHQZXAXIXBXLXJXKXICLEZCMNZCONZPZXLXBXIXMXLXNXOXIXMXCXHABCRS
      XIXNIZXHXCXQXHXEXDMFGZIZAJKZXIXTXNXIXEXDBTUAUBZFGZIZAJKZXTXIYALEZBYAFGYDB
      UCZBUDABYARUEXIYCXSAJXIXDJEZIZYBXRXEYHYBYAMFGZXRXIYIYGXIYEYIYFYAUFUGUHXDD
      EZYADEZYBYIIXRQZYHYJYKMDEYLUIXDYAMUJUKYGYJXIYGXDLEYJXDULXDUMUGZUNXIYKYGXI
      YEYKYFYAUMUGUHUOUPUQURUSUHXNXHXTUTXIXNXGXSAJXNXFXRXECMXDFVAVBVCUNVDVEXIXO
      IZXCXHYNXCVFZBOFGZVFZXIYQXOXIXAYQBUMBVGUGUHXOYOYQUTXIXOXCYPCOBFVAVHUNVDVI
      VJCVKZVLXJXBIZXCXHYSYOMCFGZVFZXBUUAXJCVMUNXJYOUUAUTXBXJXCYTBMCFVNVHUHVDVI
      XKXPXLXBXKXMXLXNXOXKXMIZXHXCUUBXHOXDFGZXFIZAJKZXMUUEXKXMCTVOUBZXDFGZXFIZA
      JKZUUEUUFLEZXMUUFCFGUUIXMAUUFCRCVPZXMVQCVRVSXMUUHUUDAJXMYGIZUUGUUCXFUULOU
      UFFGZUUGUUCXMUUMYGXMUUJUUMUUKUUFVTUGUHUUFDEZYJUUMUUGIUUCQZXMYGODEZUUNYJUU
      OWAOUUFXDUJWBXMUUJUUNUUKUUFUMUGYMWCWDWEURUSUNXKXHUUEUTXMXKXGUUDAJXKXEUUCX
      FBOXDFVNWFVCUHVDVEXKXNIXHXCBTFGZTCFGZXHXKXNTJEZUUQUURIZXHTWGEUUSWHTWIWJXG
      UUTATJXDTNXEUUQXFUURXDTBFVAXDTCFVNWKWLWMXKUUQOTFGZTLEZUVAWNTVTWJBOTFVNWOX
      NUURTMFGZUVBUVCWNTUFWJCMTFVAWOWCVEXKXOIZXCXHUVDXCOOFGZUUPUVEVFWAOVGWJBOCO
      FWPWQVIVJYRVLWRBVKWSWT $.
      $( [8-Feb-2007] $) $( [6-Feb-2007] $)
  $}

  ${
    $d x A $.
    $( If a nonnegative real is less than any positive rational, it is zero. $)
    qsqueeze $p |- ( ( A e. RR /\ 0 <_ A /\ A. x e. QQ ( 0 < x -> A < x ) ) ->
                   A = 0 ) $=
      ( cr wcel cc0 cle wbr cv clt wi cq wral w3a wceq wn wa wrex wb 0re
      ltnlet mpan qbtwnre mp3an1 ex ltnsymt con2d qret sylan2 anim2d r19.22dva
      syld sylbird rexanali syl6ib a3d imp 3adant2 letri3t mpan2 iba bicomd
      sylan9bb 3adant3 mpbird ) BCDZEBFGZEAHZIGZBVGIGZJAKLZMBENZBEFGZVEVJVLVFVE
      VJVLVEVLVJVEVLOZVHVIOZPZAKQZVJOVEVMEBIGZVPECDZVEVQVMRSEBTUAVEVQVHVGBIGZPZ
      AKQZVPVEVQWAVRVEVQWASAEBUBUCUDVEVTVOAKVEVGKDZPVSVNVHVEVGCDZVSVNJWBVEWCPVI
      VSBVGUEUFVGUGUHUIUJUKULVHVIAKUMUNUOUPUQVEVFVKVLRVJVEVKVLVFPZVFVLVEVRVKWDR
      SBEURUSVFVLWDVFVLUTVAVBVCVD $.
      $( [6-Feb-2007] $) $( [6-Feb-2007] $)
  $}

