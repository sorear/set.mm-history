$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rational numbers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d m n x $.
    $( Define the set of rational numbers.  Based on definition of rationals in
       [Apostol] p. 22. $)
    df-q $a |- QQ = ( / " ( ZZ X. NN ) ) $.
  $}

  ${
    $d x y z A $.
    $( Membership in the set of rationals. $)
    elq $p |- ( A e. QQ <-> E. x e. ZZ E. y e. NN A = ( x / y ) ) $=
      ( vz cq wcel cdiv cz cn cxp cima cv co wceq wrex df-q cc cc0 wss mp2an wb
      eleq2i csn cdif wfn cmul crio df-div riotaex fnoprab2a zsscn nncn eldifsn
      wne nnne0 sylanbrc ssriv xpss12 oprelimab bitri ) CEFCGHIJZKZFZCALZBLZGMN
      BIOAHOZEVBCPUBGQQRUCUDZJZUEVAVHSZVCVFUAABQVGVEDLUFMVDNZDQUGGABDUHVJDQUIUJ
      HQSIVGSVIUKAIVGVDIFVDQFVDRUNVDVGFVDULVDUOVDQRUMUPUQHQIVGURTABVHHICGUSTUT
      $.
      $( [28-Jan-2014] $) $( [8-Jan-2002] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The ratio of an integer and a natural number is a rational number. $)
    znq $p |- ( ( A e. ZZ /\ B e. NN ) -> ( A / B ) e. QQ ) $=
      ( vx vy cz wcel cn wa cdiv co cv wceq wrex cq rcla4eopr mp3an3 elq sylibr
      eqid ) AEFZBGFZHABIJZCKDKIJLDGMCEMZUBNFTUAUBUBLUCUBSCDEGABUBIOPCDUBQR $.
      $( [12-Jan-2002] $)
  $}

  ${
    $d x y A $.
    $( A rational number is a real number. $)
    qre $p |- ( A e. QQ -> A e. RR ) $=
      ( vx vy cq wcel cv cdiv co wceq cn wrex cz cr elq cc0 wne zre nnre nnne0
      wa jca redivcl 3expb syl2an eleq1 syl5ibrcom rexlimivv sylbi ) ADEABFZCFZ
      GHZIZCJKBLKAMEZBCANULUMBCLJUILEZUJJEZTUMULUKMEZUNUIMEZUJMEZUJOPZTUPUOUIQU
      OURUSUJRUJSUAUQURUSUPUIUJUBUCUDAUKMUEUFUGUH $.
      $( [14-Nov-2002] $)

    $( An integer is a rational number. $)
    zq $p |- ( A e. ZZ -> A e. QQ ) $=
      ( vx vy cv wceq cz wrex cdiv co cn wcel cq c1 cr cc recn div1 3syl eqeq2d
      zre eqcom syl6rbbr 1nn opreq2 rcla4ev mpan syl6bi reximia risset 3imtr4i
      elq ) BDZAEZBFGAULCDZHIZEZCJGZBFGAFKALKUMUQBFULFKZUMAULMHIZEZUQURUTAULEUM
      URUSULAURULNKULOKUSULEULTULPULQRSULAUAUBMJKUTUQUCUPUTCMJUNMEUOUSAUNMULHUD
      SUEUFUGUHBAFUIBCAUKUJ $.
      $( [9-Jan-2002] $)
  $}

  $( The integers are a subset of the rationals. $)
  zssq $p |- ZZ C_ QQ $=
    ( vx cz cq cv zq ssriv ) ABCADEF $.
    $( [9-Jan-2002] $)

  $( The nonnegative integers are a subset of the rationals. $)
  nn0ssq $p |- NN0 C_ QQ $=
    ( cn0 cz cq nn0ssz zssq sstri ) ABCDEF $.
    $( [31-Jul-2004] $)

  $( The natural numbers are a subset of the rationals. $)
  nnssq $p |- NN C_ QQ $=
    ( cn cz cq nnssz zssq sstri ) ABCDEF $.
    $( [31-Jul-2004] $)

  $( The rationals are a subset of the reals. $)
  qssre $p |- QQ C_ RR $=
    ( vx cq cr cv qre ssriv ) ABCADEF $.
    $( [9-Jan-2002] $)

  $( The rationals are a subset of the complex numbers. $)
  qsscn $p |- QQ C_ CC $=
    ( cq cr cc qssre ax-resscn sstri ) ABCDEF $.
    $( [2-Aug-2004] $)

  $( A natural number is rational. $)
  nnq $p |- ( A e. NN -> A e. QQ ) $=
    ( cn cq nnssq sseli ) BCADE $.
    $( [17-Nov-2004] $)

  $( A rational number is a complex number. $)
  qcn $p |- ( A e. QQ -> A e. CC ) $=
    ( cq cc qsscn sseli ) BCADE $.
    $( [2-Aug-2004] $)

  ${
    $d w x y z $.
    $( The set of rational numbers exists. $)
    qex $p |- QQ e. _V $=
      ( vy vz vw vx cq cv cz wcel cn wa cdiv wceq copab2 crn cvv wrex elq oprex
      co eqid elrnoprab bitr4i eqriv zex nnex oprabex2 rnex eqeltri ) EAFZGHBFZ
      IHJCFUIUJKSZLJABCMZNZODEUMDFZEHUNUKLBIPAGPUNUMHABUNQABCGIUKUNULUIUJKRULTZ
      UAUBUCULABCGIUKULUDUEUOUFUGUH $.
      $( [16-Jun-2013] $) $( [30-Jul-2004] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.
    $( Closure of addition of rationals. $)
    qaddcl $p |- ( ( A e. QQ /\ B e. QQ ) -> ( A + B ) e. QQ ) $=
      ( vx vy vz vw vu vv cq wcel cv cdiv co wceq cn wrex cz caddc wa cc elq wi
      cmul nnz zmulcl sylan2 ad2ant2rl ad2ant2lr zaddcl syl11anc adantr nnmulcl
      sylan ad2ant2l opreq12 cc0 wne zcn anim12i nncn jca divadddiv syl2an an4s
      nnne0 sylan9eqr w3a rcla4eopr syl111anc exp43 rexlimivv rexlimdvv syl2anb
      sylibr imp ) AIJACKZDKZLMZNZDOPCQPZBEKZFKZLMZNZFOPEQPZABRMZIJZBIJCDAUAEFB
      UAVTWEWGVTWDWGEFQOVSWAQJZWBOJZSZWDWGUBUBCDQOVPQJZVQOJZSZVSWJWDWGWMWJVSWDW
      GWMWJSZVSWDSZSVPWBUCMZVQWAUCMZRMZQJZVQWBUCMZOJZWFWRWTLMZNZWGWNWSWOWNWPQJZ
      WQQJZWSWKWIXDWLWHWIWKWBQJXDWBUDVPWBUEUFUGWLWHXEWKWIWLVQQJWHXEVQUDVQWAUEUM
      UHWPWQUIUJUKWNXAWOWLWIXAWKWHVQWBULUNUKWOWNWFVRWCRMZXBAVRBWCRUOWKWHWLWIXFX
      BNZWKWHSVPTJZWATJZSVQTJZVQUPUQZSZWBTJZWBUPUQZSZSXGWLWISWKXHWHXIVPURWAURUS
      WLXLWIXOWLXJXKVQUTVQVEVAWIXMXNWBUTWBVEVAUSVPWAVQWBVBVCVDVFWSXAXCVGWFGKHKL
      MNHOPGQPWGGHQOWRWTWFLVHGHWFUAVNVIVDVJVKVLVOVM $.
      $( [1-Aug-2004] $)

    $( Closure law for the negative of a rational. $)
    qnegcl $p |- ( A e. QQ -> -u A e. QQ ) $=
      ( vx vy vz vw cq wcel cv cdiv co wceq cn wrex cz cneg elq wa negeq cc cc0
      wne zcn nncn nnne0 divneg 3expb syl2an eqeq2d syl5ib znegcl anim1i jctild
      jca rcla4eopr 3expa sylibr syl6 rexlimivv sylbi ) AFGABHZCHZIJZKZCLMBNMAO
      ZFGZBCAPVCVEBCNLUTNGZVALGZQZVCUTOZNGZVGQZVDVIVAIJZKZQZVEVHVCVMVKVCVDVBOZK
      VHVMAVBRVHVOVLVDVFUTSGZVASGZVATUAZQVOVLKZVGUTUBVGVQVRVAUCVAUDUMVPVQVRVSUT
      VAUEUFUGUHUIVFVJVGUTUJUKULVNVDDHEHIJKELMDNMZVEVJVGVMVTDENLVIVAVDIUNUODEVD
      PUPUQURUS $.
      $( [2-Aug-2004] $)

    $( Closure of multiplication of rationals. $)
    qmulcl $p |- ( ( A e. QQ /\ B e. QQ ) -> ( A x. B ) e. QQ ) $=
      ( vx vy vz vw vv vu cq wcel cv cdiv co wceq cn wrex cz cmul wa cc nnmulcl
      elq wi zmulcl anim12i an4s adantr opreq12 cc0 wne zcn ad2ant2r nncn nnne0
      jca ad2ant2l divmuldiv syl11anc sylan9eqr rcla4eopr 3expa exp43 rexlimivv
      sylibr rexlimdvv imp syl2anb ) AIJACKZDKZLMZNZDOPCQPZBEKZFKZLMZNZFOPEQPZA
      BRMZIJZBIJCDAUBEFBUBVLVQVSVLVPVSEFQOVKVMQJZVNOJZSZVPVSUCUCCDQOVHQJZVIOJZS
      ZVKWBVPVSWEWBVKVPVSWEWBSZVKVPSZSVHVMRMZQJZVIVNRMZOJZSZVRWHWJLMZNZVSWFWLWG
      WCVTWDWAWLWCVTSWIWDWASWKVHVMUDVIVNUAUEUFUGWGWFVRVJVORMZWMAVJBVORUHWFVHTJZ
      VMTJZSZVITJZVIUIUJZSZVNTJZVNUIUJZSZSZWOWMNWCVTWRWDWAWCWPVTWQVHUKVMUKUEULW
      DWAXEWCVTWDXAWAXDWDWSWTVIUMVIUNUOWAXBXCVNUMVNUNUOUEUPVHVMVIVNUQURUSWLWNSV
      RGKHKLMNHOPGQPZVSWIWKWNXFGHQOWHWJVRLUTVAGHVRUBVDURUFVBVCVEVFVG $.
      $( [1-Aug-2004] $)
  $}

  $( Closure of subtraction of rationals. $)
  qsubcl $p |- ( ( A e. QQ /\ B e. QQ ) -> ( A - B ) e. QQ ) $=
    ( cq wcel wa cneg caddc co cmin wceq qcn negsub syl2an qnegcl qaddcl sylan2
    cc eqeltrrd ) ACDZBCDZEABFZGHZABIHZCSAQDBQDUBUCJTAKBKABLMTSUACDUBCDBNAUAOPR
    $.
    $( [2-Aug-2006] $) $( [2-Aug-2004] $)

  ${
    $d x y z w A $.
    $( Closure of reciprocal of rationals. $)
    qreccl $p |- ( ( A e. QQ /\ A =/= 0 ) -> ( 1 / A ) e. QQ ) $=
      ( vx vy vz vw cq wcel cc0 wne c1 cdiv co cv wceq cn wrex cz wi wa adantlr
      elq cmul nnne0 ancli neeq1 cc zcn nncn anim12i divne0b 3expa sylan bicomd
      sylan9bbr nnz zmulcl sylan2 adantr msqznn jca opreq2 divid opreq1d simpll
      wb simpl simpr divdivdiv syl22anc an4s ancom2s anassrs sylan9eqr an32s ex
      eqtr3d sylbid anasss rcla4eopr sylibr syl8 rexlimivv sylbi imp ) AFGZAHIZ
      JAKLZFGZWEABMZCMZKLZNZCOPBQPWFWHRZBCAUAWLWMBCQOWIQGZWJOGZSZWLWFWIWJUBLZQG
      ZWIWIUBLZOGZSZWGWQWSKLZNZSZWHWOWNWOWJHIZSWLWFXDRZRZWOXEWJUCUDWNWOXEXGWPXE
      SZWLXFXHWLSZWFWIHIZXDWLWFWKHIZXHXJAWKHUEXHXJXKWPWIUFGZWJUFGZSZXEXJXKVEZWN
      XLWOXMWIUGWJUHUIZXLXMXEXOWIWJUJUKULUMUNXIXJXDXIXJSXAXCXHXJXAWLWPXJXAXEWPX
      JSWRWTWPWRXJWOWNWJQGWRWJUOWIWJUPUQURWNXJWTWOWIUSTUTTTXHXJWLXCWLXHXJSWGJWK
      KLZXBAWKJKVAWPXEXJXQXBNZWPXJXEXRWPXNXJXESXRXPXLXJXMXEXRXLXJSZXMXESZSZWIWI
      KLZWKKLZXQXBYAYBJWKKXSYBJNXTWIVBURVCYAXLXSXSXTYCXBNXLXJXTVDXSXTVFZYDXSXTV
      GWIWIWIWJVHVIVPVJULVKVLVMVNUTVOVQVOVRUQXDWGDMEMKLNEOPDQPZWHWRWTXCYEDEQOWQ
      WSWGKVSUKDEWGUAVTWAWBWCWD $.
      $( [3-Aug-2004] $)
  $}

  ${
    $d x y z w A $.
    $( Closure of division of rationals. $)
    qdivcl $p |- ( ( A e. QQ /\ B e. QQ /\ B =/= 0 ) -> ( A / B ) e. QQ ) $=
      ( cq wcel cc0 wne w3a cdiv co c1 cmul cc wceq qcn id divrec syl3an qreccl
      wa qmulcl sylan2 3impb eqeltrd ) ACDZBCDZBEFZGABHIZAJBHIZKIZCUDALDUEBLDUF
      UFUGUIMANBNUFOABPQUDUEUFUICDZUEUFSUDUHCDUJBRAUHTUAUBUC $.
      $( [7-Nov-2008] $) $( [3-Aug-2004] $)
  $}

  $( Reverse closure law for addition of rationals. $)
  qrevaddcl $p |- ( B e. QQ ->
                   ( ( A e. CC /\ ( A + B ) e. QQ ) <-> A e. QQ ) ) $=
    ( cq wcel cc caddc co wa cmin qcn pncan sylan2 ancoms adantr qsubcl adantlr
    wceq eqeltrrd ex wi qaddcl expcom impbid pm5.32da pm4.71ri syl6bbr ) BCDZAE
    DZABFGZCDZHUHACDZHUKUGUHUJUKUGUHHZUJUKULUJUKULUJHUIBIGZACULUMAQZUJUHUGUNUGU
    HBEDUNBJABKLMNUGUJUMCDZUHUJUGUOUIBOMPRSUGUKUJTUHUKUGUJABUAUBNUCUDUKUHAJUEUF
    $.
    $( [2-Aug-2004] $)

  $( The reciprocal of a natural number is rational. $)
  nnrecq $p |- ( A e. NN -> ( 1 / A ) e. QQ ) $=
    ( c1 cz wcel cn cdiv co cq 1z znq mpan ) BCDAEDBAFGHDIBAJK $.
    $( [17-Nov-2004] $)

  $( The sum of an irrational number and a rational number is irrational. $)
  irradd $p |- ( ( A e. ( RR \ QQ ) /\ B e. QQ )
                -> ( A + B ) e. ( RR \ QQ ) ) $=
    ( cr wcel cq wn wa caddc cdif qre readdcl sylan2 adantlr cmin qsubcl expcom
    co wi cc eldif adantl wceq qcn pncan syl2an eleq1d sylibd con3d com23 imp31
    recn ex jca anbi1i 3imtr4i ) ACDZAEDZFZGZBEDZGZABHQZCDZVBEDZFZGACEIZDZUTGVB
    VFDVAVCVEUPUTVCURUTUPBCDVCBJABKLMUPURUTVEUPUTURVEUPUTURVERUPUTGZVDUQVHVDVBB
    NQZEDZUQUTVDVJRUPVDUTVJVBBOPUAVHVIAEUPASDBSDVIAUBUTAUKBUCABUDUEUFUGUHULUIUJ
    UMVGUSUTACETUNVBCETUO $.
    $( [7-Nov-2008] $)

  $( The product of an irrational with a nonzero rational is irrational. $)
  irrmul $p |- ( ( A e. ( RR \ QQ ) /\ B e. QQ /\ B =/= 0 )
                -> ( A x. B ) e. ( RR \ QQ ) ) $=
    ( cr wcel cq wn wa cc0 wne w3a cmul co qre remulcl sylan2 wi 3expb cc eldif
    cdif ad2ant2r cdiv qdivcl expcom adantl wceq divcan4 syl3an1 syl3an2 eleq1d
    qcn recn sylibd con3d ex com23 imp31 jca 3impb 3anbi1i 3imtr4i ) ACDZAEDZFZ
    GZBEDZBHIZJABKLZCDZVHEDZFZGZACETZDZVFVGJVHVMDVEVFVGVLVEVFVGGZGVIVKVBVFVIVDV
    GVFVBBCDVIBMABNOUAVBVDVOVKVBVOVDVKVBVOVDVKPVBVOGZVJVCVPVJVHBUBLZEDZVCVOVJVR
    PVBVJVOVRVJVFVGVRVHBUCQUDUEVPVQAEVBVFVGVQAUFZVFVBBRDZVGVSBUKVBARDVTVGVSAULA
    BUGUHUIQUJUMUNUOUPUQURUSVNVEVFVGACESUTVHCESVA $.
    $( [7-Nov-2008] $)

  ${
    $d F k n x y $.  $d T n y $.
    rpnnen1.1 $e |- T = { n e. ZZ | ( n / k ) < x } $.
    rpnnen1.2 $e |- F = ( x e. RR |->
        ( k e. NN |-> ( sup ( T , RR , < ) / k ) ) ) $.
    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem1 $p |- ( x e. RR -> ( F ` x ) e. ( QQ ^m NN ) ) $=
      ( vy cv cr wcel cn clt co cq cvv cz wa wbr wrex cfv csup cdiv cmpt mptexg
      cmap wceq nnex ax-mp fvmpt2 mpan2 wf wss crab ssrab2 eqsstri a1i wne wral
      c0 cle cmul ax-mulrcl ancoms sylan2 btwnz simpld syl cc0 wb adantl simpll
      nnre zre nngt0 jca ad2antlr ltdivmul syl111anc mpbird rabn0 sylibr neeq1i
      rexbidva rabeq2i wi syl11anc ltle sylan2b ralrimiva breq2 ralbidv rcla4ev
      sylbid impr suprzcl sseldd znq sylancom ex eqid fmptd qex elmap eqeltrd )
      AIZJKZXFEUAZCLBJMUBZCIZUCNZUDZOLUFNZXGXLPKZXHXLUGLPKXNUHCLXKPUEUIAJXLPEGU
      JUKXGLOXLULXLXMKXGCLXKOXLXGXJLKZXKOKZXGXOXIQKXPXGXORZBQXIBQUMZXQBDIZXJUCN
      XFMSZDQUNZQFXTDQUOUPUQZXQXRBUTURZXSHIZVASZDBUSZHJTZXIBKYBXQYAUTURZYCXQXTD
      QTZYHXQYIXSXJXFVBNZMSZDQTZXQYJJKZYLXOXGXJJKZYMXJVMZYNXGYMXJXFVCZVDVEZYMYL
      YJXSMSDQTDDYJVFVGVHXQXTYKDQXQXSQKZRZXSJKZXGYNVIXJMSZRZXTYKVJYRYTXQXSVNVKZ
      XGXOYRVLZXOUUBXGYRXOYNUUAYOXJVOVPVQXSXFXJVRVSZWDVTXTDQWAWBBYAUTFWCWBXQYMX
      SYJVASZDBUSZYGYQXQUUFDBXSBKXQYRXTRUUFXTDBQFWEXQYRXTUUFYSXTYKUUFUUEYSYTYMY
      KUUFWFUUCYSYNXGYMXOYNXGYRYOVQUUDYPWGXSYJWHWGWNWOWIWJYFUUGHYJJYDYJUGYEUUFD
      BYDYJXSVAWKWLWMWGHDBWPVSWQXIXJWRWSWTXLXAXBOLXLXCUHXDWBXE $.
      $( [12-May-2013] $)


    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem2 $p |- ( ( x e. RR /\ k e. NN ) -> sup ( T , RR , < ) e. ZZ ) $=
      ( vy cv cr wcel wa cz clt co wbr c0 cle wrex syl11anc cn csup cdiv ssrab2
      wss crab eqsstri a1i wne wral cmul nnre ax-mulrcl ancoms sylan2 btwnz syl
      simpld cc0 zre adantl simpll nngt0 jca ad2antlr ltdivmul syl111anc mpbird
      wb rexbidva sylibr neeq1i rabeq2i ltle sylbid impr sylan2b ralrimiva wceq
      rabn0 wi breq2 ralbidv rcla4ev suprzcl sseldd ) AIZJKZCIZUAKZLZBMBJNUBZBM
      UEZWKBDIZWIUCOWGNPZDMUFZMFWODMUDUGUHZWKWMBQUIZWNHIZRPZDBUJZHJSZWLBKWQWKWP
      QUIZWRWKWODMSZXCWKXDWNWIWGUKOZNPZDMSZWKXEJKZXGWJWHWIJKZXHWIULZXIWHXHWIWGU
      MZUNUOZXHXGXEWNNPDMSDDXEUPURUQWKWOXFDMWKWNMKZLZWNJKZWHXIUSWINPZLZWOXFVIXM
      XOWKWNUTVAZWHWJXMVBZWJXQWHXMWJXIXPXJWIVCVDVEWNWGWIVFVGZVJVHWODMVTVKBWPQFV
      LVKWKXHWNXERPZDBUJZXBXLWKYADBWNBKWKXMWOLYAWODBMFVMWKXMWOYAXNWOXFYAXTXNXOX
      HXFYAWAXRXNXIWHXHWJXIWHXMXJVEXSXKTWNXEVNTVOVPVQVRXAYBHXEJWSXEVSWTYADBWSXE
      WNRWBWCWDTHDBWEVGWF $.
      $( [12-May-2013] $)

    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem3 $p |- ( x e. RR -> A. n e. ran ( F ` x ) n <_ x ) $=
      ( cr wcel cle wbr wceq cn wrex cq co wa clt cvv cz cv cfv crn wf wfn cmap
      wb rpnnen1lem1 qex nnex elmap sylib ffn fvelrnb 3syl csup cdiv cmpt ax-mp
      mptexg fvmpt2 mpan2 fveq1d eqid sylan9eq eqeq1d cmul wss c0 wne wral crab
      oprex ssrab2 eqsstri zssre sstri a1i ax-mulrcl ancoms sylan2 btwnz simpld
      nnre syl cc0 zre adantl simpll nngt0 ad2antlr ltdivmul syl111anc rexbidva
      mpbird rabn0 sylibr neeq1i rabeq2i wi syl11anc ltle sylbid impr ralrimiva
      jca sylan2b suprleub syl22anc rpnnen1lem2 simpl breq1 syl5ibcom rexlimdva
      ledivmul ralrimiv ) AUAZHIZDUAZXQJKZDXQEUBZUCZXRXSYBIZCUAZYAUBZXSLZCMNZXT
      XRMOYAUDZYAMUEYCYGUGXRYAOMUFPIYHABCDEFGUHOMYAUIUJUKULMOYAUMCMXSYAUNUOXRYF
      XTCMXRYDMIZQZYFBHRUPZYDUQPZXSLZXTYJYEYLXSXRYIYEYDCMYLURZUBZYLXRYDYAYNXRYN
      SIZYAYNLMSIYPUJCMYLSUTUSAHYNSEGVAVBVCYIYLSIYOYLLYKYDUQVMCMYLSYNYNVDVAVBVE
      VFYJYLXQJKZYMXTYJYQYKYDXQVGPZJKZYJBHVHZBVIVJZYRHIZXSYRJKZDBVKYSYTYJBTHBXS
      YDUQPXQRKZDTVLZTFUUDDTVNVOVPVQVRYJUUEVIVJZUUAYJUUDDTNZUUFYJUUGXSYRRKZDTNZ
      YJUUBUUIYIXRYDHIZUUBYDWDZUUJXRUUBYDXQVSZVTWAZUUBUUIYRXSRKDTNDDYRWBWCWEYJU
      UDUUHDTYJXSTIZQZXSHIZXRUUJWFYDRKZQZUUDUUHUGUUNUUPYJXSWGWHZXRYIUUNWIZYIUUR
      XRUUNYIUUJUUQUUKYDWJXFZWKXSXQYDWLWMZWNWOUUDDTWPWQBUUEVIFWRWQUUMYJUUCDBXSB
      IYJUUNUUDQUUCUUDDBTFWSYJUUNUUDUUCUUOUUDUUHUUCUVBUUOUUPUUBUUHUUCWTUUSUUOUU
      JXRUUBYIUUJXRUUNUUKWKUUTUULXAXSYRXBXAXCXDXGXEDBYRXHXIYJYKHIZXRUURYQYSUGYJ
      YKTIUVCABCDEFGXJYKWGWEXRYIXKYIUURXRUVAWHYKXQYDXOWMWOYLXSXQJXLXMXCXNXCXP
      $.
      $( [12-May-2013] $)

    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem4 $p |- ( x e. RR -> sup ( ran ( F ` x ) , RR , < ) e. RR ) $=
      ( vy cv cr wcel c0 wne cle wbr wral cn cq sylib syl cfv crn wss wrex csup
      clt wf cmap co rpnnen1lem1 qex nnex elmap frn qssre syl6ss cdm ne0i ax-mp
      c1 1nn fdm neeq1d mpbiri dm0rn0 necon3bii rpnnen1lem3 weq ralbidv rcla4ev
      breq2 mpdan suprcl syl111anc ) AIZJKZVOEUAZUBZJUCZVRLMZDIZHIZNOZDVRPZHJUD
      ZVRJUFUEJKVPQRVQUGZVSVPVQRQUHUIKWFABCDEFGUJRQVQUKULUMSZWFVRRJQRVQUNUOUPTV
      PWFVTWGWFVQUQZLMZVTWFWIQLMZUTQKWJVAQUTURUSWFWHQLQRVQVBVCVDWHLVRLVQVEVFSTV
      PWAVONOZDVRPZWEABCDEFGVGWDWLHVOJHAVHWCWKDVRWBVOWANVKVIVJVLHDVRVMVN $.
      $( [12-May-2013] $)


    $( Lemma for ~ rpnnen1 . $)
    rpnnen1lem5 $p |- ( x e. RR -> sup ( ran ( F ` x ) , RR , < ) = x ) $=
      ( vy cr wcel clt cle wbr wa c0 cn co syl syl11anc cz cv cfv crn csup wceq
      wn wb rpnnen1lem4 eqlelt mpancom wss wne wral cq wf cmap rpnnen1lem1 nnex
      qex elmap sylib frn qssre syl6ss cdm c1 1nn ne0i fdm neeq1d mpbiri dm0rn0
      ax-mp necon3bii id rpnnen1lem3 suprleub syl22anc cmin cdiv wrex cc0 mpdan
      resubcl adantr posdif biimpa gt0ne0 rereccl arch ex w3a caddc rpnnen1lem2
      zre 3adant3 ltp1 wi jca nnre ltrec1 syl2an ad2antrr nnrecre adantl simpll
      nngt0 ltaddsub2 syl111anc weq breq2 ralbidv rcla4ev 3jca wfn ffn fnfvelrn
      sylan suprub sseldd leadd1 mpbid ax-addrcl simpl lelttr exp3a mpd adantlr
      sylbird sylbid peano2zdi breq1d a1i ad2antlr sylibr cvv fvmpt2 mpan2 syld
      cc opreq1 elrab2 biimpri crab ssrab2 eqsstri zssre sstri ax-mulrcl ancoms
      cmul sylan2 btwnz simpld ltdivmul rexbidva mpbird rabn0 rabeq2i ltle impr
      neeq1i ralrimiva syldan recnd ax-1cn nncn nnne0 divdir cmpt mptexg fveq1d
      sylan2b oprex eqid sylan9eq opreq1d eqtr4d lenlt 3imtr3d exp31 com4l 3imp
      com14 mt2d 3exp rexlimdv pm2.01d mpbir2and ) AUAZIJZUWJEUBZUCZIKUDZUWJUEZ
      UWNUWJLMZUWNUWJKMZUFZUWNIJZUWKUWOUWPUWRNUGABCDEFGUHZUWNUWJUIUJUWKUWMIUKZU
      WMOULZUWKDUAZUWJLMZDUWMUMZUWPUWKPUNUWLUOZUXAUWKUWLUNPUPQJUXFABCDEFGUQUNPU
      WLUSURUTVAZUXFUWMUNIPUNUWLVBVCVDRZUWKUXFUXBUXGUXFUWLVEZOULZUXBUXFUXJPOULZ
      VFPJUXKVGPVFVHVMUXFUXIPOPUNUWLVIVJVKUXIOUWMOUWLVLVNVARZUWKVOABCDEFGVPZDUW
      MUWJVQVRUWKUWQUWKUWQVFUWJUWNVSQZVTQZCUAZKMZCPWAZUWRUWKUWQUXRUWKUWQNZUXOIJ
      ZUXRUXSUXNIJZUXNWBULZUXTUWKUYAUWQUWKUWSUYAUWTUWJUWNWDWCWEZUXSUYAWBUXNKMZU
      YBUYCUWKUWQUYDUWSUWKUWQUYDUGUWTUWNUWJWFUJWGZUXNWHSUXNWISUXOCWJRWKUWKUXQUW
      RCPUWKUXPPJZUXQUWRUWKUYFUXQWLZUWQBIKUDZUYHVFWMQZKMZUYGUYHIJZUYJUWKUYFUYKU
      XQUWKUYFNZUYHTJUYKABCDEFGWNZUYHWORZWPUYHWQRUWKUYFUXQUWQUYJUFZWRUWQUYFUXQU
      WKUYOUWKUWQUYFUXQUYOUWKUWQUYFUXQUYOWRUXSUYFNZUXQUXPUWLUBZVFUXPVTQZWMQZUWJ
      KMZUYOUYPUXQUYRUXNKMZUYTUXSUYAUYDNUXPIJZWBUXPKMZNZUXQVUAUGUYFUXSUYAUYDUYC
      UYEWSUYFVUBVUCUXPWTZUXPXGWSZUXNUXPXAXBUYPVUAUWNUYRWMQZUWJKMZUYTUYPUWSUYRI
      JZUWKVUHVUAUGUWKUWSUWQUYFUWTXCUYFVUIUXSUXPXDZXEUWKUWQUYFXFUWNUYRUWJXHXIUW
      KUYFVUHUYTWRZUWQUYLUYSVUGLMZVUKUYLUYQUWNLMZVULUYLUXAUXBUXCHUAZLMZDUWMUMZH
      IWAZWLZUYQUWMJZVUMUWKVURUYFUWKUXAUXBVUQUXHUXLUWKUXEVUQUXMVUPUXEHUWJIHAXJV
      UOUXDDUWMVUNUWJUXCLXKXLXMWCXNWEUWKUWLPXOZUYFVUSUWKUXFVUTUXGPUNUWLXPRPUXPU
      WLXQXRZHDUWMUYQXSSUYLUYQIJZUWSVUIVUMVULUGUYLUWMIUYQUWKUXAUYFUXHWEVVAXTZUW
      KUWSUYFUWTWEUYFVUIUWKVUJXEZUYQUWNUYRYAXIYBUYLUYSIJZVUGIJZUWKVULVUKWRUYLVV
      BVUIVVEVVCVVDUYQUYRYCSUWKUWSVUIVVFUYFUWTVUJUWNUYRYCXBUWKUYFYDVVEVVFUWKWLV
      ULVUHUYTUYSVUGUWJYEYFXIYGYHYIYJUWKUYFUYTUYOWRUWQUYLUYIUXPVTQZUWJKMZUYIUYH
      LMZUYTUYOUYLVVHVVIUYLVVHUYIBJZVVIUYLUYITJZVVHVVJUYLUYHUYMYKZVVJVVKVVHNUXC
      UXPVTQZUWJKMZVVHDUYITBUXCUYIUEVVMVVGUWJKUXCUYIUXPVTUUAYLFUUBUUCXRUYLBIUKZ
      BOULZVUODBUMZHIWAZWLVVJVVIUYLVVOVVPVVRVVOUYLBTIBVVNDTUUDZTFVVNDTUUEUUFUUG
      UUHYMUYLVVSOULZVVPUYLVVNDTWAZVVTUYLVWAUXCUXPUWJUUKQZKMZDTWAZUYLVWBIJZVWDU
      YFUWKVUBVWEVUEVUBUWKVWEUXPUWJUUIZUUJUULZVWEVWDVWBUXCKMDTWADDVWBUUMUUNRUYL
      VVNVWCDTUYLUXCTJZNZUXCIJZUWKVUDVVNVWCUGVWHVWJUYLUXCWOXEZUWKUYFVWHXFZUYFVU
      DUWKVWHVUFYNUXCUWJUXPUUOXIZUUPUUQVVNDTUURYOBVVSOFUVBYOUYLVWEUXCVWBLMZDBUM
      ZVVRVWGUYLVWNDBUXCBJUYLVWHVVNNVWNVVNDBTFUUSUYLVWHVVNVWNVWIVVNVWCVWNVWMVWI
      VWJVWEVWCVWNWRVWKVWIVUBUWKVWEUYFVUBUWKVWHVUEYNVWLVWFSUXCVWBUUTSYJUVAUVMUV
      CVVQVWOHVWBIVUNVWBUEVUOVWNDBVUNVWBUXCLXKXLXMSXNHDBUYIXSXRUVDWKUYLVVGUYSUW
      JKUYLVVGUYHUXPVTQZUYRWMQZUYSUYLUYHYTJVFYTJZUXPYTJZUXPWBULZNZVVGVWQUEUYLUY
      HUYNUVEVWRUYLUVFYMUYFVXAUWKUYFVWSVWTUXPUVGUXPUVHWSXEUYHVFUXPUVIXIUYLUYQVW
      PUYRWMUWKUYFUYQUXPCPVWPUVJZUBZVWPUWKUXPUWLVXBUWKVXBYPJZUWLVXBUEPYPJVXDURC
      PVWPYPUVKVMAIVXBYPEGYQYRUVLUYFVWPYPJVXCVWPUEUYHUXPVTUVNCPVWPYPVXBVXBUVOYQ
      YRUVPUVQUVRYLUYLUYIIJZUYKVVIUYOUGUYLVVKVXEVVLUYIWORUYNUYIUYHUVSSUVTYHYSUW
      AUWBUWDUWCUWEUWFUWGYSUWHUWI $.
      $( [12-May-2013] $)

    $( One half of ~ rpnnen , where we show an injection from the real numbers
       to sequences of rational numbers.  Specifically, we map a real number
       ` x ` to the sequence ` ( F `` x ) : NN --> QQ ` such that
       ` ( ( F `` x ) `` k ) ` is the largest rational number with denominator
       ` k ` that is strictly less than ` x ` .  In this manner, we get a
       monotonically increasing sequence that converges to ` x ` , and since
       each sequence converges to a unique real number, this mapping from reals
       to sequences of rational numbers is injective.  (Contributed by Mario
       Carneiro, 13-May-2013.) $)
    rpnnen1 $p |- RR ~<_ ( QQ ^m NN ) $=
      ( vy cq cn cmap cvv wcel cr cv cfv wceq crn clt csup cdom wbr rpnnen1lem1
      co oprex wa weq rneq supeq1d rpnnen1lem5 fveq2 rneqd id eqeq12d eqeqan12d
      vtoclga syl5ib impbid1 dom3 ax-mp ) IJKUDZLMNVAUAUBIJKUEAHNVAAOZEPZHOZEPZ
      LABCDEFGUCVBNMZVDNMZUFZVCVEQZAHUGZVIVCRZNSTZVERZNSTZQVHVJVINVKVMSVCVEUHUI
      VFVGVLVBVNVDABCDEFGUJZVLVBQVNVDQAVDNVJVLVNVBVDVJNVKVMSVJVCVEVBVDEUKZULUIV
      JUMUNVOUPUOUQVPURUSUT $.
      $( [16-Jun-2013] $) $( [12-May-2013] $)
  $}

  ${
    $d j k m n x y $.
    $( The set of real numbers exists. $)
    reex $p |- RR e. _V $=
      ( vx vm vk vn vy vj cr cn co cdom wbr wcel cv cdiv clt crab csup cmpt weq
      cz cmap cvv opreq1 breq1d cbvrabv opreq2 rabbidv supeq1d opreq12d cbvmptv
      cq id breq2 opreq1d adantr mpteq2dva syl5eq rpnnen1 reldom brrelexi ax-mp
      wceq ) GUKHUAIZJKGUBLABMZCMZNIZAMZOKZBTPZCDEGFHVDFMZNIZEMZOKZBTPZGOQZVJNI
      ZRZRVHDMZVENIZVGOKBDTBDSVFVSVGOVDVRVENUCUDUEEAGVQCHVIGOQZVENIZRZEASZVQCHV
      FVLOKZBTPZGOQZVENIZRWBFCHVPWGFCSZVOWFVJVENWHGVNWEOWHVMWDBTWHVKVFVLOVJVEVD
      NUFUDUGUHWHULUIUJWCCHWGWAWCWGWAVBVEHLWCWFVTVENWCGWEVIOWCWDVHBTVLVGVFOUMUG
      UHUNUOUPUQUJURGVCJUSUTVA $.
      $( [30-Jul-2004] $)
  $}

  ${
    $d F u v w z $.  $d x y u v w z $.
    cnref1o.1 $e |- F = ( x e. RR , y e. RR |-> ( x + ( _i x. y ) ) ) $.
    $( There is a natural one-to-one mapping from ` ( RR X. RR ) ` to ` CC ` ,
       where we map ` <. x , y >. ` to ` ( x + ( _i x. y ) ) ` .  In our
       construction of the complex numbers, this is in fact our _definition_ of
       ` CC ` (see ~ df-c ), but in the axiomatic treatment we can only show
       that there is the expected mapping between these two sets.  (Contributed
       by Mario Carneiro, 16-Jun-2013.) $)
    cnref1o $p |- F : ( RR X. RR ) -1-1-onto-> CC $=
      ( vz vw vu vv cr cc cv cfv wceq wcel ci cmul co caddc c2nd wa cxp wf1 wfo
      wf1o df-f1o wf wi wral dff13 wfn ffnfv oprex fnoprab2a cop 1st2nd2 fveq2d
      c1st df-opr syl6eqr xp1st xp2nd opreq1 opreq2 ovmpt2 syl11anc eqtrd recnd
      opreq2d ax-icn ax-mulcl sylancr ax-addcl eqeltrd rgen mpbir2an wb jca cru
      syl2an opreq12d eqeq12d vtoclga eqeqan12d fvex opth syl6bb 3bitr4d biimpd
      fveq2 rgen2a wrex dffo3 ax-cnre eqeq2d 2rexbiia sylibr rexxp ) IIUAZJCUDW
      RJCUBZWRJCUCZWRJCUEWSWRJCUFZEKZCLZFKZCLZMZXBXDMZUGZFWRUHEWRUHEFWRJCUIXACW
      RUJXCJNZEWRUHEWRJCUKABIIAKZOBKZPQZRQZCDXJXLRULUMXIEWRXBWRNZXCXBUQLZOXBSLZ
      PQZRQZJXNXCXOXPCQZXRXNXCXOXPUNZCLXSXNXBXTCXBIIUOZUPXOXPCURUSXNXOINZXPINZX
      SXRMXBIIUTZXBIIVAZABXOXPIIXMXRCXOXLRQXJXOXLRVBXKXPMXLXQXORXKXPOPVCVHDXOXQ
      RULVDVEVFZXNXOJNXQJNZXRJNXNXOYDVGXNOJNXPJNYGVIXNXPYEVGOXPVJVKXOXQVLVEVMVN
      VOZXHEFWRXNXDWRNZTZXFXGYJXRXDUQLZOXDSLZPQZRQZMZXOYKMXPYLMTZXFXGXNYBYCTYKI
      NZYLINZTYOYPVPYIXNYBYCYDYEVQYIYQYRXDIIUTXDIIVAVQXOXPYKYLVRVSXNYIXCXRXEYNY
      FXCXRMXEYNMEXDWRXGXCXEXRYNXBXDCWIXGXOYKXQYMRXBXDUQWIXGXPYLOPXBXDSWIVHVTWA
      YFWBWCYJXGXTYKYLUNZMYPXNYIXBXTXDYSYAXDIIUOWCXOXPYKYLXBUQWDXBSWDXDSWDWEWFW
      GWHWJVOWTXAXDXCMZEWRWKZFJUHEFWRJCWLYHUUAFJXDJNZXDGKZHKZCQZMZHIWKGIWKZUUAU
      UBXDUUCOUUDPQZRQZMZHIWKGIWKUUGGHXDWMUUFUUJGHIIUUCINUUDINTUUEUUIXDABUUCUUD
      IIXMUUICUUCXLRQXJUUCXLRVBXKUUDMXLUUHUUCRXKUUDOPVCVHDUUCUUHRULVDWNWOWPYTUU
      FEGHIIXBUUCUUDUNZMZXCUUEXDUULXCUUKCLUUEXBUUKCWIUUCUUDCURUSWNWQWPVNVOVO $.
      $( [17-Feb-2014] $) $( [16-Jun-2013] $)
  $}

  ${
    $d x y $.
    $( The set of complex numbers exists.  (Revised by Mario Carneiro,
       16-Jun-2013.) $)
    cnex $p |- CC e. _V $=
      ( vx vy cr cxp cvv wcel cc cv ci cmul caddc cmpt2 wfo reex xpex wf1o eqid
      co cnref1o f1ofo ax-mp fornex mp2 ) CCDZEFUDGABCCAHIBHJRKRLZMZGEFCCNNOUDG
      UEPUFABUEUEQSUDGUETUAUDGEUEUBUC $.
      $( [16-Jun-2013] $) $( [30-Jul-2004] $)
  $}

  $( The addition operation is a set. $)
  addex $p |- + e. _V $=
    ( cc cxp caddc wf cvv wcel ax-addopr cnex xpex fex mp2an ) AABZACDLEFCEFGAA
    HHILAECJK $.
    $( [19-Oct-2004] $)

  $( The multiplication operation is a set. $)
  mulex $p |- x. e. _V $=
    ( cc cxp cmul wf cvv wcel ax-mulopr cnex xpex fex mp2an ) AABZACDLEFCEFGAAH
    HILAECJK $.
    $( [19-Oct-2004] $)

