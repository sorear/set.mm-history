$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rational numbers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d m n p q x y $.
    $( Soundness justification theorem for ~ df-q . $)
    qjust $p |- { x | E. m e. ZZ E. n e. NN x = ( m / n ) }
           = { y | E. p e. ZZ E. q e. NN y = ( p / q ) } $=
      ( cv cdiv co wceq cn wrex cz cab opreq2 eqeq2d cbvrexv rexbii opreq1
      rexbidv bitri abbii eqeq1 2rexbidv cbvabv eqtri ) AGZCGZDGZHIZJZDKLZCMLZA
      NUGFGZEGZHIZJZEKLZFMLZANBGZUPJZEKLFMLZBNUMUSAUMUGUHUOHIZJZEKLZCMLUSULVECM
      UKVDDEKUIUOJUJVCUGUIUOUHHOPQRVEURCFMUHUNJZVDUQEKVFVCUPUGUHUNUOHSPTQUAUBUS
      VBABUGUTJUQVAFEMKUGUTUPUCUDUEUF $.
      $( [16-Feb-2013] $)
  $}

  ${
    $d m n x $.
    $( Define the set of rational numbers.  Definition of rationals in
       [Apostol] p. 22. $)
    df-q $a |- QQ = { x | E. m e. ZZ E. n e. NN x = ( m / n ) } $.
  $}

  ${
    $d x y z A $.
    $( Membership in the set of rationals. $)
    elq $p |- ( A e. QQ <-> E. x e. ZZ E. y e. NN A = ( x / y ) ) $=
      ( vz cq wcel cv cdiv co wceq cn wrex cz cab df-q eleq2i cvv oprex eleq1
      mpbiri r19.23aivw eqeq1 2rexbidv elab3 bitri ) CEFCDGZAGZBGZHIZJZBKLAMLZD
      NZFCUIJZBKLZAMLZEULCDABOPUKUODCUNCQFZAMUMUPBKUMUPUIQFUGUHHRCUIQSTUAUAUFCJ
      UJUMABMKUFCUIUBUCUDUE $.
      $( [8-Jan-2002] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The ratio of an integer and a natural number is a rational number. $)
    znq $p |- ( ( A e. ZZ /\ B e. NN ) -> ( A / B ) e. QQ ) $=
      ( vx vy cz wcel cn wa cdiv co cv wceq wrex cq eqid rcla4eopr mp3an3 elq
      sylibr ) AEFZBGFZHABIJZCKDKIJLDGMCEMZUBNFTUAUBUBLUCUBOCDEGABUBIPQCDUBRS
      $.
      $( [12-Jan-2002] $)
  $}

  ${
    $d x y A $.
    $( A rational number is a real number. $)
    qre $p |- ( A e. QQ -> A e. RR ) $=
      ( vx vy cq wcel cv cdiv co wceq cn wrex cz cr elq wa cc0 wne zre nnre
      nnne0 jca redivcl 3expb syl2an eleq1 syl5ibrcom r19.23aivv sylbi ) ADEABF
      ZCFZGHZIZCJKBLKAMEZBCANULUMBCLJUILEZUJJEZOUMULUKMEZUNUIMEZUJMEZUJPQZOUPUO
      UIRUOURUSUJSUJTUAUQURUSUPUIUJUBUCUDAUKMUEUFUGUH $.
      $( [14-Nov-2002] $)

    $( An integer is a rational number. $)
    zq $p |- ( A e. ZZ -> A e. QQ ) $=
      ( vx vy cv wceq cz wrex cdiv co cn wcel cq c1 cr cc zre recn div1 3syl
      eqeq2d eqcom syl6rbbr 1nn opreq2 rcla4ev mpan syl6bi reximia risset elq
      3imtr4i ) BDZAEZBFGAULCDZHIZEZCJGZBFGAFKALKUMUQBFULFKZUMAULMHIZEZUQURUTAU
      LEUMURUSULAURULNKULOKUSULEULPULQULRSTULAUAUBMJKUTUQUCUPUTCMJUNMEUOUSAUNMU
      LHUDTUEUFUGUHBAFUIBCAUJUK $.
      $( [9-Jan-2002] $)
  $}

  $( The integers are a subset of the rationals. $)
  zssq $p |- ZZ C_ QQ $=
    ( vx cz cq cv zq ssriv ) ABCADEF $.
    $( [9-Jan-2002] $)

  $( The nonnegative integers are a subset of the rationals. $)
  nn0ssq $p |- NN0 C_ QQ $=
    ( cn0 cz cq nn0ssz zssq sstri ) ABCDEF $.
    $( [31-Jul-2004] $)

  $( The natural numbers are a subset of the rationals. $)
  nnssq $p |- NN C_ QQ $=
    ( cn cz cq nnssz zssq sstri ) ABCDEF $.
    $( [31-Jul-2004] $)

  $( The rationals are a subset of the reals. $)
  qssre $p |- QQ C_ RR $=
    ( vx cq cr cv qre ssriv ) ABCADEF $.
    $( [9-Jan-2002] $)

  $( The rationals are a subset of the complex numbers. $)
  qsscn $p |- QQ C_ CC $=
    ( cq cr cc qssre axresscn sstri ) ABCDEF $.
    $( [2-Aug-2004] $)

  $( A natural number is rational. $)
  nnq $p |- ( A e. NN -> A e. QQ ) $=
    ( cn cq nnssq sseli ) BCADE $.
    $( [17-Nov-2004] $)

  $( A rational number is a complex number. $)
  qcn $p |- ( A e. QQ -> A e. CC ) $=
    ( cq cc qsscn sseli ) BCADE $.
    $( [2-Aug-2004] $)

  $( The set of rational numbers exists. $)
  qex $p |- QQ e. _V $=
    ( cq cr reex qssre ssexi ) ABCDE $.
    $( [30-Jul-2004] $)

  ${
    $d x y z w v u A $.  $d x y z w v u B $.
    $( Closure of addition of rationals. $)
    qaddcl $p |- ( ( A e. QQ /\ B e. QQ ) -> ( A + B ) e. QQ ) $=
      ( vx vy vz vw vu vv cq wcel cv cdiv co wceq cn wrex cz caddc elq wa wi
      cmul nnz zmulcl sylan2 ad2ant2rl sylan ad2ant2lr zaddcl syl11anc adantr
      nnmulcl ad2ant2l opreq12 cc cc0 wne zcn anim12i nncn nnne0 jca divadddiv
      syl2an an4s sylan9eqr w3a rcla4eopr sylibr syl111anc exp43 r19.23aivv
      r19.23advv imp syl2anb ) AIJACKZDKZLMZNZDOPCQPZBEKZFKZLMZNZFOPEQPZABRMZIJ
      ZBIJCDASEFBSVTWEWGVTWDWGEFQOVSWAQJZWBOJZTZWDWGUAUACDQOVPQJZVQOJZTZVSWJWDW
      GWMWJVSWDWGWMWJTZVSWDTZTVPWBUBMZVQWAUBMZRMZQJZVQWBUBMZOJZWFWRWTLMZNZWGWNW
      SWOWNWPQJZWQQJZWSWKWIXDWLWHWIWKWBQJXDWBUCVPWBUDUEUFWLWHXEWKWIWLVQQJWHXEVQ
      UCVQWAUDUGUHWPWQUIUJUKWNXAWOWLWIXAWKWHVQWBULUMUKWOWNWFVRWCRMZXBAVRBWCRUNW
      KWHWLWIXFXBNZWKWHTVPUOJZWAUOJZTVQUOJZVQUPUQZTZWBUOJZWBUPUQZTZTXGWLWITWKXH
      WHXIVPURWAURUSWLXLWIXOWLXJXKVQUTVQVAVBWIXMXNWBUTWBVAVBUSVPWAVQWBVCVDVEVFW
      SXAXCVGWFGKHKLMNHOPGQPWGGHQOWRWTWFLVHGHWFSVIVJVEVKVLVMVNVO $.
      $( [1-Aug-2004] $)

    $( Closure law for the negative of a rational. $)
    qnegcl $p |- ( A e. QQ -> -u A e. QQ ) $=
      ( vx vy vz vw cq wcel cv cdiv co wceq cn wrex cz cneg elq wa negeq cc
      cc0 wne zcn nncn nnne0 jca divneg 3expb syl2an eqeq2d syl5ib znegcl
      anim1i jctild rcla4eopr 3expa sylibr syl6 r19.23aivv sylbi ) AFGABHZCHZIJ
      ZKZCLMBNMAOZFGZBCAPVCVEBCNLUTNGZVALGZQZVCUTOZNGZVGQZVDVIVAIJZKZQZVEVHVCVM
      VKVCVDVBOZKVHVMAVBRVHVOVLVDVFUTSGZVASGZVATUAZQVOVLKZVGUTUBVGVQVRVAUCVAUDU
      EVPVQVRVSUTVAUFUGUHUIUJVFVJVGUTUKULUMVNVDDHEHIJKELMDNMZVEVJVGVMVTDENLVIVA
      VDIUNUODEVDPUPUQURUS $.
      $( [2-Aug-2004] $)

    $( Closure of multiplication of rationals. $)
    qmulcl $p |- ( ( A e. QQ /\ B e. QQ ) -> ( A x. B ) e. QQ ) $=
      ( vx vy vz vw vv vu cq wcel cv cdiv co wceq cn wrex cz cmul elq wa wi
      zmulcl nnmulcl anim12i an4s adantr opreq12 cc cc0 wne zcn ad2ant2r nncn
      nnne0 jca ad2ant2l divmuldiv syl11anc sylan9eqr rcla4eopr 3expa sylibr
      exp43 r19.23aivv r19.23advv imp syl2anb ) AIJACKZDKZLMZNZDOPCQPZBEKZFKZLM
      ZNZFOPEQPZABRMZIJZBIJCDASEFBSVLVQVSVLVPVSEFQOVKVMQJZVNOJZTZVPVSUAUACDQOVH
      QJZVIOJZTZVKWBVPVSWEWBVKVPVSWEWBTZVKVPTZTVHVMRMZQJZVIVNRMZOJZTZVRWHWJLMZN
      ZVSWFWLWGWCVTWDWAWLWCVTTWIWDWATWKVHVMUBVIVNUCUDUEUFWGWFVRVJVORMZWMAVJBVOR
      UGWFVHUHJZVMUHJZTZVIUHJZVIUIUJZTZVNUHJZVNUIUJZTZTZWOWMNWCVTWRWDWAWCWPVTWQ
      VHUKVMUKUDULWDWAXEWCVTWDXAWAXDWDWSWTVIUMVIUNUOWAXBXCVNUMVNUNUOUDUPVHVMVIV
      NUQURUSWLWNTVRGKHKLMNHOPGQPZVSWIWKWNXFGHQOWHWJVRLUTVAGHVRSVBURUEVCVDVEVFV
      G $.
      $( [1-Aug-2004] $)
  $}

  $( Closure of subtraction of rationals. $)
  qsubcl $p |- ( ( A e. QQ /\ B e. QQ ) -> ( A - B ) e. QQ ) $=
    ( cq wcel wa cneg caddc co cmin cc wceq qcn negsub syl2an qnegcl qaddcl
    sylan2 eqeltrrd ) ACDZBCDZEABFZGHZABIHZCSAJDBJDUBUCKTALBLABMNTSUACDUBCDBOAU
    APQR $.
    $( [2-Aug-2006] $) $( [2-Aug-2004] $)

  ${
    $d x y z w A $.
    $( Closure of reciprocal of rationals. $)
    qreccl $p |- ( ( A e. QQ /\ A =/= 0 ) -> ( 1 / A ) e. QQ ) $=
      ( vx vy vz vw cq wcel cc0 wne c1 cdiv co cv wceq cn wrex cz wi elq wa
      cmul nnne0 ancli neeq1 cc wb zcn nncn anim12i divne0b 3expa sylan bicomd
      sylan9bbr nnz zmulcl sylan2 adantr msqznn adantlr jca opreq2 divid
      opreq1d simpll simpl simpr divdivdiv syl22anc eqtr3d an4s ancom2s
      anassrs sylan9eqr an32s ex sylbid anasss rcla4eopr sylibr syl8
      r19.23aivv sylbi imp ) AFGZAHIZJAKLZFGZWEABMZCMZKLZNZCOPBQPWFWHRZBCASWLWM
      BCQOWIQGZWJOGZTZWLWFWIWJUALZQGZWIWIUALZOGZTZWGWQWSKLZNZTZWHWOWNWOWJHIZTWL
      WFXDRZRZWOXEWJUBUCWNWOXEXGWPXETZWLXFXHWLTZWFWIHIZXDWLWFWKHIZXHXJAWKHUDXHX
      JXKWPWIUEGZWJUEGZTZXEXJXKUFZWNXLWOXMWIUGWJUHUIZXLXMXEXOWIWJUJUKULUMUNXIXJ
      XDXIXJTXAXCXHXJXAWLWPXJXAXEWPXJTWRWTWPWRXJWOWNWJQGWRWJUOWIWJUPUQURWNXJWTW
      OWIUSUTVAUTUTXHXJWLXCWLXHXJTWGJWKKLZXBAWKJKVBWPXEXJXQXBNZWPXJXEXRWPXNXJXE
      TXRXPXLXJXMXEXRXLXJTZXMXETZTZWIWIKLZWKKLZXQXBYAYBJWKKXSYBJNXTWIVCURVDYAXL
      XSXSXTYCXBNXLXJXTVEXSXTVFZYDXSXTVGWIWIWIWJVHVIVJVKULVLVMVNVOVAVPVQVPVRUQX
      DWGDMEMKLNEOPDQPZWHWRWTXCYEDEQOWQWSWGKVSUKDEWGSVTWAWBWCWD $.
      $( [3-Aug-2004] $)
  $}

  ${
    $d x y z w A $.
    $( Closure of division of rationals. $)
    qdivcl $p |- ( ( A e. QQ /\ B e. QQ /\ B =/= 0 ) -> ( A / B ) e. QQ ) $=
      ( cq wcel cc0 wne w3a cdiv co c1 cmul cc wceq qcn id divrec syl3an wa
      qreccl qmulcl sylan2 3impb eqeltrd ) ACDZBCDZBEFZGABHIZAJBHIZKIZCUDALDUEB
      LDUFUFUGUIMANBNUFOABPQUDUEUFUICDZUEUFRUDUHCDUJBSAUHTUAUBUC $.
      $( [7-Nov-2008] $) $( [3-Aug-2004] $)
  $}

  $( Reverse closure law for addition of rationals. $)
  qrevaddcl $p |- ( B e. QQ ->
                   ( ( A e. CC /\ ( A + B ) e. QQ ) <-> A e. QQ ) ) $=
    ( cq wcel cc caddc co wa cmin wceq qcn pncan sylan2 ancoms adantr qsubcl
    adantlr eqeltrrd ex wi qaddcl expcom impbid pm5.32da pm4.71ri syl6bbr ) BCD
    ZAEDZABFGZCDZHUHACDZHUKUGUHUJUKUGUHHZUJUKULUJUKULUJHUIBIGZACULUMAJZUJUHUGUN
    UGUHBEDUNBKABLMNOUGUJUMCDZUHUJUGUOUIBPNQRSUGUKUJTUHUKUGUJABUAUBOUCUDUKUHAKU
    EUF $.
    $( [2-Aug-2004] $)

  $( The reciprocal of a natural number is rational. $)
  nnrecq $p |- ( A e. NN -> ( 1 / A ) e. QQ ) $=
    ( c1 cz wcel cn cdiv co cq 1z znq mpan ) BCDAEDBAFGHDIBAJK $.
    $( [17-Nov-2004] $)

  $( The sum of an irrational number and a rational number is irrational. $)
  irradd $p |- ( ( A e. ( RR \ QQ ) /\ B e. QQ )
                -> ( A + B ) e. ( RR \ QQ ) ) $=
    ( cr wcel cq wn wa caddc co cdif qre readdcl sylan2 adantlr wi cmin qsubcl
    expcom adantl cc wceq recn qcn pncan syl2an eleq1d sylibd con3d ex com23
    imp31 jca eldif anbi1i 3imtr4i ) ACDZAEDZFZGZBEDZGZABHIZCDZVBEDZFZGACEJZDZU
    TGVBVFDVAVCVEUPUTVCURUTUPBCDVCBKABLMNUPURUTVEUPUTURVEUPUTURVEOUPUTGZVDUQVHV
    DVBBPIZEDZUQUTVDVJOUPVDUTVJVBBQRSVHVIAEUPATDBTDVIAUAUTAUBBUCABUDUEUFUGUHUIU
    JUKULVGUSUTACEUMUNVBCEUMUO $.
    $( [7-Nov-2008] $)

  $( The product of an irrational with a nonzero rational is irrational. $)
  irrmul $p |- ( ( A e. ( RR \ QQ ) /\ B e. QQ /\ B =/= 0 )
                -> ( A x. B ) e. ( RR \ QQ ) ) $=
    ( cr wcel cq wn wa cc0 wne w3a cmul co cdif qre remulcl sylan2 ad2ant2r wi
    cdiv qdivcl 3expb expcom adantl wceq cc qcn recn divcan4 syl3an1 syl3an2
    eleq1d sylibd con3d ex com23 imp31 jca 3impb eldif 3anbi1i 3imtr4i ) ACDZAE
    DZFZGZBEDZBHIZJABKLZCDZVHEDZFZGZACEMZDZVFVGJVHVMDVEVFVGVLVEVFVGGZGVIVKVBVFV
    IVDVGVFVBBCDVIBNABOPQVBVDVOVKVBVOVDVKVBVOVDVKRVBVOGZVJVCVPVJVHBSLZEDZVCVOVJ
    VRRVBVJVOVRVJVFVGVRVHBTUAUBUCVPVQAEVBVFVGVQAUDZVFVBBUEDZVGVSBUFVBAUEDVTVGVS
    AUGABUHUIUJUAUKULUMUNUOUPUQURVNVEVFVGACEUSUTVHCEUSVA $.
    $( [7-Nov-2008] $)

  ${
    $d x y z A $.  $d x y z B $.
    $( The rational numbers are dense in ` RR ` : any two real numbers have a
       rational between them.  Exercise 6 of [Apostol] p. 28. $)
    qbtwnre $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
                  E. x e. QQ ( A < x /\ x < B ) ) $=
      ( vy vz cr wcel clt wbr cv wa cq wrex c1 cdiv co cmin cn cc0 posdif wi
      resubcl nnrecl sylan ex ancoms sylbid cmul cle caddc cz wreu nnre
      remulcl peano2rem syl adantrl zbtwnre reurex 3syl anass zre wb wceq cc
      nncn recn subdi syl3an 3com23 3expb breq2d nngt0 1re ltdivmulOLD mp3anl1
      exp31 com23 sylc syl5 imp ltsub13 mp3an3 syl2an anandis 3bitr4d adantr
      anbi1d adantrr jca ltletr 3expa ltmuldiv2OLD 3exp1 com4r mpcom adantrd
      imp31 sylibd mulcl ax1cn npcan sylancl w3a 3ad2ant2 syl3anl2 mpdan 3coml
      bitr4d biimpd adantlrl anim12d sylan2 znq adantlr jctild breq2 breq1
      anbi12d rcla4ev syl6 syl5bir exp4b com34 r19.23adv mpd expcom syld
      3impia ) BFGZCFGZBCHIZBAJZHIZYMCHIZKZALMZYJYKKZYLNDJZOPCBQPZHIZDRMZYQYRYL
      SYTHIZUUBBCTYKYJUUCUUBUAYKYJKZUUCUUBUUDYTFGZUUCUUBCBUBZYTDUCUDUEUFUGYRUUA
      YQDRYSRGZYRUUAYQUAZUUGYRKZYSCUHPZNQPZEJZUIIZUULUUKNUJPZHIZKZEUKMZUUHUUIUU
      KFGZUUPEUKULUUQUUGYKUURYJUUGYKKZUUJFGZUURUUGYSFGZYKUUTYSUMZYSCUNZUDUUJUOU
      PUQZEUUKURUUPEUKUSUTUUIUUPUUHEUKUUIUULUKGZUUAUUPYQUUIUVEUUAUUPYQUUAUUPKUU
      AUUMKZUUOKZUUIUVEKZYQUUAUUMUUOVAUVHUVGUULYSOPZLGZBUVIHIZUVICHIZKZKYQUVHUV
      GUVMUVJUVEUUIUULFGZUVGUVMUAUULVBUUIUVNKZUVFUVKUUOUVLUVOUVFYSBUHPZUULHIZUV
      KUVOUVFUVPUUKHIZUUMKZUVQUVOUUAUVRUUMUUIUUAUVRVCUVNUUINYSYTUHPZHIZNUUJUVPQ
      PZHIZUUAUVRUUIUVTUWBNHUUGYJYKUVTUWBVDZUUGYKYJUWDUUGYSVEGZYKCVEGZYJBVEGUWD
      YSVFZCVGZBVGYSCBVHVIVJVKVLUUGYRUUAUWAVCZYRUUEUUGUWIYKYJUUEUUFUFUUGUVASYSH
      IZUUEUWIUAUVBYSVMZUVAUUEUWJUWIUVAUUEUWJUWINFGZUVAUUEUWJUWIVNNYSYTVOVPVQVR
      VSVTWAUUGUVAYRUVRUWCVCZUVBUVAYJYKUWMUVAYJKUVPFGZUUTUWMUVAYKKYSBUNZUVCUWNU
      UTUWLUWMVNUVPUUJNWBWCWDWEUDWFWGWHUUIUWNUURKUVNUVSUVQUAZUUIUWNUURUUGYJUWNY
      KUUGUVAYJUWNUVBUWOUDWIUVDWJUWNUURUVNUWPUVPUUKUULWKWLUDUGUUGYRUVNUVQUVKVCZ
      UUGYJUVNUWQUAZYKUWJUUGYJUWRUAUWKUUGYJUVNUWJUWQUUGUVAYJUVNUWJUWQUAUAUAUVBU
      VAYJUVNUWJUWQYSBUULWMWNUPWOWPWQWRWSUUGYKUVNUUOUVLUAYJUUSUVNKZUUOUVLUWSUUO
      UULUUJHIZUVLUWSUUNUUJUULHUUSUUNUUJVDZUVNUUSUUJVEGZNVEGUXAUUGUWEUWFUXBYKUW
      GUWHYSCWTWDXAUUJNXBXCWGVLUUGYKUVNUVLUWTVCZUVNUUGYKUXCUVNUUGYKXDUWJUXCUUGU
      VNUWJYKUWKXEUUGUVNUVAYKUWJUXCUVBUULYSCVOXFXGXHWLXIXJXKXLXMUUGUVEUVJYRUVEU
      UGUVJUULYSXNUFXOXPYPUVMAUVILYMUVIVDYNUVKYOUVLYMUVIBHXQYMUVICHXRXSXTYAYBYC
      YDYEYFYGYEYHYI $.
      $( [15-Feb-2007] $) $( [18-Nov-2004] $)

    $( The rational numbers are dense in ` RR* ` : any two extended real
       numbers have a rational between them. $)
    qbtwnxr $p |- ( ( A e. RR* /\ B e. RR* /\ A < B )
                   -> E. x e. QQ ( A < x /\ x < B ) ) $=
      ( cxr wcel clt wbr cv wa cq wrex cr cpnf wceq cmnf w3o wi elxr qbtwnre
      3expia c1 caddc co peano2re ltp1 mpd3an23 ltpnf syl adantr qre rexr
      adantl pnfxr xrlttr mp3an3 syl11anc mpan2d anim2d reximdva mpd wb breq2
      anbi2d rexbidv mpbird a1d wn nltmnf notbid pm2.21d 3jaodan sylan2b
      pnfnlt breq1 cmin peano2rem id ltm1 syl111anc mnflt mnfxr mp3an1 syl2an
      mpand anim1d anbi1d 1re ax-mp mpbiri cz 1z zq anbi12d rcla4ev mpan
      breq12 mtbiri 3jaoian sylanb 3impia ) BDEZCDEZBCFGZBAHZFGZXDCFGZIZAJKZXAB
      LEZBMNZBONZPXBXCXHQZBRXIXBXLXJXKXBXICLEZCMNZCONZPZXLCRZXIXMXLXNXOXIXMXCXH
      ABCSTXIXNIZXHXCXRXHXEXDMFGZIZAJKZXIYAXNXIXEXDBUAUBUCZFGZIZAJKZYAXIYBLEZBY
      BFGYEBUDZBUEABYBSUFXIYDXTAJXIXDJEZIZYCXSXEYIYCYBMFGZXSXIYJYHXIYFYJYGYBUGU
      HUIYIXDDEZYBDEZYCYJIXSQZYHYKXIYHXDLEYKXDUJXDUKUHZULXIYLYHXIYFYLYGYBUKUHUI
      YKYLMDEYMUMXDYBMUNUOUPUQURUSUTUIXNXHYAVAXIXNXGXTAJXNXFXSXECMXDFVBVCVDULVE
      VFXIXOIZXCXHYOXCVGZBOFGZVGZXIYRXOXIXAYRBUKBVHUHUIXOYPYRVAXIXOXCYQCOBFVBVI
      ULVEVJVKVLXJXBIZXCXHYSYPMCFGZVGZXBUUAXJCVMULXJYPUUAVAXBXJXCYTBMCFVNVIUIVE
      VJXBXKXPXLXQXKXMXLXNXOXKXMIZXHXCUUBXHOXDFGZXFIZAJKZXMUUEXKXMCUAVOUCZXDFGZ
      XFIZAJKZUUEXMUUFLEZXMUUFCFGUUICVPZXMVQCVRAUUFCSVSXMUUHUUDAJXMYHIZUUGUUCXF
      UULOUUFFGZUUGUUCXMUUMYHXMUUJUUMUUKUUFVTUHUIXMUUFDEZYKUUMUUGIUUCQZYHXMUUJU
      UNUUKUUFUKUHYNODEZUUNYKUUOWAOUUFXDUNWBWCWDWEUSUTULXKXHUUEVAXMXKXGUUDAJXKX
      EUUCXFBOXDFVNWFVDUIVEVFXKXNIXHXCXKBUAFGZUACFGZXHXNXKUUQOUAFGZUALEZUUSWGUA
      VTWHBOUAFVNWIXNUURUAMFGZUUTUVAWGUAUGWHCMUAFVBWIUAJEZUUQUURIZXHUAWJEUVBWKU
      AWLWHXGUVCAUAJXDUANXEUUQXFUURXDUABFVBXDUACFVNWMWNWOWCVFXKXOIZXCXHUVDXCOOF
      GZUUPUVEVGWAOVHWHBOCOFWPWQVJVKVLWRWSWT $.
      $( [6-Feb-2007] $)
  $}

  ${
    $d x A $.
    $( If a nonnegative real is less than any positive rational, it is zero. $)
    qsqueeze $p |- ( ( A e. RR /\ 0 <_ A /\ A. x e. QQ ( 0 < x -> A < x ) ) ->
                   A = 0 ) $=
      ( cr wcel cc0 cle wbr cv clt wi cq wral w3a wceq wn wa wrex wb 0re ltnle
      mpan qbtwnre mp3an1 ex qre ltnsym con2d sylan2 anim2d reximdva syld
      sylbird rexanali syl6ib con4d imp 3adant2 letri3 mpan2 iba bicomd
      sylan9bb 3adant3 mpbird ) BCDZEBFGZEAHZIGZBVGIGZJAKLZMBENZBEFGZVEVJVLVFVE
      VJVLVEVLVJVEVLOZVHVIOZPZAKQZVJOVEVMEBIGZVPECDZVEVQVMRSEBTUAVEVQVHVGBIGZPZ
      AKQZVPVEVQWAVRVEVQWASAEBUBUCUDVEVTVOAKVEVGKDZPVSVNVHWBVEVGCDZVSVNJVGUEVEW
      CPVIVSBVGUFUGUHUIUJUKULVHVIAKUMUNUOUPUQVEVFVKVLRVJVEVKVLVFPZVFVLVEVRVKWDR
      SBEURUSVFVLWDVFVLUTVAVBVCVD $.
      $( [6-Feb-2007] $)
  $}

