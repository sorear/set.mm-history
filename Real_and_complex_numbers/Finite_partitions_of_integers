$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Finite partitions of integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ... $. $( Dots. $)

  $( Extend class notation to include the notation for a finite set of
     integers.  Read " ` M ... N ` " as "the set of integers from ` M ` to
     ` N ` inclusive." $)
  cfz $a class ... $.

  ${
    $d m n k z $.
    $( Define an operation that produces a finite set of sequential integers.
       Read " ` M ... N ` " as "the set of integers from ` M ` to ` N `
       inclusive."  See ~ fzvalt for its value and additional comments. $)
    df-fz $a |- ... = { <. <. m , n >. , z >. | ( ( m e. ZZ /\ n e. ZZ ) /\
      z = { k e. ZZ | ( m <_ k /\ k <_ n ) } ) } $.
  $}

  ${
    $d k m n z M $.  $d k m n z N $.
    $( The value of a finite set of sequential integers.  E.g., ` 2 ... 5 `
       means the set ` { 2 , 3 , 4 , 5 } ` .  A special case of this
       definition (starting at 1) appears as Definition 11-2.1 of [Gleason]
       p. 141, where ` NN `_k means our ` 1 ... k ` ; he calls these sets
       _segments_ of the integers. $)
    fzvalt $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M ... N ) =
                 { k e. ZZ | ( M <_ k /\ k <_ N ) } ) $=
      ( vm vn vz cz cv cle wbr wa crab cfz zex rabex wceq breq1 anbi1d
      rabbisdv breq2 anbi2d df-fz oprabval2 ) DEFBCGGDHZAHZIJZUEEHZIJZKZAGLBUEI
      JZUECIJZKZAGLMUJUHKZAGLULAGNOUDBPZUIUMAGUNUFUJUHUDBUEIQRSUGCPZUMULAGUOUHU
      KUJUGCUEITUASFADEUBUC $.
      $( [18-Oct-2005] $) $( [6-Sep-2005] $)
  $}

  ${
    $d j K $.  $d j M $.  $d j N $.
    $( Membership in a finite set of sequential integers. $)
    elfz1t $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( K e. ( M ... N ) <->
                 ( K e. ZZ /\ M <_ K /\ K <_ N ) ) ) $=
      ( vj cz wcel wa cfz co cv cle wbr crab w3a fzvalt eleq2d wceq breq2
      breq1 anbi12d elrab 3anass bitr4 syl6bb ) BEFCEFGZABCHIZFABDJZKLZUGCKLZGZ
      DEMZFZAEFZBAKLZACKLZNZUEUFUKADBCOPULUMUNUOGZGUPUJUQDAEUGAQUHUNUIUOUGABKRU
      GACKSTUAUMUNUOUBUCUD $.
      $( [31-Jul-2005] $) $( [21-Jul-2005] $)
  $}

  $( Membership in a finite set of sequential integers. $)
  elfzt $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) -> ( K e. ( M ... N ) <->
                 ( M <_ K /\ K <_ N ) ) ) $=
    ( cz wcel cfz co cle wbr wa wb w3a elfz1t 3anass baib sylan9bb 3impa 3comr
    ) BDEZCDEZADEZABCFGEZBAHIZACHIZJZKZSTUAUFSTJUBUAUCUDLZUAUEABCMUGUAUEUAUCUDN
    OPQR $.
    $( [11-Oct-2005] $) $( [29-Sep-2005] $)

  ${
    $d k n m z M $.  $d k n m z N $.
    $( Membership in a finite set of sequential integers.  We use the fact that
       an operation's value is empty outside of its domain to show ` M e. ZZ `
       and ` N e. ZZ ` . $)
    elfz2t $p |- ( N e. A -> ( K e. ( M ... N ) <->
           ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) /\ ( M <_ K /\ K <_ N ) ) ) ) $=
      ( vm vn vz vk wcel cfz co cz wa cle wbr w3a wb elfz1t ibar 3anass syl5bb
      bitrd adantl wn c0 cv crab zex rabex df-fz dmoprab2 ndmoprg eleq2d noel
      pm2.21i pm3.26 pm5.21ni pm2.61dan df-3an anbi1i anass bitr2 syl6bb ) DAIZ
      BCDJKZIZCLIZDLIZMZBLIZCBNOZBDNOZMZMZMZVGVHVJPZVMMZVDVIVFVOQZVIVRVDVIVFVJV
      KVLPZVOBCDRVIVNVOVSVIVNSVJVKVLTUAUBUCVDVIUDZMZVFBUEIZVOWAVEUEBCDALJEFGLLE
      UFHUFZNOWCFUFNOMZHLUGJWDHLUHUIGHEFUJUKULUMVTWBVOQVDWBVIVOWBVIBUNUOVIVNUPU
      QUCUBURVQVIVJMZVMMVOVPWEVMVGVHVJUSUTVIVJVMVAVBVC $.
      $( [7-Sep-2005] $) $( [6-Sep-2005] $)

    $( Lemma for ~ elfzel1 and others. $)
    elfzlem $p |- ( K e. ( M ... N ) ->
                    ( ( M e. ZZ /\ K e. ZZ ) /\ ( M <_ K /\ K <_ N ) ) ) $=
      ( vm vn vz vk cvv wcel cfz co cz wa cle wbr wi w3a elfz2t 3simpb anim1i
      syl6bi wn oprprc2 eleq2d brprc anbi2d pm3.26 anim12i cr zret leidt syl
      3ad2ant3 adantr jcad anass syl6ib c0 wceq cdm wrel cxp relxp cv crab zex
      rabex df-fz dmoprab2 releqi mpbir oprprc1 pm2.21nd n0i syl5 pm2.61i
      syl5bir sylbid ) CHIZABCJKZIZBLIZALIZMZBANOZACNOZMZMZPVSWAWBCLIZWCQZWGMWH
      HABCRWJWDWGWBWIWCSTUAVSUBZWAABBJKZIZWHWKVTWLABCJUCUDWKWHWDWEAANOZMZMZWMWK
      WGWOWDWKWFWNWEACNUEUFUFBHIZWMWPPWQWMWDWEMZWNMWPWQWMWRWNWQWMWBWBWCQZWEABNO
      ZMZMZWRHABBRZWSWDXAWEWBWBWCSWEWTUGUHUAWQWMXBWNXCWSWNXAWCWBWNWBWCAUIIWNAUJ
      AUKULUMUNUAUOWDWEWNUPUQWQUBZWLURUSZUBWPWMXDXEWPBBJJUTZVALLVBZVALLVCXFXGDE
      FLLDVDGVDZNOXHEVDNOMZGLVEJXIGLVFVGFGDEVHVIVJVKVLVMWLAVNVOVPVQVRVP $.
      $( [19-Sep-2005] $) $( [6-Sep-2005] $)
  $}

  $( Membership in a finite set of sequential integers. $)
  elfz5t $p |- ( ( K e. ( ZZ> ` M ) /\ N e. ZZ ) ->
                 ( K e. ( M ... N ) <-> K <_ N ) ) $=
    ( cuz cfv wcel cz wa cfz co cle wbr wb elfzt 3expa eluzelz eluzel2 jca
    sylan eluzle biantrurd adantr bitr4d ) ABDEFZCGFZHABCIJFZBAKLZACKLZHZUHAGFZ
    BGFZHUEUFUIMZUDUJUKUEULABCNOUDUJUKBAPBAQRSUDUHUIMUEUDUGUHBATUAUBUC $.
    $( [10-Mar-2006] $) $( [26-Dec-2005] $)

  $( Membership in a finite set of sequential integers. $)
  elfz4t $p |- ( ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) /\ ( M <_ K /\ K <_ N ) )
               -> K e. ( M ... N ) ) $=
    ( cz wcel w3a cle wbr cfz co wi wa elfz1t biimprcd 3exp com4r 3impia imp32
    ) BDEZCDEZADEZFBAGHZACGHZABCIJEZSTUAUBUCUDKKUAUBUCSTLZUDUAUBUCUEUDKUEUDUAUB
    UCFABCMNOPQR $.
    $( [3-Aug-2005] $) $( [21-Jul-2005] $)

  $( Membership in a finite set of sequential integers in terms of upper
     integer partitions. $)
  elfzuzb $p |- ( N e. A -> ( K e. ( M ... N ) <->
         ( K e. ( ZZ> ` M ) /\ N e. ( ZZ> ` K ) ) ) ) $=
    ( wcel cfz co cz w3a cle wbr wa cuz cfv elfz2t an6 eluz2t anbi12i df-3an
    anandir ancom anbi2i 3bitr anbi1i bitr4 3bitr4r syl6bb ) DAEBCDFGECHEZDHEZB
    HEZIZCBJKZBDJKZLZLZBCMNEZDBMNEZLZABCDOUHUJULIZUJUIUMIZLUHUJLZUJUILZUNIZURUO
    UHUJULUJUIUMPUPUSUQUTCBQBDQRUOVAVBLZUNLVCUKVDUNUKUHUILUJLVAUIUJLZLVDUHUIUJS
    UHUIUJTVEVBVAUIUJUAUBUCUDVAVBUNSUEUFUG $.
    $( [29-Sep-2005] $) $( [18-Sep-2005] $)

  $( Membership in a finite set of sequential integers. $)
  eluzfzt $p |- ( ( K e. ( ZZ> ` M ) /\ N e. ( ZZ> ` K ) )
               -> K e. ( M ... N ) ) $=
    ( cz wcel cuz cfv wa cfz co eluzelz adantl elfzuzb biimpar mpancom ) CDEZAB
    FGEZCAFGEZHZABCIJEZRPQACKLPTSDABCMNO $.
    $( [6-Oct-2005] $) $( [4-Oct-2005] $)

  $( Membership in a finite set of sequential integers implies membership in
     an upper integer partition. $)
  elfzuz3t $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> N e. ( ZZ> ` K ) ) $=
    ( wcel cfz co wa cuz cfv elfzuzb biimpa pm3.27d ) DAEZBCDFGEZHBCIJEZDBIJEZN
    OPQHABCDKLM $.
    $( [27-Nov-2005] $) $( [28-Sep-2005] $)

  ${
    elfzel2.1 $e |- N e. V $.
    $( Membership in a finite set of sequential integer implies the upper
       bound is an integer. $)
    elfzel2 $p |- ( K e. ( M ... N ) -> N e. ZZ ) $=
      ( cfz co wcel cz w3a cle wbr wa cvv wb elfz2t ax-mp 3simp2 adantr sylbi
      ) ABCEFGZBHGZCHGZAHGZIZBAJKACJKLZLZUBCMGTUFNDMABCOPUDUBUEUAUBUCQRS $.
      $( [17-Sep-2005] $) $( [6-Sep-2005] $)
  $}

  $( Membership in a finite set of sequential integers implies the upper
     bound is an integer. $)
  elfzel2g $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> N e. ZZ ) $=
    ( wcel cfz co cz w3a cle wbr wa elfz2t 3simp2 adantr syl6bi imp ) DAEZBCDFG
    EZDHEZRSCHEZTBHEZIZCBJKBDJKLZLTABCDMUCTUDUATUBNOPQ $.
    $( [6-Dec-2005] $) $( [6-Sep-2005] $)

  $( Membership in a finite set of sequential integer implies the lower
     bound is an integer. $)
  elfzel1 $p |- ( K e. ( M ... N ) -> M e. ZZ ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simpll syl ) ABCDEFBGFZAGFZHBAIJACIJHZH
    NABCKNOPLM $.
    $( [19-Sep-2005] $) $( [6-Sep-2005] $)

  $( A member of a finite set of sequential integer is an integer. $)
  elfzelz $p |- ( K e. ( M ... N ) -> K e. ZZ ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simplr syl ) ABCDEFBGFZAGFZHBAIJACIJHZH
    OABCKNOPLM $.
    $( [20-Sep-2005] $) $( [6-Sep-2005] $)

  $( A member of a finite set of sequential integer is greater than or
     equal to the lower bound. $)
  elfzle1 $p |- ( K e. ( M ... N ) -> M <_ K ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simprl syl ) ABCDEFBGFAGFHZBAIJZACIJZHH
    OABCKNOPLM $.
    $( [20-Sep-2005] $) $( [6-Sep-2005] $)

  $( A member of a finite set of sequential integer is less than or
     equal to the upper bound. $)
  elfzle2 $p |- ( K e. ( M ... N ) -> K <_ N ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simprr syl ) ABCDEFBGFAGFHZBAIJZACIJZHH
    PABCKNOPLM $.
    $( [2-Oct-2005] $) $( [6-Sep-2005] $)

  $( Membership in a finite set of sequential integer implies the bounds are
     comparable. $)
  elfzle3 $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> M <_ N ) $=
    ( wcel cfz co wa cle wbr elfzle1 adantl elfzle2 cz wi cr letrt zret syl3an
    elfzel1 elfzelz elfzel2g syl3anc mp2and ) DAEZBCDFGEZHZCBIJZBDIJZCDIJZUFUHU
    EBCDKLUFUIUEBCDMLCNEZBNEZDNEZUHUIHUJOZUGCPEBPEDPEUNUKULUMCBDQCRBRDRSUFUKUEB
    CDTLUFULUEBCDUALABCDUBUCUD $.
    $( [17-Apr-2006] $) $( [18-Sep-2005] $)

  $( Implication of membership in a finite set of sequential integers. $)
  elfzuz2t $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> N e. ( ZZ> ` M ) ) $=
    ( wcel cfz co wa cuz cfv elfzuzb ancom syl6bb biimpa uztrn syl ) DAEZBCDFGE
    ZHDBIJEZBCIJZEZHZDTEQRUBQRUASHUBABCDKUASLMNBDCOP $.
    $( [1-Nov-2005] $) $( [20-Sep-2005] $)

  $( Membership in a finite set of sequential integers - special case. $)
  eluzfz1t $p |- ( N e. ( ZZ> ` M ) -> M e. ( M ... N ) ) $=
    ( cuz cfv wcel cz cle wbr w3a cfz co eluz2t wa elfz4t 3simp1 3simp2 3jca
    cr zret leidt syl 3ad2ant1 3simp3 jca sylanc sylbi ) BACDEAFEZBFEZABGHZIZAA
    BJKEZABLUGUHUGIAAGHZUIMUKUJAABNUJUGUHUGUGUHUIOZUGUHUIPUMQUJULUIUGUHULUIUGAR
    EULASATUAUBUGUHUIUCUDUEUF $.
    $( [18-Sep-2005] $) $( [21-Jul-2005] $)

  $( A member of a finite set of sequential integers belongs to an upper
     partition of integers. $)
  elfzuzt $p |- ( K e. ( M ... N ) -> K e. ( ZZ> ` M ) ) $=
    ( cfz co wcel cz cle wbr w3a cuz cfv elfzel1 elfzelz elfzle1 3jca eluz2t
    sylibr ) ABCDEFZBGFZAGFZBAHIZJABKLFSTUAUBABCMABCNABCOPBAQR $.
    $( [28-Oct-2005] $) $( [17-Sep-2005] $)

  $( Membership in a finite set of sequential integers - special case. $)
  eluzfz2t $p |- ( N e. ( ZZ> ` M ) -> N e. ( M ... N ) ) $=
    ( cz wcel w3a cle wbr wa cfz co cuz cfv elfz4t eluzel2 eluzelz 3jca eluzle
    cr zret leidt 3syl jca sylanc ) ACDZBCDZUEEABFGZBBFGZHBABIJDBAKLDZBABMUHUDU
    EUEABNABOZUIPUHUFUGABQUHUEBRDUGUIBSBTUAUBUC $.
    $( [14-Sep-2005] $) $( [13-Sep-2005] $)

  $( Membership in a finite set of sequential integers - special case. $)
  eluzfz2b $p |- ( N e. ( ZZ> ` M ) <-> N e. ( M ... N ) ) $=
    ( cuz cfv wcel cfz co eluzfz2t elfzuzt impbi ) BACDEBABFGEABHBABIJ $.
    $( [20-Sep-2005] $) $( [14-Sep-2005] $)

  $( Membership in a finite set of sequential integers containing one
     integer. $)
  elfz3t $p |- ( N e. ZZ -> N e. ( N ... N ) ) $=
    ( cz wcel cuz cfv cfz co uzidt eluzfz1t syl ) ABCAADECAAAFGCAHAAIJ $.
    $( [19-Aug-2005] $) $( [21-Jul-2005] $)

  $( Membership in a finite set of sequential integers containing one
     integer. $)
  elfz1eqt $p |- ( K e. ( N ... N ) -> K = N ) $=
    ( cfz co wcel cz wa cle wbr wceq elfzlem cr wb letri3t zret syl2an biimpar
    eqcomd syl ) ABBCDEBFEZAFEZGZBAHIABHIGZGZABJABBKUDBAUBBAJZUCBLEALEUEUCMTUAB
    ANBOAOPQRS $.
    $( [3-Oct-2005] $) $( [19-Sep-2005] $)

  ${
    $d k M $.  $d k N $.
    $( A finite set of sequential integers is empty if the bounds are
       reversed. $)
    fznt $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( N < M <-> ( M ... N ) = (/) ) ) $=
      ( vk cz wcel wa clt wbr cfz co c0 wceq cle wn cr wb lenltt zret syl2an
      w3a cuz cfv eluz2t eluzfz1t sylbir n0i syl 3expa ex wi cv wex elfzle3
      19.23adv n0 syl5ib adantl impbid bitr3d con4bid ) ADEZBDEZFZBAGHZABIJZKLZ
      VCABMHZVDNZVFNZAOEBOEVGVHPVAVBABQARBRSVCVGVIVCVGVIVAVBVGVIVAVBVGTZAVEEZVI
      VJBAUAUBEVKABUCABUDUEVEAUFUGUHUIVBVIVGUJVAVBCUKZVEEZCULVGVIVBVMVGCVBVMVGD
      VLABUMUIUNCVEUOUPUQURUSUT $.
      $( [18-Apr-2006] $) $( [22-Aug-2005] $)
  $}

  $( A member of a finite set of sequential integers starting at 1 is a
     natural number. $)
  elfznnt $p |- ( K e. ( 1 ... N ) -> K e. NN ) $=
    ( c1 cfz co wcel cz cle wbr wa cn elfzelz elfzle1 jca elnnz1 sylibr ) ACBDE
    FZAGFZCAHIZJAKFQRSACBLACBMNAOP $.
    $( [27-Nov-2005] $) $( [24-Aug-2005] $)

  $( Membership in a finite set of sequential integers starting at 0. $)
  elfz2nn0t $p |- ( N e. A ->
           ( K e. ( 0 ... N ) <-> ( K e. NN0 /\ N e. NN0 /\ K <_ N ) ) ) $=
    ( wcel cc0 cfz co cz w3a cle wbr wa cn0 elfz2t pm3.27 pm3.26 anim12i
    simpll cr wi 0re letrt mp3an1 ancoms zret syl2an imp jca simprr 3jca
    3adantl1 0z a1i 3anim123i 3com13 id adantll 3adant2 impbi elnn0z pm4.2
    3anbi123i bitr4 syl6bb ) CADBECFGDEHDZCHDZBHDZIZEBJKZBCJKZLZLZBMDZCMDZVJIZA
    BECNVLVGVILZVFECJKZLZVJIZVOVLVSVFVGVKVSVEVFVGLZVKLZVPVRVJVTVGVKVIVFVGOVIVJP
    QWAVFVQVFVGVKRVTVKVQCSDZBSDZVKVQTZVFVGWCWBWDESDWCWBWDUAEBCUBUCUDCUEBUEUFUGU
    HVTVIVJUIUJUKVSVHVKVJVRVPVHVJVEVRVFVPVGVEVJULUMVFVQPVGVIPUNUOVPVJVKVRVIVJVK
    VGVKUPUQURUHUSVMVPVNVRVJVJBUTCUTVJVAVBVCVD $.
    $( [2-Feb-2006] $) $( [16-Sep-2005] $)

  $( A member of a finite set of sequential integers starting at 0 is a
     nonnegative integer. $)
  elfznn0t $p |- ( K e. ( 0 ... N ) -> K e. NN0 ) $=
    ( cc0 cfz co wcel cz cle wbr wa cn0 elfzelz elfzle1 jca elnn0z sylibr ) ACB
    DEFZAGFZCAHIZJAKFQRSACBLACBMNAOP $.
    $( [22-Sep-2005] $) $( [5-Aug-2005] $)

  $( The upper bound of a nonempty finite set of sequential integers starting
     at 0 is a nonnegative integer. $)
  elfz3nn0t $p |- ( ( N e. A /\ K e. ( 0 ... N ) ) -> N e. NN0 ) $=
    ( wcel cc0 cfz co cn0 cle wbr w3a elfz2nn0t 3simp2 syl6bi imp ) CADZBECFGDZ
    CHDZPQBHDZRBCIJZKRABCLSRTMNO $.
    $( [6-Feb-2006] $) $( [16-Sep-2005] $)

  $( Subtraction closure for a member of a finite set of sequential
     integers. $)
  fznn0subt $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> ( N - K ) e. NN0 ) $=
    ( wcel cfz co cmin cn0 cz w3a cle wbr wa elfz2t znn0sub2t 3com12 3expa
    adantrl 3adantl1 syl6bi imp ) DAEZBCDFGEZDBHGIEZUCUDCJEZDJEZBJEZKCBLMZBDLMZ
    NZNUEABCDOUGUHUKUEUFUGUHNUJUEUIUGUHUJUEUHUGUJUEBDPQRSTUAUB $.
    $( [14-Jan-2006] $) $( [16-Sep-2005] $)

  $( Subtraction closure for a member of a finite set of sequential
     integers. $)
  fznn0sub2t $p |- ( ( N e. A /\ K e. ( 0 ... N ) ) ->
                   ( N - K ) e. ( 0 ... N ) ) $=
    ( wcel cc0 cfz co cmin cn0 cle wbr w3a wa fznn0subt elfz3nn0t elfzle1
    adantl cz wb cr subge02t zret syl2an elfzel2g elfzelz sylanc mpbid 3jca
    elfz2nn0t biimpar syldan ) CADZBECFGZDZCBHGZIDZCIDZUOCJKZLZUOUMDZULUNMZUPUQ
    URABECNABCOVAEBJKZURUNVBULBECPQCRDZBRDZVBURSZVACTDBTDVEVCVDCBUACUBBUBUCABEC
    UDUNVDULBECUEQUFUGUHULUTUSAUOCUIUJUK $.
    $( [5-Feb-2006] $) $( [26-Sep-2005] $)

  $( Membership of a sum in a finite set of sequential integers. $)
  fzaddelt $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( J e. ( M ... N ) <-> ( J + K ) e. ( ( M + K ) ... ( N + K ) ) ) ) $=
    ( cz wcel wa cle wbr w3a caddc co cfz wb zaddclt a1d pm3.26 impbid adantl
    cr leadd1t zret syl3an 3expb adantlr 3com12 adantll 3anbi123d elfz1t
    adantr syl2an anandirs adantrl 3bitr4d ) CEFZDEFZGZAEFZBEFZGZGZURCAHIZADHIZ
    JZABKLZEFZCBKLZVEHIZVEDBKLZHIZJZACDMLFZVEVGVIMLFZVAURVFVBVHVCVJUTURVFNUQUTU
    RVFUTVFURABOPUTURVFURUSQPRSUOUTVBVHNZUPUOURUSVNCTFATFZBTFZVNUOURUSCABUACUBA
    UBZBUBZUCUDUEUPUTVCVJNZUOUPURUSVSURUPUSVSVODTFVPVSURUPUSADBUAVQDUBVRUCUFUDU
    GUHUQVLVDNUTACDUIUJUQUSVMVKNZURUOUPUSVTVGEFVIEFVTUOUSGUPUSGVEVGVIUICBODBOUK
    ULUMUN $.
    $( [24-Aug-2005] $) $( [30-Jul-2005] $)

  $( Membership of a difference in a finite set of sequential integers. $)
  fzsubelt $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( J e. ( M ... N ) <-> ( J - K ) e. ( ( M - K ) ... ( N - K ) ) ) ) $=
    ( cz wcel wa cfz co cneg caddc cmin wb fzaddelt znegclt sylanr2 cc wceq
    negsubt adantl opreqan12d anandirs adantrl eleq12d zcnt anim12i syl2an
    bitrd ) CEFZDEFZGZAEFZBEFZGZGACDHIFZABJZKIZCUPKIZDUPKIZHIZFZABLIZCBLIZDBLIZ
    HIZFZUKULUPEFUOVAMUMAUPCDNBOPCQFZDQFZGZAQFZBQFZGZVAVFMUKUNVIVLGUQVBUTVEVLUQ
    VBRVIABSTVIVKUTVERZVJVGVHVKVMVGVKGVHVKGURVCUSVDHCBSDBSUAUBUCUDUIVGUJVHCUEDU
    EUFULVJUMVKAUEBUEUFUGUH $.
    $( [1-Aug-2006] $) $( [30-Jul-2005] $)

  $( A finite set of sequential integers can represent an ordered pair. $)
  fzoptht $p |- ( ( N e. ( ZZ> ` M ) /\ K e. A ) ->
                ( ( M ... N ) = ( J ... K ) <-> ( M = J /\ N = K ) ) ) $=
    ( cuz cfv wcel wa cfz co wceq eleq2 eluzfz2t syl5bi anim1d elfzuz2t ancoms
    syl6com wi cle wbr eluzfz1t syl5bir elfzle1 adantl adantr jcad cr wb
    letri3t cz eluzel2 zret syl syl2an sylibrd elfzle2 eluzelz ex com23 mpdd
    opreq12 impbid1 ) EDFGHZCAHZIZDEJKZBCJKZLZDBLZECLZIZVGVJCBFGHZVMVJVGEVIHZVF
    IVNVJVEVOVFVJEVHHVOVEVHVIEMDENOZPVFVOVNAEBCQRSVEVJVNVMTTVFVEVNVJVMVEVNVJVMT
    VEVNIZVJVKVLVQVJDBUAUBZBDUAUBZIZVKVQVJVRVSVNVJVRTVEVJVNBVHHZVRVJWABVIHVNVHV
    IBMBCUCUDBDEUESUFVEVJVSTVNVJVEDVIHZVSVJDVHHWBVEVHVIDMDEUCODBCUESUGUHDUIHZBU
    IHZVKVTUJVEVNDBUKVEDULHWCDEUMDUNUOVNBULHWDBCUMBUNUOUPUQVQVJECUAUBZCEUAUBZIZ
    VLVQVJWEWFVEVJWETVNVJVEVOWEVPEBCURSUGVNVJWFTVEVJVNCVHHZWFVJWHCVIHVNVHVICMBC
    NUDCDEURSUFUHEUIHZCUIHZVLWGUJVEVNECUKVEEULHWIDEUSEUNUOVNCULHWJBCUSCUNUOUPUQ
    UHUTVAUGVBDBECJVCVD $.
    $( [1-Nov-2005] $) $( [31-Oct-2005] $)

$(
  @( Union of two adjacent finite sets of sequential integers. @)
  fzunt @p |- ( ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) /\ ( K <_ M /\ M <_ N ) )
               -> ( ( K ... M ) u. ( M ... N ) ) = ( K ... N ) ) @=
    ( vj cz wcel w3a cle wbr wa cfz co cun cv wo cr wb wi letrt exp3a com23
    3exp com3l imp42 adantlll adantrl anim2d 3expa imp adantlrl adantrr anim1d
    jaod impbid exp42 com34 ex 3imp zret syl3an syl5 pm5.32d andi bicomi
    syl5bb elfz1t 3anass syl6bb 3adant3 adantr 3adant1 orbi12d 3adant2 3bitr4d
    elun eqrdv ) AEFZBEFZCEFZGZABHIZBCHIZJZJZDABKL
    ZBCKLZMZACKLZWDDNZWEFZWIWFFZOZ
    WIWHFZWIWGFWDWIEFZAWIHIZWIBHIZJZJZWNBWIHIZWICHIZJZJZOZWNWOWTJZJZWLWMWDWNWQX
    AOZJZXEXCWDWNXFXDWDWIPFZXFXDQZWNVTWCXHXIRZAPFZBPFZCPFZWCXJRZVQVRVSXKXLXMXNX
    KXLXMXNRXKXLJZXMXHWCXIXOXMXHWCXIXOXMXHJZJZWCJZXFXDXRWQXDXAXRWPWTWOXQWBWPWTR
    ZWAXLXPWBXSXKXLXMXHWBXSXHXLXMWBXSRZXHXLXMXTXHXLXMGZWPWBWTYAWPWBWTWIBCSTUAUB
    UCUDUEUFUGXRWSWOWTXQWAWSWORZWBXOXHWAYBXMXOXHJZWAYBYCWAWSWOXKXLXHWAWSJWORABW
    ISUHTUIUJUKULUM?UNUOUPUQURAUSBUSCUSUTUIWIUSVAVBXGXCWNWQXAVCVDVEWDWJWRWKXBVT
    WJWRQZWCVQVRYDVSVQVRJWJWNWOWPGWRWIABVFWNWOWPVGVHVIVJVTWKXBQZWCVRVSYEVQVRVSJ
    WKWNWSWTGXBWIBCVFWNWSWTVGVHVKVJVLVTWMXEQZWCVQVSYFVRVQVSJWMWNWOWTGXEWIACVFWN
    WOWTVGVHVMVJVNWIWEWFVOVEVP @.
    @( [?] @) @( [21-Jul-05] @)
  $}
$)

  ${
    $d k M $.  $d k N $.  $d k K $.
    $( Subset relationship for finite sets of sequential integers. $)
    fzss1t $p |- ( ( K e. ( ZZ> ` M ) /\ N e. ZZ ) ->
                  ( K ... N ) (_ ( M ... N ) ) $=
      ( vk cuz cfv wcel cz wa cv cle wbr crab cfz co wss eluzle adantr wi cr
      letrt zret syl3an 3expa eluzel2 eluzelz jca sylan mpand anim1d ss2rabdv
      wceq fzvalt 3sstr4d ) ABEFGZCHGZIADJZKLZUQCKLZIZDHMZBUQKLZUSIZDHMZACNOZBC
      NOZUOVAVDPUPUOUTVCDHUOUQHGZIZURVBUSVHBAKLZURVBUOVIVGBAQRBHGZAHGZIVGVIURIV
      BSZUOVJVKVGVLBTGATGUQTGVLVJVKVGBAUQUABUBAUBUQUBUCUDUOVJVKBAUEZBAUFZUGUHUI
      UJUKRVKUPVEVAULUODACUMVNUHVJUPVFVDULUODBCUMVMUHUN $.
      $( [10-Oct-2005] $) $( [28-Sep-2005] $)

    $( Subset relationship for finite sets of sequential integers. $)
    fzss2t $p |- ( ( N e. ( ZZ> ` K ) /\ M e. ZZ ) ->
                  ( M ... K ) (_ ( M ... N ) ) $=
      ( vk cz wcel cuz cfv cfz co wss wa cv cle wbr crab wi eluzle adantl cr
      letrt zret syl3an 3expb eluzel2 eluzelz jca sylan2 mpan2d ancoms anim2d
      ss2rabdv wceq fzvalt 3sstr4d ) BEFZCAGHFZBAIJZBCIJZKUPUQLBDMZNOZUTANOZLZD
      EPZVAUTCNOZLZDEPZURUSUQVDVGKUPUQVCVFDEUQUTEFZLVBVEVAVHUQVBVEQVHUQLVBACNOZ
      VEUQVIVHACRSVHAEFZCEFZLVBVILVEQZUQVHVJVKVLUTTFATFCTFVLVHVJVKUTACUAUTUBAUB
      CUBUCUDUQVJVKACUEZACUFZUGUHUIUJUKULSUPVJURVDUMUQDBAUNVMUHUPVKUSVGUMUQDBCU
      NVNUHUOUJ $.
      $( [2-Nov-2005] $) $( [4-Oct-2005] $)

    $( A finite set of sequential integers is a subset of an upper partition
       of integers. $)
    fzssuzt $p |- ( M ... N ) (_ ( ZZ> ` M ) $=
      ( vk cfz co cuz cfv cv elfzuzt ssriv ) CABDEAFGCHABIJ $.
      $( [25-Apr-2006] $) $( [28-Oct-2005] $)
  $}

  ${
    $d k M $.  $d k N $.
    $( Subset relationship for finite sets of sequential integers. $)
    fzssp1t $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                  ( M ... N ) (_ ( M ... ( N + 1 ) ) ) $=
      ( cz wcel cfz co c1 caddc wss cuz cfv fzss2t uzidt peano2uz syl sylan
      ancoms ) BCDZACDZABEFABGHFZEFIZTBJKZDZSUARBATLRBUBDUCBMBBNOPQ $.
      $( [19-Aug-2005] $) $( [21-Jul-2005] $)

    $( Subset relationship for finite sets of sequential integers. $)
    fzp1sst $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                  ( ( M + 1 ) ... N ) (_ ( M ... N ) ) $=
      ( c1 caddc co cuz cfv wcel cz cfz wss fzss1t uzidt peano2uz syl sylan )
      ACDEZAFGZHZBIHQBJEABJEKAIHZQABLTARHSAMAANOP $.
      $( [23-Aug-2005] $) $( [26-Jul-2005] $)
  $}

  $( Membership in a set of sequential integers with an appended element. $)
  fzelp1t $p |- ( ( N e. A /\ K e. ( M ... N ) ) ->
                K e. ( M ... ( N + 1 ) ) ) $=
    ( wcel cfz co wa c1 caddc cz wss fzssp1t elfzel1 adantl elfzel2g sylanc
    pm3.27 sseldd ) DAEZBCDFGZEZHZUACDIJGFGZBCKEZDKEUAUDLUCCDMUBUETBCDNOABCDPQT
    UBRS $.
    $( [31-Jan-2006] $) $( [7-Dec-2005] $)

  ${
    fzelp1.1 $e |- N e. V $.
    $( Membership in a set of sequential integers with an appended element. $)
    fzelp1 $p |- ( K e. ( M ... N ) -> K e. ( M ... ( N + 1 ) ) ) $=
      ( cvv wcel cfz co c1 caddc fzelp1t mpan ) CEFABCGHFABCIJHGHFDEABCKL $.
      $( [8-Feb-2006] $) $( [3-Dec-2005] $)
  $}

  $( Append an element to a finite set of sequential integers. $)
  elfzp1 $p |- ( N e. ( ZZ> ` M ) ->
    ( K e. ( M ... ( N + 1 ) ) <-> ( K e. ( M ... N ) \/ K = ( N + 1 ) ) ) ) $=
    ( cuz cfv wcel c1 caddc co cfz wceq wo wa wn cz w3a cle wbr eluzel2
    ad2antrr eluzelz elfzelz ad2antlr 3jca elfzle1 clt elfzle2 adantl anim1i
    cr wb ltlent zret syl peano2re 3syl sylanc mpbird zleltp1t jca ex elfz2t
    adantr sylibrd orrd orcom sylib wss fzssp1t sseld eleq1 peano2uz eluzfz2t
    syl5bir com12 jaod impbid ) CBDEZFZABCGHIZJIZFZABCJIZFZAVTKZLZVSWBWFVSWBMZW
    EWDLWFWGWEWDWGWENZBOFZCOFZAOFZPZBAQRZACQRZMZMZWDWGWHWPWGWHMZWLWOWQWIWJWKVSW
    IWBWHBCSZTVSWJWBWHBCUAZTZWBWKVSWHABVTUBZUCZUDWQWMWNWBWMVSWHABVTUEUCWQWNAVTU
    FRZWQXCAVTQRZWHMZWGXDWHWBXDVSABVTUGUHUIAUJFZVTUJFZXCXEUKWQAVTULWBXFVSWHWBWK
    XFXAAUMUNUCVSXGWBWHVSWJCUJFXGWSCUMCUOUPTUQURWKWJWNXCUKWQACUSXBWTUQURUTUTVAV
    SWDWPUKWBVRABCVBVCVDVEWEWDVFVGVAVSWDWBWEVSWCWAAWIWJWCWAVHVSBCVIWRWSUQVJWEVS
    WBWEWBVTWAFZVSAVTWAVKVSVTVRFXHBCVLBVTVMUNVNVOVPVQ $.
    $( [3-Oct-2005] $) $( [19-Sep-2005] $)

  $( Reversal of start and end of a finite set of sequential integers. $)
  fzrevt $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( K e. ( ( J - N ) ... ( J - M ) ) <-> ( J - K ) e. ( M ... N ) ) ) $=
    ( cz wcel wa cmin co cle wbr cfz wb cr sublet zret syl3an 3comr 3expb
    adantll lesubt adantlr anbi12d ancom syl5rbbr elfzt simprr zsubclt ancoms
    adantrr ad2ant2r syl3anc adantl simpll simplr 3bitr4d ) CEFZDEFZGZAEFZBEFZG
    ZGZADHIZBJKZBACHIZJKZGZCABHIZJKZVIDJKZGZBVDVFLIFZVICDLIFZVCVKVJGVHVLVCVKVEV
    JVGURVBVKVEMZUQURUTVAVOUTVAURVOANFZBNFZDNFVOUTVAURABDOAPZBPZDPQRSTUQVBVJVGM
    ZURUQUTVAVTCNFVPVQVTUQUTVACABUACPVRVSQSUBUCVKVJUDUEVAVDEFZVFEFZVMVHMVCBVDVF
    UFUSUTVAUGURVBWAUQURUTWAVAUTURWAADUHUIUJTUQUTWBURVAUTUQWBACUHUIUKULVIEFZUQU
    RVNVLMVCVICDUFVBWCUSABUHUMUQURVBUNUQURVBUOULUP $.
    $( [2-Dec-2005] $) $( [25-Nov-2005] $)

  $( Reversal of start and end of a finite set of sequential integers. $)
  fzrev2t $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( K e. ( M ... N ) <-> ( J - K ) e. ( ( J - N ) ... ( J - M ) ) ) ) $=
    ( cz wcel wa cmin co cfz wb fzrevt pm3.26 zsubclt jca sylan2 wceq cc
    nncant zcnt syl2an adantl eleq1d bitr2d ) CEFDEFGZAEFZBEFZGZGZABHIZADHIACHI
    JIFZAUJHIZCDJIZFZBUMFUEUFUJEFZGUKUNKUHAUJCDLUHUFUOUFUGMABNOPUIULBUMUHULBQZU
    EARFBRFUPUFUGABSATBTUAUBUCUD $.
    $( [4-Dec-2005] $) $( [25-Nov-2005] $)

  $( Reversal of start and end of a finite set of sequential integers. $)
  fzrev2it $p |- ( ( N e. A /\ J e. ZZ /\ K e. ( M ... N ) ) ->
                 ( J - K ) e. ( ( J - N ) ... ( J - M ) ) ) $=
    ( wcel cz cfz co w3a cmin 3simp3 wa wb fzrev2t elfzel1 adantl elfzel2g jca
    3adant2 elfzelz anim2i 3adant1 sylanc mpbid ) EAFZBGFZCDEHIFZJZUHBCKIBEKIBD
    KIHIFZUFUGUHLDGFZEGFZMZUGCGFZMZUHUJNUIBCDEOUFUHUMUGUFUHMUKULUHUKUFCDEPQACDE
    RSTUGUHUOUFUHUNUGCDEUAUBUCUDUE $.
    $( [7-Dec-2005] $) $( [25-Nov-2005] $)

  $( The "complement" of a member of a finite set of sequential integers. $)
  fzrev3t $p |- ( ( N e. A /\ K e. ZZ ) -> ( K e. ( M ... N ) <->
                ( ( M + N ) - K ) e. ( M ... N ) ) ) $=
    ( wcel cz wa cfz co caddc cmin w3a simplr elfzel1 adantl elfzel2g adantlr
    3jca wb cc wceq pncant pncan2t opreq12d zcnt syl2an eleq2d 3adant1 fzrevt
    3simpc zaddclt 3simp1 jca sylanc bitr3d pm5.21nd ) DAEZBFEZGZBCDHIZEZCDJIZB
    KIZUTEZURCFEZDFEZLZUSVAGURVEVFUQURVAMVAVEUSBCDNOUQVAVFURABCDPQRUSVDGURVEVFU
    QURVDMVDVEUSVCCDNOUQVDVFURAVCCDPQRVGBVBDKIZVBCKIZHIZEZVAVDVEVFVKVASURVEVFGZ
    VJUTBCTEZDTEZVJUTUAVEVFVMVNGVHCVIDHCDUBCDUCUDCUEDUEUFUGUHVLVBFEZURGVKVDSVGV
    BBCDUIURVEVFUJVGVOURVEVFVOURCDUKUHURVEVFULUMUNUOUP $.
    $( [23-Feb-2006] $) $( [20-Nov-2005] $)

  $( The "complement" of a member of a finite set of sequential integers. $)
  fzrev3it $p |- ( ( N e. A /\ K e. ( M ... N ) ) ->
                ( ( M + N ) - K ) e. ( M ... N ) ) $=
    ( wcel cfz co wa caddc cmin pm3.27 cz wb fzrev3t elfzelz sylan2 mpbid ) DAE
    ZBCDFGZEZHTCDIGBJGSEZRTKRBLETUAMTABCDNBCDOPQ $.
    $( [1-May-2006] $) $( [20-Nov-2005] $)

  $( Finite set of sequential integers starting at 0. $)
  fznn0t $p |- ( N e. NN0 ->
              ( K e. ( 0 ... N ) <-> ( K e. NN0 /\ K <_ N ) ) ) $=
    ( cn0 wcel cc0 cfz co cz cle wbr w3a wa wb nn0zt 0z jctil elfz1t syl
    df-3an elnn0z anbi1i bitr4 syl6bb ) BCDZAEBFGDZAHDZEAIJZABIJZKZACDZUHLZUDEH
    DZBHDZLUEUIMUDUMULBNOPAEBQRUIUFUGLZUHLUKUFUGUHSUJUNUHATUAUBUC $.
    $( [23-Aug-2005] $) $( [1-Aug-2005] $)

  ${
    $d k N $.
    $( Quantification over a one-member finite set of sequential integers in
       terms of substitution. $)
    fz1sbct $p |- ( N e. ZZ -> ( A. k e. ( N ... N ) ph <-> [ N / k ] ph ) ) $=
      ( cz wcel wsbc cv wceq wi wal cfz co wral sbc6g elfz1eqt eleq1 elfz3t
      syl5bir com12 impbid2 imbi1d albidv df-ral syl5rbb bitr2d ) CDEZABCFBGZCH
      ZAIZBJZABCCKLZMZABCDNUFUGUKEZAIZBJUJULUFUNUIBUFUMUHAUFUMUHUGCOUHUFUMUHUMC
      UKEUFUGCUKPCQRSTUAUBABUKUCUDUE $.
      $( [12-Dec-2005] $) $( [28-Nov-2005] $)
  $}

  $( No finite set of sequential integers equals an upper partition of
     integers. $)
  fzneuzt $p |- ( ( N e. ( ZZ> ` M ) /\ K e. ZZ )
               -> -. ( M ... N ) = ( ZZ> ` K ) ) $=
    ( cuz cfv wcel cz wa cfz co wceq wn c1 caddc nelneq2 peano2uz adantl cle
    wbr cr eluzelz zret clt ltp1t wb peano2re ltnlet mpdan mpbid 3syl elfzle2
    nsyl ad2antrr sylanc eqcom negbii sylib eluzfz2t pm3.27 pm2.61dan ) CBDEFZA
    GFZHZCADEZFZBCIJZVDKZLZVCVEHZVDVFKZLZVHCMNJZVDFZVLVFFZLZVKVIVLVDVFOVEVMVCAC
    PQVAVOVBVEVAVLCRSZVNVACGFCTFZVPLZBCUACUBVQCVLUCSZVRCUDVQVLTFVSVRUECUFCVLUGU
    HUIUJVLBCUKULUMUNVJVGVDVFUOUPUQCVFFZVELZVHVCWAHCVFVDOVAVTVBWABCURUMVCWAUSUN
    UT $.
    $( [20-Dec-2005] $) $( [11-Dec-2005] $)

  ${
    $d j k x K $.  $d j k x M $.  $d j k x N $.  $d k x ph $.
    $( Reversal of scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzrevralt $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) -> ( A. j e. ( M ... N
       ) ph <-> A. k e. ( ( K - N ) ... ( K - M ) ) [ ( K - k ) / j ] ph ) ) $=
      ( cz wcel w3a cfz co wral cv cmin wsbc wi wa pm3.27 wb fzrevt anassrs
      elfzelz sylan2 mpbid ra4sbc syl ex 3impa com23 r19.21adv ax-17 oprex
      hbsbc1v hbral fzrev2it 3expa wceq opreq2 dfsbcq rcla4v cc nncant zcnt
      syl2an eqcomd sbceq1a adantll sylibrd r19.21ad 3adant1 impbid ) EGHZFGHZD
      GHZIZABEFJKZLZABDCMZNKZOZCDFNKZDENKZJKZLZVOVQVTCWCVOVRWCHZVQVTVLVMVNWEVQV
      TPZPVLVMQZVNQZWEWFWHWEQZVSVPHZWFWIWEWJWHWERWHVRGHZWEWJSZWEWGVNWKWLDVREFTU
      AVRWAWBUBUCUDABVSVPUEUFUGUHUIUJVMVNWDVQPVLVMVNQZWDABVPWMBUKVTBCWCWEBUKABV
      SDVRNULUMUNWMBMZVPHZWDAWMWOWDAPWMWOQZWDABDDWNNKZNKZOZAWPWQWCHZWDWSPVMVNWO
      WTGDWNEFUOUPVTWSCWQWCVRWQUQVSWRUQVTWSSVRWQDNURABVSWRUSUFUTUFVNWOAWSSZVMVN
      WOQZWNWRUQXAXBWRWNDVAHWNVAHZWRWNUQVNWODWNVBDVCWOWNGHXCWNEFUBWNVCUFVDVEABW
      RVFUFVGVHUGUIVIVJVK $.
      $( [8-Dec-2005] $) $( [25-Nov-2005] $)

    $( Reversal of scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzrevral2t $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) -> ( A. j e. ( ( K - N
       ) ... ( K - M ) ) ph <-> A. k e. ( M ... N ) [ ( K - k ) / j ] ph ) ) $=
      ( cz wcel cmin co cfz wral cv wsbc wb w3a fzrevralt zsubclt 3adant2
      3adant3 3simp1 syl3anc cc wceq nncant opreq12d zcnt syl3an raleq1d bitrd
      3coml ) DGHZEGHZFGHZABDFIJZDEIJZKJLZABDCMIJNZCEFKJZLZOULUMUNPZUQURCDUPIJZ
      DUOIJZKJZLZUTUOGHZUPGHZULUQVEOVAABCDUOUPQULUNVFUMDFRSULUMVGUNDERTULUMUNUA
      UBVAURCVDUSDUCHZEUCHZFUCHZVDUSUDULUMUNVHVIVJPVBEVCFKVHVIVBEUDVJDEUETVHVJV
      CFUDVIDFUESUFDUGEUGFUGUHUIUJUK $.
      $( [10-May-2006] $) $( [25-Nov-2005] $)

    $( Reversal of scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzrevral3t $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( A. j e. ( M ... N ) ph
                   <-> A. k e. ( M ... N ) [ ( ( M + N ) - k ) / j ] ph ) ) $=
      ( cz wcel wa cfz co wral caddc cv cmin wsbc wb fzrevralt pm3.26 pm3.27
      zaddclt syl3anc cc wceq pncant pncan2t opreq12d zcnt syl2an raleq1d
      bitrd ) DFGZEFGZHZABDEIJZKZABDELJZCMNJOZCUPENJZUPDNJZIJZKZUQCUNKUKULUPFGU
      OVAPUMABCUPDEQUKULRUKULSDETUAUMUQCUTUNDUBGZEUBGZUTUNUCUKULVBVCHURDUSEIDEU
      DDEUEUFDUGEUGUHUIUJ $.
      $( [11-Jul-2006] $) $( [20-Nov-2005] $)

    $( Shift the scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzshftralt $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) -> ( A. j e. ( M ... N
       ) ph <-> A. k e. ( ( M + K ) ... ( N + K ) ) [ ( k - K ) / j ] ph ) ) $=
      ( vx cz wcel w3a cfz co wral cc0 cv cmin wsbc caddc wb 0z fzrevralt
      mp3an3 3adant3 zsubclt mpan id syl3an 3com12 cc wceq wa cneg subnegt
      axaddcom eqtrd df-neg opreq2i syl5eqr 3adant2 opreq12d 3coml zcnt
      raleq1d negsubdi2t elfzelz syl syl2an dfsbcq ralbidva 3ad2ant3 bitrd cvv
      wal oprex ax-gen opreq2 sbcco3g mp2an ralbii syl5bb 3bitrd ) EHIZFHIZDHIZ
      JZABEFKLMZABNGOZPLZQZGNFPLZNEPLZKLMZWIGDCOZPLZQZCDWKPLZDWJPLZKLZMZABWMDPL
      ZQZCEDRLZFDRLZKLZMZWBWCWFWLSZWDWBWCNHIZXFTABGNEFUAUBUCWCWBWDWLWSSZWJHIZWK
      HIZWDXHWCWBWDWIGCDWJWKUAXGWCXITNFUDUEXGWBXJTNEUDUEWDUFUGUHWEABNWNPLZQZCWR
      MZXEWSWEXMXLCXDMZXEWEXLCWRXDEUIIZFUIIZDUIIZWRXDUJZWBWCWDXQXOXPXRXQXOXPJWP
      XBWQXCKXQXOWPXBUJXPXQXOUKZDEULZPLZXBWPXSYADERLXBDEUMDEUNUOXTWKDPEUPUQURUC
      XQXPWQXCUJXOXQXPUKZDFULZPLZXCWQYBYDDFRLXCDFUMDFUNUOYCWJDPFUPUQURUSUTVAEVB
      FVBDVBZUGVCWDWBXNXESWCWDXLXACXDWDWMXDIZUKXKWTUJZXLXASXQWMUIIZYGWDYFXQYHUK
      WNULWTXKDWMVDWNUPURYEYFWMHIYHWMXBXCVEWMVBVFVGABXKWTVHVFVIVJVKWOXLCWRWNVLI
      WHVLIZGVMWOXLSDWMPVNYIGNWGPVNVOAGBWNWHXKVLVLWGWNNPVPVQVRVSVTWA $.
      $( [?] $) $( [27-Nov-2005] $)
  $}

  ${
    $d j k m n x y F $.  $d j k m n x y M $.  $d j k n x y N $.
    $( The values of a finite real sequence have an upper bound.  _Warning:
       The HTML proof page is 1/2 megabyte in size_. $)
    fsequb $p |- ( ( N e. ( ZZ> ` M ) /\ A. k e. ( M ... N ) ( F ` k ) e. RR )
                 -> E. x e. RR A. k e. ( M ... N ) ( F ` k ) < x ) $=
      ( vn vj vm vy cuz cfv wcel cv cr cfz co wral clt wbr wrex wi c1 caddc
      wceq opreq2 raleq1d rexbidv imbi12d cz elfz3t fveq2 eleq1d rcla4v syl wa
      peano2re breq1d ltp1t syl5bir com12 elfz1eqt syl5 r19.21aiv jca a1i
      breq2 ralbidv rcla4ev syl6 syld wss fzssp1t eluzel2 eluzelz sylanc sseld
      imim1d r19.20dv2 imp32 rcla4va peano2uz eluzfz2t sylan adantrl cle cif
      wo wb elfzp1 adantr ad2antrr max2 ancoms ad2antlr ltletrt fzelp1t an1rs
      adantlr pm3.26 ifcl sylan2 syl3anc mpan2d ex a2d imp pm3.27 adantl
      max1ALT ltletrd jaod sylbid jctild exp32 com34 r19.23adv imp3a cbvrexv
      syl6ib mp2and uzind4 cbvralv sylan2b ) EDJKZLZFMZCKZNLZFDEOPZQZBMZCKZAMZR
      SZBYIQZANTZYLNLZBYIQYEYJYPYHFDGMZOPZQZYNBYSQZANTZUAYHFDDOPZQZYNBUUCQZANTZ
      UAYHFDHMZOPZQZYNBUUHQZANTZUAZYHFDUUGUBUCPZOPZQZYNBUUNQZANTZUAYJYPUAGHDEYR
      DUDZYTUUDUUBUUFUURYHFYSUUCYRDDOUEZUFUURUUAUUEANUURYNBYSUUCUUSUFUGUHYRUUGU
      DZYTUUIUUBUUKUUTYHFYSUUHYRUUGDOUEZUFUUTUUAUUJANUUTYNBYSUUHUVAUFUGUHYRUUMU
      DZYTUUOUUBUUQUVBYHFYSUUNYRUUMDOUEZUFUVBUUAUUPANUVBYNBYSUUNUVCUFUGUHYREUDZ
      YTYJUUBYPUVDYHFYSYIYREDOUEZUFUVDUUAYOANUVDYNBYSYIUVEUFUGUHDUILZUUDDCKZNLZ
      UUFUVFDUUCLUUDUVHUADUJYHUVHFDUUCYFDUDYGUVGNYFDCUKULUMUNUVFUVHUVGUBUCPZNLZ
      YLUVIRSZBUUCQZUOZUUFUVHUVMUAUVFUVHUVJUVLUVGUPUVHUVKBUUCUVHYKDUDZUVKYKUUCL
      UVNUVHUVKUVNUVKUVGUVIRSUVHUVNYLUVGUVIRYKDCUKUQUVGURUSUTYKDVAVBVCVDVEUUEUV
      LAUVINYMUVIUDYNUVKBUUCYMUVIYLRVFVGVHVIVJUUGYDLZUULUUOUUQUVOUULUUOUOUOUUKU
      UMCKZNLZUUQUVOUULUUOUUKUVOUUOUUIUUKUVOYHYHFUUNUUHUVOYFUUHLYFUUNLYHUVOUUHU
      UNYFUVFUUGUILUUHUUNVKUVODUUGVLDUUGVMDUUGVNVOVPVQVRVQVSUVOUUOUVQUULUUMUUNL
      ZUUOUVQUVOYHUVQFUUMUUNYFUUMUDYGUVPNYFUUMCUKULVTUVOUUMYDLUVRDUUGWADUUMWBUN
      WCWDUVOUUOUUKUVQUOZUUQUAUULUVOUUOUOZUVSYLIMZRSZBUUNQZINTZUUQUVTUUKUVQUWDU
      VTUUJUVQUWDUAANUVTYMNLZUVQUUJUWDUVTUWEUVQUUJUWDUAUVTUWEUVQUOZUOZUUJUVPUBU
      CPZYMWESZYMUWHWFZNLZYLUWJRSZBUUNQZUOUWDUWGUUJUWMUWKUWGYNUWLBUUHUUNUWGYKUU
      HLZYNUAZYKUUNLZUWLUAUWGUWOUOZUWPUWNYKUUMUDZWGZUWLUVTUWPUWSWHZUWFUWOUVOUWT
      UUOYKDUUGWIWJWKUWQUWNUWLUWRUWGUWOUWNUWLUAUWGUWNYNUWLUWGUWNYNUWLUAUWGUWNUO
      ZYNYMUWJWESZUWLUWFUXBUVTUWNUVQUWEUXBUWHNLZUWEUXBUVQUWHYMWLUVPUPZWCWMWNYQU
      WEUWKYNUXBUOUWLUAUXAYLYMUWJWOUVTUWNYQUWFUVOUWNUUOYQUWPUUOYQUVOUWNUOYHYQFY
      KUUNYFYKUDYGYLNYFYKCUKULVTYDYKDUUGWPWCWQWRUWFUWEUVTUWNUWEUVQWSWNUWFUWKUVT
      UWNUWEUXCUWKUVQUWIYMUWHNWTUXDXAZWNXBXCXDXEXFUWFUWRUWLUAUVTUWOUWRUWFUWLUWR
      UWLUVPUWJRSUWFUWRYLUVPUWJRYKUUMCUKUQUWFUVPUWHUWJUWEUVQXGUVQUXCUWEUXDXHUXE
      UVQUVPUWHRSUWEUVPURXHUVQUWHUWJWESZUWEUVQUXCUXFUXDUWHYMXIUNXHXJUSUTWNXKXLX
      DVRUWFUWKUVTUXEXHXMUWCUWMIUWJNUWAUWJUDUWBUWLBUUNUWAUWJYLRVFVGVHVIXNXOXPXQ
      UWCUUPIANUWAYMUDUWBYNBUUNUWAYMYLRVFVGXRXSWDXTXNYAXFYQYHBFYIYKYFUDYLYGNYKY
      FCUKULYBYC $.
      $( [3-Oct-2005] $) $( [19-Sep-2005] $)

    $( The values of a finite real sequence have an upper bound. $)
    fsequb2 $p |- ( ( N e. ( ZZ> ` M ) /\ F : ( M ... N ) --> RR ) ->
                  E. x e. RR A. y e. ran F y <_ x ) $=
      ( vk cuz cfv wcel cfz co cr wf wa cv clt wbr wral wrex cle crn fsequb
      ffvrn r19.21aiva sylan2 wi wceq wfn wb ffn fvelrn syl biimpa breq1
      biimpd r19.22si r19.36av adantll adantlr ltlet frn sseld imp simplr
      sylanc adantlll syld ex com23 r19.21adv r19.22dva mpd ) EDGHIZDEJKZLCMZNZ
      FOZCHZAOZPQZFVNRZALSZBOZVSTQZBCUAZRZALSVMVRLIZFVNRWBVOAFCDEUBVOWGFVNVNLVQ
      CUCUDUEVPWAWFALVPVSLIZNZWAWDBWEWIWCWEIZWAWDWIWJWAWDUFWIWJNWAWCVSPQZWDVPWJ
      WAWKUFZWHVOWJWLVMVOWJNZVTWKUFZFVNSZWLWMVRWCUGZFVNSZWOVOWJWQVOCVNUHWJWQUIV
      NLCUJFVNWCCUKULUMWPWNFVNWPVTWKVRWCVSPUNUOUPULVTWKFVNUQULURUSVOWHWJWKWDUFZ
      VMWCLIZWHWRVOWHNWJNWCVSUTVOWJWSWHVOWJWSVOWELWCVNLCVAVBVCUSVOWHWJVDVEVFVGV
      HVIVJVKVL $.
      $( [15-Oct-2006] $) $( [20-Sep-2005] $)

    $( The values of a finite real sequence have a supremum. $)
    fseqsupcl $p |- ( ( N e. ( ZZ> ` M ) /\ F : ( M ... N ) --> RR )
                 -> sup ( ran F , RR , < ) e. RR ) $=
      ( vy vx crn cr wss c0 wceq wn cv cle wbr wral wrex clt csup wcel cuz cfv
      cfz co wf wa suprcl frn adantl cdm eqeq1 biimpd dm0rn0 syl5ibr con3d n0i
      syl5com imp eluzfz1t fdm syl2an fsequb2 syl3anc ) AFZGHZVCIJZKZDLELMNDVCO
      EGPVCGQRGSCBTUASZBCUBUCZGAUDZUEEDVCUFVIVDVGVHGAUGUHBVHSZAUIZVHJZVFVGVIVJV
      LVFVLVHIJZKVFVJVLVEVMVLVKIJZVMVEVLVNVMVKVHIUJUKAULUMUNVHBUOUPUQBCURVHGAUS
      UTEDABCVAVB $.
      $( [?] $) $( [20-Sep-2005] $)

    fseqsupub.1 $e |- N e. V $.
    $( The values of a finite real sequence are bounded by their supremum. $)
    fseqsupub $p |- ( ( K e. ( M ... N ) /\ F : ( M ... N ) --> RR ) ->
               ( F ` K ) <_ sup ( ran F , RR , < ) ) $=
      ( vy vx crn cr wss c0 wceq wn cv cle wbr wral wrex w3a cfv wcel clt csup
      cfz co wf wa suprub frn adantl cdm eqeq1 biimpd dm0rn0 syl5ibr con3d n0i
      syl5com imp fdm sylan2 cuz fsequb2 cvv elfzuz2t mpan sylan 3jca wfn
      fnfvrn ancoms ffn sylanc ) AHZIJZVNKLZMZFNGNOPFVNQGIRZSBATZVNUAZVSVNIUBUC
      OPBCDUDUEZUAZWAIAUFZUGZGFVNVSUHWDVOVQVRWCVOWBWAIAUIUJWBAUKZWALZVQWCWBWFVQ
      WFWAKLZMVQWBWFVPWGWFWEKLZWGVPWFWHWGWEWAKULUMAUNUOUPWABUQURUSWAIAUTVADCVBT
      UAZWCVRWBGFACDVCDVDUAWBWIEVDBCDVEVFVGVHWBAWAVIZVTWCWJWBVTWABAVJVKWAIAVLVA
      VM $.
      $( [?] $) $( [20-Sep-2005] $)
  $}

