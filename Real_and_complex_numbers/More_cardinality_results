$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        More cardinality results
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The nonnegative integers are equinumerous to the natural numbers. $)
    nn0ennn $p |- NN0 ~~ NN $=
      ( vx vy cn0 cn cv c1 caddc co cmin nn0ex wcel cc elnn0nn pm3.27bd
      elnnnn0 wceq wb wa 1cn subaddt mp3an2 cleqcom 3bitr4g axaddcom mpan
      cleq2d adantl bitrd nncnt nn0cnt syl2an ancoms en3 ) ABCDAEZFGHZBEZFIHZJU
      NCKZUNLKZUODKUNMNUPDKZUPLKZUQCKUPONUTURUNUQPZUPUOPZQZVAUSVDUTURVAUSRZVBUP
      FUNGHZPZVCVEUQUNPZVFUPPZVBVGVAFLKZUSVHVIQSUPFUNTUAUNUQUBUPVFUBUCUSVGVCQVA
      USVFUOUPVJUSVFUOPSFUNUDUEUFUGUHUPUIUNUJUKULUM $.
      $( [?] $) $( [19-Jul-04] $)

    $( The set of natural numbers (as a subset of complex numbers) is
       equinumerous to omega (the set of finite ordinal numbers). $)
    nnenom $p |- NN ~~ om $=
      ( vy vx com cn0 cn nnex cv c1 caddc co wceq copab cc0 crdg cres wf1o cen
      wbr cleqid om2nn0f1o omex f1oen ax-mp nn0ennn entr2 ) CDEFCDAGBGHIJKBALMN
      COZPCDQRBAUFUFSTCDUFUAUBUCUDUE $.
      $( [?] $) $( [31-Jul-04] $)
  $}

  ${
    $d x y z w v u $.
    $( The cross product of the set of natural numbers with itself is
       equinumerous to the set of natural numbers. $)
    xpnnen $p |- ( NN X. NN ) ~~ NN $=
      ( vx vy vz vw vv vu cn cxp cdom wbr cen cvv wcel nnex xpex cv cint csn
      crn cuni caddc co c2 cexp cop wceq wa elxp5 nnaddclt cn0 2nn0 nnexpclt
      mpan2 syl pm3.27 sylan13 adantl sylbi wrex weq wb wi inteq inteqd visset
      op1st syl6eq sneq rneqd unieqd op2nda opreq12d opreq1d cleqan12d
      nn0opth2t nnnn0t im2an syl2an sylan9bbr cleq12 opth syl6bb bitr4d exp43
      com23 r19.23aivv r19.23advv imp elxp2 syl2anb dom2 ax-mp c1 1nn elisseti
      xpsnen ensymi wss ssid snssi ssxp mp2an ssdom2g mp2 endomtr sbth ) GGHZGI
      JZGXGIJZXGGKJXGLMZXHGGNNOZABXGGAPZQZQZXLRZSZTZUAUBZUCUDUBZXQUAUBZBPZQZQZY
      ARZSZTZUAUBZUCUDUBZYFUAUBZLXLXGMZXLXNXQUEUFZXNGMZXQGMZUGZUGXTGMZXLGGUHYNY
      OYKXSGMZYMYOYNXSXQUIYNXRGMZYPXNXQUIYQUCUJMYPUKXRUCULUMUNYLYMUOUPUQURXLCPZ
      DPZUEZUFZDGUSCGUSZYAEPZFPZUEZUFZFGUSEGUSZXTYIUFZABUTZVAZYJYAXGMUVBUVGUVJU
      VBUVFUVJEFGGUVAUVCGMZUVDGMZUGZUVFUVJVBZVBCDGGYRGMZYSGMZUGZUVMUVAUVNUVQUVM
      UVAUVFUVJUVQUVMUGZUVAUVFUGZUGUVHCEUTDFUTUGZUVIUVSUVHYRYSUAUBZUCUDUBZYSUAU
      BZUVCUVDUAUBZUCUDUBZUVDUAUBZUFZUVRUVTUVAUVFXTUWCYIUWFUVAXSUWBXQYSUAUVAXRU
      WAUCUDUVAXNYRXQYSUAUVAXNYTQZQYRUVAXMUWHXLYTVCVDYRYSCVEZVFVGUVAXQYTRZSZTYS
      UVAXPUWKUVAXOUWJXLYTVHVIVJYRYSUWIDVEZVKVGZVLVMUWMVLUVFYHUWEYFUVDUAUVFYGUW
      DUCUDUVFYCUVCYFUVDUAUVFYCUVEQZQUVCUVFYBUWNYAUVEVCVDUVCUVDEVEZVFVGUVFYFUVE
      RZSZTUVDUVFYEUWQUVFYDUWPYAUVEVHVIVJUVCUVDUWOFVEZVKVGZVLVMUWSVLVNYRUJMZYSU
      JMZUGUVCUJMZUVDUJMZUGUWGUVTVAUVQUVMYRYSUVCUVDVOUVOUWTUVPUXAYRVPYSVPVQUVKU
      XBUVLUXCUVCVPUVDVPVQVRVSUVSUVIUVTVAUVRUVSUVIYTUVEUFUVTXLYTYAUVEVTYRYSUVCU
      VDUWIUWLUWRWAWBUQWCWDWEWFWGWHCDXLGGWIEFYAGGWIWJWKWLGGWMRZHZKJUXEXGIJZXIUX
      EGNGWMNWMGWNWOWPWQXJUXEXGWRZUXFXKGGWRUXDGWRZUXGGWSWMGMUXHWNWMGWTWLGGUXDGX
      AXBUXEXGLXCXDGUXEXGXEXBXGGXFXB $.
      $( [?] $) $( [1-Aug-04] $)
  $}

  $( The cross product of omega (the set of ordinal natural numbers) with
     itself is equinumerous to omega.  Exercise 1 of [Enderton] p. 133 (which
     proves this with a direct, but longer, proof; ours uses instead the
     Schroeder-Bernstein Theorem ~ sbth in ~ xpnnen ). $)
  xpomen $p |- ( om X. om ) ~~ om $=
    ( com cxp cn omex xpex cen wbr nnenom nnex xpen mp2an xpnnen entr3 entr )
    AABZCACCBZOCAADDECAFGZQPOFGHHCACAIDIDJKLMHN $.
    $( [?] $) $( [23-Jul-04] $)

  $( Lemma for ~ znnen . $)
  znnenlem $p |- ( ( ( 0 <_ x /\ -. 0 <_ y ) /\ ( x e. ZZ /\ y e. ZZ ) ) ->
                  ( y = x <-> ( 2 x. x ) = ( ( -u 2 x. y ) + 1 ) ) ) $=
    ( weq wn c2 cv cmulc co cneg c1 caddc wceq wb cc0 cle wbr wa cz wcel
    pm5.21 cr wi clt ax0re leltt mpan bicon2d adantr ltletrt mp3an2 exp3a
    sylbird com23 imp3a ltnet syld ancoms zret syl2an imp zneo znegclt sylan5
    cc zcnt 2cn mulneg12t syl adantl opreq1d cleq2d mtbird sylan13 ) BACZDZEAFZ
    GHZEIBFZGHZJKHZLZDZVNWAMNVPOPZNVROPZDZQZVPRSZVRRSZQZQVNWATWIWFVOWIWFVOVPUAS
    ZVRUASZWFVOUBZWGWHWKWJWLWKWJQZWFVRVPUCPZVOWMWCWEWNWMWEWCWNWMWEVRNUCPZWCWNUB
    WKWOWEMWJWKWDWONUASZWKWDWODMUDNVRUEUFUGUHWMWOWCWNWKWPWJWOWCQWNUBUDVRNVPUIUJ
    UKULUMUNVRVPUOUPUQVPURVRURUSUTUQWIWBWFWIWAVQEVRIZGHZJKHZLZWGWQRSWTDWHVPWQVA
    VRVBVCWIVTWSVQWIVSWRJKWHVSWRLZWGWHVRVDSZXAVRVEEVDSXBXAVFEVRVGUFVHVIVJVKVLVI
    VM $.
    $( [?] $) $( [31-Jul-04] $)

  ${
    $d x y $.
    $( The set of integers and the set of natural numbers are equinumerous. $)
    znnen $p |- ZZ ~~ NN $=
      ( vx vy cz cn0 cn cdom wbr cen cvv wcel zex cc0 cv cle c2 cmulc co cneg
      c1 caddc cded wa elnn0z 2nn0 nn0mulclt mpan sylbir wb dedlem1 eleq1d
      adantl mpbird exp wn 2z znegclt ax-mp zmulclt adantr cr wi zret clt
      negrclt 2pos 2re axmulgt0 mpani syl ax0re leltt bicon2d lt0neg1t bitr3d
      cc wceq recnt 2cn mulneg12t breq2d 3imtr4d negrcl axmulrcl ltlet syld
      imp jca biimpr peano2nn0 3syl dedlem2 pm2.61d weq cleqan12d gt0ne0i
      mulcant mp3an1 zcnt syl2an sylan9bb bicomd znnenlem eqcomb syl5bb bitr4d
      ancom syl2anb cleqcom syl6bbr 1cn addcan2t mp3an3 negcl axmulcl negn0
      bitr2d sylan9bbr 4cases dom2 wss nn0ex nn0ssz ssdomg mp2 sbth mp2an
      nn0ennn entr ) CDECDFGZDCFGZCDHGCIJYIKABCDLAMZNGZOYKPQZORZYKPQZSTQZUAZLBM
      ZNGZOYRPQZYNYRPQZSTQZUAZIYKCJZYLYQDJZUVDYLUVEUVDYLUBZUVEYMDJZUVFYKDJZUVGY
      KUCODJUVHUVGUDOYKUEUFUGYLUVEUVGUHUVDYLYQYMDYLYMYPUIZUJUKULUMUVDYLUNZUVEUV
      DUVJUBZUVEYPDJZUVKYOCJZLYONGZUBZYODJZUVLUVKUVMUVNUVDUVMUVJYNCJZUVDUVMOCJU
      VQUOOUPUQYNYKURUFUSUVDUVJUVNUVDYKUTJZUVJUVNVAYKVBUVRUVJLYOVCGZUVNUVRLYKRZ
      VCGZLOUVTPQZVCGZUVJUVSUVRUVTUTJZUWAUWCVAYKVDUWDLOVCGZUWAUWCVEOUTJUWDUWEUW
      AUBUWCVAVFOUVTVGUFVHVIUVRYKLVCGZUVJUWAUVRYLUWFLUTJZUVRYLUWFUNUHVJLYKVKUFV
      LYKVMVNUVRYOUWBLVCUVRYKVOJZYOUWBVPZYKVQOVOJZUWHUWIVROYKVSUFVIVTWAUVRYOUTJ
      ZUVSUVNVAZYNUTJUVRUWKOVFWBYNYKWCUFUWGUWKUWLVJLYOWDUFVIWEVIWFWGUVPUVOYOUCW
      HYOWIWJUVJUVEUVLUHUVDUVJYQYPDYLYMYPWKZUJUKULUMWLUVDYRCJZUBZABWMZYQUVCVPZY
      LYSUWOUWPUWQUHZVAYLYSUBZUWOUWRUWSUWOUBUWQUWPUWSUWQYMYTVPZUWOUWPYLYSYQYMUV
      CYTUVIYSYTUVBUIZWNUWHYRVOJZUWTUWPUHZUVDUWNUWJUWHUXBUXCVROYKYROVFVEWOZWPWQ
      YKWRZYRWRZWSWTXAUMYLYSUNZUBZUWOUWRUXHUWOUBZUWPYMUVBVPZUWQUXIBAWMUXJUWPABX
      BABXCXDUXHUWQUXJUHUWOYLUXGYQYMUVCUVBUVIYSYTUVBWKZWNUSXEUMUVJYSUBZUWOUWRUX
      LUWOUBZUWPYPYTVPZUWQUXMUWPYTYPVPZUXNYSUVJUBUWNUVDUBUWPUXOUHUXLUWOBAXBUVJY
      SXFUVDUWNXFXGYPYTXHXIUXLUWQUXNUHUWOUVJYSYQYPUVCYTUWMUXAWNUSXEUMUVJUXGUBZU
      WOUWRUWOUWPYPUVBVPZUXPUWQUWHUXBUWPUXQUHUVDUWNUWHUXBUBUXQYOUVAVPZUWPYOVOJZ
      UVAVOJZUXQUXRUHZUWHUXBUXSUXTSVOJUYAXJYOUVASXKXLYNVOJZUWHUXSOVRXMZYNYKXNUF
      UYBUXBUXTUYCYNYRXNUFWSUYBUWHUXBUXRUWPUHUYCYNYKYROVRUXDXOWPWQXPUXEUXFWSUXP
      UWQUXQUVJUXGYQYPUVCUVBUWMUXKWNXAXQUMXRXAXSUQDIJDCXTYJYAYBDCIYCYDCDYEYFYGY
      H $.
      $( [?] $) $( [31-Jul-04] $)
  $}

  ${
    $d x y z w $.
    $( The set of rationals and the set of nonnegative integers are
       equinumerous.  (We use the Axiom of Choice via ~ fodom to give us a
       shorter proof, but this theorem can also be proved without it.  See,
       for example, Exercise 2 of [Enderton] p. 133.) $)
    qnnen $p |- QQ ~~ NN $=
      ( vx vy vz vw cq cn cdom wbr cen cz cxp cv wcel wa cdiv co wceq copab2
      wfo wfn crn oprex cleqid fnoprab2 wrex elrnoprab elq bitr4 cleqri pm3.2i
      df-fo mpbir zex nnex xpex fodom ax-mp znnen enref xpen mp2an xpnnen entr
      domentr cvv wss nnssq ssdomg mp2 sbth ) EFGHZFEGHZEFIHEJFKZGHZVMFIHVKVMEA
      LZJMBLZFMNCLVOVPOPZQNABCRZSZVNVSVRVMTZVRUAZEQZNVTWBABCJFVQVRVOVPOUBZVRUCZ
      UDDWAEDLZWAMWEVQQBFUEAJUEWEEMABCJFVQWEVRWCWDUFABWEUGUHUIUJVMEVRUKULVMEVRJ
      FUMUNUOUPUQVMFFKZFJFIHFFIHVMWFIHURFUNUSJFFFUMUNUNUNUTVAVBVCEVMFVDVAFVEMFE
      VFVLUNVGFEVEVHVIEFVJVA $.
      $( [?] $) $( [31-Jul-04] $)
  $}

  $( Lemma for ~ infxp .  An infinite set is not empty. $)
  infxplem1 $p |- ( om ~<_ x -> -. x = (/) ) $=
    ( com cv cdom wbr c0 csdm wceq wn wcel peano1 nnsdom ax-mp cvv wa wi
    visset sdomdomtr mpan 0sdom sylib ) BACZDEZFUBGEZUBFHIFBGEZUCUDFBJUEKFLMUBN
    JUEUCOUDPAQZFBUBNRMSUBUFTUA $.
    $( [?] $) $( [15-Sep-04] $)

  $( Lemma for ~ infxp .  An infinite set strictly dominates ordinal one. $)
  infxplem2 $p |- ( om ~<_ x -> 1o ~< x ) $=
    ( c1o com csdm wbr cv cdom wcel 1onn nnsdom ax-mp cvv wa wi visset
    sdomdomtr mpan ) BCDEZCAFZGEZBSDEZBCHRIBJKSLHRTMUANAOBCSLPKQ $.
    $( [?] $) $( [17-Sep-04] $)

  ${
    infxplem3.1 $e |- A e. V $.
    infxplem3.2 $e |- B e. V $.
    $( Lemma for ~ infxp .  An infinite idempotent set ` x ` is equinumerous
       to the union of any two sets ` A ` and ` B ` equinumerous to it. $)
    infxplem3 $p |- ( ( om ~<_ x /\ x ~~ ( x X. x ) ) ->
                     ( ( x ~~ A /\ x ~~ B ) -> x ~~ ( A u. B ) ) ) $=
      ( c1o cv csdm wbr cxp cen wa cun wi com cdom sbth cvv wcel wss ssun1
      ssdomg mp2 endomtr mpan2 ad2antrl domentr unxpdom sdomentr ax-mp syl2an
      anandis adantlr entrt xpex ensym syl visset xpen sylan5 adantll sylan13
      exp infxplem2 sylan ) FAGZHIZVFVFVFJZKIZVFBKIZVFCKIZLZVFBCMZKIZNOVFPIVGVI
      LZVLVNVFVMPIZVMVFPIZVNVOVLLZVFVMQVJVPVOVKVJBVMPIZVPBRSZBVMTVSDBCUABVMRUBU
      CVFBVMUDUEUFVMBCJZPIZWAVFKIZVQVRVMWAVFUGVGVLWBVIVGVJVKWBFBHIZFCHIZWBVGVJL
      ZVGVKLZBCUHVTWFWDNDFVFBRUIUJCRSWGWENEFVFCRUIUJUKULUMVIVLWCVGVIVHWAKIZWCVL
      VIWHLVFWAKIWCVFVHWAUNVFWABCDEUOUPUQVFBVFCAURZDWIEUSUTVAVBVBVCAVDVE $.
      $( [?] $) $( [15-Sep-04] $)
  $}

  ${
    $d x y z w f g h v u t A $.  $d x y z w v f g h t B $.
    $d x y z w v f g h t C $.  $d x y z w v u g h H $.
    infxplem.1 $e |- H = { f | ( f = (/) \/ E. t ( ( om ~<_ t /\ t (_ A ) /\
                      f : ( t X. t ) -1-1-onto-> t ) ) } $.
    ${
      infxplem4.2 $e |- B e. V $.
      $( Lemma for ~ infxp .  A necessary and sufficient condition for a
         set ` B ` to belong to ` H ` . $)
      infxplem4 $p |- ( B e. H <-> ( B = (/) \/
        E. x ( ( om ~<_ x /\ x (_ A ) /\ B : ( x X. x ) -1-1-onto-> x ) ) ) $=
        ( wcel c0 wceq com cv cdom wbr wss wa cxp wf1o wex wo cleq1 f1oeq1
        biland biexdv bi2ord elab2 weq breq2 sseq1 bi2and wb xpeq1 xpeq2 eqtrd
        f1oeq2 syl f1oeq3 bitrd cbvexv bilor bitr4 ) DFIDJKZLBMZNOZVDCPZQZVDVDR
        ZVDDSZQZBTZUAZVCLAMZNOZVMCPZQZVMVMRZVMDSZQZATZUAEMZJKZVGVHVDWASZQZBTZUA
        VLEDFHWADKZWBVCWEVKWADJUBWFWDVJBWFWCVIVGVHVDWADUCUDUEUFGUGVTVKVCVSVJABA
        BUHZVPVGVRVIWGVNVEVOVFVMVDLNUIVMVDCUJUKWGVRVHVMDSZVIWGVQVHKVRWHULWGVQVD
        VMRVHVMVDVMUMVMVDVDUNUOVQVHVMDUPUQVMVDVHDURUSUKUTVAVB $.
        $( [?] $) $( [16-Sep-04] $)

      ${
        $d x D $.
        infxplem5.3 $e |- D e. V $.
        $( Lemma for ~ infxp .  A sufficient condition for a set ` B ` to
           belong to ` H ` . $)
        infxplem5 $p |- ( ( ( om ~<_ D /\ D (_ A ) /\
                         B : ( D X. D ) -1-1-onto-> D ) -> B e. H ) $=
          ( vx com cdom wbr wss wa cxp wf1o cv wex wcel wceq breq2 sseq1
          bi2and wb xpeq1 xpeq2 eqtrd f1oeq2 syl f1oeq3 bitrd cla4ev c0 wo olc
          infxplem4 sylibr ) KDLMZDBNZOZDDPZDCQZOZKJRZLMZVEBNZOZVEVEPZVECQZOZJS
          ZCFTZVKVDJDIVEDUAZVHVAVJVCVNVFUSVGUTVEDKLUBVEDBUCUDVNVJVBVECQZVCVNVIV
          BUAVJVOUEVNVIDVEPVBVEDVEUFVEDDUGUHVIVBVECUIUJVEDVBCUKULUDUMVLCUNUAZVL
          UOVMVLVPUPJABCEFGHUQURUJ $.
          $( [?] $) $( [16-Sep-04] $)
      $}
    $}

    $( Lemma for ~ infxp .  The domain of a member of ` H` is the cross
       product of its range. $)
    infxplem6 $p |- ( g e. H -> dom g = ( ran g X. ran g ) ) $=
      ( vx cv wcel c0 wceq com cdom wbr wss wa cxp wf1o wex wo cdm crn visset
      infxplem4 dmeq dm0 syl6eq rneq rn0 xpeq2 syl xp0r eqtr4d wfn ccnv wfun
      w3a f1o2 fndm xpeq1 eqtr2d sylan9eq 3adant2 sylbi adantl 19.23aiv jaoi )
      DHZEIVHJKZLGHZMNVJBOPZVJVJQZVJVHRZPZGSZTVHUAZVHUBZVQQZKZGABVHCEFDUCUDVIVS
      VOVIVPJVRVIVPJUAJVHJUEUFUGVIVRVQJQZJVIVQJKVRVTKVIVQJUBJVHJUHUIUGVQJVQUJUK
      VQULUGUMVNVSGVMVSVKVMVHVLUNZVHUOUPZVQVJKZUQVSVLVJVHURWAWCVSWBWAWCVPVLVRVL
      VHUSWCVRVJVQQVLVQVJVQUTVQVJVJUJVAVBVCVDVEVFVGVD $.
      $( [?] $) $( [11-Sep-04] $)

    $( Lemma for ~ infxp .  Two members in the range of a member of a subset
       of ` H ` form an ordered pair belonging to the domain of the union
       of the subset. $)
    infxplem7 $p |- ( ( C (_ H /\ g e. C ) ->
             ( ( y e. ran g /\ z e. ran g ) -> <. y , z >. e. dom U. C ) ) $=
      ( wss cv wcel wa crn cop cdm cuni cxp wceq ssel2 infxplem6 syl eleq2d
      visset opelxp syl6bb wi ciun ssiun2 dmuni syl6ssr sseld adantl sylbird )
      EHJZGKZELZMZAKZUPNZLBKZUTLMZUSVAOZUPPZLZVCEQPZLZURVEVCUTUTRZLVBURVDVHVCUR
      UPHLVDVHSEHUPTCDFGHIUAUBUCUSVAUTUTBUDUEUFUQVEVGUGUOUQVDVFVCUQVDGEVDUHVFGE
      VDUIGEUJUKULUMUN $.
      $( [?] $) $( [11-Sep-04] $)

    ${
      infxplem8.2 $e |- B = ran U. C $.
      $( Lemma for ~ infxp .  A simple but frequently used fact. $)
      infxplem8 $p |- ( y e. B <-> E. g e. C y e. ran g ) $=
        ( cv wcel crn ciun wrex cuni rnuni eqtr eleq2i eliun bitr ) AKZDLUBGEGK
        MZNZLUBUCLGEODUDUBDEPMUDJGEQRSGUBEUCTUA $.
        $( [?] $) $( [12-Sep-04] $)

      $( Lemma for ~ infxp .  The union of a collection ` C ` of chains in
         ` H ` is a bijection. $)
      infxplem9 $p |- ( ( C (_ H /\ A. g e. C A. h e. C ( g (_ h \/ h (_ g ) )
                      -> U. C : ( B X. B ) -1-1-onto-> B ) $=
        ( vx vy vz vw vv wss cv wo wral wa cuni cxp wfn ccnv wfun crn wceq
        wf1o cdm r19.26 fun11uni sylbir wcel ssel c0 com cdom wbr wex visset
        infxplem4 wf1 f10 f1eq1 mpbiri wf df-f1 ffun imran sylbi syl f1of1
        adantl 19.23aiv jaoi syl6 r19.21aiv sylan wrex wi ssel2 infxplem6 ra4e
        infxplem8 sylibr exp ssrdv ssxp anidms eqsstrd sseld r19.23adv ciun
        dmuni eleq2i eliun bitr syl5ib adantr wrel relxp a1i cop weq sseq1
        sseq2 bi2ord rcla42v com12 rnss imrand infxplem7 syl9r adantrl imland
        adantrr jaod com3l syld com13 imp r19.23advv bi2an opelxp reeanv
        3bitr4 relssdv eqssd jca an23 df-fn biran bitr4 sylib cleqcomi jctir
        w3a f1o2 df-3an ) DHPZFQZGQZPZUVBUVAPZRZGDSZFDSZTZDUAZCCUBZUCZUVIUDUEZT
        ZUVIUFZCUGZTZUVJCUVIUHZUVHUVMUVOUVHUVIUEZUVLTZUVIUIZUVJUGZTZUVMUVHUVSUW
        AUVAUEZUVAUDUEZTZFDSZUVGUVSYTUWFUVGTUWEUVFTFDSUVSUWEUVFFDUJDFGUKULYTUWE
        FDYTUVADUMZUVAHUMZUWEDHUVAUNUWHUVAUOUGZUPKQZUQURUWJBPTZUWJUWJUBZUWJUVAU
        HZTZKUSZRUWEKABUVAEHIFUTVAUWIUWEUWOUWIUOUOUVAVBZUWEUWIUWPUOUOUOVBUOVCUO
        UOUVAUOVDVEUWPUOUOUVAVFZUWDTUWEUOUOUVAVGUWQUWCUWDUOUOUVAVHVIVJVKUWNUWEK
        UWMUWEUWKUWMUWLUWJUVAVBZUWEUWLUWJUVAVLUWRUWLUWJUVAVFZUWDTUWEUWLUWJUVAVG
        UWSUWCUWDUWLUWJUVAVHVIVJVKVMVNVOVJVPVQVRUVHUVTUVJYTUVTUVJPUVGYTLUVTUVJY
        TLQZUVAUIZUMZFDVSZUWTUVJUMZUWTUVTUMZYTUXBUXDFDYTUWGUXBUXDVTYTUWGTZUXAUV
        JUWTUXFUXAUVAUFZUXGUBZUVJUXFUWHUXAUXHUGDHUVAWAABEFHIWBVKUWGUXHUVJPZYTUW
        GUXGCPZUXIUWGLUXGCUWGUWTUXGUMZUWTCUMZUWGUXKTUXKFDVSUXLUXKFDWCLABCDEFHIJ
        WDWEWFWGUXJUXIUXGCUXGCWHWIVKVMWJWKWFWLUXEUWTFDUXAWMZUMUXCUVTUXMUWTFDWNW
        OFUWTDUXAWPWQWRWGWSUVHLMUVJUVTUVJWTUVHCCXAXBUVHUWTNQZUFZUMZMQZOQZUFZUMZ
        TZODVSNDVSZUWTUXQXCZUVTUMZUYCUVJUMZUVHUYAUYDNODDYTUVGUXNDUMZUXRDUMZTZUY
        AUYDVTZVTUYHUVGYTUYIUYHUVGUXNUXRPZUXRUXNPZRZYTUYIVTUVGUYHUYLUVEUYLUXNUV
        BPZUVBUXNPZRFGUXNUXRDDFNXDUVCUYMUVDUYNUVAUXNUVBXEUVAUXNUVBXFXGGOXDUYMUY
        JUYNUYKUVBUXRUXNXFUVBUXRUXNXEXGXHXIYTUYHUYLUYIYTUYHUYLUYIVTYTUYHTUYJUYI
        UYKYTUYGUYJUYIVTUYFUYJUYAUWTUXSUMZUXTTYTUYGTUYDUYJUXPUYOUXTUYJUXOUXSUWT
        UXNUXRXJWKXKLMABDEOHIXLXMXNYTUYFUYKUYIVTUYGUYKUYAUXPUXQUXOUMZTYTUYFTUYD
        UYKUXTUYPUXPUYKUXSUXOUXQUXRUXNXJWKXOLMABDENHIXLXMXPXQWFXRXSXTYAYBUXLUXQ
        CUMZTUXPNDVSZUXTODVSZTUYEUYBUXLUYRUYQUYSLABCDENHIJWDMABCDEOHIJWDYCUWTUX
        QCCMUTYDUXPUXTNODDYEYFWRYGYHYIUWBUVRUWATZUVLTUVMUVRUVLUWAYJUVKUYTUVLUVI
        UVJYKYLYMYNCUVNJYOYPUVQUVKUVLUVOYQUVPUVJCUVIYRUVKUVLUVOYSWQWE $.
        $( [?] $) $( [11-Sep-04] $)

      ${
        infxplem10.3 $e |- C e. V $.
        $( Lemma for ~ infxp .  The union of a collection of chains ` C ` in
           the collection of bijections ` H ` belongs to ` H ` .  This property
           will be needed to apply Zorn's Lemma in ~ infxplem11 . $)
        infxplem10 $p |- ( ( C (_ H /\
                   A. g e. C A. h e. C ( g (_ h \/ h (_ g ) ) -> U. C e. H ) $=
          ( vx vy wss cv wo wral wa cuni c0 wceq wcel wn com cdom wbr cxp wf1o
          wi wrex crn ssel2 wex visset infxplem4 biimp ord wfo f1ofo forn syl
          cleqcomd imran ancoms 19.22i cvv rnexg ax-mp breq2 sseq1 bi2and
          ceqsexv sylib syl6 domtr ra4e infxplem8 sylibr exp ssrdv ssdomg
          sylan5 com12 adantl adantrd syld r19.23adv csn uni0b dfss3 elsn
          biral 3bitr bineg rexnal bitr4 syl5ib pm3.27 rneq rn0 syl6eq 0ss a1i
          eqsstrd pm2.61d2 sseld a1d jcad adantr infxplem9 uniex eqeltr
          infxplem5 orc ) DHNZFOZGOZNXQXPNPGDQFDQZRZDSZTUAZXTHUBZXSYAUCZUDCUEUF
          ZCBNZRZCCUGCXTUHZRYBXSYCYFYGXOYCYFUIXRXOYCYDYEXOXPTUAZUCZFDUJZYDYCXOY
          IYDFDXOXPDUBZYIYDUIXOYKRZYIUDXPUKZUEUFZYMBNZRZYDYLXPHUBZYIYPUIDHXPULY
          QYIUDLOZUEUFZYRBNZRZYRYRUGZYRXPUHZRZLUMZYPYQYHUVEYQYHUVEPLABXPEHIFUNZ
          UOUPUQUVEYRYMUAZUVARZLUMYPUVDUVHLUVCUVAUVHUVCUVGUVAUVCYMYRUVCUVBYRXPU
          RYMYRUAUVBYRXPUSUVBYRXPUTVAVBVCVDVEUVAYPLYMXPVFUBYMVFUBZUVFXPVFVGVHZU
          VGYSYNYTYOYRYMUDUEVIYRYMBVJVKVLVMVNVAZYLYNYDYOYKYNYDUIXOYNYKYDYNYKYDY
          NYMCUEUFZYDYKUDYMCVOYKYMCNZUVLYKMYMCYKMOZYMUBZUVNCUBZYKUVORUVOFDUJZUV
          PUVOFDVPMABCDEFHIJVQZVRVSVTUVIUVMUVLUIUVJYMCVFWAVHVAWBVSWCWDWEWFVSWGY
          CYHFDQZUCYJYAUVSYADTWHZNXPUVTUBZFDQUVSDWIFDUVTWJUWAYHFDFTWKWLWMWNYHFD
          WOWPWQXOYEYCXOMCBXOUVQUVNBUBZUVPXOUVOUWBFDXOYKUVOUWBUIYLYMBUVNYLYHYOY
          LYIYPYOUVKYNYOWRVNYHYMTBYHYMTUKTXPTWSWTXATBNYHBXBXCXDXEXFVSWGUVRWQVTX
          GXHXIXSYGYCABCDEFGHIJXJXGXHABXTCEHIDKXKZCXTUKZVFJXTVFUBUWDVFUBUWCXTVF
          VGVHXLXMVNYAYAUVAUVBYRXTUHRLUMZPYBYAUWEXNLABXTEHIUWCUOVRXE $.
          $( [?] $) $( [12-Sep-04] $)
      $}
    $}

    ${
      infxplem.2 $e |- A e. V $.
      $( Lemma for ~ infxp .  By Zorn's Lemma ~ zorn2 , the collection ` H `
         (which we show here to be a set) has a maximal element. $)
      infxplem11 $p |- E. g e. H A. h e. H -. g (. h $=
        ( vz cv wss wo wral wa cuni wcel wi wpss wn wrex c0 wceq cab com cdom
        wbr cxp wf1o wex cun cvv unab eqtr4 csn df-sn p0ex eqeltrr cpw df-rex
        visset elpw biran ancom an23 3bitr biex bitr biabi pwex wf xpex mapex
        mp2an f1of adantl ss2abi ssexi abrexex2 unex eqeltr zorn2 crn cleqid
        infxplem10 mpg ) IJZFKDJZEJZKWHWGKLEWFMDWFMNWFOZFPQWGWHRSEFMDFTIDEIFFCJ
        ZUAUBZCUCZUDAJZUEUFZWMBKZNWMWMUGZWMWJUHZNZAUIZCUCZUJZUKFWKWSLCUCXAGWKWS
        CULUMWLWTUAUNWLUKCUAUOUPUQWNWQNZABURZTZCUCWTUKXDWSCXDWMXCPZXBNZAUIWSXBA
        XCUSXFWRAXFWOXBNXBWONWRXEWOXBWMBAUTZVAVBWOXBVCWNWQWOVDVEVFVGVHXBACXCBHV
        IXBCUCWPWMWJVJZCUCZWPUKPWMUKPXIUKPWMWMXGXGVKXGWPWMUKUKCVLVMXBXHCWQXHWNW
        PWMWJVNVOVPVQVRUQVSVTWAABWIWBZWFCDEFGXJWCIUTWDWE $.
        $( [?] $) $( [12-Sep-04] $)

      $( Lemma for ~ infxp .  A maximal bijection ` g ` in ` H ` is
         non-empty. $)
      infxplem12 $p |- ( A. h e. H -. g (. h -> ( om ~<_ A ->
                        -. g = (/) ) ) $=
        ( vv vy cv wpss wn wral c0 wceq wrex com cdom wbr wcel wi wa psseq1
        0pss syl6bb bicon2d weq psseq2 binegd rcla4v imp syl5bir com12 con3d
        exp r19.23adv cen wss wex cxp wf1o visset infxplem5 crn wfo f1ofo forn
        syl cleqcomd rneq rn0 syl6eq sylan9eq infxplem1 nsyli adantr jcad
        19.22dv an1rs endom omex xpen anidms xpomen cvv wb xpex enen1 mpan
        mpbii enen2 mpbid bren sylib jca sylan 19.23aiv domen df-rex 3imtr4
        syl5 ) DKZEKZLZMZEFNZIKZOPZMZIFQZXCOPZMZRBSTZXGXJXMIFXGXHFUAZXJXMUBXGXO
        UCZXLXIXLXPXIXLXIXCXHLZMZXPXLXQXIXLXQOXHLXJXCOXHUDXHUEUFUGXGXOXRXFXREXH
        FEIUHXEXQXDXHXCUIUJUKULUMUNUOUPUQRJKZURTZXSBUSZUCZJUTXOXJUCZIUTZXNXKYBY
        DJRXSSTZXSXSVAZXSXHVBZIUTZUCYAYDXTYEYAYHYDYEYAUCZYHYDYIYGYCIYIYGXOXJYIY
        GXOABXHXSCFGIVCJVCZVDUPYEYGXJUBYAYGYEXJYGXIXSOPZYEYGXIYKYGXIXSXHVEZOYGY
        LXSYGYFXSXHVFYLXSPYFXSXHVGYFXSXHVHVIVJXIYLOVEOXHOVKVLVMVNUPJVOVPUNVQVRV
        SULVTXTYEYHRXSWAXTYFXSURTZYHXTYFRURTZYMXTRRVAZYFURTZYNXTYPRXSRXSWBYJWBY
        JWCWDYPYORURTZYNWEYFWFUAYPYQYNWGXSXSYJYJWHYOYFRWFWIWJWKVIXSWFUAXTYNYMWG
        YJRXSYFWFWLWJWMYFXSIYJWNWOWPWQWRJRBHWSXJIFWTXAXB $.
        $( [?] $) $( [16-Sep-04] $)

      $( Lemma for ~ infxp .  We show that the union of a bijection ` g ` in
         ` H ` with a disjoint bijection ` u ` is a member ` h ` of ` H ` that
         is larger than (properly extends) ` g ` .  Thus if the antecedent is
         true then ` g ` cannot be maximal. $)
      infxplem13 $p |- ( ( ( ( ( om ~<_ x /\ x (_ A ) /\
            g : ( x X. x ) -1-1-onto-> x ) /\ ( x ~~ y /\ y (_ ( A \ x ) ) ) /\
          u : ( ( x X. y ) u. ( ( y X. x ) u. ( y X. y ) ) ) -1-1-onto-> y ) ->
                        E. h e. H g (. h ) $=
        ( com cv cdom wbr wss wa cxp wf1o cen cdif cun wpss wrex wcel psseq2
        rcla4ev cin c0 wceq f1oun xpdisj2 xpdisj1 jca undisj2 sylib ancri
        sylan5 wb xpun unass eqtr f1oeq2 ax-mp sylibr sslin indif0 syl6ss ss0b
        adantll wi visset unex infxplem5 cvv ssun1 ssdomg mp2 domtr mpan2
        difss sstr2 imlan unss im2an anassrs sylan exp adantlr mpd adantrl wn
        disjpss crn ineq12 cleq1d wfo f1ofo forn syl syl2an wrel f1orel relin
        relrn0 rnin sseq2 mpbii ss0 syl5bir 3syl adantr sylbird imp bitrd
        binegd infxplem1 domentr syl5 sylan13 exp43 com4t com23 imp43 exp42
        com34 imp41 ) LAMZNOZYHEPZQZYHYHRZYHGMZSZYHBMZTOZYOEYHUAZPZQZYHYORZYOYH
        RZYOYORZUBZUBZYOCMZSZYMHMZUCZHIUDZYKYNUVFYSUVIYKYNUVFYSUVIYMUVEUBZIUEZY
        MUVJUCZUVIYKYNUVFQZQZYSQUVHUVLHUVJIUVGUVJYMUFUGUVNYRUVKYPUVNYRQYHYOUBZU
        VORZUVOUVJSZUVKUVMYRUVQYKUVMYHYOUHZUIUJZUVQYRUVMUVSQZYLUVDUBZUVOUVJSZUV
        QUVMYLUVDUHUIUJZUVSQUWBUVSYLYHUVDYOYMUVEUKUVSUWCUVSYLYTUHUIUJZYLUVCUHUI
        UJZQUWCUVSUWDUWEYHYOYHYHULUVSYLUVAUHUIUJZYLUVBUHUIUJZQUWEUVSUWFUWGYHYOY
        HYHUMYHYOYHYOUMUNYLUVAUVBUOUPUNYLYTUVCUOUPUQURUVPUWAUJUVQUWBUSUVPYLYTUB
        UVCUBUWAYHYOYHYOUTYLYTUVCVAVBUVPUWAUVOUVJVCVDVEYRUVRUIPUVSYRUVRYHYQUHUI
        YOYQYHVFYHEVGVHUVRVIUPZURVJYKYRUVQUVKVKUVMYKYRQZUVQUVKLUVONOZUVOEPZQZUV
        QUVKUWIDEUVJUVOFIJYMUVEGVLCVLVMYHYOAVLZBVLVMVNYIYJYRUWLYIUWJYJYRQZUWKYI
        YHUVONOZUWJYHVOUEYHUVOPUWOUWMYHYOVPYHUVOVOVQVRLYHUVOVSVTUWNYJYOEPZQUWKY
        RUWPYJYRYQEPUWPEYHWAYOYQEWBVTWCYHYOEWDUPWEWFWGWHWIWJWKYKUVMYPYRUVLYIUVM
        YPYRUVLVKZVKVKYJYIYPUVMUWQUVMYRYIYPUVLUVMUVSYIYPUVLVKVKYRUVMUVSYIYPUVLY
        MUVEUHZUIUJZUVEUIUJZWLZUVLUVTYIYPQZQYMUVEWMUVTUWSUXBUVMUVSUWSUVMUVSYMWN
        ZUVEWNZUHZUIUJZUWSUXCYHUJZUXDYOUJZUXFUVSUSYNUVFUXGUXHQUXEUVRUIUXCYHUXDY
        OWOWPYNYLYHYMWQUXGYLYHYMWRYLYHYMWSWTUVFUVDYOUVEWQUXHUVDYOUVEWRUVDYOUVEW
        SWTZXAYNUXFUWSVKZUVFYNYMXBUWRXBZUXJYLYHYMXCYMUVEXDUXKUWSUWRWNZUIUJZUXFU
        WRXEUXFUXLUIPZUXMUXFUXLUXEPUXNYMUVEXFUXEUIUXLXGXHUXLXIWTXJXKXLXMXNXLUVM
        UXBUXAUVSUVFUXBUXAYNUVFUXBUXAUVFLYONOZUXAUXBUVFUXAYOUIUJZWLUXOUVFUWTUXP
        UVFUWTUXDUIUJZUXPUVFUVEXBUWTUXQUSUVDYOUVEXCUVEXEWTUVFUXDYOUIUXIWPXOXPBX
        QXJLYHYOXRXSXNVJWIXTYAUWHXSYBYCXLYDXTYEYFYG $.
        $( [?] $) $( [16-Sep-04] $)

      $( Lemma for ~ infxp . Letting ` x ` be the range of a maximal bijection
         ` g ` in ` H ` , ~ infxplem13 lets us show that assuming
         ` x ~<_ ( A \ x ) ` leads to the contradiction ` E. h e. H g (. h `
         meaning ` g ` is not maximal.  Thus ` ( A \ x ) ~< x ` . This allows
         us to show that ` x ` is as big as ` A ` . Since ` x ` is idempotent,
         and ` g ` exists by Zorn's Lemma in ~ infxplem11 , ` A ` is also
         idempotent. $)
      infxplem14 $p |- ( om ~<_ A -> ( A X. A ) ~~ A ) $=
        ( vg vh vx vy vu cv wpss wn wral wrex com cdom wbr cxp cen wi
        infxplem11 wcel c0 wceq infxplem12 wss wa wf1o wex wo visset infxplem4
        df-or bitr cdif csdm cun entrt enref xpen mpan sylan5 adantll mpan2
        anidms jca xpex infxplem3 adantr mpd unex ensym syl pm3.27 sylan13
        imlan sylan bren sylib f1oen adantlr adantrr infxplem13 exp 19.23adv
        cvv difexg ax-mp domen syl5ib wb domtri mp2an dfrex2 3imtr3g a3d sbth
        domentr c1o unxpdom2 ssun2 ssdomg mp2 undif2 breqtrr domtr infxplem2
        sdomdom syl2an ancoms ad2antlr exp41 com24 imp31 syld 19.23aiv syl3
        sylbi com13 com3r r19.23aiv ) GLZHLMZNHDOZGDPQBRSZBBTZBUASZUBZABCGHDEFU
        CYFYJGDYFYGYDDUDZYIYFYGYDUEUFZNZYKYIUBABCGHDEFUGYKYMYFYIYKYMQILZRSZYNBU
        HZUIZYNYNTZYNYDUJZUIZIUKZUBZYMYFYIUBZUBYKYLUVAULUVBIABYDCDEGUMUNYLUVAUO
        UPUVAUVCYMYTUVCIYTYFBYNUQZYNURSZYIYTUVEYFYTYNUVDRSZYEHDPZUVENZYFNYTYNJL
        ZUASZUVIUVDUHZUIZJUKUVGUVFYTUVLUVGJYTUVLUVGYTUVLUIZYNUVITZUVIYNTZUVIUVI
        TZUSZUSZUVIKLUJZKUKZUVGYTUVJUVTUVKYOYRYNUASZUIZUVJUVTYTUWBUVJUIUVRUVIUA
        SZUVTYOYNYRUASZUIZUVJUWCUWBUVRYNUASZUVJUWCUWEUVJUIZUVRYNUVIUTUWGYNUVRUA
        SZUWFUWGYNUVNUASZYNUVQUASZUIZUWHUWGUWIUWJUWDUVJUWIYOUWDYRUVNUASZUWIUVJY
        NYRUVNUTYNYNUASZUVJUWLYNIUMZVAZYNYNYNUVIUWNUWNUWNJUMZVBVCVDVEUWGYNUVOUA
        SZYNUVPUASZUIZUWJUWDUVJUWSYOUWDUVJUIUWQUWRUWDYRUVOUASZUWQUVJYNYRUVOUTUV
        JUWMUWTUWOYNUVIYNYNUWNUWPUWNUWNVBVFVDUWDYRUVPUASZUWRUVJYNYRUVPUTUVJUXAY
        NUVIYNUVIUWNUWPUWNUWPVBVGVDVHVEUWEUWSUWJUBUVJIUVOUVPUVIYNUWPUWNVIZUVIUV
        IUWPUWPVIZVJVKVLVHUWEUWKUWHUBUVJIUVNUVQYNUVIUWNUWPVIZUVOUVPUXBUXCVMZVJV
        KVLYNUVRUVNUVQUXDUXEVMVNVOUWEUVJVPVQUWAUWDYOYRYNUWNVNVRVSUVRUVIKUWPVTWA
        YOYSUWBYPYSUWAYOYRYNYDYNYNUWNUWNVIWBZVRWCVSWDUVMUVSUVGKUVMUVSUVGIJKABCG
        HDEFWEWFWGVLWFWGJYNUVDBWHUDZUVDWHUDZFBYNWHWIWJZWKWLYNWHUDZUXHUVFUVHWMUW
        NUXIYNUVDWHWHWNWOYEHDWPWQWRYQUWAUVEYIUBZYSYOYPUWAUXKYOUVEUWAYPYIYOUVEUW
        AYPYIYOUVEUIZUWAUIZYPUIBYNUASZYIBYNRSZYNBRSZUXNUXMYPBYNWSBYRRSZUWAUXOUX
        LBYRYNWTXAYNURSZUVDYNRSZUXQYOUVEUXRUXSUIYNUVDUSZYRRSZUXQYNUVDUWNUXIXBBU
        XTRSUYAUXQBYNBUSZUXTRUXGBUYBUHBUYBRSFBYNXCBUYBWHXDXEYNBXFXGBUXTYRXHVCVO
        IXIUVDYNXJXKVSUXJYPUXPUBUWNYNBWHXDWJXKUWAUXNYIUBUXLYPUWAUXNYIUXNUWAYIYH
        YNUASZYNBUASZYIUXNUWAUIYHYNBUTYHYRUASZUWAUYCUXNYHYRYNUTUXNUYEBYNBYNFUWN
        FUWNVBVGVSUXNUYDUWABYNUWNVNVKVQXLWFXMVLXNXOXPUXFVDXQXRXSXTYAXQYBYCWJ $.
        $( [?] $) $( [17-Sep-04] $)
    $}
  $}

  ${
    $d f x A $.
    infxp.1 $e |- A e. V $.
    $( The cross product of an infinite set is idempotent.  This theorem
       provides the basis for multiplication of infinite cardinal numbers.
       Lemma 6R of [Enderton] p. 162, whose proof we closely follow.  The
       main proof consists of ~ infxplem1 through ~ infxplem14 .  This
       proof eliminates the hypothesis of ~ infxplem14 . $)
    infxp $p |- ( om ~<_ A -> ( A X. A ) ~~ A ) $=
      ( vx vf cv c0 wceq com cdom wbr wss wa cxp wf1o wex wo cab cleqid
      infxplem14 ) CADDEZFGHCEZIJUAAKLUAUAMUATNLCOPDQZUBRBS $.
      $( [?] $) $( [17-Sep-04] $)
  $}

