$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        More cardinality results
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The nonnegative integers are equinumerous to the natural numbers. $)
    nn0ennn $p |- NN0 ~~ NN $=
      ( vx vy cn0 cn cv c1 caddc co cmin nn0ex wcel cc elnn0nn pm3.27bd
      elnnnn0 wceq wb wa 1cn subaddt mp3an2 cleqcom 3bitr4g axaddcom mpan
      cleq2d adantl bitrd nncnt nn0cnt syl2an ancoms en3 ) ABCDAEZFGHZBEZFIHZJU
      NCKZUNLKZUODKUNMNUPDKZUPLKZUQCKUPONUTURUNUQPZUPUOPZQZVAUSVDUTURVAUSRZVBUP
      FUNGHZPZVCVEUQUNPZVFUPPZVBVGVAFLKZUSVHVIQSUPFUNTUAUNUQUBUPVFUBUCUSVGVCQVA
      USVFUOUPVJUSVFUOPSFUNUDUEUFUGUHUPUIUNUJUKULUM $.
      $( [?] $) $( [19-Jul-04] $)

    $( The set of natural numbers (as a subset of complex numbers) is
       equinumerous to omega (the set of finite ordinal numbers). $)
    nnenom $p |- NN ~~ om $=
      ( vy vx cn cn0 cen wbr com cvv wcel nnex nn0ennn ensymg mp2 nn0ex cv c1
      caddc co wceq copab cc0 crdg cres wf1o cleqid om2nn0f1o omex f1oen ax-mp
      entr mp2an ) CDEFZDGEFZCGEFCHIDCEFULJKDCHLMDHIGDEFZUMNGDAOBOPQRSBATUAUBGU
      CZUDUNBAUOUOUEUFGDUOUGUHUIGDHLMCDGUJUK $.
      $( [?] $) $( [31-Jul-04] $)
  $}

  ${
    $d x y z w v u $.
    $( The cross product of the set of natural numbers with itself is
       equinumerous to the set of natural numbers. $)
    xpnnen $p |- ( NN X. NN ) ~~ NN $=
      ( vx vy vz vw vv vu cn cxp cdom wbr cen cvv wcel nnex xpex cv cint csn
      crn cuni caddc co c2 cexp cop wceq wa elxp5 nnaddclt cn0 2nn0 nnexpclt
      mpan2 syl pm3.27 sylan13 adantl sylbi wrex weq wb wi inteq inteqd visset
      op1st syl6eq sneq rneqd unieqd op2nda opreq12d opreq1d cleqan12d
      nn0opth2t nnnn0t im2an syl2an sylan9bbr cleq12 opth syl6bb bitr4d exp43
      com23 r19.23aivv r19.23advv imp elxp2 syl2anb dom2 ax-mp c1 1nn elisseti
      xpsnen ensymg mp2 wss ssid snssi ssxp mp2an ssdom2g endomtr sbth ) GGHZGI
      JZGXGIJZXGGKJXGLMZXHGGNNOZABXGGAPZQZQZXLRZSZTZUAUBZUCUDUBZXQUAUBZBPZQZQZY
      ARZSZTZUAUBZUCUDUBZYFUAUBZLXLXGMZXLXNXQUEUFZXNGMZXQGMZUGZUGXTGMZXLGGUHYNY
      OYKXSGMZYMYOYNXSXQUIYNXRGMZYPXNXQUIYQUCUJMYPUKXRUCULUMUNYLYMUOUPUQURXLCPZ
      DPZUEZUFZDGUSCGUSZYAEPZFPZUEZUFZFGUSEGUSZXTYIUFZABUTZVAZYJYAXGMUVBUVGUVJU
      VBUVFUVJEFGGUVAUVCGMZUVDGMZUGZUVFUVJVBZVBCDGGYRGMZYSGMZUGZUVMUVAUVNUVQUVM
      UVAUVFUVJUVQUVMUGZUVAUVFUGZUGUVHCEUTDFUTUGZUVIUVSUVHYRYSUAUBZUCUDUBZYSUAU
      BZUVCUVDUAUBZUCUDUBZUVDUAUBZUFZUVRUVTUVAUVFXTUWCYIUWFUVAXSUWBXQYSUAUVAXRU
      WAUCUDUVAXNYRXQYSUAUVAXNYTQZQYRUVAXMUWHXLYTVCVDYRYSCVEZVFVGUVAXQYTRZSZTYS
      UVAXPUWKUVAXOUWJXLYTVHVIVJYRYSUWIDVEZVKVGZVLVMUWMVLUVFYHUWEYFUVDUAUVFYGUW
      DUCUDUVFYCUVCYFUVDUAUVFYCUVEQZQUVCUVFYBUWNYAUVEVCVDUVCUVDEVEZVFVGUVFYFUVE
      RZSZTUVDUVFYEUWQUVFYDUWPYAUVEVHVIVJUVCUVDUWOFVEZVKVGZVLVMUWSVLVNYRUJMZYSU
      JMZUGUVCUJMZUVDUJMZUGUWGUVTVAUVQUVMYRYSUVCUVDVOUVOUWTUVPUXAYRVPYSVPVQUVKU
      XBUVLUXCUVCVPUVDVPVQVRVSUVSUVIUVTVAUVRUVSUVIYTUVEUFUVTXLYTYAUVEVTYRYSUVCU
      VDUWIUWLUWRWAWBUQWCWDWEWFWGWHCDXLGGWIEFYAGGWIWJWKWLGGWMRZHZKJZUXEXGIJZXIG
      LMUXEGKJUXFNGWMNWMGWNWOWPUXEGLWQWRXJUXEXGWSZUXGXKGGWSUXDGWSZUXHGWTWMGMUXI
      WNWMGXAWLGGUXDGXBXCUXEXGLXDWRGUXEXGXEXCXGGXFXC $.
      $( [?] $) $( [1-Aug-04] $)
  $}

  $( The cross product of omega (the set of ordinal natural numbers) with
     itself is equinumerous to omega.  Exercise 1 of [Enderton] p. 133 (which
     proves this with a direct, but longer, proof; ours uses instead the
     Schroeder-Bernstein Theorem ~ sbth in ~ xpnnen ). $)
  xpomen $p |- ( om X. om ) ~~ om $=
    ( com cxp cn cen wbr xpnnen cvv wcel wb omex xpex nnenom nnex xpen mp2an
    enen1 mpbi enen2 ) AABZCDEZSADEZCCBZCDEZTFSGHUBSDEZUCTIAAJJKCADEZUEUDLLCACA
    MJMJNOUBSCGPOQAGHUETUAIJLCASGROQ $.
    $( [?] $) $( [23-Jul-04] $)

  $( Lemma for ~ znnen . $)
  znnenlem $p |- ( ( ( 0 <_ x /\ -. 0 <_ y ) /\ ( x e. ZZ /\ y e. ZZ ) ) ->
                  ( y = x <-> ( 2 x. x ) = ( ( -u 2 x. y ) + 1 ) ) ) $=
    ( weq wn c2 cv cmulc co cneg c1 caddc wceq wb cc0 cle wbr wa cz wcel
    pm5.21 cr wi clt ax0re leltt mpan bicon2d adantr ltletrt mp3an2 exp3a
    sylbird com23 imp3a ltnet syld ancoms zret syl2an imp zneo znegclt sylan5
    cc zcnt 2cn mulneg12t syl adantl opreq1d cleq2d mtbird sylan13 ) BACZDZEAFZ
    GHZEIBFZGHZJKHZLZDZVNWAMNVPOPZNVROPZDZQZVPRSZVRRSZQZQVNWATWIWFVOWIWFVOVPUAS
    ZVRUASZWFVOUBZWGWHWKWJWLWKWJQZWFVRVPUCPZVOWMWCWEWNWMWEWCWNWMWEVRNUCPZWCWNUB
    WKWOWEMWJWKWDWONUASZWKWDWODMUDNVRUEUFUGUHWMWOWCWNWKWPWJWOWCQWNUBUDVRNVPUIUJ
    UKULUMUNVRVPUOUPUQVPURVRURUSUTUQWIWBWFWIWAVQEVRIZGHZJKHZLZWGWQRSWTDWHVPWQVA
    VRVBVCWIVTWSVQWIVSWRJKWHVSWRLZWGWHVRVDSZXAVRVEEVDSXBXAVFEVRVGUFVHVIVJVKVLVI
    VM $.
    $( [?] $) $( [31-Jul-04] $)

  ${
    $d x y $.
    $( The set of integers and the set of natural numbers are equinumerous. $)
    znnen $p |- ZZ ~~ NN $=
      ( vx vy cz cn0 cen wbr cn cdom cvv wcel zex cc0 cv cle c2 cmulc co cneg
      c1 caddc cded wa elnn0z 2nn0 nn0mulclt mpan sylbir wb dedlem1 eleq1d
      adantl mpbird exp wn 2z znegclt ax-mp zmulclt adantr cr wi zret clt
      negrclt 2pos 2re axmulgt0 mpani syl ax0re leltt bicon2d lt0neg1t bitr3d
      cc wceq recnt 2cn mulneg12t breq2d 3imtr4d negrcl axmulrcl ltlet syld
      imp jca biimpr peano2nn0 3syl dedlem2 pm2.61d weq cleqan12d gt0ne0i
      mulcant mp3an1 zcnt syl2an sylan9bb bicomd znnenlem eqcomb syl5bb bitr4d
      ancom syl2anb cleqcom syl6bbr 1cn addcan2t mp3an3 negcl axmulcl negn0
      bitr2d sylan9bbr 4cases dom2 wss nn0ex nn0ssz ssdomg mp2 sbth mp2an
      nn0ennn entr ) CDEFZDGEFCGEFCDHFZDCHFZYICIJYJKABCDLAMZNFZOYLPQZORZYLPQZST
      QZUAZLBMZNFZOYSPQZYOYSPQZSTQZUAZIYLCJZYMYRDJZUVEYMUVFUVEYMUBZUVFYNDJZUVGY
      LDJZUVHYLUCODJUVIUVHUDOYLUEUFUGYMUVFUVHUHUVEYMYRYNDYMYNYQUIZUJUKULUMUVEYM
      UNZUVFUVEUVKUBZUVFYQDJZUVLYPCJZLYPNFZUBZYPDJZUVMUVLUVNUVOUVEUVNUVKYOCJZUV
      EUVNOCJUVRUOOUPUQYOYLURUFUSUVEUVKUVOUVEYLUTJZUVKUVOVAYLVBUVSUVKLYPVCFZUVO
      UVSLYLRZVCFZLOUWAPQZVCFZUVKUVTUVSUWAUTJZUWBUWDVAYLVDUWELOVCFZUWBUWDVEOUTJ
      UWEUWFUWBUBUWDVAVFOUWAVGUFVHVIUVSYLLVCFZUVKUWBUVSYMUWGLUTJZUVSYMUWGUNUHVJ
      LYLVKUFVLYLVMVNUVSYPUWCLVCUVSYLVOJZYPUWCVPZYLVQOVOJZUWIUWJVROYLVSUFVIVTWA
      UVSYPUTJZUVTUVOVAZYOUTJUVSUWLOVFWBYOYLWCUFUWHUWLUWMVJLYPWDUFVIWEVIWFWGUVQ
      UVPYPUCWHYPWIWJUVKUVFUVMUHUVEUVKYRYQDYMYNYQWKZUJUKULUMWLUVEYSCJZUBZABWMZY
      RUVDVPZYMYTUWPUWQUWRUHZVAYMYTUBZUWPUWSUWTUWPUBUWRUWQUWTUWRYNUVAVPZUWPUWQY
      MYTYRYNUVDUVAUVJYTUVAUVCUIZWNUWIYSVOJZUXAUWQUHZUVEUWOUWKUWIUXCUXDVROYLYSO
      VFVEWOZWPWQYLWRZYSWRZWSWTXAUMYMYTUNZUBZUWPUWSUXIUWPUBZUWQYNUVCVPZUWRUXJBA
      WMUXKUWQABXBABXCXDUXIUWRUXKUHUWPYMUXHYRYNUVDUVCUVJYTUVAUVCWKZWNUSXEUMUVKY
      TUBZUWPUWSUXMUWPUBZUWQYQUVAVPZUWRUXNUWQUVAYQVPZUXOYTUVKUBUWOUVEUBUWQUXPUH
      UXMUWPBAXBUVKYTXFUVEUWOXFXGYQUVAXHXIUXMUWRUXOUHUWPUVKYTYRYQUVDUVAUWNUXBWN
      USXEUMUVKUXHUBZUWPUWSUWPUWQYQUVCVPZUXQUWRUWIUXCUWQUXRUHUVEUWOUWIUXCUBUXRY
      PUVBVPZUWQYPVOJZUVBVOJZUXRUXSUHZUWIUXCUXTUYASVOJUYBXJYPUVBSXKXLYOVOJZUWIU
      XTOVRXMZYOYLXNUFUYCUXCUYAUYDYOYSXNUFWSUYCUWIUXCUXSUWQUHUYDYOYLYSOVRUXEXOW
      PWQXPUXFUXGWSUXQUWRUXRUVKUXHYRYQUVDUVCUWNUXLWNXAXQUMXRXAXSUQDIJDCXTYKYAYB
      DCIYCYDCDYEYFYGCDGYHYF $.
      $( [?] $) $( [31-Jul-04] $)
  $}

  ${
    $d x y z w $.
    $( The set of rationals and the set of nonnegative integers are
       equinumerous.  (We use the Axiom of Choice via ~ fodom to give us a
       shorter proof, but this theorem can also be proved without it.  See,
       for example, Exercise 2 of [Enderton] p. 133.) $)
    qnnen $p |- QQ ~~ NN $=
      ( vx vy vz vw cq cn cdom wbr cen cz cxp cv wcel wa cdiv co wceq copab2
      wfo wfn crn oprex cleqid fnoprab2 wrex elrnoprab elq bitr4 cleqri pm3.2i
      df-fo mpbir zex nnex xpex fodom ax-mp znnen cvv enrefg xpen mp2an xpnnen
      entr domentr wss nnssq ssdomg mp2 sbth ) EFGHZFEGHZEFIHEJFKZGHZVMFIHZVKVM
      EALZJMBLZFMNCLVPVQOPZQNABCRZSZVNVTVSVMTZVSUAZEQZNWAWCABCJFVRVSVPVQOUBZVSU
      CZUDDWBEDLZWBMWFVRQBFUEAJUEWFEMABCJFVRWFVSWDWEUFABWFUGUHUIUJVMEVSUKULVMEV
      SJFUMUNUOUPUQVMFFKZIHZWGFIHVOJFIHFFIHZWHURFUSMZWIUNFUSUTUQJFFFUMUNUNUNVAV
      BVCVMWGFVDVBEVMFVEVBWJFEVFVLUNVGFEUSVHVIEFVJVB $.
      $( [?] $) $( [31-Jul-04] $)
  $}

  $( Lemma for ~ infxpen . $)
  infxplem16a $p |- ( om ~<_ x -> 1o ~< x ) $=
    ( c1o com csdm wbr cv cdom wcel 1onn nnsdom ax-mp cvv wa wi visset
    sdomdomtr mpan ) BCDEZCAFZGEZBSDEZBCHRIBJKSLHRTMUANAOBCSLPKQ $.
    $( [?] $) $( [17-Sep-04] $)

  ${
    infxplem16.1 $e |- A e. V $.
    infxplem16.2 $e |- B e. V $.
    $( Lemma for ~ infxpen . $)
    infxplem16 $p |- ( ( om ~<_ x /\ x ~~ ( x X. x ) ) ->
                     ( ( x ~~ A /\ x ~~ B ) -> x ~~ ( A u. B ) ) ) $=
      ( c1o cv csdm wbr cxp cen wa cun wi com cdom sbth cvv wcel wss ssun1
      ssdomg mp2 endomtr mpan2 ad2antrl domentr unxpdom sdomentr ax-mp syl2an
      anandis adantlr entr xpex ensymg syl visset xpen sylan5 adantll sylan13
      exp infxplem16a sylan ) FAGZHIZVFVFVFJZKIZVFBKIZVFCKIZLZVFBCMZKIZNOVFPIVG
      VILZVLVNVFVMPIZVMVFPIZVNVOVLLZVFVMQVJVPVOVKVJBVMPIZVPBRSZBVMTVSDBCUABVMRU
      BUCVFBVMUDUEUFVMBCJZPIZWAVFKIZVQVRVMWAVFUGVGVLWBVIVGVJVKWBFBHIZFCHIZWBVGV
      JLZVGVKLZBCUHVTWFWDNDFVFBRUIUJCRSWGWENEFVFCRUIUJUKULUMVIVLWCVGVIVHWAKIZWC
      VLVIWHLVFWAKIZWCVFVHWAUNWARSWIWCNBCDEUOVFWARUPUJUQVFBVFCAURZDWJEUSUTVAVBV
      BVCAVDVE $.
      $( [?] $) $( [15-Sep-04] $)
  $}

  $( Lemma for ~ infxpen . $)
  infxplem17 $p |- ( ( ( om ~<_ x /\ ( x X. x ) ~~ x ) /\ x ~~ y ) ->
                   ( ( x X. y ) u. ( ( y X. x ) u. ( y X. y ) ) ) ~~ y ) $=
    ( com cv cdom wbr cxp cen wa cun entr cvv wcel visset enrefg ax-mp xpen
    mpan sylan5 adantll mpan2 anidms jca wi xpex infxplem16 adantr mpd unex
    ensymg syl pm3.27 sylan13 imlan sylan ) CADZEFZUPUPUPGZHFZIZUPBDZHFZUPVAGZV
    AUPGZVAVAGZJZJZVAHFZUQURUPHFZIVGUPHFZVBVHUTVBIZVGUPVAKVKUPVGHFZVJVKUPVCHFZU
    PVFHFZIZVLVKVMVNUSVBVMUQUSURVCHFZVMVBUPURVCKUPUPHFZVBVPUPLMZVQANZUPLOPZUPUP
    UPVAVSVSVSBNZQRSTVKUPVDHFZUPVEHFZIZVNUSVBWDUQUSVBIWBWCUSURVDHFZWBVBUPURVDKV
    BVQWEVTUPVAUPUPVSWAVSVSQUASUSURVEHFZWCVBUPURVEKVBWFUPVAUPVAVSWAVSWAQUBSUCTU
    TWDVNUDVBAVDVEVAUPWAVSUEZVAVAWAWAUEZUFUGUHUCUTVOVLUDVBAVCVFUPVAVSWAUEZVDVEW
    GWHUIZUFUGUHVGLMVLVJUDVCVFWIWJUIUPVGLUJPUKUTVBULUMVIUSUQVRVIUSUDVSURUPLUJPU
    NUO $.
    $( [?] $) $( [15-Sep-04] $)

  $( Lemma for ~ infxpen . $)
  infxplem18 $p |- ( ( ( g : ( x X. x ) -1-1-onto-> x /\
          u : ( ( x X. y ) u. ( ( y X. x ) u. ( y X. y ) ) ) -1-1-onto-> y ) /\
                ( x i^i y ) = (/) ) ->
          ( g u. u ) : ( ( x u. y ) X. ( x u. y ) ) -1-1-onto-> ( x u. y ) ) $=
    ( cv cxp wf1o cun wa cin c0 wceq f1oun xpdisj2 xpdisj1 jca undisj sylib
    ancri sylan5 wb xpun unass eqtr f1oeq2 ax-mp sylibr ) AEZUHFZUHDEZGUHBEZFZU
    KUHFZUKUKFZHZHZUKCEZGIZUHUKJKLZIUIUPHZUHUKHZUJUQHZGZVAVAFZVAVBGZURUIUPJKLZU
    SIVCUSUIUHUPUKUJUQMUSVFUSUIULJKLZUIUOJKLZIVFUSVGVHUHUKUHUHNUSUIUMJKLZUIUNJK
    LZIVHUSVIVJUHUKUHUHOUHUKUHUKOPUIUMUNQRPUIULUOQRSTVDUTLVEVCUAVDUIULHUOHUTUHU
    KUHUKUBUIULUOUCUDVDUTVAVBUEUFUG $.
    $( [?] $) $( [15-Sep-04] $)

  $( Lemma for ~ infxpen . $)
  infxplem19 $p |- ( ( ( g : ( x X. x ) -1-1-onto-> x /\
          u : ( ( x X. y ) u. ( ( y X. x ) u. ( y X. y ) ) ) -1-1-onto-> y ) /\
                ( x i^i y ) = (/) ) -> ( g i^i u ) = (/) ) $=
    ( cv cxp wf1o cun wa cin c0 wceq crn wb ineq12 cleq1d wfo f1ofo forn syl
    syl2an wi wrel f1orel relin relrn0 wss rnin sseq2 mpbii ss0 syl5bir 3syl
    adantr sylbird imp ) AEZUQFZUQDEZGZUQBEZFVAUQFVAVAFHHZVACEZGZIZUQVAJZKLZUSV
    CJZKLZVEVGUSMZVCMZJZKLZVIVJUQLZVKVALZVMVGNUTVDVNVOIVLVFKVJUQVKVAOPUTURUQUSQ
    VNURUQUSRURUQUSSTVDVBVAVCQVOVBVAVCRVBVAVCSTUAUTVMVIUBZVDUTUSUCVHUCZVPURUQUS
    UDUSVCUEVQVIVHMZKLZVMVHUFVMVRKUGZVSVMVRVLUGVTUSVCUHVLKVRUIUJVRUKTULUMUNUOUP
    $.
    $( [?] $) $( [15-Sep-04] $)

  $( Lemma for ~ infxpen . $)
  infxplem0a $p |- ( om ~<_ y -> -. y = (/) ) $=
    ( com cv cdom wbr c0 csdm wceq wn wcel peano1 nnsdom ax-mp cvv wa wi
    visset sdomdomtr mpan 0sdom sylib ) BACZDEZFUBGEZUBFHIFBGEZUCUDFBJUEKFLMUBN
    JUEUCOUDPAQZFBUBNRMSUBUFTUA $.
    $( [?] $) $( [15-Sep-04] $)

  $( Lemma for ~ infxpen . $)
  infxplem20 $p |- ( ( ( ( g : ( x X. x ) -1-1-onto-> x /\
        u : ( ( x X. y ) u. ( ( y X. x ) u. ( y X. y ) ) ) -1-1-onto-> y ) /\
        ( x i^i y ) = (/) ) /\ ( om ~<_ x /\ x ~~ y ) ) -> g (. ( g u. u ) ) $=
    ( cv cin c0 wceq wn cun wpss cxp wf1o wa com cdom wbr cen disjpss
    infxplem19 adantr crn wrel wb f1orel relrn0 syl wfo f1ofo forn cleq1d
    bitrd binegd infxplem0a syl5bir domentr syl5 imp adantll adantlr sylan13 )
    DEZCEZFGHZVCGHZIZVBVBVCJKAEZVGLVGVBMZVGBEZLVIVGLVIVILJJZVIVCMZNZVGVIFGHZNZO
    VGPQVGVIRQNZNVBVCSVNVDVOABCDTUAVLVOVFVMVKVOVFVHVKVOVFVKOVIPQZVFVOVKVFVIGHZI
    VPVKVEVQVKVEVCUBZGHZVQVKVCUCVEVSUDVJVIVCUEVCUFUGVKVRVIGVKVJVIVCUHVRVIHVJVIV
    CUIVJVIVCUJUGUKULUMBUNUOOVGVIUPUQURUSUTVA $.
    $( [?] $) $( [15-Sep-04] $)

  ${
    infxplem25.1 $e |- B e. V $.
    $( Lemma for ~ infxpen . $)
    infxplem25 $p |- ( ( om ~<_ x /\ ( B \ x ) ~< x ) -> B ~<_ ( x X. x ) ) $=
      ( c1o cv csdm wbr cdif cdom cxp com wa cun visset cvv wcel difexg ax-mp
      unxpdom2 wss ssun2 ssdomg mp2 undif2 breqtrr domtr mpan syl infxplem16a
      sdomdom syl2an ) DAEZFGZBULHZULIGZBULULJZIGZKULIGUNULFGUMUOLULUNMZUPIGZUQ
      ULUNANBOPZUNOPCBULOQRSBURIGUSUQBULBMZURIUTBVATBVAIGCBULUABVAOUBUCULBUDUEB
      URUPUFUGUHAUIUNULUJUK $.
      $( [?] $) $( [17-Sep-04] $)

    $( Lemma for ~ infxpen . $)
    infxplem26 $p |- ( ( ( ( om ~<_ x /\ ( B \ x ) ~< x ) /\
                     ( x X. x ) ~~ x ) /\ x (_ B ) -> ( B X. B ) ~~ B ) $=
      ( com cv cdom wbr cdif csdm wa cxp cen wss sbth domentr infxplem25 sylan
      cvv wcel wi visset ssdomg ax-mp syl2an entr xpen anidms ensymg adantr
      sylan13 ancoms exp ad2antlr mpd ) DAEZFGBUOHUOIGJZUOUOKZUOLGZJZUOBMZJBUOL
      GZBBKZBLGZBUOFGZUOBFGZVAUSUTBUONBUQFGURVDUPBUQUOOABCPQUORSZUTVETAUAZUOBRU
      BUCUDURVAVCTUPUTURVAVCVAURVCVBUOLGZUOBLGZVCVAURJVBUOBUEVBUQLGZURVHVAVBUQU
      OUEVAVJBUOBUOCVGCVGUFUGQVAVIURVFVAVITVGBUORUHUCUIUJUKULUMUN $.
      $( [?] $) $( [17-Sep-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Lemma for ~ infxpen . $)
    infxplem1ba $p |- ( E. x ( ( om ~<_ x /\ x (_ B ) /\
                      A : ( x X. x ) -1-1-onto-> x ) <->
           E. y ( ( om ~<_ y /\ y (_ B ) /\ A : ( y X. y ) -1-1-onto-> y ) ) $=
      ( com cv cdom wbr wss wa cxp wf1o weq breq2 sseq1 bi2and wceq wb xpeq1
      xpeq2 eqtrd f1oeq2 syl f1oeq3 bitrd cbvexv ) EAFZGHZUGDIZJZUGUGKZUGCLZJEB
      FZGHZUMDIZJZUMUMKZUMCLZJABABMZUJUPULURUSUHUNUIUOUGUMEGNUGUMDOPUSULUQUGCLZ
      URUSUKUQQULUTRUSUKUMUGKUQUGUMUGSUGUMUMTUAUKUQUGCUBUCUGUMUQCUDUEPUF $.
      $( [?] $) $( [17-Sep-04] $)
  $}

  ${
    $d x y z w f g h v u t B $.  $d x y z w v f g h t A $.
    $d x y z w v f g h t C $.  $d x y z w v u g h H $.
    infxplem.1 $e |- H = { f | ( f = (/) \/ E. t ( ( om ~<_ t /\ t (_ B ) /\
                      f : ( t X. t ) -1-1-onto-> t ) ) } $.
    ${
      infxplem1a.2 $e |- A e. V $.
      $( Lemma for ~ infxpen . $)
      infxplem1a $p |- ( A e. H <-> ( A = (/) \/
        E. x ( ( om ~<_ x /\ x (_ B ) /\ A : ( x X. x ) -1-1-onto-> x ) ) ) $=
        ( wcel c0 wceq com cv cdom wbr wss wa cxp wf1o wex wo cleq1 f1oeq1
        biland biexdv bi2ord elab2 infxplem1ba bilor bicom bitr ) CFICJKZLBMZNO
        UMDPQZUMUMRZUMCSZQZBTZUAZULLAMZNOUTDPQUTUTRUTCSQATZUAZEMZJKZUNUOUMVCSZQ
        ZBTZUAUSECFHVCCKZVDULVGURVCCJUBVHVFUQBVHVEUPUNUOUMVCCUCUDUEUFGUGVBUSVAU
        RULABCDUHUIUJUK $.
        $( [?] $) $( [16-Sep-04] $)

      ${
        $d x D $.
        infxplem1b.3 $e |- D e. V $.
        $( Lemma for ~ infxpen . $)
        infxplem1b $p |- ( ( ( om ~<_ D /\ D (_ B ) /\
                         A : ( D X. D ) -1-1-onto-> D ) -> A e. H ) $=
          ( vx com cdom wbr wss wa cxp wf1o cv wex wcel wceq breq2 sseq1
          bi2and wb xpeq1 xpeq2 eqtrd f1oeq2 syl f1oeq3 bitrd cla4ev c0 wo olc
          infxplem1a sylibr ) KDLMZDCNZOZDDPZDBQZOZKJRZLMZVECNZOZVEVEPZVEBQZOZJ
          SZBFTZVKVDJDIVEDUAZVHVAVJVCVNVFUSVGUTVEDKLUBVEDCUCUDVNVJVBVEBQZVCVNVI
          VBUAVJVOUEVNVIDVEPVBVEDVEUFVEDDUGUHVIVBVEBUIUJVEDVBBUKULUDUMVLBUNUAZV
          LUOVMVLVPUPJABCEFGHUQURUJ $.
          $( [?] $) $( [16-Sep-04] $)
      $}
    $}

    $( Lemma for ~ infxpen . $)
    infxplem1 $p |- ( g e. H -> dom g = ( ran g X. ran g ) ) $=
      ( vx cv wcel c0 wceq com cdom wbr wss wa cxp wf1o wex wo cdm crn visset
      infxplem1a dmeq dm0 syl6eq rneq rn0 xpeq2 syl xp0r eqtr4d wfn ccnv wfun
      w3a f1o2 fndm xpeq1 eqtr2d sylan9eq 3adant2 sylbi adantl 19.23aiv jaoi )
      DHZEIVHJKZLGHZMNVJBOPZVJVJQZVJVHRZPZGSZTVHUAZVHUBZVQQZKZGAVHBCEFDUCUDVIVS
      VOVIVPJVRVIVPJUAJVHJUEUFUGVIVRVQJQZJVIVQJKVRVTKVIVQJUBJVHJUHUIUGVQJVQUJUK
      VQULUGUMVNVSGVMVSVKVMVHVLUNZVHUOUPZVQVJKZUQVSVLVJVHURWAWCVSWBWAWCVPVLVRVL
      VHUSWCVRVJVQQVLVQVJVQUTVQVJVJUJVAVBVCVDVEVFVGVD $.
      $( [?] $) $( [11-Sep-04] $)

    $( Lemma for ~ infxpen . $)
    infxplem2 $p |- ( ( C (_ H /\ A. g e. C A. h e. C ( g (_ h \/ h (_ g ) ) ->
                    ( Fun U. C /\ Fun `' U. C ) ) $=
      ( vx cv wfun ccnv wa wral wss wo cuni r19.26 fun11uni sylbir wcel ssel
      c0 wceq com cdom wbr cxp wf1o wex visset infxplem1a wf1 f10 f1eq1 mpbiri
      wf df-f1 ffun imran sylbi syl f1of1 adantl 19.23aiv jaoi syl6 r19.21aiv
      sylan ) EJZKZVJLKZMZECNZVJFJZOVOVJOPFCNZECNZCQZKVRLKMZCGOZVNVQMVMVPMECNVS
      VMVPECRCEFSTVTVMECVTVJCUAVJGUAZVMCGVJUBWAVJUCUDZUEIJZUFUGWCBOMZWCWCUHZWCV
      JUIZMZIUJZPVMIAVJBDGHEUKULWBVMWHWBUCUCVJUMZVMWBWIUCUCUCUMUCUNUCUCVJUCUOUP
      WIUCUCVJUQZVLMVMUCUCVJURWJVKVLUCUCVJUSUTVAVBWGVMIWFVMWDWFWEWCVJUMZVMWEWCV
      JVCWKWEWCVJUQZVLMVMWEWCVJURWLVKVLWEWCVJUSUTVAVBVDVEVFVAVGVHVI $.
      $( [?] $) $( [11-Sep-04] $)

    $( Lemma for ~ infxpen . $)
    infxplem3 $p |- ( ( C (_ H /\ g e. C ) ->
             ( ( y e. ran g /\ z e. ran g ) -> <. y , z >. e. dom U. C ) ) $=
      ( wss cv wcel wa crn cop cdm cuni cxp wceq ssel2 infxplem1 syl eleq2d
      visset opelxp syl6bb wi ciun ssiun2 dmuni syl6ssr sseld adantl sylbird )
      EHJZGKZELZMZAKZUPNZLBKZUTLMZUSVAOZUPPZLZVCEQPZLZURVEVCUTUTRZLVBURVDVHVCUR
      UPHLVDVHSEHUPTCDFGHIUAUBUCUSVAUTUTBUDUEUFUQVEVGUGUOUQVDVFVCUQVDGEVDUHVFGE
      VDUIGEUJUKULUMUN $.
      $( [?] $) $( [11-Sep-04] $)

    $( Lemma for ~ infxpen . $)
    infxplem4 $p |- ( ( C (_ H /\ h e. C ) -> ( g (_ h ->
             ( ( y e. ran g /\ z e. ran h ) -> <. y , z >. e. dom U. C ) ) ) $=
      ( cv wss crn wcel wa cop cuni cdm rnss sseld imrand infxplem3 syl9r ) GKZ
      HKZLZAKZUDMZNZBKZUEMZNZOUGUKNZULOEILUEENOUGUJPEQRNUFUIUMULUFUHUKUGUDUESTU
      AABCDEFHIJUBUC $.
      $( [?] $) $( [11-Sep-04] $)

    $( Lemma for ~ infxpen . $)
    infxplem5 $p |- ( ( C (_ H /\ g e. C ) -> ( h (_ g ->
             ( ( y e. ran g /\ z e. ran h ) -> <. y , z >. e. dom U. C ) ) ) $=
      ( cv wss crn wcel wa cop cuni cdm rnss sseld imland infxplem3 syl9r ) HKZ
      GKZLZAKZUEMZNZBKZUDMZNZOUIUJUHNZOEILUEENOUGUJPEQRNUFULUMUIUFUKUHUJUDUESTU
      AABCDEFGIJUBUC $.
      $( [?] $) $( [11-Sep-04] $)

    ${
      infxplem.2 $e |- A = ran U. C $.
      infxplem0 $p |- ( y e. A <-> E. g e. C y e. ran g ) $=
        ( cv wcel crn ciun wrex cuni rnuni eqtr eleq2i eliun bitr ) AKZCLUBGEGK
        MZNZLUBUCLGEOCUDUBCEPMUDJGEQRSGUBEUCTUA $.
        $( [?] $) $( [12-Sep-04] $)

      $( Lemma for ~ infxpen . $)
      infxplem6 $p |- ( ( C (_ H /\ A. g e. C A. h e. C ( g (_ h \/ h (_ g ) )
                      -> ( A X. A ) (_ dom U. C ) $=
        ( vy vz vw vv wss cv wo wral wa cxp cuni cdm wrel relxp a1i crn wcel
        wrex cop wi weq sseq1 sseq2 bi2ord rcla42v com12 infxplem4 adantrl
        infxplem5 adantrr jaod exp com3l syld com13 imp r19.23advv infxplem0
        bi2an visset opelxp reeanv 3bitr4 syl5ib relssdv ) DHOZFPZGPZOZVRVQOZQZ
        GDRFDRZSZKLBBTZDUAUBZWDUCWCBBUDUEWCKPZMPZUFUGZLPZNPZUFUGZSZNDUHMDUHZWFW
        IUIZWEUGZWNWDUGZWCWLWOMNDDVPWBWGDUGZWJDUGZSZWLWOUJZUJWSWBVPWTWSWBWGWJOZ
        WJWGOZQZVPWTUJWBWSXCWAXCWGVROZVRWGOZQFGWGWJDDFMUKVSXDVTXEVQWGVRULVQWGVR
        UMUNGNUKXDXAXEXBVRWJWGUMVRWJWGULUNUOUPVPWSXCWTVPWSXCWTUJVPWSSXAWTXBVPWR
        XAWTUJWQKLACDEMNHIUQURVPWQXBWTUJWRKLACDEMNHIUSUTVAVBVCVDVEVFVGWFBUGZWIB
        UGZSWHMDUHZWKNDUHZSWPWMXFXHXGXIKABCDEMHIJVHLABCDENHIJVHVIWFWIBBLVJVKWHW
        KMNDDVLVMVNVO $.
        $( [?] $) $( [11-Sep-04] $)

      $( Lemma for ~ infxpen . $)
      infxplem7 $p |- ( C (_ H -> dom U. C (_ ( A X. A ) ) $=
        ( vy vg wss cuni cdm cxp cv wcel wrex wi wa crn wceq ssel2 infxplem1
        syl ra4e infxplem0 sylibr exp ssrdv ssxp anidms adantl eqsstrd sseld
        r19.23adv ciun dmuni eleq2i eliun bitr syl5ib ) DFKZIDLMZBBNZVBIOZJOZMZ
        PZJDQZVEVDPZVEVCPZVBVHVJJDVBVFDPZVHVJRVBVLSZVGVDVEVMVGVFTZVNNZVDVMVFFPV
        GVOUADFVFUBACEJFGUCUDVLVOVDKZVBVLVNBKZVPVLIVNBVLVEVNPZVEBPZVLVRSVRJDQVS
        VRJDUEIABCDEJFGHUFUGUHUIVQVPVNBVNBUJUKUDULUMUNUHUOVKVEJDVGUPZPVIVCVTVEJ
        DUQURJVEDVGUSUTVAUI $.
        $( [?] $) $( [11-Sep-04] $)

      $( Lemma for ~ infxpen . $)
      infxplem8 $p |- ( ( C (_ H /\ A. g e. C A. h e. C ( g (_ h \/ h (_ g ) )
                      -> U. C : ( A X. A ) -1-1-onto-> A ) $=
        ( wss cv wo wral wa cuni cxp wfn ccnv wfun crn wceq wf1o cdm infxplem2
        infxplem7 adantr infxplem6 eqssd jca an23 df-fn biran bitr4 sylib
        cleqcomi jctir w3a f1o2 df-3an bitr sylibr ) DHKZFLZGLZKVEVDKMGDNFDNZOZ
        DPZBBQZRZVHSTZOZVHUAZBUBZOZVIBVHUCZVGVLVNVGVHTZVKOZVHUDZVIUBZOZVLVGVRVT
        ACDEFGHIUEVGVSVIVCVSVIKVFABCDEHIJUFUGABCDEFGHIJUHUIUJWAVQVTOZVKOVLVQVKV
        TUKVJWBVKVHVIULUMUNUOBVMJUPUQVPVJVKVNURVOVIBVHUSVJVKVNUTVAVB $.
        $( [?] $) $( [11-Sep-04] $)

      $( Lemma for ~ infxpen . $)
      infxplem9 $p |- ( C (_ H -> ( -. U. C = (/) ->
                      ( om ~<_ A /\ A (_ B ) ) ) $=
        ( vg vx vy wss cuni c0 wceq wn com cdom wbr cv wrex wcel wi wa crn
        ssel2 cxp wf1o wex wo visset infxplem1a biimp ord wfo f1ofo forn syl
        cleqcomd imran ancoms 19.22i cvv rnexg ax-mp breq2 sseq1 bi2and
        ceqsexv sylib syl6 domtr ra4e infxplem0 sylibr exp ssrdv ssdomg sylan5
        com12 adantl adantrd syld r19.23adv wral csn uni0b dfss3 elsn biral
        3bitr bineg rexnal bitr4 syl5ib pm3.27 rneq rn0 syl6eq 0ss a1i eqsstrd
        pm2.61d2 sseld a1d jcad ) DFLZDMNOZPZQBRSZBCLZXGITZNOZPZIDUAZXJXIXGXNXJ
        IDXGXLDUBZXNXJUCXGXPUDZXNQXLUEZRSZXRCLZUDZXJXQXLFUBZXNYAUCDFXLUFYBXNQJT
        ZRSZYCCLZUDZYCYCUGZYCXLUHZUDZJUIZYAYBXMYJYBXMYJUJJAXLCEFGIUKZULUMUNYJYC
        XROZYFUDZJUIYAYIYMJYHYFYMYHYLYFYHXRYCYHYGYCXLUOXRYCOYGYCXLUPYGYCXLUQURU
        SUTVAVBYFYAJXRXLVCUBXRVCUBZYKXLVCVDVEZYLYDXSYEXTYCXRQRVFYCXRCVGVHVIVJVK
        URZXQXSXJXTXPXSXJUCXGXSXPXJXSXPXJXSXRBRSZXJXPQXRBVLXPXRBLZYQXPKXRBXPKTZ
        XRUBZYSBUBZXPYTUDYTIDUAZUVAYTIDVMKABCDEIFGHVNZVOVPVQYNYRYQUCYOXRBVCVRVE
        URVSVPVTWAWBWCVPWDXIXMIDWEZPXOXHUVDXHDNWFZLXLUVEUBZIDWEUVDDWGIDUVEWHUVF
        XMIDINWIWJWKWLXMIDWMWNWOXGXKXIXGKBCXGUVBYSCUBZUVAXGYTUVGIDXGXPYTUVGUCXQ
        XRCYSXQXMXTXQXNYAXTYPXSXTWPVKXMXRNCXMXRNUENXLNWQWRWSNCLXMCWTXAXBXCXDVPW
        DUVCWOVQXEXF $.
        $( [?] $) $( [12-Sep-04] $)

      ${
        infxplem.3 $e |- C e. V $.
        $( Lemma for ~ infxpen . $)
        infxplem10 $p |- ( ( C (_ H /\
                   A. g e. C A. h e. C ( g (_ h \/ h (_ g ) ) -> U. C e. H ) $=
          ( vx wss cv wo wral wa cuni c0 wceq wcel wn com cdom wbr cxp wf1o wi
          infxplem9 adantr infxplem8 a1d jcad uniex crn cvv rnexg ax-mp eqeltr
          infxplem1b syl6 wex orc infxplem1a sylibr pm2.61d2 ) DHMZFNZGNZMVIVHM
          OGDPFDPZQZDRZSTZVLHUAZVKVMUBZUCBUDUEBCMQZBBUFBVLUGZQVNVKVOVPVQVGVOVPU
          HVJABCDEHIJUIUJVKVQVOABCDEFGHIJUKULUMAVLCBEHIDKUNZBVLUOZUPJVLUPUAVSUP
          UAVRVLUPUQURUSUTVAVMVMUCLNZUDUEVTCMQVTVTUFVTVLUGQLVBZOVNVMWAVCLAVLCEH
          IVRVDVEVF $.
          $( [?] $) $( [12-Sep-04] $)
      $}
    $}

    ${
      infxplem.4 $e |- B e. V $.
      $( Lemma for ~ infxpen . $)
      infxplem11 $p |- H e. V $=
        ( cv c0 wceq cab com cdom wbr wss wa cxp wf1o wex cun cvv wo unab
        eqtr4 csn df-sn p0ex eqeltrr cpw wrex wcel df-rex visset elpw biran
        ancom an23 3bitr biex bitr biabi pwex wf xpex mapex mp2an f1of adantl
        ss2abi ssexi abrexex2 unex eqeltr ) DCGZHIZCJZKAGZLMZVPBNZOVPVPPZVPVMQZ
        OZARZCJZSZTDVNWBUACJWDEVNWBCUBUCVOWCHUDVOTCHUEUFUGVQVTOZABUHZUIZCJWCTWG
        WBCWGVPWFUJZWEOZARWBWEAWFUKWIWAAWIVRWEOWEVROWAWHVRWEVPBAULZUMUNVRWEUOVQ
        VTVRUPUQURUSUTWEACWFBFVAWECJVSVPVMVBZCJZVSTUJVPTUJWLTUJVPVPWJWJVCWJVSVP
        TTCVDVEWEWKCVTWKVQVSVPVMVFVGVHVIVJUGVKVL $.
        $( [?] $) $( [12-Sep-04] $)

      $( Lemma for ~ infxpen . $)
      infxplem12 $p |- E. g e. H A. h e. H -. g (. h $=
        ( vz cv wss wo wral wa cuni wcel wi wpss wn wrex infxplem11 zorn2 crn
        cleqid visset infxplem10 mpg ) IJZFKDJZEJZKUJUIKLEUHMDUHMNUHOZFPQUIUJRS
        EFMDFTIDEIFABCFGHUAUBAUKUCZBUHCDEFGULUDIUEUFUG $.
        $( [?] $) $( [12-Sep-04] $)

      $( Lemma for ~ infxpen . $)
      infxplem13 $p |- ( om ~<_ B -> E. h e. H -. h = (/) ) $=
        ( vy com cv cen wbr wss wa wex wcel c0 wceq wn cdom wrex cxp wf1o
        visset infxplem1b exp wi crn wfo f1ofo forn syl cleqcomd rneq rn0
        syl6eq sylan9eq infxplem0a nsyli com12 adantr jcad 19.22dv imp an1rs
        endom omex xpen anidms xpomen cvv wb xpex enen1 mpan mpbii enen2 mpbid
        bren sylib jca sylan 19.23aiv domen df-rex 3imtr4 ) IHJZKLZWGBMZNZHODJZ
        EPZWKQRZSZNZDOZIBTLWNDEUAWJWPHIWGTLZWGWGUBZWGWKUCZDOZNWIWPWHWQWIWTWPWQW
        INZWTWPXAWSWODXAWSWLWNXAWSWLAWKBWGCEFDUDHUDZUEUFWQWSWNUGWIWSWQWNWSWMWGQ
        RZWQWSWMXCWSWMWGWKUHZQWSXDWGWSWRWGWKUIXDWGRWRWGWKUJWRWGWKUKULUMWMXDQUHQ
        WKQUNUOUPUQUFHURUSUTVAVBVCVDVEWHWQWTIWGVFWHWRWGKLZWTWHWRIKLZXEWHIIUBZWR
        KLZXFWHXHIWGIWGVGXBVGXBVHVIXHXGIKLZXFVJWRVKPXHXIXFVLWGWGXBXBVMXGWRIVKVN
        VOVPULWGVKPWHXFXEVLXBIWGWRVKVQVOVRWRWGDXBVSVTWAWBWCHIBGWDWNDEWEWF $.
        $( [?] $) $( [12-Sep-04] $)

      infxplem14a $p |- ( A. h e. H -. g (. h -> ( om ~<_ B ->
                        -. g = (/) ) ) $=
        ( vv cv wpss wn wral c0 wceq wrex com cdom wbr wcel wi wa psseq1 0pss
        syl6bb bicon2d weq psseq2 binegd rcla4v imp syl5bir com12 con3d exp
        r19.23adv infxplem13 syl5 ) DJZEJZKZLZEFMZIJZNOZLZIFPUSNOZLZQBRSVCVFVHI
        FVCVDFTZVFVHUAVCVIUBZVGVEVGVJVEVGVEUSVDKZLZVJVGVKVEVGVKNVDKVFUSNVDUCVDU
        DUEUFVCVIVLVBVLEVDFEIUGVAVKUTVDUSUHUIUJUKULUMUNUOUPABCIFGHUQUR $.
        $( [?] $) $( [16-Sep-04] $)

      $( Lemma for ~ infxpen . $)
      infxplem14 $p |- ( ( g e. H /\ A. h e. H -. g (. h ) -> ( om ~<_ B ->
         E. x ( ( om ~<_ x /\ x (_ B ) /\ g : ( x X. x ) -1-1-onto-> x ) ) ) $=
        ( cv wpss wn wral com cdom wbr c0 wceq wcel wss wa cxp wf1o wex
        infxplem14a wo visset infxplem1a biimp ord sylan9r ) EJZFJKLFGMNCOPULQR
        ZLULGSZNAJZOPUOCTUAUOUOUBUOULUCUAAUDZBCDEFGHIUEUNUMUPUNUMUPUFABULCDGHEU
        GUHUIUJUK $.
        $( [?] $) $( [13-Sep-04] $)

      $( Lemma for ~ infxpen . $)
      infxplem21 $p |- ( ( ( ( om ~<_ x /\ x (_ B ) /\ y (_ ( B \ x ) ) /\
          ( g u. u ) : ( ( x u. y ) X. ( x u. y ) ) -1-1-onto-> ( x u. y ) ) ->
            ( g u. u ) e. H ) $=
        ( com cv cun cdom wbr wss wa cxp wf1o wcel cdif visset unex infxplem1b
        cvv ssun1 ssdomg mp2 domtr mpan2 difss sstr2 imlan unss sylib im2an
        anassrs sylan ) KALZBLZMZNOZVAEPZQZVAVARVAGLZCLZMZSVGHTKUSNOZUSEPZQUTEU
        SUAZPZQDVGEVAFHIVEVFGUBCUBUCUSUTAUBZBUBUCUDVHVIVKVDVHVBVIVKQZVCVHUSVANO
        ZVBUSUETUSVAPVNVLUSUTUFUSVAUEUGUHKUSVAUIUJVMVIUTEPZQVCVKVOVIVKVJEPVOEUS
        UKUTVJEULUJUMUSUTEUNUOUPUQUR $.
        $( [?] $) $( [16-Sep-04] $)

      $( Lemma for ~ infxpen . COMBINE WITH ~ infxplem22 AND SHORTEN. $)
      infxplem22x $p |- ( ( ( ( ( om ~<_ x /\ x (_ B ) /\
                        g : ( x X. x ) -1-1-onto-> x ) /\
          u : ( ( x X. y ) u. ( ( y X. x ) u. ( y X. y ) ) ) -1-1-onto-> y ) /\
                        ( x ~~ y /\ y (_ ( B \ x ) ) ) -> E. h e. H g (. h ) $=
        ( cv cun wcel wpss wrex com cdom wbr wss wa cxp wf1o cen cdif psseq2
        rcla4ev infxplem21 pm3.26 ad2antll pm3.27 adantl jca cin c0 wceq
        infxplem18 imran sslin indif0 syl6ss ss0b sylib syl2an sylan13 wi
        infxplem20 exp43 com34 com12 syl com14 imp4a exp3a adantr imp41 ) GLZCL
        ZMZINZVQVSOZVQHLZOZHIPQALZRSZWDETZUAZWDWDUBWDVQUCZUAZWDBLZUBWJWDUBWJWJU
        BMMWJVRUCZUAZWDWJUDSZWJEWDUEZTZUAZUAZWCWAHVSIWBVSVQUFUGWGWOUAWDWJMZWRUB
        WRVSUCZVTWQABCDEFGIJKUHWQWGWOWIWGWKWPWGWHUIUJWPWOWLWMWOUKULUMWHWKUAZWDW
        JUNZUOUPZWSWLWPABCGUQWIWHWKWGWHUKURWOXBWMWOXAUOTXBWOXAWDWNUNUOWJWNWDUSW
        DEUTVAXAVBVCZULVDVEWGWHWKWPWAWEWHWKWPWAVFZVFVFWFWEWHWKXDWEWTWMWOWAWOWTW
        MWEWAWOXBWTWMWEWAVFVFZVFXCWTXBXEWTXBWEWMWAWTXBWEWMWAABCGVGVHVIVJVKVLVMV
        NVOVPVE $.
        $( [?] $) $( [16-Sep-04] $)

      $( Lemma for ~ infxpen . $)
      infxplem22 $p |- ( ( ( ( ( om ~<_ x /\ x (_ B ) /\
            g : ( x X. x ) -1-1-onto-> x ) /\ ( x ~~ y /\ y (_ ( B \ x ) ) ) /\
          u : ( ( x X. y ) u. ( ( y X. x ) u. ( y X. y ) ) ) -1-1-onto-> y ) ->
                        E. h e. H g (. h ) $=
        ( com cv cdom wbr wss wa cxp wf1o cun cen cdif wpss wrex infxplem22x
        an1rs ) LAMZNOUGEPQUGUGRUGGMZSQUGBMZRUIUGRUIUIRTTUICMSUGUIUAOUIEUGUBPQU
        HHMUCHIUDABCDEFGHIJKUEUF $.
        $( [?] $) $( [16-Sep-04] $)

      $( Lemma for ~ infxpen . $)
      infxplem23 $p |- ( ( ( ( om ~<_ x /\ x (_ B ) /\
                       g : ( x X. x ) -1-1-onto-> x ) /\
                       ( x ~~ y /\ y (_ ( B \ x ) ) ) -> E. h e. H g (. h ) $=
        ( vu com cv cdom wbr wss wa cxp wf1o cen cdif cun wex wpss wrex
        infxplem17 visset bren sylib xpex f1oen imlan adantlr sylan adantrr
        infxplem22 exp 19.23adv mpd ) LAMZNOZUTDPZQUTUTRZUTFMZSZQZUTBMZTOZVGDUT
        UAPZQQZUTVGRVGUTRVGVGRUBUBZVGKMSZKUCZVDGMUDGHUEZVFVHVMVIVAVCUTTOZQZVHVM
        VFVPVHQVKVGTOVMABUFVKVGKBUGUHUIVAVEVPVBVEVOVAVCUTVDUTUTAUGZVQUJUKULUMUN
        UOVJVLVNKVJVLVNABKCDEFGHIJUPUQURUS $.
        $( [?] $) $( [16-Sep-04] $)

      $( Lemma for ~ infxpen . $)
      infxplem24 $p |- ( ( ( om ~<_ x /\ x (_ B ) /\
                       g : ( x X. x ) -1-1-onto-> x ) ->
                       ( A. h e. H -. g (. h  ->  ( B \ x ) ~< x ) ) $=
        ( vy com cv cdom wbr wss wa cxp wf1o cdif csdm wpss wn wral wrex cen
        wex infxplem23 exp 19.23adv cvv wcel difexg ax-mp domen syl5ib wb
        visset domtri mp2an dfrex2 3imtr3g a3d ) KALZMNVCCOPVCVCQVCELZRPZCVCSZV
        CTNZVDFLUAZUBFGUCZVEVCVFMNZVHFGUDZVGUBZVIUBVEVCJLZUENVMVFOPZJUFVKVJVEVN
        VKJVEVNVKAJBCDEFGHIUGUHUIJVCVFCUJUKVFUJUKZICVCUJULUMZUNUOVCUJUKVOVJVLUP
        AUQVPVCVFUJUJURUSVHFGUTVAVB $.
        $( [?] $) $( [16-Sep-04] $)

      $( Lemma for ~ infxpen . $)
      infxplem27 $p |- ( om ~<_ B -> ( B X. B ) ~~ B ) $=
        ( vg vh vx cv wpss wn wral wrex com cdom wbr cxp cen wi infxplem12
        wcel c0 wceq infxplem14a wss wa wf1o wex wo visset infxplem1a df-or
        bitr cdif csdm infxplem24 infxplem26 exp41 com24 imp31 xpex f1oen
        sylan5 syld 19.23aiv syl3 sylbi com13 com3r r19.23aiv ax-mp ) GJZHJKLHD
        MZGDNOBPQZBBRBSQZTZABCGHDEFUAVNVQGDVNVOVMDUBZVPVNVOVMUCUDZLZVRVPTABCGHD
        EFUEVRVTVNVPVRVTOIJZPQZWABUFZUGZWAWARZWAVMUHZUGZIUIZTZVTVNVPTZTVRVSWHUJ
        WIIAVMBCDEGUKULVSWHUMUNWHWJVTWGWJIWGVNBWAUOWAUPQZVPIABCGHDEFUQWDWEWASQZ
        WKVPTZWFWBWCWLWMWBWKWLWCVPWBWKWLWCVPIBFURUSUTVAWEWAVMWAWAIUKZWNVBVCVDVE
        VFVGVHVIVEVJVKVL $.
        $( [?] $) $( [17-Sep-04] $)
    $}
  $}

  ${
    $d f x A $.
    infxp.1 $e |- A e. V $.
    $( The cross product of an infinite set is idempotent.  This theorem
       provides the basis for multiplication of infinite cardinal numbers.
       Lemma 6R of [Enderton] p. 162. $)
    infxp $p |- ( om ~<_ A -> ( A X. A ) ~~ A ) $=
      ( vx vf cv c0 wceq com cdom wbr wss wa cxp wf1o wex wo cab cleqid
      infxplem27 ) CADDEZFGHCEZIJUAAKLUAUAMUATNLCOPDQZUBRBS $.
      $( [?] $) $( [17-Sep-04] $)
  $}


