$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Infinite sequence builders "seq" and "seq0"
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f g h k x S $.  $d f g h k x F $.  $d k A $.  $d g h k x M $.  $d k N $.
    seq0fval.1 $e |- S e. _V $.
    seq0fval.2 $e |- F e. _V $.
    $( Value of the recursive sequence builder in terms of its _previous_
       value. $)
    seqzm1 $p |- ( ( M e. ZZ /\ N e. ZZ /\ M < N ) ->
                ( ( <. M , S >. seqz F ) ` N ) =
                ( ( ( <. M , S >. seqz F ) ` ( N - 1 ) ) S ( F ` N ) ) ) $=
      ( cz wcel clt wbr w3a c1 cmin co caddc cfv wceq 3ad2ant2 cc fveq2d seqzp1
      cop cseqz cle simp1 peano2zm zltlem1 biimp3a cuz eluz2 sylbir syl3anc zcn
      ax-1cn npcan sylancl opreq2d 3eqtr3d ) CGHZDGHZCDIJZKZDLMNZLONZCAUBBUCNZP
      ZVCVEPZVDBPZANZDVEPZVGDBPZANZVBUSVCGHZCVCUDJZVFVIQZUSUTVAUEUTUSVMVADUFRUS
      UTVAVNCDUGUHUSVMVNKVCCUIPHVOCVCUJABCVCEFUAUKULUTUSVFVJQVAUTVDDVEUTDSHLSHV
      DDQDUMUNDLUOUPZTRUTUSVIVLQVAUTVHVKVGAUTVDDBVPTUQRUR $.
      $( [1-Sep-2005] $)

    $( Value of the 0-based recursive sequence builder at 1. $)
    seq01 $p |- ( ( S seq0 F ) ` 1 ) = ( ( F ` 0 ) S ( F ` 1 ) ) $=
      ( cc0 c1 caddc co cseq0 cfv cn0 wcel wceq 0nn0 seq0p1 ax-mp ax-1cn fveq2i
      addid2i seq00 opreq12i 3eqtr3i ) EFGHZABIHZJZEUDJZUCBJZAHZFUDJEBJZFBJZAHE
      KLUEUHMNABECDOPUCFUDFQSZRUFUIUGUJAABCDTUCFBUKRUAUB $.
      $( [3-Aug-2005] $)
  $}

  ${
    $d S s f $.  $d F f $.  $d M s f $.
    $( Value of the arbitrary-based recursive sequence builder at its initial
       value.  (Contributed by Jeff Madsen, 7-Jan-2011.) $)
    seqz1g $p |- ( ( S e. A /\ F e. B /\ M e. ZZ ) ->
                              ( ( <. M , S >. seqz F ) ` M ) = ( F ` M ) ) $=
      ( vs vf wcel cz cop cseqz co cfv wceq cv wi opeq2 fveq1d imbi2d vex fveq1
      opreq1d eqeq1d opreq2 eqeq12d seqz1 vtocl2g 3impia ) CAHDBHEIHZEECJZDKLZM
      ZEDMZNZUIEEFOZJZGOZKLZMZEUQMZNZPUIEUJUQKLZMZUTNZPUIUNPFGCDABUOCNZVAVDUIVE
      USVCUTVEEURVBVEUPUJUQKUOCEQUBRUCSUQDNZVDUNUIVFVCULUTUMVFEVBUKUQDUJKUDREUQ
      DUAUESUOUQEFTGTUFUGUH $.
      $( [7-Jan-2011] $)
  $}

  ${
    $d S s f $.  $d F f $.  $d M s f $.  $d N s f $.
    $( Value of the arbitrary-based recursive seqeunce builder at a successor.
       (Contributed by Jeff Madsen, 7-Jan-2011.) $)
    seqzp1g $p |- ( ( S e. A /\ F e. B /\ N e. ( ZZ>= ` M ) ) ->
                        ( ( <. M , S >. seqz F ) ` ( N + 1 ) ) =
                ( ( ( <. M , S >. seqz F ) ` N ) S ( F ` ( N + 1 ) ) ) ) $=
      ( vs vf wcel cfv co cop cseqz wceq cv wi fveq1d eqeq12d imbi2d vex cuz c1
      caddc opeq2 opreq1d id eqidd opreq123d opreq2 fveq1 seqzp1 vtocl2g 3impia
      opreq12d ) CAIDBIFEUAJIZFUBUCKZECLZDMKZJZFURJZUPDJZCKZNZUOUPEGOZLZHOZMKZJ
      ZFVGJZUPVFJZVDKZNZPUOUPUQVFMKZJZFVMJZVJCKZNZPUOVCPGHCDABVDCNZVLVQUOVRVHVN
      VKVPVRUPVGVMVRVEUQVFMVDCEUDUEZQVRVIVOVJVJVDCVRUFVRFVGVMVSQVRVJUGUHRSVFDNZ
      VQVCUOVTVNUSVPVBVTUPVMURVFDUQMUIZQVTVOUTVJVACVTFVMURWAQUPVFDUJUNRSVDVFEFG
      THTUKULUM $.
      $( [7-Jan-2011] $)
  $}

  ${
    $d S s f $.  $d F f $.
    $( The 1-based recursive sequence in terms of the arbitrary-based one.
       (Contributed by Jeff Madsen, 7-Jan-2011.) $)
    seq1seqzg $p |- ( ( S e. A /\ F e. B ) ->
                                ( S seq1 F ) = ( <. 1 , S >. seqz F ) ) $=
      ( vs vf cv cseq1 co c1 cop cseqz wceq opreq1 opeq2 opreq1d eqeq12d opreq2
      vex seq1seqz vtocl2g ) EGZFGZHIZJUBKZUCLIZMCUCHIZJCKZUCLIZMCDHIZUHDLIZMEF
      CDABUBCMZUDUGUFUIUBCUCHNULUEUHUCLUBCJOPQUCDMUGUJUIUKUCDCHRUCDUHLRQUBUCESF
      STUA $.
      $( [7-Jan-2011] $)
  $}

  ${
    $d j k m F $.  $d j k m G $.  $d j k m M $.  $d j k N $.  $d j m S $.
    seqzfveq.1 $e |- S e. _V $.
    seqzfveq.2 $e |- F e. _V $.
    seqzfveq.3 $e |- G e. _V $.
    $( Equality theorem for the recursive sequence builder. $)
    seqzfveq $p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... N ) ( F ` k ) = ( G
  ` k ) ) -> ( ( <. M , S >. seqz F ) ` N ) = ( ( <. M , S >. seqz G ) ` N ) )
        $=
      ( cfv wcel wceq cfz co wral wi opreq2 raleqdv fveq2 eqeq12d vj vm cuz cop
      cv cseqz c1 caddc imbi12d cz wa elfz3 rcla4va sylan adantr 3eqtr4d fzssp1
      seqz1 ex sseld imim1d ralimdv2 id peano2uz eluzfz2 syl opreqan12rd seqzp1
      ad2antrr exp31 a2d syld uzind4 imp ) FEUCJZKBUEZCJZVPDJZLZBEFMNZOZFEAUDZC
      UFNZJZFWBDUFNZJZLZVSBEUAUEZMNZOZWHWCJZWHWEJZLZPVSBEEMNZOZEWCJZEWEJZLZPVSB
      EUBUEZMNZOZWSWCJZWSWEJZLZPZVSBEWSUGUHNZMNZOZXFWCJZXFWEJZLZPZWAWGPUAUBEFWH
      ELZWJWOWMWRXMVSBWIWNWHEEMQRXMWKWPWLWQWHEWCSWHEWESTUIWHWSLZWJXAWMXDXNVSBWI
      WTWHWSEMQRXNWKXBWLXCWHWSWCSWHWSWESTUIWHXFLZWJXHWMXKXOVSBWIXGWHXFEMQRXOWKX
      IWLXJWHXFWCSWHXFWESTUIWHFLZWJWAWMWGXPVSBWIVTWHFEMQRXPWKWDWLWFWHFWCSWHFWES
      TUIEUJKZWOWRXQWOUKECJZEDJZWPWQXQEWNKWOXRXSLZEULVSXTBEWNVPELVQXRVRXSVPECSV
      PEDSTUMUNXQWPXRLWOACEGHURUOXQWQXSLWOADEGIURUOUPUSWSVOKZXEXHXDPXLYAXHXAXDY
      AVSVSBXGWTYAVPWTKVPXGKVSYAWTXGVPEWSVOUQUTVAVBVAYAXHXDXKYAXHXDXKYAXHUKZXDU
      KXBXFCJZANZXCXFDJZANZXIXJXDYBXBXCYCYEAXDVCYAXFXGKZXHYCYELZYAXFVOKYGEWSVDE
      XFVEVFVSYHBXFXGVPXFLVQYCVRYEVPXFCSVPXFDSTUMUNVGYAXIYDLXHXDACEWSGHVHVIYAXJ
      YFLXHXDADEWSGIVHVIUPVJVKVLVMVN $.
      $( [2-Sep-2013] $) $( [3-Nov-2005] $)

    $( Equality theorem for the recursive sequence builder. $)
    seqzeq $p |- ( ( M e. ZZ /\ A. k e. ( ZZ>= ` M ) ( F ` k ) = ( G ` k ) ) ->
                 ( <. M , S >. seqz F ) = ( <. M , S >. seqz G ) ) $=
      ( vm cz wcel cv cfv wceq wral wa cseqz co wfn seqzfn cuz cop cfz seqzfveq
      elfzuz imim1i ralimi2 sylan2 expcom ralrimiv adantl wb jca adantr eqfnfv2
      syl mpbird ) EJKZBLZCMUSDMNZBEUAMZOZPZEAUBZCQRZVDDQRZNZILZVEMVHVFMNZIVAOZ
      VBVJURVBVIIVAVHVAKZVBVIVBVKUTBEVHUCRZOVIUTUTBVAVLUSVLKUSVAKUTUSEVHUEUFUGA
      BCDEVHFGHUDUHUIUJUKVCVEVASZVFVASZPZVGVJULURVOVBURVMVNACEFGTADEFHTUMUNIVAV
      EVFUOUPUQ $.
      $( [11-Dec-2005] $)
  $}

  ${
    $d B s $.  $d C s $.  $d F f g k s $.  $d G g k s $.  $d M f g k s $.
    $d N f g k s $.  $d S s $.
    $( Equality theorem for the recursive sequence builder. $)
    seqzfveqg $p |- ( ( ( S e. A /\ F e. B /\ G e. C ) /\
        ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... N ) ( F ` k ) = ( G ` k ) ) ) ->
      ( ( <. M , S >. seqz F ) ` N ) = ( ( <. M , S >. seqz G ) ` N ) ) $=
      ( vs vf wcel cfv cv wceq co wa cseqz wi fveq1d w3a cuz cfz wral cop opeq2
      vg opreq1d eqeq12d imbi2d eqeq1d ralbidv anbi2d opreq2 imbi12d eqeq2d vex
      fveq1 seqzfveq vtocl2g vtoclg 3impib imp ) DALZFBLZGCLZUAIHUBMLZENZFMZVHG
      MZOZEHIUCPZUDZQZIHDUEZFRPZMZIVOGRPZMZOZVDVEVFVNVTSZVEVFQZVNIHJNZUEZFRPZMZ
      IWDGRPZMZOZSZSWBWASJDAWCDOZWJWAWBWKWIVTVNWKWFVQWHVSWKIWEVPWKWDVOFRWCDHUFZ
      UHTWKIWGVRWKWDVOGRWLUHTUIUJUJVGVHKNZMZVHUGNZMZOZEVLUDZQZIWDWMRPZMZIWDWORP
      ZMZOZSVGVIWPOZEVLUDZQZWFXCOZSWJKUGFGBCWMFOZWSXGXDXHXIWRXFVGXIWQXEEVLXIWNV
      IWPVHWMFURUKULUMXIXAWFXCXIIWTWEWMFWDRUNTUKUOWOGOZXGVNXHWIXJXFVMVGXJXEVKEV
      LXJWPVJVIVHWOGURUPULUMXJXCWHWFXJIXBWGWOGWDRUNTUPUOWCEWMWOHIJUQKUQUGUQUSUT
      VAVBVC $.
      $( [17-Nov-2012] $)
  $}

  ${
    $d j m n v y B $.  $d j m n v y C $.  $d j k m n v y F $.
    $d j m n v y M $.  $d m N $.  $d j m n v y S $.
    seqzrn.1 $e |- S e. _V $.
    seqzrn.2 $e |- F e. _V $.
    $( Range of a sequence generated by the arbitrary-based recursive sequence
       builder. $)
    seqzrn2 $p |- ( ( ( M e. ZZ /\ ( F ` M ) e. C ) /\
       ( ( F |` ( ZZ>= ` ( M + 1 ) ) ) : ( ZZ>= ` ( M + 1 ) ) --> B /\
               S : ( C X. B ) --> C ) ) -> ran ( <. M , S >. seqz F ) C_ C ) $=
      ( vn vy vv wcel cfv co wa cv wi wceq fveq2 eleq1d imbi2d vj vm cz c1 cres
      caddc cuz wf cxp cop cseqz crn wss wfn wral seqzfn simpl syl5ibr eluzp1p1
      seqz1 ffvelrn wb fvres adantl mpbid sylan2 ffnoprv simprbi opreq1 rcla42v
      opreq2 ancoms syl5 imp seqzp1 exp4c ex com4r impcom com12 uzind4 ralrimiv
      mpd a2d fnfvrnss syl2an anassrs ) EUCKZEDLZBKZEUDUFMUGLZADWKUEZUHZBAUIZBC
      UHZNZECUJDUKMZULBUMZWHWQEUGLZUNHOZWQLZBKZHWSUOWRWJWPNZCDEFGUPXCXBHWSWTWSK
      XCXBXCUAOZWQLZBKZPXCEWQLZBKZPXCUBOZWQLZBKZPXCXIUDUFMZWQLZBKZPXCXBPUAUBEWT
      XDEQZXFXHXCXOXEXGBXDEWQRSTXDXIQZXFXKXCXPXEXJBXDXIWQRSTXDXLQZXFXNXCXQXEXMB
      XDXLWQRSTXDWTQZXFXBXCXRXEXABXDWTWQRSTXCXHWHWJWJWPUQWHXGWIBCDEFGUTSURXIWSK
      ZXCXKXNXCXSXKXNPZWPXSXTPZWJWOWMYAWMXSXKWOXNWMXSXKWOXNPPZWMXSNXLDLZAKZYBXS
      WMXLWKKZYDEXIUSWMYENXLWLLZAKZYDWKAXLWLVAYEYGYDVBWMYEYFYCAXLWKDVCSVDVEVFXS
      YDYBPWMXSYDXKWOXNYDXKNZWONXNXSXJYCCMZBKZYHWOYJWOIOZJOZCMZBKZJAUOIBUOZYHYJ
      WOCWNUNYOIJBABCVGVHXKYDYOYJPYNYJXJYLCMZBKIJXJYCBAYKXJQYMYPBYKXJYLCVISYLYC
      QYPYIBYLYCXJCVKSVJVLVMVNXSXMYIBCDEXIFGVOSURVPVDWCVQVRVSVDVTWDWAVTWBHWSBWQ
      WEWFWG $.
      $( [7-Sep-2005] $)

    $( Range of the arbitrary-based recursive sequence builder (special case of
       ~ seqzrn2 ). $)
    seqzrn $p |- ( ( M e. ZZ /\ F : ( ZZ>= ` M ) --> C /\
                 S : ( C X. C ) --> C ) -> ran ( <. M , S >. seqz F ) C_ C ) $=
      ( cz wcel cuz cfv wf cxp cop cseqz co crn wss wa sylan2 syl c1 caddc cres
      uzid ffvelrn peano2uz fssres jca expcom seqzrn2 exp43 imp4c syland 3impib
      uzss ) DGHZDIJZACKZAALABKZDBMCNOPAQZUPURDCJAHZDUAUBOZIJZACVCUCKZRZUSUTURU
      PVEURUPRVAVDUPURDUQHZVADUDZUQADCUESUPURVCUQQZVDUPVBUQHZVHUPVFVIVGDDUFTDVB
      UOTUQAVCCUGSUHUIUPVAVDUSUTUPVAVDUSUTAABCDEFUJUKULUMUN $.
      $( [7-Sep-2005] $)

    $( Closure of the value of the arbitrary-based recursive sequence
       builder. $)
    seqzcl $p |- ( ( N e. ( ZZ>= ` M ) /\ F : ( ZZ>= ` M ) --> C /\
              S : ( C X. C ) --> C ) -> ( ( <. M , S >. seqz F ) ` N ) e. C )
        $=
      ( cuz cfv wcel wf cxp w3a cop cseqz co crn cz wss eluzel2 syl3an1 wfn syl
      seqzrn seqzfn fnfvelrn mpancom 3ad2ant1 sseldd ) EDHIZJZUJACKZAALABKZMDBN
      COPZQZAEUNIZUKDRJZULUMUOASDETZABCDFGUDUAUKULUPUOJZUMUNUJUBZUKUSUKUQUTURBC
      DFGUEUCUJEUNUFUGUHUI $.
      $( [7-Sep-2005] $)

    $( A restriction of its characteristic function that doesn't change the
       value of the ` seqz ` function. $)
    seqzresval $p |- ( N e. ( ZZ>= ` M ) -> ( ( <. M , S >. seqz
              ( F |` ( M ... N ) ) ) ` N ) = ( ( <. M , S >. seqz F ) ` N ) )
        $=
      ( vm cuz cfv wcel cv cfz co cres wceq wral cop cseqz fvres rgen seqzfveq
      resex mpan2 ) DCHIJGKZBCDLMZNZIUDBIOZGUEPDCAQZUFRMIDUHBRMIOUGGUEUDUEBSTAG
      UFBCDEBUEFUBFUAUC $.
      $( [2-Nov-2005] $)

    $( The ` seqz ` function is unchanged by restricting its characteristic
       function to the ` seqz ` function's domain. $)
    seqzres $p |- ( M e. ZZ -> ( <. M , S >. seqz ( F |` ( ZZ>= ` M ) ) ) =
                     ( <. M , S >. seqz F ) ) $=
      ( vn vm cz wcel cop cuz cfv cseqz co wfn wceq seqzfn cv wral rgen cres wa
      resex cfz elfzuz fvres syl seqzfveq mpan2 eqfnfv2 mpbiri syl2anc ) CHICAJ
      ZBCKLZUAZMNZUNOZUMBMNZUNOZUPURPZAUOCDBUNEUCZQABCDEQUQUSUBUTFRZUPLVBURLPZF
      UNSVCFUNVBUNIGRZUOLVDBLPZGCVBUDNZSVCVEGVFVDVFIVDUNIVEVDCVBUEVDUNBUFUGTAGU
      OBCVBDVAEUHUITFUNUPURUJUKUL $.
      $( [7-Sep-2005] $)

    $( The ` seqz ` function is unchanged by substituting its characteristic
       function with a restricted class builder based on that function. $)
    seqzres2 $p |- ( M e. ZZ -> ( <. M , S >. seqz ( { <. k , y >. | y =
                   ( F ` k ) } |` ZZ ) ) = ( <. M , S >. seqz F ) ) $=
      ( vn vv cz wcel cv cfv wceq copab cseqz co wa wfn seqzfn cop cres resopab
      opreq2i cuz zex opabex2 wral cfz elfzelz fveq2 eqid fvex fvopab4 syl rgen
      seqzfveq mpan2 eqfnfv2 mpbiri syl2anc syl5eq ) EJKZEBUAZALCLZDMZNZCAOJUBZ
      PQVDVEJKVGRCAOZPQZVDDPQZVHVIVDPVGCAJUCUDVCVJEUEMZSZVKVLSZVJVKNZBVIEFCAJVF
      UFUGZTBDEFGTVMVNRVOHLZVJMVQVKMNZHVLUHVRHVLVQVLKILZVIMVSDMZNZIEVQUIQZUHVRW
      AIWBVSWBKVSJKWAVSEVQUJCAVSVFVTJVIVEVSDUKVIULVSDUMUNUOUPBIVIDEVQFVPGUQURUP
      HVLVJVKUSUTVAVB $.
      $( [1-Nov-2005] $)
  $}

  ${
    seq0cl.1 $e |- S e. _V $.
    seq0cl.2 $e |- F e. _V $.
    $( Closure of the value of the 0-based recursive sequence builder.
       (Contributed by Paul Chapman, 31-Mar-2011.) $)
    seq0cl $p |- ( ( N e. NN0 /\ F : NN0 --> C /\ S : ( C X. C ) --> C ) ->
                   ( ( S seq0 F ) ` N ) e. C ) $=
      ( cn0 wcel wf cxp w3a cseq0 co cfv cc0 cop cseqz seq0seqz fveq1i cuz
      nn0uz feq2i elnn0uz seqzcl syl3an1b syl3an2b syl5eqel ) DGHZGACIZAAJABIZK
      DBCLMZNDOBPCQMZNZADUKULBCEFRSUIUHOTNZACIZUJUMAHZGUNACUAUBUHDUNHUOUJUPDUCA
      BCODEFUDUEUFUG $.
      $( [31-Mar-2011] $)
  $}

  ${
    $d A k m x y z $.  $d F k m x y z $.  $d M k m x y z $.  $d N m z $.
    $d S k m x y $.
    seqzalcl.1 $e |- S e. _V $.
    seqzalcl.2 $e |- F e. _V $.
    seqzalcl.3 $e |- ( ( x e. A /\ y e. A ) -> ( x S y ) e. A ) $.
    $( Closure of the value of the arbitrary-based recursive sequence builder.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    seqzalcl $p |- ( ( N e. ( ZZ>= ` M ) /\ A. z e. ( M ... N )
        ( F ` z ) e. A ) -> ( ( <. M , S >. seqz F ) ` N ) e. A ) $=
      ( cfv wcel cfz co wral wi wceq fveq2 eleq1d vm vk cuz cv cop cseqz opreq2
      c1 caddc raleqdv imbi12d cz elfz3 rcla4v seqz1 sylibrd wa seqzp1 ad2antrr
      syl peano2uz eluzfz2 3syl imp rgen2 opreq1 rcla42v mpi ancoms sylan exp31
      eqeltrd wb wss fzssp1 ssralv imim21b mpbird uzind4 ) HGUCLZMCUDZFLZDMZCGH
      NOZPZHGEUEFUFOZLZDMZWCCGUAUDZNOZPZWIWFLZDMZQWCCGGNOZPZGWFLZDMZQWCCGUBUDZN
      OZPZWRWFLZDMZQZWCCGWRUHUIOZNOZPZXDWFLZDMZQZWEWHQUAUBGHWIGRZWKWOWMWQXJWCCW
      JWNWIGGNUGUJXJWLWPDWIGWFSTUKWIWRRZWKWTWMXBXKWCCWJWSWIWRGNUGUJXKWLXADWIWRW
      FSTUKWIXDRZWKXFWMXHXLWCCWJXEWIXDGNUGUJXLWLXGDWIXDWFSTUKWIHRZWKWEWMWHXMWCC
      WJWDWIHGNUGUJXMWLWGDWIHWFSTUKGULMZWOGFLZDMZWQXNGWNMWOXPQGUMWCXPCGWNWAGRWB
      XODWAGFSTUNUTXNWPXODEFGIJUOTUPWRVTMZXCXIQZXFXBXHQQZXQXFXBXHXQXFUQZXBUQXGX
      AXDFLZEOZDXQXGYBRXFXBEFGWRIJURUSXTYADMZXBYBDMZXQXFYCXQXDVTMXDXEMXFYCQGWRV
      AGXDVBWCYCCXDXEWAXDRWBYADWAXDFSTUNVCVDXBYCYDXBYCUQAUDZBUDZEOZDMZBDPADPYDY
      HABDDKVEYHYDXAYFEOZDMABXAYADDYEXARYGYIDYEXAYFEVFTYFYARYIYBDYFYAXAEUGTVGVH
      VIVJVLVKXQXFWTQZXRXSVMXQWSXEVNYJGWRVTVOWCCWSXEVPUTWTXFXBXHVQUTVRVSVD $.
      $( [2-Sep-2013] $) $( [26-Oct-2012] $)

    $( Closure of the value of the 1-based recursive sequence builder.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    seq1alcl $p |- ( ( N e. NN /\ A. z e. ( 1 ... N ) ( F ` z ) e. A ) ->
                     ( ( S seq1 F ) ` N ) e. A ) $=
      ( cn wcel cv cfv c1 cfz co wral wa cseq1 cop cseqz seq1seqz fveq1i elnnuz
      cuz seqzalcl sylanb syl5eqel ) GKLZCMFNDLCOGPQRZSGEFTQZNGOEUAFUBQZNZDGULU
      MEFHIUCUDUJGOUFNLUKUNDLGUEABCDEFOGHIJUGUHUI $.
      $( [26-Oct-2012] $)
  $}

  ${
    $d F k $.  $d K k $.  $d M k $.  $d N k $.
    $( A restriction of its characteristic function that doesn't change the
       value of the ` seqz ` function.  (Contributed by Paul Chapman,
       17-Nov-2012.) $)
    seqzresval2g $p |- ( ( ( S e. A /\ F e. B ) /\
                           ( N e. ( ZZ>= ` M ) /\ K e. ( M ... N ) ) ) ->
                         ( ( <. M , S >. seqz ( F |` ( M ... N ) ) ) ` K ) =
                         ( ( <. M , S >. seqz F ) ` K ) ) $=
      ( vk wcel wa cuz cfv cfz co cop cseqz cres cvv wceq eqcomd w3a wral simpl
      cv simpr resexg adantl 3jca elfzuzb biimpa simpld wss elfzuz3 fzss2 sseld
      syl fvres syl6 ralrimiv jca seqzfveqg syl2an ) CAIZDBIZJZGFKLZIZEFGMNZIZJ
      ZJEFCOZDPNLZEVKDVHQZPNLZVEVCVDVMRIZUAEVFIZHUDZDLZVQVMLZSZHFEMNZUBZJVLVNSV
      JVEVCVDVOVCVDUCVCVDUEVDVOVCDVHBUFUGUHVJVPWBVJVPGEKLIZVGVIVPWCJVFEFGUIUJUK
      VJVTHWAVJVQWAIVQVHIZVTVJWAVHVQVJWCWAVHULVFEFGUMEFGUNUPUOWDVSVRVQVHDUQTURU
      SUTABRCHDVMFEVAVBT $.
      $( [2-Sep-2013] $) $( [17-Nov-2012] $)
  $}

  $( A restriction of its characteristic function that doesn't change the value
     of the ` seq1 ` function.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
  seq1resval2g $p |- ( ( ( S e. A /\ F e. B ) /\
                          ( N e. NN /\ K e. ( 1 ... N ) ) ) ->
      ( ( S seq1 ( F |` ( 1 ... N ) ) ) ` K ) = ( ( S seq1 F ) ` K ) ) $=
    ( cn wcel wa c1 cuz cfv cfz co cseq1 wceq cseqz cvv seq1seqzg fveq1d elnnuz
    cres biimpi cop seqzresval2g wb resexg sylan2 eqeq12d adantr mpbird sylanr1
    ) FGHZCAHZDBHZIZFJKLHZEJFMNZHZECDURUBZONZLZECDONZLZPZUMUQFUAUCUPUQUSIZIVEEJ
    CUDZUTQNZLZEVGDQNZLZPZABCDEJFUEUPVEVLUFVFUPVBVIVDVKUPEVAVHUOUNUTRHVAVHPDURB
    UGARCUTSUHTUPEVCVJABCDSTUIUJUKUL $.
    $( [17-Nov-2012] $)

  ${
    $d k M $.  $d k F $.
    serzcl1.1 $e |- F : ( ZZ>= ` M ) --> CC $.
    $( The partial sums in an infinite series of complex terms are complex. $)
    serzcl1i $p |- ( N e. ( ZZ>= ` M ) ->
                 ( ( <. M , + >. seqz F ) ` N ) e. CC ) $=
      ( cuz cfv wcel cc wf cxp caddc cop cseqz co ax-addopr addex cvv fex mp2an
      fvex seqzcl mp3an23 ) CBEFZGUCHAIZHHJHKICBKLAMNFHGDOHKABCPUDUCQGAQGDBETUC
      HQARSUAUB $.
      $( [8-Sep-2005] $)
  $}

  ${
    $d j N $.  $d j k F $.
    ser0cl.1 $e |- F : NN0 --> CC $.
    $( The partial sums in an infinite 0-based series of complex terms are
       complex. $)
    ser0cl1i $p |- ( N e. NN0 -> ( ( + seq0 F ) ` N ) e. CC ) $=
      ( vj vk cv caddc cseq0 co cfv cc wcel cc0 wceq fveq2 eleq1d addex cn0 cvv
      ffvelrni c1 wf nn0ex fex mp2an seq00 0nn0 eqeltri seq0p1 adantr peano2nn0
      ax-mp wa syl addcl sylan2 ancoms eqeltrd ex nn0ind ) DFZGAHIZJZKLMVBJZKLE
      FZVBJZKLZVEUAGIZVBJZKLZBVBJZKLDEBVAMNVCVDKVAMVBOPVAVENVCVFKVAVEVBOPVAVHNV
      CVIKVAVHVBOPVABNVCVKKVABVBOPVDMAJZKGAQRKAUBRSLASLCUCRKSAUDUEZUFMRLVLKLUGR
      KMACTULUHVERLZVGVJVNVGUMVIVFVHAJZGIZKVNVIVPNVGGAVEQVMUIUJVGVNVPKLZVNVGVOK
      LZVQVNVHRLVRVEUKRKVHACTUNVFVOUOUPUQURUSUT $.
      $( [17-Apr-2006] $) $( [19-Aug-2005] $)

    $( A 0-based infinite series is a function from ` NN0 ` to ` CC ` . $)
    ser0fi $p |- ( + seq0 F ) : NN0 --> CC $=
      ( vj cn0 cc caddc cseq0 co wf wfn cfv wcel wral ffnfv addex cvv nn0ex fex
      cv mp2an seq0fn ser0cl1i rgen mpbir2an ) DEFAGHZIUEDJCSZUEKELZCDMCDEUENFA
      ODEAIDPLAPLBQDEPARTUAUGCDAUFBUBUCUD $.
      $( [24-Apr-2006] $) $( [19-Aug-2005] $)
  $}

  ${
    $d y A $.  $d k y B $.  $d k y N $.
    ser00.1 $e |- F = { <. k , y >. | ( k e. NN0 /\ y = A ) } $.
    ser00.2 $e |- B e. _V $.
    ${
      ser00.3 $e |- ( k = 0 -> A = B ) $.
      $( The value of the first term in a 0-based infinite series. $)
      ser00i $p |- ( ( + seq0 F ) ` 0 ) = B $=
        ( cc0 caddc cseq0 co cfv addex cn0 nn0ex fopabex2 seq00 wcel wceq ax-mp
        0nn0 fvopab4 eqtri ) IJEKLMIEMZCJENDAOBEPFQRIOSUECTUBDAIBCOEHFGUCUAUD
        $.
        $( [13-May-2006] $)
    $}

    ${
      ser0p1.3 $e |- ( k = ( N + 1 ) -> A = B ) $.
      $( The value of the next term in a 0-based infinite series. $)
      ser0p1i $p |- ( N e. NN0 ->
               ( ( + seq0 F ) ` ( N + 1 ) ) = ( ( ( + seq0 F ) ` N ) + B ) ) $=
        ( cn0 wcel c1 caddc co cseq0 cfv addex nn0ex fopabex2 seq0p1 wceq eqtrd
        peano2nn0 fvopab4 syl opreq2d ) FJKZFLMNZMEONZPFUIPZUHEPZMNUJCMNMEFQDAJ
        BERGSTUGUKCUJMUGUHJKUKCUAFUCDAUHBCJEIGHUDUEUFUB $.
        $( [13-May-2006] $)
    $}
  $}

