$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordered pair theorem for nonnegative integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    nn0le2sqet.1 $e |- A e. NN0 $.
    nn0le2sqet.2 $e |- B e. NN0 $.
    $( Two nonnegative integers compare the same as their squares.
       (Contributed by Raph Levien, 10-Dec-02.) $)
    nn0le2sqet $p |- ( A <_ B <-> ( A x. A ) <_ ( B x. B ) ) $=
      ( cle wbr c2 cexp co cmulc cc0 wb nn0ge0i nn0re le2sqe mp2an nn0cn sqval
      breq12i bitr ) ABEFZAGHIZBGHIZEFZAAJIZBBJIZEFKAEFKBEFUAUDLACMBDMABACNBDNO
      PUBUEUCUFEAACQRBBDQRST $.
      $( [14-May-03] $)
  $}

  ${
    nn0opthlem1.1 $e |- A e. NN0 $.
    nn0opthlem1.2 $e |- C e. NN0 $.
    $( A rather pretty lemma for ~ nn0opth .  (Contributed by Raph Levien,
      10-Dec-02.) $)
    nn0opthlem1 $p |- ( A < C <-> ( ( A x. A ) + ( 2 x. A ) ) < ( C x. C ) ) $=
      ( c1 caddc co cle wbr cmulc clt c2 1nn0 nn0addcl nn0le2sqet cn0 wcel wb
      nn0ltle1 mp2an nn0mulcl 2nn0 cexp nn0cn 1cn binom addcl sqval opreq1i
      opreq12i 3eqtr3 mulid1 opreq2i eqtr breq1i bitr4 3bitr4 ) AEFGZBHIZURURJG
      ZBBJGZHIZABKIZAAJGZLAJGZFGZVAKIZURBAECMNDOAPQBPQVCUSRCDABSTVGVFEFGZVAHIZV
      BVFPQVAPQVGVIRVDVEAACCUALAUBCUANBBDDUAVFVASTUTVHVAHUTVDLAEJGZJGZFGZEEJGZF
      GZVHURLUCGALUCGZVKFGZELUCGZFGUTVNAEACUDZUEUFURAEVRUEUGUHVPVLVQVMFVOVDVKFA
      VRUHUIEUEUHUJUKVLVFVMEFVKVEVDFVJALJAVRULUMUMEUEULUJUNUOUPUQ $.
      $( [14-May-03] $)
  $}

  ${
    nn0opth.1 $e |- A e. NN0 $.
    nn0opth.2 $e |- B e. NN0 $.
    nn0opth.3 $e |- C e. NN0 $.
    nn0opth.4 $e |- D e. NN0 $.
    $( Lemma for ~ nn0opth .  (Contributed by Raph Levien, 10-Dec-02.) $)
    nn0opthlem2 $p |- ( ( B <_ A /\ D <_ C ) ->
              ( A < C -> -. ( ( A x. A ) + B ) = ( ( C x. C ) + D ) ) ) $=
      ( cle wbr wa clt cmulc co caddc wceq wn wi c2 nn0re mulrcl addrcl 2re
      lelttr nn0lele2x leadd2 sylib nn0opthlem1 biimp syl2an cc0 recn addid1
      nn0ge0i ax0re mpbi eqbrtrr ltletr mpan2 syl exp adantr ltne syl6 ) BAIJZD
      CIJZKACLJZAAMNZBONZCCMNZDONZLJZVIVKPQVEVGVLRVFVEVGVLVEVGKVIVJLJZVLVIVHSAM
      NZONZIJZVOVJLJZVMVEVGVIVOVJVHBAAAETZVRUAZBFTZUBZVHVNVSSAUCVRUAZUBCCCGTZWC
      UAZUDVEBVNIJVPABEFUEBVNVHVTWBVSUFUGVGVQACEGUHUIUJVMVJVKIJVLVJUKONZVJVKIVJ
      VJWDULUMUKDIJWEVKIJDHUNUKDVJUODHTZWDUFUPUQVIVJVKWAWDVJDWDWFUBZURUSUTVAVBV
      IVKWAWGVCVD $.
      $( [14-May-03] $)

    $( An ordered pair theorem for nonnegative integers.  Theorem 17.3 of
       [Quine] p. 124.  We can represent an ordered pair of nonnegative
       integers ` A ` and ` B ` by ` ( ( ( A + B ) x. ( A + B ) ) + B ) ` . If
       two such ordered pairs are equal, their first elements are equal and
       their second elements are equal.  Contrast this ordered pair
       representation with the standard one ~ df-op that works for any set.
       (Contributed by Raph Levien, 10-Dec-02.)  $)
    nn0opth $p |- ( ( ( ( A + B ) x. ( A + B ) ) + B ) =
            ( ( ( C + D ) x. ( C + D ) ) + D ) <-> ( A = C /\ B = D ) ) $=
      ( caddc co cmulc wceq wa cle wbr wi nn0addge2 clt wo wn nn0addcl
      nn0opthlem2 ancoms cleqcom bineg syl6ibr jaod nn0re addrcl lttri2 syl5ib
      a3d mp2an id opreq12d opreq1d eqtr4d nn0cn addcl mulcl addcan sylib
      opreq2d addcan2 jca opreq12 pm3.27 impbi ) ABIJZVIKJZBIJZCDIJZVLKJZDIJZLZ
      ACLZBDLZMZVOVPVQVOVICBIJZLVPVOVIVLVSBVINOZDVLNOZVOVIVLLZPABEFQCDGHQVTWAMZ
      WBVOWCVIVLROZVLVIROZSVOTZWBTWCWDWFWEVIBVLDABEFUAZFCDGHUAZHUBWCWEVNVKLZTZW
      FWAVTWEWJPVLDVIBWHHWGFUBUCVOWIVKVNUDUEUFUGVIVLABAEUHBFUHUIVLWHUHUJUKULUMZ
      VOBDCIVOVKVJDIJZLVQVOVKVNWLVOUNVOVJVMDIVOVIVLVIVLKWKWKUOUPUQVJBDVIVIABAEU
      RZBFURZUSZWOUTWNDHURVAVBZVCUQACBWMCGURWNVDVBWPVEVRVJVMBDIVRVIVLVIVLKACBDI
      VFZWQUOVPVQVGUOVH $.
      $( [14-May-03] $)

    $( An ordered pair theorem for nonnegative integers.  Theorem 17.3 of
       [Quine] p. 124.  See ~ nn0opth .)  $)
    nn0opth2 $p |- ( ( ( ( A + B ) ^ 2 ) + B ) = ( ( ( C + D ) ^ 2 ) + D ) <->
                   ( A = C /\ B = D ) ) $=
      ( caddc co c2 cexp wceq cmulc wa nn0cn addcl sqval opreq1i cleq12i
      nn0opth bitr ) ABIJZKLJZBIJZCDIJZKLJZDIJZMUCUCNJZBIJZUFUFNJZDIJZMACMBDMOU
      EUJUHULUDUIBIUCABAEPBFPQRSUGUKDIUFCDCGPDHPQRSTABCDEFGHUAUB $.
      $( [?] $) $( [22-Jul-04] $)
  $}

  $( An ordered pair theorem for nonnegative integers.  Theorem 17.3 of
     [Quine] p. 124.  See ~ nn0opth .  $)
  nn0opth2t $p |- ( ( ( A e. NN0 /\ B e. NN0 ) /\ ( C e. NN0 /\ D e. NN0 ) ) ->
          ( ( ( ( A + B ) ^ 2 ) + B ) = ( ( ( C + D ) ^ 2 ) + D ) <->
                   ( A = C /\ B = D ) ) ) $=
    ( cn0 wcel caddc co c2 cexp wceq wa wb cc0 cded opreq1 opreq1d cleq1d
    cleq1 birand bi2bid opreq2 id opreq12d biland cleq2d cleq2 0nn0 elimel
    nn0opth2 dedth4h ) AEFZBEFZCEFZDEFZABGHZIJHZBGHZCDGHZIJHZDGHZKZACKZBDKZLZMA
    BCDNNULANOZBGHZIJHZBGHZVAKZVFCKZVDLZMVFUMBNOZGHZIJHZVMGHZVAKZVKVMDKZLZMVPUN
    CNOZDGHZIJHZDGHZKZVFVTKZVRLZMVPVTUODNOZGHZIJHZWGGHZKZWEVMWGKZLZMNNAVFKZVBVJ
    VEVLWNURVIVAWNUQVHBGWNUPVGIJAVFBGPQQRWNVCVKVDAVFCSTUABVMKZVJVQVLVSWOVIVPVAW
    OVHVOBVMGWOVGVNIJBVMVFGUBQWOUCUDRWOVDVRVKBVMDSUEUACVTKZVQWDVSWFWPVAWCVPWPUT
    WBDGWPUSWAIJCVTDGPQQUFWPVKWEVRCVTVFUGTUADWGKZWDWKWFWMWQWCWJVPWQWBWIDWGGWQWA
    WHIJDWGVTGUBQWQUCUDUFWQVRWLWEDWGVMUGUEUAVFVMVTWGANEUHUIBNEUHUICNEUHUIDNEUHU
    IUJUK $.
    $( [?] $) $( [22-Jul-04] $)

