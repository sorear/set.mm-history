$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordered pair theorem for nonnegative integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    nn0le2msqi.1 $e |- A e. NN0 $.
    nn0le2msqi.2 $e |- B e. NN0 $.
    $( The square function on nonnegative integers is monotonic.  (Contributed
       by Raph Levien, 10-Dec-2002.) $)
    nn0le2msqi $p |- ( A <_ B <-> ( A x. A ) <_ ( B x. B ) ) $=
      ( cle wbr c2 cexp co cmul cc0 nn0ge0i nn0rei le2sqi nn0cni sqvali breq12i
      wb mp2an bitri ) ABEFZAGHIZBGHIZEFZAAJIZBBJIZEFKAEFKBEFUAUDRACLBDLABACMBD
      MNSUBUEUCUFEAACOPBBDOPQT $.
      $( [10-Dec-2002] $)
  $}

  ${
    nn0opthlem1.1 $e |- A e. NN0 $.
    nn0opthlem1.2 $e |- C e. NN0 $.
    $( A rather pretty lemma for ~ nn0opthi .  (Contributed by Raph Levien,
       10-Dec-2002.) $)
    nn0opthlem1 $p |- ( A < C <-> ( ( A x. A ) + ( 2 x. A ) ) < ( C x. C ) ) $=
      ( c1 caddc co cle wbr cmul clt c2 nn0addcli wcel wb nn0mulcli cexp ax-1cn
      cn0 sqvali 1nn0 nn0le2msqi nn0ltp1le mp2an nn0cni binom2i addcli opreq12i
      2nn0 opreq1i 3eqtr3i mulid1i opreq2i eqtri breq1i bitr4i 3bitr4i ) AEFGZB
      HIZURURJGZBBJGZHIZABKIZAAJGZLAJGZFGZVAKIZURBAECUAMDUBASNBSNVCUSOCDABUCUDV
      GVFEFGZVAHIZVBVFSNVASNVGVIOVDVEAACCPLAUICPMBBDDPVFVAUCUDUTVHVAHUTVDLAEJGZ
      JGZFGZEEJGZFGZVHURLQGALQGZVKFGZELQGZFGUTVNAEACUEZRUFURAEVRRUGTVPVLVQVMFVO
      VDVKFAVRTUJERTUHUKVLVFVMEFVKVEVDFVJALJAVRULUMUMERULUHUNUOUPUQ $.
      $( [10-Dec-2002] $)
  $}

  ${
    nn0opth.1 $e |- A e. NN0 $.
    nn0opth.2 $e |- B e. NN0 $.
    nn0opth.3 $e |- C e. NN0 $.
    nn0opth.4 $e |- D e. NN0 $.
    $( Lemma for ~ nn0opthi . $)
    nn0opthlem2 $p |- ( ( A + B ) < C
           -> ( ( C x. C ) + D ) =/= ( ( ( A + B ) x. ( A + B ) ) + B ) ) $=
      ( caddc co clt wbr cmul wne c2 nn0addcli cle nn0rei remulcli readdcli 2re
      nn0opthlem1 nn0addge2i nn0lele2xi leadd2i lelttri mpan nn0addge1i ltletri
      sylib ax-mp sylbi mpan2 ltnei 3syl ) ABIJZCKLZUPUPMJZBIJZCCMJZKLZUSUTDIJZ
      KLZVBUSNUQUROUPMJZIJZUTKLZVAUPCABEFPZGUBUSVEQLZVFVABUPQLZVHBABFRZEUCVIBVD
      QLVHUPBVGFUDBVDURVJOUPUAUPVGRZSZUPUPVKVKSZUEUJUKUSVEUTURBVMVJTZURVDVMVLTC
      CCGRZVOSZUFUGULVAUTVBQLVCUTDVPHUHUSUTVBVNVPUTDVPDHRTZUIUMUSVBVNVQUNUO $.
      $( [8-Sep-2010] $) $( [14-May-2003] $)

    $( An ordered pair theorem for nonnegative integers.  Theorem 17.3 of
       [Quine] p. 124.  We can represent an ordered pair of nonnegative
       integers ` A ` and ` B ` by ` ( ( ( A + B ) x. ( A + B ) ) + B ) ` .  If
       two such ordered pairs are equal, their first elements are equal and
       their second elements are equal.  Contrast this ordered pair
       representation with the standard one ~ df-op that works for any set.
       (Contributed by Raph Levien, 10-Dec-2002.  Proof shortened by Scott
       Fenton, 7-Sep-2010.) $)
    nn0opthi $p |- ( ( ( ( A + B ) x. ( A + B ) ) + B ) =
            ( ( ( C + D ) x. ( C + D ) ) + D ) <-> ( A = C /\ B = D ) ) $=
      ( caddc co cmul wceq wne clt nn0addcli nn0rei nn0opthlem2 opreq12d nn0cni
      wbr wa wo lttri2i necomd jaoi sylbi necon4i opreq1d eqtr4d mulcli addcani
      id sylib opreq2d addcan2i jca opreq12 simpr impbii ) ABIJZUTKJZBIJZCDIJZV
      CKJZDIJZLZACLZBDLZUAZVFVGVHVFUTCBIJZLVGVFUTVCVJUTVCVBVEUTVCMUTVCNTZVCUTNT
      ZUBVBVEMZUTVCUTABEFOZPVCCDGHOZPUCVKVMVLVKVEVBABVCDEFVOHQUDCDUTBGHVNFQUEUF
      UGZVFBDCIVFVBVADIJZLVHVFVBVEVQVFULVFVAVDDIVFUTVCUTVCKVPVPRUHUIVABDUTUTUTV
      NSZVRUJBFSZDHSUKUMZUNUIACBAESCGSVSUOUMVTUPVIVAVDBDIVIUTVCUTVCKACBDIUQZWAR
      VGVHURRUS $.
      $( [8-Sep-2010] $) $( [10-Dec-2002] $)

    $( An ordered pair theorem for nonnegative integers.  Theorem 17.3 of
       [Quine] p. 124.  See comments for ~ nn0opthi . $)
    nn0opth2i $p |- ( ( ( ( A + B ) ^ 2 ) + B ) = ( ( ( C + D ) ^ 2 ) + D ) <->
                   ( A = C /\ B = D ) ) $=
      ( caddc co c2 cexp wceq cmul wa nn0cni addcli sqvali opreq1i eqeq12i
      nn0opthi bitri ) ABIJZKLJZBIJZCDIJZKLJZDIJZMUCUCNJZBIJZUFUFNJZDIJZMACMBDM
      OUEUJUHULUDUIBIUCABAEPBFPQRSUGUKDIUFCDCGPDHPQRSTABCDEFGHUAUB $.
      $( [22-Jul-2004] $)
  $}

  $( An ordered pair theorem for nonnegative integers.  Theorem 17.3 of [Quine]
     p. 124.  See ~ nn0opthi . $)
  nn0opth2 $p |- ( ( ( A e. NN0 /\ B e. NN0 ) /\ ( C e. NN0 /\ D e. NN0 ) ) ->
          ( ( ( ( A + B ) ^ 2 ) + B ) = ( ( ( C + D ) ^ 2 ) + D ) <->
                   ( A = C /\ B = D ) ) ) $=
    ( cn0 wcel caddc co c2 cexp wceq cc0 cif opreq1 opreq1d bibi12d 0nn0 elimel
    wa wb eqeq1d eqeq1 anbi1d opreq2 id opreq12d anbi2d eqeq2 nn0opth2i dedth4h
    eqeq2d ) AEFZBEFZCEFZDEFZABGHZIJHZBGHZCDGHZIJHZDGHZKZACKZBDKZSZTULALMZBGHZI
    JHZBGHZVAKZVFCKZVDSZTVFUMBLMZGHZIJHZVMGHZVAKZVKVMDKZSZTVPUNCLMZDGHZIJHZDGHZ
    KZVFVTKZVRSZTVPVTUODLMZGHZIJHZWGGHZKZWEVMWGKZSZTABCDLLLLAVFKZVBVJVEVLWNURVI
    VAWNUQVHBGWNUPVGIJAVFBGNOOUAWNVCVKVDAVFCUBUCPBVMKZVJVQVLVSWOVIVPVAWOVHVOBVM
    GWOVGVNIJBVMVFGUDOWOUEUFUAWOVDVRVKBVMDUBUGPCVTKZVQWDVSWFWPVAWCVPWPUTWBDGWPU
    SWAIJCVTDGNOOUKWPVKWEVRCVTVFUHUCPDWGKZWDWKWFWMWQWCWJVPWQWBWIDWGGWQWAWHIJDWG
    VTGUDOWQUEUFUKWQVRWLWEDWGVMUHUGPVFVMVTWGALEQRBLEQRCLEQRDLEQRUIUJ $.
    $( [22-Jul-2004] $)

