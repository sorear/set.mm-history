$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordered pair theorem for nonnegative integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    nn0le2msqi.1 $e |- A e. NN0 $.
    nn0le2msqi.2 $e |- B e. NN0 $.
    $( The square function on nonnegative integers is monotonic.  (Contributed
       by Raph Levien, 10-Dec-2002.) $)
    nn0le2msqi $p |- ( A <_ B <-> ( A x. A ) <_ ( B x. B ) ) $=
      ( cle wbr c2 cexp co cmul cc0 wb nn0ge0i nn0rei le2sqi mp2an nn0cni
      sqvali breq12i bitri ) ABEFZAGHIZBGHIZEFZAAJIZBBJIZEFKAEFKBEFUAUDLACMBDMA
      BACNBDNOPUBUEUCUFEAACQRBBDQRST $.
      $( [10-Dec-2002] $)
  $}

  ${
    nn0opthlem1.1 $e |- A e. NN0 $.
    nn0opthlem1.2 $e |- C e. NN0 $.
    $( A rather pretty lemma for ~ nn0opthi .  (Contributed by Raph Levien,
       10-Dec-2002.) $)
    nn0opthlem1 $p |- ( A < C <-> ( ( A x. A ) + ( 2 x. A ) ) < ( C x. C ) ) $=
      ( c1 caddc co cle wbr cmul clt c2 1nn0 nn0addcli nn0le2msqi cn0 wcel wb
      nn0ltp1le mp2an nn0mulcli 2nn0 cexp nn0cni ax-1cn binom2i addcli sqvali
      opreq1i opreq12i 3eqtr3i mulid1i opreq2i eqtri breq1i bitr4i 3bitr4i ) AE
      FGZBHIZURURJGZBBJGZHIZABKIZAAJGZLAJGZFGZVAKIZURBAECMNDOAPQBPQVCUSRCDABSTV
      GVFEFGZVAHIZVBVFPQVAPQVGVIRVDVEAACCUALAUBCUANBBDDUAVFVASTUTVHVAHUTVDLAEJG
      ZJGZFGZEEJGZFGZVHURLUCGALUCGZVKFGZELUCGZFGUTVNAEACUDZUEUFURAEVRUEUGUHVPVL
      VQVMFVOVDVKFAVRUHUIEUEUHUJUKVLVFVMEFVKVEVDFVJALJAVRULUMUMEUEULUJUNUOUPUQ
      $.
      $( [10-Dec-2002] $)
  $}

  ${
    nn0opth.1 $e |- A e. NN0 $.
    nn0opth.2 $e |- B e. NN0 $.
    nn0opth.3 $e |- C e. NN0 $.
    nn0opth.4 $e |- D e. NN0 $.
    $( Lemma for ~ nn0opthi . $)
    nn0opthlem2 $p |- ( ( A + B ) < C
           -> ( ( C x. C ) + D ) =/= ( ( ( A + B ) x. ( A + B ) ) + B ) ) $=
      ( caddc co clt wbr cmul wne c2 nn0addcli nn0opthlem1 cle nn0rei
      nn0addge2i nn0lele2xi 2re remulcli leadd2i sylib ax-mp readdcli lelttri
      mpan sylbi nn0addge1i ltletri mpan2 ltnei 3syl ) ABIJZCKLZUPUPMJZBIJZCCMJ
      ZKLZUSUTDIJZKLZVBUSNUQUROUPMJZIJZUTKLZVAUPCABEFPZGQUSVERLZVFVABUPRLZVHBAB
      FSZETVIBVDRLVHUPBVGFUABVDURVJOUPUBUPVGSZUCZUPUPVKVKUCZUDUEUFUSVEUTURBVMVJ
      UGZURVDVMVLUGCCCGSZVOUCZUHUIUJVAUTVBRLVCUTDVPHUKUSUTVBVNVPUTDVPDHSUGZULUM
      USVBVNVQUNUO $.
      $( [8-Sep-2010] $) $( [14-May-2003] $)

    $( An ordered pair theorem for nonnegative integers.  Theorem 17.3 of
       [Quine] p. 124.  We can represent an ordered pair of nonnegative
       integers ` A ` and ` B ` by ` ( ( ( A + B ) x. ( A + B ) ) + B ) ` .  If
       two such ordered pairs are equal, their first elements are equal and
       their second elements are equal.  Contrast this ordered pair
       representation with the standard one ~ df-op that works for any set.
       (Contributed by Raph Levien, 10-Dec-2002.  Proof shortened by Scott
       Fenton, 7-Sep-2010.) $)
    nn0opthi $p |- ( ( ( ( A + B ) x. ( A + B ) ) + B ) =
            ( ( ( C + D ) x. ( C + D ) ) + D ) <-> ( A = C /\ B = D ) ) $=
      ( caddc co cmul wceq wa wne clt wbr wo nn0addcli nn0rei lttri2i
      nn0opthlem2 necomd jaoi sylbi necon4i id opreq12d opreq1d eqtr4d nn0cni
      mulcli addcani sylib opreq2d addcan2i jca opreq12 simpr impbii ) ABIJZUTK
      JZBIJZCDIJZVCKJZDIJZLZACLZBDLZMZVFVGVHVFUTCBIJZLVGVFUTVCVJUTVCVBVEUTVCNUT
      VCOPZVCUTOPZQVBVENZUTVCUTABEFRZSVCCDGHRZSTVKVMVLVKVEVBABVCDEFVOHUAUBCDUTB
      GHVNFUAUCUDUEZVFBDCIVFVBVADIJZLVHVFVBVEVQVFUFVFVAVDDIVFUTVCUTVCKVPVPUGUHU
      IVABDUTUTUTVNUJZVRUKBFUJZDHUJULUMZUNUIACBAEUJCGUJVSUOUMVTUPVIVAVDBDIVIUTV
      CUTVCKACBDIUQZWAUGVGVHURUGUS $.
      $( [8-Sep-2010] $) $( [10-Dec-2002] $)

    $( An ordered pair theorem for nonnegative integers.  Theorem 17.3 of
       [Quine] p. 124.  See comments for ~ nn0opthi . $)
    nn0opth2i $p |- ( ( ( ( A + B ) ^ 2 ) + B ) = ( ( ( C + D ) ^ 2 ) + D ) <->
                   ( A = C /\ B = D ) ) $=
      ( caddc co c2 cexp wceq cmul wa nn0cni addcli sqvali opreq1i eqeq12i
      nn0opthi bitri ) ABIJZKLJZBIJZCDIJZKLJZDIJZMUCUCNJZBIJZUFUFNJZDIJZMACMBDM
      OUEUJUHULUDUIBIUCABAEPBFPQRSUGUKDIUFCDCGPDHPQRSTABCDEFGHUAUB $.
      $( [22-Jul-2004] $)
  $}

  $( An ordered pair theorem for nonnegative integers.  Theorem 17.3 of [Quine]
     p. 124.  See ~ nn0opthi . $)
  nn0opth2 $p |- ( ( ( A e. NN0 /\ B e. NN0 ) /\ ( C e. NN0 /\ D e. NN0 ) ) ->
          ( ( ( ( A + B ) ^ 2 ) + B ) = ( ( ( C + D ) ^ 2 ) + D ) <->
                   ( A = C /\ B = D ) ) ) $=
    ( cn0 wcel caddc co c2 cexp wceq wa wb cc0 cif opreq1 opreq1d eqeq1d eqeq1
    anbi1d bibi12d opreq2 id opreq12d anbi2d eqeq2d eqeq2 0nn0 elimel
    nn0opth2i dedth4h ) AEFZBEFZCEFZDEFZABGHZIJHZBGHZCDGHZIJHZDGHZKZACKZBDKZLZM
    ULANOZBGHZIJHZBGHZVAKZVFCKZVDLZMVFUMBNOZGHZIJHZVMGHZVAKZVKVMDKZLZMVPUNCNOZD
    GHZIJHZDGHZKZVFVTKZVRLZMVPVTUODNOZGHZIJHZWGGHZKZWEVMWGKZLZMABCDNNNNAVFKZVBV
    JVEVLWNURVIVAWNUQVHBGWNUPVGIJAVFBGPQQRWNVCVKVDAVFCSTUABVMKZVJVQVLVSWOVIVPVA
    WOVHVOBVMGWOVGVNIJBVMVFGUBQWOUCUDRWOVDVRVKBVMDSUEUACVTKZVQWDVSWFWPVAWCVPWPU
    TWBDGWPUSWAIJCVTDGPQQUFWPVKWEVRCVTVFUGTUADWGKZWDWKWFWMWQWCWJVPWQWBWIDWGGWQW
    AWHIJDWGVTGUBQWQUCUDUFWQVRWLWEDWGVMUGUEUAVFVMVTWGANEUHUIBNEUHUICNEUHUIDNEUH
    UIUJUK $.
    $( [22-Jul-2004] $)

