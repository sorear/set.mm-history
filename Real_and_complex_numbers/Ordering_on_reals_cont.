$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordering on reals (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Equality implies 'less than or equal to'. $)
  eqle $p |- ( ( A e. RR /\ A = B ) -> A <_ B ) $=
    ( cle wbr wceq cr wcel breq2 biimpac leid sylan ) AACDZABEZABCDZAFGMLNABACH
    IAJK $.
    $( [6-Apr-2005] $) $( [4-Apr-2005] $)

  ${
    lt.1 $e |- A e. RR $.

    ${
      lt.2 $e |- B e. RR $.
      $( Consequence of trichotomy. $)
      lttri2i $p |- ( A =/= B <-> ( A < B \/ B < A ) ) $=
        ( cr wcel wne clt wbr wo wb lttri2 mp2an ) AEFBEFABGABHIBAHIJKCDABLM $.
        $( [22-Aug-2007] $) $( [19-Jan-1997] $)

      $( Consequence of trichotomy. $)
      lttri3i $p |- ( A = B <-> ( -. A < B /\ -. B < A ) ) $=
        ( cr wcel wceq clt wbr wn wa wb lttri3 mp2an ) AEFBEFABGABHIJBAHIJKLCDA
        BMN $.
        $( [14-May-1999] $)

      $( Consequence of trichotomy. $)
      letri3i $p |- ( A = B <-> ( A <_ B /\ B <_ A ) ) $=
        ( cr wcel wceq cle wbr wa wb letri3 mp2an ) AEFBEFABGABHIBAHIJKCDABLM
        $.
        $( [14-May-1999] $)

      $( 'Less than or equal to' in terms of 'less than'. $)
      leloei $p |- ( A <_ B <-> ( A < B \/ A = B ) ) $=
        ( cr wcel cle wbr clt wceq wo wb leloe mp2an ) AEFBEFABGHABIHABJKLCDABM
        N $.
        $( [14-May-1999] $)

      $( 'Less than' expressed in terms of 'less than or equal to'. $)
      ltleni $p |- ( A < B <-> ( A <_ B /\ B =/= A ) ) $=
        ( cr wcel clt wbr cle wne wa wb ltlen mp2an ) AEFBEFABGHABIHBAJKLCDABMN
        $.
        $( [8-Aug-2007] $) $( [27-Oct-1999] $)

      $( 'Less than' is not symmetric. $)
      ltnsymi $p |- ( A < B -> -. B < A ) $=
        ( cr wcel clt wbr wn wi ltnsym mp2an ) AEFBEFABGHBAGHIJCDABKL $.
        $( [6-May-1999] $)

      $( 'Less than or equal to' in terms of 'less than'. $)
      lenlti $p |- ( A <_ B <-> -. B < A ) $=
        ( cr wcel cle wbr clt wn wb lenlt mp2an ) AEFBEFABGHBAIHJKCDABLM $.
        $( [24-May-1999] $)

      $( 'Less than' in terms of 'less than or equal to'. $)
      ltnlei $p |- ( A < B <-> -. B <_ A ) $=
        ( cle wbr clt lenlti con2bii ) BAEFABGFBADCHI $.
        $( [12-Jul-2005] $) $( [11-Jul-2005] $)

      $( 'Less than' implies 'less than or equal to'. $)
      ltlei $p |- ( A < B -> A <_ B ) $=
        ( cr wcel clt wbr cle wi ltle mp2an ) AEFBEFABGHABIHJCDABKL $.
        $( [14-May-1999] $)

      ${
        ltlei.1 $e |- A < B $.
        $( 'Less than' implies 'less than or equal to' (inference). $)
        ltleii $p |- A <_ B $=
          ( clt wbr cle ltlei ax-mp ) ABFGABHGEABCDIJ $.
          $( [22-Aug-1999] $)
      $}

      $( Equality implies 'less than or equal to'. $)
      eqlei $p |- ( A = B -> A <_ B ) $=
        ( wceq cle wbr letri3i pm3.26bi ) ABEABFGBAFGABCDHI $.
        $( [23-May-1999] $)

      $( 'Less than' implies not equal. $)
      ltnei $p |- ( A < B -> B =/= A ) $=
        ( cr wcel clt wbr wne ltne mp3an12 ) AEFBEFABGHBAICDABJK $.
        $( [14-Aug-2007] $) $( [28-Jul-1999] $)

      $( Trichotomy law for 'less than or equal to'. $)
      letrii $p |- ( A <_ B \/ B <_ A ) $=
        ( cle wbr wn clt ltnlei ltlei sylbir orri ) ABEFZBAEFZMGBAHFNBADCIBADCJ
        KL $.
        $( [2-Aug-1999] $)

      ${
        lt.3 $e |- C e. RR $.
        $( 'Less than' is transitive.  Theorem I.17 of [Apostol] p. 20. $)
        lttri $p |- ( ( A < B /\ B < C ) -> A < C ) $=
          ( cr wcel clt wbr wa wi axlttrn mp3an ) AGHBGHCGHABIJBCIJKACIJLDEFABC
          MN $.
          $( [14-May-1999] $)

        $( 'Less than or equal to', 'less than' transitive law. $)
        lelttri $p |- ( ( A <_ B /\ B < C ) -> A < C ) $=
          ( cr wcel cle wbr clt wa wi lelttr mp3an ) AGHBGHCGHABIJBCKJLACKJMDEF
          ABCNO $.
          $( [14-May-1999] $)

        $( 'Less than', 'less than or equal to' transitive law. $)
        ltletri $p |- ( ( A < B /\ B <_ C ) -> A < C ) $=
          ( cr wcel clt wbr cle wa wi ltletr mp3an ) AGHBGHCGHABIJBCKJLACIJMDEF
          ABCNO $.
          $( [14-May-1999] $)

        $( 'Less than or equal to' is transitive. $)
        letri $p |- ( ( A <_ B /\ B <_ C ) -> A <_ C ) $=
          ( cr wcel cle wbr wa wi letr mp3an ) AGHBGHCGHABIJBCIJKACIJLDEFABCMN
          $.
          $( [14-May-1999] $)

        $( Extended trichotomy law for 'less than or equal to'. $)
        le2tri3i $p |- ( ( A <_ B /\ B <_ C /\ C <_ A ) <->
                        ( A = B /\ B = C /\ C = A ) ) $=
          ( cle wbr w3a wceq wa letri3i biimpri letri sylan2 3impb 3comr
          eqcomd sylan 3impa 3jca eqlei 3anim123i impbii ) ABGHZBCGHZCAGHZIZABJ
          ZBCJZCAJZIUHUIUJUKUEUFUGUIUEBAGHZUIUFUGKUIUEULKABDELMBCAEFDNOPUFUGUEU
          JUFUGUEUJUFCBGHZUJUGUEKUJUFUMKBCEFLMCABFDENOPQUEUFUGUKACGHZUGUKUEUFKU
          NUGKZACACJUOACDFLMRABCDEFNSTUAUIUEUJUFUKUGABDEUBBCEFUBCAFDUBUCUD $.
          $( [14-Aug-2000] $)

        $( Addition to both sides of 'less than'.  (Proof shortened by Paul
           Chapman, 27-Jan-2008.) $)
        ltadd2i $p |- ( A < B <-> ( C + A ) < ( C + B ) ) $=
          ( clt wbr caddc co cr wcel wi axltadd mp3an cle wn wceq wo opreq2
          orim12i leloei readdcli 3imtr4i lenlti 3imtr3i con4i impbii ) ABGHZCA
          IJZCBIJZGHZAKLZBKLZCKLZUIULMDEFABCNOUIULBAPHZUKUJPHZUIQULQBAGHZBARZSU
          KUJGHZUKUJRZSUPUQURUTUSVAUNUMUOURUTMEDFBACNOBACITUABAEDUBUKUJCBFEUCZC
          AFDUCZUBUDBAEDUEUKUJVBVCUEUFUGUH $.
          $( [28-Jan-2008] $) $( [21-Jan-1997] $)

        $( Addition to both sides of 'less than'.  Theorem I.18 of [Apostol]
           p. 20. $)
        ltadd1i $p |- ( A < B <-> ( A + C ) < ( B + C ) ) $=
          ( clt wbr caddc co ltadd2i recni addcomi breq12i bitri ) ABGHCAIJZCBI
          JZGHACIJZBCIJZGHABCDEFKPRQSGCACFLZADLMCBTBELMNO $.
          $( [21-Jan-1997] $)

        $( Addition to both sides of 'less than or equal to'. $)
        leadd1i $p |- ( A <_ B <-> ( A + C ) <_ ( B + C ) ) $=
          ( clt wbr wn caddc co cle ltadd1i notbii lenlti readdcli 3bitr4i ) BA
          GHZIBCJKZACJKZGHZIABLHTSLHRUABACEDFMNABDEOTSACDFPBCEFPOQ $.
          $( [11-Aug-1999] $)

        $( Addition to both sides of 'less than or equal to'. $)
        leadd2i $p |- ( A <_ B <-> ( C + A ) <_ ( C + B ) ) $=
          ( cle wbr caddc co leadd1i recni addcomi breq12i bitri ) ABGHACIJZBCI
          JZGHCAIJZCBIJZGHABCDEFKPRQSGACADLCFLZMBCBELTMNO $.
          $( [11-Aug-1999] $)

        $( 'Less than' relationship between subtraction and addition. $)
        ltsubaddi $p |- ( ( A - B ) < C <-> A < ( C + B ) ) $=
          ( cneg caddc co clt wbr cmin renegcli readdcli ltadd1i recni negsubi
          breq1i cc0 addassi addcomi negidi eqtri opreq2i addid1i 3eqtri
          3bitr3i ) ABGZHIZCJKUIBHIZCBHIZJKABLIZCJKAUKJKUICBAUHDBEMZNFEOUIULCJA
          BADPZBEPZQRUJAUKJUJAUHBHIZHIASHIAAUHBUNUHUMPZUOTUPSAHUPBUHHISUHBUQUOU
          ABUOUBUCUDAUNUEUFRUG $.
          $( [21-Jan-1997] $)

        $( 'Less than or equal to' relationship between subtraction and
           addition. $)
        lesubaddi $p |- ( ( A - B ) <_ C <-> A <_ ( C + B ) ) $=
          ( cmin co clt wbr wceq wo caddc cle ltsubaddi recni subaddi addcomi
          eqeq1i eqcom 3bitri orbi12i resubcli leloei readdcli 3bitr4i ) ABGHZC
          IJZUGCKZLACBMHZIJZAUJKZLUGCNJAUJNJUHUKUIULABCDEFOUIBCMHZAKUJAKULABCAD
          PBEPZCFPZQUMUJABCUNUORSUJATUAUBUGCABDEUCFUDAUJDCBFEUEUDUF $.
          $( [30-Sep-1999] $)

        ${
          lt.4 $e |- D e. RR $.
          $( Adding both side of two inequalities.  Theorem I.25 of [Apostol]
             p. 20. $)
          lt2addi $p |- ( ( A < C /\ B < D ) -> ( A + B ) < ( C + D ) ) $=
            ( caddc co clt wbr readdcli lttri ltadd1i ltadd2i syl2anb ) ABIJZCB
            IJZKLSCDIJZKLRTKLACKLBDKLRSTABEFMCBGFMCDGHMNACBEGFOBDCFHGPQ $.
            $( [14-May-1999] $)

          $( Adding both side of two inequalities. $)
          le2addi $p |- ( ( A <_ C /\ B <_ D ) -> ( A + B ) <_ ( C + D ) ) $=
            ( caddc co cle wbr readdcli letri leadd1i leadd2i syl2anb ) ABIJZCB
            IJZKLSCDIJZKLRTKLACKLBDKLRSTABEFMCBGFMCDGHMNACBEGFOBDCFHGPQ $.
            $( [16-Sep-1999] $)
        $}
      $}

      $( Addition of 2 positive numbers is positive. $)
      addgt0i $p |- ( ( 0 < A /\ 0 < B ) -> 0 < ( A + B ) ) $=
        ( cc0 clt wbr caddc co 0re readdcli lttri ltadd2i recni addid1i breq1i
        bitri sylan2b ) EAFGAABHIZFGZESFGEBFGZEASJCABCDKLUAAEHIZSFGTEBAJDCMUBAS
        FAACNOPQR $.
        $( [16-May-1999] $)

      $( Addition of 2 nonnegative numbers is nonnegative. $)
      addge0i $p |- ( ( 0 <_ A /\ 0 <_ B ) -> 0 <_ ( A + B ) ) $=
        ( cc0 clt wbr wceq wo caddc co cle wa addgt0i 0re readdcli ltlei syl
        opreq1 recni addid2i syl5eqr breq2d biimpa opreq2 addid1i biimpac
        opreq12 0cn eqlei ccase leloei syl2anb ) EAFGZEAHZIEBFGZEBHZIEABJKZLGZE
        ALGEBLGUNUPUOUQUSUNUPMEURFGZUSABCDNEUROABCDPZQZRUOUPMUTUSUOUPUTUOBUREFU
        OEBJKURBEABJSBBDTUAUBUCUDVBRUNUQMUTUSUQUNUTUQAUREFUQAEJKURAEBAJUEAACTUF
        UBUCUGVBRUOUQMZEURHUSVCEEJKUREEAEBJUHEUIUFUBEUROVAUJRUKEAOCULEBODULUM
        $.
        $( [28-May-1999] $)

      $( Addition of nonnegative and positive numbers is positive. $)
      addgegt0i $p |- ( ( 0 <_ A /\ 0 < B ) -> 0 < ( A + B ) ) $=
        ( cc0 cle wbr clt caddc co wceq wo wi 0re leloei addgt0i ex opreq1
        recni addid2i syl5eqr breq2d biimpd jaoi sylbi imp ) EAFGZEBHGZEABIJZHG
        ZUGEAHGZEAKZLUHUJMZEANCOUKUMULUKUHUJABCDPQULUHUJULBUIEHULEBIJUIBEABIRBB
        DSTUAUBUCUDUEUF $.
        $( [25-Sep-1999] $)

      ${
        addgt0i.3 $e |- 0 < A $.
        addgt0i.4 $e |- 0 < B $.
        $( Addition of 2 positive numbers is positive. $)
        addgt0ii $p |- 0 < ( A + B ) $=
          ( cc0 clt wbr caddc co addgt0i mp2an ) GAHIGBHIGABJKHIEFABCDLM $.
          $( [18-May-1999] $)
      $}

      $( Two nonnegative numbers are zero iff their sum is zero. $)
      add20i $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
        ( ( A + B ) = 0 <-> ( A = 0 /\ B = 0 ) ) ) $=
        ( cc0 cle wbr wa caddc co wceq clt wo wi wn readdcli 0re lttri3i
        pm3.27bi addgt0i nsyl3 pm2.21d opreq1 recni addid2i syl5eqr eqeq1d
        biimprd eqcom biimpi jctild opreq2 addid1i jctird ccase2 leloei
        syl2anb 0cn syl6eq sylan9eq impbid1 ) EAFGZEBFGZHABIJZEKZAEKZBEKZHZEALG
        ZEAKZMEBLGZEBKZMVEVHNZVBVCVIVKVJVLVMVIVKHZVEVHVEEVDLGZVNVEVDELGOVOOVDEA
        BCDPQRSABCDTUAUBVJVEVGVFVJVGVEVJBVDEVJEBIJZVDBEABIUCBBDUDUEUFUGUHVJVFEA
        UIUJUKVLVEVFVGVLVFVEVLAVDEVLAEIJVDAEBAIULAACUDUMUFUGUHVLVGEBUIUJUNUOEAQ
        CUPEBQDUPUQVFVGVDVPEAEBIUCVGVPEEIJEBEEIULEURUMUSUTVA $.
        $( [28-Jul-1999] $)

      $( Negative of both sides of 'less than'.  Theorem I.23 of [Apostol]
         p. 20. $)
      ltnegi $p |- ( A < B <-> -u B < -u A ) $=
        ( clt wbr cneg caddc co renegcli readdcli ltadd1i cc0 recni negidi
        opreq1i addassi addid2i 3eqtr3i add12i opreq2i addid1i 3eqtri breq12i
        bitri ) ABEFAAGZBGZHIZHIZBUHHIZEFUGUFEFABUHCDUFUGACJZBDJZKLUIUGUJUFEAUF
        HIZUGHIMUGHIUIUGUMMUGHAACNZOPAUFUGUNUFUKNZUGULNZQUGUPRSUJUFBUGHIZHIUFMH
        IUFBUFUGBDNZUOUPTUQMUFHBUROUAUFUOUBUCUDUE $.
        $( [21-Jan-1997] $)

      $( Negative of both sides of 'less than or equal to'. $)
      lenegi $p |- ( A <_ B <-> -u B <_ -u A ) $=
        ( clt wbr wceq wo cneg cle ltnegi recni neg11i eqcom bitr2i orbi12i
        leloei renegcli 3bitr4i ) ABEFZABGZHBIZAIZEFZUBUCGZHABJFUBUCJFTUDUAUEAB
        CDKUEBAGUABABDLACLMBANOPABCDQUBUCBDRACRQS $.
        $( [1-Aug-1999] $)

      $( Contraposition of negative in 'less than'. $)
      ltnegcon2i $p |- ( A < -u B <-> B < -u A ) $=
        ( cneg clt wbr renegcli ltnegi recni negnegi breq1i bitri ) ABEZFGNEZAE
        ZFGBPFGANCBDHIOBPFBBDJKLM $.
        $( [14-May-1999] $)

      $( The product of two positive numbers is positive. $)
      mulgt0i $p |- ( ( 0 < A /\ 0 < B ) -> 0 < ( A x. B ) ) $=
        ( cr wcel cc0 clt wbr wa cmul co wi axmulgt0 mp2an ) AEFBEFGAHIGBHIJGAB
        KLHIMCDABNO $.
        $( [16-May-1999] $)

      $( The product of two nonnegative numbers is nonnegative. $)
      mulge0i $p |- ( ( 0 <_ A /\ 0 <_ B ) -> 0 <_ ( A x. B ) ) $=
        ( cc0 clt wbr wceq wo cmul co cle wa mulgt0i 0re remulcli ltlei syl
        opreq1 recni mul02i syl5eqr eqlei opreq2 mul01i ccase2 leloei syl2anb
        ) EAFGZEAHZIEBFGZEBHZIEABJKZLGZEALGEBLGUIUKUJULUNUIUKMEUMFGUNABCDNEUMOA
        BCDPZQRUJEUMHZUNUJEBJKUMEEABJSBBDTUAUBEUMOUOUCZRULUPUNULAEJKUMEEBAJUDAA
        CTUEUBUQRUFEAOCUGEBODUGUH $.
        $( [30-Jul-1999] $)

      ${
        mulgt0i.3 $e |- 0 < A $.
        mulgt0i.4 $e |- 0 < B $.
        $( The product of two positive numbers is positive. $)
        mulgt0ii $p |- 0 < ( A x. B ) $=
          ( cc0 clt wbr cmul co mulgt0i mp2an ) GAHIGBHIGABJKHIEFABCDLM $.
          $( [18-May-1999] $)
      $}
    $}

    $( 'Less than' is irreflexive. $)
    ltnri $p |- -. A < A $=
      ( cr wcel clt wbr wn ltnr ax-mp ) ACDAAEFGBAHI $.
      $( [18-Aug-1999] $)

    $( 'Less than or equal to' is reflexive. $)
    leidi $p |- A <_ A $=
      ( cr wcel cle wbr leid ax-mp ) ACDAAEFBAGH $.
      $( [18-Aug-1999] $)

    $( Positive means nonzero (useful for ordering theorems involving
       division). $)
    gt0ne0i $p |- ( 0 < A -> A =/= 0 ) $=
      ( cc0 0re ltnei ) CADBE $.
      $( [16-Sep-1999] $)

    ${
      lesub0.1 $e |- B e. RR $.
      $( Lemma to show a nonnegative number is zero. $)
      lesub0i $p |- ( ( 0 <_ A /\ B <_ ( B - A ) ) <-> A = 0 ) $=
        ( cmin co cle wbr cc0 wa wceq cneg caddc 0re renegcli leadd2i lenegi
        neg0 breq1i bitr2i recni addid1i negsubi breq12i 3bitr3ri anbi1i ancom
        letri3i 3bitr4i ) BBAEFZGHZIAGHZJAIGHZULJULUKJAIKUKUMULIALZGHZBIMFZBUNM
        FZGHUMUKIUNBNACODPUMILZUNGHUOAICNQURIUNGRSTUPBUQUJGBBDUAZUBBAUSACUAUCUD
        UEUFULUKUGAICNUHUI $.
        $( [8-Oct-1999] $)
    $}

    $( A nonzero square is positive.  Theorem I.20 of [Apostol] p. 20. $)
    msqgt0i $p |- ( A =/= 0 -> 0 < ( A x. A ) ) $=
      ( cc0 wne clt wbr wo cmul co 0re lttri2i cneg ltnegi neg0 breq1i bitri
      renegcli mulgt0i anidms recni mul2negi syl6breq sylbi jaoi ) ACDACEFZCAEF
      ZGCAAHIZEFZACBJKUEUHUFUECALZEFZUHUECLZUIEFUJACBJMUKCUIENOPUJCUIUIHIZUGEUJ
      CULEFUIUIABQZUMRSAAABTZUNUAUBUCUFUHAABBRSUDUC $.
      $( [23-May-2007] $) $( [17-Jan-1997] $)

    $( A square is nonnegative. $)
    msqge0i $p |- 0 <_ ( A x. A ) $=
      ( cc0 cmul co cle wbr wceq clt wo opreq2 recni mul01i syl6req olcd 0re
      remulcli leloei sylibr wne msqgt0i ltlei syl pm2.61ine ) CAADEZFGZACACHZC
      UEIGZCUEHZJUFUGUIUHUGUEACDECACADKAABLMNOCUEPAABBQZRSACTUHUFABUACUEPUJUBUC
      UD $.
      $( [14-May-1999] $)
  $}

  $( A nonzero square is positive.  Theorem I.20 of [Apostol] p. 20. $)
  msqgt0 $p |- ( ( A e. RR /\ A =/= 0 ) -> 0 < ( A x. A ) ) $=
    ( cr wcel cc0 wne cmul co clt wbr wi cif wceq neeq1 id opreq12d breq2d
    imbi12d 0re elimel msqgt0i dedth imp ) ABCZADEZDAAFGZHIZUCUDUFJUCADKZDEZDUG
    UGFGZHIZJADAUGLZUDUHUFUJAUGDMUKUEUIDHUKAUGAUGFUKNZULOPQUGADBRSTUAUB $.
    $( [24-May-2007] $) $( [6-May-1999] $)

  $( A square is nonnegative. $)
  msqge0 $p |- ( A e. RR -> 0 <_ ( A x. A ) ) $=
    ( cr wcel cc0 cmul co cle wbr cif wceq id opreq12d breq2d 0re elimel
    msqge0i dedth ) ABCZDAAEFZGHDRADIZTEFZGHADATJZSUADGUBATATEUBKZUCLMTADBNOPQ
    $.
    $( [23-May-2007] $) $( [23-May-2007] $)

  ${
    gt0ne0i.1 $e |- A e. RR $.
    gt0ne0i.2 $e |- 0 < A $.
    $( Positive implies nonzero. $)
    gt0ne0ii $p |- A =/= 0 $=
      ( cc0 clt wbr wne gt0ne0i ax-mp ) DAEFADGCABHI $.
      $( [15-May-1999] $)
  $}

  $( Positive implies nonzero. $)
  gt0ne0 $p |- ( ( A e. RR /\ 0 < A ) -> A =/= 0 ) $=
    ( cr wcel cc0 clt wbr wne wi cif wceq breq2 neeq1 imbi12d 0re elimel
    gt0ne0i dedth imp ) ABCZDAEFZADGZSTUAHDSADIZEFZUBDGZHADAUBJTUCUAUDAUBDEKAUB
    DLMUBADBNOPQR $.
    $( [25-Feb-2005] $) $( [3-Oct-1999] $)

  $( A nonzero nonnegative number is positive. $)
  ne0gt0 $p |- ( ( A e. RR /\ 0 <_ A ) -> ( A =/= 0 <-> 0 < A ) ) $=
    ( cr wcel cc0 cle wbr wa wne clt wo wb 0re lttri2 mpan2 adantr wn lenlt
    mpan biimpa biorf syl bitr4d ) ABCZDAEFZGZADHZADIFZDAIFZJZUHUCUFUIKZUDUCDBC
    ZUJLADMNOUEUGPZUHUIKUCUDULUKUCUDULKLDAQRSUGUHTUAUB $.
    $( [9-Dec-2007] $) $( [20-Nov-2007] $)

  $( Trichotomy law. $)
  letric $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B \/ B <_ A ) ) $=
    ( cr wcel wa cle wbr wn clt wb ltnle ancoms wi ltle sylbird orrd ) ACDZBCDZ
    EZABFGZBAFGZSTHZBAIGZUARQUCUBJBAKLRQUCUAMBANLOP $.
    $( [18-Aug-1999] $)

  ${
    lecase.1 $e |- ( ph -> A e. RR ) $.
    lecase.2 $e |- ( ph -> B e. RR ) $.
    lecase.3 $e |- ( ( ph /\ A <_ B ) -> ps ) $.
    lecase.4 $e |- ( ( ph /\ B <_ A ) -> ps ) $.
    $( Ordering elimination by cases. $)
    lecasei $p |- ( ph -> ps ) $=
      ( cle wbr wo cr wcel letric sylanc jaodan mpdan ) ACDIJZDCIJZKZBCLMDLMTAC
      DNEFOARBSGHPQ $.
      $( [17-Jul-2007] $) $( [6-Jul-2007] $)
  $}

  $( Trichotomy law. $)
  lelttric $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B \/ B < A ) ) $=
    ( cr wcel wa cle wbr clt wo wn pm2.1 lenlt orbi1d mpbiri ) ACDBCDEZABFGZBAH
    GZIQJZQIQKOPRQABLMN $.
    $( [26-Apr-2005] $) $( [4-Apr-2005] $)

  $( Addition to both sides of 'less than'. $)
  ltadd1 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( A + C ) < ( B + C ) ) ) $=
    ( cr wcel clt wbr caddc co wb cc0 cif wceq breq1 opreq1 breq1d bibi12d
    breq2 breq2d opreq2 breq12d bibi2d 0re elimel ltadd1i dedth3h ) ADEZBDEZCDE
    ZABFGZACHIZBCHIZFGZJUGAKLZBFGZUNCHIZULFGZJUNUHBKLZFGZUPURCHIZFGZJUSUNUICKLZ
    HIZURVBHIZFGZJABCKKKAUNMZUJUOUMUQAUNBFNVFUKUPULFAUNCHOPQBURMZUOUSUQVABURUNF
    RVGULUTUPFBURCHOSQCVBMZVAVEUSVHUPVCUTVDFCVBUNHTCVBURHTUAUBUNURVBAKDUCUDBKDU
    CUDCKDUCUDUEUF $.
    $( [12-Nov-1999] $)

  $( Addition to both sides of 'less than'. $)
  ltadd2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( C + A ) < ( C + B ) ) ) $=
    ( cr wcel w3a clt wbr caddc co ltadd1 wceq cc addcom recn syl2an 3adant2
    3adant1 breq12d bitrd ) ADEZBDEZCDEZFZABGHACIJZBCIJZGHCAIJZCBIJZGHABCKUDUEU
    GUFUHGUAUCUEUGLZUBAMECMEZUIUAUCACNAOCOZPQUBUCUFUHLZUABMEUJULUBUCBCNBOUKPRST
    $.
    $( [12-Nov-1999] $)

  $( Addition to both sides of 'less than or equal to'. $)
  leadd1 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( A + C ) <_ ( B + C ) ) ) $=
    ( cr wcel cle wbr caddc co wb cc0 cif wceq breq1 opreq1 breq1d bibi12d
    breq2 breq2d opreq2 breq12d bibi2d 0re elimel leadd1i dedth3h ) ADEZBDEZCDE
    ZABFGZACHIZBCHIZFGZJUGAKLZBFGZUNCHIZULFGZJUNUHBKLZFGZUPURCHIZFGZJUSUNUICKLZ
    HIZURVBHIZFGZJABCKKKAUNMZUJUOUMUQAUNBFNVFUKUPULFAUNCHOPQBURMZUOUSUQVABURUNF
    RVGULUTUPFBURCHOSQCVBMZVAVEUSVHUPVCUTVDFCVBUNHTCVBURHTUAUBUNURVBAKDUCUDBKDU
    CUDCKDUCUDUEUF $.
    $( [18-Oct-1999] $)

  $( Addition to both sides of 'less than or equal to'. $)
  leadd2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( C + A ) <_ ( C + B ) ) ) $=
    ( cr wcel w3a cle wbr caddc co leadd1 wceq cc addcom recn syl2an 3adant2
    3adant1 breq12d bitrd ) ADEZBDEZCDEZFZABGHACIJZBCIJZGHCAIJZCBIJZGHABCKUDUEU
    GUFUHGUAUCUEUGLZUBAMECMEZUIUAUCACNAOCOZPQUBUCUFUHLZUABMEUJULUBUCBCNBOUKPRST
    $.
    $( [26-Oct-1999] $)

  $( 'Less than' relationship between subtraction and addition. $)
  ltsubadd $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( ( A - B ) < C <-> A < ( C + B ) ) ) $=
    ( cr wcel cmin co clt wbr caddc wb cc0 cif wceq opreq1 breq1d breq1
    bibi12d opreq2 breq2d breq2 0re elimel ltsubaddi dedth3h ) ADEZBDEZCDEZABFG
    ZCHIZACBJGZHIZKUFALMZBFGZCHIZUMUKHIZKUMUGBLMZFGZCHIZUMCUQJGZHIZKURUHCLMZHIZ
    UMVBUQJGZHIZKABCLLLAUMNZUJUOULUPVFUIUNCHAUMBFOPAUMUKHQRBUQNZUOUSUPVAVGUNURC
    HBUQUMFSPVGUKUTUMHBUQCJSTRCVBNZUSVCVAVECVBURHUAVHUTVDUMHCVBUQJOTRUMUQVBALDU
    BUCBLDUBUCCLDUBUCUDUE $.
    $( [21-Jan-1997] $)

  $( 'Less than' relationship between subtraction and addition. $)
  ltsubadd2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) < C <-> A < ( B + C ) ) ) $=
    ( cr wcel w3a cmin co clt wbr caddc ltsubadd wceq cc addcom recn syl2an
    3adant1 breq2d bitr4d ) ADEZBDEZCDEZFZABGHCIJACBKHZIJABCKHZIJABCLUDUFUEAIUB
    UCUFUEMZUABNECNEUGUBUCBCOBPCPQRST $.
    $( [21-Jan-1997] $)

  $( 'Less than or equal to' relationship between subtraction and addition. $)
  lesubadd $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) <_ C <-> A <_ ( C + B ) ) ) $=
    ( cr wcel cmin co cle wbr caddc wb cc0 cif wceq opreq1 breq1d breq1
    bibi12d opreq2 breq2d breq2 0re elimel lesubaddi dedth3h ) ADEZBDEZCDEZABFG
    ZCHIZACBJGZHIZKUFALMZBFGZCHIZUMUKHIZKUMUGBLMZFGZCHIZUMCUQJGZHIZKURUHCLMZHIZ
    UMVBUQJGZHIZKABCLLLAUMNZUJUOULUPVFUIUNCHAUMBFOPAUMUKHQRBUQNZUOUSUPVAVGUNURC
    HBUQUMFSPVGUKUTUMHBUQCJSTRCVBNZUSVCVAVECVBURHUAVHUTVDUMHCVBUQJOTRUMUQVBALDU
    BUCBLDUBUCCLDUBUCUDUE $.
    $( [19-Nov-2004] $) $( [17-Nov-2004] $)

  $( 'Less than or equal to' relationship between subtraction and addition. $)
  lesubadd2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) <_ C <-> A <_ ( B + C ) ) ) $=
    ( cr wcel w3a cmin co cle wbr caddc lesubadd wceq cc addcom recn syl2an
    3adant1 breq2d bitr4d ) ADEZBDEZCDEZFZABGHCIJACBKHZIJABCKHZIJABCLUDUFUEAIUB
    UCUFUEMZUABNECNEUGUBUCBCOBPCPQRST $.
    $( [10-Aug-1999] $)

  $( 'Less than' relationship between addition and subtraction. $)
  ltaddsub $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A + B ) < C <-> A < ( C - B ) ) ) $=
    ( cr wcel w3a caddc co clt wbr cmin cle wn wb lesubadd 3com13 wa lenlt
    resubcl sylan 3impa readdcl sylan2 3impb 3coml 3bitr3rd con4bid ) ADEZBDEZC
    DEZFZABGHZCIJZACBKHZIJZUKUNALJZCULLJZUOMZUMMZUJUIUHUPUQNCBAOPUJUIUHUPURNZUJ
    UIUHUTUNDEUHUTUJUIQUNARCBSTUAPUJUHUIUQUSNZUJUHUIVAUJULDEVAUHUIQCULRABUBUCUD
    UEUFUG $.
    $( [22-Nov-2004] $) $( [17-Nov-2004] $)

  $( 'Less than' relationship between addition and subtraction. $)
  ltaddsub2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A + B ) < C <-> B < ( C - A ) ) ) $=
    ( cr wcel w3a caddc co clt wbr cmin wceq cc addcom recn syl2an 3adant3
    breq1d wb ltaddsub 3com12 bitrd ) ADEZBDEZCDEZFZABGHZCIJBAGHZCIJZBCAKHIJZUF
    UGUHCIUCUDUGUHLZUEAMEBMEUKUCUDABNAOBOPQRUDUCUEUIUJSBACTUAUB $.
    $( [28-Nov-2004] $) $( [17-Nov-2004] $)

  $( 'Less than or equal to' relationship between addition and subtraction. $)
  leaddsub $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A + B ) <_ C <-> A <_ ( C - B ) ) ) $=
    ( cr wcel w3a caddc co cle wbr cmin clt wn wb ltsubadd 3com13 wa ltnle
    resubcl sylan 3impa readdcl sylan2 3impb 3coml 3bitr3rd con4bid ) ADEZBDEZC
    DEZFZABGHZCIJZACBKHZIJZUKUNALJZCULLJZUOMZUMMZUJUIUHUPUQNCBAOPUJUIUHUPURNZUJ
    UIUHUTUNDEUHUTUJUIQUNARCBSTUAPUJUHUIUQUSNZUJUHUIVAUJULDEVAUHUIQCULRABUBUCUD
    UEUFUG $.
    $( [23-Apr-2005] $) $( [6-Apr-2005] $)

  $( 'Less than or equal to' relationship between and addition and
     subtraction. $)
  leaddsub2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A + B ) <_ C <-> B <_ ( C - A ) ) ) $=
    ( cr wcel w3a caddc co cle wbr cmin wceq cc addcom recn syl2an 3adant3
    breq1d wb leaddsub 3com12 bitrd ) ADEZBDEZCDEZFZABGHZCIJBAGHZCIJZBCAKHIJZUF
    UGUHCIUCUDUGUHLZUEAMEBMEUKUCUDABNAOBOPQRUDUCUEUIUJSBACTUAUB $.
    $( [13-Jun-2005] $) $( [6-Apr-2005] $)

  $( Swap subtrahends in an inequality. $)
  suble $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) <_ C <-> ( A - C ) <_ B ) ) $=
    ( cr wcel w3a cmin co cle wbr caddc lesubadd wb lesubadd2 3com23 bitr4d )
    ADEZBDEZCDEZFABGHCIJACBKHIJZACGHBIJZABCLQSRUATMACBNOP $.
    $( [17-Oct-2005] $) $( [29-Sep-2005] $)

  $( Swap subtrahends in an inequality. $)
  lesub $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( A <_ ( B - C ) <-> C <_ ( B - A ) ) ) $=
    ( cr wcel w3a caddc co cle wbr cmin wb leaddsub 3com23 leaddsub2 bitr3d )
    ADEZBDEZCDEZFACGHBIJZABCKHIJZCBAKHIJZQSRTUALACBMNQSRTUBLACBONP $.
    $( [3-Nov-2005] $) $( [29-Sep-2005] $)

  ${
    ltsubadd.1 $e |- A e. RR $.
    ltsubadd.2 $e |- B e. RR $.
    ltsubadd.3 $e |- C e. RR $.
    $( 'Less than' relationship between subtraction and addition. $)
    ltsubadd2i $p |- ( ( A - B ) < C <-> A < ( B + C ) ) $=
      ( cr wcel cmin co clt wbr caddc wb ltsubadd2 mp3an ) AGHBGHCGHABIJCKLABCM
      JKLNDEFABCOP $.
      $( [21-Jan-1997] $)

    $( 'Less than or equal to' relationship between subtraction and
       addition. $)
    lesubadd2i $p |- ( ( A - B ) <_ C <-> A <_ ( B + C ) ) $=
      ( cr wcel cmin co cle wbr caddc wb lesubadd2 mp3an ) AGHBGHCGHABIJCKLABCM
      JKLNDEFABCOP $.
      $( [3-Aug-1999] $)

    $( 'Less than' relationship between subtraction and addition. $)
    ltaddsubi $p |- ( ( A + B ) < C <-> A < ( C - B ) ) $=
      ( cr wcel caddc co clt wbr cmin wb ltaddsub mp3an ) AGHBGHCGHABIJCKLACBMJ
      KLNDEFABCOP $.
      $( [14-May-1999] $)

    $( Multiplication of both sides of 'less than' by a positive number.
       Theorem I.19 of [Apostol] p. 20. $)
    ltmullem $p |- ( 0 < C -> ( A < B -> ( A x. C ) < ( B x. C ) ) ) $=
      ( cc0 clt wbr cmin co cmul resubcli mulgt0i expcom caddc recni addid2i
      breq1i 0re ltaddsubi bitr3i subdiri breq2i remulcli 3bitr2ri 3imtr4g ) GC
      HIZGBAJKZHIZGUICLKZHIZABHIZACLKZBCLKZHIZUJUHULUICBAEDMFNOUMGAPKZBHIUJUQAB
      HAADQZRSGABTDEUAUBULGUOUNJKZHIGUNPKZUOHIUPUKUSGHBACBEQURCFQUCUDGUNUOTACDF
      UEZBCEFUEUAUTUNUOHUNUNVAQRSUFUG $.
      $( [15-May-1999] $)
  $}

  $( 'Less than' relationship between subtraction and addition. $)
  ltsub23 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) < C <-> ( A - C ) < B ) ) $=
    ( cr wcel w3a cmin co clt wbr caddc ltsubadd wb ltsubadd2 3com23 bitr4d )
    ADEZBDEZCDEZFABGHCIJACBKHIJZACGHBIJZABCLQSRUATMACBNOP $.
    $( [4-Oct-1999] $)

  $( 'Less than' relationship between subtraction and addition. $)
  ltsub13 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( A < ( B - C ) <-> C < ( B - A ) ) ) $=
    ( cr wcel cmin co clt wbr wb w3a caddc ltaddsub ltaddsub2 bitr3d 3com23 )
    ADEZCDEZBDEZABCFGHIZCBAFGHIZJQRSKACLGBHITUAACBMACBNOP $.
    $( [28-Nov-2004] $) $( [17-Nov-2004] $)

  $( Adding both sides of two 'less than' relations.  Theorem I.25 of [Apostol]
     p. 20. $)
  lt2add $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A < C /\ B < D ) -> ( A + B ) < ( C + D ) ) ) $=
    ( cr wcel clt wbr wa caddc co wi cc0 cif wceq breq1 anbi1d opreq1 breq1d
    imbi12d anbi2d opreq2 breq2 breq2d 0re elimel lt2addi dedth4h ) AEFZBEFZCEF
    ZDEFZACGHZBDGHZIZABJKZCDJKZGHZLUIAMNZCGHZUNIZUSBJKZUQGHZLUTUJBMNZDGHZIZUSVD
    JKZUQGHZLUSUKCMNZGHZVEIZVGVIDJKZGHZLVJVDULDMNZGHZIZVGVIVNJKZGHZLABCDMMMMAUS
    OZUOVAURVCVSUMUTUNAUSCGPQVSUPVBUQGAUSBJRSTBVDOZVAVFVCVHVTUNVEUTBVDDGPUAVTVB
    VGUQGBVDUSJUBSTCVIOZVFVKVHVMWAUTVJVECVIUSGUCQWAUQVLVGGCVIDJRUDTDVNOZVKVPVMV
    RWBVEVOVJDVNVDGUCUAWBVLVQVGGDVNVIJUBUDTUSVDVIVNAMEUEUFBMEUEUFCMEUEUFDMEUEUF
    UGUH $.
    $( [15-Aug-1999] $)

  $( Adding both sides of two 'less than or equal to' relations. $)
  le2add $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A <_ C /\ B <_ D ) -> ( A + B ) <_ ( C + D ) ) ) $=
    ( cr wcel cle wbr wa caddc co wi cc0 cif wceq breq1 anbi1d opreq1 breq1d
    imbi12d anbi2d opreq2 breq2 breq2d 0re elimel le2addi dedth4h ) AEFZBEFZCEF
    ZDEFZACGHZBDGHZIZABJKZCDJKZGHZLUIAMNZCGHZUNIZUSBJKZUQGHZLUTUJBMNZDGHZIZUSVD
    JKZUQGHZLUSUKCMNZGHZVEIZVGVIDJKZGHZLVJVDULDMNZGHZIZVGVIVNJKZGHZLABCDMMMMAUS
    OZUOVAURVCVSUMUTUNAUSCGPQVSUPVBUQGAUSBJRSTBVDOZVAVFVCVHVTUNVEUTBVDDGPUAVTVB
    VGUQGBVDUSJUBSTCVIOZVFVKVHVMWAUTVJVECVIUSGUCQWAUQVLVGGCVIDJRUDTDVNOZVKVPVMV
    RWBVEVOVJDVNVDGUCUAWBVLVQVGGDVNVIJUBUDTUSVDVIVNAMEUEUFBMEUEUFCMEUEUFDMEUEUF
    UGUH $.
    $( [16-Jun-2005] $) $( [17-Apr-2005] $)

  $( Adding both sides of two orderings. $)
  ltleadd $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A < C /\ B <_ D ) -> ( A + B ) < ( C + D ) ) ) $=
    ( cr wcel wa clt wbr cle caddc co wb ltadd1 3com23 3expa adantrr leadd2
    3expb adantll anbi12d wi ltletr readdcl adantr ancoms ad2ant2lr adantl
    syl3anc sylbid ) AEFZBEFZGZCEFZDEFZGZGZACHIZBDJIZGABKLZCBKLZHIZVACDKLZJIZGZ
    UTVCHIZUQURVBUSVDUMUNURVBMZUOUKULUNVGUKUNULVGACBNOPQULUPUSVDMZUKULUNUOVHULU
    OUNVHBDCROSTUAUTEFZVAEFZVCEFZVEVFUBUQUTVAVCUCUMVIUPABUDUEULUNVJUKUOUNULVJCB
    UDUFUGUPVKUMCDUDUHUIUJ $.
    $( [24-Dec-2007] $) $( [23-Dec-2007] $)

  $( Adding both sides of two orderings. $)
  leltadd $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A <_ C /\ B < D ) -> ( A + B ) < ( C + D ) ) ) $=
    ( cr wcel wa cle wbr clt caddc co wi ltleadd ancomsd ancom2s ancom1s cc
    wceq addcom recn syl2an breqan12d sylibrd ) AEFZBEFZGZCEFZDEFZGZGACHIZBDJIZ
    GZBAKLZDCKLZJIZABKLZCDKLZJIUFUEUJUMUPMZUFUEGZUIUHUSUTUIUHGGULUKUPBADCNOPQUG
    UJUQUNURUOJARFBRFUQUNSUEUFABTAUABUAUBCRFDRFURUOSUHUICDTCUADUAUBUCUD $.
    $( [27-Aug-2008] $) $( [15-Aug-2008] $)

  $( The sum of 2 positive numbers is positive. $)
  addgt0 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 0 < A /\ 0 < B ) ) ->
                0 < ( A + B ) ) $=
    ( cr wcel wa cc0 clt wbr caddc co wi cif wceq breq2 anbi1d opreq1 breq2d
    imbi12d anbi2d opreq2 0re elimel addgt0i dedth2h imp ) ACDZBCDZEFAGHZFBGHZE
    ZFABIJZGHZUFUGUJULKFUFAFLZGHZUIEZFUMBIJZGHZKUNFUGBFLZGHZEZFUMURIJZGHZKABFFA
    UMMZUJUOULUQVCUHUNUIAUMFGNOVCUKUPFGAUMBIPQRBURMZUOUTUQVBVDUIUSUNBURFGNSVDUP
    VAFGBURUMITQRUMURAFCUAUBBFCUAUBUCUDUE $.
    $( [7-Jun-2005] $) $( [1-Jun-2005] $)

  $( The sum of nonnegative and positive numbers is positive. $)
  addgegt0 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 0 <_ A /\ 0 < B ) ) ->
                  0 < ( A + B ) ) $=
    ( cr wcel wa cc0 cle wbr clt caddc co wi cif wceq breq2 anbi1d opreq1
    breq2d imbi12d anbi2d opreq2 0re elimel addgegt0i dedth2h imp ) ACDZBCDZEFA
    GHZFBIHZEZFABJKZIHZUGUHUKUMLFUGAFMZGHZUJEZFUNBJKZIHZLUOFUHBFMZIHZEZFUNUSJKZ
    IHZLABFFAUNNZUKUPUMURVDUIUOUJAUNFGOPVDULUQFIAUNBJQRSBUSNZUPVAURVCVEUJUTUOBU
    SFIOTVEUQVBFIBUSUNJUARSUNUSAFCUBUCBFCUBUCUDUEUF $.
    $( [25-Jan-2006] $) $( [28-Dec-2005] $)

  $( The sum of nonnegative and positive numbers is positive. $)
  addgtge0 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 0 < A /\ 0 <_ B ) ) ->
                  0 < ( A + B ) ) $=
    ( cr wcel wa cc0 clt wbr cle caddc co addgegt0 ancom syl2anb wceq cc
    addcom recn syl2an adantr breqtrrd ) ACDZBCDZEZFAGHZFBIHZEZEFBAJKZABJKZGUCU
    BEUFUEEFUHGHUDUGBALUBUCMUEUFMNUDUIUHOZUGAPDBPDUJUBUCABQARBRSTUA $.
    $( [4-Feb-2006] $) $( [28-Dec-2005] $)

  $( The sum of 2 nonnegative numbers is nonnegative. $)
  addge0 $p |- ( ( ( A e. RR /\ B e. RR ) /\
               ( 0 <_ A /\ 0 <_ B ) ) -> 0 <_ ( A + B ) ) $=
    ( cr wcel wa cc0 cle wbr caddc co wi cif wceq breq2 anbi1d opreq1 breq2d
    imbi12d anbi2d opreq2 0re elimel addge0i dedth2h imp ) ACDZBCDZEFAGHZFBGHZE
    ZFABIJZGHZUFUGUJULKFUFAFLZGHZUIEZFUMBIJZGHZKUNFUGBFLZGHZEZFUMURIJZGHZKABFFA
    UMMZUJUOULUQVCUHUNUIAUMFGNOVCUKUPFGAUMBIPQRBURMZUOUTUQVBVDUIUSUNBURFGNSVDUP
    VAFGBURUMITQRUMURAFCUAUBBFCUAUBUCUDUE $.
    $( [3-Apr-2005] $) $( [17-Mar-2005] $)

  $( Adding a positive number to another number increases it. $)
  ltaddpos $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 < A <-> B < ( B + A ) ) ) $=
    ( cr wcel wa cc0 clt wbr caddc co wb 0re ltadd2 mp3an1 wceq cc recn addid1
    syl adantl breq1d bitrd ) ACDZBCDZEZFAGHZBFIJZBAIJZGHZBUHGHFCDUCUDUFUIKLFAB
    MNUEUGBUHGUDUGBOZUCUDBPDUJBQBRSTUAUB $.
    $( [29-Nov-2004] $) $( [17-Nov-2004] $)

  $( Adding a positive number to another number increases it. $)
  ltaddpos2 $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 < A <-> B < ( A + B ) ) ) $=
    ( cr wcel wa cc0 clt wbr caddc co ltaddpos cc wceq addcom recn syl2an
    breq2d bitr4d ) ACDZBCDZEZFAGHBBAIJZGHBABIJZGHABKUAUCUBBGALDBLDUCUBMSTABNAO
    BOPQR $.
    $( [3-May-2005] $) $( [8-Apr-2005] $)

  $( Subtracting a positive number from another number decreases it. $)
  ltsubpos $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 < A <-> ( B - A ) < B ) ) $=
    ( cr wcel wa cc0 clt wbr caddc co cmin ltaddpos wb ltsubadd pm3.27 pm3.26
    syl3anc bitr4d ) ACDZBCDZEZFAGHBBAIJGHZBAKJBGHZABLTSTUCUBMUABABNSTOZSTPUDQR
    $.
    $( [29-Nov-2004] $) $( [17-Nov-2004] $)

  $( Comparison of two numbers whose difference is positive. $)
  posdif $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> 0 < ( B - A ) ) ) $=
    ( cr wcel wa cc0 cmin co clt wbr caddc wb ltaddpos resubcl ancoms pm3.26
    sylanc cc wceq pncan3 recn syl2an breq2d bitr2d ) ACDZBCDZEZFBAGHZIJZAAUHKH
    ZIJZABIJUHCDZUEUIUKLUGUHAMUFUEULBANOUEUFPQUGUJBAIARDBRDUJBSUEUFABTAUABUAUBU
    CUD $.
    $( [18-Dec-2004] $) $( [17-Nov-2004] $)

  $( Negative of both sides of 'less than'.  Theorem I.23 of [Apostol]
     p. 20. $)
  ltneg $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> -u B < -u A ) ) $=
    ( cr wcel clt wbr cneg wb cc0 cif wceq breq1 negeq breq2d bibi12d breq2
    breq1d 0re elimel ltnegi dedth2h ) ACDZBCDZABEFZBGZAGZEFZHUBAIJZBEFZUEUHGZE
    FZHUHUCBIJZEFZULGZUJEFZHABIIAUHKZUDUIUGUKAUHBELUPUFUJUEEAUHMNOBULKZUIUMUKUO
    BULUHEPUQUEUNUJEBULMQOUHULAICRSBICRSTUA $.
    $( [27-Aug-1999] $)

  $( Contraposition of negative in 'less than'. $)
  ltnegcon1 $p |- ( ( A e. RR /\ B e. RR ) -> ( -u A < B <-> -u B < A ) ) $=
    ( cr wcel wa cneg clt wbr wb ltneg renegcl sylan cc wceq recn negneg syl
    breq2d adantr bitrd ) ACDZBCDZEAFZBGHZBFZUCFZGHZUEAGHZUCCDUBUDUGIUAUCBJAKLU
    AUGUHIUBUAUFAUEGUAAMDUFANAOAPQRST $.
    $( [12-Nov-2004] $) $( [8-Nov-2004] $)

  $( Negative of both sides of 'less than or equal to'. $)
  leneg $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B <-> -u B <_ -u A ) ) $=
    ( cr wcel cle wbr cneg wb cc0 cif wceq breq1 negeq breq2d bibi12d breq2
    breq1d 0re elimel lenegi dedth2h ) ACDZBCDZABEFZBGZAGZEFZHUBAIJZBEFZUEUHGZE
    FZHUHUCBIJZEFZULGZUJEFZHABIIAUHKZUDUIUGUKAUHBELUPUFUJUEEAUHMNOBULKZUIUMUKUO
    BULUHEPUQUEUNUJEBULMQOUHULAICRSBICRSTUA $.
    $( [12-Sep-1999] $)

  $( Contraposition of negative in 'less than or equal to'. $)
  lenegcon1 $p |- ( ( A e. RR /\ B e. RR ) -> ( -u A <_ B <-> -u B <_ A ) ) $=
    ( cr wcel wa cneg cle wbr wb leneg renegcl sylan cc wceq recn negneg syl
    breq2d adantr bitrd ) ACDZBCDZEAFZBGHZBFZUCFZGHZUEAGHZUCCDUBUDUGIUAUCBJAKLU
    AUGUHIUBUAUFAUEGUAAMDUFANAOAPQRST $.
    $( [14-Jun-2004] $) $( [10-May-2004] $)

  $( Contraposition of negative in 'less than or equal to'. $)
  lenegcon2 $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ -u B <-> B <_ -u A ) ) $=
    ( cr wcel wa cneg cle wbr wb leneg renegcl sylan2 wceq cc recn negneg syl
    adantl breq1d bitrd ) ACDZBCDZEZABFZGHZUDFZAFZGHZBUGGHUAUDCDUEUHIUBAUDJBKLU
    CUFBUGGUBUFBMZUAUBBNDUIBOBPQRST $.
    $( [9-Oct-2005] $) $( [8-Oct-2005] $)

  $( Subtraction from both sides of 'less than or equal to'. $)
  lesub1 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( A - C ) <_ ( B - C ) ) ) $=
    ( cr wcel w3a cle wbr cneg caddc co cmin wb leadd1 renegcl syl3an3 cc wceq
    negsub 3adant2 3adant1 breq12d recn syl3an bitrd ) ADEZBDEZCDEZFABGHZACIZJK
    ZBUJJKZGHZACLKZBCLKZGHZUFUGUJDEUIUMMUHABUJNCOPAQEZBQEZCQEZUMUPMUFUGUHUQURUS
    FUKUNULUOGUQUSUKUNRURACSTURUSULUORUQBCSUAUBAUCBUCCUCUDUE $.
    $( [1-Aug-2006] $) $( [13-May-2004] $)

  $( Subtraction of both sides of 'less than or equal to'. $)
  lesub2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( C - B ) <_ ( C - A ) ) ) $=
    ( cr wcel w3a cle wbr cneg caddc co cmin wb leneg 3adant3 leadd2 renegcl
    syl3an2 syl3an1 3com12 cc wceq negsub 3adant2 breq12d 3coml recn syl3an
    3bitrd ) ADEZBDEZCDEZFABGHZBIZAIZGHZCUNJKZCUOJKZGHZCBLKZCALKZGHZUJUKUMUPMUL
    ABNOUKUJULUPUSMZUNDEZUJULVCUKVDUODEULVCUJUNUOCPAQRBQSTAUAEZBUAEZCUAEZUSVBMZ
    UJUKULVGVEVFVHVGVEVFFUQUTURVAGVGVFUQUTUBVECBUCUDVGVEURVAUBVFCAUCOUEUFAUGBUG
    CUGUHUI $.
    $( [1-Aug-2006] $) $( [29-Sep-2005] $)

  $( Subtraction from both sides of 'less than'.  (Contributed by FL,
     3-Jan-2008.) $)
  ltsub1 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( A - C ) < ( B - C ) ) ) $=
    ( cr wcel w3a clt wbr cneg caddc co cmin wb ltadd1 renegcl syl3an3 cc wceq
    negsub 3adant2 3adant1 breq12d recn syl3an bitrd ) ADEZBDEZCDEZFABGHZACIZJK
    ZBUJJKZGHZACLKZBCLKZGHZUFUGUJDEUIUMMUHABUJNCOPAQEZBQEZCQEZUMUPMUFUGUHUQURUS
    FUKUNULUOGUQUSUKUNRURACSTURUSULUORUQBCSUAUBAUCBUCCUCUDUE $.
    $( [3-Jan-2008] $) $( [3-Jan-2008] $)

  $( Subtraction of both sides of 'less than'. $)
  ltsub2 $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( C - B ) < ( C - A ) ) ) $=
    ( cr wcel w3a clt wbr cneg caddc co cmin wb ltneg 3adant3 ltadd2 renegcl
    syl3an2 syl3an1 3com12 cc wceq negsub 3adant2 breq12d 3coml recn syl3an
    3bitrd ) ADEZBDEZCDEZFABGHZBIZAIZGHZCUNJKZCUOJKZGHZCBLKZCALKZGHZUJUKUMUPMUL
    ABNOUKUJULUPUSMZUNDEZUJULVCUKVDUODEULVCUJUNUOCPAQRBQSTAUAEZBUAEZCUAEZUSVBMZ
    UJUKULVGVEVFVHVGVEVFFUQUTURVAGVGVFUQUTUBVECBUCUDVGVEURVAUBVFCAUCOUEUFAUGBUG
    CUGUHUI $.
    $( [1-Aug-2006] $) $( [29-Sep-2005] $)

  ${
    ltaddpos.1 $e |- A e. RR $.
    ltaddpos.2 $e |- B e. RR $.
    $( Adding a positive number to another number increases it. $)
    ltaddposi $p |- ( 0 < A <-> B < ( B + A ) ) $=
      ( cr wcel cc0 clt wbr caddc co wb ltaddpos mp2an ) AEFBEFGAHIBBAJKHILCDAB
      MN $.
      $( [25-Aug-1999] $)

    $( Comparison of two numbers whose difference is positive. $)
    posdifi $p |- ( A < B <-> 0 < ( B - A ) ) $=
      ( cr wcel clt wbr cc0 cmin co wb posdif mp2an ) AEFBEFABGHIBAJKGHLCDABMN
      $.
      $( [17-Nov-2004] $) $( [19-Aug-2001] $)

    $( Contraposition of negative in 'less than'. $)
    ltnegcon1i $p |- ( -u A < B <-> -u B < A ) $=
      ( cr wcel cneg clt wbr wb ltnegcon1 mp2an ) AEFBEFAGBHIBGAHIJCDABKL $.
      $( [14-May-1999] $)

    $( Contraposition of negative in 'less than or equal to'. $)
    lenegcon1i $p |- ( -u A <_ B <-> -u B <_ A ) $=
      ( cr wcel cneg cle wbr wb lenegcon1 mp2an ) AEFBEFAGBHIBGAHIJCDABKL $.
      $( [3-May-2005] $) $( [6-Apr-2005] $)
  $}

  $( Comparison of a number and its negative to zero.  Theorem I.23 of
     [Apostol] p. 20. $)
  lt0neg1 $p |- ( A e. RR -> ( A < 0 <-> 0 < -u A ) ) $=
    ( cr wcel cc0 clt wbr cneg wb 0re ltneg mpan2 neg0 breq1i syl6bb ) ABCZADEF
    ZDGZAGZEFZDREFODBCPSHIADJKQDRELMN $.
    $( [14-May-1999] $)

  $( Comparison of a number and its negative to zero. $)
  lt0neg2 $p |- ( A e. RR -> ( 0 < A <-> -u A < 0 ) ) $=
    ( cr wcel cc0 clt wbr cneg wb 0re ltneg mpan neg0 breq2i syl6bb ) ABCZDAEFZ
    AGZDGZEFZQDEFDBCOPSHIDAJKRDQELMN $.
    $( [10-May-2004] $) $( [10-May-2004] $)

  $( Comparison of a number and its negative to zero. $)
  le0neg1 $p |- ( A e. RR -> ( A <_ 0 <-> 0 <_ -u A ) ) $=
    ( cr wcel cc0 cle wbr cneg wb 0re leneg mpan2 neg0 breq1i syl6bb ) ABCZADEF
    ZDGZAGZEFZDREFODBCPSHIADJKQDRELMN $.
    $( [15-Jun-2004] $) $( [10-May-2004] $)

  $( Comparison of a number and its negative to zero. $)
  le0neg2 $p |- ( A e. RR -> ( 0 <_ A <-> -u A <_ 0 ) ) $=
    ( cr wcel cc0 cle wbr cneg wb 0re leneg mpan neg0 breq2i syl6bb ) ABCZDAEFZ
    AGZDGZEFZQDEFDBCOPSHIDAJKRDQELMN $.
    $( [24-Aug-1999] $)

  $( A number is less than or equal to itself plus a nonnegative number. $)
  addge01 $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 <_ B <-> A <_ ( A + B ) ) ) $=
    ( cr wcel wa cc0 cle wbr caddc co wb 0re leadd2 mp3an1 ancoms wceq cc recn
    addid1 syl adantr breq1d bitrd ) ACDZBCDZEZFBGHZAFIJZABIJZGHZAUIGHUEUDUGUJK
    ZFCDUEUDUKLFBAMNOUFUHAUIGUDUHAPZUEUDAQDULARASTUAUBUC $.
    $( [28-Jul-2005] $) $( [21-Feb-2005] $)

  $( A number is less than or equal to itself plus a nonnegative number. $)
  addge02 $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 <_ B <-> A <_ ( B + A ) ) ) $=
    ( cr wcel wa cc0 cle wbr caddc co addge01 cc wceq addcom recn syl2an
    breq2d bitrd ) ACDZBCDZEZFBGHAABIJZGHABAIJZGHABKUAUBUCAGALDBLDUBUCMSTABNAOB
    OPQR $.
    $( [30-Jul-2005] $) $( [27-Jul-2005] $)

  $( Nonnegative subtraction. $)
  subge0 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( 0 <_ ( A - B ) <-> B <_ A ) ) $=
    ( cr wcel wa cle wbr cneg caddc co cc0 cmin wb leadd1 pm3.27 pm3.26
    renegcl adantl syl3anc cc wceq negid negsub breq12d recn syl2an bitr2d ) AC
    DZBCDZEZBAFGZBBHZIJZAULIJZFGZKABLJZFGZUIUHULCDZUKUOMUJBAULNUHUIOUHUIPUIURUH
    BQRSATDZBTDZUOUQMUHUIUSUTEUMKUNUPFUTUMKUAUSBUBRABUCUDAUEBUEUFUG $.
    $( [15-Mar-2005] $) $( [14-Mar-2005] $)

  $( Nonpositive subtraction. $)
  suble0 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( ( A - B ) <_ 0 <-> A <_ B ) ) $=
    ( cr wcel wa cmin co cc0 cle wbr cneg wb resubcl le0neg1 syl cc wceq
    negsubdi2 recn syl2an breq2d subge0 ancoms 3bitrd ) ACDZBCDZEZABFGZHIJZHUHK
    ZIJZHBAFGZIJZABIJZUGUHCDUIUKLABMUHNOUGUJULHIAPDBPDUJULQUEUFABRASBSTUAUFUEUM
    UNLBAUBUCUD $.
    $( [24-Mar-2008] $) $( [20-Mar-2008] $)

  ${
    subge0.1 $e |- A e. RR $.
    subge0.2 $e |- B e. RR $.
    $( Nonnegative subtraction. $)
    subge0i $p |- ( 0 <_ ( A - B ) <-> B <_ A ) $=
      ( cr wcel cc0 cmin co cle wbr wb subge0 mp2an ) AEFBEFGABHIJKBAJKLCDABMN
      $.
      $( [13-Aug-2000] $)
  $}

  $( Nonnegative subtraction. $)
  subge02 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( 0 <_ B <-> ( A - B ) <_ A ) ) $=
    ( cr wcel wa cc0 cle wbr caddc co cmin addge01 wb lesubadd 3anidm13 bitr4d
    ) ACDZBCDZEFBGHAABIJGHZABKJAGHZABLQRTSMABANOP $.
    $( [31-Jul-2005] $) $( [27-Jul-2005] $)

  $( Lemma to show a nonnegative number is zero. $)
  lesub0 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( ( 0 <_ A /\ B <_ ( B - A ) ) <-> A = 0 ) ) $=
    ( cr wcel cc0 cle wbr cmin co wa wceq wb cif breq2 opreq2 breq2d anbi12d
    eqeq1 bibi12d id opreq1 breq12d anbi2d bibi1d 0re elimel lesub0i dedth2h )
    ACDZBCDZEAFGZBBAHIZFGZJZAEKZLEUIAEMZFGZBBUPHIZFGZJZUPEKZLUQUJBEMZVBUPHIZFGZ
    JZVALABEEAUPKZUNUTUOVAVFUKUQUMUSAUPEFNVFULURBFAUPBHOPQAUPERSBVBKZUTVEVAVGUS
    VDUQVGBVBURVCFVGTBVBUPHUAUBUCUDUPVBAECUEUFBECUEUFUGUH $.
    $( [8-Oct-1999] $)

  $( The product of two nonnegative numbers is nonnegative. $)
  mulge0 $p |- ( ( ( A e. RR /\ 0 <_ A ) /\
                ( B e. RR /\ 0 <_ B ) ) -> 0 <_ ( A x. B ) ) $=
    ( cr wcel cc0 cle wbr cmul co wa wi cif wceq breq2 anbi1d opreq1 breq2d
    imbi12d anbi2d opreq2 0re elimel mulge0i dedth2h imp an4s ) ACDZBCDZEAFGZEB
    FGZEABHIZFGZUGUHJUIUJJZULUGUHUMULKEUGAELZFGZUJJZEUNBHIZFGZKUOEUHBELZFGZJZEU
    NUSHIZFGZKABEEAUNMZUMUPULURVDUIUOUJAUNEFNOVDUKUQEFAUNBHPQRBUSMZUPVAURVCVEUJ
    UTUOBUSEFNSVEUQVBEFBUSUNHTQRUNUSAECUAUBBECUAUBUCUDUEUF $.
    $( [16-Jun-2009] $) $( [8-Oct-1999] $)

  $( The product of two nonnegative numbers is nonnegative. $)
  mulge0OLD $p |- ( ( ( A e. RR /\ B e. RR ) /\
                ( 0 <_ A /\ 0 <_ B ) ) -> 0 <_ ( A x. B ) ) $=
    ( cr wcel wa cc0 cle wbr cmul co wi cif wceq breq2 anbi1d opreq1 breq2d
    imbi12d anbi2d opreq2 0re elimel mulge0i dedth2h imp ) ACDZBCDZEFAGHZFBGHZE
    ZFABIJZGHZUFUGUJULKFUFAFLZGHZUIEZFUMBIJZGHZKUNFUGBFLZGHZEZFUMURIJZGHZKABFFA
    UMMZUJUOULUQVCUHUNUIAUMFGNOVCUKUPFGAUMBIPQRBURMZUOUTUQVBVDUIUSUNBURFGNSVDUP
    VAFGBURUMITQRUMURAFCUAUBBFCUAUBUCUDUE $.
    $( [17-Jul-2005] $) $( [8-Oct-1999] $)

  $( The product of two negative numbers is positive.  (Contributed by Jeffrey
     Hankins, 8-Jun-2009.) $)
  mullt0 $p |- ( ( ( A e. RR /\ A < 0 ) /\ ( B e. RR /\ B < 0 ) )
               -> 0 < ( A x. B ) ) $=
    ( cr wcel cc0 clt wbr wa cneg cmul co mulgt0 renegcl adantr lt0neg1 biimpa
    jca syl2an wceq cc mul2neg recn ad2ant2r breqtrd ) ACDZAEFGZHZBCDZBEFGZHZHE
    AIZBIZJKZABJKZFUKCDZEUKFGZHULCDZEULFGZHEUMFGUGUJUKULLUGUOUPUEUOUFAMNUEUFUPA
    OPQUJUQURUHUQUIBMNUHUIURBOPQRUEUHUMUNSZUFUIATDBTDUSUEUHABUAAUBBUBRUCUD $.
    $( [12-Jun-2009] $) $( [8-Jun-2009] $)

  $( 0 is less than 1.  Theorem I.21 of [Apostol] p. 20. $)
  lt01 $p |- 0 < 1 $=
    ( cc0 c1 cmul co clt wne wbr ax1ne0 1re msqgt0i ax-mp ax1cn mulid1i
    breqtri ) ABBCDZBEBAFAOEGHBIJKBLMN $.
    $( [17-Jan-1997] $)

