$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordering on reals (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Equality implies 'less than or equal to'. $)
  eqlet $p |- ( ( A e. RR /\ A = B ) -> A <_ B ) $=
    ( cle wbr wceq cr wcel breq2 biimpac leidt sylan ) AACDZABEZABCDZAFGMLNABAC
    HIAJK $.
    $( [6-Apr-2005] $) $( [4-Apr-2005] $)

  ${
    lt.1 $e |- A e. RR $.

    ${
      lt.2 $e |- B e. RR $.
      $( Consequence of trichotomy. $)
      lttri2 $p |- ( A =/= B <-> ( A < B \/ B < A ) ) $=
        ( cr wcel wne clt wbr wo wb lttri2t mp2an ) AEFBEFABGABHIBAHIJKCDABLM
        $.
        $( [22-Aug-2007] $) $( [19-Jan-1997] $)

      $( Consequence of trichotomy. $)
      lttri3 $p |- ( A = B <-> ( -. A < B /\ -. B < A ) ) $=
        ( cr wcel wceq clt wbr wn wa wb lttri3t mp2an ) AEFBEFABGABHIJBAHIJKLCD
        ABMN $.
        $( [14-May-1999] $)

      $( Consequence of trichotomy. $)
      letri3 $p |- ( A = B <-> ( A <_ B /\ B <_ A ) ) $=
        ( cr wcel wceq cle wbr wa wb letri3t mp2an ) AEFBEFABGABHIBAHIJKCDABLM
        $.
        $( [14-May-1999] $)

      $( 'Less than or equal to' in terms of 'less than'. $)
      leloe $p |- ( A <_ B <-> ( A < B \/ A = B ) ) $=
        ( cr wcel cle wbr clt wceq wo wb leloet mp2an ) AEFBEFABGHABIHABJKLCDAB
        MN $.
        $( [14-May-1999] $)

      $( 'Less than' expressed in terms of 'less than or equal to'. $)
      ltlen $p |- ( A < B <-> ( A <_ B /\ B =/= A ) ) $=
        ( cr wcel clt wbr cle wne wa wb ltlent mp2an ) AEFBEFABGHABIHBAJKLCDABM
        N $.
        $( [8-Aug-2007] $) $( [27-Oct-1999] $)

      $( 'Less than' is not symmetric. $)
      ltnsym $p |- ( A < B -> -. B < A ) $=
        ( cr wcel clt wbr wn wi ltnsymt mp2an ) AEFBEFABGHBAGHIJCDABKL $.
        $( [6-May-1999] $)

      $( 'Less than or equal to'  in terms of 'less than'. $)
      lenlt $p |- ( A <_ B <-> -. B < A ) $=
        ( cr wcel cle wbr clt wn wb lenltt mp2an ) AEFBEFABGHBAIHJKCDABLM $.
        $( [24-May-1999] $)

      $( 'Less than'  in terms of 'less than or equal to'. $)
      ltnle $p |- ( A < B <-> -. B <_ A ) $=
        ( cle wbr clt lenlt con2bii ) BAEFABGFBADCHI $.
        $( [12-Jul-2005] $) $( [11-Jul-2005] $)

      $( 'Less than' implies 'less than or equal to'. $)
      ltle $p |- ( A < B -> A <_ B ) $=
        ( cr wcel clt wbr cle wi ltlet mp2an ) AEFBEFABGHABIHJCDABKL $.
        $( [14-May-1999] $)

      ${
        ltlei.1 $e |- A < B $.
        $( 'Less than' implies 'less than or equal to' (inference). $)
        ltlei $p |- A <_ B $=
          ( clt wbr cle ltle ax-mp ) ABFGABHGEABCDIJ $.
          $( [22-Aug-1999] $)
      $}

      $( Equality implies 'less than or equal to'. $)
      eqle $p |- ( A = B -> A <_ B ) $=
        ( wceq cle wbr letri3 pm3.26bi ) ABEABFGBAFGABCDHI $.
        $( [23-May-1999] $)

      $( 'Less than' implies not equal. $)
      ltne $p |- ( A < B -> B =/= A ) $=
        ( cr wcel clt wbr wne ltnet mp3an12 ) AEFBEFABGHBAICDABJK $.
        $( [14-Aug-2007] $) $( [28-Jul-1999] $)

      $( 'Less than' implies not equal. $)
      ltneOLD $p |- ( A < B -> -. A = B ) $=
        ( cr wcel clt wbr wceq wn wi ltnetOLD mp2an ) AEFBEFABGHABIJKCDABLM $.
        $( [28-Jul-1999] $)

      $( Trichotomy law for 'less than or equal to'. $)
      letri $p |- ( A <_ B \/ B <_ A ) $=
        ( cle wbr wn clt ltnle ltle sylbir orri ) ABEFZBAEFZMGBAHFNBADCIBADCJKL
        $.
        $( [2-Aug-1999] $)

      ${
        lt.3 $e |- C e. RR $.
        $( 'Less than' is transitive.  Theorem I.17 of [Apostol] p. 20. $)
        lttr $p |- ( ( A < B /\ B < C ) -> A < C ) $=
          ( cr wcel clt wbr wa wi axlttrn mp3an ) AGHBGHCGHABIJBCIJKACIJLDEFABC
          MN $.
          $( [14-May-1999] $)

        $( 'Less than or equal to', 'less than' transitive law. $)
        lelttr $p |- ( ( A <_ B /\ B < C ) -> A < C ) $=
          ( cr wcel cle wbr clt wa wi lelttrt mp3an ) AGHBGHCGHABIJBCKJLACKJMDE
          FABCNO $.
          $( [14-May-1999] $)

        $( 'Less than', 'less than or equal to' transitive law. $)
        ltletr $p |- ( ( A < B /\ B <_ C ) -> A < C ) $=
          ( cr wcel clt wbr cle wa wi ltletrt mp3an ) AGHBGHCGHABIJBCKJLACIJMDE
          FABCNO $.
          $( [14-May-1999] $)

        $( 'Less than or equal to' is transitive. $)
        letr $p |- ( ( A <_ B /\ B <_ C ) -> A <_ C ) $=
          ( cr wcel cle wbr wa wi letrt mp3an ) AGHBGHCGHABIJBCIJKACIJLDEFABCMN
          $.
          $( [14-May-1999] $)

        $( Extended trichotomy law for 'less than or equal to'. $)
        le2tri3 $p |- ( ( A <_ B /\ B <_ C /\ C <_ A ) <->
                        ( A = B /\ B = C /\ C = A ) ) $=
          ( cle wbr w3a wceq wa letri3 biimpr letr sylan2 3impb 3comr eqcomd
          sylan 3impa 3jca eqle 3anim123i impbi ) ABGHZBCGHZCAGHZIZABJZBCJZCAJZ
          IUHUIUJUKUEUFUGUIUEBAGHZUIUFUGKUIUEULKABDELMBCAEFDNOPUFUGUEUJUFUGUEUJ
          UFCBGHZUJUGUEKUJUFUMKBCEFLMCABFDENOPQUEUFUGUKACGHZUGUKUEUFKUNUGKZACAC
          JUOACDFLMRABCDEFNSTUAUIUEUJUFUKUGABDEUBBCEFUBCAFDUBUCUD $.
          $( [14-Aug-2000] $)

        $( Addition to both sides of 'less than'.  (Proof shortened by Paul
           Chapman, 27-Jan-2008.) $)
        ltadd2 $p |- ( A < B <-> ( C + A ) < ( C + B ) ) $=
          ( clt wbr caddc co cr wcel wi axltadd mp3an cle wn wceq wo opreq2
          orim12i leloe readdcl 3imtr4 lenlt 3imtr3 a3i impbi ) ABGHZCAIJZCBIJZ
          GHZAKLZBKLZCKLZUIULMDEFABCNOUIULBAPHZUKUJPHZUIQULQBAGHZBARZSUKUJGHZUK
          UJRZSUPUQURUTUSVAUNUMUOURUTMEDFBACNOBACITUABAEDUBUKUJCBFEUCZCAFDUCZUB
          UDBAEDUEUKUJVBVCUEUFUGUH $.
          $( [28-Jan-2008] $) $( [21-Jan-1997] $)

        $( Addition to both sides of 'less than'.  Theorem I.18 of [Apostol]
           p. 20. $)
        ltadd1 $p |- ( A < B <-> ( A + C ) < ( B + C ) ) $=
          ( clt wbr caddc co ltadd2 recn addcom breq12i bitr ) ABGHCAIJZCBIJZGH
          ACIJZBCIJZGHABCDEFKPRQSGCACFLZADLMCBTBELMNO $.
          $( [21-Jan-1997] $)

        $( Addition to both sides of 'less than or equal to'. $)
        leadd1 $p |- ( A <_ B <-> ( A + C ) <_ ( B + C ) ) $=
          ( clt wbr wn caddc co cle ltadd1 negbii lenlt readdcl 3bitr4 ) BAGHZI
          BCJKZACJKZGHZIABLHTSLHRUABACEDFMNABDEOTSACDFPBCEFPOQ $.
          $( [11-Aug-1999] $)

        $( Addition to both sides of 'less than or equal to'. $)
        leadd2 $p |- ( A <_ B <-> ( C + A ) <_ ( C + B ) ) $=
          ( cle wbr caddc co leadd1 recn addcom breq12i bitr ) ABGHACIJZBCIJZGH
          CAIJZCBIJZGHABCDEFKPRQSGACADLCFLZMBCBELTMNO $.
          $( [11-Aug-1999] $)

        $( 'Less than' relationship between subtraction and addition. $)
        ltsubadd $p |- ( ( A - B ) < C <-> A < ( C + B ) ) $=
          ( cneg caddc co clt wbr cmin renegcl readdcl ltadd1 recn negsub
          breq1i cc0 addass addcom negid eqtr opreq2i addid1 3eqtr 3bitr3 ) ABG
          ZHIZCJKUIBHIZCBHIZJKABLIZCJKAUKJKUICBAUHDBEMZNFEOUIULCJABADPZBEPZQRUJ
          AUKJUJAUHBHIZHIASHIAAUHBUNUHUMPZUOTUPSAHUPBUHHISUHBUQUOUABUOUBUCUDAUN
          UEUFRUG $.
          $( [21-Jan-1997] $)

        $( 'Less than or equal to' relationship between subtraction and
           addition. $)
        lesubadd $p |- ( ( A - B ) <_ C <-> A <_ ( C + B ) ) $=
          ( cmin co clt wbr wceq wo caddc cle ltsubadd recn subadd addcom
          eqeq1i eqcom 3bitr orbi12i resubcl leloe readdcl 3bitr4 ) ABGHZCIJZUG
          CKZLACBMHZIJZAUJKZLUGCNJAUJNJUHUKUIULABCDEFOUIBCMHZAKUJAKULABCADPBEPZ
          CFPZQUMUJABCUNUORSUJATUAUBUGCABDEUCFUDAUJDCBFEUEUDUF $.
          $( [30-Sep-1999] $)

        ${
          lt.4 $e |- D e. RR $.
          $( Adding both side of two inequalities.  Theorem I.25 of [Apostol]
             p. 20. $)
          lt2add $p |- ( ( A < C /\ B < D ) -> ( A + B ) < ( C + D ) ) $=
            ( caddc co clt wbr readdcl lttr ltadd1 ltadd2 syl2anb ) ABIJZCBIJZK
            LSCDIJZKLRTKLACKLBDKLRSTABEFMCBGFMCDGHMNACBEGFOBDCFHGPQ $.
            $( [14-May-1999] $)

          $( Adding both side of two inequalities. $)
          le2add $p |- ( ( A <_ C /\ B <_ D ) -> ( A + B ) <_ ( C + D ) ) $=
            ( caddc co cle wbr readdcl letr leadd1 leadd2 syl2anb ) ABIJZCBIJZK
            LSCDIJZKLRTKLACKLBDKLRSTABEFMCBGFMCDGHMNACBEGFOBDCFHGPQ $.
            $( [16-Sep-1999] $)
        $}
      $}

      $( Addition of 2 positive numbers is positive. $)
      addgt0 $p |- ( ( 0 < A /\ 0 < B ) -> 0 < ( A + B ) ) $=
        ( cc0 clt wbr caddc co 0re readdcl lttr ltadd2 recn addid1 breq1i bitr
        sylan2b ) EAFGAABHIZFGZESFGEBFGZEASJCABCDKLUAAEHIZSFGTEBAJDCMUBASFAACNO
        PQR $.
        $( [16-May-1999] $)

      $( Addition of 2 nonnegative numbers is nonnegative. $)
      addge0 $p |- ( ( 0 <_ A /\ 0 <_ B ) -> 0 <_ ( A + B ) ) $=
        ( cc0 clt wbr wceq wo caddc co cle wa addgt0 0re readdcl ltle syl
        opreq1 recn addid2 syl5eqr breq2d biimpa opreq2 addid1 biimpac opreq12
        0cn eqle ccase leloe syl2anb ) EAFGZEAHZIEBFGZEBHZIEABJKZLGZEALGEBLGUNU
        PUOUQUSUNUPMEURFGZUSABCDNEUROABCDPZQZRUOUPMUTUSUOUPUTUOBUREFUOEBJKURBEA
        BJSBBDTUAUBUCUDVBRUNUQMUTUSUQUNUTUQAUREFUQAEJKURAEBAJUEAACTUFUBUCUGVBRU
        OUQMZEURHUSVCEEJKUREEAEBJUHEUIUFUBEUROVAUJRUKEAOCULEBODULUM $.
        $( [28-May-1999] $)

      $( Addition of nonnegative and positive numbers is positive. $)
      addgegt0 $p |- ( ( 0 <_ A /\ 0 < B ) -> 0 < ( A + B ) ) $=
        ( cc0 cle wbr clt caddc co wceq wo wi 0re leloe addgt0 ex opreq1 recn
        addid2 syl5eqr breq2d biimpd jaoi sylbi imp ) EAFGZEBHGZEABIJZHGZUGEAHG
        ZEAKZLUHUJMZEANCOUKUMULUKUHUJABCDPQULUHUJULBUIEHULEBIJUIBEABIRBBDSTUAUB
        UCUDUEUF $.
        $( [25-Sep-1999] $)

      ${
        addgt0i.3 $e |- 0 < A $.
        addgt0i.4 $e |- 0 < B $.
        $( Addition of 2 positive numbers is positive. $)
        addgt0i $p |- 0 < ( A + B ) $=
          ( cc0 clt wbr caddc co addgt0 mp2an ) GAHIGBHIGABJKHIEFABCDLM $.
          $( [18-May-1999] $)
      $}

      $( Two nonnegative numbers are zero iff their sum is zero. $)
      add20 $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
        ( ( A + B ) = 0 <-> ( A = 0 /\ B = 0 ) ) ) $=
        ( cc0 cle wbr wa caddc co wceq clt wo wi wn readdcl 0re lttri3
        pm3.27bi addgt0 nsyl3 pm2.21d opreq1 recn addid2 syl5eqr eqeq1d
        biimprd eqcom biimp jctild opreq2 addid1 jctird ccase2 leloe syl2anb
        0cn syl6eq sylan9eq impbid1 ) EAFGZEBFGZHABIJZEKZAEKZBEKZHZEALGZEAKZMEB
        LGZEBKZMVEVHNZVBVCVIVKVJVLVMVIVKHZVEVHVEEVDLGZVNVEVDELGOVOOVDEABCDPQRSA
        BCDTUAUBVJVEVGVFVJVGVEVJBVDEVJEBIJZVDBEABIUCBBDUDUEUFUGUHVJVFEAUIUJUKVL
        VEVFVGVLVFVEVLAVDEVLAEIJVDAEBAIULAACUDUMUFUGUHVLVGEBUIUJUNUOEAQCUPEBQDU
        PUQVFVGVDVPEAEBIUCVGVPEEIJEBEEIULEURUMUSUTVA $.
        $( [28-Jul-1999] $)

      $( Negative of both sides of 'less than'.  Theorem I.23 of [Apostol]
         p. 20. $)
      ltneg $p |- ( A < B <-> -u B < -u A ) $=
        ( clt wbr cneg caddc co renegcl readdcl ltadd1 cc0 recn negid opreq1i
        addass addid2 3eqtr3 add12 opreq2i addid1 3eqtr breq12i bitr ) ABEFAAGZ
        BGZHIZHIZBUHHIZEFUGUFEFABUHCDUFUGACJZBDJZKLUIUGUJUFEAUFHIZUGHIMUGHIUIUG
        UMMUGHAACNZOPAUFUGUNUFUKNZUGULNZQUGUPRSUJUFBUGHIZHIUFMHIUFBUFUGBDNZUOUP
        TUQMUFHBUROUAUFUOUBUCUDUE $.
        $( [21-Jan-1997] $)

      $( Negative of both sides of 'less than or equal to'. $)
      leneg $p |- ( A <_ B <-> -u B <_ -u A ) $=
        ( clt wbr wceq wo cneg cle ltneg recn neg11 eqcom bitr2 orbi12i leloe
        renegcl 3bitr4 ) ABEFZABGZHBIZAIZEFZUBUCGZHABJFUBUCJFTUDUAUEABCDKUEBAGU
        ABABDLACLMBANOPABCDQUBUCBDRACRQS $.
        $( [1-Aug-1999] $)

      $( Contraposition of negative in 'less than'. $)
      ltnegcon2 $p |- ( A < -u B <-> B < -u A ) $=
        ( cneg clt wbr renegcl ltneg recn negneg breq1i bitr ) ABEZFGNEZAEZFGBP
        FGANCBDHIOBPFBBDJKLM $.
        $( [14-May-1999] $)

      $( The product of two positive numbers is positive. $)
      mulgt0 $p |- ( ( 0 < A /\ 0 < B ) -> 0 < ( A x. B ) ) $=
        ( cr wcel cc0 clt wbr wa cmul co wi axmulgt0 mp2an ) AEFBEFGAHIGBHIJGAB
        KLHIMCDABNO $.
        $( [16-May-1999] $)

      $( The product of two nonnegative numbers is nonnegative. $)
      mulge0 $p |- ( ( 0 <_ A /\ 0 <_ B ) -> 0 <_ ( A x. B ) ) $=
        ( cc0 clt wbr wceq wo cmul co cle wa mulgt0 0re remulcl ltle syl
        opreq1 recn mul02 syl5eqr eqle opreq2 mul01 ccase2 leloe syl2anb ) EAFG
        ZEAHZIEBFGZEBHZIEABJKZLGZEALGEBLGUIUKUJULUNUIUKMEUMFGUNABCDNEUMOABCDPZQ
        RUJEUMHZUNUJEBJKUMEEABJSBBDTUAUBEUMOUOUCZRULUPUNULAEJKUMEEBAJUDAACTUEUB
        UQRUFEAOCUGEBODUGUH $.
        $( [30-Jul-1999] $)

      ${
        mulgt0i.3 $e |- 0 < A $.
        mulgt0i.4 $e |- 0 < B $.
        $( The product of two positive numbers is positive. $)
        mulgt0i $p |- 0 < ( A x. B ) $=
          ( cc0 clt wbr cmul co mulgt0 mp2an ) GAHIGBHIGABJKHIEFABCDLM $.
          $( [18-May-1999] $)
      $}
    $}

    $( 'Less than' is irreflexive. $)
    ltnr $p |- -. A < A $=
      ( cr wcel clt wbr wn ltnrt ax-mp ) ACDAAEFGBAHI $.
      $( [18-Aug-1999] $)

    $( 'Less than or equal to' is reflexive. $)
    leid $p |- A <_ A $=
      ( cr wcel cle wbr leidt ax-mp ) ACDAAEFBAGH $.
      $( [18-Aug-1999] $)

    $( Positive means non-zero (useful for ordering theorems involving
       division). $)
    gt0ne0 $p |- ( 0 < A -> A =/= 0 ) $=
      ( cc0 0re ltne ) CADBE $.
      $( [16-Sep-1999] $)

    ${
      lesub0.1 $e |- B e. RR $.
      $( Lemma to show a nonnegative number is zero. $)
      lesub0 $p |- ( ( 0 <_ A /\ B <_ ( B - A ) ) <-> A = 0 ) $=
        ( cmin co cle wbr cc0 wa wceq cneg caddc 0re renegcl leadd2 leneg neg0
        breq1i bitr2 recn addid1 negsub breq12i 3bitr3r anbi1i ancom letri3
        3bitr4 ) BBAEFZGHZIAGHZJAIGHZULJULUKJAIKUKUMULIALZGHZBIMFZBUNMFZGHUMUKI
        UNBNACODPUMILZUNGHUOAICNQURIUNGRSTUPBUQUJGBBDUAZUBBAUSACUAUCUDUEUFULUKU
        GAICNUHUI $.
        $( [8-Oct-1999] $)
    $}

    $( A non-zero square is positive.  Theorem I.20 of [Apostol] p. 20. $)
    msqgt0 $p |- ( A =/= 0 -> 0 < ( A x. A ) ) $=
      ( cc0 wne clt wbr wo cmul co 0re lttri2 cneg ltneg neg0 breq1i bitr
      renegcl mulgt0 anidms recn mul2neg syl6breq sylbi jaoi ) ACDACEFZCAEFZGCA
      AHIZEFZACBJKUEUHUFUECALZEFZUHUECLZUIEFUJACBJMUKCUIENOPUJCUIUIHIZUGEUJCULE
      FUIUIABQZUMRSAAABTZUNUAUBUCUFUHAABBRSUDUC $.
      $( [23-May-2007] $) $( [17-Jan-1997] $)

    $( A square is nonnegative. $)
    msqge0 $p |- 0 <_ ( A x. A ) $=
      ( cc0 cmul co cle wbr wceq clt wo opreq2 recn mul01 syl6req olc syl 0re
      remulcl leloe sylibr wne msqgt0 ltle pm2.61ine ) CAADEZFGZACACHZCUEIGZCUE
      HZJZUFUGUIUJUGUEACDECACADKAABLMNUIUHOPCUEQAABBRZSTACUAUHUFABUBCUEQUKUCPUD
      $.
      $( [14-May-1999] $)
  $}

  $( A non-zero square is positive.  Theorem I.20 of [Apostol] p. 20. $)
  msqgt0t $p |- ( ( A e. RR /\ A =/= 0 ) -> 0 < ( A x. A ) ) $=
    ( cr wcel cc0 wne cmul co clt wbr wi cif wceq neeq1 id opreq12d breq2d
    imbi12d 0re elimel msqgt0 dedth imp ) ABCZADEZDAAFGZHIZUCUDUFJUCADKZDEZDUGU
    GFGZHIZJADAUGLZUDUHUFUJAUGDMUKUEUIDHUKAUGAUGFUKNZULOPQUGADBRSTUAUB $.
    $( [24-May-2007] $) $( [6-May-1999] $)

  $( A square is nonnegative. $)
  msqge0t $p |- ( A e. RR -> 0 <_ ( A x. A ) ) $=
    ( cr wcel cc0 cmul co cle wbr cif wceq id opreq12d breq2d 0re elimel
    msqge0 dedth ) ABCZDAAEFZGHDRADIZTEFZGHADATJZSUADGUBATATEUBKZUCLMTADBNOPQ
    $.
    $( [23-May-2007] $) $( [23-May-2007] $)

  ${
    gt0ne0i.1 $e |- A e. RR $.
    gt0ne0i.2 $e |- 0 < A $.
    $( Positive implies nonzero. $)
    gt0ne0i $p |- A =/= 0 $=
      ( cc0 clt wbr wne gt0ne0 ax-mp ) DAEFADGCABHI $.
      $( [15-May-1999] $)
  $}

  $( Positive implies nonzero. $)
  gt0ne0t $p |- ( ( A e. RR /\ 0 < A ) -> A =/= 0 ) $=
    ( cr wcel cc0 clt wbr wne wi cif wceq breq2 neeq1 imbi12d 0re elimel
    gt0ne0 dedth imp ) ABCZDAEFZADGZSTUAHDSADIZEFZUBDGZHADAUBJTUCUAUDAUBDEKAUBD
    LMUBADBNOPQR $.
    $( [25-Feb-2005] $) $( [3-Oct-1999] $)

  $( A nonzero nonnegative number is positive. $)
  ne0gt0t $p |- ( ( A e. RR /\ 0 <_ A ) -> ( A =/= 0 <-> 0 < A ) ) $=
    ( cr wcel cc0 cle wbr wa wne clt wo wb 0re lttri2t mpan2 adantr wn lenltt
    mpan biimpa biorf syl bitr4d ) ABCZDAEFZGZADHZADIFZDAIFZJZUHUCUFUIKZUDUCDBC
    ZUJLADMNOUEUGPZUHUIKUCUDULUKUCUDULKLDAQRSUGUHTUAUB $.
    $( [9-Dec-2007] $) $( [20-Nov-2007] $)

  $( Trichotomy law. $)
  letrit $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B \/ B <_ A ) ) $=
    ( cr wcel wa cle wbr wn clt wb ltnlet ancoms wi ltlet sylbird orrd ) ACDZBC
    DZEZABFGZBAFGZSTHZBAIGZUARQUCUBJBAKLRQUCUAMBANLOP $.
    $( [18-Aug-1999] $)

  ${
    lecase.1 $e |- ( ph -> A e. RR ) $.
    lecase.2 $e |- ( ph -> B e. RR ) $.
    lecase.3 $e |- ( ( ph /\ A <_ B ) -> ps ) $.
    lecase.4 $e |- ( ( ph /\ B <_ A ) -> ps ) $.
    $( Ordering elimination by cases. $)
    lecase $p |- ( ph -> ps ) $=
      ( cle wbr wo cr wcel letrit sylanc jaodan mpdan ) ACDIJZDCIJZKZBCLMDLMTAC
      DNEFOARBSGHPQ $.
      $( [17-Jul-2007] $) $( [6-Jul-2007] $)
  $}

  $( Trichotomy law. $)
  lelttrit $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B \/ B < A ) ) $=
    ( cr wcel wa cle wbr clt wo wn pm2.1 lenltt orbi1d mpbiri ) ACDBCDEZABFGZBA
    HGZIQJZQIQKOPRQABLMN $.
    $( [26-Apr-2005] $) $( [4-Apr-2005] $)

  $( Addition to both sides of 'less than'. $)
  ltadd1t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( A + C ) < ( B + C ) ) ) $=
    ( cr wcel clt wbr caddc co wb cc0 cif wceq breq1 opreq1 breq1d bibi12d
    breq2 breq2d opreq2 breq12d bibi2d 0re elimel ltadd1 dedth3h ) ADEZBDEZCDEZ
    ABFGZACHIZBCHIZFGZJUGAKLZBFGZUNCHIZULFGZJABCKKKUNUHBKLZFGZUPURCHIZFGZJUSUNU
    ICKLZHIZURVBHIZFGZJAUNMZUJUOUMUQAUNBFNVFUKUPULFAUNCHOPQBURMZUOUSUQVABURUNFR
    VGULUTUPFBURCHOSQCVBMZVAVEUSVHUPVCUTVDFCVBUNHTCVBURHTUAUBUNURVBAKDUCUDBKDUC
    UDCKDUCUDUEUF $.
    $( [12-Nov-1999] $)

  $( Addition to both sides of 'less than'. $)
  ltadd2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( C + A ) < ( C + B ) ) ) $=
    ( cr wcel w3a clt wbr caddc co ltadd1t wceq cc axaddcom recnt syl2an
    3adant2 3adant1 breq12d bitrd ) ADEZBDEZCDEZFZABGHACIJZBCIJZGHCAIJZCBIJZGHA
    BCKUDUEUGUFUHGUAUCUEUGLZUBAMECMEZUIUAUCACNAOCOZPQUBUCUFUHLZUABMEUJULUBUCBCN
    BOUKPRST $.
    $( [12-Nov-1999] $)

  $( Addition to both sides of 'less than or equal to'. $)
  leadd1t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( A + C ) <_ ( B + C ) ) ) $=
    ( cr wcel cle wbr caddc co wb cc0 cif wceq breq1 opreq1 breq1d bibi12d
    breq2 breq2d opreq2 breq12d bibi2d 0re elimel leadd1 dedth3h ) ADEZBDEZCDEZ
    ABFGZACHIZBCHIZFGZJUGAKLZBFGZUNCHIZULFGZJABCKKKUNUHBKLZFGZUPURCHIZFGZJUSUNU
    ICKLZHIZURVBHIZFGZJAUNMZUJUOUMUQAUNBFNVFUKUPULFAUNCHOPQBURMZUOUSUQVABURUNFR
    VGULUTUPFBURCHOSQCVBMZVAVEUSVHUPVCUTVDFCVBUNHTCVBURHTUAUBUNURVBAKDUCUDBKDUC
    UDCKDUCUDUEUF $.
    $( [18-Oct-1999] $)

  $( Addition to both sides of 'less than or equal to'. $)
  leadd2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( C + A ) <_ ( C + B ) ) ) $=
    ( cr wcel w3a cle wbr caddc co leadd1t wceq cc axaddcom recnt syl2an
    3adant2 3adant1 breq12d bitrd ) ADEZBDEZCDEZFZABGHACIJZBCIJZGHCAIJZCBIJZGHA
    BCKUDUEUGUFUHGUAUCUEUGLZUBAMECMEZUIUAUCACNAOCOZPQUBUCUFUHLZUABMEUJULUBUCBCN
    BOUKPRST $.
    $( [26-Oct-1999] $)

  $( 'Less than' relationship between subtraction and addition. $)
  ltsubaddt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( ( A - B ) < C <-> A < ( C + B ) ) ) $=
    ( cr wcel cmin co clt wbr caddc wb cc0 cif wceq opreq1 breq1d breq1
    bibi12d opreq2 breq2d breq2 0re elimel ltsubadd dedth3h ) ADEZBDEZCDEZABFGZ
    CHIZACBJGZHIZKUFALMZBFGZCHIZUMUKHIZKABCLLLUMUGBLMZFGZCHIZUMCUQJGZHIZKURUHCL
    MZHIZUMVBUQJGZHIZKAUMNZUJUOULUPVFUIUNCHAUMBFOPAUMUKHQRBUQNZUOUSUPVAVGUNURCH
    BUQUMFSPVGUKUTUMHBUQCJSTRCVBNZUSVCVAVECVBURHUAVHUTVDUMHCVBUQJOTRUMUQVBALDUB
    UCBLDUBUCCLDUBUCUDUE $.
    $( [21-Jan-1997] $)

  $( 'Less than' relationship between subtraction and addition. $)
  ltsubadd2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) < C <-> A < ( B + C ) ) ) $=
    ( cr wcel w3a cmin co clt wbr caddc ltsubaddt wceq cc axaddcom recnt
    syl2an 3adant1 breq2d bitr4d ) ADEZBDEZCDEZFZABGHCIJACBKHZIJABCKHZIJABCLUDU
    FUEAIUBUCUFUEMZUABNECNEUGUBUCBCOBPCPQRST $.
    $( [21-Jan-1997] $)

  $( 'Less than or equal to' relationship between subtraction and addition. $)
  lesubaddt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) <_ C <-> A <_ ( C + B ) ) ) $=
    ( cr wcel cmin co cle wbr caddc wb cc0 cif wceq opreq1 breq1d breq1
    bibi12d opreq2 breq2d breq2 0re elimel lesubadd dedth3h ) ADEZBDEZCDEZABFGZ
    CHIZACBJGZHIZKUFALMZBFGZCHIZUMUKHIZKABCLLLUMUGBLMZFGZCHIZUMCUQJGZHIZKURUHCL
    MZHIZUMVBUQJGZHIZKAUMNZUJUOULUPVFUIUNCHAUMBFOPAUMUKHQRBUQNZUOUSUPVAVGUNURCH
    BUQUMFSPVGUKUTUMHBUQCJSTRCVBNZUSVCVAVECVBURHUAVHUTVDUMHCVBUQJOTRUMUQVBALDUB
    UCBLDUBUCCLDUBUCUDUE $.
    $( [19-Nov-2004] $) $( [17-Nov-2004] $)

  $( 'Less than or equal to' relationship between subtraction and addition. $)
  lesubadd2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) <_ C <-> A <_ ( B + C ) ) ) $=
    ( cr wcel w3a cmin co cle wbr caddc lesubaddt wceq cc axaddcom recnt
    syl2an 3adant1 breq2d bitr4d ) ADEZBDEZCDEZFZABGHCIJACBKHZIJABCKHZIJABCLUDU
    FUEAIUBUCUFUEMZUABNECNEUGUBUCBCOBPCPQRST $.
    $( [10-Aug-1999] $)

  $( 'Less than' relationship between addition and subtraction. $)
  ltaddsubt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A + B ) < C <-> A < ( C - B ) ) ) $=
    ( cr wcel w3a caddc co clt wbr cmin cle wn wb lesubaddt 3com13 wa lenltt
    resubclt sylan 3impa axaddrcl sylan2 3impb 3coml 3bitr3rd con4bid ) ADEZBDE
    ZCDEZFZABGHZCIJZACBKHZIJZUKUNALJZCULLJZUOMZUMMZUJUIUHUPUQNCBAOPUJUIUHUPURNZ
    UJUIUHUTUNDEUHUTUJUIQUNARCBSTUAPUJUHUIUQUSNZUJUHUIVAUJULDEVAUHUIQCULRABUBUC
    UDUEUFUG $.
    $( [22-Nov-2004] $) $( [17-Nov-2004] $)

  $( 'Less than' relationship between addition and subtraction. $)
  ltaddsub2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A + B ) < C <-> B < ( C - A ) ) ) $=
    ( cr wcel w3a caddc co clt wbr cmin wceq cc axaddcom recnt syl2an 3adant3
    breq1d wb ltaddsubt 3com12 bitrd ) ADEZBDEZCDEZFZABGHZCIJBAGHZCIJZBCAKHIJZU
    FUGUHCIUCUDUGUHLZUEAMEBMEUKUCUDABNAOBOPQRUDUCUEUIUJSBACTUAUB $.
    $( [28-Nov-2004] $) $( [17-Nov-2004] $)

  $( 'Less than or equal to' relationship between addition and subtraction. $)
  leaddsubt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A + B ) <_ C <-> A <_ ( C - B ) ) ) $=
    ( cr wcel w3a caddc co cle wbr cmin clt wn wb ltsubaddt 3com13 wa ltnlet
    resubclt sylan 3impa axaddrcl sylan2 3impb 3coml 3bitr3rd con4bid ) ADEZBDE
    ZCDEZFZABGHZCIJZACBKHZIJZUKUNALJZCULLJZUOMZUMMZUJUIUHUPUQNCBAOPUJUIUHUPURNZ
    UJUIUHUTUNDEUHUTUJUIQUNARCBSTUAPUJUHUIUQUSNZUJUHUIVAUJULDEVAUHUIQCULRABUBUC
    UDUEUFUG $.
    $( [23-Apr-2005] $) $( [6-Apr-2005] $)

  $( 'Less than or equal to' relationship between and addition and
     subtraction. $)
  leaddsub2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A + B ) <_ C <-> B <_ ( C - A ) ) ) $=
    ( cr wcel w3a caddc co cle wbr cmin wceq cc axaddcom recnt syl2an 3adant3
    breq1d wb leaddsubt 3com12 bitrd ) ADEZBDEZCDEZFZABGHZCIJBAGHZCIJZBCAKHIJZU
    FUGUHCIUCUDUGUHLZUEAMEBMEUKUCUDABNAOBOPQRUDUCUEUIUJSBACTUAUB $.
    $( [13-Jun-2005] $) $( [6-Apr-2005] $)

  $( Swap subtrahends in an inequality. $)
  sublet $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) <_ C <-> ( A - C ) <_ B ) ) $=
    ( cr wcel w3a cmin co cle wbr caddc lesubaddt wb lesubadd2t 3com23 bitr4d
    ) ADEZBDEZCDEZFABGHCIJACBKHIJZACGHBIJZABCLQSRUATMACBNOP $.
    $( [17-Oct-2005] $) $( [29-Sep-2005] $)

  $( Swap subtrahends in an inequality. $)
  lesubt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( A <_ ( B - C ) <-> C <_ ( B - A ) ) ) $=
    ( cr wcel w3a caddc co cle wbr cmin wb leaddsubt 3com23 leaddsub2t bitr3d
    ) ADEZBDEZCDEZFACGHBIJZABCKHIJZCBAKHIJZQSRTUALACBMNQSRTUBLACBONP $.
    $( [3-Nov-2005] $) $( [29-Sep-2005] $)

  ${
    ltsubadd.1 $e |- A e. RR $.
    ltsubadd.2 $e |- B e. RR $.
    ltsubadd.3 $e |- C e. RR $.
    $( 'Less than' relationship between subtraction and addition. $)
    ltsubadd2 $p |- ( ( A - B ) < C <-> A < ( B + C ) ) $=
      ( cr wcel cmin co clt wbr caddc wb ltsubadd2t mp3an ) AGHBGHCGHABIJCKLABC
      MJKLNDEFABCOP $.
      $( [21-Jan-1997] $)

    $( 'Less than or equal to' relationship between subtraction and
       addition. $)
    lesubadd2 $p |- ( ( A - B ) <_ C <-> A <_ ( B + C ) ) $=
      ( cr wcel cmin co cle wbr caddc wb lesubadd2t mp3an ) AGHBGHCGHABIJCKLABC
      MJKLNDEFABCOP $.
      $( [3-Aug-1999] $)

    $( 'Less than' relationship between subtraction and addition. $)
    ltaddsub $p |- ( ( A + B ) < C <-> A < ( C - B ) ) $=
      ( cr wcel caddc co clt wbr cmin wb ltaddsubt mp3an ) AGHBGHCGHABIJCKLACBM
      JKLNDEFABCOP $.
      $( [14-May-1999] $)

    $( Multiplication of both sides of 'less than' by a positive
       number.  Theorem I.19 of [Apostol] p. 20. $)
    ltmullem $p |- ( 0 < C -> ( A < B -> ( A x. C ) < ( B x. C ) ) ) $=
      ( cc0 clt wbr cmin co cmul resubcl mulgt0 expcom caddc recn addid2
      breq1i 0re ltaddsub bitr3 subdir breq2i remulcl 3bitr2r 3imtr4g ) GCHIZGB
      AJKZHIZGUICLKZHIZABHIZACLKZBCLKZHIZUJUHULUICBAEDMFNOUMGAPKZBHIUJUQABHAADQ
      ZRSGABTDEUAUBULGUOUNJKZHIGUNPKZUOHIUPUKUSGHBACBEQURCFQUCUDGUNUOTACDFUEZBC
      EFUEUAUTUNUOHUNUNVAQRSUFUG $.
      $( [15-May-1999] $)
  $}

  $( 'Less than' relationship between subtraction and addition. $)
  ltsub23t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) < C <-> ( A - C ) < B ) ) $=
    ( cr wcel w3a cmin co clt wbr caddc ltsubaddt wb ltsubadd2t 3com23 bitr4d
    ) ADEZBDEZCDEZFABGHCIJACBKHIJZACGHBIJZABCLQSRUATMACBNOP $.
    $( [4-Oct-1999] $)

  $( 'Less than' relationship between subtraction and addition. $)
  ltsub13t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( A < ( B - C ) <-> C < ( B - A ) ) ) $=
    ( cr wcel cmin co clt wbr wb w3a caddc ltaddsubt ltaddsub2t bitr3d 3com23
    ) ADEZCDEZBDEZABCFGHIZCBAFGHIZJQRSKACLGBHITUAACBMACBNOP $.
    $( [28-Nov-2004] $) $( [17-Nov-2004] $)

  $( Adding both sides of two 'less than' relations.  Theorem I.25 of [Apostol]
     p. 20. $)
  lt2addt $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A < C /\ B < D ) -> ( A + B ) < ( C + D ) ) ) $=
    ( cr wcel clt wbr wa caddc co wi cc0 cif wceq breq1 anbi1d opreq1 breq1d
    imbi12d anbi2d opreq2 breq2 breq2d 0re elimel lt2add dedth4h ) AEFZBEFZCEFZ
    DEFZACGHZBDGHZIZABJKZCDJKZGHZLABCDMMUIAMNZCGHZUNIZUSBJKZUQGHZLUTUJBMNZDGHZI
    ZUSVDJKZUQGHZLUSUKCMNZGHZVEIZVGVIDJKZGHZLVJVDULDMNZGHZIZVGVIVNJKZGHZLMMAUSO
    ZUOVAURVCVSUMUTUNAUSCGPQVSUPVBUQGAUSBJRSTBVDOZVAVFVCVHVTUNVEUTBVDDGPUAVTVBV
    GUQGBVDUSJUBSTCVIOZVFVKVHVMWAUTVJVECVIUSGUCQWAUQVLVGGCVIDJRUDTDVNOZVKVPVMVR
    WBVEVOVJDVNVDGUCUAWBVLVQVGGDVNVIJUBUDTUSVDVIVNAMEUEUFBMEUEUFCMEUEUFDMEUEUFU
    GUH $.
    $( [15-Aug-1999] $)

  $( Adding both sides of two 'less than or equal to' relations. $)
  le2addt $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A <_ C /\ B <_ D ) -> ( A + B ) <_ ( C + D ) ) ) $=
    ( cr wcel cle wbr wa caddc co wi cc0 cif wceq breq1 anbi1d opreq1 breq1d
    imbi12d anbi2d opreq2 breq2 breq2d 0re elimel le2add dedth4h ) AEFZBEFZCEFZ
    DEFZACGHZBDGHZIZABJKZCDJKZGHZLABCDMMUIAMNZCGHZUNIZUSBJKZUQGHZLUTUJBMNZDGHZI
    ZUSVDJKZUQGHZLUSUKCMNZGHZVEIZVGVIDJKZGHZLVJVDULDMNZGHZIZVGVIVNJKZGHZLMMAUSO
    ZUOVAURVCVSUMUTUNAUSCGPQVSUPVBUQGAUSBJRSTBVDOZVAVFVCVHVTUNVEUTBVDDGPUAVTVBV
    GUQGBVDUSJUBSTCVIOZVFVKVHVMWAUTVJVECVIUSGUCQWAUQVLVGGCVIDJRUDTDVNOZVKVPVMVR
    WBVEVOVJDVNVDGUCUAWBVLVQVGGDVNVIJUBUDTUSVDVIVNAMEUEUFBMEUEUFCMEUEUFDMEUEUFU
    GUH $.
    $( [16-Jun-2005] $) $( [17-Apr-2005] $)

  $( Adding both sides of two orderings. $)
  ltleaddt $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A < C /\ B <_ D ) -> ( A + B ) < ( C + D ) ) ) $=
    ( cr wcel wa clt wbr cle caddc co wb ltadd1t 3com23 3expa adantrr leadd2t
    3expb adantll anbi12d wi ltletrt axaddrcl adantr ancoms ad2ant2lr adantl
    syl3anc sylbid ) AEFZBEFZGZCEFZDEFZGZGZACHIZBDJIZGABKLZCBKLZHIZVACDKLZJIZGZ
    UTVCHIZUQURVBUSVDUMUNURVBMZUOUKULUNVGUKUNULVGACBNOPQULUPUSVDMZUKULUNUOVHULU
    OUNVHBDCROSTUAUTEFZVAEFZVCEFZVEVFUBUQUTVAVCUCUMVIUPABUDUEULUNVJUKUOUNULVJCB
    UDUFUGUPVKUMCDUDUHUIUJ $.
    $( [24-Dec-2007] $) $( [23-Dec-2007] $)

  $( The sum of 2 positive numbers is positive. $)
  addgt0t $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 0 < A /\ 0 < B ) ) ->
                0 < ( A + B ) ) $=
    ( cr wcel wa cc0 clt wbr caddc co wi cif wceq breq2 anbi1d opreq1 breq2d
    imbi12d anbi2d opreq2 0re elimel addgt0 dedth2h imp ) ACDZBCDZEFAGHZFBGHZEZ
    FABIJZGHZUFUGUJULKFUFAFLZGHZUIEZFUMBIJZGHZKUNFUGBFLZGHZEZFUMURIJZGHZKABFFAU
    MMZUJUOULUQVCUHUNUIAUMFGNOVCUKUPFGAUMBIPQRBURMZUOUTUQVBVDUIUSUNBURFGNSVDUPV
    AFGBURUMITQRUMURAFCUAUBBFCUAUBUCUDUE $.
    $( [7-Jun-2005] $) $( [1-Jun-2005] $)

  $( The sum of nonnegative and positive numbers is positive. $)
  addgegt0t $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 0 <_ A /\ 0 < B ) ) ->
                  0 < ( A + B ) ) $=
    ( cr wcel wa cc0 cle wbr clt caddc co wi cif wceq breq2 anbi1d opreq1
    breq2d imbi12d anbi2d opreq2 0re elimel addgegt0 dedth2h imp ) ACDZBCDZEFAG
    HZFBIHZEZFABJKZIHZUGUHUKUMLFUGAFMZGHZUJEZFUNBJKZIHZLUOFUHBFMZIHZEZFUNUSJKZI
    HZLABFFAUNNZUKUPUMURVDUIUOUJAUNFGOPVDULUQFIAUNBJQRSBUSNZUPVAURVCVEUJUTUOBUS
    FIOTVEUQVBFIBUSUNJUARSUNUSAFCUBUCBFCUBUCUDUEUF $.
    $( [25-Jan-2006] $) $( [28-Dec-2005] $)

  $( The sum of nonnegative and positive numbers is positive. $)
  addgtge0t $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 0 < A /\ 0 <_ B ) ) ->
                  0 < ( A + B ) ) $=
    ( cr wcel wa cc0 clt wbr cle caddc co addgegt0t ancom syl2anb wceq cc
    axaddcom recnt syl2an adantr breqtrrd ) ACDZBCDZEZFAGHZFBIHZEZEFBAJKZABJKZG
    UCUBEUFUEEFUHGHUDUGBALUBUCMUEUFMNUDUIUHOZUGAPDBPDUJUBUCABQARBRSTUA $.
    $( [4-Feb-2006] $) $( [28-Dec-2005] $)

  $( The sum of 2 nonnegative numbers is nonnegative. $)
  addge0t $p |- ( ( ( A e. RR /\ B e. RR ) /\
               ( 0 <_ A /\ 0 <_ B ) ) -> 0 <_ ( A + B ) ) $=
    ( cr wcel wa cc0 cle wbr caddc co wi cif wceq breq2 anbi1d opreq1 breq2d
    imbi12d anbi2d opreq2 0re elimel addge0 dedth2h imp ) ACDZBCDZEFAGHZFBGHZEZ
    FABIJZGHZUFUGUJULKFUFAFLZGHZUIEZFUMBIJZGHZKUNFUGBFLZGHZEZFUMURIJZGHZKABFFAU
    MMZUJUOULUQVCUHUNUIAUMFGNOVCUKUPFGAUMBIPQRBURMZUOUTUQVBVDUIUSUNBURFGNSVDUPV
    AFGBURUMITQRUMURAFCUAUBBFCUAUBUCUDUE $.
    $( [3-Apr-2005] $) $( [17-Mar-2005] $)

  $( Adding a positive number to another number increases it. $)
  ltaddpost $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 < A <-> B < ( B + A ) ) ) $=
    ( cr wcel wa cc0 clt wbr caddc co wb 0re ltadd2t mp3an1 wceq cc recnt
    ax0id syl adantl breq1d bitrd ) ACDZBCDZEZFAGHZBFIJZBAIJZGHZBUHGHFCDUCUDUFU
    IKLFABMNUEUGBUHGUDUGBOZUCUDBPDUJBQBRSTUAUB $.
    $( [29-Nov-2004] $) $( [17-Nov-2004] $)

  $( Adding a positive number to another number increases it. $)
  ltaddpos2t $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 < A <-> B < ( A + B ) ) ) $=
    ( cr wcel wa cc0 clt wbr caddc co ltaddpost cc wceq axaddcom recnt syl2an
    breq2d bitr4d ) ACDZBCDZEZFAGHBBAIJZGHBABIJZGHABKUAUCUBBGALDBLDUCUBMSTABNAO
    BOPQR $.
    $( [3-May-2005] $) $( [8-Apr-2005] $)

  $( Subtracting a positive number from another number decreases it. $)
  ltsubpost $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 < A <-> ( B - A ) < B ) ) $=
    ( cr wcel wa cc0 clt wbr caddc co cmin ltaddpost wb ltsubaddt pm3.27
    pm3.26 syl3anc bitr4d ) ACDZBCDZEZFAGHBBAIJGHZBAKJBGHZABLTSTUCUBMUABABNSTOZ
    STPUDQR $.
    $( [29-Nov-2004] $) $( [17-Nov-2004] $)

  $( Comparison of two numbers whose difference is positive. $)
  posdift $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> 0 < ( B - A ) ) ) $=
    ( cr wcel wa cc0 cmin co clt wbr caddc wb ltaddpost resubclt ancoms pm3.26
    sylanc cc wceq pncan3t recnt syl2an breq2d bitr2d ) ACDZBCDZEZFBAGHZIJZAAUH
    KHZIJZABIJUHCDZUEUIUKLUGUHAMUFUEULBANOUEUFPQUGUJBAIARDBRDUJBSUEUFABTAUABUAU
    BUCUD $.
    $( [18-Dec-2004] $) $( [17-Nov-2004] $)

  $( Negative of both sides of 'less than'.  Theorem I.23 of [Apostol]
     p. 20. $)
  ltnegt $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> -u B < -u A ) ) $=
    ( cr wcel clt wbr cneg wb cc0 cif wceq breq1 negeq breq2d bibi12d breq2
    breq1d 0re elimel ltneg dedth2h ) ACDZBCDZABEFZBGZAGZEFZHUBAIJZBEFZUEUHGZEF
    ZHUHUCBIJZEFZULGZUJEFZHABIIAUHKZUDUIUGUKAUHBELUPUFUJUEEAUHMNOBULKZUIUMUKUOB
    ULUHEPUQUEUNUJEBULMQOUHULAICRSBICRSTUA $.
    $( [27-Aug-1999] $)

  $( Contraposition of negative in 'less than'. $)
  ltnegcon1t $p |- ( ( A e. RR /\ B e. RR ) -> ( -u A < B <-> -u B < A ) ) $=
    ( cr wcel wa cneg clt wbr wb ltnegt renegclt sylan cc wceq recnt negnegt
    syl breq2d adantr bitrd ) ACDZBCDZEAFZBGHZBFZUCFZGHZUEAGHZUCCDUBUDUGIUAUCBJ
    AKLUAUGUHIUBUAUFAUEGUAAMDUFANAOAPQRST $.
    $( [12-Nov-2004] $) $( [8-Nov-2004] $)

  $( Negative of both sides of 'less than or equal to'. $)
  lenegt $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B <-> -u B <_ -u A ) ) $=
    ( cr wcel cle wbr cneg wb cc0 cif wceq breq1 negeq breq2d bibi12d breq2
    breq1d 0re elimel leneg dedth2h ) ACDZBCDZABEFZBGZAGZEFZHUBAIJZBEFZUEUHGZEF
    ZHUHUCBIJZEFZULGZUJEFZHABIIAUHKZUDUIUGUKAUHBELUPUFUJUEEAUHMNOBULKZUIUMUKUOB
    ULUHEPUQUEUNUJEBULMQOUHULAICRSBICRSTUA $.
    $( [12-Sep-1999] $)

  $( Contraposition of negative in 'less than or equal to'. $)
  lenegcon1t $p |- ( ( A e. RR /\ B e. RR ) -> ( -u A <_ B <-> -u B <_ A ) ) $=
    ( cr wcel wa cneg cle wbr wb lenegt renegclt sylan cc wceq recnt negnegt
    syl breq2d adantr bitrd ) ACDZBCDZEAFZBGHZBFZUCFZGHZUEAGHZUCCDUBUDUGIUAUCBJ
    AKLUAUGUHIUBUAUFAUEGUAAMDUFANAOAPQRST $.
    $( [14-Jun-2004] $) $( [10-May-2004] $)

  $( Contraposition of negative in 'less than or equal to'. $)
  lenegcon2t $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ -u B <-> B <_ -u A ) ) $=
    ( cr wcel wa cneg cle wbr wb lenegt renegclt sylan2 wceq cc recnt negnegt
    syl adantl breq1d bitrd ) ACDZBCDZEZABFZGHZUDFZAFZGHZBUGGHUAUDCDUEUHIUBAUDJ
    BKLUCUFBUGGUBUFBMZUAUBBNDUIBOBPQRST $.
    $( [9-Oct-2005] $) $( [8-Oct-2005] $)

  $( Subtraction from both sides of 'less than or equal to'. $)
  lesub1t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( A - C ) <_ ( B - C ) ) ) $=
    ( cr wcel w3a cle wbr cneg caddc co cmin wb leadd1t renegclt syl3an3 cc
    wceq negsubt 3adant2 3adant1 breq12d recnt syl3an bitrd ) ADEZBDEZCDEZFABGH
    ZACIZJKZBUJJKZGHZACLKZBCLKZGHZUFUGUJDEUIUMMUHABUJNCOPAQEZBQEZCQEZUMUPMUFUGU
    HUQURUSFUKUNULUOGUQUSUKUNRURACSTURUSULUORUQBCSUAUBAUCBUCCUCUDUE $.
    $( [1-Aug-2006] $) $( [13-May-2004] $)

  $( Subtraction of both sides of 'less than or equal to'. $)
  lesub2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( C - B ) <_ ( C - A ) ) ) $=
    ( cr wcel w3a cle wbr cneg caddc co cmin wb lenegt 3adant3 leadd2t
    renegclt syl3an2 syl3an1 3com12 cc wceq negsubt 3adant2 breq12d 3coml
    recnt syl3an 3bitrd ) ADEZBDEZCDEZFABGHZBIZAIZGHZCUNJKZCUOJKZGHZCBLKZCALKZG
    HZUJUKUMUPMULABNOUKUJULUPUSMZUNDEZUJULVCUKVDUODEULVCUJUNUOCPAQRBQSTAUAEZBUA
    EZCUAEZUSVBMZUJUKULVGVEVFVHVGVEVFFUQUTURVAGVGVFUQUTUBVECBUCUDVGVEURVAUBVFCA
    UCOUEUFAUGBUGCUGUHUI $.
    $( [1-Aug-2006] $) $( [29-Sep-2005] $)

  $( Subtraction from both sides of 'less than'.  (Contributed by FL,
     3-Jan-2008.) $)
  ltsub1t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( A - C ) < ( B - C ) ) ) $=
    ( cr wcel w3a clt wbr cneg caddc co cmin wb ltadd1t renegclt syl3an3 cc
    wceq negsubt 3adant2 3adant1 breq12d recnt syl3an bitrd ) ADEZBDEZCDEZFABGH
    ZACIZJKZBUJJKZGHZACLKZBCLKZGHZUFUGUJDEUIUMMUHABUJNCOPAQEZBQEZCQEZUMUPMUFUGU
    HUQURUSFUKUNULUOGUQUSUKUNRURACSTURUSULUORUQBCSUAUBAUCBUCCUCUDUE $.
    $( [3-Jan-2008] $) $( [3-Jan-2008] $)

  $( Subtraction of both sides of 'less than'. $)
  ltsub2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( C - B ) < ( C - A ) ) ) $=
    ( cr wcel w3a clt wbr cneg caddc co cmin wb ltnegt 3adant3 ltadd2t
    renegclt syl3an2 syl3an1 3com12 cc wceq negsubt 3adant2 breq12d 3coml
    recnt syl3an 3bitrd ) ADEZBDEZCDEZFABGHZBIZAIZGHZCUNJKZCUOJKZGHZCBLKZCALKZG
    HZUJUKUMUPMULABNOUKUJULUPUSMZUNDEZUJULVCUKVDUODEULVCUJUNUOCPAQRBQSTAUAEZBUA
    EZCUAEZUSVBMZUJUKULVGVEVFVHVGVEVFFUQUTURVAGVGVFUQUTUBVECBUCUDVGVEURVAUBVFCA
    UCOUEUFAUGBUGCUGUHUI $.
    $( [1-Aug-2006] $) $( [29-Sep-2005] $)

  ${
    ltaddpos.1 $e |- A e. RR $.
    ltaddpos.2 $e |- B e. RR $.
    $( Adding a positive number to another number increases it. $)
    ltaddpos $p |- ( 0 < A <-> B < ( B + A ) ) $=
      ( cr wcel cc0 clt wbr caddc co wb ltaddpost mp2an ) AEFBEFGAHIBBAJKHILCDA
      BMN $.
      $( [25-Aug-1999] $)

    $( Comparison of two numbers whose difference is positive. $)
    posdif $p |- ( A < B <-> 0 < ( B - A ) ) $=
      ( cr wcel clt wbr cc0 cmin co wb posdift mp2an ) AEFBEFABGHIBAJKGHLCDABMN
      $.
      $( [17-Nov-2004] $) $( [19-Aug-2001] $)

    $( Contraposition of negative in 'less than'. $)
    ltnegcon1 $p |- ( -u A < B <-> -u B < A ) $=
      ( cr wcel cneg clt wbr wb ltnegcon1t mp2an ) AEFBEFAGBHIBGAHIJCDABKL $.
      $( [14-May-1999] $)

    $( Contraposition of negative in 'less than or equal to'. $)
    lenegcon1 $p |- ( -u A <_ B <-> -u B <_ A ) $=
      ( cr wcel cneg cle wbr wb lenegcon1t mp2an ) AEFBEFAGBHIBGAHIJCDABKL $.
      $( [3-May-2005] $) $( [6-Apr-2005] $)
  $}

  $( Comparison of a number and its negative to zero.  Theorem I.23 of
     [Apostol] p. 20. $)
  lt0neg1t $p |- ( A e. RR -> ( A < 0 <-> 0 < -u A ) ) $=
    ( cr wcel cc0 clt wbr cneg wb 0re ltnegt mpan2 neg0 breq1i syl6bb ) ABCZADE
    FZDGZAGZEFZDREFODBCPSHIADJKQDRELMN $.
    $( [14-May-1999] $)

  $( Comparison of a number and its negative to zero. $)
  lt0neg2t $p |- ( A e. RR -> ( 0 < A <-> -u A < 0 ) ) $=
    ( cr wcel cc0 clt wbr cneg wb 0re ltnegt mpan neg0 breq2i syl6bb ) ABCZDAEF
    ZAGZDGZEFZQDEFDBCOPSHIDAJKRDQELMN $.
    $( [10-May-2004] $) $( [10-May-2004] $)

  $( Comparison of a number and its negative to zero. $)
  le0neg1t $p |- ( A e. RR -> ( A <_ 0 <-> 0 <_ -u A ) ) $=
    ( cr wcel cc0 cle wbr cneg wb 0re lenegt mpan2 neg0 breq1i syl6bb ) ABCZADE
    FZDGZAGZEFZDREFODBCPSHIADJKQDRELMN $.
    $( [15-Jun-2004] $) $( [10-May-2004] $)

  $( Comparison of a number and its negative to zero. $)
  le0neg2t $p |- ( A e. RR -> ( 0 <_ A <-> -u A <_ 0 ) ) $=
    ( cr wcel cc0 cle wbr cneg wb 0re lenegt mpan neg0 breq2i syl6bb ) ABCZDAEF
    ZAGZDGZEFZQDEFDBCOPSHIDAJKRDQELMN $.
    $( [24-Aug-1999] $)

  $( A number is less than or equal to itself plus a nonnegative number. $)
  addge01t $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 <_ B <-> A <_ ( A + B ) ) ) $=
    ( cr wcel wa cc0 cle wbr caddc co wb 0re leadd2t mp3an1 ancoms wceq cc
    recnt ax0id syl adantr breq1d bitrd ) ACDZBCDZEZFBGHZAFIJZABIJZGHZAUIGHUEUD
    UGUJKZFCDUEUDUKLFBAMNOUFUHAUIGUDUHAPZUEUDAQDULARASTUAUBUC $.
    $( [28-Jul-2005] $) $( [21-Feb-2005] $)

  $( A number is less than or equal to itself plus a nonnegative number. $)
  addge02t $p |- ( ( A e. RR /\ B e. RR ) -> ( 0 <_ B <-> A <_ ( B + A ) ) ) $=
    ( cr wcel wa cc0 cle wbr caddc co addge01t cc wceq axaddcom recnt syl2an
    breq2d bitrd ) ACDZBCDZEZFBGHAABIJZGHABAIJZGHABKUAUBUCAGALDBLDUBUCMSTABNAOB
    OPQR $.
    $( [30-Jul-2005] $) $( [27-Jul-2005] $)

  $( Nonnegative subtraction. $)
  subge0t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( 0 <_ ( A - B ) <-> B <_ A ) ) $=
    ( cr wcel wa cle wbr cneg caddc co cc0 cmin wb leadd1t pm3.27 pm3.26
    renegclt adantl syl3anc cc wceq negidt negsubt breq12d recnt syl2an bitr2d
    ) ACDZBCDZEZBAFGZBBHZIJZAULIJZFGZKABLJZFGZUIUHULCDZUKUOMUJBAULNUHUIOUHUIPUI
    URUHBQRSATDZBTDZUOUQMUHUIUSUTEUMKUNUPFUTUMKUAUSBUBRABUCUDAUEBUEUFUG $.
    $( [15-Mar-2005] $) $( [14-Mar-2005] $)

  $( Nonpositive subtraction. $)
  suble0t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( ( A - B ) <_ 0 <-> A <_ B ) ) $=
    ( cr wcel wa cmin co cc0 cle wbr cneg wb resubclt le0neg1t syl cc wceq
    negsubdi2t recnt syl2an breq2d subge0t ancoms 3bitrd ) ACDZBCDZEZABFGZHIJZH
    UHKZIJZHBAFGZIJZABIJZUGUHCDUIUKLABMUHNOUGUJULHIAPDBPDUJULQUEUFABRASBSTUAUFU
    EUMUNLBAUBUCUD $.
    $( [24-Mar-2008] $) $( [20-Mar-2008] $)

  ${
    subge0.1 $e |- A e. RR $.
    subge0.2 $e |- B e. RR $.
    $( Nonnegative subtraction. $)
    subge0 $p |- ( 0 <_ ( A - B ) <-> B <_ A ) $=
      ( cr wcel cc0 cmin co cle wbr wb subge0t mp2an ) AEFBEFGABHIJKBAJKLCDABMN
      $.
      $( [13-Aug-2000] $)
  $}

  $( Nonnegative subtraction. $)
  subge02t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( 0 <_ B <-> ( A - B ) <_ A ) ) $=
    ( cr wcel wa cc0 cle wbr caddc co cmin addge01t wb lesubaddt 3anidm13
    bitr4d ) ACDZBCDZEFBGHAABIJGHZABKJAGHZABLQRTSMABANOP $.
    $( [31-Jul-2005] $) $( [27-Jul-2005] $)

  $( Lemma to show a nonnegative number is zero. $)
  lesub0t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( ( 0 <_ A /\ B <_ ( B - A ) ) <-> A = 0 ) ) $=
    ( cr wcel cc0 cle wbr cmin co wa wceq wb cif breq2 opreq2 breq2d anbi12d
    eqeq1 bibi12d id opreq1 breq12d anbi2d bibi1d 0re elimel lesub0 dedth2h )
    ACDZBCDZEAFGZBBAHIZFGZJZAEKZLEUIAEMZFGZBBUPHIZFGZJZUPEKZLUQUJBEMZVBUPHIZFGZ
    JZVALABEEAUPKZUNUTUOVAVFUKUQUMUSAUPEFNVFULURBFAUPBHOPQAUPERSBVBKZUTVEVAVGUS
    VDUQVGBVBURVCFVGTBVBUPHUAUBUCUDUPVBAECUEUFBECUEUFUGUH $.
    $( [8-Oct-1999] $)

  $( The product of two nonnegative numbers is nonnegative. $)
  mulge0t $p |- ( ( ( A e. RR /\ B e. RR ) /\
                ( 0 <_ A /\ 0 <_ B ) ) -> 0 <_ ( A x. B ) ) $=
    ( cr wcel wa cc0 cle wbr cmul co wi cif wceq breq2 anbi1d opreq1 breq2d
    imbi12d anbi2d opreq2 0re elimel mulge0 dedth2h imp ) ACDZBCDZEFAGHZFBGHZEZ
    FABIJZGHZUFUGUJULKFUFAFLZGHZUIEZFUMBIJZGHZKUNFUGBFLZGHZEZFUMURIJZGHZKABFFAU
    MMZUJUOULUQVCUHUNUIAUMFGNOVCUKUPFGAUMBIPQRBURMZUOUTUQVBVDUIUSUNBURFGNSVDUPV
    AFGBURUMITQRUMURAFCUAUBBFCUAUBUCUDUE $.
    $( [17-Jul-2005] $) $( [8-Oct-1999] $)

  $( 0 is less than 1.  Theorem I.21 of [Apostol] p. 20. $)
  lt01 $p |- 0 < 1 $=
    ( cc0 c1 cmul co clt wne wbr ax1ne0 1re msqgt0 ax-mp ax1cn mulid1 breqtr )
    ABBCDZBEBAFAOEGHBIJKBLMN $.
    $( [17-Jan-1997] $)

