$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Geometric series
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x ph $.  $d x y A $.
    expcnvlem1.1 $e |- A e. RR $.
    expcnvlem1.2 $e |- ( y e. NN -> ( A < y -> ph ) ) $.
    $( Lemma for ~ expcnv .  Convert an antecedent from a comparison with a
       real into comparison with a natural number. $)
    expcnvlem1 $p |- E. x e. NN A. y e. NN ( x <_ y -> ph ) $=
      ( cfl cfv cabs c1 caddc co cn wcel cv cle wbr wi wral wrex cn0 cz cr
      flcl ax-mp nn0abscl nn0p1nn clt nnre flltp1 reflcl leabsi recni abscli
      1re leadd1i mpbi readdcli ltletri mp2an wa ltletr mp3an12 mpani syl
      imim1d ralimia mprg wceq breq1 imbi1d ralbidv rcla4ev ) DGHZIHZJKLZMNZVPC
      OZPQZARZCMSZBOZVRPQZARZCMSZBMTVOUANZVQVNUBNZWFDUCNZWGEDUDUEVNUFUEVOUGUEDV
      RUHQZARZWACMWJVTCMVRMNZVSWIAWKVRUCNZVSWIRVRUIWLDVPUHQZVSWIDVNJKLZUHQZWNVP
      PQZWMWHWOEDUJUEVNVOPQWPVNWHVNUCNEDUKUEZULVNVOJWQVNVNWQUMUNZUOUPUQDWNVPEVN
      JWQUOURVOJWRUOURZUSUTWHVPUCNWLWMVSVAWIREWSDVPVRVBVCVDVEVFVGFVHWEWABVPMWBV
      PVIZWDVTCMWTWCVSAWBVPVRPVJVKVLVMUT $.
      $( [27-Feb-2005] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    expcnvlem.1 $e |- ( A e. RR /\ 0 < A /\ A < 1 ) $.
    expcnvlem.2 $e |- ( B e. RR /\ 0 < B ) $.
    ${
      expcnvlem.3 $e |- C e. NN $.
      $( Lemma for ~ expcnv .  Compute an upper bound for exponentiation using
         Bernoulli's inequality ~ bernneq . $)
      expcnvlem2 $p |- ( ( ( ( 1 / B ) - 1 ) / ( ( 1 / A ) - 1 ) ) < C
                      -> ( A ^ C ) < B ) $=
        ( c1 cdiv co cmin clt wbr cmul caddc cexp cr wcel wb cc0 simpli simpri
        gt0ne0ii rereccli 1re resubcli simp1i simp2i nnrei w3a simp3i reclt1
        mp2an mpbi posdifi ltdivmulOLD mpan2 mp3an remulcli ltsubaddi readdcli
        nngt0i mulgt0ii lt01 addgt0ii ltdiv23ii 3bitri cle cn0 nnnn0i 0re
        recgt0ii ltleii bernneq2 cc cn wne wceq recni exprecOLD nnnn0 syl3an2
        breqtri reexpcl expgt0 lerec2 mp4an lelttri mpan sylbi ) GBHIZGJIZGAHIZ
        GJIZHICKLZGWMCMIZGNIZHIZBKLZACOIZBKLZWNWKWOKLZWJWPKLWRWKPQZWMPQZCPQZWNX
        ARZWJGBBPQZSBKLZETZBXHXFXGEUAZUBUCZUDUEWLGAAPQZSAKLZAGKLZDUFZAXNXKXLXMD
        UGZUBZUCZUDUEZCFUHZXBXCXDUISWMKLZXEGWLKLZXTXMYAXKXLXMDUJXKXLXMYARXNXOAU
        KULUMGWLUDXQUNUMZWKWMCUOUPUQWJGWOXJUDWMCXRXSURZUSGBWPUDXHWOGYCUDUTZXIWO
        GYCUDWMCXRXSYBCFVAVBVCVDZVEVFWSWQVGLZWRWTWPGWSHIZVGLZYFWPWLCOIZYGVGWLPQ
        CVHQZSWLVGLWPYIVGLXQCFVIZSWLVJXQAXNXOVKVLWLCVMUQAVNQZCVOQZASVPZYIYGVQZA
        XNVRFXPYLYJYNYOYMACVSCVTWAUQWBWPPQSWPKLWSPQZSWSKLZYHYFRYDYEXKYJYPXNYKAC
        WCULZXKYJXLYQXNYKXOACWDUQWPWSWEWFUMWSWQBYRWPYDWPYDYEUBUCXHWGWHWI $.
        $( [28-Jul-2006] $) $( [24-Feb-2005] $)
    $}

    $( Lemma for ~ expcnv .  Apply weak deduction theorem. $)
    expcnvlem3 $p |- ( C e. NN -> ( ( ( ( 1 / B ) - 1 ) /
                       ( ( 1 / A ) - 1 ) ) < C -> ( A ^ C ) < B ) ) $=
      ( cn wcel c1 cdiv co cmin clt wbr cexp wi cif wceq breq2 opreq2 breq1d
      imbi12d 1nn elimel expcnvlem2 dedth ) CFGZHBIJHKJHAIJHKJIJZCLMZACNJZBLMZO
      UGUFCHPZLMZAUKNJZBLMZOCHCUKQZUHULUJUNCUKUGLRUOUIUMBLCUKANSTUAABUKDECHFUBU
      CUDUE $.
      $( [27-Feb-2005] $)

    $( Lemma for ~ expcnv .  Combine ~ expcnvlem1 and ~ expcnvlem3 . $)
    expcnvlem4 $p |- E. x e. NN A. y e. NN ( x <_ y -> ( A ^ y ) < B ) $=
      ( cv cexp co clt wbr c1 cdiv cmin cr wcel cc0 simpli simpri gt0ne0ii
      rereccli 1re resubcli simp1i simp2i simp3i wb reclt1 mp2an mpbi posdifi
      redivcli expcnvlem3 expcnvlem1 ) CBGZHIDJKABLDMIZLNIZLCMIZLNIZMIUQUSUPLDD
      OPZQDJKZFRZDVBUTVAFSTUAUBUCURLCCOPZQCJKZCLJKZEUDZCVFVCVDVEEUEZTUAZUBUCZUS
      VILURJKZQUSJKVEVJVCVDVEEUFVCVDVEVJUGVFVGCUHUIUJLURUBVHUKUJTULCDUOEFUMUN
      $.
      $( [25-Feb-2005] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Lemma for ~ expcnv .  Apply weak deduction theorem. $)
    expcnvlem5 $p |- ( ( ( A e. RR /\ 0 < A /\ A < 1 ) /\ ( B e. RR
        /\ 0 < B ) ) -> E. x e. NN A. y e. NN ( x <_ y -> ( A ^ y ) < B ) ) $=
      ( cr wcel cc0 clt wbr c1 w3a wa cv cle cexp co wi cn wral wrex c2 cdiv
      cif wceq opreq1 breq1d imbi2d rexralbidv breq2 eleq1 breq1 3anbi123d 2re
      2ne0 rereccli halfgt0 halflt1 3pm3.2i elimhyp anbi12d 1re lt01 pm3.2i
      expcnvlem4 dedth2h ) CEFZGCHIZCJHIZKZDEFZGDHIZLZAMBMZNIZCVMOPZDHIZQZBRSAR
      TVNVICJUAUBPZUCZVMOPZDHIZQZBRSARTVNVTVLDJUCZHIZQZBRSARTCDVRJCVSUDZVQWBABR
      RWFVPWAVNWFVOVTDHCVSVMOUEUFUGUHDWCUDZWBWEABRRWGWAWDVNDWCVTHUIUGUHABVSWCVI
      VSEFZGVSHIZVSJHIZKVREFZGVRHIZVRJHIZKCVRWFVFWHVGWIVHWJCVSEUJCVSGHUICVSJHUK
      ULVRVSUDWKWHWLWIWMWJVRVSEUJVRVSGHUIVRVSJHUKULWKWLWMUAUMUNUOUPUQURUSVLWCEF
      ZGWCHIZLJEFZGJHIZLDJWGVJWNVKWODWCEUJDWCGHUIUTJWCUDWPWNWQWOJWCEUJJWCGHUIUT
      WPWQVAVBVCUSVDVE $.
      $( [27-Feb-2005] $)

    $( Lemma for ~ expcnv .  Add in the case of ` A = 0 ` . $)
    expcnvlem6 $p |- ( ( ( A e. RR /\ 0 <_ A /\ A < 1 ) /\ ( B e. RR
        /\ 0 < B ) ) -> E. x e. NN A. y e. NN ( x <_ y -> ( A ^ y ) < B ) ) $=
      ( cr wcel cc0 cle wbr c1 clt wa cv cexp co wi cn wral wrex wceq wo wb
      0re leloe mpan expcnvlem5 3exp1 1nn a1i12 0exp breq1d adantl opreq1
      adantr bitr3d biimpd ex a1d com24 r19.21adv jcad breq1 imbi1d ralbidv
      rcla4ev syl6 adantld a1i jaod sylbid 3imp1 ) CEFZGCHIZCJKIZDEFZGDKIZLZAMZ
      BMZHIZCVSNOZDKIZPZBQRZAQSZVLVMGCKIZGCTZUAZVNVQWEPZPZGEFVLVMWHUBUCGCUDUEVL
      WFWJWGVLWFVNVQWEABCDUFUGWGWJPVLWGWIVNWGVPWEVOWGVPJQFZJVSHIZWBPZBQRZLWEWGV
      PWKWNWGVPWKUHUIWGVPWMBQWGWLVSQFZVPWBWGWOVPWBPZPWLWGWOWPWGWOLZVPWBWQGVSNOZ
      DKIZVPWBWOWSVPUBWGWOWRGDKVSUJUKULWGWSWBUBWOWGWRWADKGCVSNUMUKUNUOUPUQURUSU
      TVAWDWNAJQVRJTZWCWMBQWTVTWLWBVRJVSHVBVCVDVEVFVGURVHVIVJVK $.
      $( [27-Feb-2005] $)
  $}

  ${
    $d j k x A $.  $d j x k F $.
    expcnv.1 $e |- F e. _V $.
    $( A sequence of powers of a complex number ` A ` with absolute value
       smaller than 1 converges to zero. $)
    expcnv $p |- ( ( A e. CC /\ A. k e. NN ( F ` k ) = ( A ^ k ) /\
                 ( abs ` A ) < 1 ) -> F ~~> 0 ) $=
      ( vx vj cc wcel cv cfv cexp co wceq cn wral cabs c1 clt wbr w3a cc0 cli
      cle wi wrex cr wa abscl adantr absge0 simpr 3jca expcnvlem6 sylan
      adantllr wb fveq2 cn0 nnnn0 absexp sylan2 sylan9eqr breq1d imbi2d ex
      ralimdva imp ralbi syl rexbidv ad2antrr mpbird exp32 3impa r19.21aiv
      eleq1 expcl syl5cbir 1z cuz nnuz eqimss2i nnssz clm0i 3adant3 ) AGHZBIZCJ
      ZAWGKLZMZBNOZAPJZQRSZTZCUAUBSZUAEIZRSZFIWGUCSZWHPJZWPRSZUDZBNOZFNUEZUDZEU
      FOZWNXDEUFWFWKWMWPUFHZXDUDWFWKUGZWMUGZXFWQXCXHXFWQUGZUGXCWRWLWGKLZWPRSZUD
      ZBNOZFNUEZWFWMXIXNWKWFWMUGZWLUFHZUAWLUCSZWMTXIXNXOXPXQWMWFXPWMAUHUIWFXQWM
      AUJUIWFWMUKULFBWLWPUMUNUOXGXCXNUPWMXIXGXBXMFNXGXAXLUPZBNOZXBXMUPWFWKXSWFW
      JXRBNWFWGNHZUGZWJXRYAWJUGZWTXKWRYBWSXJWPRWJYAWSWIPJZXJWHWIPUQXTWFWGURHZYC
      XJMWGUSZAWGUTVAVBVCVDVEVFVGXAXLBNVHVIVJVKVLVMVNVOWFWKWOXEUPZWMXGWHGHZBNOZ
      YFWFWKYHWFWJYGBNWJYGWIGHZYAWHWIGVPXTWFYDYIYEAWGVQVAVRVFVGEFBCQQNNVSNQVTJW
      AWBZWCVSYJWCDWDVIWEVL $.
      $( [8-May-2006] $)

    $( A sequence of terms converges to zero when it is less than powers of a
       number ` A ` whose absolute value is smaller than 1. $)
    explecnv $p |- ( ( A e. RR /\ ( abs ` A ) < 1 /\ A. k e. NN
      ( ( F ` k ) e. CC /\ ( abs ` ( F ` k ) ) <_ ( A ^ k ) ) ) -> F ~~> 0 ) $=
      ( vj vx cr wcel cabs cfv c1 clt wbr cv cc cexp co cle wa cn wral w3a cc0
      cli wceq copab recn opreq2 eqid oprex fvopab4 rgen nnex opabex2 expcnv
      mp3an2 sylan 3adant3 wi abscl adantr a1i cn0 nnnn0 reexpcl sylan2
      adantlr jctild absge0 simpr jcad wb eleq1d fveq2 fveq2d fvex anbi12d
      breq2d breq12d adantl sylibrd ralimdva 3impia cuz nnuz raleqi cz 1z 0cn
      elisseti climsqueeze2 sylan2b syl11anc simpl ralimi climabs0i syl
      3ad2ant3 mpbird ) AGHZAIJKLMZBNZCJZOHZXCIJZAXBPQZRMZSZBTUAZUBZCUCUDMZENZT
      HZFNZXLCJZIJZUESEFUFZUCUDMZXJXMXNAXLPQZUESEFUFZUCUDMZXBXTJZGHZXBXQJZGHZSZ
      UCYDRMZYDYBRMZSZSZBTUAZXRWTXAYAXIWTAOHZXAYAAUGYLYBXFUEZBTUAXAYAYMBTEFXBXS
      XFTXTXLXBAPUHXTUIAXBPUJUKZULABXTEFTXSUMUNZUOUPUQURWTXAXIYKWTXASZXHYJBTYPX
      BTHZSZXHXFGHZXEGHZSZUCXERMZXGSZSZYJYRXHUUAUUCYRXHYTYSXHYTUSYRXDYTXGXCUTVA
      VBWTYQYSXAYQWTXBVCHYSXBVDAXBVEVFVGVHYRXHUUBXGXHUUBUSYRXDUUBXGXCVIVAVBXHXG
      USYRXDXGVJVBVKVKYQYJUUDVLYPYQYFUUAYIUUCYQYCYSYEYTYQYBXFGYNVMYQYDXEGEFXBXP
      XETXQXLXBUEXOXCIXLXBCVNVOXQUIXCIVPUKZVMVQYQYGUUBYHXGYQYDXEUCRUUEVRYQYDXEY
      BXFRUUEYNVSVQVQVTWAWBWCYKYAYJBKWDJZUAZXRYJBTUUFWEWFYAKWGHUUGXRWHUCBXTXQKY
      OEFTXPUMUNZUCOWIWJWKUPWLWMXIWTXKXRVLZXAXIXDBTUAUUIXHXDBTXDXGWNWOBCXQDUUHU
      UEWPWQWRWS $.
      $( [18-Mar-2009] $) $( [19-Jul-2008] $)
  $}

  ${
    $d j k m n y z A $.  $d j m n z F $.  $d j N $.  $d n G $.
    geoser.1 $e |- F = { <. k , y >. | ( k e. NN0 /\ y = ( A ^ k ) ) } $.
    geoser.2 $e |- A e. CC $.
    $( The value of the finite geometric series ` 1 + A ^ 1 + A ^ 2 + ` ...
       ` + A ^ N ` . $)
    geoseri $p |- ( ( N e. NN0 /\ A =/= 1 ) -> ( ( + seq0 F ) ` N ) =
                 ( ( 1 - ( A ^ ( N + 1 ) ) ) / ( 1 - A ) ) ) $=
      ( vj vm cn0 wcel c1 wne wa caddc cseq0 co cfv cmin cmul cdiv cexp cc cc0
      wceq cv expcl mpan fopab ser0cl1i subidi eqeq1i ax1cn subcan2i eqcom
      3bitr3i necon3bii biimpi subcli divcan4 mp3an2 syl2an oprex opreq2
      ser0p1i opreq1d peano2nn0 sylancr adddir mp3an3 syl11anc eqtrd adantr
      opreq1 expp1 opreq2d mulid1 syl subdi mp3an23 nncan mp2an opreq2i
      syl5reqr eqtr3d mulcl sylancl subsub mp3an1 3eqtr2rd sylan9eqr ex exp0
      ax-mp opreq1i mulid2i eqtri ser00i addid2i exp1 3eqtr4i fveq2 eqeq12d
      nn0indALT ) EJKZBLMZNEODPQZRZLBSQZTQZXIUAQZXHLBELOQZUBQZSQZXIUAQZXEXHUCKZ
      XIUDMZXKXHUEZXFDECAJUCBCUFZUBQZDFBUCKZXSJKXTUCKGBXSUGUHUIZUJXFXQBLXIUDBBS
      QZXIUEUDXIUEBLUEXIUDUEYCUDXIBGUKULBLBGUMGUNUDXIUOUPUQURXPXIUCKZXQXRLBUMGU
      SZXHXIUTVAVBXEXKXOUEXFXEXJXNXIUAHUFZXGRZXITQZLBYFLOQZUBQZSQZUEUDXGRZXITQZ
      LBUDLOQZUBQZSQZUEIUFZXGRZXITQZLBYQLOQZUBQZSQZUEZYTXGRZXITQZLBYTLOQZUBQZSQ
      ZUEZXJXNUEHIEYQJKZUUCUUIUUJUUCNUUEYSUUAXITQZOQZUUHUUJUUEUULUEUUCUUJUUEYRU
      UAOQZXITQZUULUUJUUDUUMXITAXTUUACDYQFBYTUBVCXSYTBUBVDVEVFUUJYRUCKZUUAUCKZU
      UNUULUEZDYQYBUJUUJYAYTJKZUUPGYQVGZBYTUGVHZUUOUUPYDUUQYEYRUUAXIVIVJVKVLVMU
      UCUUJUULUUBUUKOQZUUHYSUUBUUKOVNUUJUUHLUUABTQZSQLUUAUUKSQZSQZUVAUUJUUGUVBL
      SUUJYAUURUUGUVBUEGUUSBYTVOVHVPUUJUVCUVBLSUUJUUALTQZUUKSQZUVCUVBUUJUVEUUAU
      UKSUUJUUPUVEUUAUEUUTUUAVQVRVFUUJUUALXISQZTQZUVFUVBUUJUUPUVHUVFUEZUUTUUPLU
      CKZYDUVIUMYEUUALXIVSVTVRUVGBUUATUVJYAUVGBUEUMGLBWAWBWCWDWEVPUUJUUPUUKUCKZ
      UVDUVAUEZUUTUUJUUPYDUVKUUTYEUUAXIWFWGUVJUUPUVKUVLUMLUUAUUKWHWIVKWJWKVLWLB
      UDUBQZXITQZXIYMYPUVNLXITQXIUVMLXITYAUVMLUEGBWMWNWOXIYEWPWQYLUVMXITAXTUVMC
      DFBUDUBVCXSUDBUBVDWRWOYOBLSYOBLUBQZBYNLBUBLUMWSWCYAUVOBUEGBWTWNWQWCXAYFUD
      UEZYHYMYKYPUVPYGYLXITYFUDXGXBVFUVPYJYOLSUVPYIYNBUBYFUDLOVNVPVPXCYFYQUEZYH
      YSYKUUBUVQYGYRXITYFYQXGXBVFUVQYJUUALSUVQYIYTBUBYFYQLOVNVPVPXCYFYTUEZYHUUE
      YKUUHUVRYGUUDXITYFYTXGXBVFUVRYJUUGLSUVRYIUUFBUBYFYTLOVNVPVPXCYFEUEZYHXJYK
      XNUVSYGXHXITYFEXGXBVFUVSYJXMLSUVSYIXLBUBYFELOVNVPVPXCXDVFVMWE $.
      $( [12-May-2006] $)

    ${
      geolimilem.3 $e |- ( abs ` A ) < 1 $.
      geolimilem.4 $e |- G Fn NN0 $.
      geolimilem.5 $e |- ( n e. NN0 ->
               ( G ` n ) = ( -u ( A x. ( F ` n ) ) / ( 1 - A ) ) ) $.
      $( Lemma for ~ geolimi . $)
      geolimilem $p |- ( + seq0 F ) ~~> ( 1 / ( 1 - A ) ) $=
        ( caddc cseq0 co c1 cmin cdiv cc0 cli wbr cc wcel cz cv cfv wceq wa
        cuz wral cneg cmul ax1cn subcli cr cabs clt wne 1re abssubne0 mp3an
        reccli negcli mulcli cexp cn cn0 nnnn0 opreq2 oprex fvopab4 syl rgen
        nn0ex fopabex2 expcnv 0z elnn0uz expcl mpan fopab ffvelrni pm3.2i
        div23 mp3an13 mulneg1 sylancr opreq1d mulneg12 mp2an divrec2 eqtr4i
        opreq1i eqcomi a1i 3eqtr3d eqtrd jca sylbir wfn cvv fnex elisseti
        climmulc2 mp4an mul01i breqtri mulcl negcl divcl mp3an23 eqeltrd
        mulcom opreq2d expp1 3eqtr4rd negsub eqtr4d divdir subeq0i necon3bii
        mpbi necomi geoseri mpan2 3eqtr4d climaddc2 addid1i ) LEMNZOOBPNZQNZRLN
        ZYJSFRSTYJUAUBZRUCUBZDUDZFUEZUAUBZYNYHUEZYJYOLNZUFZUGZDRUHUEZUIYHYKSTFY
        JUJZBUKNZRUKNZRSUUCUAUBERSTZYMYNEUEZUAUBZYOUUCUUFUKNZUFZUGZDUUAUIFUUDST
        UUBBYJYIOBULHUMZBUAUBZOUNUBBUOUEOUPTZYIRUQZHURIBOUSUTZVAZVBHVCZUULUUFBY
        NVDNZUFZDVEUIUUMUUEHUUSDVEYNVEUBYNVFUBZUUSYNVGCAYNBCUDZVDNZUURVFEUVAYNB
        VDVHGBYNVDVIVJZVKVLIBDECAVFUVBEVMGVNZVOUTVPUUJDUUAYNUUAUBZUUTUUJYNVQZUU
        TUUGUUIVFUAYNECAVFUAUVBEGUULUVAVFUBUVBUAUBHBUVAVRVSVTWAZUUTYOBUUFUKNZUJ
        ZYIQNZUUHKUUTBUJZUUFUKNZYIQNZUVKYIQNZUUFUKNZUVJUUHUUTUUGUVMUVOUFZUVGUVK
        UAUBZUUGYIUAUBZUUNUGZUVPBHVBZUVRUUNUUKUUOWBZUVKUUFYIWCWDVKUUTUVLUVIYIQU
        UTUULUUGUVLUVIUFHUVGBUUFWEWFWGUVOUUHUFUUTUUHUVOUUCUVNUUFUKUUCYJUVKUKNZU
        VNYLUULUUCUWBUFUUPHYJBWHWIUVQUVRUUNUVNUWBUFUVTUUKUUOUVKYIWJUTWKWLWMWNWO
        WPWQWRVLRUUCDEFRUVDFVFWSVFWTUBFWTUBJVMVFWTFXAWIZRUCVPXBZXCXDUUCUUQXEXFU
        UPVPYTDUUAUVEUUTYTUVFUUTYPYSUUTYOUVJUAKUUTUVIUAUBZUVJUAUBZUUTUVHUAUBZUW
        EUUTUULUUGUWGHUVGBUUFXGWFZUVHXHVKZUWEUVRUUNUWFUUKUUOUVIYIXIXJVKXKUUTOBY
        NOLNVDNZPNZYIQNZYJUVJLNZYQYRUUTUWLOUVILNZYIQNZUWMUUTUWKUWNYIQUUTUWKOUVH
        PNZUWNUUTUWJUVHOPUUTBUURUKNZUURBUKNZUVHUWJUUTUULUURUAUBZUWQUWRUFHUULUUT
        UWSHBYNVRVSBUURXLWFUUTUUFUURBUKUVCXMUULUUTUWJUWRUFHBYNXNVSXOXMUUTOUAUBZ
        UWGUWNUWPUFULUWHOUVHXPWFXQWGUUTUWEUWOUWMUFZUWIUWTUWEUVSUXAULUWAOUVIYIXR
        WDVKWPUUTBOUQYQUWLUFOBUUNOBUQUUOYIROBOBULHXSXTYAYBABCEYNGHYCYDUUTYOUVJY
        JLKXMYEWQWRVLRYJDFYHRUWCLEMVIUWDOYIQVIYFXDYJUUPYGXF $.
        $( [15-May-2006] $)
    $}

    ${
      geolimi.3 $e |- ( abs ` A ) < 1 $.
      $( The partial sums in the infinite series ` 1 + A ^ 1 + A ^ 2 ` ...
         converge to ` ( 1 / ( 1 - A ) ) ` . $)
      geolimi $p |- ( + seq0 F ) ~~> ( 1 / ( 1 - A ) ) $=
        ( vn vj vz cv cn0 wcel cfv cmul co cneg c1 cmin cdiv wceq wa copab
        oprex eqid fnopab2 fveq2 opreq2d negeqd opreq1d fvopab4 geolimilem ) AB
        CHDIKZLMJKBUMDNZOPZQZRBSPZTPZUAUBIJUCZEFGIJLURUSUPUQTUDUSUEZUFIJHKZURBV
        ADNZOPZQZUQTPLUSUMVAUAZUPVDUQTVEUOVCVEUNVBBOUMVADUGUHUIUJUTVDUQTUDUKUL
        $.
        $( [15-May-2006] $)
    $}
  $}

  ${
    $d k y A $.
    geolim.1 $e |- F = { <. k , y >. | ( k e. NN0 /\ y = ( A ^ k ) ) } $.
    $( The partial sums in the infinite series ` 1 + A ^ 1 + A ^ 2 ` ...
       converge to ` ( 1 / ( 1 - A ) ) ` . $)
    geolim $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  ( + seq0 F ) ~~> ( 1 / ( 1 - A ) ) ) $=
      ( cc wcel cabs cfv c1 clt wbr wa caddc cv cn0 cexp co wceq copab cseq0
      cmin cdiv cli cc0 cif opreq1 eqeq2d anbi2d opabbidv opreq2d opreq2
      breq12d eqid eleq1 fveq2 breq1d anbi12d 0cn abs0 lt01 eqbrtri pm3.2i
      elimhyp simpli simpri geolimi dedth opreq2i syl5eqbr ) BFGZBHIZJKLZMZNCOZ
      PGZAOZBVOQRZSZMZCATZUARZJJBUBRZUCRZNDUARUDVNWBWDUDLNVPVQVNBUEUFZVOQRZSZMZ
      CATZUARZJJWEUBRZUCRZUDLBUEBWESZWBWJWDWLUDWMWAWINUAWMVTWHCAWMVSWGVPWMVRWFV
      QBWEVOQUGUHUIUJUKWMWCWKJUCBWEJUBULUKUMAWECWIWIUNWEFGZWEHIZJKLZVNWNWPMUEFG
      ZUEHIZJKLZMBUEWMVKWNVMWPBWEFUOWMVLWOJKBWEHUPUQURUEWESZWQWNWSWPUEWEFUOWTWR
      WOJKUEWEHUPUQURWQWSUSWRUEJKUTVAVBVCVDZVEWNWPXAVFVGVHDWANUAEVIVJ $.
      $( [15-May-2006] $)
  $}

  ${
    $d k y A $.
    geolim1i.1 $e |- F = { <. k , y >. | ( k e. NN /\ y = ( A ^ k ) ) } $.
    geolim1i.2 $e |- A e. CC $.
    geolim1i.3 $e |- ( abs ` A ) < 1 $.
    $( The partial sums in the geometric series ` A ^ 1 + A ^ 2 ` ... converge
       to ` ( A / ( 1 - A ) ) ` . $)
    geolim1i $p |- ( + seq1 F ) ~~> ( A / ( 1 - A ) ) $=
      ( cc0 c1 caddc co cop cv cn0 wcel cexp wceq wa copab cseqz cmin cdiv cfv
      cseq1 cli cuz wbr cz 0z uzid ax-mp nn0ex opabex2 oprex cseq0 addex
      seq0seqz eqid geolimi eqbrtrri cc wf expcl mpan fopab nn0uz feq2i mpbi
      clim2serzi cres cn wss nnssnn0 resopab2 nnuz reseq2i 3eqtr2i opreq2i 1z
      seqzres eqtri nnex fopabex2 seq1seqz ax1cn addid2i opeq1i opreq1i
      3eqtr4i wne subcli cr cabs clt 1re abssubne0 mp3an pm3.2i divsubdir
      nncan mp2an dividi 3eqtr3i seqz1 0nn0 opreq2 fvopab4 exp0 3eqtri eqtr4i
      3brtr4i ) HIJKZJLZCMZNOZAMBXNPKZQZRCASZTKZIIBUAKZUBKZHHJLXRTKZUCZUAKZJDUD
      KZBXTUBKZUEHHUFUCZOZXSYDUEUGHUHOZYHUIHUJUKYAXRHHCANXPULUMZIXTUBUNJXRUOKYB
      YAUEJXRUPYJUQABCXRXRURZFGUSUTNVAXRVBYGVAXRVBCANVAXPXRYKBVAOZXOXPVAOFBXNVC
      VDVENYGVAXRVFVGVHVIUKIJLZDTKZYMXRTKZYEXSYNYMXRIUFUCZVJZTKZYODYQYMTDXNVKOX
      QRCASZXRVKVJZYQEVKNVLYTYSQVMXQCAVKNVNUKVKYPXRVOVPVQVRIUHOYRYOQVSJXRIUPYJV
      TUKWAJDUPCAVKXPDWBEWCWDXMYMXRTXLIJIWEWFWGWHWIYFYAIUAKZYDIXTUAKZXTUBKZYAXT
      XTUBKZUAKZYFUUAIVAOZXTVAOZUUGXTHWJZRUUCUUEQWEIBWEFWKZUUGUUHUUIYLIWLOBWMUC
      IWNUGUUHFWOGBIWPWQZWRIXTXTWSWQUUBBXTUBUUFYLUUBBQWEFIBWTXAWHUUDIYAUAXTUUIU
      UJXBVRXCYCIYAUAYCHXRUCZBHPKZIYIYCUUKQUIJXRHUPYJXDUKHNOUUKUULQXECAHXPUULNX
      RXNHBPXFYKBHPUNXGUKYLUULIQFBXHUKXIVRXJXK $.
      $( [6-Jun-2006] $)
  $}

  ${
    $d k y A $.
    geolim1.1 $e |- F = { <. k , y >. | ( k e. NN /\ y = ( A ^ k ) ) } $.
    $( The partial sums in the infinite series ` A ^ 1 + A ^ 2 ` ... converge
       to ` ( A / ( 1 - A ) ) ` . $)
    geolim1 $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  ( + seq1 F ) ~~> ( A / ( 1 - A ) ) ) $=
      ( cc wcel cabs cfv c1 clt wbr wa caddc cv cn cexp co wceq copab cseq1
      cmin cdiv cli cc0 cif opreq1 eqeq2d anbi2d opabbidv opreq2d id opreq2
      opreq12d breq12d eqid eleq1 fveq2 breq1d anbi12d 0cn abs0 lt01 eqbrtri
      pm3.2i elimhyp simpli simpri geolim1i dedth opreq2i syl5eqbr ) BFGZBHIZJK
      LZMZNCOZPGZAOZBVQQRZSZMZCATZUARZBJBUBRZUCRZNDUARUDVPWDWFUDLNVRVSVPBUEUFZV
      QQRZSZMZCATZUARZWGJWGUBRZUCRZUDLBUEBWGSZWDWLWFWNUDWOWCWKNUAWOWBWJCAWOWAWI
      VRWOVTWHVSBWGVQQUGUHUIUJUKWOBWGWEWMUCWOULBWGJUBUMUNUOAWGCWKWKUPWGFGZWGHIZ
      JKLZVPWPWRMUEFGZUEHIZJKLZMBUEWOVMWPVOWRBWGFUQWOVNWQJKBWGHURUSUTUEWGSZWSWP
      XAWRUEWGFUQXBWTWQJKUEWGHURUSUTWSXAVAWTUEJKVBVCVDVEVFZVGWPWRXCVHVIVJDWCNUA
      EVKVL $.
      $( [6-Jun-2006] $)
  $}

  ${
    $d A j y $.
    georeclim.1 $e |- F = { <. j , y >. |
                            ( j e. NN0 /\ y = ( ( 1 / A ) ^ j ) ) } $.
    $( The limit of a geometric series of reciprocals.  (Contributed by Paul
       Chapman, 28-Dec-2007.) $)
    georeclim $p |- ( ( A e. CC /\ 1 < ( abs ` A ) ) ->
                      ( + seq0 F ) ~~> ( A / ( A - 1 ) ) ) $=
      ( cc wcel c1 cabs cfv clt wbr wa caddc cseq0 co cdiv cmin cli cc0 wne cr
      abscl adantr lt01 wi 0re 1re axlttrn mp3an12 syl mpani imp gt0ne0
      syl11anc wb abs00 necon3bid mpbid reccl syldan wceq ax1cn absdiv mp3an1
      cle ltleii absid mp2an opreq1i syl6eq recgt1 biimpd ex pm2.43d eqbrtrd
      geolim cmul divid opreq2d a1i subcl sylancr abssubne0 mp3an2 jca simpl
      divdivdiv syl22anc div1 3eqtr3rd mulid2 subdir syl111anc recid2 opreq12d
      eqtrd breqtrd ) BFGZHBIJZKLZMZNDOPZHHHBQPZRPZQPZBBHRPZQPZSXBXDFGZXDIJZHKL
      ZXCXFSLWSXABTUAZXIXBWTTUAZXLXBWTUBGZTWTKLZXMWSXNXABUCZUDZWSXAXOWSTHKLZXAX
      OUEWSXNXRXAMXOUFZXPTUBGHUBGZXNXSUGUHTHWTUIUJUKULUMZWTUNUOWSXMXLUPXAWSWTTB
      TBUQURUDUSZBUTVAZXBXJHWTQPZHKXBXJHIJZWTQPZYDWSXAXLXJYFVBZYBHFGZWSXLYGVCHB
      VDVEVAYEHWTQXTTHVFLYEHVBUHTHUGUHUEVGHVHVIVJVKWSXAYDHKLZWSXAYIWSXAXAYIUFXB
      XAYIXBXNXOXAYIUPXQYAWTVLUOVMVNVOUMVPZAXDCDEVQUOXBXFHBVRPZXEBVRPZQPZXHXBXF
      BBQPZQPZXFHQPZYMXFXBYNHXFQWSXAXLYNHVBYBBVSVAVTXBYHXEFGZXETUAZMWSXLMZYSYOY
      MVBYHXBVCWAZXBYQYRXBYHXIYQVCYCHXDWBWCZXBXIXKYRYCYJXIXTXKYRUHXDHWDWEUOZWFX
      BWSXLWSXAWGZYBWFZUUDHXEBBWHWIXBXFFGZYPXFVBXBYQYRUUEUUAUUBXEUTUOXFWJUKWKXB
      YKBYLXGQWSYKBVBXABWLUDZXBYLYKXDBVRPZRPZXGXBYHXIWSYLUUHVBYTYCUUCHXDBWMWNXB
      YKBUUGHRUUFWSXAXLUUGHVBYBBWOVAWPWQWPWQWR $.
      $( [28-Dec-2007] $)
  $}

  ${
    $d k y A $.  $d y k N $.
    geosum.1 $e |- A e. CC $.
    $( The value of the finite geometric series ` 1 + A ^ 1 + A ^ 2 + ` ...
       ` + A ^ N ` . $)
    geosumi $p |- ( ( N e. NN0 /\ A =/= 1 ) -> sum_ k e. ( 0 ... N ) ( A ^ k )
               = ( ( 1 - ( A ^ ( N + 1 ) ) ) / ( 1 - A ) ) ) $=
      ( vy cn0 wcel c1 wne wa cc0 cfz co cv cexp csu caddc wceq copab cseq0
      cfv cmin cdiv cop cseqz cuz elnn0uz oprex anbi1i opabbii fsumserz2 sylbi
      addex nn0ex opabex2 seq0seqz fveq1i syl6eqr adantr eqid geoseri eqtrd )
      CFGZAHIZJKCLMABNZOMZBPZCQVEFGZENVFRZJZBESZTMZUAZHACHQMOMUBMHAUBMUCMVCVGVM
      RVDVCVGCKQUDVKUEMZUAZVMVCCKUFUAZGVGVORCUGEVFBVKKCAVEOUHVJVEVPGZVIJBEVHVQV
      IVEUGUIUJUKULCVLVNQVKUMBEFVFUNUOUPUQURUSEABVKCVKUTDVAVB $.
      $( [9-May-2006] $)
  $}

  ${
    $d k x y z A $.  $d k x y z R $.
    $( The infinite sum of ` 1 + A ^ 1 + A ^ 2 ` ... is
       ` ( 1 / ( 1 - A ) ) ` . $)
    geoisum $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  sum_ k e. NN0 ( A ^ k ) = ( 1 / ( 1 - A ) ) ) $=
      ( vy cc wcel cabs cfv c1 clt wbr wa cc0 cuz cv cexp co csu cmin cdiv cn0
      cz caddc cop wceq copab cseqz cli 0z cseq0 eqid geolim addex nn0ex
      opabex2 seq0seqz syl5eqbrr oprex elnn0uz anbi1i opabbii isumclim4
      sylancr nn0uz sumeq1i syl5eq ) ADEAFGHIJKZLMGZABNZOPZBQZHHARPZSPZTVIBQVFL
      UAELUBUCVHTEZCNVIUDZKZBCUEZUFPZVLUGJVJVLUDUHVFUBVPUIPVLVQUGCABVPVPUJUKUBV
      PULBCTVIUMUNUOUPCVIVLBVPLAVHOUQVOVHVGEZVNKBCVMVRVNVHURUSUTHVKSUQVAVBTVGVI
      BVCVDVE $.
      $( [4-Jan-2007] $) $( [15-May-2006] $)

    $( The infinite sum of reciprocals
       ` 1 + ( 1 / A ) ^ 1 + ( 1 / A ) ^ 2 ` ... is ` A / ( A - 1 ) ` .
       (Contributed by rpenner, 3-Nov-2007.) $)
    geoisumr $p |- ( ( A e. CC /\ 1 < ( abs ` A ) ) ->
              sum_ k e. NN0 ( ( 1 / A ) ^ k ) = ( A / ( A - 1 ) ) ) $=
      ( cc wcel c1 cabs cfv clt wbr wa cn0 cdiv co cv cexp csu cmin wceq cc0
      wne lt01 cr wi abscl 0re 1re axlttrn mp3an12 syl mpani imp absgt0
      biimpar syldan reccl ax1cn absdiv mp3an1 cle ltleii absidi ax-mp opreq1i
      syl6eq recgt1i sylan simprd eqbrtrd geoisum syl11anc divsubdir mp3an2
      anabss5 divid opreq1d eqtr2d opreq2d subcl mpan2 adantr ltne wb subeq0
      fveq2 syl6bi necon3d mpd simpl recdiv syl22anc 3eqtrd ) ACDZEAFGZHIZJZKEA
      LMZBNOMBPZEEWPQMZLMZEAEQMZALMZLMZAWTLMZWOWPCDZWPFGZEHIWQWSRWLWNASTZXDWLWN
      SWMHIZXFWLWNXGWLSEHIZWNXGUAWLWMUBDZXHWNJXGUCZAUDZSUBDEUBDZXIXJUEUFSEWMUGU
      HUIUJUKWLXFXGAULUMUNZAUOUNWOXEEWMLMZEHWOXEEFGZWMLMZXNWLWNXFXEXPRZXMECDZWL
      XFXQUPEAUQURUNXOEWMLSEUSIXOERSEUEUFUAUTEUFVAVBZVCVDWOSXNHIZXNEHIZWLXIWNXT
      YAJXKWMVEVFVGVHWPBVIVJWOWRXAELWLWNXFWRXARXMWLXFJZXAAALMZWPQMZWRWLXFXAYDRZ
      WLXRYBYEUPAEAVKVLVMYBYCEWPQAVNVOVPUNVQWOWTCDZWTSTZWLXFXBXCRWLYFWNWLXRYFUP
      AEVRVSVTWOWMETZYGWLXIWNYHXKXLXIWNYHUFEWMWAURVFWLYHYGUCWNWLWTSWMEWLWTSRZAE
      RZWMERWLXRYIYJWBUPAEWCVSYJWMXOEAEFWDXSVDWEWFVTWGWLWNWHXMWTAWIWJWK $.
      $( [3-Nov-2007] $)

    $( The infinite sum of ` A ^ 1 + A ^ 2 ` ... is ` ( A / ( 1 - A ) ) ` . $)
    geoisum1 $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  sum_ k e. NN ( A ^ k ) = ( A / ( 1 - A ) ) ) $=
      ( vy cc wcel cabs cfv c1 clt wbr wa cuz cv cexp co csu cmin cdiv cn cz
      caddc cop wceq copab cseqz cli 1z cseq1 eqid geolim1 addex nnex opabex2
      seq1seqz syl5eqbrr oprex elnnuz anbi1i opabbii isumclim4 sylancr nnuz
      sumeq1i syl5eq ) ADEAFGHIJKZHLGZABMZNOZBPZAHAQOZROZSVHBPVEHTEHUAUBVGSEZCM
      VHUCZKZBCUDZUEOZVKUFJVIVKUCUGVEUAVOUHOVKVPUFCABVOVOUIUJUAVOUKBCSVHULUMUNU
      OCVHVKBVOHAVGNUPVNVGVFEZVMKBCVLVQVMVGUQURUSAVJRUPUTVASVFVHBVBVCVD $.
      $( [1-Nov-2007] $)

    $( The infinite sum of ` A x. ( R ^ 1 ) + A x. ( R ^ 2 ) ` ... is
       ` ( A x. R ) / ( 1 - R ) ` . $)
    geoisum1c $p |- ( ( A e. CC /\ R e. CC /\ ( abs ` R ) < 1 ) ->
          sum_ k e. NN ( A x. ( R ^ k ) ) = ( ( A x. R ) / ( 1 - R ) ) ) $=
      ( vy vx vz cc wcel cabs cfv c1 clt wbr w3a cmul co cmin cdiv cuz cv cexp
      csu cn cc0 wne wa wceq divass ax1cn subcl mpan 3ad2ant2 cr 1re abssubne0
      mp3an2 3adant1 jca syld3an3 geoisum1 nnuz sumeq1i syl5eqr opreq2d wral
      caddc cop copab cseqz cli wex cn0 elnnuz nnnn0 sylbir expcl sylan2
      r19.21aiva adantr cseq1 eqid geolim1 oprex breq2 addex nnex opabex2
      seq1seqz breq1i syl5bbr cla4ev syl cz 1z hbopab1 cvv fvopab2 mpan2
      isummulc1ai mpanl1 3impb syl6eqr 3eqtr2rd ) AGHZBGHZBIJKLMZNZABOPKBQPZRPZ
      ABXHRPZOPZAKSJZBCTZUAPZCUBZOPZUCAXNOPZCUBZXDXEXHGHZXHUDUEZUFXIXKUGXFABXHU
      HXGXSXTXEXDXSXFKGHXEXSUIKBUJUKULXEXFXTXDXEKUMHXFXTUNBKUOUPUQURUSXGXOXJAOX
      EXFXOXJUGXDXEXFUFZUCXNCUBXJXOBCUTUCXLXNCVAVBVCUQVDXGXPXLXQCUBZXRXDXEXFXPY
      BUGZYAXDXNGHZCXLVEZKVFVGXMUCHZDTXNUGUFZCDVHZVIPZETZVJMZEVKZUFZYCYAYEYLXEY
      EXFXEYDCXLXMXLHZXEXMVLHZYDYNYFYOXMVMZXMVNVOBXMVPVQVRVSYAVFYHVTPZXJVJMZYLD
      BCYHYHWAWBYKYREXJBXHRWCYJXJUGYQYJVJMYRYKYJXJYQVJWDYQYIYJVJVFYHWECDUCXNWFW
      GZWHWIWJWKWLURKWMHXDYMYCWNEFXNACYHKBXMUAWCZYSYGCDFWOYNYFXMYHJXNUGZYPYFXNW
      PHUUAYTCDUCXNWPWQWRVOWSWTVQXAUCXLXQCVAVBXBXC $.
      $( [2-Nov-2007] $)
  $}

  $( The recurring decimal 0.999..., which is defined as the infinite sum 0.9 +
     0.09 + 0.009 + ... i.e. ` 9 / 10 ^ 1 + 9 / 10 ^ 2 + 9 / 10 ^ 3 + ... ` ,
     is exactly equal to 1, according to ZF set theory.  Interestingly, about
     40% of the people responding to a poll at
     ~ http://forum.physorg.com/index.php?showtopic=13177 disagree. $)
  0.999... $p |- sum_ k e. NN ( 9 / ( 10 ^ k ) ) = 1 $=
    ( cn c9 c10 cv cexp co cdiv csu c1 cmul cmin wcel cn0 wceq nnnn0 cc cc0
    wne 10re recni expcl mpan 10pos gt0ne0ii expne0i mp3an12 9re divrec mp3an1
    syl11anc exprecOLD mp3an13 opreq2d eqtr4d syl sumeq2i cabs cfv clt wbr
    rereccli cle 0re recgt0ii ltleii absidi ax-mp caddc 9pos cr wb 1re
    ltaddpos2 mp2an mpbi df-10 breqtrri recgt1 eqbrtri geoisum1c mp3an divreci
    divcan2i ax1cn subdii mulid1i recidi opreq12i addcomi eqtr4i subaddrii
    3eqtrri eqtri redivcli subcli mulcani divgt0ii dividi eqtr3i 3eqtri ) BCDAE
    ZFGZHGZAIBCJDHGZXBFGZKGZAIZCXEKGZJXELGZHGZJBXDXGAXBBMXBNMZXDXGOXBPXLXDCJXCH
    GZKGZXGXLXCQMZXCRSZXDXNOZDQMZXLXODTUAZDXBUBUCXRDRSZXLXPXSDTUDUEZDXBUFUGCQMZ
    XOXPXQCUHUAZCXCUIUJUKXLXFXMCKXRXLXTXFXMOXSYADXBULUMUNUOUPUQYBXEQMXEURUSZJUT
    VAXHXKOYCXEDTYAVBZUAZYDXEJUTRXEVCVAYDXEORXEVDYEDTUDVEVFXEYEVGVHJDUTVAZXEJUT
    VAZJCJVIGZDUTRCUTVAZJYIUTVAZVJCVKMJVKMYJYKVLUHVMCJVNVOVPVQVRDVKMRDUTVAYGYHV
    LTUDDVSVOVPVTCXEAWAWBCDHGZYLHGXKJYLXIYLXJHCDYCXSYAWCDYLKGZDXJKGZOYLXJOYMCYN
    CDYCXSYAWDYNDJKGZDXEKGZLGDJLGCDJXEXSWEYFWFYODYPJLDXSWGDXSYAWHWIDJCXSWEYCJCV
    IGYIDJCWEYCWJVQWKWLWMWNYLXJDYLCDUHTYAWOZUAZJXEWEYFWPXSYAWQVPWIYLYRYLYQCDUHT
    VJUDWRUEWSWTXA $.
    $( [2-Nov-2007] $)


