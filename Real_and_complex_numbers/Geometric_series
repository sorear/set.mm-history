$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Geometric series
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d k n A $.  $d k ph $.
    expcnv.1 $e |- ( ph -> A e. CC ) $.
    expcnv.2 $e |- ( ph -> ( abs ` A ) < 1 ) $.
    $( A sequence of powers of a complex number ` A ` with absolute value
       smaller than 1 converges to zero.  (The proof was shortened by Mario
       Carneiro, 26-Apr-2014.) $)
    expcnv $p |- ( ph -> ( n e. NN0 |-> ( A ^ n ) ) ~~> 0 ) $=
      ( cexp co cc0 wbr wceq wa c1 cn wcel a1i cc syl crp cr cle vk cn0 cv cmpt
      cli cvv nnuz cz 1z nn0ex mptex 0cn cfv nnnn0 oveq2 eqid ovex fvmpt oveq1d
      simpr sylan9eqr 0exp adantl eqtrd climconst wne cabs cdiv cmin clt adantr
      wb absrpcl sylan elrp reclt1 sylbi mpbid rpreccl rpre difrp sylancr recnd
      1re divcnv nnex nndivre eqeltrd nnz rpexpcl syl2an caddc rpmulcl peano2re
      cmul nnrp lep1 rpge0 bernneq2 syl3anc letrd rpcnne0 exprec breqtrd lerec2
      3expa syl2anb syl2anc nncn nnne0 jca recdiv2 breqtrrd climsqueeze2 absexp
      3brtr4d expcl fveq2d 3eqtr4rd climabs0 biimpar syldan pm2.61dane ) ACUBBC
      UCZFGZUDZHUEIZBHABHJZKZHUAYFLUFMUGLUHNZYIUIOYFUFNZYICUBYEUJUKZOHPNZYIULOY
      IUAUCZMNZKYNYFUMZHYNFGZHYOYIYPBYNFGZYQYOYNUBNZYPYRJZYNUNZCYNYEYRUBYFYDYNB
      FUOYFUPBYNFUQURZQYIBHYNFAYHUTUSVAYOYQHJYIYNVBVCVDVEABHVFZCMBVGUMZYDFGZUDZ
      HUEIZYGAUUCKZHUACMLLUUDVHGZLVIGZVHGZYDVHGZUDZUUFLPUFMUGYJUUHUIOYMUUHULOUU
      HUUKPNUUMHUEIUUHUUKUUHUUKRNZUUKSNZUUHUUJRNZUUNUUHLUUIVJIZUUPUUHUUDLVJIZUU
      QAUURUUCEVKUUHUUDRNZUURUUQVLZABPNZUUCUUSDBVMVNZUUSUUDSNHUUDVJIKUUTUUDVOUU
      DVPVQQVRUUHLSNUUISNZUUQUUPVLWDUUHUUIRNZUVCUUHUUSUVDUVBUUDVSQZUUIVTQZLUUIW
      AWBVRZUUJVSQUUKVTQZWCUUKCWEQUUFUFNZUUHCMUUEWFUKZOUUHYOKZYNUUMUMZUUKYNVHGZ
      SYOUVLUVMJUUHCYNUULUVMMUUMYDYNUUKVHUOUUMUPUUKYNVHUQURVCZUUHUUOYOUVMSNUVHU
      UKYNWGVNWHUVKYNUUFUMZRNZUVOSNUVKUVOUUDYNFGZRYOUVOUVQJZUUHCYNUUEUVQMUUFYDY
      NUUDFUOUUFUPUUDYNFUQURZVCZUUHUUSYNUHNZUVQRNZYOUVBYNWIZUUDYNWJWKZWHZUVOVTQ
      UVKUVQUVMUVOUVLTUVKUVQLUUJYNWOGZVHGZUVMTUVKUWFLUVQVHGZTIZUVQUWGTIZUVKUWFU
      UIYNFGZUWHTUVKUWFUWFLWLGZUWKUVKUWFRNZUWFSNZUUHUUPYNRNUWMYOUVGYNWPUUJYNWMW
      KZUWFVTQZUVKUWNUWLSNUWPUWFWNQUVKUWKRNZUWKSNUUHUVDUWAUWQYOUVEUWCUUIYNWJWKU
      WKVTQUVKUWNUWFUWLTIUWPUWFWQQUVKUVCYSHUUITIZUWLUWKTIUUHUVCYOUVFVKYOYSUUHUU
      AVCUUHUWRYOUUHUVDUWRUVEUUIWRQVKUUIYNWSWTXAUUHUUDPNZUUDHVFZKZUWAUWKUWHJZYO
      UUHUUSUXAUVBUUDXBQUWCUWSUWTUWAUXBUUDYNXCXFWKXDUVKUWMUWBUWIUWJVLZUWOUWDUWM
      UWNHUWFVJIKUVQSNHUVQVJIKUXCUWBUWFVOUVQVOUWFUVQXEXGXHVRUUHUUJPNUUJHVFKZYNP
      NZYNHVFZKUVMUWGJYOUUHUUPUXDUVGUUJXBQYOUXEUXFYNXIYNXJXKUUJYNXLWKXMUVTUVNXP
      UVKUVPHUVOTIUWEUVOWRQXNAYGUUGAUAYFUUFLUFUFMUGYJAUIOYKAYLOUVIAUVJOAYOKZYPY
      RPUXGYSYTYOYSAUUAVCUUBQZAUVAYSYRPNYODUUABYNXQWKWHUXGYRVGUMZUVQYPVGUMUVOAU
      VAYSUXIUVQJYODUUABYNXOWKUXGYPYRVGUXHXRYOUVRAUVSVCXSXTYAYBYC $.
      $( [26-Apr-2014] $) $( [8-May-2006] $)
  $}

  ${
    $d k n A $.  $d k ph $.  $d k n F $.  $d k n Z $.  $d k M $.
    explecnv.1 $e |- Z = ( ZZ>= ` M ) $.
    explecnv.2 $e |- ( ph -> F e. V ) $.
    explecnv.3 $e |- ( ph -> M e. ZZ ) $.
    explecnv.5 $e |- ( ph -> A e. RR ) $.
    explecnv.4 $e |- ( ph -> ( abs ` A ) < 1 ) $.
    explecnv.6 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. CC ) $.
    explecnv.7 $e |- ( ( ph /\ k e. Z ) ->
                       ( abs ` ( F ` k ) ) <_ ( A ^ k ) ) $.
    $( A sequence of terms converges to zero when it is less than powers of a
       number ` A ` whose absolute value is smaller than 1.  (Revised by Mario
       Carneiro, 26-Apr-2014.) $)
    explecnv $p |- ( ph -> F ~~> 0 ) $=
      ( vn cc0 wbr cfv cn0 wcel cli cv cabs cmpt cexp co cle cif cc cvv eqid cz
      cuz 0z ifcl sylancr 0cn a1i recnd expcnv fvex eqeltri mptex wa wceq nn0uz
      cr cin ineq12i uzin sylancl syl5req eleq2d biimpa elin sylib simprd oveq2
      fvmpt syl adantr reexpcl syl2anc eqeltrd simpld fveq2 fveq2d syldan abscl
      ovex 3brtr4d absge0 breqtrrd climsqueeze2 adantl climabs0 mpbird ) ADPUAQ
      OGOUBZDRZUCRZUDZPUAQAPCOSBWRUEUFZUDZXAEPUGQZPEUHZUIUJXEUMRZXFUKAPULTZEULT
      ZXEULTUNJXDPEULUOUPPUITAUQURABOABKUSLUTXAUJTAOGWTGEUMRZUJHEUMVAVBVCURZACU
      BZXFTZVDZXKXCRZBXKUEUFZVGXMXKSTZXNXOVEXMXKGTZXPXMXKGSVHZTZXQXPVDAXLXSAXFX
      RXKAXRXIPUMRZVHZXFGXISXTHVFVIAXHXGYAXFVEJUNEPVJVKVLVMVNXKGSVOVPZVQZOXKXBX
      OSXCWRXKBUEVRXCUKBXKUEWJVSVTZXMBVGTZXPXOVGTAYEXLKWAYCBXKWBWCWDXMXKXARZXKD
      RZUCRZVGXMXQYFYHVEZXMXQXPYBWEZOXKWTYHGXAWRXKVEWSYGUCWRXKDWFWGXAUKYGUCVAVS
      ZVTZXMYGUITZYHVGTAXLXQYMYJMWHZYGWIVTWDXMYHXOYFXNUGAXLXQYHXOUGQYJNWHYLYDWK
      XMPYHYFUGXMYMPYHUGQYNYGWLVTYLWMWNACDXAEFUJGHJIXJMXQYIAYKWOWPWQ $.
      $( [26-Apr-2014] $) $( [19-Jul-2008] $)
  $}

  ${
    $d k n x A $.  $d k x N $.  $d k n x ph $.
    geoser.1 $e |- ( ph -> A e. CC ) $.
    geoser.2 $e |- ( ph -> A =/= 1 ) $.
    geoser.3 $e |- ( ph -> N e. NN0 ) $.
    $( The value of the finite geometric series ` 1 + A ^ 1 + A ^ 2 + ` ...
       ` + A ^ ( N - 1 ) ` .  (Revised by Mario Carneiro, 26-Apr-2014.) $)
    geoser $p |- ( ph -> sum_ k e. ( 0 ... ( N - 1 ) ) ( A ^ k ) =
                    ( ( 1 - ( A ^ N ) ) / ( 1 - A ) ) ) $=
      ( wcel cc0 c1 cmin co cfz cexp csu cdiv wceq caddc oveq2d cc vx vn cn0 cv
      wi oveq1 sumeq1d oveq2 oveq1d eqeq12d imbi2d syl ax-1cn subidi syl6eq wne
      exp0 subcl sylancr necomd wb subeq0 necon3bid mpbird syl2anc eqtrd c0 clt
      div0 wbr cr 0re ltm1 ax-mp cz 0z peano2zm fzn mp2an mpbi sumeq1i syl6reqr
      sum0 eqtri nn0cn adantl pncan sylancl cuz cfv simpr nn0uz syl6eleq adantr
      wa elfznn0 expcl syl2an fsumm1 expp1 sylan nncan eqtr4d a1i subdi syl3anc
      mulid1 3eqtrd mulcl subsub divdir syl112anc divcan4 syl5ibr expcom nn0ind
      cmul a2d mpcom ) DUCHAIDJKLZMLZBCUDZNLZCOZJBDNLZKLZJBKLZPLZQZGAIUAUDZJKLZ
      MLZYCCOZJBYJNLZKLZYGPLZQZUEAIIJKLZMLZYCCOZJBINLZKLZYGPLZQZUEAIUBUDZJKLZML
      ZYCCOZJBUUENLZKLZYGPLZQZUEAIUUEJRLZJKLZMLZYCCOZJBUUMNLZKLZYGPLZQZUEAYIUEU
      AUBDYJIQZYQUUDAUVAYMYTYPUUCUVAYLYSYCCUVAYKYRIMYJIJKUFSUGUVAYOUUBYGPUVAYNU
      UAJKYJIBNUHSUIUJUKYJUUEQZYQUULAUVBYMUUHYPUUKUVBYLUUGYCCUVBYKUUFIMYJUUEJKU
      FSUGUVBYOUUJYGPUVBYNUUIJKYJUUEBNUHSUIUJUKYJUUMQZYQUUTAUVCYMUUPYPUUSUVCYLU
      UOYCCUVCYKUUNIMYJUUMJKUFSUGUVCYOUURYGPUVCYNUUQJKYJUUMBNUHSUIUJUKYJDQZYQYI
      AUVDYMYDYPYHUVDYLYAYCCUVDYKXTIMYJDJKUFSUGUVDYOYFYGPUVDYNYEJKYJDBNUHSUIUJU
      KAUUCIYTAUUCIYGPLZIAUUBIYGPAUUBJJKLIAUUAJJKABTHZUUAJQEBUQULSJUMUNUOUIAYGT
      HZYGIUPZUVEIQAJTHZUVFUVGUMEJBURUSZAUVHJBUPABJFUTAYGIJBAUVIUVFYGIQJBQVAUME
      JBVBUSVCVDZYGVIVEVFYTVGYCCOIYSVGYCCYRIVHVJZYSVGQZIVKHUVLVLIVMVNIVOHZYRVOH
      ZUVLUVMVAVPUVNUVOVPIVQVNIYRVRVSVTWAYCCWCWDWBUUEUCHZAUULUUTAUVPUULUUTUEUUL
      UUTAUVPWOZUUHUUIRLZUUKUUIRLZQUUHUUKUUIRUFUVQUUPUVRUUSUVSUVQUUPIUUEMLZYCCO
      UVRUVQUUOUVTYCCUVQUUNUUEIMUVQUUETHZUVIUUNUUEQUVPUWAAUUEWEWFUMUUEJWGWHSUGU
      VQYCUUICIUUEUVQUUEUCIWIWJAUVPWKWLWMUVQUVFYBUCHYCTHYBUVTHAUVFUVPEWNZYBUUEW
      PBYBWQWRYBUUEBNUHWSVFUVQUUSUUJUUIYGXQLZRLZYGPLZUUKUWCYGPLZRLZUVSUVQUURUWD
      YGPUVQUURJUUIUWCKLZKLZUWDUVQUUQUWHJKUVQUUQUUIJYGKLZXQLZUUIJXQLZUWCKLZUWHU
      VQUUQUUIBXQLZUWKAUVFUVPUUQUWNQEBUUEWTXAUVQUWJBUUIXQUVQUVIUVFUWJBQUMUWBJBX
      BUSSXCUVQUUITHZUVIUVGUWKUWMQAUVFUVPUWOEBUUEWQXAZUVIUVQUMXDZAUVGUVPUVJWNZU
      UIJYGXEXFUVQUWLUUIUWCKUVQUWOUWLUUIQUWPUUIXGULUIXHSUVQUVIUWOUWCTHZUWIUWDQU
      WQUWPUVQUWOUVGUWSUWPUWRUUIYGXIVEZJUUIUWCXJXFVFUIUVQUUJTHZUWSUVGUVHUWEUWGQ
      UVQUVIUWOUXAUMUWPJUUIURUSUWTUWRAUVHUVPUVKWNZUUJUWCYGXKXLUVQUWFUUIUUKRUVQU
      WOUVGUVHUWFUUIQUWPUWRUXBUUIYGXMXFSXHUJXNXOXRXPXS $.
      $( [26-Apr-2014] $) $( [12-May-2006] $)
  $}

  ${
    $d j k n A $.  $d j k F $.  $d j k M $.  $d j k ph $.
    geolim.1 $e |- ( ph -> A e. CC ) $.
    geolim.2 $e |- ( ph -> ( abs ` A ) < 1 ) $.
    ${
      geolim.3 $e |- ( ( ph /\ k e. NN0 ) -> ( F ` k ) = ( A ^ k ) ) $.
      $( The partial sums in the infinite series ` 1 + A ^ 1 + A ^ 2 ` ...
         converge to ` ( 1 / ( 1 - A ) ) ` . $)
      geolim $p |- ( ph -> seq 0 ( + , F ) ~~> ( 1 / ( 1 - A ) ) ) $=
        ( vn cc0 c1 cmin co cdiv cn0 cexp cc wcel cmul cfv wceq vj caddc cli cv
        cseq cmpt cvv nn0uz cz a1i 0cn expcnv wne ax-1cn subcl sylancr cabs clt
        0z wbr 1re ltnri fveq2 1nn0 cle nn0ge0 absid ax-mp syl6eq breq1d mtbiri
        cr necon2ai necomd wb subeq0 necon3bid mpbird divcl syl3anc nn0ex mptex
        wa oveq2 eqid ovex fvmpt adantl expcl sylan eqeltrd expp1 adantr mulcom
        syl syl2anc eqtrd oveq1d div23 syl112anc oveq1 oveq2d 3eqtr4d climmulc2
        mul01 breqtrd reccl seqex peano2nn0 cfz csu nn0cn pncan sylancl sumeq1d
        syl2an divsubdir geoser simpll elfznn0 simpr syl6eleq fsumser climsubc2
        cuz 3eqtr3rd subid1 ) AUBDIUEZJJBKLZMLZIKLZYJUCAIYJUAHNBHUDZJUBLZOLZYIM
        LZUFZYHIPUGNUHIUIQAUSUJZIPQAUKUJZAYPBYIMLZIRLZIUCAIYSUAHNBYLOLZUFZYPIPU
        GNUHYQYRABHEFULABPQZYIPQZYIIUMZYSPQZEAJPQZUUCUUDUNEJBUOUPZAUUEJBUMABJAB
        UQSZJURUTZBJUMZFUUJBJBJTZUUJJJURUTJVAVBUULUUIJJURUULUUIJUQSZJBJUQVCJNQZ
        UUMJTZVDUUNJVLQIJVEUTUUOVAJVFJVGUPVHVIVJVKVMWOZVNAYIIJBAUUGUUCYIITJBTVO
        UNEJBVPUPVQVRZBYIVSVTZYPUGQAHNYOWAWBUJAUAUDZNQZWCZUUSUUBSZBUUSOLZPUUTUV
        BUVCTAHUUSUUAUVCNUUBYLUUSBOWDUUBWEBUUSOWFWGWHZAUUCUUTUVCPQZEBUUSWIWJZWK
        UVABUUSJUBLZOLZYIMLZYSUVCRLZUUSYPSZYSUVBRLUVAUVIBUVCRLZYIMLZUVJUVAUVHUV
        LYIMUVAUVHUVCBRLZUVLAUUCUUTUVHUVNTEBUUSWLWJUVAUVEUUCUVNUVLTUVFAUUCUUTEW
        MZUVCBWNWPWQWRUVAUUCUVEUUDUUEUVMUVJTUVOUVFAUUDUUTUUHWMZAUUEUUTUUQWMZBUV
        CYIWSWTWQUUTUVKUVITAHUUSYOUVINYPYLUUSTZYNUVHYIMUVRYMUVGBOYLUUSJUBXAXBWR
        YPWEUVHYIMWFWGWHZUVAUVBUVCYSRUVDXBXCXDAUUFYTITUURYSXEWOXFAUUDUUEYJPQZUU
        HUUQYIXGWPZYHUGQAUBDIXHUJUVAUVKUVIPUVSUVAUVHPQZUUDUUEUVIPQAUUCUVGNQZUWB
        UUTEUUSXIZBUVGWIXPZUVPUVQUVHYIVSVTWKUVAIUVGJKLZXJLZBCUDZOLZCXKZIUUSXJLZ
        UWICXKYJUVKKLZUUSYHSUVAUWGUWKUWICUVAUWFUUSIXJUVAUUSPQZUUGUWFUUSTUUTUWMA
        UUSXLWHUNUUSJXMXNXBXOUVAJUVHKLYIMLZYJUVIKLZUWJUWLUVAUUGUWBUUDUUEUWNUWOT
        UUGUVAUNUJUWEUVPUVQJUVHYIXQWTUVABCUVGUVOAUUKUUTUUPWMUUTUWCAUWDWHXRUVAUV
        KUVIYJKUVSXBXCUVAUWICDIUUSUVAUWHUWKQZWCZAUWHNQZUWHDSUWITAUUTUWPXSZUWPUW
        RUVAUWHUUSXTWHZGWPUVAUUSNIYESAUUTYAUHYBUWQUUCUWRUWIPQUWQAUUCUWSEWOUWTBU
        WHWIWPYCYFYDAUVTYKYJTUWAYJYGWOXF $.
        $( [15-May-2006] $)
    $}

    geolim2.3 $e |- ( ph -> M e. NN0 ) $.
    geolim2.4 $e |- ( ( ph /\ k e. ( ZZ>= ` M ) ) -> ( F ` k ) = ( A ^ k ) ) $.
    $( The partial sums in the geometric series ` A ^ M + A ^ ( M + 1 ) ` ...
       converge to ` ( ( A ^ M ) / ( 1 - A ) ) ` .  (Contributed by Mario
       Carneiro, 26-Apr-2014.) $)
    geolim2 $p |- ( ph -> seq M ( + , F ) ~~> ( ( A ^ M ) / ( 1 - A ) ) ) $=
      ( caddc cfv cexp co c1 cmin cn0 wcel cc wceq cc0 vn cseq cuz csu cdiv cli
      vj cv eqid cz nn0z syl adantr eluznn0 sylan expcl syl2anc cmpt oveq2 ovex
      wa cdm fvmpt eqtr4d seqfeq adantl geolim seqex breldm nn0uz eqeltrd mpbid
      wbr iserex eqeltrrd isumclim2 cfz isumsplit cvv a1i isumclim cabs clt wne
      0z 1re ltnri fveq2 cr cle nn0ge0 absid sylancr ax-mp syl6eq breq1d mtbiri
      1nn0 necon2ai geoser oveq1d 3eqtr3d ax-1cn necomd subeq0 necon3bid mpbird
      subcl wb divsubdir syl112anc nncan eqtr3d syl3anc isumcl 3eqtr3rd breqtrd
      divcl pncan2 ) AJDEUBZEUCKZBCUHZLMZCUDZBELMZNBOMZUEMZUFAYCCDEYAYAUIZAEPQZ
      EUJQHEUKULZIAYBYAQZVAZBRQZYBPQZYCRQZAYMYKFUMAYIYKYNHYBEUNUOZBYBUPZUQZAJUA
      PBUAUHZLMZURZEUBZXTUFVBZAJCUUADEYJYLYBUUAKZYCYBDKYLYNUUDYCSZYPUAYBYTYCPUU
      AYSYBBLUSUUAUIZBYBLUTVCZULZIVDVEAJUUATUBZUUCQZUUBUUCQAUUINYFUEMZUFVMUUJAB
      UGUUAFGUGUHZPQZUULUUAKZBUULLMZSAUAUULYTUUOPUUAYSUULBLUSUUFBUULLUTVCVFZVGZ
      UUIUUKUFJUUATVHVIULZAUGUUATEPVJHAUUMVAUUNUUORUUPAYMUUMUUORQFBUULUPUOVKVNV
      LZVOVPAUUKNYEOMZYFUEMZOMZUVAYDJMZUVAOMZYGYDAUUKUVCUVAOAPYCCUDTENOMVQMYCCU
      DZYDJMUUKUVCAYCCUUATEYAPVJYHHYNUUEAUUGVFZAYMYNYOFYQUOZUURVRAYCUUKCUUATVSP
      VJTUJQAWEVTUVFUVGUUKVSQANYFUEUTVTUUQWAAUVEUVAYDJABCEFABWBKZNWCVMZBNWDGUVI
      BNBNSZUVINNWCVMNWFWGUVJUVHNNWCUVJUVHNWBKZNBNWBWHNPQZUVKNSZWRUVLNWIQTNWJVM
      UVMWFNWKNWLWMWNWOWPWQWSULZHWTXAXBXAANUUTOMZYFUEMZUVBYGANRQZUUTRQZYFRQZYFT
      WDZUVPUVBSUVQAXCVTAUVQYERQZUVRXCAYMYIUWAFHBEUPUQZNYEXHWMZAUVQYMUVSXCFNBXH
      WMZAUVTNBWDABNUVNXDAYFTNBAUVQYMYFTSNBSXIXCFNBXEWMXFXGZNUUTYFXJXKAUVOYEYFU
      EAUVQUWAUVOYESXCUWBNYEXLWMXAXMAUVARQZYDRQUVDYDSAUVRUVSUVTUWFUWCUWDUWEUUTY
      FXRXNAYCCUUAEYAYHYJUUHYRUUSXOUVAYDXSUQXPXQ $.
      $( [26-Apr-2014] $) $( [6-Jun-2006] $)
  $}

  ${
    $d k A $.  $d k F $.  $d k ph $.
    georeclim.1 $e |- ( ph -> A e. CC ) $.
    georeclim.2 $e |- ( ph -> 1 < ( abs ` A ) ) $.
    georeclim.3 $e |- ( ( ph /\ k e. NN0 ) ->
                        ( F ` k ) = ( ( 1 / A ) ^ k ) ) $.
    $( The limit of a geometric series of reciprocals.  (Contributed by Paul
       Chapman, 28-Dec-2007.)  (Revised by Mario Carneiro, 26-Apr-2014.) $)
    georeclim $p |- ( ph -> seq 0 ( + , F ) ~~> ( A / ( A - 1 ) ) ) $=
      ( cc0 c1 cdiv co cmin cc wcel cabs cfv clt wbr wceq 1re caddc cli wne cle
      cseq 0re lt01 ltleii lenlti mpbi fveq2 abs0 syl6eq breq2d mtbiri necon2ai
      wn syl reccl syl2anc ax-1cn a1i absdiv syl3anc absid mp2an oveq1i absrpcl
      cr wb wa elrp recgt1 sylbi mpbid eqbrtrd geolim divsubdir syl112anc divid
      crp oveq1d eqtrd oveq2d subcl sylancl ltnri subeq0 mpbird recdiv syl22anc
      necon3bid eqtr3d breqtrd ) AUADHUEIIIBJKZLKZJKZBBILKZJKZUBAWOCDABMNZBHUCZ
      WOMNEAIBOPZQRZXAFXCBHBHSZXCIHQRZHIUDRZXEUQHIUFTUGUHZHIUFTUIUJXDXBHIQXDXBH
      OPHBHOUKULUMUNUOUPURZBUSUTAWOOPZIXBJKZIQAXIIOPZXBJKZXJAIMNZWTXAXIXLSXMAVA
      VBZEXHIBVCVDXKIXBJIVINXFXKISTXGIVEVFZVGUMAXCXJIQRZFAXBWANZXCXPVJZAWTXAXQE
      XHBVHUTXQXBVINHXBQRVKXRXBVLXBVMVNURVOVPGVQAIWRBJKZJKZWQWSAXSWPIJAXSBBJKZW
      OLKZWPAWTXMWTXAXSYBSEXNEXHBIBVRVSAYAIWOLAWTXAYAISEXHBVTUTWBWCWDAWRMNZWRHU
      CZWTXAXTWSSAWTXMYCEVABIWEWFAYDBIUCZAXCYEFXCBIBISZXCIIQRITWGYFXBIIQYFXBXKI
      BIOUKXOUMUNUOUPURAWRHBIAWTXMWRHSYFVJEVABIWHWFWLWIEXHWRBWJWKWMWN $.
      $( [26-Apr-2014] $) $( [28-Dec-2007] $)
  $}

  ${
    $d j k N $.
    $( The value of the finite geometric series ` 2 ^ -u 1 + 2 ^ -u 2 + ` ...
       ` + 2 ^ -u N ` .  (Contributed by Mario Carneiro, 17-Mar-2014.) $)
    geo2sum $p |- ( N e. NN -> sum_ k e. ( 1 ... N ) ( 1 / ( 2 ^ k ) ) =
                                 ( 1 - ( 1 / ( 2 ^ N ) ) ) ) $=
      ( vj cn wcel c1 cfz co c2 cexp cdiv csu cmin cc0 cmul a1i cc sylancr wceq
      ax-1cn cv caddc cz 1z nnz wa crp cn0 2nn elfznn adantl nnnn0 nnexpcl nnrp
      syl rpreccl rpcn 3syl oveq2 oveq2d fsumshftm fzfid cr nnrecre ax-mp recni
      elfznn0 expcl fsummulc1 subidi oveq1i sumeq1i wne 2cn 2ne0 elfzelz exprec
      peano2zdi syl3anc expp1 eqtr3d sumeq2i syl6reqr 1re halflt1 ltneii necomi
      eqtri geoser 2halves subaddrii oveq2i oveq1d syl5req eqtr4d nndivre recnd
      subcl 0re halfgt0 divcan1 eqtrd 3eqtrd ) BDEZFBGHZFIAUAZJHZKHZALFFMHZBFMH
      ZGHZFICUAZFUBHZJHZKHZCLZNXJGHZFIKHZXLJHZCLZXROHZFFIBJHZKHZMHZXDXHXOACFFBF
      UCEXDUDPZYEBUEZXDXFXEEZUFZXGUGEZXHUGEXHQEYHXGDEZYIYHIDEZXFUHEZYJUIYHXFDEZ
      YLYGYMXDXFBUJUKXFULUOIXFUMRXGUNUOXGUPXHUQURXFXMSXGXNFKXFXMIJUSUTVAXDYAXQX
      SXROHZCLZXPXDXQXSXRCXDNXJVBXRQEZXDXRYKXRVCEUIIVDVEZVFZPZXDXLXQEZUFYPXLUHE
      ZXSQEYRYTUUAXDXLXJVGZUKXRXLVHRVIXPXQXOCLYOXKXQXOCXINXJGFTVJVKVLXQXOYNCYTX
      RXMJHZXOYNYTIQEZINVMZXMUCEUUCXOSUUDYTVNPUUEYTVOPYTXLXLNXJVPVRIXMVQVSYTYPU
      UAUUCYNSYRUUBXRXLVTRWAWBWHWCXDYAYDXRKHZXROHZYDXDXTUUFXROXDXTFXRBJHZMHZFXR
      MHZKHZUUFXDXRCBYSXRFVMXDFXRXRFYQWDWEWFWGPBULZWIXDUUKUUIXRKHUUFUUJXRUUIKFX
      RXRTYRYRFQEZXRXRUBHFSTFWJVEWKWLXDUUIYDXRKXDUUHYCFMXDUUDUUEBUCEUUHYCSUUDXD
      VNPUUEXDVOPYFIBVQVSUTWMWNWOWMXDYDQEZYPXRNVMZUUGYDSXDUUMYCQEUUNTXDYCXDFVCE
      YBDEZYCVCEWDXDYKBUHEUUPUIUULIBUMRFYBWPRWQFYCWRRYSUUOXDNXRWSYQWTWFPYDXRXAV
      SXBXC $.
      $( [26-Apr-2014] $) $( [17-Mar-2014] $)
  $}

  ${
    $d k n A $.  $d k n R $.
    $( The infinite sum of ` 1 + A ^ 1 + A ^ 2 ` ... is
       ` ( 1 / ( 1 - A ) ) ` . $)
    geoisum $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  sum_ k e. NN0 ( A ^ k ) = ( 1 / ( 1 - A ) ) ) $=
      ( vn cc wcel cabs cfv c1 clt wbr wa cv cexp co cdiv cn0 cc0 cvv a1i ovex
      cmin cmpt nn0uz cz wceq oveq2 eqid fvmpt adantl expcl adantlr simpl simpr
      0z geolim isumclim ) ADEZAFGHIJZKZABLZMNZHHAUANZONZBCPACLZMNZUBZQRPUCQUDE
      USUNSUTPEZUTVFGVAUEUSCUTVEVAPVFVDUTAMUFVFUGAUTMTUHUIZUQVGVADEURAUTUJUKVCR
      EUSHVBOTSUSABVFUQURULUQURUMVHUOUP $.
      $( [26-Apr-2014] $) $( [15-May-2006] $)

    $( The infinite sum of reciprocals
       ` 1 + ( 1 / A ) ^ 1 + ( 1 / A ) ^ 2 ` ... is ` A / ( A - 1 ) ` .
       (Contributed by rpenner, 3-Nov-2007.)  (Revised by Mario Carneiro,
       26-Apr-2014.) $)
    geoisumr $p |- ( ( A e. CC /\ 1 < ( abs ` A ) ) ->
              sum_ k e. NN0 ( ( 1 / A ) ^ k ) = ( A / ( A - 1 ) ) ) $=
      ( vn cc wcel c1 cabs cfv clt wbr cdiv co cexp cn0 cc0 cvv a1i wceq ovex
      cv wa cmin cmpt nn0uz cz 0z oveq2 eqid fvmpt adantl wne cle wn 0re ltleii
      1re lt01 lenlti mpbi fveq2 abs0 syl6eq breq2d mtbiri necon2ai reccl expcl
      sylan2 sylan simpl simpr georeclim isumclim ) ADEZFAGHZIJZUAZFAKLZBTZMLZA
      AFUBLZKLZBCNVRCTZMLZUCZOPNUDOUEEVQUFQVSNEZVSWEHVTRVQCVSWDVTNWEWCVSVRMUGWE
      UHVRVSMSUIUJZVQVRDEZWFVTDEVPVNAOUKWHVPAOAORZVPFOIJZOFULJWJUMOFUNUPUQUOOFU
      NUPURUSWIVOOFIWIVOOGHOAOGUTVAVBVCVDVEAVFVHVRVSVGVIWBPEVQAWAKSQVQABWEVNVPV
      JVNVPVKWGVLVM $.
      $( [26-Apr-2014] $) $( [3-Nov-2007] $)

    $( The infinite sum of ` A ^ 1 + A ^ 2 ` ... is ` ( A / ( 1 - A ) ) ` . $)
    geoisum1 $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  sum_ k e. NN ( A ^ k ) = ( A / ( 1 - A ) ) ) $=
      ( vn cc wcel cabs cfv c1 clt wbr cn cv cexp co cdiv cvv a1i wceq ovex cn0
      wa csu cmin cmpt nnuz cz oveq2 eqid fvmpt adantl simpl nnnn0 expcl syl2an
      simpr 1nn0 cuz elnnuz sylan2br geolim2 isumclim exp1 adantr oveq1d eqtrd
      1z ) ADEZAFGHIJZUAZKABLZMNZBUBAHMNZHAUCNZONZAVMONVIVKVNBCKACLZMNZUDZHPKUE
      HUFEVIVFQVJKEZVJVQGVKRZVICVJVPVKKVQVOVJAMUGVQUHAVJMSUIUJZVIVGVJTEVKDEVRVG
      VHUKZVJULAVJUMUNVNPEVIVLVMOSQVIABVQHWAVGVHUOHTEVIUPQVJHUQGEVIVRVSVJURVTUS
      UTVAVIVLAVMOVGVLARVHAVBVCVDVE $.
      $( [26-Apr-2014] $) $( [1-Nov-2007] $)

    $( The infinite sum of ` A x. ( R ^ 1 ) + A x. ( R ^ 2 ) ` ... is
       ` ( A x. R ) / ( 1 - R ) ` . $)
    geoisum1c $p |- ( ( A e. CC /\ R e. CC /\ ( abs ` R ) < 1 ) ->
          sum_ k e. NN ( A x. ( R ^ k ) ) = ( ( A x. R ) / ( 1 - R ) ) ) $=
      ( vn cc wcel cabs cfv c1 clt wbr cmul co cdiv cn cexp cc0 wceq 1re cli cv
      w3a cmin csu wne simp1 simp2 ax-1cn subcl sylancr simp3 ltnri cr cle lt01
      0re ltleii absid mp2an fveq2 syl5eqr breq1d necon2ai syl subeq0 necon3bid
      mtbii wb wa mpbird divass syl112anc geoisum1 3adant1 oveq2d cmpt nnuz a1i
      cz 1z oveq2 eqid ovex fvmpt adantl cn0 nnnn0 expcl syl2an caddc cseq 1nn0
      cdm cuz elnnuz sylan2br geolim2 seqex breldm isummulc2 3eqtr2rd ) AEFZBEF
      ZBGHZIJKZUBZABLMIBUCMZNMZABXGNMZLMZAOBCUAZPMZCUDZLMOAXLLMCUDXFXBXCXGEFZXG
      QUEZXHXJRXBXCXEUFZXBXCXEUGZXFIEFZXCXNUHXQIBUIUJXFXOIBUEZXFXEXSXBXCXEUKZXE
      IBIBRZIIJKXEISULYAIXDIJYAIIGHZXDIUMFQIUNKYBIRSQIUPSUOUQIURUSIBGUTVAVBVGVC
      VDXFXRXCXOXSVHUHXQXRXCVIXGQIBIBVEVFUJVJABXGVKVLXFXMXIALXCXEXMXIRXBBCVMVNV
      OXFXLACDOBDUAZPMZVPZIOVQIVSFXFVTVRXKOFZXKYEHXLRZXFDXKYDXLOYEYCXKBPWAYEWBB
      XKPWCWDWEZXFXCXKWFFXLEFYFXQXKWGBXKWHWIXFWJYEIWKZBIPMXGNMZTKYITWMFXFBCYEIX
      QXTIWFFXFWLVRXKIWNHFXFYFYGXKWOYHWPWQYIYJTWJYEIWRWSVDXPWTXA $.
      $( [26-Apr-2014] $) $( [2-Nov-2007] $)
  $}

  $( The recurring decimal 0.999..., which is defined as the infinite sum 0.9 +
     0.09 + 0.009 + ... i.e. ` 9 / 10 ^ 1 + 9 / 10 ^ 2 + 9 / 10 ^ 3 + ... ` ,
     is exactly equal to 1, according to ZF set theory.  Interestingly, about
     40% of the people responding to a poll at
     ~ http://forum.physorg.com/index.php?showtopic=13177 disagree. $)
  0.999... $p |- sum_ k e. NN ( 9 / ( 10 ^ k ) ) = 1 $=
    ( cn c9 c10 co cdiv c1 cmul cmin wcel cc0 wceq 10re recni 10pos 9re clt wbr
    cc ax-1cn cv cexp csu wne cn0 nnnn0 expcl sylancr a1i gt0ne0ii nn0z expne0i
    cz syl3anc divrec mp3an1 syl2anc exprec oveq2d eqtr4d sumeq2i cabs rereccli
    syl cfv cle 0re recgt0ii ltleii absidi ax-mp caddc 9pos cr wb 1re ltaddpos2
    mp2an df-10 breqtrri recgt1 eqbrtri geoisum1c mp3an divreci divcan2i subdii
    mpbi mulid1i recidi oveq12i addcomi eqtr4i subaddrii 3eqtrri eqtri redivcli
    subcli mulcani divgt0ii dividi eqtr3i 3eqtri ) BCDAUAZUBEZFEZAUCBCGDFEZXDUB
    EZHEZAUCZCXGHEZGXGIEZFEZGBXFXIAXDBJZXFCGXEFEZHEZXIXNXESJZXEKUDZXFXPLZXNDSJZ
    XDUEJZXQDMNZXDUFZDXDUGUHXNXTDKUDZXDUMJZXRXTXNYBUIZYDXNDMOUJZUIZXNYAYEYCXDUK
    VDZDXDULUNCSJZXQXRXSCPNZCXEUOUPUQXNXHXOCHXNXTYDYEXHXOLYFYHYIDXDURUNUSUTVAYJ
    XGSJXGVBVEZGQRXJXMLYKXGDMYGVCZNZYLXGGQKXGVFRYLXGLKXGVGYMDMOVHVIXGYMVJVKGDQR
    ZXGGQRZGCGVLEZDQKCQRZGYQQRZVMCVNJGVNJYRYSVOPVPCGVQVRWHVSVTDVNJKDQRYOYPVOMOD
    WAVRWHWBCXGAWCWDCDFEZYTFEXMGYTXKYTXLFCDYKYBYGWEDYTHEZDXLHEZLYTXLLUUACUUBCDY
    KYBYGWFUUBDGHEZDXGHEZIEDGIECDGXGYBTYNWGUUCDUUDGIDYBWIDYBYGWJWKDGCYBTYKGCVLE
    YQDGCTYKWLVSWMWNWOWPYTXLDYTCDPMYGWQZNZGXGTYNWRYBYGWSWHWKYTUUFYTUUECDPMVMOWT
    UJXAXBXC $.
    $( [2-Nov-2007] $)


