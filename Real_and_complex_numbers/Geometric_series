$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Geometric series
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d k n A $.  $d k ph $.
    expcnv.1 $e |- ( ph -> A e. CC ) $.
    expcnv.2 $e |- ( ph -> ( abs ` A ) < 1 ) $.
    $( A sequence of powers of a complex number ` A ` with absolute value
       smaller than 1 converges to zero.  (The proof was shortened by Mario
       Carneiro, 26-Apr-2014.) $)
    expcnv $p |- ( ph -> ( n e. NN0 |-> ( A ^ n ) ) ~~> 0 ) $=
      ( cexp co cc0 wbr wceq wa c1 cn wcel a1i cc syl crp cr cle vk cn0 cv cmpt
      cli cvv nnuz cz adantr nn0ex mptex 0cn cfv nnnn0 opreq2 oprex fvmpt simpr
      1z eqid opreq1d sylan9eqr 0exp eqtrd climconst wne cabs cdiv cmin absrpcl
      wi clt sylan elrp reclt1 sylbi mpbid 1re rpreccl rpre difrp sylancr recnd
      idi divcnv nnex adantl nndivre eqeltrd nnz rpexpcl syl2an cmul caddc nnrp
      rpmulcl peano2re lep1 rpge0 bernneq2 syl3anc letrd rpcnne0 exprec breqtrd
      3expa lerec2 syl2anb syl2anc nncn nnne0 jca recdiv2 breqtrrd climsqueeze2
      3brtr4d expcl absexp fveq2d 3eqtr4rd climabs0 biimpar syldan pm2.61dane
      wb ) ACUBBCUCZFGZUDZHUEIZBHABHJZKZHUAYHLUFMUGALUHNZYJYLAUSOZUIYHUFNZYKCUB
      YGUJUKZOHPNZYKULOYKUAUCZMNZKZYQYHUMZHYQFGZHYRYKYTBYQFGZUUAYRYQUBNZYTUUBJZ
      YQUNZCYQYGUUBUBYHYFYQBFUOYHUTBYQFUPUQZQYKBHYQFAYJURVAVBYSYRUUAHJYKYRURYQV
      CQVDVEABHVFZCMBVGUMZYFFGZUDZHUEIZYIAUUGKZHUACMLLUUHVHGZLVIGZVHGZYFVHGZUDZ
      UUJLPUFMUGYLUULUSOYPUULULOUULUUOPNZUUQHUEIUULUURVKUULUUOUULUUORNZUUOSNZUU
      LUUNRNZUUSUULLUUMVLIZUVAUULUUHLVLIZUVBAUVCUUGEUIUULUUHRNZUVCUVBYEZABPNZUU
      GUVDDBVJVMZUVDUUHSNHUUHVLIKUVEUUHVNUUHVOVPQVQUULLSNUUMSNZUVBUVAYEVRUULUUM
      RNZUVHUULUVDUVIUVGUUHVSQZUUMVTQZLUUMWAWBVQZUUNVSQUUOVTQZWCWDUUOCWEQUUJUFN
      ZUULCMUUIWFUKZOUULYRKZYQUUQUMZUUOYQVHGZSYRUVQUVRJUULCYQUUPUVRMUUQYFYQUUOV
      HUOUUQUTUUOYQVHUPUQWGZUULUUTYRUVRSNUVMUUOYQWHVMWIUVPYQUUJUMZRNZUVTSNUVPUV
      TUUHYQFGZRYRUVTUWBJZUULCYQUUIUWBMUUJYFYQUUHFUOUUJUTUUHYQFUPUQZWGZUULUVDYQ
      UHNZUWBRNZYRUVGYQWJZUUHYQWKWLZWIZUVTVTQUVPUWBUVRUVTUVQTUVPUWBLUUNYQWMGZVH
      GZUVRTUVPUWKLUWBVHGZTIZUWBUWLTIZUVPUWKUUMYQFGZUWMTUVPUWKUWKLWNGZUWPUVPUWK
      RNZUWKSNZUULUVAYQRNUWRYRUVLYQWOUUNYQWPWLZUWKVTQZUVPUWSUWQSNUXAUWKWQQUVPUW
      PRNZUWPSNUULUVIUWFUXBYRUVJUWHUUMYQWKWLUWPVTQUVPUWSUWKUWQTIUXAUWKWRQUVPUVH
      UUCHUUMTIZUWQUWPTIUULUVHYRUVKUIYRUUCUULUUEWGUULUXCYRUULUVIUXCUVJUUMWSQUIU
      UMYQWTXAXBUULUUHPNZUUHHVFZKZUWFUWPUWMJZYRUULUVDUXFUVGUUHXCQUWHUXDUXEUWFUX
      GUUHYQXDXFWLXEUVPUWRUWGUWNUWOYEZUWTUWIUWRUWSHUWKVLIKUWBSNHUWBVLIKUXHUWGUW
      KVNUWBVNUWKUWBXGXHXIVQUULUUNPNUUNHVFKZYQPNZYQHVFZKUVRUWLJYRUULUVAUXIUVLUU
      NXCQYRUXJUXKYQXJYQXKXLUUNYQXMWLXNUWEUVSXPUVPUWAHUVTTIUWJUVTWSQXOAYIUUKAUA
      YHUUJLUFUFMUGYMYNAYOOUVNAUVOOAYRKZYTUUBPUXLUUCUUDYRUUCAUUEWGUUFQZUXLUVFUU
      CUUBPNAUVFYRDUIZUXLYRUUCAYRURZUUEQZBYQXQXIWIUXLUUBVGUMZUWBYTVGUMUVTUXLUVF
      UUCUXQUWBJUXNUXPBYQXRXIUXLYTUUBVGUXMXSUXLYRUWCUXOUWDQXTYAYBYCYD $.
      $( [26-Apr-2014] $) $( [8-May-2006] $)
  $}

  ${
    $d k n A $.  $d k ph $.  $d k n F $.  $d k n Z $.  $d k M $.
    explecnv.1 $e |- Z = ( ZZ>= ` M ) $.
    explecnv.2 $e |- ( ph -> F e. V ) $.
    explecnv.3 $e |- ( ph -> M e. ZZ ) $.
    explecnv.5 $e |- ( ph -> A e. RR ) $.
    explecnv.4 $e |- ( ph -> ( abs ` A ) < 1 ) $.
    explecnv.6 $e |- ( ( ph /\ k e. Z ) -> ( F ` k ) e. CC ) $.
    explecnv.7 $e |- ( ( ph /\ k e. Z ) ->
                       ( abs ` ( F ` k ) ) <_ ( A ^ k ) ) $.
    $( A sequence of terms converges to zero when it is less than powers of a
       number ` A ` whose absolute value is smaller than 1.  (Revised by Mario
       Carneiro, 26-Apr-2014.) $)
    explecnv $p |- ( ph -> F ~~> 0 ) $=
      ( vn cc0 wbr cfv cn0 wcel cli cv cabs cmpt cexp co cle cif cc cvv eqid cz
      cuz 0z ifcl sylancr 0cn a1i recnd expcnv fvex eqeltri mptex wa wceq nn0uz
      cr cin ineq12i uzin sylancl syl5req eleq2d biimpa elin sylib simprd oprex
      opreq2 fvmpt syl adantr reexpcl syl2anc eqeltrd simpld fveq2 fveq2d abscl
      syldan 3brtr4d absge0 breqtrrd climsqueeze2 adantl climabs0 mpbird ) ADPU
      AQOGOUBZDRZUCRZUDZPUAQAPCOSBWRUEUFZUDZXAEPUGQZPEUHZUIUJXEUMRZXFUKAPULTZEU
      LTZXEULTUNJXDPEULUOUPPUITAUQURABOABKUSLUTXAUJTAOGWTGEUMRZUJHEUMVAVBVCURZA
      CUBZXFTZVDZXKXCRZBXKUEUFZVGXMXKSTZXNXOVEXMXKGTZXPXMXKGSVHZTZXQXPVDAXLXSAX
      FXRXKAXRXIPUMRZVHZXFGXISXTHVFVIAXHXGYAXFVEJUNEPVJVKVLVMVNXKGSVOVPZVQZOXKX
      BXOSXCWRXKBUEVSXCUKBXKUEVRVTWAZXMBVGTZXPXOVGTAYEXLKWBYCBXKWCWDWEXMXKXARZX
      KDRZUCRZVGXMXQYFYHVEZXMXQXPYBWFZOXKWTYHGXAWRXKVEWSYGUCWRXKDWGWHXAUKYGUCVA
      VTZWAZXMYGUITZYHVGTAXLXQYMYJMWJZYGWIWAWEXMYHXOYFXNUGAXLXQYHXOUGQYJNWJYLYD
      WKXMPYHYFUGXMYMPYHUGQYNYGWLWAYLWMWNACDXAEFUJGHJIXJMXQYIAYKWOWPWQ $.
      $( [26-Apr-2014] $) $( [19-Jul-2008] $)
  $}

  ${
    $d k n x A $.  $d k x N $.  $d k n x ph $.
    geoser.1 $e |- ( ph -> A e. CC ) $.
    geoser.2 $e |- ( ph -> A =/= 1 ) $.
    geoser.3 $e |- ( ph -> N e. NN0 ) $.
    $( The value of the finite geometric series ` 1 + A ^ 1 + A ^ 2 + ` ...
       ` + A ^ ( N - 1 ) ` .  (Revised by Mario Carneiro, 26-Apr-2014.) $)
    geoser $p |- ( ph -> sum_ k e. ( 0 ... ( N - 1 ) ) ( A ^ k ) =
                    ( ( 1 - ( A ^ N ) ) / ( 1 - A ) ) ) $=
      ( wcel cc0 c1 cmin co cfz cexp csu cdiv wceq caddc opreq2d cc vx vn cv wi
      cn0 opreq1 sumeq1d opreq2 opreq1d eqeq12d imbi2d syl ax-1cn subidi syl6eq
      exp0 wne subcl sylancr necomd wb subeq0 necon3bid mpbird syl2anc eqtrd c0
      div0 clt wbr cr 0re ltm1 ax-mp cz 0z peano2zm fzn mp2an mpbi sumeq1i sum0
      eqtri syl6reqr wa nn0cn adantl pncan sylancl cuz cfv simpr nn0uz syl6eleq
      adantr elfznn0 expcl syl2an fsumm1 expp1 sylan nncan eqtr4d subdi syl3anc
      cmul a1i mulid1 3eqtrd subsub divdir syl112anc divcan4 syl5ibr expcom a2d
      mulcl nn0ind mpcom ) DUEHAIDJKLZMLZBCUCZNLZCOZJBDNLZKLZJBKLZPLZQZGAIUAUCZ
      JKLZMLZYCCOZJBYJNLZKLZYGPLZQZUDAIIJKLZMLZYCCOZJBINLZKLZYGPLZQZUDAIUBUCZJK
      LZMLZYCCOZJBUUENLZKLZYGPLZQZUDAIUUEJRLZJKLZMLZYCCOZJBUUMNLZKLZYGPLZQZUDAY
      IUDUAUBDYJIQZYQUUDAUVAYMYTYPUUCUVAYLYSYCCUVAYKYRIMYJIJKUFSUGUVAYOUUBYGPUV
      AYNUUAJKYJIBNUHSUIUJUKYJUUEQZYQUULAUVBYMUUHYPUUKUVBYLUUGYCCUVBYKUUFIMYJUU
      EJKUFSUGUVBYOUUJYGPUVBYNUUIJKYJUUEBNUHSUIUJUKYJUUMQZYQUUTAUVCYMUUPYPUUSUV
      CYLUUOYCCUVCYKUUNIMYJUUMJKUFSUGUVCYOUURYGPUVCYNUUQJKYJUUMBNUHSUIUJUKYJDQZ
      YQYIAUVDYMYDYPYHUVDYLYAYCCUVDYKXTIMYJDJKUFSUGUVDYOYFYGPUVDYNYEJKYJDBNUHSU
      IUJUKAUUCIYTAUUCIYGPLZIAUUBIYGPAUUBJJKLIAUUAJJKABTHZUUAJQEBUPULSJUMUNUOUI
      AYGTHZYGIUQZUVEIQAJTHZUVFUVGUMEJBURUSZAUVHJBUQABJFUTAYGIJBAUVIUVFYGIQJBQV
      AUMEJBVBUSVCVDZYGVHVEVFYTVGYCCOIYSVGYCCYRIVIVJZYSVGQZIVKHUVLVLIVMVNIVOHZY
      RVOHZUVLUVMVAVPUVNUVOVPIVQVNIYRVRVSVTWAYCCWBWCWDUUEUEHZAUULUUTAUVPUULUUTU
      DUULUUTAUVPWEZUUHUUIRLZUUKUUIRLZQUUHUUKUUIRUFUVQUUPUVRUUSUVSUVQUUPIUUEMLZ
      YCCOUVRUVQUUOUVTYCCUVQUUNUUEIMUVQUUETHZUVIUUNUUEQUVPUWAAUUEWFWGUMUUEJWHWI
      SUGUVQYCUUICIUUEUVQUUEUEIWJWKAUVPWLWMWNUVQUVFYBUEHYCTHYBUVTHAUVFUVPEWOZYB
      UUEWPBYBWQWRYBUUEBNUHWSVFUVQUUSUUJUUIYGXFLZRLZYGPLZUUKUWCYGPLZRLZUVSUVQUU
      RUWDYGPUVQUURJUUIUWCKLZKLZUWDUVQUUQUWHJKUVQUUQUUIJYGKLZXFLZUUIJXFLZUWCKLZ
      UWHUVQUUQUUIBXFLZUWKAUVFUVPUUQUWNQEBUUEWTXAUVQUWJBUUIXFUVQUVIUVFUWJBQUMUW
      BJBXBUSSXCUVQUUITHZUVIUVGUWKUWMQAUVFUVPUWOEBUUEWQXAZUVIUVQUMXGZAUVGUVPUVJ
      WOZUUIJYGXDXEUVQUWLUUIUWCKUVQUWOUWLUUIQUWPUUIXHULUIXISUVQUVIUWOUWCTHZUWIU
      WDQUWQUWPUVQUWOUVGUWSUWPUWRUUIYGXQVEZJUUIUWCXJXEVFUIUVQUUJTHZUWSUVGUVHUWE
      UWGQUVQUVIUWOUXAUMUWPJUUIURUSUWTUWRAUVHUVPUVKWOZUUJUWCYGXKXLUVQUWFUUIUUKR
      UVQUWOUVGUVHUWFUUIQUWPUWRUXBUUIYGXMXESXIUJXNXOXPXRXS $.
      $( [26-Apr-2014] $) $( [12-May-2006] $)
  $}

  ${
    $d j k n A $.  $d j k F $.  $d j k M $.  $d j k ph $.
    geolim.1 $e |- ( ph -> A e. CC ) $.
    geolim.2 $e |- ( ph -> ( abs ` A ) < 1 ) $.
    ${
      geolim.3 $e |- ( ( ph /\ k e. NN0 ) -> ( F ` k ) = ( A ^ k ) ) $.
      $( The partial sums in the infinite series ` 1 + A ^ 1 + A ^ 2 ` ...
         converge to ` ( 1 / ( 1 - A ) ) ` . $)
      geolim $p |- ( ph -> seq 0 ( + , F ) ~~> ( 1 / ( 1 - A ) ) ) $=
        ( vn cc0 c1 cmin co cdiv cn0 cexp cc wcel cmul cfv wceq vj caddc cli cv
        cseq cmpt cvv nn0uz cz a1i 0cn expcnv wne ax-1cn subcl sylancr cabs clt
        0z wbr 1re ltnri fveq2 1nn0 cle nn0ge0 absid ax-mp syl6eq breq1d mtbiri
        cr necon2ai necomd wb subeq0 necon3bid mpbird divcl syl3anc nn0ex mptex
        syl wa opreq2 eqid oprex fvmpt adantl expcl sylan eqeltrd adantr mulcom
        expp1 syl2anc eqtrd opreq1d syl112anc opreq2d 3eqtr4d climmulc2 breqtrd
        div23 opreq1 mul01 reccl seqex peano2nn0 syl2an cfz nn0cn pncan sylancl
        csu sumeq1d divsubdir geoser simpll elfznn0 cuz simpr syl6eleq 3eqtr3rd
        fsumser climsubc2 subid1 ) AUBDIUEZJJBKLZMLZIKLZYJUCAIYJUAHNBHUDZJUBLZO
        LZYIMLZUFZYHIPUGNUHIUIQAUSUJZIPQAUKUJZAYPBYIMLZIRLZIUCAIYSUAHNBYLOLZUFZ
        YPIPUGNUHYQYRABHEFULABPQZYIPQZYIIUMZYSPQZEAJPQZUUCUUDUNEJBUOUPZAUUEJBUM
        ABJABUQSZJURUTZBJUMZFUUJBJBJTZUUJJJURUTJVAVBUULUUIJJURUULUUIJUQSZJBJUQV
        CJNQZUUMJTZVDUUNJVLQIJVEUTUUOVAJVFJVGUPVHVIVJVKVMWCZVNAYIIJBAUUGUUCYIIT
        JBTVOUNEJBVPUPVQVRZBYIVSVTZYPUGQAHNYOWAWBUJAUAUDZNQZWDZUUSUUBSZBUUSOLZP
        UUTUVBUVCTAHUUSUUAUVCNUUBYLUUSBOWEUUBWFBUUSOWGWHWIZAUUCUUTUVCPQZEBUUSWJ
        WKZWLUVABUUSJUBLZOLZYIMLZYSUVCRLZUUSYPSZYSUVBRLUVAUVIBUVCRLZYIMLZUVJUVA
        UVHUVLYIMUVAUVHUVCBRLZUVLAUUCUUTUVHUVNTEBUUSWOWKUVAUVEUUCUVNUVLTUVFAUUC
        UUTEWMZUVCBWNWPWQWRUVAUUCUVEUUDUUEUVMUVJTUVOUVFAUUDUUTUUHWMZAUUEUUTUUQW
        MZBUVCYIXDWSWQUUTUVKUVITAHUUSYOUVINYPYLUUSTZYNUVHYIMUVRYMUVGBOYLUUSJUBX
        EWTWRYPWFUVHYIMWGWHWIZUVAUVBUVCYSRUVDWTXAXBAUUFYTITUURYSXFWCXCAUUDUUEYJ
        PQZUUHUUQYIXGWPZYHUGQAUBDIXHUJUVAUVKUVIPUVSUVAUVHPQZUUDUUEUVIPQAUUCUVGN
        QZUWBUUTEUUSXIZBUVGWJXJZUVPUVQUVHYIVSVTWLUVAIUVGJKLZXKLZBCUDZOLZCXOZIUU
        SXKLZUWICXOYJUVKKLZUUSYHSUVAUWGUWKUWICUVAUWFUUSIXKUVAUUSPQZUUGUWFUUSTUU
        TUWMAUUSXLWIUNUUSJXMXNWTXPUVAJUVHKLYIMLZYJUVIKLZUWJUWLUVAUUGUWBUUDUUEUW
        NUWOTUUGUVAUNUJUWEUVPUVQJUVHYIXQWSUVABCUVGUVOAUUKUUTUUPWMUUTUWCAUWDWIXR
        UVAUVKUVIYJKUVSWTXAUVAUWICDIUUSUVAUWHUWKQZWDZAUWHNQZUWHDSUWITAUUTUWPXSZ
        UWPUWRUVAUWHUUSXTWIZGWPUVAUUSNIYASAUUTYBUHYCUWQUUCUWRUWIPQUWQAUUCUWSEWC
        UWTBUWHWJWPYEYDYFAUVTYKYJTUWAYJYGWCXC $.
        $( [15-May-2006] $)
    $}

    geolim2.3 $e |- ( ph -> M e. NN0 ) $.
    geolim2.4 $e |- ( ( ph /\ k e. ( ZZ>= ` M ) ) -> ( F ` k ) = ( A ^ k ) ) $.
    $( The partial sums in the geometric series ` A ^ M + A ^ ( M + 1 ) ` ...
       converge to ` ( ( A ^ M ) / ( 1 - A ) ) ` .  (Contributed by Mario
       Carneiro, 26-Apr-2014.) $)
    geolim2 $p |- ( ph -> seq M ( + , F ) ~~> ( ( A ^ M ) / ( 1 - A ) ) ) $=
      ( caddc cfv cexp co c1 cmin cn0 wcel cc wceq cc0 vn cseq cuz csu cdiv cli
      vj cv eqid cz nn0z syl adantr eluznn0 sylan expcl syl2anc cmpt cdm opreq2
      wa oprex fvmpt eqtr4d seqfeq wbr adantl geolim seqex breldm nn0uz eqeltrd
      iserex mpbid eqeltrrd isumclim2 cfz isumsplit cvv 0z a1i isumclim clt wne
      cabs 1re ltnri fveq2 1nn0 cr cle nn0ge0 absid sylancr ax-mp syl6eq breq1d
      mtbiri geoser opreq1d 3eqtr3d ax-1cn subcl necomd subeq0 necon3bid mpbird
      necon2ai wb divsubdir syl112anc nncan eqtr3d divcl isumcl pncan2 3eqtr3rd
      syl3anc breqtrd ) AJDEUBZEUCKZBCUHZLMZCUDZBELMZNBOMZUEMZUFAYCCDEYAYAUIZAE
      PQZEUJQHEUKULZIAYBYAQZVAZBRQZYBPQZYCRQZAYMYKFUMAYIYKYNHYBEUNUOZBYBUPZUQZA
      JUAPBUAUHZLMZURZEUBZXTUFUSZAJCUUADEYJYLYBUUAKZYCYBDKYLYNUUDYCSZYPUAYBYTYC
      PUUAYSYBBLUTUUAUIZBYBLVBVCZULZIVDVEAJUUATUBZUUCQZUUBUUCQAUUINYFUEMZUFVFUU
      JABUGUUAFGUGUHZPQZUULUUAKZBUULLMZSAUAUULYTUUOPUUAYSUULBLUTUUFBUULLVBVCVGZ
      VHZUUIUUKUFJUUATVIVJULZAUGUUATEPVKHAUUMVAUUNUUORUUPAYMUUMUUORQFBUULUPUOVL
      VMVNZVOVPAUUKNYEOMZYFUEMZOMZUVAYDJMZUVAOMZYGYDAUUKUVCUVAOAPYCCUDTENOMVQMY
      CCUDZYDJMUUKUVCAYCCUUATEYAPVKYHHYNUUEAUUGVGZAYMYNYOFYQUOZUURVRAYCUUKCUUAT
      VSPVKTUJQAVTWAUVFUVGUUKVSQANYFUEVBWAUUQWBAUVEUVAYDJABCEFABWEKZNWCVFZBNWDG
      UVIBNBNSZUVINNWCVFNWFWGUVJUVHNNWCUVJUVHNWEKZNBNWEWHNPQZUVKNSZWIUVLNWJQTNW
      KVFUVMWFNWLNWMWNWOWPWQWRXHULZHWSWTXAWTANUUTOMZYFUEMZUVBYGANRQZUUTRQZYFRQZ
      YFTWDZUVPUVBSUVQAXBWAAUVQYERQZUVRXBAYMYIUWAFHBEUPUQZNYEXCWNZAUVQYMUVSXBFN
      BXCWNZAUVTNBWDABNUVNXDAYFTNBAUVQYMYFTSNBSXIXBFNBXEWNXFXGZNUUTYFXJXKAUVOYE
      YFUEAUVQUWAUVOYESXBUWBNYEXLWNWTXMAUVARQZYDRQUVDYDSAUVRUVSUVTUWFUWCUWDUWEU
      UTYFXNXRAYCCUUAEYAYHYJUUHYRUUSXOUVAYDXPUQXQXS $.
      $( [26-Apr-2014] $) $( [6-Jun-2006] $)
  $}

  ${
    $d k A $.  $d k F $.  $d k ph $.
    georeclim.1 $e |- ( ph -> A e. CC ) $.
    georeclim.2 $e |- ( ph -> 1 < ( abs ` A ) ) $.
    georeclim.3 $e |- ( ( ph /\ k e. NN0 ) ->
                        ( F ` k ) = ( ( 1 / A ) ^ k ) ) $.
    $( The limit of a geometric series of reciprocals.  (Contributed by Paul
       Chapman, 28-Dec-2007.)  (Revised by Mario Carneiro, 26-Apr-2014.) $)
    georeclim $p |- ( ph -> seq 0 ( + , F ) ~~> ( A / ( A - 1 ) ) ) $=
      ( cc0 c1 cdiv co cmin cc wcel cabs cfv clt wbr wceq 1re caddc cli wne cle
      cseq 0re lt01 ltleii lenlti mpbi fveq2 abs0 syl6eq breq2d mtbiri necon2ai
      wn syl reccl syl2anc ax-1cn a1i absdiv syl3anc cr absid mp2an opreq1i crp
      wb absrpcl wa recgt1 sylbi mpbid eqbrtrd geolim divsubdir syl112anc divid
      elrp opreq1d eqtrd opreq2d subcl sylancl subeq0 necon3bid mpbird syl22anc
      ltnri recdiv eqtr3d breqtrd ) AUADHUEIIIBJKZLKZJKZBBILKZJKZUBAWOCDABMNZBH
      UCZWOMNEAIBOPZQRZXAFXCBHBHSZXCIHQRZHIUDRZXEUQHIUFTUGUHZHIUFTUIUJXDXBHIQXD
      XBHOPHBHOUKULUMUNUOUPURZBUSUTAWOOPZIXBJKZIQAXIIOPZXBJKZXJAIMNZWTXAXIXLSXM
      AVAVBZEXHIBVCVDXKIXBJIVENXFXKISTXGIVFVGZVHUMAXCXJIQRZFAXBVINZXCXPVJZAWTXA
      XQEXHBVKUTXQXBVENHXBQRVLXRXBWAXBVMVNURVOVPGVQAIWRBJKZJKZWQWSAXSWPIJAXSBBJ
      KZWOLKZWPAWTXMWTXAXSYBSEXNEXHBIBVRVSAYAIWOLAWTXAYAISEXHBVTUTWBWCWDAWRMNZW
      RHUCZWTXAXTWSSAWTXMYCEVABIWEWFAYDBIUCZAXCYEFXCBIBISZXCIIQRITWKYFXBIIQYFXB
      XKIBIOUKXOUMUNUOUPURAWRHBIAWTXMWRHSYFVJEXNBIWGUTWHWIEXHWRBWLWJWMWN $.
      $( [26-Apr-2014] $) $( [28-Dec-2007] $)
  $}

  ${
    $d j k N $.
    $( The value of the finite geometric series ` 2 ^ -u 1 + 2 ^ -u 2 + ` ...
       ` + 2 ^ -u N ` .  (Contributed by Mario Carneiro, 17-Mar-2014.) $)
    geo2sum $p |- ( N e. NN -> sum_ k e. ( 1 ... N ) ( 1 / ( 2 ^ k ) ) =
                                 ( 1 - ( 1 / ( 2 ^ N ) ) ) ) $=
      ( vj cn wcel c1 cfz co c2 cexp cdiv csu cmin cc0 cmul cz a1i sylancr wceq
      cc cv caddc 1z nnz wa crp cn0 2nn elfznn adantl nnnn0 syl nnexpcl rpreccl
      nnrp rpcn 3syl opreq2 opreq2d fsumshftm fzfid nnrecre ax-mp recni elfznn0
      cr expcl fsummulc1 ax-1cn subidi opreq1i sumeq1i wne 2ne0 elfzelz peano2z
      2cn exprec syl3anc expp1 eqtr3d sumeq2i eqtri syl6reqr 1re halflt1 ltneii
      necomi geoser 2halves pncan eqtr3i opreq2i opreq1d syl5req eqtr4d nndivre
      mp2an recnd subcl 0re halfgt0 divcan1 eqtrd 3eqtrd ) BDEZFBGHZFIAUAZJHZKH
      ZALFFMHZBFMHZGHZFICUAZFUBHZJHZKHZCLZNXLGHZFIKHZXNJHZCLZXTOHZFFIBJHZKHZMHZ
      XFXJXQACFFBFPEXFUCQZYGBUDZXFXHXGEZUEZXIUFEZXJUFEXJTEYJXIDEZYKYJIDEZXHUGEZ
      YLUHYJXHDEZYNYIYOXFXHBUIUJXHUKULIXHUMRXIUOULXIUNXJUPUQXHXOSXIXPFKXHXOIJUR
      USUTXFYCXSYAXTOHZCLZXRXFXSYAXTCXFNXLVAXTTEZXFXTYMXTVFEUHIVBVCZVDZQZXFXNXS
      EZUEYRXNUGEZYATEYTUUBUUCXFXNXLVEZUJXTXNVGRVHXRXSXQCLYQXMXSXQCXKNXLGFVIVJV
      KVLXSXQYPCUUBXTXOJHZXQYPUUBITEZINVMZXOPEZUUEXQSUUFUUBVQQUUGUUBVNQUUBXNPEU
      UHXNNXLVOXNVPULIXOVRVSUUBYRUUCUUEYPSYTUUDXTXNVTRWAWBWCWDXFYCYFXTKHZXTOHZY
      FXFYBUUIXTOXFYBFXTBJHZMHZFXTMHZKHZUUIXFXTCBUUAXTFVMXFFXTXTFYSWEWFWGWHQBUK
      ZWIXFUUNUULXTKHUUIUUMXTUULKXTXTUBHZXTMHZUUMXTUUPFXTMFTEZUUPFSVIFWJVCVKYRY
      RUUQXTSYTYTXTXTWKWRWLWMXFUULYFXTKXFUUKYEFMXFUUFUUGBPEUUKYESUUFXFVQQUUGXFV
      NQYHIBVRVSUSWNWOWPWNXFYFTEZYRXTNVMZUUJYFSXFUURYETEUUSVIXFYEXFFVFEYDDEZYEV
      FEWEXFYMBUGEUVAUHUUOIBUMRFYDWQRWSFYEWTRUUAUUTXFNXTXAYSXBWGQYFXTXCVSXDXE
      $.
      $( [26-Apr-2014] $) $( [17-Mar-2014] $)
  $}

  ${
    $d k n A $.  $d k n R $.
    $( The infinite sum of ` 1 + A ^ 1 + A ^ 2 ` ... is
       ` ( 1 / ( 1 - A ) ) ` . $)
    geoisum $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  sum_ k e. NN0 ( A ^ k ) = ( 1 / ( 1 - A ) ) ) $=
      ( vn cc wcel cabs cfv c1 clt wbr wa cv cexp co cdiv cn0 cc0 cvv a1i oprex
      cmin cmpt nn0uz cz 0z wceq opreq2 fvmpt adantl expcl adantlr simpl geolim
      eqid simpr isumclim ) ADEZAFGHIJZKZABLZMNZHHAUANZONZBCPACLZMNZUBZQRPUCQUD
      EUSUESUTPEZUTVFGVAUFUSCUTVEVAPVFVDUTAMUGVFUNAUTMTUHUIZUQVGVADEURAUTUJUKVC
      REUSHVBOTSUSABVFUQURULUQURUOVHUMUP $.
      $( [26-Apr-2014] $) $( [15-May-2006] $)

    $( The infinite sum of reciprocals
       ` 1 + ( 1 / A ) ^ 1 + ( 1 / A ) ^ 2 ` ... is ` A / ( A - 1 ) ` .
       (Contributed by rpenner, 3-Nov-2007.)  (Revised by Mario Carneiro,
       26-Apr-2014.) $)
    geoisumr $p |- ( ( A e. CC /\ 1 < ( abs ` A ) ) ->
              sum_ k e. NN0 ( ( 1 / A ) ^ k ) = ( A / ( A - 1 ) ) ) $=
      ( vn cc wcel c1 cabs cfv clt wbr cdiv co cexp cn0 cc0 cvv a1i wceq oprex
      cv wa cmin cmpt nn0uz cz 0z opreq2 eqid fvmpt adantl wne cle 0re 1re lt01
      ltleii lenlti mpbi fveq2 syl6eq breq2d mtbiri necon2ai reccl sylan2 expcl
      wn abs0 sylan simpl simpr georeclim isumclim ) ADEZFAGHZIJZUAZFAKLZBTZMLZ
      AAFUBLZKLZBCNVRCTZMLZUCZOPNUDOUEEVQUFQVSNEZVSWEHVTRVQCVSWDVTNWEWCVSVRMUGW
      EUHVRVSMSUIUJZVQVRDEZWFVTDEVPVNAOUKWHVPAOAORZVPFOIJZOFULJWJVGOFUMUNUOUPOF
      UMUNUQURWIVOOFIWIVOOGHOAOGUSVHUTVAVBVCAVDVEVRVSVFVIWBPEVQAWAKSQVQABWEVNVP
      VJVNVPVKWGVLVM $.
      $( [26-Apr-2014] $) $( [3-Nov-2007] $)

    $( The infinite sum of ` A ^ 1 + A ^ 2 ` ... is ` ( A / ( 1 - A ) ) ` . $)
    geoisum1 $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  sum_ k e. NN ( A ^ k ) = ( A / ( 1 - A ) ) ) $=
      ( vn cc wcel cabs cfv c1 clt wbr cn cv cexp cdiv cvv a1i wceq oprex cn0
      co wa csu cmin cmpt nnuz cz 1z opreq2 eqid fvmpt adantl simpl nnnn0 expcl
      syl2an simpr 1nn0 cuz elnnuz sylan2br geolim2 isumclim exp1 opreq1d eqtrd
      adantr ) ADEZAFGHIJZUAZKABLZMTZBUBAHMTZHAUCTZNTZAVMNTVIVKVNBCKACLZMTZUDZH
      OKUEHUFEVIUGPVJKEZVJVQGVKQZVICVJVPVKKVQVOVJAMUHVQUIAVJMRUJUKZVIVGVJSEVKDE
      VRVGVHULZVJUMAVJUNUOVNOEVIVLVMNRPVIABVQHWAVGVHUPHSEVIUQPVJHURGEVIVRVSVJUS
      VTUTVAVBVIVLAVMNVGVLAQVHAVCVFVDVE $.
      $( [26-Apr-2014] $) $( [1-Nov-2007] $)

    $( The infinite sum of ` A x. ( R ^ 1 ) + A x. ( R ^ 2 ) ` ... is
       ` ( A x. R ) / ( 1 - R ) ` . $)
    geoisum1c $p |- ( ( A e. CC /\ R e. CC /\ ( abs ` R ) < 1 ) ->
          sum_ k e. NN ( A x. ( R ^ k ) ) = ( ( A x. R ) / ( 1 - R ) ) ) $=
      ( vn cc wcel cabs cfv c1 clt wbr cn cexp co cmul cdiv wceq cli cc0 1re cv
      w3a csu cmin cmpt nnuz cz 1z a1i opreq2 eqid oprex fvmpt adantl cn0 simp2
      nnnn0 expcl syl2an caddc cseq cdm simp3 cuz elnnuz sylan2br geolim2 seqex
      1nn0 breldm syl simp1 isummulc2 geoisum1 3adant1 opreq2d wne ax-1cn subcl
      sylancr ltnri cr cle 0re ltleii absid mp2an fveq2 syl5eqr breq1d necon2ai
      lt01 mtbii wb wa subeq0 necon3bid mpbird divass syl112anc eqtr4d eqtr3d )
      AEFZBEFZBGHZIJKZUBZALBCUAZMNZCUCZONZLAXIONCUCABONIBUDNZPNZXGXIACDLBDUAZMN
      ZUEZILUFIUGFXGUHUIXHLFZXHXPHXIQZXGDXHXOXILXPXNXHBMUJXPUKBXHMULUMUNZXGXDXH
      UOFXIEFXQXCXDXFUPZXHUQBXHURUSXGUTXPIVAZBIMNXLPNZRKYARVBFXGBCXPIXTXCXDXFVC
      ZIUOFXGVIUIXHIVDHFXGXQXRXHVEXSVFVGYAYBRUTXPIVHVJVKXCXDXFVLZVMXGXKABXLPNZO
      NZXMXGXJYEAOXDXFXJYEQXCBCVNVOVPXGXCXDXLEFZXLSVQZXMYFQYDXTXGIEFZXDYGVRXTIB
      VSVTXGYHIBVQZXGXFYJYCXFIBIBQZIIJKXFITWAYKIXEIJYKIIGHZXEIWBFSIWCKYLIQTSIWD
      TWLWEIWFWGIBGWHWIWJWMWKVKXGYIXDYHYJWNVRXTYIXDWOXLSIBIBWPWQVTWRABXLWSWTXAX
      B $.
      $( [26-Apr-2014] $) $( [2-Nov-2007] $)
  $}

  $( The recurring decimal 0.999..., which is defined as the infinite sum 0.9 +
     0.09 + 0.009 + ... i.e. ` 9 / 10 ^ 1 + 9 / 10 ^ 2 + 9 / 10 ^ 3 + ... ` ,
     is exactly equal to 1, according to ZF set theory.  Interestingly, about
     40% of the people responding to a poll at
     ~ http://forum.physorg.com/index.php?showtopic=13177 disagree. $)
  0.999... $p |- sum_ k e. NN ( 9 / ( 10 ^ k ) ) = 1 $=
    ( cn c9 c10 co cdiv c1 cmul cmin wcel cc0 wceq 10re recni 10pos 9re clt wbr
    cc ax-1cn cv cexp csu wne cn0 nnnn0 expcl sylancr a1i gt0ne0ii nn0z expne0i
    cz syl syl3anc divrec mp3an1 syl2anc exprec opreq2d eqtr4d sumeq2i cabs cfv
    rereccli cle 0re recgt0ii ltleii absidi ax-mp caddc 9pos cr ltaddpos2 mp2an
    wb 1re mpbi df-10 breqtrri recgt1 eqbrtri geoisum1c divreci divcan2i subdii
    mp3an mulid1i recidi opreq12i addcomi eqtr4i subaddrii 3eqtrri eqtri subcli
    redivcli mulcani divgt0ii dividi eqtr3i 3eqtri ) BCDAUAZUBEZFEZAUCBCGDFEZXD
    UBEZHEZAUCZCXGHEZGXGIEZFEZGBXFXIAXDBJZXFCGXEFEZHEZXIXNXESJZXEKUDZXFXPLZXNDS
    JZXDUEJZXQDMNZXDUFZDXDUGUHXNXTDKUDZXDUMJZXRXTXNYBUIZYDXNDMOUJZUIZXNYAYEYCXD
    UKUNZDXDULUOCSJZXQXRXSCPNZCXEUPUQURXNXHXOCHXNXTYDYEXHXOLYFYHYIDXDUSUOUTVAVB
    YJXGSJXGVCVDZGQRXJXMLYKXGDMYGVEZNZYLXGGQKXGVFRYLXGLKXGVGYMDMOVHVIXGYMVJVKGD
    QRZXGGQRZGCGVLEZDQKCQRZGYQQRZVMCVNJGVNJYRYSVQPVRCGVOVPVSVTWADVNJKDQRYOYPVQM
    ODWBVPVSWCCXGAWDWHCDFEZYTFEXMGYTXKYTXLFCDYKYBYGWEDYTHEZDXLHEZLYTXLLUUACUUBC
    DYKYBYGWFUUBDGHEZDXGHEZIEDGIECDGXGYBTYNWGUUCDUUDGIDYBWIDYBYGWJWKDGCYBTYKGCV
    LEYQDGCTYKWLVTWMWNWOWPYTXLDYTCDPMYGWRZNZGXGTYNWQYBYGWSVSWKYTUUFYTUUECDPMVMO
    WTUJXAXBXC $.
    $( [2-Nov-2007] $)


