$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Geometric series
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x ph $.  $d x y A $.
    expcnvlem1.1 $e |- A e. RR $.
    expcnvlem1.2 $e |- ( y e. NN -> ( A < y -> ph ) ) $.
    $( Lemma for ~ expcnv .  Convert an antecedent from a comparison with a
       real into comparison with a natural number. $)
    expcnvlem1 $p |- E. x e. NN A. y e. NN ( x <_ y -> ph ) $=
      ( cfv c1 caddc co cn wcel cv cle wbr wi cr ax-mp clt 1re cfl cabs wral cz
      wrex flcl nn0abscl nn0p1nn nnre flltp1 reflcl leabsi recni abscli leadd1i
      cn0 mpbi readdcli ltletri mp2an wa ltletr mp3an12 syl imim1d ralimia mprg
      mpani wceq breq1 imbi1d ralbidv rcla4ev ) DUAGZUBGZHIJZKLZVPCMZNOZAPZCKUC
      ZBMZVRNOZAPZCKUCZBKUEVOUPLZVQVNUDLZWFDQLZWGEDUFRVNUGRVOUHRDVRSOZAPZWACKWJ
      VTCKVRKLZVSWIAWKVRQLZVSWIPVRUIWLDVPSOZVSWIDVNHIJZSOZWNVPNOZWMWHWOEDUJRVNV
      ONOWPVNWHVNQLEDUKRZULVNVOHWQVNVNWQUMUNZTUOUQDWNVPEVNHWQTURVOHWRTURZUSUTWH
      VPQLWLWMVSVAWIPEWSDVPVRVBVCVHVDVEVFFVGWEWABVPKWBVPVIZWDVTCKWTWCVSAWBVPVRN
      VJVKVLVMUT $.
      $( [27-Feb-2005] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    expcnvlem.1 $e |- ( A e. RR /\ 0 < A /\ A < 1 ) $.
    expcnvlem.2 $e |- ( B e. RR /\ 0 < B ) $.
    ${
      expcnvlem.3 $e |- C e. NN $.
      $( Lemma for ~ expcnv .  Compute an upper bound for exponentiation using
         Bernoulli's inequality ~ bernneq . $)
      expcnvlem2 $p |- ( ( ( ( 1 / B ) - 1 ) / ( ( 1 / A ) - 1 ) ) < C
                      -> ( A ^ C ) < B ) $=
        ( c1 cdiv co clt wbr cr wcel wb cc0 gt0ne0ii rereccli 1re mp3an cle w3a
        cmin cmul caddc cexp simpli simpri resubcli simp1i simp2i simp3i reclt1
        nnrei mp2an mpbi posdifi ltdivmulOLD remulcli ltsubaddi readdcli nngt0i
        mpan2 mulgt0ii addgt0ii ltdiv23ii 3bitri cn0 nnnn0i 0re recgt0ii ltleii
        lt01 bernneq2 wne wceq recni exprec breqtri reexpcl expgt0 lerec2 mp4an
        cc lelttri mpan sylbi ) GBHIZGUBIZGAHIZGUBIZHICJKZGWJCUCIZGUDIZHIZBJKZA
        CUEIZBJKZWKWHWLJKZWGWMJKWOWHLMZWJLMZCLMZWKWRNZWGGBBLMZOBJKZEUFZBXEXCXDE
        UGZPQZRUHWIGAALMZOAJKZAGJKZDUIZAXKXHXIXJDUJZPZQZRUHZCFUMZWSWTXAUAOWJJKZ
        XBGWIJKZXQXJXRXHXIXJDUKXHXIXJXRNXKXLAULUNUOGWIRXNUPUOZWHWJCUQVBSWGGWLXG
        RWJCXOXPURZUSGBWMRXEWLGXTRUTZXFWLGXTRWJCXOXPXSCFVAVCVLVDZVEVFWPWNTKZWOW
        QWMGWPHIZTKZYCWMWICUEIZYDTWILMCVGMZOWITKWMYFTKXNCFVHZOWIVIXNAXKXLVJVKWI
        CVMSAWCMAOVNYGYFYDVOAXKVPXMYHACVQSVRWMLMOWMJKWPLMZOWPJKZYEYCNYAYBXHYGYI
        XKYHACVSUNZXHYGXIYJXKYHXLACVTSWMWPWAWBUOWPWNBYKWMYAWMYAYBPQXEWDWEWF $.
        $( [28-Jul-2006] $) $( [24-Feb-2005] $)
    $}

    $( Lemma for ~ expcnv .  Apply weak deduction theorem. $)
    expcnvlem3 $p |- ( C e. NN -> ( ( ( ( 1 / B ) - 1 ) /
                       ( ( 1 / A ) - 1 ) ) < C -> ( A ^ C ) < B ) ) $=
      ( cn wcel c1 cdiv co cmin clt wbr cexp wi cif wceq breq2 opreq2 breq1d
      imbi12d 1nn elimel expcnvlem2 dedth ) CFGZHBIJHKJHAIJHKJIJZCLMZACNJZBLMZO
      UGUFCHPZLMZAUKNJZBLMZOCHCUKQZUHULUJUNCUKUGLRUOUIUMBLCUKANSTUAABUKDECHFUBU
      CUDUE $.
      $( [27-Feb-2005] $)

    $( Lemma for ~ expcnv .  Combine ~ expcnvlem1 and ~ expcnvlem3 . $)
    expcnvlem4 $p |- E. x e. NN A. y e. NN ( x <_ y -> ( A ^ y ) < B ) $=
      ( co clt wbr c1 cdiv cmin cr wcel cc0 gt0ne0ii rereccli 1re resubcli mpbi
      cv simpli simpri simp1i simp2i simp3i wb reclt1 mp2an redivcli expcnvlem3
      cexp posdifi expcnvlem1 ) CBUAZULGDHIABJDKGZJLGZJCKGZJLGZKGUQUSUPJDDMNZOD
      HIZFUBZDVBUTVAFUCPQRSURJCCMNZOCHIZCJHIZEUDZCVFVCVDVEEUEZPQZRSZUSVIJURHIZO
      USHIVEVJVCVDVEEUFVCVDVEVJUGVFVGCUHUITJURRVHUMTPUJCDUOEFUKUN $.
      $( [25-Feb-2005] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Lemma for ~ expcnv .  Apply weak deduction theorem. $)
    expcnvlem5 $p |- ( ( ( A e. RR /\ 0 < A /\ A < 1 ) /\ ( B e. RR
        /\ 0 < B ) ) -> E. x e. NN A. y e. NN ( x <_ y -> ( A ^ y ) < B ) ) $=
      ( cr wcel cc0 clt wbr c1 w3a wa cexp co wi cn wral wceq breq2 eleq1 cv c2
      cle wrex cif opreq1 breq1d imbi2d rexralbidv breq1 3anbi123d 2re rereccli
      cdiv 2ne0 halfgt0 halflt1 3pm3.2i elimhyp anbi12d 1re lt01 pm3.2i dedth2h
      expcnvlem4 ) CEFZGCHIZCJHIZKZDEFZGDHIZLZAUABUAZUCIZCVMMNZDHIZOZBPQAPUDVNV
      ICJUBUNNZUEZVMMNZDHIZOZBPQAPUDVNVTVLDJUEZHIZOZBPQAPUDCDVRJCVSRZVQWBABPPWF
      VPWAVNWFVOVTDHCVSVMMUFUGUHUIDWCRZWBWEABPPWGWAWDVNDWCVTHSUHUIABVSWCVIVSEFZ
      GVSHIZVSJHIZKVREFZGVRHIZVRJHIZKCVRWFVFWHVGWIVHWJCVSETCVSGHSCVSJHUJUKVRVSR
      WKWHWLWIWMWJVRVSETVRVSGHSVRVSJHUJUKWKWLWMUBULUOUMUPUQURUSVLWCEFZGWCHIZLJE
      FZGJHIZLDJWGVJWNVKWODWCETDWCGHSUTJWCRWPWNWQWOJWCETJWCGHSUTWPWQVAVBVCUSVEV
      D $.
      $( [27-Feb-2005] $)

    $( Lemma for ~ expcnv .  Add in the case of ` A = 0 ` . $)
    expcnvlem6 $p |- ( ( ( A e. RR /\ 0 <_ A /\ A < 1 ) /\ ( B e. RR
        /\ 0 < B ) ) -> E. x e. NN A. y e. NN ( x <_ y -> ( A ^ y ) < B ) ) $=
      ( cr wcel cc0 cle wbr c1 clt wa cv cexp co wi cn wral wceq wb wrex wo 0re
      leloe mpan expcnvlem5 3exp1 a1i12 0exp breq1d adantl opreq1 adantr bitr3d
      1nn biimpd ex a1d com24 ralrimdv jcad imbi1d ralbidv rcla4ev syl6 adantld
      breq1 a1i jaod sylbid 3imp1 ) CEFZGCHIZCJKIZDEFZGDKIZLZAMZBMZHIZCVSNOZDKI
      ZPZBQRZAQUAZVLVMGCKIZGCSZUBZVNVQWEPZPZGEFVLVMWHTUCGCUDUEVLWFWJWGVLWFVNVQW
      EABCDUFUGWGWJPVLWGWIVNWGVPWEVOWGVPJQFZJVSHIZWBPZBQRZLWEWGVPWKWNWGVPWKUOUH
      WGVPWMBQWGWLVSQFZVPWBWGWOVPWBPZPWLWGWOWPWGWOLZVPWBWQGVSNOZDKIZVPWBWOWSVPT
      WGWOWRGDKVSUIUJUKWGWSWBTWOWGWRWADKGCVSNULUJUMUNUPUQURUSUTVAWDWNAJQVRJSZWC
      WMBQWTVTWLWBVRJVSHVGVBVCVDVEVFURVHVIVJVK $.
      $( [27-Feb-2005] $)
  $}

  ${
    $d j k x A $.  $d j x k F $.
    expcnv.1 $e |- F e. _V $.
    $( A sequence of powers of a complex number ` A ` with absolute value
       smaller than 1 converges to zero. $)
    expcnv $p |- ( ( A e. CC /\ A. k e. NN ( F ` k ) = ( A ^ k ) /\
                 ( abs ` A ) < 1 ) -> F ~~> 0 ) $=
      ( vx vj cc wcel cv cfv cn wral cabs c1 clt wbr wi cr wa wb co w3a cc0 cli
      cexp wceq cle wrex abscl adantr absge0 simpr 3jca expcnvlem6 adantllr cn0
      sylan fveq2 nnnn0 absexp sylan2 sylan9eqr breq1d imbi2d ex ralimdva ralbi
      imp syl rexbidv ad2antrr mpbird exp32 3impa ralrimiv expcl syl5ibrcom cuz
      eleq1 1z nnuz eqimss2i nnssz clm0i 3adant3 ) AGHZBIZCJZAWGUEUAZUFZBKLZAMJ
      ZNOPZUBZCUCUDPZUCEIZOPZFIWGUGPZWHMJZWPOPZQZBKLZFKUHZQZERLZWNXDERWFWKWMWPR
      HZXDQWFWKSZWMSZXFWQXCXHXFWQSZSXCWRWLWGUEUAZWPOPZQZBKLZFKUHZWFWMXIXNWKWFWM
      SZWLRHZUCWLUGPZWMUBXIXNXOXPXQWMWFXPWMAUIUJWFXQWMAUKUJWFWMULUMFBWLWPUNUQUO
      XGXCXNTWMXIXGXBXMFKXGXAXLTZBKLZXBXMTWFWKXSWFWJXRBKWFWGKHZSZWJXRYAWJSZWTXK
      WRYBWSXJWPOWJYAWSWIMJZXJWHWIMURXTWFWGUPHZYCXJUFWGUSZAWGUTVAVBVCVDVEVFVHXA
      XLBKVGVIVJVKVLVMVNVOWFWKWOXETZWMXGWHGHZBKLZYFWFWKYHWFWJYGBKYAYGWJWIGHZXTW
      FYDYIYEAWGVPVAWHWIGVSVQVFVHEFBCNNKKVTKNVRJWAWBZWCVTYJWCDWDVIWEVL $.
      $( [8-May-2006] $)

    $( A sequence of terms converges to zero when it is less than powers of a
       number ` A ` whose absolute value is smaller than 1. $)
    explecnv $p |- ( ( A e. RR /\ ( abs ` A ) < 1 /\ A. k e. NN
      ( ( F ` k ) e. CC /\ ( abs ` ( F ` k ) ) <_ ( A ^ k ) ) ) -> F ~~> 0 ) $=
      ( vj vx cr wcel cabs cfv c1 wbr cv cexp cle wa cn wral cc0 wceq clt cc co
      w3a copab recn opreq2 eqid oprex fvopab4 rgen opabex2 expcnv mp3an2 sylan
      cli nnex 3adant3 abscl adantr a1i cn0 nnnn0 reexpcl sylan2 adantlr jctild
      wi absge0 simpr jcad wb eleq1d fveq2 fveq2d anbi12d breq2d breq12d adantl
      fvex sylibrd ralimdva 3impia cuz nnuz raleqi cz 0cn elisseti climsqueeze2
      1z sylan2b syl11anc simpl ralimi climabs0i syl 3ad2ant3 mpbird ) AGHZAIJK
      UALZBMZCJZUBHZXCIJZAXBNUCZOLZPZBQRZUDZCSUPLZEMZQHZFMZXLCJZIJZTPEFUEZSUPLZ
      XJXMXNAXLNUCZTPEFUEZSUPLZXBXTJZGHZXBXQJZGHZPZSYDOLZYDYBOLZPZPZBQRZXRWTXAY
      AXIWTAUBHZXAYAAUFYLYBXFTZBQRXAYAYMBQEFXBXSXFQXTXLXBANUGXTUHAXBNUIUJZUKABX
      TEFQXSUQULZUMUNUOURWTXAXIYKWTXAPZXHYJBQYPXBQHZPZXHXFGHZXEGHZPZSXEOLZXGPZP
      ZYJYRXHUUAUUCYRXHYTYSXHYTVHYRXDYTXGXCUSUTVAWTYQYSXAYQWTXBVBHYSXBVCAXBVDVE
      VFVGYRXHUUBXGXHUUBVHYRXDUUBXGXCVIUTVAXHXGVHYRXDXGVJVAVKVKYQYJUUDVLYPYQYFU
      UAYIUUCYQYCYSYEYTYQYBXFGYNVMYQYDXEGEFXBXPXEQXQXLXBTXOXCIXLXBCVNVOXQUHXCIV
      TUJZVMVPYQYGUUBYHXGYQYDXESOUUEVQYQYDXEYBXFOUUEYNVRVPVPVSWAWBWCYKYAYJBKWDJ
      ZRZXRYJBQUUFWEWFYAKWGHUUGXRWKSBXTXQKYOEFQXPUQULZSUBWHWIWJUNWLWMXIWTXKXRVL
      ZXAXIXDBQRUUIXHXDBQXDXGWNWOBCXQDUUHUUEWPWQWRWS $.
      $( [18-Mar-2009] $) $( [19-Jul-2008] $)
  $}

  ${
    $d j k m n y z A $.  $d j m n z F $.  $d j N $.  $d n G $.
    geoser.1 $e |- F = { <. k , y >. | ( k e. NN0 /\ y = ( A ^ k ) ) } $.
    geoser.2 $e |- A e. CC $.
    $( The value of the finite geometric series ` 1 + A ^ 1 + A ^ 2 + ` ...
       ` + A ^ N ` . $)
    geoseri $p |- ( ( N e. NN0 /\ A =/= 1 ) -> ( ( + seq0 F ) ` N ) =
                 ( ( 1 - ( A ^ ( N + 1 ) ) ) / ( 1 - A ) ) ) $=
      ( wcel c1 caddc co cmin cmul cexp cc cc0 wceq ax-1cn opreq1d opreq2d cdiv
      vj vm cn0 wne wa cseq0 cfv cv expcl fopab ser0cl1i subidi eqeq1i subcan2i
      eqcom 3bitr3i necon3bii biimpi subcli divcan4 mp3an2 syl2an oprex ser0p1i
      opreq2 peano2nn0 sylancr adddir mp3an3 syl11anc eqtrd adantr opreq1 expp1
      mpan mulid1 syl nncan mp2an opreq2i subdi mp3an23 syl5reqr eqtr3d sylancl
      mulcl subsub mp3an1 3eqtr2rd sylan9eqr ex exp0 ax-mp opreq1i eqtri ser00i
      mulid2i addid2i exp1 3eqtr4i fveq2 eqeq12d nn0indALT ) EUDHZBIUEZUFEJDUGK
      ZUHZIBLKZMKZXIUAKZXHIBEIJKZNKZLKZXIUAKZXEXHOHZXIPUEZXKXHQZXFDECAUDOBCUIZN
      KZDFBOHZXSUDHXTOHGBXSUJVPUKZULXFXQBIXIPBBLKZXIQPXIQBIQXIPQYCPXIBGUMUNBIBG
      RGUOPXIUPUQURUSXPXIOHZXQXRIBRGUTZXHXIVAVBVCXEXKXOQXFXEXJXNXIUAUBUIZXGUHZX
      IMKZIBYFIJKZNKZLKZQPXGUHZXIMKZIBPIJKZNKZLKZQUCUIZXGUHZXIMKZIBYQIJKZNKZLKZ
      QZYTXGUHZXIMKZIBYTIJKZNKZLKZQZXJXNQUBUCEYQUDHZUUCUUIUUJUUCUFUUEYSUUAXIMKZ
      JKZUUHUUJUUEUULQUUCUUJUUEYRUUAJKZXIMKZUULUUJUUDUUMXIMAXTUUACDYQFBYTNVDXSY
      TBNVFVESUUJYROHZUUAOHZUUNUULQZDYQYBULUUJYAYTUDHZUUPGYQVGZBYTUJVHZUUOUUPYD
      UUQYEYRUUAXIVIVJVKVLVMUUCUUJUULUUBUUKJKZUUHYSUUBUUKJVNUUJUUHIUUABMKZLKIUU
      AUUKLKZLKZUVAUUJUUGUVBILUUJYAUURUUGUVBQGUUSBYTVOVHTUUJUVCUVBILUUJUUAIMKZU
      UKLKZUVCUVBUUJUVEUUAUUKLUUJUUPUVEUUAQUUTUUAVQVRSUUJUVBUUAIXILKZMKZUVFUVGB
      UUAMIOHZYAUVGBQRGIBVSVTWAUUJUUPUVHUVFQZUUTUUPUVIYDUVJRYEUUAIXIWBWCVRWDWET
      UUJUUPUUKOHZUVDUVAQZUUTUUJUUPYDUVKUUTYEUUAXIWGWFUVIUUPUVKUVLRIUUAUUKWHWIV
      KWJWKVLWLBPNKZXIMKZXIYMYPUVNIXIMKXIUVMIXIMYAUVMIQGBWMWNWOXIYEWRWPYLUVMXIM
      AXTUVMCDFBPNVDXSPBNVFWQWOYOBILYOBINKZBYNIBNIRWSWAYAUVOBQGBWTWNWPWAXAYFPQZ
      YHYMYKYPUVPYGYLXIMYFPXGXBSUVPYJYOILUVPYIYNBNYFPIJVNTTXCYFYQQZYHYSYKUUBUVQ
      YGYRXIMYFYQXGXBSUVQYJUUAILUVQYIYTBNYFYQIJVNTTXCYFYTQZYHUUEYKUUHUVRYGUUDXI
      MYFYTXGXBSUVRYJUUGILUVRYIUUFBNYFYTIJVNTTXCYFEQZYHXJYKXNUVSYGXHXIMYFEXGXBS
      UVSYJXMILUVSYIXLBNYFEIJVNTTXCXDSVMWE $.
      $( [12-May-2006] $)

    ${
      geolimilem.3 $e |- ( abs ` A ) < 1 $.
      geolimilem.4 $e |- G Fn NN0 $.
      geolimilem.5 $e |- ( n e. NN0 ->
               ( G ` n ) = ( -u ( A x. ( F ` n ) ) / ( 1 - A ) ) ) $.
      $( Lemma for ~ geolimi . $)
      geolimilem $p |- ( + seq0 F ) ~~> ( 1 / ( 1 - A ) ) $=
        ( co c1 cdiv cc0 cc wcel wceq cmul cn0 caddc cseq0 cli wbr cz cv cfv wa
        cmin cuz wral cneg ax-1cn subcli cr cabs clt wne abssubne0 mp3an reccli
        1re negcli mulcli cexp cn nnnn0 opreq2 oprex fvopab4 syl nn0ex fopabex2
        rgen expcnv 0z elnn0uz expcl mpan fopab ffvelrni pm3.2i mp3an13 mulneg1
        div23 sylancr opreq1d mulneg12 mp2an divrec2 eqtr4i opreq1i a1i 3eqtr3d
        eqcomi eqtrd jca sylbir wfn cvv elisseti climmulc2 mp4an mul01i breqtri
        mulcl negcl divcl mp3an23 eqeltrd mulcom opreq2d 3eqtr4rd negsub eqtr4d
        fnex expp1 divdir subeq0i necon3bii mpbi necomi geoseri mpan2 climaddc2
        3eqtr4d addid1i ) UAEUBLZMMBUILZNLZOUALZYJUCFOUCUDYJPQZOUEQZDUFZFUGZPQZ
        YNYHUGZYJYOUALZRZUHZDOUJUGZUKYHYKUCUDFYJULZBSLZOSLZOUCUUCPQEOUCUDZYMYNE
        UGZPQZYOUUCUUFSLZRZUHZDUUAUKFUUDUCUDUUBBYJYIMBUMHUNZBPQZMUOQBUPUGMUQUDZ
        YIOURZHVBIBMUSUTZVAZVCHVDZUULUUFBYNVELZRZDVFUKUUMUUEHUUSDVFYNVFQYNTQZUU
        SYNVGCAYNBCUFZVELZUURTEUVAYNBVEVHGBYNVEVIVJZVKVNIBDECATUVBEVLGVMZVOUTVP
        UUJDUUAYNUUAQZUUTUUJYNVQZUUTUUGUUITPYNECATPUVBEGUULUVATQUVBPQHBUVAVRVSV
        TWAZUUTYOBUUFSLZULZYINLZUUHKUUTBULZUUFSLZYINLZUVKYINLZUUFSLZUVJUUHUUTUU
        GUVMUVORZUVGUVKPQZUUGYIPQZUUNUHZUVPBHVCZUVRUUNUUKUUOWBZUVKUUFYIWEWCVKUU
        TUVLUVIYINUUTUULUUGUVLUVIRHUVGBUUFWDWFWGUVOUUHRUUTUUHUVOUUCUVNUUFSUUCYJ
        UVKSLZUVNYLUULUUCUWBRUUPHYJBWHWIUVQUVRUUNUVNUWBRUVTUUKUUOUVKYIWJUTWKWLW
        OWMWNWPWQWRVNOUUCDEFOUVDFTWSTWTQFWTQJVLTWTFXPWIZOUEVPXAZXBXCUUCUUQXDXEU
        UPVPYTDUUAUVEUUTYTUVFUUTYPYSUUTYOUVJPKUUTUVIPQZUVJPQZUUTUVHPQZUWEUUTUUL
        UUGUWGHUVGBUUFXFWFZUVHXGVKZUWEUVRUUNUWFUUKUUOUVIYIXHXIVKXJUUTMBYNMUALVE
        LZUILZYINLZYJUVJUALZYQYRUUTUWLMUVIUALZYINLZUWMUUTUWKUWNYINUUTUWKMUVHUIL
        ZUWNUUTUWJUVHMUIUUTBUURSLZUURBSLZUVHUWJUUTUULUURPQZUWQUWRRHUULUUTUWSHBY
        NVRVSBUURXKWFUUTUUFUURBSUVCXLUULUUTUWJUWRRHBYNXQVSXMXLUUTMPQZUWGUWNUWPR
        UMUWHMUVHXNWFXOWGUUTUWEUWOUWMRZUWIUWTUWEUVSUXAUMUWAMUVIYIXRWCVKWPUUTBMU
        RYQUWLRMBUUNMBURUUOYIOMBMBUMHXSXTYAYBABCEYNGHYCYDUUTYOUVJYJUAKXLYFWQWRV
        NOYJDFYHOUWCUAEUBVIUWDMYINVIYEXCYJUUPYGXE $.
        $( [15-May-2006] $)
    $}

    ${
      geolimi.3 $e |- ( abs ` A ) < 1 $.
      $( The partial sums in the infinite series ` 1 + A ^ 1 + A ^ 2 ` ...
         converge to ` ( 1 / ( 1 - A ) ) ` . $)
      geolimi $p |- ( + seq0 F ) ~~> ( 1 / ( 1 - A ) ) $=
        ( vn vj vz cv cn0 wcel cfv cmul co cneg cdiv wceq oprex c1 cmin wa eqid
        copab fnopab2 fveq2 opreq2d negeqd opreq1d fvopab4 geolimilem ) ABCHDIK
        ZLMJKBUMDNZOPZQZUABUBPZRPZSUCIJUEZEFGIJLURUSUPUQRTUSUDZUFIJHKZURBVADNZO
        PZQZUQRPLUSUMVASZUPVDUQRVEUOVCVEUNVBBOUMVADUGUHUIUJUTVDUQRTUKUL $.
        $( [15-May-2006] $)
    $}
  $}

  ${
    $d k y A $.
    geolim.1 $e |- F = { <. k , y >. | ( k e. NN0 /\ y = ( A ^ k ) ) } $.
    $( The partial sums in the infinite series ` 1 + A ^ 1 + A ^ 2 ` ...
       converge to ` ( 1 / ( 1 - A ) ) ` . $)
    geolim $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  ( + seq0 F ) ~~> ( 1 / ( 1 - A ) ) ) $=
      ( cc wcel cabs cfv c1 clt wbr wa caddc cseq0 co cexp wceq cli cc0 cv cmin
      cn0 cdiv opreq2i cif opreq1 eqeq2d anbi2d opabbidv opreq2d opreq2 breq12d
      copab eqid eleq1 fveq2 breq1d anbi12d 0cn abs0 lt01 eqbrtri pm3.2i simpli
      elimhyp simpri geolimi dedth syl5eqbr ) BFGZBHIZJKLZMZNDOPNCUAZUCGZAUAZBV
      OQPZRZMZCAUNZOPZJJBUBPZUDPZSDWANOEUEVNWBWDSLNVPVQVNBTUFZVOQPZRZMZCAUNZOPZ
      JJWEUBPZUDPZSLBTBWERZWBWJWDWLSWMWAWINOWMVTWHCAWMVSWGVPWMVRWFVQBWEVOQUGUHU
      IUJUKWMWCWKJUDBWEJUBULUKUMAWECWIWIUOWEFGZWEHIZJKLZVNWNWPMTFGZTHIZJKLZMBTW
      MVKWNVMWPBWEFUPWMVLWOJKBWEHUQURUSTWERZWQWNWSWPTWEFUPWTWRWOJKTWEHUQURUSWQW
      SUTWRTJKVAVBVCVDVFZVEWNWPXAVGVHVIVJ $.
      $( [15-May-2006] $)
  $}

  ${
    $d k y A $.
    geolim1i.1 $e |- F = { <. k , y >. | ( k e. NN /\ y = ( A ^ k ) ) } $.
    geolim1i.2 $e |- A e. CC $.
    geolim1i.3 $e |- ( abs ` A ) < 1 $.
    $( The partial sums in the geometric series ` A ^ 1 + A ^ 2 ` ... converge
       to ` ( A / ( 1 - A ) ) ` . $)
    geolim1i $p |- ( + seq1 F ) ~~> ( A / ( 1 - A ) ) $=
      ( cc0 c1 caddc co cn0 wcel wceq cseqz cmin cdiv ax-mp cc cn cv cexp copab
      cop wa cfv cseq1 cli cuz cz uzid nn0ex opabex2 oprex cseq0 addex seq0seqz
      wbr 0z eqid geolimi eqbrtrri expcl mpan fopab nn0uz feq2i mpbi clim2serzi
      wf cres wss nnssnn0 resopab2 nnuz reseq2i 3eqtr2i opreq2i 1z seqzres nnex
      eqtri fopabex2 seq1seqz ax-1cn addid2i opeq1i opreq1i 3eqtr4i subcli cabs
      wne cr clt 1re abssubne0 mp3an pm3.2i divsubdir nncan mp2an 3eqtr3i seqz1
      dividi 0nn0 opreq2 fvopab4 exp0 3eqtri eqtr4i 3brtr4i ) HIJKZJUDZCUAZLMZA
      UABXNUBKZNZUECAUCZOKZIIBPKZQKZHHJUDXROKZUFZPKZJDUGKZBXTQKZUHHHUIUFZMZXSYD
      UHURHUJMZYHUSHUKRYAXRHHCALXPULUMZIXTQUNJXRUOKYBYAUHJXRUPYJUQABCXRXRUTZFGV
      AVBLSXRVJYGSXRVJCALSXPXRYKBSMZXOXPSMFBXNVCVDVELYGSXRVFVGVHVIRIJUDZDOKZYMX
      ROKZYEXSYNYMXRIUIUFZVKZOKZYODYQYMODXNTMXQUECAUCZXRTVKZYQETLVLYTYSNVMXQCAT
      LVNRTYPXRVOVPVQVRIUJMYRYONVSJXRIUPYJVTRWBJDUPCATXPDWAEWCWDXMYMXROXLIJIWEW
      FWGWHWIYFYAIPKZYDIXTPKZXTQKZYAXTXTQKZPKZYFUUAISMZXTSMZUUGXTHWLZUEUUCUUENW
      EIBWEFWJZUUGUUHUUIYLIWMMBWKUFIWNURUUHFWOGBIWPWQZWRIXTXTWSWQUUBBXTQUUFYLUU
      BBNWEFIBWTXAWHUUDIYAPXTUUIUUJXDVRXBYCIYAPYCHXRUFZBHUBKZIYIYCUUKNUSJXRHUPY
      JXCRHLMUUKUULNXECAHXPUULLXRXNHBUBXFYKBHUBUNXGRYLUULINFBXHRXIVRXJXK $.
      $( [6-Jun-2006] $)
  $}

  ${
    $d k y A $.
    geolim1.1 $e |- F = { <. k , y >. | ( k e. NN /\ y = ( A ^ k ) ) } $.
    $( The partial sums in the infinite series ` A ^ 1 + A ^ 2 ` ... converge
       to ` ( A / ( 1 - A ) ) ` . $)
    geolim1 $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  ( + seq1 F ) ~~> ( A / ( 1 - A ) ) ) $=
      ( cc wcel cabs cfv c1 clt wbr wa caddc cseq1 co cexp wceq cli cc0 cv cmin
      cn copab cdiv opreq2i cif opreq1 eqeq2d anbi2d opabbidv opreq2d id opreq2
      opreq12d breq12d eqid eleq1 fveq2 breq1d anbi12d abs0 lt01 eqbrtri pm3.2i
      0cn elimhyp simpli simpri geolim1i dedth syl5eqbr ) BFGZBHIZJKLZMZNDOPNCU
      AZUCGZAUAZBVQQPZRZMZCAUDZOPZBJBUBPZUEPZSDWCNOEUFVPWDWFSLNVRVSVPBTUGZVQQPZ
      RZMZCAUDZOPZWGJWGUBPZUEPZSLBTBWGRZWDWLWFWNSWOWCWKNOWOWBWJCAWOWAWIVRWOVTWH
      VSBWGVQQUHUIUJUKULWOBWGWEWMUEWOUMBWGJUBUNUOUPAWGCWKWKUQWGFGZWGHIZJKLZVPWP
      WRMTFGZTHIZJKLZMBTWOVMWPVOWRBWGFURWOVNWQJKBWGHUSUTVATWGRZWSWPXAWRTWGFURXB
      WTWQJKTWGHUSUTVAWSXAVFWTTJKVBVCVDVEVGZVHWPWRXCVIVJVKVL $.
      $( [6-Jun-2006] $)
  $}

  ${
    $d A j y $.
    georeclim.1 $e |- F = { <. j , y >. |
                            ( j e. NN0 /\ y = ( ( 1 / A ) ^ j ) ) } $.
    $( The limit of a geometric series of reciprocals.  (Contributed by Paul
       Chapman, 28-Dec-2007.) $)
    georeclim $p |- ( ( A e. CC /\ 1 < ( abs ` A ) ) ->
                      ( + seq0 F ) ~~> ( A / ( A - 1 ) ) ) $=
      ( cc wcel c1 cabs clt wbr wa co cdiv cmin cc0 1re syl11anc syldan wceq cr
      cfv caddc cseq0 cli wne abscl adantr wi 0re axlttrn mp3an12 syl mpani imp
      lt01 gt0ne0 wb abs00 necon3bid mpbid reccl ax-1cn absdiv mp3an1 cle absid
      ltleii mp2an opreq1i syl6eq recgt1 biimpd ex pm2.43d eqbrtrd geolim divid
      cmul opreq2d subcl sylancr abssubne0 mp3an2 simpl divdivdiv syl22anc div1
      a1i jca 3eqtr3rd mulid2 subdir syl111anc recid2 opreq12d eqtrd breqtrd )
      BFGZHBIUBZJKZLZUCDUDMZHHHBNMZOMZNMZBBHOMZNMZUEXBXDFGZXDIUBZHJKZXCXFUEKWSX
      ABPUFZXIXBWTPUFZXLXBWTUAGZPWTJKZXMWSXNXABUGZUHZWSXAXOWSPHJKZXAXOUPWSXNXRX
      ALXOUIZXPPUAGHUAGZXNXSUJQPHWTUKULUMUNUOZWTUQRWSXMXLURXAWSWTPBPBUSUTUHVAZB
      VBSZXBXJHWTNMZHJXBXJHIUBZWTNMZYDWSXAXLXJYFTZYBHFGZWSXLYGVCHBVDVESYEHWTNXT
      PHVFKYEHTQPHUJQUPVHHVGVIVJVKWSXAYDHJKZWSXAYIWSXAXAYIUIXBXAYIXBXNXOXAYIURX
      QYAWTVLRVMVNVOUOVPZAXDCDEVQRXBXFHBVSMZXEBVSMZNMZXHXBXFBBNMZNMZXFHNMZYMXFX
      BYNHXFNWSXAXLYNHTYBBVRSVTXBYHXEFGZXEPUFZLWSXLLZYSYOYMTYHXBVCWIZXBYQYRXBYH
      XIYQVCYCHXDWAWBZXBXIXKYRYCYJXIXTXKYRQXDHWCWDRZWJXBWSXLWSXAWEZYBWJZUUDHXEB
      BWFWGXBXFFGZYPXFTXBYQYRUUEUUAUUBXEVBRXFWHUMWKXBYKBYLXGNWSYKBTXABWLUHZXBYL
      YKXDBVSMZOMZXGXBYHXIWSYLUUHTYTYCUUCHXDBWMWNXBYKBUUGHOUUFWSXAXLUUGHTYBBWOS
      WPWQWPWQWR $.
      $( [28-Dec-2007] $)
  $}

  ${
    $d k y A $.  $d y k N $.
    geosum.1 $e |- A e. CC $.
    $( The value of the finite geometric series ` 1 + A ^ 1 + A ^ 2 + ` ...
       ` + A ^ N ` . $)
    geosumi $p |- ( ( N e. NN0 /\ A =/= 1 ) -> sum_ k e. ( 0 ... N ) ( A ^ k )
               = ( ( 1 - ( A ^ ( N + 1 ) ) ) / ( 1 - A ) ) ) $=
      ( vy cn0 wcel c1 wne wa cc0 cfz co cv cexp caddc wceq cfv cmin elnn0uz
      csu copab cseq0 cdiv cop cseqz oprex anbi1i opabbii fsumserz2 sylbi addex
      cuz nn0ex opabex2 seq0seqz fveq1i syl6eqr adantr eqid geoseri eqtrd ) CFG
      ZAHIZJKCLMABNZOMZBUAZCPVEFGZENVFQZJZBEUBZUCMZRZHACHPMOMSMHASMUDMVCVGVMQVD
      VCVGCKPUEVKUFMZRZVMVCCKUMRZGVGVOQCTEVFBVKKCAVEOUGVJVEVPGZVIJBEVHVQVIVETUH
      UIUJUKCVLVNPVKULBEFVFUNUOUPUQURUSEABVKCVKUTDVAVB $.
      $( [9-May-2006] $)
  $}

  ${
    $d k x y z A $.  $d k x y z R $.
    $( The infinite sum of ` 1 + A ^ 1 + A ^ 2 ` ... is
       ` ( 1 / ( 1 - A ) ) ` . $)
    geoisum $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  sum_ k e. NN0 ( A ^ k ) = ( 1 / ( 1 - A ) ) ) $=
      ( vy cc wcel cfv c1 wbr wa cn0 cv cexp csu cc0 cdiv caddc wceq cli oprex
      co cabs clt cuz cmin nn0uz sumeq1i cz cop copab cseqz cseq0 addex opabex2
      0z nn0ex seq0seqz eqid geolim syl5eqbrr elnn0uz opabbii isumclim4 sylancr
      anbi1i syl5eq ) ADEAUAFGUBHIZJABKZLTZBMNUCFZVHBMZGGAUDTZOTZJVIVHBUEUFVFNU
      GENPUHVGJEZCKVHQZIZBCUIZUJTZVLRHVJVLQUNVFVQPVPUKTVLRPVPULBCJVHUOUMUPCABVP
      VPUQURUSCVHVLBVPNAVGLSVOVGVIEZVNIBCVMVRVNVGUTVDVAGVKOSVBVCVE $.
      $( [4-Jan-2007] $) $( [15-May-2006] $)

    $( The infinite sum of reciprocals
       ` 1 + ( 1 / A ) ^ 1 + ( 1 / A ) ^ 2 ` ... is ` A / ( A - 1 ) ` .
       (Contributed by rpenner, 3-Nov-2007.) $)
    geoisumr $p |- ( ( A e. CC /\ 1 < ( abs ` A ) ) ->
              sum_ k e. NN0 ( ( 1 / A ) ^ k ) = ( A / ( A - 1 ) ) ) $=
      ( cc wcel c1 cabs cfv clt wbr wa cdiv cmin wceq cc0 wne 1re syldan ax-1cn
      co cr cn0 cv cexp csu lt01 abscl 0re axlttrn mp3an12 syl mpani imp absgt0
      wi biimpar reccl absdiv mp3an1 ltleii absidi ax-mp opreq1i syl6eq recgt1i
      cle sylan simprd eqbrtrd geoisum syl11anc divsubdir anabss5 divid opreq1d
      mp3an2 eqtr2d opreq2d subcl mpan2 adantr ltne subeq0 fveq2 syl6bi necon3d
      wb mpd simpl recdiv syl22anc 3eqtrd ) ACDZEAFGZHIZJZUAEAKSZBUBUCSBUDZEEWP
      LSZKSZEAELSZAKSZKSZAWTKSZWOWPCDZWPFGZEHIWQWSMWLWNANOZXDWLWNNWMHIZXFWLWNXG
      WLNEHIZWNXGUEWLWMTDZXHWNJXGUNZAUFZNTDETDZXIXJUGPNEWMUHUIUJUKULWLXFXGAUMUO
      QZAUPQWOXEEWMKSZEHWOXEEFGZWMKSZXNWLWNXFXEXPMZXMECDZWLXFXQREAUQURQXOEWMKNE
      VEIXOEMNEUGPUEUSEPUTVAZVBVCWONXNHIZXNEHIZWLXIWNXTYAJXKWMVDVFVGVHWPBVIVJWO
      WRXAEKWLWNXFWRXAMXMWLXFJZXAAAKSZWPLSZWRWLXFXAYDMZWLXRYBYERAEAVKVOVLYBYCEW
      PLAVMVNVPQVQWOWTCDZWTNOZWLXFXBXCMWLYFWNWLXRYFRAEVRVSVTWOWMEOZYGWLXIWNYHXK
      XLXIWNYHPEWMWAURVFWLYHYGUNWNWLWTNWMEWLWTNMZAEMZWMEMWLXRYIYJWFRAEWBVSYJWMX
      OEAEFWCXSVCWDWEVTWGWLWNWHXMWTAWIWJWK $.
      $( [3-Nov-2007] $)

    $( The infinite sum of ` A ^ 1 + A ^ 2 ` ... is ` ( A / ( 1 - A ) ) ` . $)
    geoisum1 $p |- ( ( A e. CC /\ ( abs ` A ) < 1 ) ->
                  sum_ k e. NN ( A ^ k ) = ( A / ( 1 - A ) ) ) $=
      ( vy cc wcel cabs cfv c1 wbr wa cn cv cexp csu cdiv caddc wceq cli oprex
      co clt cuz cmin nnuz sumeq1i cz cop copab cseqz cseq1 addex nnex seq1seqz
      1z opabex2 eqid geolim1 syl5eqbrr elnnuz anbi1i opabbii isumclim4 sylancr
      syl5eq ) ADEAFGHUAIJZKABLZMTZBNHUBGZVGBNZAHAUCTZOTZKVHVGBUDUEVEHUFEHPUGVF
      KEZCLVGQZJZBCUHZUITZVKRIVIVKQUNVEVPPVOUJTVKRPVOUKBCKVGULUOUMCABVOVOUPUQUR
      CVGVKBVOHAVFMSVNVFVHEZVMJBCVLVQVMVFUSUTVAAVJOSVBVCVD $.
      $( [1-Nov-2007] $)

    $( The infinite sum of ` A x. ( R ^ 1 ) + A x. ( R ^ 2 ) ` ... is
       ` ( A x. R ) / ( 1 - R ) ` . $)
    geoisum1c $p |- ( ( A e. CC /\ R e. CC /\ ( abs ` R ) < 1 ) ->
          sum_ k e. NN ( A x. ( R ^ k ) ) = ( ( A x. R ) / ( 1 - R ) ) ) $=
      ( vy vx cc wcel cfv c1 wbr cmul co cdiv cv csu cn wa wceq caddc cli subcl
      vz cabs clt w3a cmin cuz cexp cc0 wne ax-1cn 3ad2ant2 cr abssubne0 mp3an2
      mpan 1re 3adant1 jca divass syld3an3 sumeq1i geoisum1 syl5eqr opreq2d cop
      nnuz wral copab cseqz wex cn0 elnnuz nnnn0 sylbir sylan2 ralrimiva adantr
      expcl cseq1 eqid geolim1 oprex addex nnex opabex2 seq1seqz breq1i syl5bbr
      breq2 cla4ev syl cz 1z hbopab1 cvv fvopab2 mpan2 isummulc1ai mpanl1 3impb
      syl6eqr 3eqtr2rd ) AFGZBFGZBUCHIUDJZUEZABKLIBUFLZMLZABXHMLZKLZAIUGHZBCNZU
      HLZCOZKLZPAXNKLZCOZXDXEXFXHFGZXHUIUJZQXIXKRXGXSXTXEXDXSXFIFGXEXSUKIBUAUPU
      LXEXFXTXDXEIUMGXFXTUQBIUNUOURUSABXHUTVAXGXOXJAKXEXFXOXJRXDXEXFQZXOPXNCOXJ
      PXLXNCVGVBBCVCVDURVEXGXPXLXQCOZXRXDXEXFXPYBRZYAXDXNFGZCXLVHZISVFXMPGZDNXN
      RQZCDVIZVJLZENZTJZEVKZQZYCYAYEYLXEYEXFXEYDCXLXMXLGZXEXMVLGZYDYNYFYOXMVMZX
      MVNVOBXMVSVPVQVRYASYHVTLZXJTJZYLDBCYHYHWAWBYKYREXJBXHMWCYKYQYJTJYJXJRYRYQ
      YIYJTSYHWDCDPXNWEWFZWGWHYJXJYQTWJWIWKWLUSIWMGXDYMYCWNEUBXNACYHIBXMUHWCZYS
      YGCDUBWOYNYFXMYHHXNRZYPYFXNWPGUUAYTCDPXNWPWQWRVOWSWTVPXAPXLXQCVGVBXBXC $.
      $( [2-Nov-2007] $)
  $}

  $( The recurring decimal 0.999..., which is defined as the infinite sum 0.9 +
     0.09 + 0.009 + ... i.e. ` 9 / 10 ^ 1 + 9 / 10 ^ 2 + 9 / 10 ^ 3 + ... ` ,
     is exactly equal to 1, according to ZF set theory.  Interestingly, about
     40% of the people responding to a poll at
     ~ http://forum.physorg.com/index.php?showtopic=13177 disagree. $)
  0.999... $p |- sum_ k e. NN ( 9 / ( 10 ^ k ) ) = 1 $=
    ( cn c9 c10 co cdiv c1 cmul cmin wcel wceq cc0 10re recni 10pos 9re clt wbr
    cc ax-1cn cexp csu cn0 nnnn0 wne expcl mpan gt0ne0ii expne0i mp3an12 divrec
    mp3an1 syl11anc exprec opreq2d eqtr4d syl sumeq2i cabs cfv rereccli cle 0re
    cv recgt0ii ltleii absidi ax-mp caddc 9pos cr wb ltaddpos2 mp2an mpbi df-10
    1re breqtrri recgt1 eqbrtri geoisum1c mp3an divreci divcan2i subdii mulid1i
    recidi opreq12i addcomi eqtr4i 3eqtrri eqtri redivcli subcli mulcani dividi
    subaddrii divgt0ii eqtr3i 3eqtri ) BCDAVDZUAEZFEZAUBBCGDFEZXAUAEZHEZAUBZCXD
    HEZGXDIEZFEZGBXCXFAXABJXAUCJZXCXFKXAUDXKXCCGXBFEZHEZXFXKXBSJZXBLUEZXCXMKZDS
    JZXKXNDMNZDXAUFUGXQDLUEZXKXOXRDMOUHZDXAUIUJCSJZXNXOXPCPNZCXBUKULUMXKXEXLCHX
    QXSXKXEXLKXRXTDXAUNUJUOUPUQURYAXDSJXDUSUTZGQRXGXJKYBXDDMXTVAZNZYCXDGQLXDVBR
    YCXDKLXDVCYDDMOVEVFXDYDVGVHGDQRZXDGQRZGCGVIEZDQLCQRZGYHQRZVJCVKJGVKJYIYJVLP
    VQCGVMVNVOVPVRDVKJLDQRYFYGVLMODVSVNVOVTCXDAWAWBCDFEZYKFEXJGYKXHYKXIFCDYBXRX
    TWCDYKHEZDXIHEZKYKXIKYLCYMCDYBXRXTWDYMDGHEZDXDHEZIEDGIECDGXDXRTYEWEYNDYOGID
    XRWFDXRXTWGWHDGCXRTYBGCVIEYHDGCTYBWIVPWJWQWKWLYKXIDYKCDPMXTWMZNZGXDTYEWNXRX
    TWOVOWHYKYQYKYPCDPMVJOWRUHWPWSWT $.
    $( [2-Nov-2007] $)


