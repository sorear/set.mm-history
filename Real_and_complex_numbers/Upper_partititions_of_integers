$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Upper partititions of integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ZZ>= $. $( ` ZZ ` with greater-than-or-equal-to subscript. $)

  $( Extend class notation with the upper integer function.  Read
     " ` ZZ>= `` M ` " as "the set of integers greater than or equal to
     ` M ` ." $)
  cuz $a class ZZ>= $.

  ${
    $d j k y N $.  $d k M $.
    $( Define a function whose value at ` j ` is the semi-infinite set of
       contiguous integers starting at ` j ` , which we will also call the
       upper integers starting at ` j ` .  Read " ` ZZ>= `` M ` " as "the set
       of integers greater than or equal to ` M ` ." See ~ uzval for its
       value, ~ uzssz for its relationship to ` ZZ ` , ~ nnuz and ~ nn0uz for
       its relationships to ` NN ` and ` NN0 ` , and ~ eluz1 and ~ eluz2 for
       its membership relations. $)
    df-uz $a |- ZZ>= =
                  { <. j , y >. | ( j e. ZZ /\ y = { k e. ZZ | j <_ k } ) } $.

    $( The value of the upper integers function. $)
    uzval $p |- ( N e. ZZ -> ( ZZ>= ` N ) = { k e. ZZ | N <_ k } ) $=
      ( vj vy cv cle wbr cz crab cuz wceq breq1 rabbisdv df-uz zex rabex
      fvopab4 ) CDBCEZAEZFGZAHIBSFGZAHIHJRBKTUAAHRBSFLMDCANUAAHOPQ $.
      $( [11-Sep-2005] $) $( [5-Sep-2005] $)

    $( Membership in the set of upper integers starting at ` M ` . $)
    eluz1 $p |- ( M e. ZZ ->
           ( N e. ( ZZ>= ` M ) <-> ( N e. ZZ /\ M <_ N ) ) ) $=
      ( vk cz wcel cuz cfv cv cle wbr crab wa uzval eleq2d breq2 elrab syl6bb
      ) ADEZBAFGZEBACHZIJZCDKZEBDEABIJZLRSUBBCAMNUAUCCBDTBAIOPQ $.
      $( [11-Sep-2005] $) $( [5-Sep-2005] $)

    $( Membership in a set of upper integers.  We use the fact that a
       function's value (under our function value definition) is empty outside
       of its domain to show ` M e. ZZ ` . $)
    eluz2 $p |- ( N e. ( ZZ>= ` M ) <-> ( M e. ZZ /\ N e. ZZ /\ M <_ N ) ) $=
      ( vj vy vk cuz cfv wcel cz cle wbr w3a cdm elfvdm cv crab zex rabex
      df-uz dmopab2 syl6eleq 3simp1 wa eluz1 ibar bitrd 3anass syl6bbr
      pm5.21nii ) BAFGHZAIHZUKBIHZABJKZLZUJAFMIBAFNCDICOEOJKZEIPFUOEIQRDCESTUAU
      KULUMUBUKUJUKULUMUCZUCZUNUKUJUPUQABUDUKUPUEUFUKULUMUGUHUI $.
      $( [12-Sep-2005] $) $( [5-Sep-2005] $)
  $}

  ${
    eluz.1 $e |- M e. ZZ $.
    $( Membership in a set of upper integers. $)
    eluz1i $p |- ( N e. ( ZZ>= ` M ) <-> ( N e. ZZ /\ M <_ N ) ) $=
      ( cz wcel cuz cfv cle wbr wa wb eluz1 ax-mp ) ADEBAFGEBDEABHIJKCABLM $.
      $( [12-Sep-2005] $) $( [5-Sep-2005] $)
  $}

  $( Implication of membership in a set of upper integers. $)
  eluzelz $p |- ( N e. ( ZZ>= ` M ) -> N e. ZZ ) $=
    ( cuz cfv wcel cz cle wbr w3a eluz2 3simp2 sylbi ) BACDEAFEZBFEZABGHZINABJM
    NOKL $.
    $( [14-Sep-2005] $) $( [6-Sep-2005] $)

  $( Implication of membership in a set of upper integers. $)
  eluzel2 $p |- ( N e. ( ZZ>= ` M ) -> M e. ZZ ) $=
    ( cuz cfv wcel cz cle wbr w3a eluz2 3simp1 sylbi ) BACDEAFEZBFEZABGHZIMABJM
    NOKL $.
    $( [14-Sep-2005] $) $( [6-Sep-2005] $)

  $( Implication of membership in a set of upper integers. $)
  eluzle $p |- ( N e. ( ZZ>= ` M ) -> M <_ N ) $=
    ( cuz cfv wcel cz cle wbr w3a eluz2 3simp3 sylbi ) BACDEAFEZBFEZABGHZIOABJM
    NOKL $.
    $( [14-Sep-2005] $) $( [6-Sep-2005] $)

  $( Membership in a set of upper integers. $)
  eluz $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
         ( N e. ( ZZ>= ` M ) <-> M <_ N ) ) $=
    ( cz wcel cuz cfv cle wbr wb wa wi eluz1 eluzelz pm4.71ri syl5bbr pm5.32
    sylibr imp ) ACDZBCDZBAEFDZABGHZIZSTUAJZTUBJZITUCKSUAUEUDABLUATABMNOTUAUBPQ
    R $.
    $( [4-Oct-2005] $) $( [2-Oct-2005] $)

  $( Membership of the least member in a set of upper integers. $)
  uzid $p |- ( M e. ZZ -> M e. ( ZZ>= ` M ) ) $=
    ( cz wcel cuz cfv cle wbr wa cr zre leid syl ancli eluz1 mpbird ) ABCZAADEC
    PAAFGZHPQPAICQAJAKLMAANO $.
    $( [12-Sep-2005] $) $( [2-Sep-2005] $)

  $( Transitive law for sets of upper integers. $)
  uztrn $p |- ( ( M e. ( ZZ>= ` K ) /\ K e. ( ZZ>= ` N ) ) ->
              M e. ( ZZ>= ` N ) ) $=
    ( cuz cfv wcel wa cz cle wbr w3a eluzel2 adantl eluzelz adantr eluzle wi
    cr letr zre syl3an syl3anc mp2and 3jca eluz2 sylibr ) BADEFZACDEZFZGZCHFZBH
    FZCBIJZKBUHFUJUKULUMUIUKUGCALMZUGULUIABNOZUJCAIJZABIJZUMUIUPUGCAPMUGUQUIABP
    OUKAHFZULUPUQGUMQZUJCRFARFBRFUSUKURULCABSCTATBTUAUNUIURUGCANMUOUBUCUDCBUEUF
    $.
    $( [23-Sep-2005] $) $( [20-Sep-2005] $)

  $( Contraposition law for upper integers. $)
  uzneg $p |- ( N e. ( ZZ>= ` M ) -> -u M e. ( ZZ>= ` -u N ) ) $=
    ( cuz cfv wcel cneg cle wbr eluzle cz wb cr leneg zre syl2an eluzel2
    eluzelz sylanc mpbid eluz znegcl mpbird ) BACDEZAFZBFZCDEZUEUDGHZUCABGHZUGA
    BIAJEZBJEZUHUGKZUCALEBLEUKUIUJABMANBNOABPZABQZRSUJUIUFUGKZUCUEJEUDJEUNUJUIU
    EUDTBUAAUAOUMULRUB $.
    $( [6-Jan-2006] $) $( [28-Nov-2005] $)

  ${
    $d j k y M $.  $d k N $.
    $( A set of upper integers is a subset of all integers. $)
    uzssz $p |- ( ZZ>= ` M ) (_ ZZ $=
      ( vk vj vy cz wcel cuz cfv wss cv cle wbr crab uzval ssrab2 a1i eqsstrd
      wn c0 cdm wceq wa copab df-uz dmeqi dmopabss eqsstri sseli con3i ndmfv
      syl 0ss pm2.61i ) AEFZAGHZEIUNUOABJZKLZBEMZEBANUREIUNUQBEOPQUNRZUOSEUSAGT
      ZFZRUOSUAVAUNUTEAUTCJZEFDJVBUPKLBEMUAZUBCDUCZTEGVDDCBUDUEVCCDEUFUGUHUIAGU
      JUKSEIUSEULPQUM $.
      $( [13-Sep-2005] $) $( [2-Sep-2005] $)

    $( Subset relationship for two sets of upper integers. $)
    uzss $p |- ( N e. ( ZZ>= ` M ) ->
               ( ZZ>= ` N ) (_ ( ZZ>= ` M ) ) $=
      ( vk cuz cfv wcel cv cz cle wbr wa wi eluzle adantr cr letr zre syl3an
      3expa eluzel2 eluzelz jca sylan mpand ex imdistand wb eluz1 syl 3imtr4d
      ssrdv ) BADEZFZCBDEZULUMCGZHFZBUOIJZKZUPAUOIJZKZUOUNFZUOULFZUMUPUQUSUMUPU
      QUSLUMUPKABIJZUQUSUMVCUPABMNAHFZBHFZKUPVCUQKUSLZUMVDVEUPVFAOFBOFUOOFVFVDV
      EUPABUOPAQBQUOQRSUMVDVEABTZABUAZUBUCUDUEUFUMVEVAURUGVHBUOUHUIUMVDVBUTUGVG
      AUOUHUIUJUK $.
      $( [11-Sep-2005] $) $( [5-Sep-2005] $)
  $}

  $( The upper integers function is one-to-one. $)
  uz11 $p |- ( M e. ZZ ->
                ( ( ZZ>= ` M ) = ( ZZ>= ` N ) <-> M = N ) ) $=
    ( cz wcel cuz cfv wceq wa eleq2 eluzel2 syl6bi uzid syl5 impcom cle wbr
    syl5bir eluzle syl6 syl5bi anim12d imp anassrs ancoms wb cr letri3 zre
    syl2an adantlr mpbird mpdan ex fveq2 impbid1 ) ACDZAEFZBEFZGZABGZUPUSUTUPUS
    HZBCDZUTUSUPVBUSAUQDZVBUPUSVCAURDZVBUQURAIZBAJKALZMNVAVBHUTABOPZBAOPZHZUPUS
    VBVIUSVBHUPVIUSVBUPVIUSVBUPHVIUSVBVGUPVHUSVBBUQDZVGUSVJBURDVBUQURBIBLQABRSU
    SUPVDVHUSVCVDUPVEVFTBARSUAUBUCUDUCUPVBUTVIUEZUSAUFDBUFDVKUPVBABUGAUHBUHUIUJ
    UKULUMABEUNUO $.
    $( [25-Dec-2005] $) $( [12-Dec-2005] $)

  $( Membership in the next set of upper integers. $)
  eluzp1m1 $p |- ( ( M e. ZZ /\
             N e. ( ZZ>= ` ( M + 1 ) ) ) -> ( N - 1 ) e. ( ZZ>= ` M ) ) $=
    ( cz wcel c1 caddc co cuz cfv cmin cle wbr wa peano2zm ad2antrl cr wb 1re
    leaddsub mp3an2 zre syl2an biimpa anasss jca ex peano2z eluz1 syl 3imtr4d
    imp ) ACDZBAEFGZHIDZBEJGZAHIDZULBCDZUMBKLZMZUOCDZAUOKLZMZUNUPULUSVBULUSMUTV
    AUQUTULURBNOULUQURVAULUQMURVAAPDZBPDZURVAQZULUQVCEPDVDVERAEBSTAUABUAUBUCUDU
    EUFULUMCDUNUSQAUGUMBUHUIAUOUHUJUK $.
    $( [28-Sep-2005] $) $( [12-Sep-2005] $)

  $( Strict ordering implied by membership in the next set of upper
     integers. $)
  eluzp1l $p |- ( ( M e. ZZ /\
             N e. ( ZZ>= ` ( M + 1 ) ) ) -> M < N ) $=
    ( cz wcel c1 caddc co cuz cfv wa clt wbr cle eluzle adantl wb zltp1le
    eluzelz sylan2 mpbird ) ACDZBAEFGZHIDZJABKLZUBBMLZUCUEUAUBBNOUABCDUDUEPUCAB
    QUBBRST $.
    $( [1-Oct-2005] $) $( [12-Sep-2005] $)

  $( Membership in the next set of upper integers. $)
  eluzp1p1 $p |- ( N e. ( ZZ>= ` M ) ->
                       ( N + 1 ) e. ( ZZ>= ` ( M + 1 ) ) ) $=
    ( cz wcel cle wbr w3a c1 caddc co cuz cfv peano2z 3ad2ant1 3ad2ant2 cr wb
    1re leadd1 mp3an3 zre syl2an biimp3a 3jca eluz2 3imtr4i ) ACDZBCDZABEFZGZAH
    IJZCDZBHIJZCDZUKUMEFZGBAKLDUMUKKLDUJULUNUOUGUHULUIAMNUHUGUNUIBMOUGUHUIUOAPD
    ZBPDZUIUOQZUGUHUPUQHPDURRABHSTAUABUAUBUCUDABUEUKUMUEUF $.
    $( [5-Oct-2005] $) $( [5-Oct-2005] $)

 ${
    eluzaddi.1 $e |- M e. ZZ $.
    eluzaddi.2 $e |- K e. ZZ $.
    $( Membership in a later set of upper integers.  (Contributed by Paul
       Chapman, 22-Nov-2007.) $)
    eluzaddi $p |- ( N e. ( ZZ>= ` M )
          -> ( N + K ) e. ( ZZ>= ` ( M + K ) ) ) $=
      ( cuz cfv wcel caddc co cz cle wbr wa eluzelz zaddcl mpan2 syl eluz1i cr
      wb zre zrei leadd1 mp3an13 biimpa sylbi jca mp2an sylibr ) CBFGHZCAIJZKHZ
      BAIJZULLMZNULUNFGHUKUMUOUKCKHZUMBCOUPAKHZUMECAPQRUKUPBCLMZNUOBCDSUPURUOUP
      CTHZURUOUAZCUBBTHUSATHUTBDUCAEUCBCAUDUERUFUGUHUNULBKHUQUNKHDEBAPUISUJ $.
      $( [23-Nov-2007] $) $( [22-Nov-2007] $)

    $( Membership in an earlier set of upper integers.  (Contributed by Paul
       Chapman, 22-Nov-2007.) $)
    eluzsubi $p |- ( N e. ( ZZ>= ` ( M + K ) )
         -> ( N - K ) e. ( ZZ>= ` M ) ) $=
      ( caddc co cuz cfv wcel cmin cz cle wbr wa eluzelz zsubcl mpan2 syl
      zaddcl mp2an eluz1i cr wb zre zrei leaddsub mp3an12 biimpa sylbi jca
      sylibr ) CBAFGZHIJZCAKGZLJZBUOMNZOUOBHIJUNUPUQUNCLJZUPUMCPURALJZUPECAQRSU
      NURUMCMNZOUQUMCBLJUSUMLJDEBATUAUBURUTUQURCUCJZUTUQUDZCUEBUCJAUCJVAVBBDUFA
      EUFBACUGUHSUIUJUKBUODUBUL $.
      $( [23-Nov-2007] $) $( [22-Nov-2007] $)
  $}

  $( Nonnegative integers expressed as a set of upper integers. $)
  nn0uz $p |- NN0 = ( ZZ>= ` 0 ) $=
    ( vk cn0 cc0 cv cle wbr cz crab cuz cfv nn0zrab wcel wceq 0z uzval ax-mp
    eqtr4i ) BCADEFAGHZCIJZAKCGLSRMNACOPQ $.
    $( [11-Sep-2005] $) $( [2-Sep-2005] $)

  $( Natural numbers expressed as a set of upper integers. $)
  nnuz $p |- NN = ( ZZ>= ` 1 ) $=
    ( vk cn c1 cv cle wbr cz crab cuz cfv nnzrab wcel wceq 1z uzval ax-mp
    eqtr4i ) BCADEFAGHZCIJZAKCGLSRMNACOPQ $.
    $( [11-Sep-2005] $) $( [2-Sep-2005] $)

  $( A natural number expressed as a member of a set of upper integers. $)
  elnnuz $p |- ( N e. NN <-> N e. ( ZZ>= ` 1 ) ) $=
    ( cn c1 cuz cfv nnuz eleq2i ) BCDEAFG $.
    $( [6-Jun-2006] $) $( [6-Jun-2006] $)

  $( A nonnegative integer expressed as a member a set of upper integers. $)
  elnn0uz $p |- ( N e. NN0 <-> N e. ( ZZ>= ` 0 ) ) $=
    ( cn0 cc0 cuz cfv nn0uz eleq2i ) BCDEAFG $.
    $( [7-Jun-2006] $) $( [6-Jun-2006] $)

  ${
    $d m n M $.
    $( Restricted universal quantification in a set of upper integers. $)
    raluz $p |- ( M e. ZZ -> ( A. n e. ( ZZ>= ` M ) ph <->
               A. n e. ZZ ( M <_ n -> ph ) ) ) $=
      ( cz wcel cv cle wbr wi cuz cfv wa eluz1 imbi1d impexp syl6bb ralbidv2 )
      CDEZACBFZGHZAIZBCJKZDRSUBEZAISDEZTLZAIUDUAIRUCUEACSMNUDTAOPQ $.
      $( [27-Sep-2005] $) $( [9-Sep-2005] $)

    $( Restricted universal quantification in a set of upper integers. $)
    raluz2 $p |- ( A. n e. ( ZZ>= ` M ) ph <->
               ( M e. ZZ -> A. n e. ZZ ( M <_ n -> ph ) ) ) $=
      ( cuz cfv wral cz wcel cv cle wbr wi wa w3a eluz2 3anass bitri imbi1i
      impexp imbi2i bi2.04 3bitri ralbii2 r19.21v ) ABCDEZFCGHZCBIZJKZALZLZBGFU
      FUIBGFLAUJBUEGUGUEHZALUFUGGHZUHMZMZALZUFULUILZLZULUJLUKUNAUKUFULUHNUNCUGO
      UFULUHPQRUOUFUMALZLUQUFUMASURUPUFULUHASTQUFULUIUAUBUCUFUIBGUDQ $.
      $( [3-Oct-2005] $) $( [9-Sep-2005] $)

    $( Restricted existential quantification in a set of upper integers. $)
    rexuz $p |- ( M e. ZZ -> ( E. n e. ( ZZ>= ` M ) ph <->
               E. n e. ZZ ( M <_ n /\ ph ) ) ) $=
      ( cz wcel cv cle wbr wa cuz cfv eluz1 anbi1d anass syl6bb rexbidv2 ) CDEZ
      ACBFZGHZAIZBCJKZDQRUAEZAIRDEZSIZAIUCTIQUBUDACRLMUCSANOP $.
      $( [6-Oct-2005] $) $( [9-Sep-2005] $)

    $( Restricted existential quantification in a set of upper integers. $)
    rexuz2 $p |- ( E. n e. ( ZZ>= ` M ) ph <->
               ( M e. ZZ /\ E. n e. ZZ ( M <_ n /\ ph ) ) ) $=
      ( cuz cfv wrex cz wcel cv cle wbr wa w3a eluz2 df-3an bitri anbi1i anass
      an12 3bitri rexbii2 r19.42v ) ABCDEZFCGHZCBIZJKZALZLZBGFUDUGBGFLAUHBUCGUE
      UCHZALUDUEGHZLZUFLZALUKUGLZUJUHLZUIULAUIUDUJUFMULCUENUDUJUFOPQUKUFARUMUDU
      JUGLLUNUDUJUGRUDUJUGSPTUAUDUGBGUBP $.
      $( [17-Oct-2005] $) $( [9-Sep-2005] $)

    $( Double existential quantification in a set of upper integers. $)
    2rexuz $p |- ( E. m E. n e. ( ZZ>= ` m ) ph <->
                  E. m e. ZZ E. n e. ZZ ( m <_ n /\ ph ) ) $=
      ( cv cuz cfv wrex wex cz wcel cle wbr wa rexuz2 exbii df-rex bitr4i ) ACB
      DZEFGZBHRIJRCDKLAMCIGZMZBHTBIGSUABACRNOTBIPQ $.
      $( [4-Nov-2005] $) $( [3-Nov-2005] $)
  $}

  $( Second Peano postulate for a set of upper integers. $)
  peano2uz $p |- ( N e. ( ZZ>= ` M ) ->
                 ( N + 1 ) e. ( ZZ>= ` M ) ) $=
    ( cz wcel cle wbr w3a c1 caddc co cuz cfv 3simp1 peano2z 3ad2ant2 cr
    letrp1 zre syl3an2 syl3an1 3jca eluz2 3imtr4i ) ACDZBCDZABEFZGZUDBHIJZCDZAU
    HEFZGBAKLZDUHUKDUGUDUIUJUDUEUFMUEUDUIUFBNOAPDZUEUFUJUDULBPDUFUJUEABQBRSARTU
    AABUBAUHUBUC $.
    $( [12-Sep-2005] $) $( [7-Sep-2005] $)

  $( Reversed second Peano axiom for upper integers. $)
  peano2uzr $p |- ( ( M e. ZZ /\
             N e. ( ZZ>= ` ( M + 1 ) ) ) -> N e. ( ZZ>= ` M ) ) $=
    ( cz wcel c1 caddc co cuz cfv wa cmin wceq cc eluzelz zcn ax1cn npcan
    mpan2 3syl adantl eluzp1m1 peano2uz syl eqeltrrd ) ACDZBAEFGZHIDZJZBEKGZEFG
    ZBAHIZUGUJBLZUEUGBCDBMDZULUFBNBOUMEMDULPBEQRSTUHUIUKDUJUKDABUAAUIUBUCUD $.
    $( [3-Jan-2006] $) $( [2-Jan-2006] $)

  ${
    $d j K $.  $d j k M $.  $d j k N $.
    $( Addition closure law for a set of upper integers. $)
    uzaddcl $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. NN0 ) ->
                   ( N + K ) e. ( ZZ>= ` M ) ) $=
      ( vj vk cn0 wcel cuz cfv caddc co cv wi cc0 c1 wa wceq cc ax1cn addass
      mp3an3 cz eluzelz zcn syl nn0cn syl2an ancoms adantr peano2uz adantl
      eqeltrrd exp31 a2d addid1 eleq1d ibir opreq2 imbi2d nn0indALT impcom ) AF
      GCBHIZGZCAJKZVBGZVCCDLZJKZVBGZMVCCNJKZVBGZMVCCELZJKZVBGZMVCCVKOJKZJKZVBGZ
      MVCVEMDEAVKFGZVCVMVPVQVCVMVPVQVCPZVMPVLOJKZVOVBVRVSVOQZVMVCVQVTCRGZVKRGZV
      TVCVQWAWBORGVTSCVKOTUAVCCUBGWABCUCCUDUEZVKUFUGUHUIVMVSVBGVRBVLUJUKULUMUNV
      CVJVCVICVBVCWAVICQWCCUOUEUPUQVFNQZVHVJVCWDVGVIVBVFNCJURUPUSVFVKQZVHVMVCWE
      VGVLVBVFVKCJURUPUSVFVNQZVHVPVCWFVGVOVBVFVNCJURUPUSVFAQZVHVEVCWGVGVDVBVFAC
      JURUPUSUTVA $.
      $( [4-Jun-2006] $) $( [4-Jun-2006] $)
  $}

  ${
    $d j m N $.  $d j ps $.  $d j ch $.  $d j th $.  $d j ta $.
    $d k ph $.  $d j k m M $.
    $( Substitutions. $)
    uzind4.1 $e |- ( j = M -> ( ph <-> ps ) ) $.
    uzind4.2 $e |- ( j = k -> ( ph <-> ch ) ) $.
    uzind4.3 $e |- ( j = ( k + 1 ) -> ( ph <-> th ) ) $.
    uzind4.4 $e |- ( j = N -> ( ph <-> ta ) ) $.
    $( Basis. $)
    uzind4.5 $e |- ( M e. ZZ -> ps ) $.
    $( Induction hypothesis. $)
    uzind4.6 $e |- ( k e. ( ZZ>= ` M ) -> ( ch -> th ) ) $.
    $( Induction on the set of upper integers that starts at an integer
       ` M ` .  The first four hypotheses give us the substitution instances we
       need, and the last two are the basis and the induction hypothesis. $)
    uzind4 $p |- ( N e. ( ZZ>= ` M ) -> ta ) $=
      ( vm cz wcel cv cle wbr crab cuz cfv wa wi w3a eluz2 biimpri 3expb breq2
      elrab sylan2b syl uzind3 eluzel2 eluzelz eluzle jca sylibr sylanc ) HQRZI
      HPSZTUAZPQUBZRZEIHUCUDZRZABCDEFPGHIJKLMNVBGSZVERZUEVIVGRZCDUFVBVIQRZHVITU
      AZUEVKVJVBVLVMVKVKVBVLVMUGHVIUHUIUJVDVMPVIQVCVIHTUKULUMOUNUOHIUPVHIQRZHIT
      UAZUEVFVHVNVOHIUQHIURUSVDVOPIQVCIHTUKULUTVA $.
      $( [24-Sep-2005] $) $( [7-Sep-2005] $)
  $}

  ${
    $d j N $.  $d j ps $.  $d j ch $.  $d j th $.  $d j ta $.
    $d k ph $.  $d j k M $.
    $( Basis. $)
    uzind4ALT.5 $e |- ( M e. ZZ -> ps ) $.
    $( Induction hypothesis. $)
    uzind4ALT.6 $e |- ( k e. ( ZZ>= ` M ) -> ( ch -> th ) ) $.
    $( Substitutions. $)
    uzind4ALT.1 $e |- ( j = M -> ( ph <-> ps ) ) $.
    uzind4ALT.2 $e |- ( j = k -> ( ph <-> ch ) ) $.
    uzind4ALT.3 $e |- ( j = ( k + 1 ) -> ( ph <-> th ) ) $.
    uzind4ALT.4 $e |- ( j = N -> ( ph <-> ta ) ) $.
    $( Alternate version of ~ uzind4 with different hypothesis order for easier
       use with the Metamath Proof Assistant, since "assign last" will assign
       the substitutions first.  (This may or may not be kept permanenently,
       or it may replace ~ uzind4 - I haven't decided yet. -nm) $)
    uzind4ALT $p |- ( N e. ( ZZ>= ` M ) -> ta ) $=
      ( uzind4 ) ABCDEFGHILMNOJKP $.
      $( [14-Jan-2006] $) $( [7-Sep-2005] $)
  $}

  ${
    $d m k j M $.  $d j N $.  $d j m ph $.
    $( Basis. $)
    uzind4s.1 $e |- ( M e. ZZ -> [ M / k ] ph ) $.
    $( Induction hypothesis. $)
    uzind4s.2 $e |- ( k e. ( ZZ>= ` M ) -> ( ph -> [ ( k + 1 ) / k ] ph ) ) $.
    $( Induction on the set of upper integers that starts at an integer
       ` M ` , using explicit substitution.  The hypotheses are the basis and
       the induction hypothesis. $)
    uzind4s $p |- ( N e. ( ZZ>= ` M ) -> [ N / k ] ph ) $=
      ( vj vm cv wsbc c1 caddc co dfsbcq cuz cfv wcel wi ax-17 visset hbsbc1v
      oprex hbim wceq eleq1 sbequ12 wb opreq1 syl imbi12d chvar uzind4 ) ABGIZJ
      ABCJABHIZJZABUNKLMZJZABDJGHCDABUMCNABUMUNNABUMUPNABUMDNEBIZCOPZQZAABURKLM
      ZJZRZRUNUSQZUOUQRZRBHVDVEBVDBSUOUQBABUNHTUAABUPUNKLUBUAUCUCURUNUDZUTVDVCV
      EURUNUSUEVFAUOVBUQABHUFVFVAUPUDVBUQUGURUNKLUHABVAUPNUIUJUJFUKUL $.
      $( [21-Feb-2006] $) $( [4-Nov-2005] $)
  $}

  ${
    $d k m n M $.  $d m N $.  $d k m n ph $.  $d j k m n $.
    $( Basis. $)
    uzind4s2.1 $e |- ( M e. ZZ -> [ M / j ] ph ) $.
    $( Induction hypothesis. $)
    uzind4s2.2 $e |- ( k e. ( ZZ>= ` M ) ->
                     ( [ k / j ] ph -> [ ( k + 1 ) / j ] ph ) ) $.
    $( Induction on the set of upper integers that starts at an integer
       ` M ` , using explicit substitution.  The hypotheses are the basis and
       the induction hypothesis.  Use this instead of ~ uzind4s when ` j `
       and ` k ` must be distinct in ` [ ( k + 1 ) / j ] ph ` . $)
    uzind4s2 $p |- ( N e. ( ZZ>= ` M ) -> [ N / j ] ph ) $=
      ( vm vn cv wsbc c1 caddc co dfsbcq cuz cfv wcel wi ax-17 hbim wceq eleq1
      wb opreq1 syl imbi12d chvar uzind4 ) ABHJZKABDKABIJZKZABUKLMNZKZABEKHIDEA
      BUJDOABUJUKOABUJUMOABUJEOFCJZDPQZRZABUOKZABUOLMNZKZSZSUKUPRZULUNSZSCIVBVC
      CVBCTULUNCULCTUNCTUAUAUOUKUBZUQVBVAVCUOUKUPUCVDURULUTUNABUOUKOVDUSUMUBUTU
      NUDUOUKLMUEABUSUMOUFUGUGGUHUI $.
      $( [7-Mar-2006] $) $( [16-Nov-2005] $)
  $}

  ${
    $d j m N $.  $d j ps $.  $d j ch $.  $d j th $.  $d j ta $.
    $d k ph $.  $d j k m M $.
    $( Lower bound. $)
    uzind4i.1 $e |- M e. ZZ $.
    $( Substitutions. $)
    uzind4i.2 $e |- ( j = M -> ( ph <-> ps ) ) $.
    uzind4i.3 $e |- ( j = k -> ( ph <-> ch ) ) $.
    uzind4i.4 $e |- ( j = ( k + 1 ) -> ( ph <-> th ) ) $.
    uzind4i.5 $e |- ( j = N -> ( ph <-> ta ) ) $.
    $( Basis. $)
    uzind4i.6 $e |- ps $.
    $( Induction hypothesis. $)
    uzind4i.7 $e |- ( k e. ( ZZ>= ` M ) -> ( ch -> th ) ) $.
    $( Induction on the upper integers that start at ` M ` .  The first
       hypothesis specifies the lower bound, the next four give us the
       substitution instances we need, and the last two are the basis and the
       induction hypothesis. $)
    uzind4i $p |- ( N e. ( ZZ>= ` M ) -> ta ) $=
      ( vm cuz cfv wcel cv cle wbr cz crab wceq uzval ax-mp eleq2i a1i wi
      sylbir adantl uzind3 mpan sylbi ) IHRSZTIHQUAUBUCQUDUEZTZEUQURIHUDTZUQURU
      FJQHUGUHZUIUTUSEJABCDEFQGHIKLMNBUTOUJGUAZURTZCDUKZUTVCVBUQTVDUQURVBVAUIPU
      LUMUNUOUP $.
      $( [1-Oct-2005] $) $( [4-Sep-2005] $)
  $}

  ${
    $d h t n m M $.  $d h j t k m n S $.
    $( Well-ordering principle:  any non-empty subset of a set of upper
       integers has a least element. $)
    uzwo $p |- ( ( S (_ ( ZZ>= ` M ) /\ S =/= (/) ) ->
               E. j e. S A. k e. S j <_ k ) $=
      ( vt vn vh vm cuz cfv wss c0 wne wa cv cle wbr wral wrex wn wcel wal
      wceq wi c1 caddc co breq1 ralbidv imbi2d cz ssel eluzle syl6 r19.21aiv
      adantr a1i eluzelz rcla4ev expcom con3d com12 wb cr letri3 zre syl2an
      clt zleltp1 ltnle peano2re syl bitrd ancoms anbi2d ssel2 sylan2 eleq1a
      ad2antll sylbird exp3a con1 com23 exp32 com34 imp41 r19.20dva ex sylan9r
      pm2.43d exp31 imp3a uzssz sstr mpan2 sylani a2d uzind4 adantl sylcom
      adantrd pm2.61i 19.21adv eq0 syl6ibr necon1ad imp breq2 cbvralv rexbii
      sylib ) ADIJZKZALMZNBOZEOZPQZEARZBASZXOCOZPQZCARZBASXMXNXSXMXSALXMXSTZFOZ
      AUAZTZFUBALUCXMYCYFFXMYCYFYDXLUAZXMYCNZYFUDYGYHYDXPPQZEARZYFYHGOZXPPQZEAR
      ZUDYHDXPPQZEARZUDZYHHOZXPPQZEARZUDYHYQUEUFUGZXPPQZEARZUDYHYJUDGHDYDYKDUCZ
      YMYOYHUUCYLYNEAYKDXPPUHUIUJYKYQUCZYMYSYHUUDYLYREAYKYQXPPUHUIUJYKYTUCZYMUU
      BYHUUEYLUUAEAYKYTXPPUHUIUJYKYDUCZYMYJYHUUFYLYIEAYKYDXPPUHUIUJYPDUKUAXMYOY
      CXMYNEAXMXPAUAZXPXLUAYNAXLXPULDXPUMUNUOUPUQYQXLUAZYHYSUUBUUHAUKKZYCYSUUBU
      DZXMUUHYQUKUAZUUIYCNUUJUDDYQURUUKUUIYCUUJUUKUUIYCUUJUUKUUINZYCNYSUUBYCYSY
      QAUAZTZUULUUJYSYCUUNYSUUMXSUUMYSXSXRYSBYQAXOYQUCXQYREAXOYQXPPUHUIUSUTVAVB
      UULUUNUUJUULUUNNYRUUAEAUUKUUIUUNUUGYRUUAUDZUUKUUIUUGUUNUUOUUKUUIUUGUUNUUO
      UDUUKUUIUUGNZNZYRUUNUUAUUQYRUUATZUUMUDUUNUUAUDUUQYRUURUUMUUQYRUURNZYQXPUC
      ZUUMUUKXPUKUAZUUTUUSVCUUPUUKUVANZUUTYRXPYQPQZNZUUSYQVDUAZXPVDUAZUUTUVDVCU
      UKUVAYQXPVEYQVFZXPVFZVGUVBUVCUURYRUVAUUKUVCUURVCUVAUUKNUVCXPYTVHQZUURXPYQ
      VIUVFYTVDUAZUVIUURVCUVAUUKXPYTVJUVHUUKUVEUVJUVGYQVKVLVGVMVNVOVMAUKXPVPVQU
      UGUUTUUMUDUUKUUIXPAYQVRVSVTWAUUAUUMWBUNWCWDWEWFWGWHWIWJWKWLVLXMXLUKKUUIDW
      MAXLUKWNWOWPWQWRYCYJYFUDXMYJYCYFYJYEXSYEYJXSXRYJBYDAXOYDUCXQYIEAXOYDXPPUH
      UIUSUTVAVBWSWTYGTZXMYFYCXMUVKYFXMYEYGAXLYDULVAVBXAXBWHXCFAXDXEXFXGXRYBBAX
      QYAECAXPXTXOPXHXIXJXK $.
      $( [17-Apr-2007] $) $( [8-Oct-2005] $)

    $( Well-ordering principle:  any non-empty subset of the upper integers
       has a least element. $)
    uzwoOLD $p |- ( ( S (_ ( ZZ>= ` M ) /\ -. S = (/) ) ->
               E. j e. S A. k e. S j <_ k ) $=
      ( vt vn vh vm cuz cfv wss c0 wceq wn wa cv cle wbr wral wrex wcel wal wi
      c1 caddc co breq1 ralbidv imbi2d cz ssel eluzle syl6 r19.21aiv adantr
      a1i eluzelz rcla4ev expcom con3d com12 wb cr letri3 zre syl2an clt
      zleltp1 ltnle peano2re syl bitrd ancoms anbi2d ssel2 sylan2 eleq1a
      ad2antll sylbird exp3a con1 com23 exp32 com34 imp41 r19.20dva ex sylan9r
      pm2.43d exp31 imp3a uzssz sstr mpan2 sylani a2d uzind4 adantl sylcom
      adantrd pm2.61i 19.21adv eq0 syl6ibr con1d imp breq2 cbvralv rexbii
      sylib ) ADIJZKZALMZNZOBPZEPZQRZEASZBATZXOCPZQRZCASZBATXLXNXSXLXSXMXLXSNZF
      PZAUAZNZFUBXMXLYCYFFXLYCYFYDXKUAZXLYCOZYFUCYGYHYDXPQRZEASZYFYHGPZXPQRZEAS
      ZUCYHDXPQRZEASZUCZYHHPZXPQRZEASZUCYHYQUDUEUFZXPQRZEASZUCYHYJUCGHDYDYKDMZY
      MYOYHUUCYLYNEAYKDXPQUGUHUIYKYQMZYMYSYHUUDYLYREAYKYQXPQUGUHUIYKYTMZYMUUBYH
      UUEYLUUAEAYKYTXPQUGUHUIYKYDMZYMYJYHUUFYLYIEAYKYDXPQUGUHUIYPDUJUAXLYOYCXLY
      NEAXLXPAUAZXPXKUAYNAXKXPUKDXPULUMUNUOUPYQXKUAZYHYSUUBUUHAUJKZYCYSUUBUCZXL
      UUHYQUJUAZUUIYCOUUJUCDYQUQUUKUUIYCUUJUUKUUIYCUUJUUKUUIOZYCOYSUUBYCYSYQAUA
      ZNZUULUUJYSYCUUNYSUUMXSUUMYSXSXRYSBYQAXOYQMXQYREAXOYQXPQUGUHURUSUTVAUULUU
      NUUJUULUUNOYRUUAEAUUKUUIUUNUUGYRUUAUCZUUKUUIUUGUUNUUOUUKUUIUUGUUNUUOUCUUK
      UUIUUGOZOZYRUUNUUAUUQYRUUANZUUMUCUUNUUAUCUUQYRUURUUMUUQYRUUROZYQXPMZUUMUU
      KXPUJUAZUUTUUSVBUUPUUKUVAOZUUTYRXPYQQRZOZUUSYQVCUAZXPVCUAZUUTUVDVBUUKUVAY
      QXPVDYQVEZXPVEZVFUVBUVCUURYRUVAUUKUVCUURVBUVAUUKOUVCXPYTVGRZUURXPYQVHUVFY
      TVCUAZUVIUURVBUVAUUKXPYTVIUVHUUKUVEUVJUVGYQVJVKVFVLVMVNVLAUJXPVOVPUUGUUTU
      UMUCUUKUUIXPAYQVQVRVSVTUUAUUMWAUMWBWCWDWEWFWGWHWIWJWKVKXLXKUJKUUIDWLAXKUJ
      WMWNWOWPWQYCYJYFUCXLYJYCYFYJYEXSYEYJXSXRYJBYDAXOYDMXQYIEAXOYDXPQUGUHURUSU
      TVAWRWSYGNZXLYFYCXLUVKYFXLYEYGAXKYDUKUTVAWTXAWGXBFAXCXDXEXFXRYBBAXQYAECAX
      PXTXOQXGXHXIXJ $.
      $( [10-Oct-2005] $) $( [8-Oct-2005] $)

    $( Well-ordering principle:  any non-empty subset of upper integers has a
       unique least element. $)
    uzwo2 $p |- ( ( S (_ ( ZZ>= ` M ) /\ S =/= (/) ) ->
               E! j e. S A. k e. S j <_ k ) $=
      ( cuz cfv wss c0 wne cv cle wbr wral wrex wreu uzwo cr lbreu cz uzssz
      zssre sstri sstr mpan2 sylan syldan ) ADEFZGZAHIBJCJKLCAMZBANZUIBAOZABCDP
      AQGZUJUKUHBCARUHUGQGULUGSQDTUAUBAUGQUCUDUEUF $.
      $( [22-Apr-2007] $) $( [8-Oct-2005] $)
  $}

  ${
    $d x y A $.
    $( Well-ordering principle:  any non-empty set of natural numbers has a
       least element.  Theorem I.37 (well-ordering principle) of [Apostol]
       p. 34. $)
    nnwo $p |- ( ( A (_ NN /\ A =/= (/) ) -> E. x e. A A. y e. A x <_ y ) $=
      ( c1 cuz cfv wss c0 wceq wn cv cle wbr wral wrex cn wne uzwoOLD nnuz
      sseq2i df-ne syl2anb ) CDEFZGCHIJAKBKLMBCNACOCPGCHQCABDRPUCCSTCHUAUB $.
      $( [11-Nov-2004] $) $( [17-Aug-2001] $)
  $}

  ${
    $d x y z w v $.  $d w v z A $.
    nnwof.1 $e |- ( z e. A -> A. x z e. A ) $.
    nnwof.2 $e |- ( z e. A -> A. y z e. A ) $.
    $( Well-ordering principle:  any non-empty set of natural numbers has a
       least element.  This version allows ` x ` and ` y ` to be present in
       ` A ` as long as they are effectively not free. $)
    nnwof $p |- ( ( A (_ NN /\ A =/= (/) ) -> E. x e. A A. y e. A x <_ y ) $=
      ( vw vv cn wss c0 wne wa cv cle wbr wral wrex nnwo wcel wi wal wex ax-17
      hbel hbim wceq eleq1 breq2 imbi12d cbval anbi2i exbii hbal hban breq1
      imbi2d albidv anbi12d cbvex bitri df-rex 3bitr4i df-ral rexbii sylib ) DI
      JDKLMGNZHNZOPZHDQZGDRZANZBNZOPZBDQZADRZGHDSVHDTZVIUAZHUBZGDRZVMDTZVNUAZBU
      BZADRZVKVPVGDTZVSMZGUCZVLDTZWCMZAUCZVTWDWGWEWAVGVMOPZUAZBUBZMZGUCWJWFWNGV
      SWMWEVRWLHBVQVIBBCCVHDCNZVHTBUDFUEVIBUDUFWLHUDVHVMUGVQWAVIWKVHVMDUHVHVMVG
      OUIUJUKULUMWNWIGAWEWMAACCVGDWOVGTAUDEUEWLABWAWKAACCVMDWOVMTAUDEUEWKAUDUFU
      NUOWIGUDVGVLUGZWEWHWMWCVGVLDUHWPWLWBBWPWKVNWAVGVLVMOUPUQURUSUTVAVSGDVBWCA
      DVBVCVJVSGDVIHDVDVEVOWCADVNBDVDVEVCVF $.
      $( [17-Aug-2001] $)
  $}

  ${
    $d x y z $.  $d y z ph $.  $d x ps $.
    nnwos.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Well-ordering principle:  any non-empty set of natural numbers has a
       least element (schema form). $)
    nnwos $p |- ( E. x e. NN ph ->
                  E. x e. NN ( ph /\ A. y e. NN ( ps -> x <_ y ) ) ) $=
      ( vz cn crab wss c0 wne wa cv cle wbr wral wrex wi hbrab1 wcel ax-17
      nnwof ssrab2 biantrur rabn0 bitr3i wex wal df-rex rabid df-ral elrab
      imbi1i impexp bitri albii anbi12i exbii anbi2i anass bitr4i 3bitri
      3imtr3i ) ACGHZGIZVDJKZLZCMZDMZNOZDVDPZCVDQZACGQZABVJRZDGPZLZCGQZCDFVDACF
      GSFMVDTDUAUBVGVFVMVEVFACGUCUDACGUEUFVLVHVDTZVKLZCUGVHGTZALZVIGTZVNRZDUHZL
      ZCUGZVQVKCVDUIVSWECVRWAVKWDACGUJVKVIVDTZVJRZDUHWDVJDVDUKWHWCDWHWBBLZVJRWC
      WGWIVJABCVIGEULUMWBBVJUNUOUPUOUQURWFVTVPLZCUGVQWEWJCWEWAVOLWJVOWDWAVNDGUK
      USVTAVOUTUFURVPCGUIVAVBVC $.
      $( [17-Aug-2001] $)
  $}

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    $( Substitutions. $)
    indstr.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Induction hypothesis. $)
    indstr.2 $e |- ( x e. NN -> ( A. y e. NN ( y < x -> ps ) -> ph ) ) $.
    $( Strong Mathematical Induction for positive integers (inference
       schema). $)
    indstr $p |- ( x e. NN -> ph ) $=
      ( cn wral wn wrex cv cle wbr wi wa wcel pm3.24 clt cr wb lenlt nnre
      syl2an imbi2d con34b syl6bbr ralbidva sylbid anim2d ancom syl6ib mtoi
      nrex wceq notbid nnwos mto dfral2 mpbir rspec ) ACGACGHAIZCGJZIVBVABIZCKZ
      DKZLMZNZDGHZOZCGJVICGVDGPZVIAVAOZAQVJVIVAAOVKVJVHAVAVJVHVEVDRMZBNZDGHAVJV
      GVMDGVJVEGPZOZVGVCVLIZNVMVOVFVPVCVDSPVESPVFVPTVJVNVDVEUAVDUBVEUBUCUDVLBUE
      UFUGFUHUIVAAUJUKULUMVAVCCDVDVEUNABEUOUPUQACGURUSUT $.
      $( [17-Aug-2001] $)
  $}

  ${
    $d j k M $.
    uzinfm.1 $e |- M e. ZZ $.
    $( Extract the lower bound of a set of upper integers as its infimum.  Note
       that the " ` ``' < ` " argument turns supremum into infimum (for which
       we do not currently have a separate notation). $)
    uzinfmi $p |- sup ( ( ZZ>= ` M ) , RR , `' < ) = M $=
      ( vj vk cuz cfv cr clt ccnv csup cv cle wbr wral crab cuni wss wrex wceq
      cz uzssz zssre sstri wcel uzid ax-mp eluzle rgen breq1 ralbidv rcla4ev
      mp2an lbinfm wreu wb lbreu reuuni2 mpbi eqtri ) AEFZGHIJZCKZDKZLMZDUTNZCU
      TOPZAUTGQZVECUTRZVAVFSUTTGAUAUBUCZAUTUDZAVCLMZDUTNZVHATUDVJBAUEUFZVKDUTAV
      CUGUHZVEVLCAUTVBASVDVKDUTVBAVCLUIUJZUKULZCDUTUMULVLVFASZVNVJVECUTUNZVLVQU
      OVMVGVHVRVIVPCDUTUPULVEVLCUTAVOUQULURUS $.
      $( [8-Oct-2005] $) $( [7-Oct-2005] $)
  $}

  $( The infimum of the set of natural numbers is one. $)
  nninfm $p |- sup ( NN , RR , `' < ) = 1 $=
    ( cn cr clt ccnv csup c1 cuz cfv wceq nnuz supeq1 ax-mp 1z uzinfmi eqtri )
    ABCDZEZFGHZBPEZFARIQSIJBARPKLFMNO $.
    $( [24-Jun-2005] $) $( [16-Jun-2005] $)

  $( The infimum of the set of nonnegative integers is zero.  Note that
     " ` ``' < ` " turns sup into inf. $)
  nn0infm $p |- sup ( NN0 , RR , `' < ) = 0 $=
    ( cn0 cr clt ccnv csup cc0 cuz cfv wceq nn0uz supeq1 ax-mp 0z uzinfmi
    eqtri ) ABCDZEZFGHZBPEZFARIQSIJBARPKLFMNO $.
    $( [10-Jul-2005] $) $( [16-Jun-2005] $)

  ${
    $d j k S $.  $d k A $.
    $( The infimum of a subset of a set of upper integers is less than
       or equal to all members of the subset.  Note that the " ` ``' < ` "
       argument turns supremum into infimum (for which we do not currently have
       a separate notation). $)
    infmssuzle $p |- ( ( S (_ ( ZZ>= ` M ) /\ S =/= (/) /\ A e. S ) ->
                sup ( S , RR , `' < ) <_ A ) $=
      ( vj vk cr wss cv cle wbr wral wrex wcel clt ccnv csup cuz cfv c0 wne
      w3a lbinfmle cz uzssz zssre sstri sstr mpan2 3ad2ant1 uzwo 3adant3
      3simp3 syl3anc ) BFGZDHEHIJEBKDBLZABMZBFNOPAIJBCQRZGZBSTZUPUADEABUBURUSUN
      UPURUQFGUNUQUCFCUDUEUFBUQFUGUHUIURUSUOUPBDECUJUKURUSUPULUM $.
      $( [2-Apr-2009] $) $( [11-Oct-2005] $)

    $( The infimum of a subset of a set of upper integers is less than
       or equal to all members of the subset.  Note that the " ` ``' < ` "
       argument turns supremum into infimum (for which we do not currently have
       a separate notation). $)
    infmssuzleOLD $p |- ( ( S (_ ( ZZ>= ` M ) /\ -. S = (/) /\ A e. S ) ->
                sup ( S , RR , `' < ) <_ A ) $=
      ( vj vk cr wss cv cle wbr wral wrex wcel clt ccnv csup cuz cfv c0 wceq
      wn w3a lbinfmle cz uzssz zssre sstri sstr mpan2 3ad2ant1 uzwoOLD 3adant3
      3simp3 syl3anc ) BFGZDHEHIJEBKDBLZABMZBFNOPAIJBCQRZGZBSTUAZUQUBDEABUCUSUT
      UOUQUSURFGUOURUDFCUEUFUGBURFUHUIUJUSUTUPUQBDECUKULUSUTUQUMUN $.
      $( [18-Nov-2006] $) $( [11-Oct-2005] $)

    $( The infimum of a subset of a set of upper integers belongs to the
       subset. $)
    infmssuzcl $p |- ( ( S (_ ( ZZ>= ` M ) /\ S =/= (/) ) ->
                  sup ( S , RR , `' < ) e. S ) $=
      ( vj vk cr wss cv cle wbr wral wrex clt ccnv csup wcel cuz cfv c0 wne wa
      lbinfmcl cz uzssz zssre sstri sstr mpan2 adantr wceq wn uzwoOLD df-ne
      sylan2b sylanc ) AEFZCGDGHIDAJCAKZAELMNAOABPQZFZARSZTCDAUAURUOUSURUQEFUOU
      QUBEBUCUDUEAUQEUFUGUHURARUIUJUPUSACDBUKARULUMUN $.
      $( [10-May-2007] $) $( [11-Oct-2005] $)
  $}

