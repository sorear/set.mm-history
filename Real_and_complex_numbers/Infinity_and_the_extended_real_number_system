$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Infinity and the extended real number system
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c +oo $. $( Plus infinity $)
  $c -oo $. $( Minus infinity $)
  $c RR* $. $( The set of extended reals $)
  $c < $. $( 'Less than' relation (over extended reals) $)

  $( Plus infinity. $)
  cpnf $a class +oo $.
  $( Minus infinity. $)
  cmnf $a class -oo $.
  $( The set of extended reals (includes plus and minus infinity). $)
  cxr $a class RR* $.
  $( 'Less than' predicate (extended to include the extended reals). $)
  clt $a class < $.

  $( Define plus infinity.  Note that the definition is arbitrary, requiring
     only that ` +oo ` be a set not in ` RR ` and different from ` -oo `
     ( ~ df-mnf ).  We use ` ~P U. CC ` to make it independent of the
     construction of ` CC ` , and Cantor's Theorem will show that it is
     different from any member of ` CC ` and therefore ` RR ` .  See ~ pnfnre ,
     ~ mnfnre , and ~ pnfnemnf .

     A simpler possibility is to define ` +oo ` as ` CC ` and ` -oo ` as
     ` { CC } ` , but that approach requires the Axiom of Regularity to show
     that ` +oo ` and ` -oo ` are different from each other and from all
     members of ` RR ` . $)
  df-pnf $a |- +oo = ~P U. CC $.

  $( Define minus infinity as the power set of plus infinity.  Note that the
     definition is arbitrary, requiring only that ` -oo ` be a set not in
     ` RR ` and different from ` +oo ` (see ~ mnfnre and ~ pnfnemnf ). $)
  df-mnf $a |- -oo = ~P +oo $.

  $( Define the set of extended reals that includes plus and minus infinity.
     Definition 12-3.1 of [Gleason] p. 173. $)
  df-xr $a |- RR* = ( RR u. { +oo , -oo } ) $.

  $( Define 'less than' on the set of extended reals.  Definition 12-3.1 of
     [Gleason] p. 173.  The clipping of ` <RR ` makes our definition
     independent of the complex number construction, since the postulates don't
     presuppose that ` <RR ` is a relation on ` RR ` . $)
  df-ltxr $a |- < = ( ( ( <RR i^i ( RR X. RR ) ) u. { <. -oo , +oo >. } ) u.
                ( ( RR X. { +oo } ) u. ( { -oo } X. RR ) ) ) $.

  $( Define 'less than or equal to' on the extended real subset of complex
     numbers.  Theorem ~ leloe relates it to 'less than' for reals. $)
  df-le $a |- <_ = ( ( RR* X. RR* ) \ `' < ) $.

  $( The set of extended reals exists. $)
  xrex $p |- RR* e. _V $=
    ( cxr cr cpnf cmnf cpr cun cvv df-xr reex prex unex eqeltri ) ABCDEZFGHBMIC
    DJKL $.
    $( [24-Dec-2006] $)

  $( Plus infinity belongs to the set of extended reals.  (The proof was
     shortened by Anthony Hart, 29-Aug-2011.) $)
  pnfxr $p |- +oo e. RR* $=
    ( cpnf cr cmnf cpr cun cxr ssun2 cc cuni cpw cvv df-pnf axcnex uniex pwex
    eqeltri prid1 sselii df-xr eleqtrri ) ABACDZEZFUAUBAUABGACAHIZJKLUCHMNOPQRS
    T $.
    $( [29-Aug-2011] $) $( [13-Oct-2005] $)

  $( Plus infinity belongs to the set of extended reals. $)
  pnfxrOLD $p |- +oo e. RR* $=
    ( cpnf cr cmnf cpr cun cxr wcel wo cc cuni cpw cvv df-pnf axcnex uniex
    pwex eqeltri prid1 olci elun mpbir df-xr eleqtrri ) ABACDZEZFAUEGABGZAUDGZH
    UGUFACAIJZKLMUHINOPQRSABUDTUAUBUC $.
    $( [6-May-2008] $) $( [13-Oct-2005] $)

  $( Minus infinity belongs to the set of extended reals.  (The proof was
     shortened by Anthony Hart, 29-Aug-2011.)  (The proof was shortened by
     Andrew Salmon, 19-Nov-2011.) $)
  mnfxr $p |- -oo e. RR* $=
    ( cmnf cr cpnf cpr cun cxr wcel cpw cvv df-mnf pnfxr elisseti pwex eqeltri
    prid2 elun2 ax-mp df-xr eleqtrri ) ABCADZEZFATGAUAGCAACHIJCCFKLMNOATBPQRS
    $.
    $( [19-Nov-2011] $) $( [13-Oct-2005] $)

  $( Minus infinity belongs to the set of extended reals.  (The proof was
     shortened by Anthony Hart, 29-Aug-2011.) $)
  mnfxrOLD $p |- -oo e. RR* $=
    ( cmnf cr cpnf cpr cun cxr wcel cpw cvv df-mnf cc cuni df-pnf axcnex uniex
    pwex eqeltri prid2 elun2 ax-mp df-xr eleqtrri ) ABCADZEZFAUCGAUDGCAACHIJCCK
    LZHIMUEKNOPQPQRAUCBSTUAUB $.
    $( [29-Aug-2011] $) $( [13-Oct-2005] $)


  $( The 'less than' binary relation on the set of extended reals.  Definition
     12-3.1 of [Gleason] p. 173. $)
  ltxr $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A < B <->
            ( ( ( ( A e. RR /\ B e. RR ) /\ A <RR B ) \/
                 ( A = -oo /\ B = +oo ) ) \/ ( ( A e. RR /\ B = +oo )
                \/ ( A = -oo /\ B e. RR ) ) ) ) ) $=
    ( cxr wcel wa cop cltrr cr cxp cin cmnf cpnf csn cun wo wbr wceq clt wb
    df-br bicomi a1i opelxpg anbi12d elin ancom 3bitr4g adantl pnfxr opthgg
    mp3an3 opex elsnc syl5bb orbi12d elun elsncg anbi2d bitrd anbi1d sylan9bbr
    df-ltxr eleq2i 3bitri ) ACDZBCDZEZABFZGHHIZJZKLFZMZNZDZVHHLMZIZKMZHIZNZDZOZ
    AHDZBHDZEZABGPZEZAKQZBLQZEZOZWBWHEZWGWCEZOZOABRPZVGVNWJVTWMVGVHVJDZVHVLDZOW
    JVNVGWOWFWPWIVFWOWFSVEVFVHGDZVHVIDZEWEWDEWOWFVFWQWEWRWDWQWESVFWEWQABGTUAUBA
    BHHCUCUDVHGVIUEWDWEUFUGUHVGVHVKQZWIWPVEVFLCDWSWISUIABKLCCCUJUKVHVKABULUMUNU
    OVHVJVLUPUNVGVHVPDZVHVRDZOWMVTVGWTWKXAWLVFWTWKSVEVFWTWBBVODZEWKABHVOCUCVFXB
    WHWBBLCUQURUSUHVFXAAVQDZWCEVEWLABVQHCUCVEXCWGWCAKCUQUTVAUOVHVPVRUPUNUOWNVHR
    DVHVMVSNZDWAABRTRXDVHVBVCVHVMVSUPVDUN $.
    $( [14-Oct-2005] $)

  $( Plus infinity is not a real number. $)
  pnfnre $p |- +oo e/ RR $=
    ( cpnf cr wnel wcel wn cc cuni cpw pwuninel df-pnf eleq1i mtbir recn mto
    df-nel mpbir ) ABCABDZEQAFDZRFGHZFDFIASFJKLAMNABOP $.
    $( [6-May-2008] $) $( [13-Oct-2005] $)

  $( Minus infinity is not a real number. $)
  mnfnre $p |- -oo e/ RR $=
    ( cmnf cr wnel wcel wn cc cuni cpw 2pwuninel cpnf df-mnf wceq df-pnf pweq
    ax-mp eqtri eleq1i mtbir recn mto df-nel mpbir ) ABCABDZEUCAFDZUDFGHZHZFDFI
    AUFFAJHZUFKJUELUGUFLMJUENOPQRASTABUAUB $.
    $( [6-May-2008] $) $( [13-Oct-2005] $)

  $( The standard reals are a subset of the extended reals. $)
  ressxr $p |- RR C_ RR* $=
    ( cr cpnf cmnf cpr cun cxr ssun1 df-xr sseqtr4i ) AABCDZEFAJGHI $.
    $( [14-Oct-2005] $)

  $( A standard real is an extended real. $)
  rexr $p |- ( A e. RR -> A e. RR* ) $=
    ( cr cxr ressxr sseli ) BCADE $.
    $( [14-Oct-2005] $)

  $( The standard less-than ` <RR ` and the extended real less-than ` < ` are
     identical when restricted to the non-extended reals ` RR ` . $)
  ltxrlt $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> A <RR B ) ) $=
    ( cr wcel wa clt wbr cltrr cmnf wceq cpnf wo cxr wb ltxr rexr syl2an ibar
    wn ioran wnel pnfnre df-nel mpbi eleq1 mtbiri con2i intnand adantl mnfnre
    intnanrd anim12i sylibr ancoms sylanbrc biorf syl bitr2d orass orcom bitri
    syl5bb bitrd ) ACDZBCDZEZABFGZVFABHGZEZAIJZBKJZEZLVDVKEZVJVEEZLZLZVHAMDBMDV
    GVPNVDVEABOAPBPQVFVLVOLZVILZVHVPVFVHVIVRVFVHRVFVQSZVIVRNVSVLSZVOSZVFVLVOTVE
    VTVDVEVKVJVKVEVKVEKCDZKCUAWBSUBKCUCUDBKCUEUFUGZUHUIVEVDWAVEVDEVMSZVNSZEWAVE
    WDVDWEVEVKVDWCUHVDVJVEVJVDVJVDICDZICUAWFSUJICUCUDAICUEUFUGUKULVMVNTUMUNUOVQ
    VIUPUQURVPVIVQLVRVIVLVOUSVIVQUTVAVBVC $.
    $( [13-Oct-2005] $)

  $( 'Less than or equal to' expressed in terms of 'less than', for extended
     reals. $)
  xrlenlt $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A <_ B <-> -. B < A ) ) $=
    ( cxr wcel wa cle wbr cop clt ccnv wn cxp wb opelxpi cdif df-le eleq2i
    eldif bitri baib syl df-br syl5bb opelcnvg syl6rbbr notbid bitr4d ) ACDBCDE
    ZABFGZABHZIJZDZKZBAIGZKUHUJFDZUMUIUHUJCCLZDZUOUMMABCCNUOUQUMUOUJUPUKOZDUQUM
    EFURUJPQUJUPUKRSTUAABFUBUCUHUNULUHULBAHIDUNABCCIUDBAIUBUEUFUG $.
    $( [14-Oct-2005] $)

  $( 'Less than' expressed in terms of 'less than or equal to', for extended
     reals. $)
  xrltnle $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A < B <-> -. B <_ A ) ) $=
    ( cxr wcel clt wbr cle wn wb wa xrlenlt con2bid ancoms ) BCDZACDZABEFZBAGFZ
    HINOJQPBAKLM $.
    $( [6-Feb-2007] $)

