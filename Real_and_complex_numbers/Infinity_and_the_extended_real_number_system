$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Infinity and the extended real number system
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c +oo $. $( Plus infinity $)
  $c -oo $. $( Minus infinity $)
  $c RR* $. $( The set of extended reals $)
  $c < $. $( 'Less than' relation (over extended reals) $)

  $( Plus infinity. $)
  cpnf $a class +oo $.
  $( Minus infinity. $)
  cmnf $a class -oo $.
  $( The set of extended reals (includes plus and minus infinity). $)
  cxr $a class RR* $.
  $( 'Less than' predicate (extended to include the extended reals). $)
  clt $a class < $.

  $( Define plus infinity.  Note that the definition is arbitrary, requiring
     only that ` +oo ` be a new set not in ` RR ` and different from
     ` -oo ` .  We use ` CC ` to make it independent of the construction of
     ` CC ` .  The Axiom of Regularity ensures that it does not belong to
     ` CC ` (and in particular ` RR ` ).  There appears to be no way to define
     ` +oo ` in a construction-independent way without invoking Regularity,
     which is interesting because it is generally thought that Regularity is
     not important for "ordinary" mathematics (see Axiom of Regularity in
     [Schechter] p. 31). $)
  df-pnf $a |- +oo = CC $.

  $( Define minus infinity.  Note that the definition is arbitrary, requiring
     only that ` -oo ` be a new set not in ` RR ` and different from
     ` +oo ` .  We use ` { CC } ` to make it independent of the
     construction of ` CC ` .  The Axiom of Regularity ensures that it does
     not belong to ` CC ` (or ` RR ` ). $)
  df-mnf $a |- -oo = { CC } $.

  $( Define the set of extended reals that includes plus and minus infinity.
     Definition 12-3.1 of [Gleason] p. 173. $)
  df-xr $a |- RR* = ( RR u. { +oo , -oo } ) $.

  $( Define 'less than' on the set of extended reals.  Definition 12-3.1 of
     [Gleason] p. 173.  The clipping of ` <RR ` makes our definition
     independent of the complex number construction, since the postulates don't
     presuppose that ` <RR ` is a relation on ` RR ` .  $)
  df-ltxr $a |- < = ( ( ( <RR i^i ( RR X. RR ) ) u. { <. -oo , +oo >. } ) u.
                ( ( RR X. { +oo } ) u. ( { -oo } X. RR ) ) ) $.

  $( Define 'less than or equal to' on the extended real subset of complex
     numbers.  Theorem ~ leloet relates it to 'less than' for reals. $)
  df-le $a |- <_ = ( ( RR* X. RR* ) \ `' < ) $.

  $( The set of extended reals exists. $)
  xrex $p |- RR* e. V $=
    ( cxr cr cpnf cmnf cpr cun cvv df-xr reex prex unex eqeltr ) ABCDEZFGHBMICD
    JKL $.
    $( [24-Dec-2006] $) $( [24-Dec-2006] $)

  $( Plus infinity belongs to the set of extended reals. $)
  pnfxr $p |- +oo e. RR* $=
    ( cpnf cr cmnf cpr cun cxr wcel wo cc cvv df-pnf axcnex eqeltr pri1 olc
    ax-mp elun mpbir df-xr eleqtrr ) ABACDZEZFAUBGABGZAUAGZHZUDUEACAIJKLMNUDUCO
    PABUAQRST $.
    $( [14-Oct-2005] $) $( [13-Oct-2005] $)

  $( Minus infinity belongs to the set of extended reals. $)
  mnfxr $p |- -oo e. RR* $=
    ( cmnf cr cpnf cpr cun cxr wcel wo cc csn cvv df-mnf snex eqeltr pri2 olc
    ax-mp elun mpbir df-xr eleqtrr ) ABCADZEZFAUCGABGZAUBGZHZUEUFCAAIJKLIMNOUEU
    DPQABUBRSTUA $.
    $( [14-Oct-2005] $) $( [13-Oct-2005] $)

  $( The 'less than' binary relation on the set of extended reals.  Definition
     12-3.1 of [Gleason] p. 173. $)
  ltxrt $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A < B <->
            ( ( ( ( A e. RR /\ B e. RR ) /\ A <RR B ) \/
                 ( A = -oo /\ B = +oo ) ) \/ ( ( A e. RR /\ B = +oo )
                \/ ( A = -oo /\ B e. RR ) ) ) ) ) $=
    ( cxr wcel wa cop cltrr cr cxp cin cmnf cpnf csn cun wo wbr wceq clt wb
    df-br bicomi a1i opelxpg anbi12d elin ancom 3bitr4g adantl pnfxr opthgg
    mp3an3 opex elsnc syl5bb orbi12d elun elsncg anbi2d bitrd anbi1d sylan9bbr
    df-ltxr eleq2i 3bitr ) ACDZBCDZEZABFZGHHIZJZKLFZMZNZDZVHHLMZIZKMZHIZNZDZOZA
    HDZBHDZEZABGPZEZAKQZBLQZEZOZWBWHEZWGWCEZOZOABRPZVGVNWJVTWMVGVHVJDZVHVLDZOWJ
    VNVGWOWFWPWIVFWOWFSVEVFVHGDZVHVIDZEWEWDEWOWFVFWQWEWRWDWQWESVFWEWQABGTUAUBAB
    HHCUCUDVHGVIUEWDWEUFUGUHVGVHVKQZWIWPVEVFLCDWSWISUIABKLCCCUJUKVHVKABULUMUNUO
    VHVJVLUPUNVGVHVPDZVHVRDZOWMVTVGWTWKXAWLVFWTWKSVEVFWTWBBVODZEWKABHVOCUCVFXBW
    HWBBLCUQURUSUHVFXAAVQDZWCEVEWLABVQHCUCVEXCWGWCAKCUQUTVAUOVHVPVRUPUNUOWNVHRD
    VHVMVSNZDWAABRTRXDVHVBVCVHVMVSUPVDUN $.
    $( [15-Oct-2005] $) $( [14-Oct-2005] $)

  $( Plus infinity is not a real number. $)
  pnfnre $p |- +oo e/ RR $=
    ( cpnf cr wnel wcel wn cc eirr df-pnf eleq1i mtbir recnt mto df-nel mpbir
    ) ABCABDZEOAFDZPFFDFGAFFHIJAKLABMN $.
    $( [14-Oct-2005] $) $( [13-Oct-2005] $)

  $( Minus infinity is not a real number. $)
  minfnre $p |- -oo e/ RR $=
    ( cmnf cr wnel wcel wn cc csn axcnex snid wi wa en2lp imnan mpbir ax-mp
    df-mnf eleq1i mtbir recnt mto df-nel ) ABCABDZEUBAFDZUCFGZFDZFUDDZUEEZFHIUF
    UGJUFUEKEFUDLUFUEMNOAUDFPQRASTABUAN $.
    $( [14-Oct-2005] $) $( [13-Oct-2005] $)

  $( The standard reals are a subset of the extended reals. $)
  ressxr $p |- RR (_ RR* $=
    ( cr cpnf cmnf cpr cun cxr ssun1 df-xr sseqtr4 ) AABCDZEFAJGHI $.
    $( [14-Oct-2005] $) $( [14-Oct-2005] $)

  $( A standard real is an extended real. $)
  rexrt $p |- ( A e. RR -> A e. RR* ) $=
    ( cr cxr ressxr sseli ) BCADE $.
    $( [15-Oct-2005] $) $( [14-Oct-2005] $)

  $( The standard less-than ` <RR ` and the extended real less-than ` < `
     are identical when restricted to the non-extended reals ` RR ` . $)
  ltxrltt $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> A <RR B ) ) $=
    ( cr wcel wa clt wbr cltrr cmnf wceq cpnf wo cxr wb ltxrt rexrt syl2an
    ibar wn wnel pnfnre df-nel mpbi eleq1 mtbiri con2i intnand adantl minfnre
    intnanrd anim12i ioran sylibr ancoms jca biorf syl bitr2d orass orcom bitr
    syl5bb bitrd ) ACDZBCDZEZABFGZVFABHGZEZAIJZBKJZEZLVDVKEZVJVEEZLZLZVHAMDBMDV
    GVPNVDVEABOAPBPQVFVLVOLZVILZVHVPVFVHVIVRVFVHRVFVQSZVIVRNVFVLSZVOSZEVSVFVTWA
    VEVTVDVEVKVJVKVEVKVEKCDZKCTWBSUAKCUBUCBKCUDUEUFZUGUHVEVDWAVEVDEVMSZVNSZEWAV
    EWDVDWEVEVKVDWCUGVDVJVEVJVDVJVDICDZICTWFSUIICUBUCAICUDUEUFUJUKVMVNULUMUNUOV
    LVOULUMVQVIUPUQURVPVIVQLVRVIVLVOUSVIVQUTVAVBVC $.
    $( [14-Oct-2005] $) $( [13-Oct-2005] $)

  $( 'Less than or equal to' expressed in terms of 'less than', for extended
     reals. $)
  xrlenltt $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A <_ B <-> -. B < A ) ) $=
    ( cxr wcel wa cle wbr cop clt ccnv wn cxp wb opelxpi cdif df-le eleq2i
    eldif bitr baib syl df-br syl5bb opelcnvg syl6rbbr negbid bitr4d ) ACDBCDEZ
    ABFGZABHZIJZDZKZBAIGZKUHUJFDZUMUIUHUJCCLZDZUOUMMABCCNUOUQUMUOUJUPUKOZDUQUME
    FURUJPQUJUPUKRSTUAABFUBUCUHUNULUHULBAHIDUNABCCIUDBAIUBUEUFUG $.
    $( [15-Oct-2005] $) $( [14-Oct-2005] $)

  $( 'Less than' expressed in terms of 'less than or equal to', for extended
     reals. $)
  xrltnlet $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A < B <-> -. B <_ A ) ) $=
    ( cxr wcel clt wbr cle wn wb wa xrlenltt con2bid ancoms ) BCDZACDZABEFZBAGF
    ZHINOJQPBAKLM $.
    $( [6-Feb-2007] $) $( [6-Feb-2007] $)

