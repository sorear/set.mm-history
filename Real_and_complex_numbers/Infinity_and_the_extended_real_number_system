$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Infinity and the extended real number system
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c <_ $. $( 'Less than or equal to' relation. $)
  $c +oo $. $( Plus infinity $)
  $c -oo $. $( Minus infinity $)
  $c RR* $. $( The set of extended reals $)
  $c < $. $( 'Less than' relation (over extended reals) $)

  $( Extend wff notation to include the 'less than or equal to' relation. $)
  cle $a class <_ $.
  $( Plus infinity. $)
  cpnf $a class +oo $.
  $( Minus infinity. $)
  cmnf $a class -oo $.
  $( The set of extended reals (includes plus and minus infinity). $)
  cxr $a class RR* $.
  $( 'Less than' predicate (extended to include the extended reals). $)
  clt $a class < $.

  $( Define plus infinity.  Note that the definition is arbitrary, requiring
     only that ` +oo ` be a set not in ` RR ` and different from ` -oo `
     ( ~ df-mnf ).  We use ` ~P U. CC ` to make it independent of the
     construction of ` CC ` , and Cantor's Theorem will show that it is
     different from any member of ` CC ` and therefore ` RR ` .  See ~ pnfnre ,
     ~ mnfnre , and ~ pnfnemnf .

     A simpler possibility is to define ` +oo ` as ` CC ` and ` -oo ` as
     ` { CC } ` , but that approach requires the Axiom of Regularity to show
     that ` +oo ` and ` -oo ` are different from each other and from all
     members of ` RR ` . $)
  df-pnf $a |- +oo = ~P U. CC $.

  $( Define minus infinity as the power set of plus infinity.  Note that the
     definition is arbitrary, requiring only that ` -oo ` be a set not in
     ` RR ` and different from ` +oo ` (see ~ mnfnre and ~ pnfnemnf ). $)
  df-mnf $a |- -oo = ~P +oo $.

  $( Define the set of extended reals that includes plus and minus infinity.
     Definition 12-3.1 of [Gleason] p. 173. $)
  df-xr $a |- RR* = ( RR u. { +oo , -oo } ) $.

  ${
    $d x y $.
    $( Define 'less than' on the set of extended reals.  Definition 12-3.1 of
       [Gleason] p. 173.  Note that in our postulates for complex numbers,
       ` <RR ` is primitive and not necessarily a relation on ` RR ` . $)
    df-ltxr $a |- < = ( ( { <. x , y >. | ( x e. RR /\ y e. RR /\ x <RR y ) }
    u. { <. -oo , +oo >. } ) u. ( ( RR X. { +oo } ) u. ( { -oo } X. RR ) ) ) $.
  $}

  $( Define 'less than or equal to' on the extended real subset of complex
     numbers.  Theorem ~ leloe relates it to 'less than' for reals. $)
  df-le $a |- <_ = ( ( RR* X. RR* ) \ `' < ) $.

  $( Plus infinity is not a real number. $)
  pnfnre $p |- +oo e/ RR $=
    ( cpnf cr wnel wcel wn cc cuni cpw pwuninel df-pnf eleq1i mtbir recn df-nel
    mto mpbir ) ABCABDZEQAFDZRFGHZFDFIASFJKLAMOABNP $.
    $( [6-May-2008] $) $( [13-Oct-2005] $)

  $( Minus infinity is not a real number. $)
  mnfnre $p |- -oo e/ RR $=
    ( cmnf cr wnel wcel wn cc cuni cpw 2pwuninel cpnf df-mnf df-pnf pweqi eqtri
    eleq1i mtbir recn mto df-nel mpbir ) ABCABDZEUAAFDZUBFGHZHZFDFIAUDFAJHUDKJU
    CLMNOPAQRABST $.
    $( [6-May-2008] $) $( [13-Oct-2005] $)

  $( The standard reals are a subset of the extended reals. $)
  ressxr $p |- RR C_ RR* $=
    ( cr cpnf cmnf cpr cun cxr ssun1 df-xr sseqtr4i ) AABCDZEFAJGHI $.
    $( [14-Oct-2005] $)

  $( A standard real is an extended real. $)
  rexr $p |- ( A e. RR -> A e. RR* ) $=
    ( cr cxr ressxr sseli ) BCADE $.
    $( [14-Oct-2005] $)

  $( No (finite) real equals plus infinity.  (The proof was shortened by Andrew
     Salmon, 19-Nov-2011.) $)
  renepnf $p |- ( A e. RR -> A =/= +oo ) $=
    ( cr wcel cpnf wceq wnel wn pnfnre df-nel mpbi eleq1 mtbiri necon2ai ) ABCZ
    ADADENDBCZDBFOGHDBIJADBKLM $.
    $( [19-Nov-2011] $) $( [14-Oct-2005] $)

  $( No real equals minus infinity.  (The proof was shortened by Andrew Salmon,
     19-Nov-2011.) $)
  renemnf $p |- ( A e. RR -> A =/= -oo ) $=
    ( cr wcel cmnf wceq wnel wn mnfnre df-nel mpbi eleq1 mtbiri necon2ai ) ABCZ
    ADADENDBCZDBFOGHDBIJADBKLM $.
    $( [19-Nov-2011] $) $( [14-Oct-2005] $)

  $( The reals and the infinities are disjoint.  (The proof was shortened by
     Andrew Salmon, 19-Nov-2011.) $)
  renfdisj $p |- ( RR i^i { +oo , -oo } ) = (/) $=
    ( vx cr cpnf cmnf cpr cin c0 wceq cv wcel disj visset elpr renepnf necon2bi
    wn wo renemnf jaoi sylbi con2i mprgbir ) BCDEZFGHAIZUCJZPABABUCKUEUDBJZUEUD
    CHZUDDHZQUFPZUDCDALMUGUIUHUFUDCUDNOUFUDDUDROSTUAUB $.
    $( [19-Nov-2011] $) $( [25-Oct-2005] $)

  $( 'Less than or equal to' expressed in terms of 'less than', for extended
     reals. $)
  xrlenlt $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A <_ B <-> -. B < A ) ) $=
    ( cxr wcel wa cle wbr cop clt ccnv wn df-br cxp wb opelxpi cdif df-le eldif
    eleq2i bitri baib syl syl5bb opelcnvg syl6rbbr notbid bitr4d ) ACDBCDEZABFG
    ZABHZIJZDZKZBAIGZKUIUJFDZUHUMABFLUHUJCCMZDZUOUMNABCCOUOUQUMUOUJUPUKPZDUQUME
    FURUJQSUJUPUKRTUAUBUCUHUNULUHULBAHIDUNABCCIUDBAILUEUFUG $.
    $( [14-Oct-2005] $)

  $( 'Less than' expressed in terms of 'less than or equal to', for extended
     reals. $)
  xrltnle $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A < B <-> -. B <_ A ) ) $=
    ( cxr wcel clt wbr cle wn wb wa xrlenlt con2bid ancoms ) BCDZACDZABEFZBAGFZ
    HINOJQPBAKLM $.
    $( [6-Feb-2007] $)

  ${
    $d x A $.
    $( The three (non-exclusive) possibilities implied by a subset of extended
       reals.  (The proof was shortened by Andrew Salmon, 19-Nov-2011.) $)
    ssxr $p |- ( A C_ RR* -> ( A C_ RR \/ +oo e. A \/ -oo e. A ) ) $=
      ( cr cpnf cmnf cpr cun wss wcel wo cxr w3o wn cin c0 wceq csn disjsn cdif
      wa sseq2i df-pr ineq2i indi anbi12i biimpri pm4.56 3imtr3i syl5eq reldisj
      eqtri un00 renfdisj disj3 mpbi difun2 eqtr4i syl6bbr syl5ib 3orrot df-3or
      orrd df-xr bitri 3imtr4i ) ABCDEZFZGZCAHZDAHZIZABGZIZAJGVKVHVIKZVGVJVKVJL
      ZAVEMZNOZVGVKVNVOACPZMZADPZMZFZNVOAVQVSFZMWAVEWBACDUAUBAVQVSUCUJVHLZVILZS
      ZVRNOZVTNOZSZVNWANOWHWEWFWCWGWDACQADQUDUEVHVIUFVRVTUKUGUHVGVPAVFVERZGVKAV
      EVFUIBWIABBVERZWIBVEMNOBWJOULBVEUMUNBVEUOUPTUQURVAJVFAVBTVMVHVIVKKVLVKVHV
      IUSVHVIVKUTVCVD $.
      $( [19-Nov-2011] $) $( [25-Oct-2005] $)
  $}

  ${
    $d A a b $.  $d B b $.  $d a b x y $.
    $( The standard less-than ` <RR ` and the extended real less-than ` < ` are
       identical when restricted to the non-extended reals ` RR ` .  (Revised
       by Mario Carneiro, 16-Jun-2013.) $)
    ltxrlt $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> A <RR B ) ) $=
      ( va vb vx vy cv clt wbr cltrr wb cr wceq wcel wa cmnf cpnf cun wn sylbi
      breq1 bibi12d breq2 w3a copab cop csn cxp wo brun df-br elsni visset wnel
      opth1 mnfnre df-nel mpbi simpl eleq1 syl5ib mtoi 3syl pnfnre simpr adantl
      brxp syl adantr jaoi con2i wi biimt unass uncom 3eqtri breqi df-or 3bitri
      df-ltxr syl6rbbr 3anbi13d 3anbi23d opelopab df-3an baibr bitr4d vtocl2ga
      weq ) CGZDGZHIZWJWKJIZKAWKHIZAWKJIZKABHIZABJIZKCDABLLWJAMWLWNWMWOWJAWKHUA
      WJAWKJUAUBWKBMWNWPWOWQWKBAHUCWKBAJUCUBWJLNZWKLNZOZWLWJWKEGZLNZFGZLNZXAXCJ
      IZUDZEFUEZIZWMWTWJWKPQUFZUGZLQUGZUHZPUGZLUHZRZRZIZSZWLXHKXQWTXQWJWKXJIZWJ
      WKXOIZUIWTSZWJWKXJXOUJXSYAXTXSWJWKUFZXJNZYAWJWKXJUKYCYBXIMWJPMZYAYBXIULWJ
      WKPQCUMZUOYDWTPLNZPLUNYFSUPPLUQURWTWRYDYFWRWSUSWJPLUTVAVBZVCTXTWJWKXLIZWJ
      WKXNIZUIYAWJWKXLXNUJYHYAYIYHWRWKXKNZOYAWJWKLXKDUMZVGYJYAWRYJWKQMZYAWKQULY
      LWTQLNZQLUNYMSVDQLUQURWTWSYLYMWRWSVEWKQLUTVAVBVHVFTYIWJXMNZWSOYAWJWKXMLYK
      VGYNYAWSYNYDYAWJPULYGVHVITVJTVJTVKXRXHXRXHVLZWLXRXHVMWLWJWKXPXGRZIXQXHUIY
      OWJWKHYPHXGXJRXORXGXPRYPEFVTXGXJXOVNXGXPVOVPVQWJWKXPXGUJXQXHVRVSWAVHXHWTW
      MXHYBXGNWRWSWMUDZWTWMOWJWKXGUKXFWRXDWJXCJIZUDYQEFWJWKYEYKECWIXBWRXEYRXDXA
      WJLUTXAWJXCJUAWBFDWIXDWSYRWMWRXCWKLUTXCWKWJJUCWCWDWRWSWMWEVSWFWGWH $.
      $( [16-Jun-2013] $) $( [13-Oct-2005] $)
  $}

