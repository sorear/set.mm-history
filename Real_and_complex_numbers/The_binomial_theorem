$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The binomial theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d j k x A $.  $d j k x B $.  $d j k x N $.
    binomlem.1 $e |- A e. CC $.
    binomlem.2 $e |- B e. CC $.
    $( Lemma for ~ binomi (binomial theorem).  Break out and simplify the first
       term of the summation. $)
    binomlem1 $p |- ( N e. NN -> ( sum_ k e. ( 0 ... N )
             ( ( N _C k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) x. A ) =
         ( ( A ^ ( N + 1 ) ) + sum_ k e. ( 1 ... N ) ( ( N _C k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) ) ) $=
      ( vx cn wcel cc0 cfz co cv cbc cmin cexp cmul csu c1 caddc csb cn0 wceq
      nnnn0 cuz cfv cc wral fsummulc2 mp3an2 elnn0uz biimpi wa mulcl cz bccl
      nn0cn syl elfzelz sylan2 expcl fznn0sub sylancr elfznn0 adantl sylanc
      r19.21aiva mulass mp3an3 ax1cn addsub zcn syl2an opreq2d expp1 eqtrd
      opreq1d mul23 eqtr2d sumeq2dv clt wbr fsum1ps nn0uz syl6eleq nngt0
      peano2nn0 adantr fzelp1 sylan syl3anc bcn0 peano2cn subid1 3syl mpan
      ax1id opreq12d mulid2 0cn elisseti ax-17 opreq2 exp0 ax-mp syl6eq csbief
      syl5eq addid2i opreq1i sumeq1i a1i 3eqtrd ) DHIZJDKLZDCMZNLZADXPOLZPLZBXP
      PLZQLZQLZCRAQLZXOXQADSTLZXPOLZPLZXTQLZQLZCRZCJYHUAZJSTLZDKLZYHCRZTLZAYDPL
      ZSDKLZYHCRZTLZXNDUBIZYCYIUCDUDZYSYCXOYBAQLZCRZYIDJUEUFZIZYBUGIZCXOUHZYCUU
      BUCZYSUUDAUGIZUUFUUGEYBACJDUIUJYSUUDDUKULYSUUECXOXQUGIZYAUGIZUUEYSXPXOIZU
      MZXQYAUNYSXPUOIZUUIUUKYSUUMUMXQUBIUUIXPDUPXQUQURXPJDUSZUTZXSUGIZXTUGIZUUJ
      UULXSXTUNUUHXRUBIZUUPUULAXRVAEUBXPJDVBZVCZUUKUUQYSBUGIZXPUBIUUQUUKBXPVAFX
      PDVDVCVEZVFZVFVGVFYSXOUUAYHCUULUUAXQYAAQLZQLZYHUUIUUJUUAUVEUCZUULUUIUUJUU
      HUVFEXQYAAVHVIUUOUVCVFUULUVDYGXQQUULYGXSAQLZXTQLZUVDUULYFUVGXTQUULYFAXRST
      LZPLZUVGUULYEUVIAPDUGIZXPUGIZYEUVIUCZYSUUKUVKSUGIUVLUVMVJDSXPVKUJDUQZUUKU
      UMUVLUUNXPVLURVMVNUUHUURUVJUVGUCUULAXRVOEUUSVCVPVQUUPUUQUVHUVDUCZUULUUPUU
      HUUQUVOEXSAXTVRUJUUTUVBVFVSVNVPVTVPURUUDJDWAWBYHUGIZCXOUHYIYNUCXNYHCJDWCX
      NDUBUUCYTWDWEDWFXNUVPCXOYSUUKUVPXNUUIYGUGIZUVPUULXQYGUNUUOYFUGIZUUQUVQUUL
      YFXTUNUUHYEUBIZUVRUULAYEVAEYDUBIZXPJYDKLIUVSUULUBXPJYDVBYSUVTUUKDWGZWHUBX
      PJDWIVFVCUVBVFVFYTWJVGWKXNYSYNYRUCYTYSYJYOYMYQTYSDJNLZAYDJOLZPLZSQLZQLZYO
      YJYSUWFSYOQLZYOYSUWBSUWEYOQDWLYSUWEYOSQLZYOYSUWDYOSQYSUWCYDAPYSUVKYDUGIUW
      CYDUCUVNDWMYDWNWOVNVQYSUVTYOUGIZUWHYOUCUWAUUHUVTUWIEAYDVAWPZYOWQWOVPWRYSU
      VTUWIUWGYOUCUWAUWJYOWSWOVPCGJYHUWFJUGWTXAGMUWFICXBXPJUCZXQUWBYGUWEQXPJDNX
      CUWKYFUWDXTSQUWKYEUWCAPXPJYDOXCVNUWKXTBJPLZSXPJBPXCUVAUWLSUCFBXDXEXFWRWRX
      GXHYMYQUCYSYLYPYHCYKSDKSVJXIXJXKXLWRURXM $.
      $( [30-Jan-2006] $) $( [6-Dec-2005] $)

    $( Lemma for ~ binomi (binomial theorem).  Shift up the summation index
       with ~ fsumshft , then break out and simplify the last term of the
       summation. $)
    binomlem2 $p |- ( N e. NN -> ( sum_ k e. ( 0 ... N )
             ( ( N _C k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) x. B ) =
         ( sum_ k e. ( 1 ... N ) ( ( N _C ( k - 1 ) ) x.
        ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) + ( B ^ ( N + 1 ) ) ) ) $=
      ( vj vx cn wcel cc0 cfz co cv cbc cmin cexp cmul csu c1 caddc cn0 wceq
      nnnn0 csb cuz cfv cc wral fsummulc2 mp3an2 elnn0uz biimpi wa mulcl cz
      bccl nn0cn syl elfzelz sylan2 expcl fznn0sub sylancr elfznn0 adantl
      sylanc r19.21aiva mulass mp3an3 expp1 opreq2d eqtr4d eqtrd sumeq2dv 1z
      fsumshft peano2nn0 ax-17 opreq2 opreq1 opreq12d cbvsumi syl5eq ax1cn
      addid2i opreq1i a1i subsub3 npcan mpan2 zcn syl2an oprex csbief
      sumeq12rdv 3eqtrd elnnuz fsump1i sylbi pncan sylancl bcnn peano2cn subid
      3syl exp0 ax-mp syl6eq opreq1d mpan mulid2 ) DIJZKDLMZDCNZOMZADXOPMZQMZBX
      OQMZRMZRMZCSBRMZTDTUAMZLMZDXOTPMZOMZAYCXOPMZQMZXSRMZRMZCSZTDLMYJCSZDYCTPM
      ZOMZAYCYCPMZQMZBYCQMZRMZRMZUAMZYLYQUAMXMDUBJZYBYKUCDUDZUUAYBXNXPXRBXOTUAM
      ZQMZRMZRMZCSZKTUAMZYCLMZGYEDGNZOMZADUUJPMZQMZBUUJTUAMZQMZRMZRMZUEZCSZYKUU
      AYBXNYABRMZCSZUUGDKUFUGJZYAUHJZCXNUIZYBUVAUCZUUAUVBBUHJZUVDUVEFYABCKDUJUK
      UUAUVBDULUMZUUAUVCCXNXPUHJZXTUHJZUVCUUAXOXNJZUNZXPXTUOUUAXOUPJZUVHUVJUUAU
      VLUNXPUBJUVHXODUQXPURUSXOKDUTVAZXRUHJZXSUHJZUVIUVKXRXSUOAUHJZXQUBJUVNUVKA
      XQVBEUBXOKDVCVDZUVJUVOUUAUVFXOUBJZUVOUVJBXOVBFXODVEZVDVFZVGZVGVHVGUUAXNUU
      TUUFCUVKUUTXPXTBRMZRMZUUFUVHUVIUUTUWCUCZUVKUVHUVIUVFUWDFXPXTBVIVJUVMUWAVG
      UVKUWBUUEXPRUVKUWBXRXSBRMZRMZUUEUVNUVOUWBUWFUCZUVKUVNUVOUVFUWGFXRXSBVIVJU
      VQUVTVGUVKUUDUWEXRRUVJUUDUWEUCZUUAUVFUVRUWHUVJBXOVKFUVSVDVFVLVMVLVNVOVNUU
      AXNUUQGSZUUSUUGUVBUUQUHJZGXNUIZUWIUUSUCZUUAUVBTUPJUWKUWLVPUUQGCTKDVQUKUVG
      UUAUWJGXNUUKUHJZUUPUHJZUWJUUAUUJXNJZUNZUUKUUPUOUUAUUJUPJZUWMUWOUUAUWQUNUU
      KUBJUWMUUJDUQUUKURUSUUJKDUTVAUUMUHJZUUOUHJZUWNUWPUUMUUOUOUVPUULUBJUWRUWPA
      UULVBEUBUUJKDVCVDUWOUWSUUAUVFUUNUBJZUWSUWOBUUNVBFUWOUUJUBJUWTUUJDVEUUJVRU
      SVDVFVGVGVHVGHXNUUFUUQCGHNZUUFJGVSUXAUUQJCVSXOUUJUCZXPUUKUUEUUPRXOUUJDOVT
      UXBXRUUMUUDUUORUXBXQUULAQXOUUJDPVTVLUXBUUCUUNBQXOUUJTUAWAVLWBWBWCWDUUAUUI
      YDUURYJCUUIYDUCUUAUUHTYCLTWEWFWGWHUUAXOYDJZUNZYFADYEPMZQMZBYETUAMZQMZRMZR
      MZYJUURUXDUXIYIYFRDUHJZXOUHJZUXIYIUCUUAUXCUXKUXLUNZUXFYHUXHXSRUXMUXEYGAQU
      XKUXLTUHJZUXEYGUCWEDXOTWIVJVLUXLUXHXSUCUXKUXLUXGXOBQUXLUXNUXGXOUCWEXOTWJW
      KVLVFWBDURZUXCUVLUXLXOTYCUTXOWLUSWMVLGHYEUUQUXJXOTPWNUXAUXJJGVSUUJYEUCZUU
      KYFUUPUXIRUUJYEDOVTUXPUUMUXFUUOUXHRUXPUULUXEAQUUJYEDPVTVLUXPUUNUXGBQUUJYE
      TUAWAVLWBWBWOWDWPWQUSXMDTUFUGJYKYTUCDWRYJYSCTDYFYIRWNYNYRRWNXOYCUCZYFYNYI
      YRRUXQYEYMDOXOYCTPWAVLUXQYHYPXSYQRUXQYGYOAQXOYCYCPVTVLXOYCBQVTWBWBWSWTXMY
      SYQYLUAXMUUAYSYQUCUUBUUAYSTYQRMZYQUUAYNTYRYQRUUAYNDDOMTUUAYMDDOUXKUXNYMDU
      CUUADTXAUXOWEXBVLDXCVNUUAYRUXRYQUUAYPTYQRUUAYPAKQMZTUUAYOKAQUUAUXKYCUHJYO
      KUCUXODXDYCXEXFVLUVPUXSTUCEAXGXHXIXJUUAYCUBJZYQUHJZUXRYQUCDVRUVFUXTUYAFBY
      CVBXKYQXLXFZVNWBUYBVNUSVLWQ $.
      $( [30-Jan-2006] $) $( [7-Dec-2005] $)

    $( Lemma for ~ binomi (binomial theorem).  Break out the last term of the
       summation used by the induction hypothesis. $)
    binomlem3 $p |- ( N e. NN ->
       sum_ k e. ( 1 ... ( N + 1 ) ) ( ( ( N + 1 ) _C k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) =
   ( sum_ k e. ( 1 ... N ) ( ( ( N + 1 ) _C k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) )
                  + ( B ^ ( N + 1 ) ) ) ) $=
      ( cn wcel c1 caddc co cfz cv cbc cmin cexp cmul csu cuz cfv wceq elnnuz
      oprex opreq2 opreq2d opreq12d fsump1i sylbi cn0 nnnn0 peano2nn0 bcnn cc0
      cc nn0cn subid syl exp0 ax-mp syl6eq opreq1d expcl mpan mulid2 eqtrd
      3syl ) DGHZIDIJKZLKVHCMZNKZAVHVIOKZPKZBVIPKZQKZQKZCRZIDLKVOCRZVHVHNKZAVHV
      HOKZPKZBVHPKZQKZQKZJKZVQWAJKVGDISTHVPWDUADUBVOWCCIDVJVNQUCVRWBQUCVIVHUAZV
      JVRVNWBQVIVHVHNUDWEVLVTVMWAQWEVKVSAPVIVHVHOUDUEVIVHBPUDUFUFUGUHVGWCWAVQJV
      GDUIHVHUIHZWCWAUADUJDUKWFWCIWAQKZWAWFVRIWBWAQVHULWFWBWGWAWFVTIWAQWFVTAUMP
      KZIWFVSUMAPWFVHUNHVSUMUAVHUOVHUPUQUEAUNHWHIUAEAURUSUTVAWFWAUNHZWGWAUABUNH
      WFWIFBVHVBVCWAVDUQZVEUFWJVEVFUEVE $.
      $( [30-Jan-2006] $) $( [7-Dec-2005] $)

    $( Lemma for ~ binomi (binomial theorem).  Break out the first term of the
       summation used by the induction hypothesis. $)
    binomlem4 $p |- ( N e. NN0 ->
       sum_ k e. ( 0 ... ( N + 1 ) ) ( ( ( N + 1 ) _C k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) =
   ( ( A ^ ( N + 1 ) ) + sum_ k e. ( 1 ... ( N + 1 ) ) ( ( ( N + 1 ) _C k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) ) ) $=
      ( vx cn0 wcel cc0 c1 caddc co cfz cv cbc cmin cexp cmul csu csb cuz cfv
      clt wbr cc wral wceq fsum1ps peano2nn0 nn0uz syl6eleq cn nn0p1nn nngt0
      syl wa mulcl cz bccl nn0cn elfzelz syl2an expcl cvv oprex fznn0sub mpan
      sylancr adantl elfznn0 sylanc r19.21aiva syl3anc bcn0 subid1 3syl
      opreq2d opreq1d ax1id eqtrd opreq12d mulid2 0cn elisseti ax-17 opreq2
      exp0 ax-mp syl6eq csbief syl5eq ax1cn addid2i opreq1i sumeq1i a1i ) DHIZJ
      DKLMZNMZWSCOZPMZAWSXAQMZRMZBXARMZSMZSMZCTZCJXGUAZJKLMZWSNMZXGCTZLMZAWSRMZ
      KWSNMZXGCTZLMWSJUBUCZIJWSUDUEZXGUFIZCWTUGXHXMUHWRXGCJWSUIWRWSHXQDUJZUKULW
      RWSUMIXRDUNWSUOUPWRXSCWTXBUFIZXFUFIZXSWRXAWTIZUQZXBXFURWSHIZXAUSIZYAWRYCY
      EYFUQXBHIYAXAWSUTXBVAUPXTXAJWSVBVCXDUFIZXEUFIZYBYDXDXEURYCYGWRAUFIZXCHIZY
      GYCAXCVDEWSVEIYCYJDKLVFVEXAJWSVGVHVIVJYCYHWRBUFIZXAHIYHYCBXAVDFXAWSVKVIVJ
      VLVLVMVNWRXIXNXLXPLWRWSJPMZAWSJQMZRMZKSMZSMZXNXIWRYPKXNSMZXNWRYLKYOXNSWRY
      EYLKUHXTWSVOUPWRYOXNKSMZXNWRYNXNKSWRYMWSARWRYEWSUFIYMWSUHXTWSVAWSVPVQVRVS
      WRYEXNUFIZYRXNUHXTYIYEYSEAWSVDVHZXNVTVQWAWBWRYEYSYQXNUHXTYTXNWCVQWACGJXGY
      PJUFWDWEGOYPICWFXAJUHZXBYLXFYOSXAJWSPWGUUAXDYNXEKSUUAXCYMARXAJWSQWGVRUUAX
      EBJRMZKXAJBRWGYKUUBKUHFBWHWIWJWBWBWKWLXLXPUHWRXKXOXGCXJKWSNKWMWNWOWPWQWBW
      A $.
      $( [30-Jan-2006] $) $( [7-Dec-2005] $)

    $( Lemma for ~ binomi (binomial theorem).  We use Pascal's rule ~ bcpasc to
       combine the sum of the summations in ~ binomlem1 and ~ binomlem2 into a
       single summation. $)
    binomlem5 $p |- ( N e. NN -> ( sum_ k e. ( 0 ... N )
         ( ( N _C k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) x. ( A + B ) ) =
      sum_ k e. ( 0 ... ( N + 1 ) ) ( ( ( N + 1 ) _C k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) ) $=
      ( cn wcel cc0 cfz co cv cbc cmin cexp cmul csu caddc c1 cc wceq addass
      cn0 expcl nnnn0 sylan2 peano2nn sylancr wral wa mulcl cz bccl nn0cn syl
      elfzelz syl2an fzelp1 cvv oprex fznn0sub mpan 3syl elfznn adantl sylanc
      r19.21aiva cuz cfv fsumcl elnnuz sylanb mpdan addcl peano2zm syl3anc
      binomlem1 binomlem2 opreq12d binomlem3 jca fsumadd adddir bcpasc opreq1d
      eqtr3d sumeq2dv 3eqtr2d opreq2d 3eqtr4d nn0uz syl6eleq elnn0uz biimpri
      elfznn0 adddi mp3an23 binomlem4 ) DGHZIDJKZDCLZMKZADXANKZOKZBXAOKZPKZPKZC
      QZAPKZXHBPKZRKZADSRKZOKZSXLJKZXLXAMKZAXLXANKZOKZXEPKZPKZCQZRKZXHABRKPKZIX
      LJKXSCQZWSXMSDJKZXBXRPKZCQZRKZYDDXASNKZMKZXRPKZCQZBXLOKZRKZRKZXMYFYMRKZRK
      ZXKYAXMTHZYFTHZYMTHZYNYPUAWSXMYFYMUBATHZXLGHZYQWSYTXLUCHZYQUUAAXLUDXLUEZU
      FEDUGZUHWSYETHZCYDUIZYRWSUUECYDXBTHZXRTHZUUEWSXAYDHZUJZXBXRUKZDUCHZXAULHZ
      UUGWSUUIUULUUMUJXBUCHZUUGXADUMZXBUNZUOZDUEZXASDUPZUQXQTHZXETHZUUHUUJXQXEU
      KUUJXAXNHZXPUCHZUUTGXASDURXLUSHUVBUVCDSRUTUSXASXLVAVBYTUVCUUTEAXPUDVBVCUU
      IUVAWSBTHZXAGHZUVAUUIUVDXAUCHZUVAUVEBXAUDZXAUEUFFXADVDUHVEVFZVFVGDSVHVIHZ
      UUFYRWSYECSDVJDVKZVLVMZYKTHZYLTHZYSWSYKYLVNWSYJTHZCYDUIZUVLWSUVNCYDYITHZU
      UHUVNUUJYIXRUKZUULYHULHZUVPWSUUIUULUVRUJYIUCHZUVPYHDUMZYIUNZUOUURUUIUUMUV
      RUUSXAVOUOZUQUVHVFVGUVIUVOUVLWSYJCSDVJUVJVLVMZUVDUUAUVMWSUVDUUBUVMUUABXLU
      DUUCUFFUUDUHZVFVPWSXIYGXJYMRABCDEFVQABCDEFVRVSWSXTYOXMRWSXTYDXSCQZYLRKYFY
      KRKZYLRKZYOABCDEFVTWSUWFUWEYLRWSYDYEYJRKZCQZUWFUWEWSUUEUVNUJZCYDUIZUWIUWF
      UAZWSUWJCYDUUJUUEUVNUUGUUHUUEUUJUUKUUJUUNUUGUULUUMUUNWSUUIUUOUURUUSUQUUPU
      OZUVHVFUVPUUHUVNUUJUVQUUJUVSUVPUULUVRUVSWSUUIUVTUURUWBUQUWAUOZUVHVFWAVGUV
      IUWKUWLWSYEYJCSDWBUVJVLVMWSYDUWHXSCUUJXBYIRKZXRPKZUWHXSUUGUVPUUHUWPUWHUAU
      UJXBYIXRWCUWMUWNUVHVPUUJUWOXOXRPUULUUMUWOXOUAWSUUIXADWDUURUUSUQWEWFWGWFWE
      YRUVLUVMUWGYOUAWSYFYKYLUBUVKUWCUWDVPWHWIWJWSDIVHVIZHZXHTHZYBXKUAZWSDUCUWQ
      UURWKWLUWRXGTHZCWTUIUWSUWRUXACWTUUGXFTHZUXAUWRXAWTHZUJZXBXFUKUULUUMUUGUWR
      UXCUUQUULUWRDWMWNXAIDUPUQXDTHZUVAUXBUXDXDXEUKYTXCUCHUXEUXDAXCUDEUWQXAIDVA
      UHUXCUVAUWRUVDUVFUVAUXCUVGFXADWOUHVEVFVFVGXGCIDVJVMUWSYTUVDUWTEFXHABWPWQV
      CWSUULYCYAUAUURABCDEFWRUOWJ $.
      $( [31-Jul-2006] $) $( [7-Dec-2005] $)

    $( Lemma for ~ binomi (binomial theorem).  Do the final induction. $)
    binomlem6 $p |- ( N e. NN -> ( ( A + B ) ^ N ) = sum_ k e. ( 0 ... N )
         ( ( N _C k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) ) $=
      ( vx vj caddc co cv cexp cc0 cfz cbc cmin cmul csu wceq c1 opreq2 wcel
      opreq1 opreq2d opreq1d opreq12d adantr sumeq12rdv eqeq12d cc addcli exp1
      ax-mp cuz cfv cz 0z uzid oprex elisseti ax1cn addid2i eqeq2i cn0 1nn0
      bcnn syl6eq subidi exp0 mulid2i sylbi fsump1i bcn0 subid1i mulid1i
      fsum1i mp2an opreq1i eqtr2i opreq2i sumeq1i 3eqtri cn wa expp1 nnnn0
      sylancr pm3.27 binomlem5 3eqtrd ex nnind ) ABIJZGKZLJZMWNNJZWNCKZOJZAWNWQ
      PJZLJZBWQLJZQJZQJZCRZSWMTLJZMTNJZTWQOJZATWQPJZLJZXAQJZQJZCRZSWMHKZLJZMXMN
      JZXMWQOJZAXMWQPJZLJZXAQJZQJZCRZSZWMXMTIJZLJZMYCNJZYCWQOJZAYCWQPJZLJZXAQJZ
      QJZCRZSZWMDLJZMDNJZDWQOJZADWQPJZLJZXAQJZQJZCRZSGHDWNTSZWOXEXDXLWNTWMLUAUU
      AWPXFXCXKCWNTMNUAUUAXCXKSWQXFUBUUAWRXGXBXJQWNTWQOUCUUAWTXIXAQUUAWSXHALWNT
      WQPUCUDUEUFUGUHUIWNXMSZWOXNXDYAWNXMWMLUAUUBWPXOXCXTCWNXMMNUAUUBXCXTSWQXOU
      BUUBWRXPXBXSQWNXMWQOUCUUBWTXRXAQUUBWSXQALWNXMWQPUCUDUEUFUGUHUIWNYCSZWOYDX
      DYKWNYCWMLUAUUCWPYEXCYJCWNYCMNUAUUCXCYJSWQYEUBUUCWRYFXBYIQWNYCWQOUCUUCWTY
      HXAQUUCWSYGALWNYCWQPUCUDUEUFUGUHUIWNDSZWOYMXDYTWNDWMLUAUUDWPYNXCYSCWNDMNU
      AUUDXCYSSWQYNUBUUDWRYOXBYRQWNDWQOUCUUDWTYQXAQUUDWSYPALWNDWQPUCUDUEUFUGUHU
      IXEWMMMTIJZNJZXKCRZXLWMUJUBZXEWMSABEFUKZWMULUMUUGMMNJXKCRZBIJZWMMMUNUOUBZ
      UUGUUKSMUPUBZUULUQMURUMXKBCMMXGXJQUSBUJFUTWQUUESWQTSZXKBSUUETWQTVAVBZVCUU
      NXKTBQJZBUUNXGTXJBQUUNXGTTOJZTWQTTOUATVDUBZUUQTSVETVFUMVGUUNXJUUPBUUNXITX
      ABQUUNXIAMLJZTUUNXHMALUUNXHTTPJMWQTTPUATVAVHVGUDAUJUBZUUSTSEAVIUMVGUUNXAB
      TLJZBWQTBLUABUJUBZUVABSFBULUMVGUFBFVJZVGUFUVCVGVKVLUMUUJABIUUTUUMUUJASEUQ
      XKAUJCMWQMSZXKTAQJAUVDXGTXJAQUVDXGTMOJZTWQMTOUAUURUVETSVETVMUMVGUVDXJATQJ
      AUVDXIAXATQUVDXIATLJZAUVDXHTALUVDXHTMPJTWQMTPUATVAVNVGUDUUTUVFASEAULUMVGU
      VDXABMLJZTWQMBLUAUVBUVGTSFBVIUMVGUFAEVOVGUFAEVJVGVPVQVRVSUUFXFXKCUUETMNUU
      OVTWAWBXMWCUBZYBYLUVHYBWDZYDXNWMQJZYAWMQJZYKUVHYDUVJSZYBUUHXMVDUBUVLUVHWM
      XMWEUUIXMWFWGUGUVIXNYAWMQUVHYBWHUEUVHUVKYKSYBABCXMEFWIUGWJWKWL $.
      $( [28-Jul-2006] $) $( [7-Dec-2005] $)

    $( The binomial theorem: ` ( A + B ) ^ N ` is the sum from ` k = 0 ` to
       ` N ` of ` ( N _C k ) x. ( ( A ^ k ) x. ( B ^ ( N - k ) ) ` .  Theorem
       15-2.8 of [Gleason] p. 296.  This final piece of the proof combines the
       ` 0 < N ` case of ~ binomlem6 with the ` N = 0 ` case. $)
    binomi $p |- ( N e. NN0 -> ( ( A + B ) ^ N ) = sum_ k e. ( 0 ... N )
         ( ( N _C k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) ) $=
      ( cn0 wcel cn cc0 wceq wo caddc co cexp cfz cv cbc cmin cmul csu elnn0
      binomlem6 c1 cc cz ax1cn 0z opreq2 0nn0 bcn0 ax-mp syl6eq 0cn subidi
      opreq2d exp0 opreq12d mulid1i fsum1i mp2an eqcomi addcli opreq1 opreq1d
      adantr sumeq12rdv 3eqtr4a jaoi sylbi ) DGHDIHZDJKZLABMNZDONZJDPNZDCQZRNZA
      DVPSNZONZBVPONZTNZTNZCUAZKZDUBVKWDVLABCDEFUCVLUDJJPNZJVPRNZAJVPSNZONZVTTN
      ZTNZCUAZVNWCWKUDUDUEHJUFHWKUDKUGUHWJUDUECJVPJKZWJUDUDTNZUDWLWFUDWIUDTWLWF
      JJRNZUDVPJJRUIJGHWNUDKUJJUKULUMWLWIWMUDWLWHUDVTUDTWLWHAJONZUDWLWGJAOWLWGJ
      JSNJVPJJSUIJUNUOUMUPAUEHWOUDKEAUQULUMWLVTBJONZUDVPJBOUIBUEHWPUDKFBUQULUMU
      RUDUGUSZUMURWQUMUTVAVBVLVNVMJONZUDDJVMOUIVMUEHWRUDKABEFVCVMUQULUMVLVOWEWB
      WJCDJJPUIVLWBWJKVPWEHVLVQWFWAWITDJVPRVDVLVSWHVTTVLVRWGAODJVPSVDUPVEURVFVG
      VHVIVJ $.
      $( [31-Jan-2006] $) $( [7-Dec-2005] $)
  $}

  ${
    $d k A $.  $d k N $.
    binom1p.1 $e |- A e. CC $.
    $( Special case of the binomial theorem for ` ( 1 + A ) ^ N ` .
       (Contributed by Paul Chapman, 10-May-2007.) $)
    binom1pi $p |- ( N e. NN0 -> ( ( 1 + A ) ^ N ) =
                  sum_ k e. ( 0 ... N ) ( ( N _C k ) x. ( A ^ k ) ) ) $=
      ( cn0 wcel c1 caddc co cexp cc0 cfz cv cbc cmin cmul csu ax1cn binomi wa
      wceq fznn0sub 1exp syl opreq1d elfznn0 cc expcl mpan mulid2 adantl eqtrd
      opreq2d sumeq2dv ) CEFZGAHICJIKCLIZCBMZNIZGCUQOIZJIZAUQJIZPIZPIZBQUPURVAP
      IZBQGABCRDSUOUPVCVDBUOUQUPFZTZVBVAURPVFVBGVAPIZVAVFUTGVAPVFUSEFUTGUAEUQKC
      UBUSUCUDUEVEVGVAUAZUOVEUQEFZVHUQCUFVIVAUGFZVHAUGFVIVJDAUQUHUIVAUJUDUDUKUL
      UMUNUL $.
      $( [10-May-2007] $) $( [10-May-2007] $)
  $}

  ${
    $d M m $.  $d N j k m $.
    $( Lemma for ~ bcxmas . $)
    bcxmaslem1 $p |- ( A = B -> ( ( N + A ) _C A ) = ( ( N + B ) _C B ) ) $=
      ( caddc co wceq cbc opreq2 opreq12 mpancom ) CADEZCBDEZFABFKAGELBGEFABCDH
      KLABGIJ $.
      $( [18-May-2007] $) $( [18-May-2007] $)

    $( Lemma for ~ bcxmas . $)
    bcxmaslem2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                          ( A + ( B + C ) ) = ( ( A + C ) + B ) ) $=
      ( cc wcel w3a caddc co addass add23 eqtr3d ) ADEBDECDEFABGHCGHABCGHGHACGH
      BGHABCIABCJK $.
      $( [18-May-2007] $) $( [18-May-2007] $)

    $( Parallel summation (Christmas Stocking) theorem for Pascal's Triangle.
       (Contributed by Paul Chapman, 18-May-2007.) $)
    bcxmas $p |- ( ( N e. NN0 /\ M e. NN0 ) ->
                     ( ( ( N + 1 ) + M ) _C M ) =
                     sum_ j e. ( 0 ... M ) ( ( N + j ) _C j ) ) $=
      ( vm vk cn0 wcel c1 caddc co cbc cc0 cfz cv csu wceq wi bcxmaslem1
      opreq2 sumeq1d eqeq12d imbi2d 0nn0 wa nn0addcl bcn0 syl mpan2 cz fsum1i
      1nn0 syl6eqel 0z sylancl peano2nn0 jctir 3syl 3eqtr4rd cuz cfv pm3.27
      elnn0uz sylib oprex fsump1i cc bcxmaslem2 nn0cn adantr adantl ax1cn a1i
      syl3anc opreq1d opreq2d eqtrd opreq1 cmin pncan cn cle wbr bcpasc2
      nnnn0addcl nn0p1nn sylan cr nn0addge2 nnre pm3.26 sylanc breqtrd eqtr3d
      addcom bccl nnnn0 nnz nn0z addass peano2cn 3eqtr3d 3eqtr2rd ex expcom
      a2d nn0ind impcom ) BFGCFGZCHIJZBIJBKJZLBMJZCANZIJZXLKJZAOZPZXHXIDNZIJXQK
      JZLXQMJZXNAOZPZQXHXILIJZLKJZLLMJZXNAOZPZQXHXIENZIJZYGKJZLYGMJZXNAOZPZQXHX
      IYGHIJZIJZYMKJZLYMMJZXNAOZPZQXHXPQDEBXQLPZYAYFXHYSXRYCXTYEXQLXIRYSXSYDXNA
      XQLLMSTUAUBXQYGPZYAYLXHYTXRYIXTYKXQYGXIRYTXSYJXNAXQYGLMSTUAUBXQYMPZYAYRXH
      UUAXRYOXTYQXQYMXIRUUAXSYPXNAXQYMLMSTUAUBXQBPZYAXPXHUUBXRXJXTXOXQBXIRUUBXS
      XKXNAXQBLMSTUAUBXHCLIJZLKJZHYEYCXHLFGZUUDHPZUCXHUUEUDUUCFGUUFCLUEUUCUFUGU
      HZUUDFGLUIGYEUUDPXHXNUUDFALXLLCRUJXHUUDHFUUGUKULUMUNXHXIFGZUUEUDYBFGYCHPX
      HUUHUUECUOZUCUPXILUEYBUFUQURYGFGZXHYLYRXHUUJYLYRQXHUUJUDZYLYRUUKYLUDYQYKY
      HYMKJZIJZYIUULIJZYOUUKYQUUMPYLUUKYQYKCYMIJZYMKJZIJZUUMUUKYGLUSUTGZYQUUQPU
      UKUUJUURXHUUJVAYGVBVCXNUUPALYGXMXLKVDUUOYMKVDXLYMCRVEUGUUKUUPUULYKIUUKUUO
      YHYMKCVFGZYGVFGZHVFGZUUOYHPUUKCYGHVGXHUUSUUJCVHZVIUUJUUTXHYGVHVJZUVAUUKVK
      VLZVMZVNVOVPVIYLUUNUUMPUUKYIYKUULIVQVJUUKUUNYOPYLUUKUULYIIJZYHHIJZYMKJZUU
      NYOUUKUULYHYMHVRJZKJZIJZUVFUVHUUKUVJYIUULIUUKUVIYGYHKUUTUVAUVIYGPUUKYGHVS
      UVCVKUNVOVOYHVTGZYMVTGZYMYHWAWBUVKUVHPUUKYMYHWCXIVTGUUJUVLXHXIYGWDCWEWFZU
      UJUVMXHYGWEVJZUUKYMUUOYHWAYMWGGZXHYMUUOWAWBUUKYMCWHUUKUVMUVPUVOYMWIUGXHUU
      JWJWKUVEWLVMWMUULVFGZYIVFGZUVFUUNPUUKUULYIWNUUKUULFGZUVQYHFGZYMUIGZUVSUUK
      YMYHWOUUKUVLUVTUVNYHWPUGUUKUVMUWAUVOYMWQUGWKUULVHUGUUKYIFGZUVRUUHUUJUWBXH
      UVTYGUIGZUWBUUHUUJUDYGYHWOXIYGUEUUJUWCUUHYGWRVJWKUUIWFYIVHUGWKUUKUVGYNYMK
      XIVFGZUUTUVAUVGYNPUUKXIYGHWSXHUWDUUJXHUUSUWDUVBCWTUGVIUVCUVDVMVNXAVIXBXCX
      DXEXFXG $.
      $( [18-May-2007] $) $( [18-May-2007] $)
  $}


