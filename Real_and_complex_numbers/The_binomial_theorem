$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The binomial theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d j k x A $.  $d j k x B $.  $d j k x N $.
    binomlem.1 $e |- A e. CC $.
    binomlem.2 $e |- B e. CC $.
    $( Lemma for ~ binom (binomial theorem).  Break out and simplify the first
       term of the summation. $)
    binomlem1 $p |- ( N e. NN -> ( sum_ k e. ( 0 ... N )
             ( ( N C. k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) x. A ) =
         ( ( A ^ ( N + 1 ) ) + sum_ k e. ( 1 ... N ) ( ( N C. k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) ) ) $=
      ( vx cn wcel cc0 cfz co cv cbc cmin cexp cmul csu c1 caddc csb cn0 wceq
      nnnn0t cuz cfv cc wral fsummulc2 mp3an2 elnn0uz biimp wa axmulcl cz
      bcclt nn0cnt syl elfzelz sylan2 fznn0subt expclt mpan elfznn0t adantl
      sylanc r19.21aiva axmulass mp3an3 ax1cn addsubt zcnt syl2an opreq2d
      expp1t eqtrd opreq1d mul23t eqtr2d sumeq2dv clt wbr fsum1ps nn0uz
      syl6eleq nngt0t peano2nn0 adantr fzelp1t sylan syl3anc bcn0t peano2cn
      subid1t 3syl ax1id opreq12d mulid2t 0cn elisseti ax-17 opreq2 exp0t
      ax-mp syl6eq csbief syl5eq addid2 opreq1i sumeq1i a1i 3eqtrd ) DHIZJDKLZD
      CMZNLZADXOOLZPLZBXOPLZQLZQLZCRAQLZXNXPADSTLZXOOLZPLZXSQLZQLZCRZCJYGUAZJST
      LZDKLZYGCRZTLZAYCPLZSDKLZYGCRZTLZXMDUBIZYBYHUCDUDZYRYBXNYAAQLZCRZYHDJUEUF
      ZIZYAUGIZCXNUHZYBUUAUCZYRUUCAUGIZUUEUUFEYAACJDUIUJYRUUCDUKULYRUUDCXNXPUGI
      ZXTUGIZUUDYRXOXNIZUMZXPXTUNYRXOUOIZUUHUUJYRUULUMXPUBIUUHXODUPXPUQURXOJDUS
      ZUTZXRUGIZXSUGIZUUIUUKXRXSUNUUKXQUBIZUUOUBXOJDVAZUUGUUQUUOEAXQVBVCURZUUJU
      UPYRUUJXOUBIZUUPXODVDBUGIZUUTUUPFBXOVBVCURVEZVFZVFVGVFYRXNYTYGCUUKYTXPXTA
      QLZQLZYGUUHUUIYTUVEUCZUUKUUHUUIUUGUVFEXPXTAVHVIUUNUVCVFUUKUVDYFXPQUUKYFXR
      AQLZXSQLZUVDUUKYEUVGXSQUUKYEAXQSTLZPLZUVGUUKYDUVIAPDUGIZXOUGIZYDUVIUCZYRU
      UJUVKSUGIUVLUVMVJDSXOVKUJDUQZUUJUULUVLUUMXOVLURVMVNUUKUUQUVJUVGUCZUURUUGU
      UQUVOEAXQVOVCURVPVQUUOUUPUVHUVDUCZUUKUUOUUGUUPUVPEXRAXSVRUJUUSUVBVFVSVNVP
      VTVPURUUCJDWAWBYGUGIZCXNUHYHYMUCXMYGCJDWCXMDUBUUBYSWDWEDWFXMUVQCXNYRUUJUV
      QXMUUHYFUGIZUVQUUKXPYFUNUUNYEUGIZUUPUVRUUKYEXSUNUUKYDUBIZUVSYCUBIZXOJYCKL
      IUVTUUKUBXOJYCVAYRUWAUUJDWGZWHUBXOJDWIVFUUGUVTUVSEAYDVBVCURUVBVFVFYSWJVGW
      KXMYRYMYQUCYSYRYIYNYLYPTYRDJNLZAYCJOLZPLZSQLZQLZYNYIYRUWGSYNQLZYNYRUWCSUW
      FYNQDWLYRUWFYNSQLZYNYRUWEYNSQYRUWDYCAPYRUVKYCUGIUWDYCUCUVNDWMYCWNWOVNVQYR
      UWAYNUGIZUWIYNUCUWBUUGUWAUWJEAYCVBVCZYNWPWOVPWQYRUWAUWJUWHYNUCUWBUWKYNWRW
      OVPCGJYGUWGJUGWSWTGMUWGICXAXOJUCZXPUWCYFUWFQXOJDNXBUWLYEUWEXSSQUWLYDUWDAP
      XOJYCOXBVNUWLXSBJPLZSXOJBPXBUVAUWMSUCFBXCXDXEWQWQXFXGYLYPUCYRYKYOYGCYJSDK
      SVJXHXIXJXKWQURXL $.
      $( [30-Jan-2006] $) $( [6-Dec-2005] $)

    $( Lemma for ~ binom (binomial theorem).  Shift up the summation index with
       ~ fsumshft , then break out and simplify the last term of the
       summation. $)
    binomlem2 $p |- ( N e. NN -> ( sum_ k e. ( 0 ... N )
             ( ( N C. k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) x. B ) =
         ( sum_ k e. ( 1 ... N ) ( ( N C. ( k - 1 ) ) x.
        ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) + ( B ^ ( N + 1 ) ) ) ) $=
      ( vj vx cn wcel cc0 cfz co cv cbc cmin cexp cmul csu c1 caddc cn0 wceq
      nnnn0t csb cuz cfv cc wral fsummulc2 mp3an2 elnn0uz biimp wa axmulcl cz
      bcclt nn0cnt syl elfzelz sylan2 fznn0subt expclt mpan elfznn0t adantl
      sylanc r19.21aiva axmulass mp3an3 expp1t opreq2d eqtr4d eqtrd sumeq2dv
      1z fsumshft peano2nn0 ax-17 opreq2 opreq1 opreq12d cbvsum syl5eq ax1cn
      addid2 opreq1i a1i subsub3t npcant mpan2 zcnt syl2an oprex csbief
      sumeq12rdv 3eqtrd elnnuz fsump1 sylbi pncant bcnnt peano2cn subidt 3syl
      exp0t ax-mp syl6eq opreq1d mulid2t ) DIJZKDLMZDCNZOMZADXMPMZQMZBXMQMZRMZR
      MZCSBRMZTDTUAMZLMZDXMTPMZOMZAYAXMPMZQMZXQRMZRMZCSZTDLMYHCSZDYATPMZOMZAYAY
      APMZQMZBYAQMZRMZRMZUAMZYJYOUAMXKDUBJZXTYIUCDUDZYSXTXLXNXPBXMTUAMZQMZRMZRM
      ZCSZKTUAMZYALMZGYCDGNZOMZADUUHPMZQMZBUUHTUAMZQMZRMZRMZUEZCSZYIYSXTXLXSBRM
      ZCSZUUEDKUFUGJZXSUHJZCXLUIZXTUUSUCZYSUUTBUHJZUVBUVCFXSBCKDUJUKYSUUTDULUMZ
      YSUVACXLXNUHJZXRUHJZUVAYSXMXLJZUNZXNXRUOYSXMUPJZUVFUVHYSUVJUNXNUBJUVFXMDU
      QXNURUSXMKDUTVAZXPUHJZXQUHJZUVGUVIXPXQUOUVIXOUBJZUVLUBXMKDVBAUHJZUVNUVLEA
      XOVCVDUSZUVHUVMYSUVHXMUBJZUVMXMDVEZUVDUVQUVMFBXMVCVDUSVFZVGZVGVHVGYSXLUUR
      UUDCUVIUURXNXRBRMZRMZUUDUVFUVGUURUWBUCZUVIUVFUVGUVDUWCFXNXRBVIVJUVKUVTVGU
      VIUWAUUCXNRUVIUWAXPXQBRMZRMZUUCUVLUVMUWAUWEUCZUVIUVLUVMUVDUWFFXPXQBVIVJUV
      PUVSVGUVIUUBUWDXPRUVHUUBUWDUCZYSUVHUVQUWGUVRUVDUVQUWGFBXMVKVDUSVFVLVMVLVN
      VOVNYSXLUUOGSZUUQUUEUUTUUOUHJZGXLUIZUWHUUQUCZYSUUTTUPJUWJUWKVPUUOGCTKDVQU
      KUVEYSUWIGXLUUIUHJZUUNUHJZUWIYSUUHXLJZUNZUUIUUNUOYSUUHUPJZUWLUWNYSUWPUNUU
      IUBJUWLUUHDUQUUIURUSUUHKDUTVAUUKUHJZUUMUHJZUWMUWOUUKUUMUOUWOUUJUBJZUWQUBU
      UHKDVBUVOUWSUWQEAUUJVCVDUSUWNUWRYSUWNUULUBJZUWRUWNUUHUBJUWTUUHDVEUUHVRUSU
      VDUWTUWRFBUULVCVDUSVFVGVGVHVGHXLUUDUUOCGHNZUUDJGVSUXAUUOJCVSXMUUHUCZXNUUI
      UUCUUNRXMUUHDOVTUXBXPUUKUUBUUMRUXBXOUUJAQXMUUHDPVTVLUXBUUAUULBQXMUUHTUAWA
      VLWBWBWCWDYSUUGYBUUPYHCUUGYBUCYSUUFTYALTWEWFWGWHYSXMYBJZUNZYDADYCPMZQMZBY
      CTUAMZQMZRMZRMZYHUUPUXDUXIYGYDRDUHJZXMUHJZUXIYGUCYSUXCUXKUXLUNZUXFYFUXHXQ
      RUXMUXEYEAQUXKUXLTUHJZUXEYEUCWEDXMTWIVJVLUXLUXHXQUCUXKUXLUXGXMBQUXLUXNUXG
      XMUCWEXMTWJWKVLVFWBDURZUXCUVJUXLXMTYAUTXMWLUSWMVLGHYCUUOUXJXMTPWNUXAUXJJG
      VSUUHYCUCZUUIYDUUNUXIRUUHYCDOVTUXPUUKUXFUUMUXHRUXPUUJUXEAQUUHYCDPVTVLUXPU
      ULUXGBQUUHYCTUAWAVLWBWBWOWDWPWQUSXKDTUFUGJYIYRUCDWRYHYQCTDYDYGRWNYLYPRWNX
      MYAUCZYDYLYGYPRUXQYCYKDOXMYATPWAVLUXQYFYNXQYORUXQYEYMAQXMYAYAPVTVLXMYABQV
      TWBWBWSWTXKYQYOYJUAXKYSYQYOUCYTYSYQTYORMZYOYSYLTYPYORYSYLDDOMTYSYKDDOYSUX
      KYKDUCZUXOUXKUXNUXSWEDTXAWKUSVLDXBVNYSYPUXRYOYSYNTYORYSYNAKQMZTYSYMKAQYSU
      XKYAUHJYMKUCUXODXCYAXDXEVLUVOUXTTUCEAXFXGXHXIYSYAUBJZYOUHJZUXRYOUCDVRUVDU
      YAUYBFBYAVCVDYOXJXEZVNWBUYCVNUSVLWQ $.
      $( [30-Jan-2006] $) $( [7-Dec-2005] $)

    $( Lemma for ~ binom (binomial theorem).  Break out the last term of the
       summation used by the induction hypothesis. $)
    binomlem3 $p |- ( N e. NN ->
       sum_ k e. ( 1 ... ( N + 1 ) ) ( ( ( N + 1 ) C. k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) =
   ( sum_ k e. ( 1 ... N ) ( ( ( N + 1 ) C. k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) )
                  + ( B ^ ( N + 1 ) ) ) ) $=
      ( cn wcel c1 caddc co cfz cv cbc cmin cexp cmul csu cuz cfv wceq elnnuz
      oprex opreq2 opreq2d opreq12d fsump1 sylbi cn0 nnnn0t peano2nn0 bcnnt
      cc0 cc nn0cnt subidt syl exp0t ax-mp syl6eq opreq1d expclt mpan mulid2t
      eqtrd 3syl ) DGHZIDIJKZLKVHCMZNKZAVHVIOKZPKZBVIPKZQKZQKZCRZIDLKVOCRZVHVHN
      KZAVHVHOKZPKZBVHPKZQKZQKZJKZVQWAJKVGDISTHVPWDUADUBVOWCCIDVJVNQUCVRWBQUCVI
      VHUAZVJVRVNWBQVIVHVHNUDWEVLVTVMWAQWEVKVSAPVIVHVHOUDUEVIVHBPUDUFUFUGUHVGWC
      WAVQJVGDUIHVHUIHZWCWAUADUJDUKWFWCIWAQKZWAWFVRIWBWAQVHULWFWBWGWAWFVTIWAQWF
      VTAUMPKZIWFVSUMAPWFVHUNHVSUMUAVHUOVHUPUQUEAUNHWHIUAEAURUSUTVAWFWAUNHZWGWA
      UABUNHWFWIFBVHVBVCWAVDUQZVEUFWJVEVFUEVE $.
      $( [30-Jan-2006] $) $( [7-Dec-2005] $)

    $( Lemma for ~ binom (binomial theorem).  Break out the first term of the
       summation used by the induction hypothesis. $)
    binomlem4 $p |- ( N e. NN0 ->
       sum_ k e. ( 0 ... ( N + 1 ) ) ( ( ( N + 1 ) C. k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) =
   ( ( A ^ ( N + 1 ) ) + sum_ k e. ( 1 ... ( N + 1 ) ) ( ( ( N + 1 ) C. k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) ) ) $=
      ( vx cn0 wcel cc0 c1 caddc co cfz cv cbc cmin cexp cmul csu csb cuz cfv
      clt wbr cc wral wceq fsum1ps peano2nn0 nn0uz syl6eleq cn nn0p1nnt nngt0t
      syl wa axmulcl cz bcclt nn0cnt elfzelz syl2an cvv oprex fznn0subt mpan
      expclt adantl elfznn0t sylanc r19.21aiva syl3anc bcn0t subid1t 3syl
      opreq2d opreq1d ax1id eqtrd opreq12d mulid2t 0cn elisseti ax-17 opreq2
      exp0t ax-mp syl6eq csbief syl5eq ax1cn addid2 opreq1i sumeq1i a1i ) DHIZJ
      DKLMZNMZWRCOZPMZAWRWTQMZRMZBWTRMZSMZSMZCTZCJXFUAZJKLMZWRNMZXFCTZLMZAWRRMZ
      KWRNMZXFCTZLMWRJUBUCZIJWRUDUEZXFUFIZCWSUGXGXLUHWQXFCJWRUIWQWRHXPDUJZUKULW
      QWRUMIXQDUNWRUOUPWQXRCWSXAUFIZXEUFIZXRWQWTWSIZUQZXAXEURWRHIZWTUSIZXTWQYBY
      DYEUQXAHIXTWTWRUTXAVAUPXSWTJWRVBVCXCUFIZXDUFIZYAYCXCXDURYBYFWQYBXBHIZYFWR
      VDIYBYHDKLVEVDWTJWRVFVGAUFIZYHYFEAXBVHVGUPVIYBYGWQYBWTHIZYGWTWRVJBUFIZYJY
      GFBWTVHVGUPVIVKVKVLVMWQXHXMXKXOLWQWRJPMZAWRJQMZRMZKSMZSMZXMXHWQYPKXMSMZXM
      WQYLKYOXMSWQYDYLKUHXSWRVNUPWQYOXMKSMZXMWQYNXMKSWQYMWRARWQYDWRUFIYMWRUHXSW
      RVAWRVOVPVQVRWQYDXMUFIZYRXMUHXSYIYDYSEAWRVHVGZXMVSVPVTWAWQYDYSYQXMUHXSYTX
      MWBVPVTCGJXFYPJUFWCWDGOYPICWEWTJUHZXAYLXEYOSWTJWRPWFUUAXCYNXDKSUUAXBYMARW
      TJWRQWFVQUUAXDBJRMZKWTJBRWFYKUUBKUHFBWGWHWIWAWAWJWKXKXOUHWQXJXNXFCXIKWRNK
      WLWMWNWOWPWAVT $.
      $( [30-Jan-2006] $) $( [7-Dec-2005] $)

    $( Lemma for ~ binom (binomial theorem).  We use Pascal's rule ~ bcpasct
       to combine the sum of the summations in ~ binomlem1 and ~ binomlem2
       into a single summation. $)
    binomlem5 $p |- ( N e. NN -> ( sum_ k e. ( 0 ... N )
         ( ( N C. k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) x. ( A + B ) ) =
      sum_ k e. ( 0 ... ( N + 1 ) ) ( ( ( N + 1 ) C. k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) ) $=
      ( cn wcel cc0 cfz co cv cbc cmin cexp cmul csu caddc c1 cc wceq axaddass
      peano2nn cn0 expclt nnnn0t sylan2 mpan syl wral wa axmulcl cz bcclt
      nn0cnt elfzelz syl2an fzelp1t cvv oprex fznn0subt 3syl elfznnt adantl
      sylanc r19.21aiva cuz cfv fsumclt elnnuz sylanb mpdan axaddcl peano2zm
      syl3anc binomlem1 binomlem2 opreq12d binomlem3 jca fsumadd adddirt
      bcpasct opreq1d eqtr3d sumeq2dv 3eqtr2d opreq2d 3eqtr4d nn0uz syl6eleq
      elnn0uz biimpr elfznn0t axdistr mp3an23 binomlem4 ) DGHZIDJKZDCLZMKZADWTN
      KZOKZBWTOKZPKZPKZCQZAPKZXGBPKZRKZADSRKZOKZSXKJKZXKWTMKZAXKWTNKZOKZXDPKZPK
      ZCQZRKZXGABRKPKZIXKJKXRCQZWRXLSDJKZXAXQPKZCQZRKZYCDWTSNKZMKZXQPKZCQZBXKOK
      ZRKZRKZXLYEYLRKZRKZXJXTXLTHZYETHZYLTHZYMYOUAWRXLYEYLUBWRXKGHZYPDUCZATHZYS
      YPEUUAXKUDHZYPYSAXKUEXKUFZUGUHUIWRYDTHZCYCUJZYQWRUUDCYCXATHZXQTHZUUDWRWTY
      CHZUKZXAXQULZDUDHZWTUMHZUUFWRUUHUUKUULUKXAUDHZUUFWTDUNZXAUOZUIZDUFZWTSDUP
      ZUQXPTHZXDTHZUUGUUIXPXDULUUIWTXMHZXOUDHZUUSGWTSDURXKUSHUVAUVBDSRUTUSWTSXK
      VAUHUUAUVBUUSEAXOUEUHVBUUHUUTWRUUHWTGHZUUTWTDVCBTHZUVCUUTFUVDWTUDHZUUTUVC
      BWTUEZWTUFUGUHUIVDVEZVEVFDSVGVHHZUUEYQWRYDCSDVIDVJZVKVLZYJTHZYKTHZYRWRYJY
      KVMWRYITHZCYCUJZUVKWRUVMCYCYHTHZUUGUVMUUIYHXQULZUUKYGUMHZUVOWRUUHUUKUVQUK
      YHUDHZUVOYGDUNZYHUOZUIUUQUUHUULUVQUURWTVNUIZUQUVGVEVFUVHUVNUVKWRYICSDVIUV
      IVKVLZWRYSUVLYTUVDYSUVLFUVDUUBUVLYSBXKUEUUCUGUHUIZVEVOWRXHYFXIYLRABCDEFVP
      ABCDEFVQVRWRXSYNXLRWRXSYCXRCQZYKRKYEYJRKZYKRKZYNABCDEFVSWRUWEUWDYKRWRYCYD
      YIRKZCQZUWEUWDWRUUDUVMUKZCYCUJZUWHUWEUAZWRUWICYCUUIUUDUVMUUFUUGUUDUUIUUJU
      UIUUMUUFUUKUULUUMWRUUHUUNUUQUURUQUUOUIZUVGVEUVOUUGUVMUUIUVPUUIUVRUVOUUKUV
      QUVRWRUUHUVSUUQUWAUQUVTUIZUVGVEVTVFUVHUWJUWKWRYDYICSDWAUVIVKVLWRYCUWGXRCU
      UIXAYHRKZXQPKZUWGXRUUFUVOUUGUWOUWGUAUUIXAYHXQWBUWLUWMUVGVOUUIUWNXNXQPUUKU
      ULUWNXNUAWRUUHWTDWCUUQUURUQWDWEWFWEWDYQUVKUVLUWFYNUAWRYEYJYKUBUVJUWBUWCVO
      WGWHWIWRDIVGVHZHZXGTHZYAXJUAZWRDUDUWPUUQWJWKUWQXFTHZCWSUJUWRUWQUWTCWSUUFX
      ETHZUWTUWQWTWSHZUKZXAXEULUUKUULUUFUWQUXBUUPUUKUWQDWLWMWTIDUPUQXCTHZUUTUXA
      UXCXCXDULUXCXBUDHZUXDUWPWTIDVAUUAUXEUXDEAXBUEUHUIUXBUUTUWQUXBUVEUUTWTDWNU
      VDUVEUUTFUVFUHUIVDVEVEVFXFCIDVIVLUWRUUAUVDUWSEFXGABWOWPVBWRUUKYBXTUAUUQAB
      CDEFWQUIWI $.
      $( [31-Jul-2006] $) $( [7-Dec-2005] $)

    $( Lemma for ~ binom (binomial theorem).  Do the final induction. $)
    binomlem6 $p |- ( N e. NN -> ( ( A + B ) ^ N ) = sum_ k e. ( 0 ... N )
         ( ( N C. k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) ) $=
      ( vx vj caddc co cv cexp cc0 cfz cbc cmin cmul csu wceq c1 opreq2 wcel
      opreq1 opreq2d opreq1d opreq12d adantr sumeq12rdv eqeq12d cc addcl exp1t
      ax-mp cuz cfv cz 0z uzidt oprex elisseti ax1cn addid2 eqeq2i cn0 1nn0
      bcnnt syl6eq subid exp0t mulid2 sylbi fsump1 bcn0t subid1 mulid1 fsum1
      mp2an opreq1i eqtr2 opreq2i sumeq1i 3eqtr cn wa expp1t nnnn0t sylan2
      mpan pm3.27 binomlem5 3eqtrd ex nnind ) ABIJZGKZLJZMWONJZWOCKZOJZAWOWRPJZ
      LJZBWRLJZQJZQJZCRZSWNTLJZMTNJZTWROJZATWRPJZLJZXBQJZQJZCRZSWNHKZLJZMXNNJZX
      NWROJZAXNWRPJZLJZXBQJZQJZCRZSZWNXNTIJZLJZMYDNJZYDWROJZAYDWRPJZLJZXBQJZQJZ
      CRZSZWNDLJZMDNJZDWROJZADWRPJZLJZXBQJZQJZCRZSGHDWOTSZWPXFXEXMWOTWNLUAUUBWQ
      XGXDXLCWOTMNUAUUBXDXLSWRXGUBUUBWSXHXCXKQWOTWROUCUUBXAXJXBQUUBWTXIALWOTWRP
      UCUDUEUFUGUHUIWOXNSZWPXOXEYBWOXNWNLUAUUCWQXPXDYACWOXNMNUAUUCXDYASWRXPUBUU
      CWSXQXCXTQWOXNWROUCUUCXAXSXBQUUCWTXRALWOXNWRPUCUDUEUFUGUHUIWOYDSZWPYEXEYL
      WOYDWNLUAUUDWQYFXDYKCWOYDMNUAUUDXDYKSWRYFUBUUDWSYGXCYJQWOYDWROUCUUDXAYIXB
      QUUDWTYHALWOYDWRPUCUDUEUFUGUHUIWODSZWPYNXEUUAWODWNLUAUUEWQYOXDYTCWODMNUAU
      UEXDYTSWRYOUBUUEWSYPXCYSQWODWROUCUUEXAYRXBQUUEWTYQALWODWRPUCUDUEUFUGUHUIX
      FWNMMTIJZNJZXLCRZXMWNUJUBZXFWNSABEFUKZWNULUMUUHMMNJXLCRZBIJZWNMMUNUOUBZUU
      HUULSMUPUBZUUMUQMURUMXLBCMMXHXKQUSBUJFUTWRUUFSWRTSZXLBSUUFTWRTVAVBZVCUUOX
      LTBQJZBUUOXHTXKBQUUOXHTTOJZTWRTTOUATVDUBZUURTSVETVFUMVGUUOXKUUQBUUOXJTXBB
      QUUOXJAMLJZTUUOXIMALUUOXITTPJMWRTTPUATVAVHVGUDAUJUBZUUTTSEAVIUMVGUUOXBBTL
      JZBWRTBLUABUJUBZUVBBSFBULUMVGUFBFVJZVGUFUVDVGVKVLUMUUKABIUVAUUNUUKASEUQXL
      AUJCMWRMSZXLTAQJAUVEXHTXKAQUVEXHTMOJZTWRMTOUAUUSUVFTSVETVMUMVGUVEXKATQJAU
      VEXJAXBTQUVEXJATLJZAUVEXITALUVEXITMPJTWRMTPUATVAVNVGUDUVAUVGASEAULUMVGUVE
      XBBMLJZTWRMBLUAUVCUVHTSFBVIUMVGUFAEVOVGUFAEVJVGVPVQVRVSUUGXGXLCUUFTMNUUPV
      TWAWBXNWCUBZYCYMUVIYCWDZYEXOWNQJZYBWNQJZYLUVIYEUVKSZYCUUIUVIUVMUUJUUIXNVD
      UBUVMUVIWNXNWEXNWFWGWHUGUVJXOYBWNQUVIYCWIUEUVIUVLYLSYCABCXNEFWJUGWKWLWM
      $.
      $( [28-Jul-2006] $) $( [7-Dec-2005] $)

    $( The binomial theorem:  ` ( A + B ) ^ N ` is the sum from ` k = 0 ` to
       ` N ` of ` ( N C. k ) x. ( ( A ^ k ) x. ( B ^ ( N - k ) ) ` .  Theorem
       15-2.8 of [Gleason] p. 296.  This final piece of the proof combines
       the ` 0 < N ` case of ~ binomlem6 with the ` N = 0 ` case. $)
    binom $p |- ( N e. NN0 -> ( ( A + B ) ^ N ) = sum_ k e. ( 0 ... N )
         ( ( N C. k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) ) $=
      ( cn0 wcel cn cc0 wceq wo caddc co cexp cfz cv cbc cmin cmul csu elnn0
      binomlem6 c1 cc cz ax1cn 0z opreq2 0nn0 bcn0t ax-mp syl6eq 0cn subid
      opreq2d exp0t opreq12d mulid1 fsum1 mp2an eqcomi addcl opreq1 opreq1d
      adantr sumeq12rdv 3eqtr4a jaoi sylbi ) DGHDIHZDJKZLABMNZDONZJDPNZDCQZRNZA
      DVPSNZONZBVPONZTNZTNZCUAZKZDUBVKWDVLABCDEFUCVLUDJJPNZJVPRNZAJVPSNZONZVTTN
      ZTNZCUAZVNWCWKUDUDUEHJUFHWKUDKUGUHWJUDUECJVPJKZWJUDUDTNZUDWLWFUDWIUDTWLWF
      JJRNZUDVPJJRUIJGHWNUDKUJJUKULUMWLWIWMUDWLWHUDVTUDTWLWHAJONZUDWLWGJAOWLWGJ
      JSNJVPJJSUIJUNUOUMUPAUEHWOUDKEAUQULUMWLVTBJONZUDVPJBOUIBUEHWPUDKFBUQULUMU
      RUDUGUSZUMURWQUMUTVAVBVLVNVMJONZUDDJVMOUIVMUEHWRUDKABEFVCVMUQULUMVLVOWEWB
      WJCDJJPUIVLWBWJKVPWEHVLVQWFWAWITDJVPRVDVLVSWHVTTVLVRWGAODJVPSVDUPVEURVFVG
      VHVIVJ $.
      $( [31-Jan-2006] $) $( [7-Dec-2005] $)
  $}


