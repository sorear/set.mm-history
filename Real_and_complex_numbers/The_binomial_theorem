$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The binomial theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d j k x A $.  $d j k x B $.  $d j k x N $.
    binomlem.1 $e |- A e. CC $.
    binomlem.2 $e |- B e. CC $.
    $( Lemma for ~ binomi (binomial theorem).  Break out and simplify the first
       term of the summation. $)
    binomlem1 $p |- ( N e. NN -> ( sum_ k e. ( 0 ... N )
             ( ( N _C k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) x. A ) =
         ( ( A ^ ( N + 1 ) ) + sum_ k e. ( 1 ... N ) ( ( N _C k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) ) ) $=
      ( vx cn wcel cc0 cfz co cv cbc cmin cexp cmul csu c1 caddc csb cn0 wceq
      nnnn0 cuz cfv cc wral elnn0uz biimpi wa cz elfzelz bccl nn0cn syl sylan2
      fznn0sub expcl sylancr elfznn0 adantl mulcl syl11anc r19.21aiva
      fsummulc2 mp3an2 mulass mp3an3 zcn ax1cn addsub syl2an opreq2d expp1
      eqtrd opreq1d mul32 eqtr2d sumeq2dv clt wbr nn0uz syl6eleq nngt0
      peano2nn0 adantr fzelp1 sylan fsum1ps syl111anc bcn0 peano2cn subid1
      3syl mpan mulid1 opreq12d mulid2 0cn elisseti ax-17 opreq2 exp0 ax-mp
      syl6eq csbief syl5eq addid2i opreq1i sumeq1i a1i 3eqtrd ) DHIZJDKLZDCMZNL
      ZADXPOLZPLZBXPPLZQLZQLZCRAQLZXOXQADSTLZXPOLZPLZXTQLZQLZCRZCJYHUAZJSTLZDKL
      ZYHCRZTLZAYDPLZSDKLZYHCRZTLZXNDUBIZYCYIUCDUDZYSYCXOYBAQLZCRZYIYSDJUEUFZIZ
      YBUGIZCXOUHZYCUUBUCZYSUUDDUIUJYSUUECXOYSXPXOIZUKZXQUGIZYAUGIZUUEUUHYSXPUL
      IZUUJXPJDUMZYSUULUKXQUBIUUJXPDUNXQUOUPUQZUUIXSUGIZXTUGIZUUKUUIAUGIZXRUBIZ
      UUOEUBXPJDURZAXRUSUTZUUHUUPYSUUHBUGIZXPUBIUUPFXPDVABXPUSUTVBZXSXTVCVDZXQY
      AVCVDVEUUDUUQUUFUUGEYBACJDVFVGVDYSXOUUAYHCUUIUUAXQYAAQLZQLZYHUUIUUJUUKUUA
      UVEUCZUUNUVCUUJUUKUUQUVFEXQYAAVHVIVDUUIUVDYGXQQUUIYGXSAQLZXTQLZUVDUUIYFUV
      GXTQUUIYFAXRSTLZPLZUVGUUIYEUVIAPYSDUGIZXPUGIZYEUVIUCZUUHDUOZUUHUULUVLUUMX
      PVJUPUVKSUGIUVLUVMVKDSXPVLVGVMVNUUIUUQUURUVJUVGUCEUUSAXRVOUTVPVQUUIUUOUUP
      UVHUVDUCZUUTUVBUUOUUQUUPUVOEXSAXTVRVGVDVSVNVPVTVPUPXNUUDJDWAWBYHUGIZCXOUH
      YIYNUCXNDUBUUCYTWCWDDWEXNUVPCXOXNYSUUHUVPYTUUIUUJYGUGIZUVPUUNUUIYFUGIZUUP
      UVQUUIUUQYEUBIZUVREUUIYDUBIZXPJYDKLIUVSYSUVTUUHDWFZWGUBXPJDWHUBXPJYDURVDA
      YEUSUTUVBYFXTVCVDXQYGVCVDWIVEYHCJDWJWKXNYSYNYRUCYTYSYJYOYMYQTYSDJNLZAYDJO
      LZPLZSQLZQLZYOYJYSUWFSYOQLZYOYSUWBSUWEYOQDWLYSUWEYOSQLZYOYSUWDYOSQYSUWCYD
      APYSUVKYDUGIUWCYDUCUVNDWMYDWNWOVNVQYSUVTYOUGIZUWHYOUCUWAUUQUVTUWIEAYDUSWP
      ZYOWQWOVPWRYSUVTUWIUWGYOUCUWAUWJYOWSWOVPCGJYHUWFJUGWTXAGMUWFICXBXPJUCZXQU
      WBYGUWEQXPJDNXCUWKYFUWDXTSQUWKYEUWCAPXPJYDOXCVNUWKXTBJPLZSXPJBPXCUVAUWLSU
      CFBXDXEXFWRWRXGXHYMYQUCYSYLYPYHCYKSDKSVKXIXJXKXLWRUPXM $.
      $( [6-Dec-2005] $)

    $( Lemma for ~ binomi (binomial theorem).  Shift up the summation index
       with ~ fsumshft , then break out and simplify the last term of the
       summation. $)
    binomlem2 $p |- ( N e. NN -> ( sum_ k e. ( 0 ... N )
             ( ( N _C k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) x. B ) =
         ( sum_ k e. ( 1 ... N ) ( ( N _C ( k - 1 ) ) x.
        ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) + ( B ^ ( N + 1 ) ) ) ) $=
      ( vj vx cn wcel cc0 cfz co cv cbc cmin cexp cmul csu c1 caddc cn0 wceq
      nnnn0 csb cuz cfv cc wral elnn0uz biimpi wa cz elfzelz bccl nn0cn syl
      sylan2 fznn0sub expcl sylancr elfznn0 adantl mulcl syl11anc r19.21aiva
      fsummulc2 mp3an2 mulass mp3an3 expp1 opreq2d eqtr4d eqtrd sumeq2dv
      peano2nn0 1z fsumshft ax-17 opreq2 opreq1 opreq12d cbvsumi syl5eq ax1cn
      addid2i opreq1i a1i zcn subsub3 npcan mpan2 syl2an oprex csbief
      sumeq12rdv 3eqtrd elnnuz fsump1i sylbi pncan sylancl bcnn peano2cn subid
      3syl exp0 ax-mp syl6eq opreq1d mpan mulid2 ) DIJZKDLMZDCNZOMZADXOPMZQMZBX
      OQMZRMZRMZCSBRMZTDTUAMZLMZDXOTPMZOMZAYCXOPMZQMZXSRMZRMZCSZTDLMYJCSZDYCTPM
      ZOMZAYCYCPMZQMZBYCQMZRMZRMZUAMZYLYQUAMXMDUBJZYBYKUCDUDZUUAYBXNXPXRBXOTUAM
      ZQMZRMZRMZCSZKTUAMZYCLMZGYEDGNZOMZADUUJPMZQMZBUUJTUAMZQMZRMZRMZUEZCSZYKUU
      AYBXNYABRMZCSZUUGUUADKUFUGJZYAUHJZCXNUIZYBUVAUCZUUAUVBDUJUKZUUAUVCCXNUUAX
      OXNJZULZXPUHJZXTUHJZUVCUVGUUAXOUMJZUVIXOKDUNUUAUVKULXPUBJUVIXODUOXPUPUQUR
      ZUVHXRUHJZXSUHJZUVJUVHAUHJZXQUBJUVMEUBXOKDUSAXQUTVAZUVGUVNUUAUVGBUHJZXOUB
      JZUVNFXODVBZBXOUTVAVCZXRXSVDVEZXPXTVDVEVFUVBUVQUVDUVEFYABCKDVGVHVEUUAXNUU
      TUUFCUVHUUTXPXTBRMZRMZUUFUVHUVIUVJUUTUWCUCZUVLUWAUVIUVJUVQUWDFXPXTBVIVJVE
      UVHUWBUUEXPRUVHUWBXRXSBRMZRMZUUEUVHUVMUVNUWBUWFUCZUVPUVTUVMUVNUVQUWGFXRXS
      BVIVJVEUVHUUDUWEXRRUVGUUDUWEUCZUUAUVGUVQUVRUWHFUVSBXOVKVAVCVLVMVLVNVOVNUU
      AXNUUQGSZUUSUUGUUAUVBUUQUHJZGXNUIZUWIUUSUCZUVFUUAUWJGXNUUAUUJXNJZULZUUKUH
      JZUUPUHJZUWJUWMUUAUUJUMJZUWOUUJKDUNUUAUWQULUUKUBJUWOUUJDUOUUKUPUQURUWNUUM
      UHJZUUOUHJZUWPUWNUVOUULUBJUWREUBUUJKDUSAUULUTVAUWMUWSUUAUWMUVQUUNUBJZUWSF
      UWMUUJUBJUWTUUJDVBUUJVPUQBUUNUTVAVCUUMUUOVDVEUUKUUPVDVEVFUVBTUMJUWKUWLVQU
      UQGCTKDVRVHVEHXNUUFUUQCGHNZUUFJGVSUXAUUQJCVSXOUUJUCZXPUUKUUEUUPRXOUUJDOVT
      UXBXRUUMUUDUUORUXBXQUULAQXOUUJDPVTVLUXBUUCUUNBQXOUUJTUAWAVLWBWBWCWDUUAUUI
      YDUURYJCUUIYDUCUUAUUHTYCLTWEWFWGWHUUAXOYDJZULZYFADYEPMZQMZBYETUAMZQMZRMZR
      MZYJUURUXDUXIYIYFRUUADUHJZXOUHJZUXIYIUCUXCDUPZUXCUVKUXLXOTYCUNXOWIUQUXKUX
      LULZUXFYHUXHXSRUXNUXEYGAQUXKUXLTUHJZUXEYGUCWEDXOTWJVJVLUXLUXHXSUCUXKUXLUX
      GXOBQUXLUXOUXGXOUCWEXOTWKWLVLVCWBWMVLGHYEUUQUXJXOTPWNUXAUXJJGVSUUJYEUCZUU
      KYFUUPUXIRUUJYEDOVTUXPUUMUXFUUOUXHRUXPUULUXEAQUUJYEDPVTVLUXPUUNUXGBQUUJYE
      TUAWAVLWBWBWOWDWPWQUQXMDTUFUGJYKYTUCDWRYJYSCTDYFYIRWNYNYRRWNXOYCUCZYFYNYI
      YRRUXQYEYMDOXOYCTPWAVLUXQYHYPXSYQRUXQYGYOAQXOYCYCPVTVLXOYCBQVTWBWBWSWTXMY
      SYQYLUAXMUUAYSYQUCUUBUUAYSTYQRMZYQUUAYNTYRYQRUUAYNDDOMTUUAYMDDOUUAUXKUXOY
      MDUCUXMWEDTXAXBVLDXCVNUUAYRUXRYQUUAYPTYQRUUAYPAKQMZTUUAYOKAQUUAUXKYCUHJYO
      KUCUXMDXDYCXEXFVLUVOUXSTUCEAXGXHXIXJUUAYCUBJZYQUHJZUXRYQUCDVPUVQUXTUYAFBY
      CUTXKYQXLXFZVNWBUYBVNUQVLWQ $.
      $( [7-Dec-2005] $)

    $( Lemma for ~ binomi (binomial theorem).  Break out the last term of the
       summation used by the induction hypothesis. $)
    binomlem3 $p |- ( N e. NN ->
       sum_ k e. ( 1 ... ( N + 1 ) ) ( ( ( N + 1 ) _C k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) =
   ( sum_ k e. ( 1 ... N ) ( ( ( N + 1 ) _C k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) )
                  + ( B ^ ( N + 1 ) ) ) ) $=
      ( cn wcel c1 caddc co cfz cv cbc cmin cexp cmul csu cuz cfv wceq elnnuz
      oprex opreq2 opreq2d opreq12d fsump1i sylbi cn0 nnnn0 peano2nn0 bcnn cc0
      cc nn0cn subid syl exp0 ax-mp syl6eq opreq1d expcl mpan mulid2 eqtrd
      3syl ) DGHZIDIJKZLKVHCMZNKZAVHVIOKZPKZBVIPKZQKZQKZCRZIDLKVOCRZVHVHNKZAVHV
      HOKZPKZBVHPKZQKZQKZJKZVQWAJKVGDISTHVPWDUADUBVOWCCIDVJVNQUCVRWBQUCVIVHUAZV
      JVRVNWBQVIVHVHNUDWEVLVTVMWAQWEVKVSAPVIVHVHOUDUEVIVHBPUDUFUFUGUHVGWCWAVQJV
      GDUIHVHUIHZWCWAUADUJDUKWFWCIWAQKZWAWFVRIWBWAQVHULWFWBWGWAWFVTIWAQWFVTAUMP
      KZIWFVSUMAPWFVHUNHVSUMUAVHUOVHUPUQUEAUNHWHIUAEAURUSUTVAWFWAUNHZWGWAUABUNH
      WFWIFBVHVBVCWAVDUQZVEUFWJVEVFUEVE $.
      $( [7-Dec-2005] $)

    $( Lemma for ~ binomi (binomial theorem).  Break out the first term of the
       summation used by the induction hypothesis. $)
    binomlem4 $p |- ( N e. NN0 ->
       sum_ k e. ( 0 ... ( N + 1 ) ) ( ( ( N + 1 ) _C k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) =
   ( ( A ^ ( N + 1 ) ) + sum_ k e. ( 1 ... ( N + 1 ) ) ( ( ( N + 1 ) _C k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) ) ) $=
      ( vx cn0 wcel cc0 c1 caddc co cfz cv cbc cmin cexp cmul csu csb cuz cfv
      clt wbr cc wral wceq peano2nn0 nn0uz syl6eleq cn nn0p1nn nngt0 syl wa cz
      elfzelz bccl nn0cn syl2an cvv oprex fznn0sub mpan expcl sylancr adantl
      elfznn0 mulcl syl11anc r19.21aiva fsum1ps syl111anc bcn0 subid1 3syl
      opreq2d opreq1d mulid1 eqtrd opreq12d mulid2 0cn elisseti ax-17 opreq2
      exp0 ax-mp syl6eq csbief syl5eq ax1cn addid2i opreq1i sumeq1i a1i ) DHIZJ
      DKLMZNMZWSCOZPMZAWSXAQMZRMZBXARMZSMZSMZCTZCJXGUAZJKLMZWSNMZXGCTZLMZAWSRMZ
      KWSNMZXGCTZLMWRWSJUBUCZIJWSUDUEZXGUFIZCWTUGXHXMUHWRWSHXQDUIZUJUKWRWSULIXR
      DUMWSUNUOWRXSCWTWRXAWTIZUPZXBUFIZXFUFIZXSWRWSHIZXAUQIZYCYAXTXAJWSURYEYFUP
      XBHIYCXAWSUSXBUTUOVAYBXDUFIZXEUFIZYDYAYGWRYAAUFIZXCHIZYGEWSVBIYAYJDKLVCVB
      XAJWSVDVEAXCVFVGVHYAYHWRYABUFIZXAHIYHFXAWSVIBXAVFVGVHXDXEVJVKXBXFVJVKVLXG
      CJWSVMVNWRXIXNXLXPLWRWSJPMZAWSJQMZRMZKSMZSMZXNXIWRYPKXNSMZXNWRYLKYOXNSWRY
      EYLKUHXTWSVOUOWRYOXNKSMZXNWRYNXNKSWRYMWSARWRYEWSUFIYMWSUHXTWSUTWSVPVQVRVS
      WRYEXNUFIZYRXNUHXTYIYEYSEAWSVFVEZXNVTVQWAWBWRYEYSYQXNUHXTYTXNWCVQWACGJXGY
      PJUFWDWEGOYPICWFXAJUHZXBYLXFYOSXAJWSPWGUUAXDYNXEKSUUAXCYMARXAJWSQWGVRUUAX
      EBJRMZKXAJBRWGYKUUBKUHFBWHWIWJWBWBWKWLXLXPUHWRXKXOXGCXJKWSNKWMWNWOWPWQWBW
      A $.
      $( [7-Dec-2005] $)

    $( Lemma for ~ binomi (binomial theorem).  We use Pascal's rule ~ bcpasc to
       combine the sum of the summations in ~ binomlem1 and ~ binomlem2 into a
       single summation. $)
    binomlem5 $p |- ( N e. NN -> ( sum_ k e. ( 0 ... N )
         ( ( N _C k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) x. ( A + B ) ) =
      sum_ k e. ( 0 ... ( N + 1 ) ) ( ( ( N + 1 ) _C k ) x.
             ( ( A ^ ( ( N + 1 ) - k ) ) x. ( B ^ k ) ) ) ) $=
      ( cn wcel cc0 cfz co cv cbc cmin cexp cmul csu caddc c1 cc wceq peano2nn
      cn0 nnnn0 expcl sylan2 sylancr wral wa cz elfzelz bccl nn0cn syl syl2an
      fzelp1 cvv oprex fznn0sub mpan 3syl elfznn adantl mulcl syl11anc
      r19.21aiva cuz cfv elnnuz fsumcl sylanb mpdan peano2zm addcl addass
      syl111anc binomlem1 binomlem2 opreq12d binomlem3 jca fsumadd adddir
      bcpasc opreq1d eqtr3d sumeq2dv 3eqtr2d opreq2d 3eqtr4d nn0uz syl6eleq
      elnn0uz biimpri elfznn0 adddi mp3an23 binomlem4 ) DGHZIDJKZDCLZMKZADXANKZ
      OKZBXAOKZPKZPKZCQZAPKZXHBPKZRKZADSRKZOKZSXLJKZXLXAMKZAXLXANKZOKZXEPKZPKZC
      QZRKZXHABRKPKZIXLJKXSCQZWSXMSDJKZXBXRPKZCQZRKZYDDXASNKZMKZXRPKZCQZBXLOKZR
      KZRKZXMYFYMRKZRKZXKYAWSXMTHZYFTHZYMTHZYNYPUAWSATHZXLGHZYQEDUBZUUAYTXLUCHZ
      YQXLUDZAXLUEUFUGWSYETHZCYDUHZYRWSUUECYDWSXAYDHZUIZXBTHZXRTHZUUEWSDUCHZXAU
      JHZUUIUUGDUDZXASDUKZUUKUULUIXBUCHZUUIXADULZXBUMZUNZUOUUHXQTHZXETHZUUJUUHX
      AXNHZXPUCHZUUSGXASDUPXLUQHUVAUVBDSRURUQXASXLUSUTYTUVBUUSEAXPUEUTVAUUGUUTW
      SUUGBTHZXAGHZUUTFXADVBUVDUVCXAUCHZUUTXAUDBXAUEZUFUGVCXQXEVDVEZXBXRVDZVEVF
      WSDSVGVHHZUUFYRDVIZYECSDVJVKVLZWSYKTHZYLTHZYSWSYJTHZCYDUHZUVLWSUVNCYDUUHY
      ITHZUUJUVNWSUUKYHUJHZUVPUUGUUMUUGUULUVQUUNXAVMUNZUUKUVQUIYIUCHZUVPYHDULZY
      IUMZUNUOUVGYIXRVDZVEVFWSUVIUVOUVLUVJYJCSDVJVKVLZWSUVCUUAUVMFUUBUUAUVCUUCU
      VMUUDBXLUEUFUGZYKYLVNVEXMYFYMVOVPWSXIYGXJYMRABCDEFVQABCDEFVRVSWSXTYOXMRWS
      XTYDXSCQZYLRKYFYKRKZYLRKZYOABCDEFVTWSUWFUWEYLRWSYDYEYJRKZCQZUWFUWEWSUUEUV
      NUIZCYDUHZUWIUWFUAZWSUWJCYDUUHUUEUVNUUHUUIUUJUUEUUHUUOUUIWSUUKUULUUOUUGUU
      MUUNUUPUOUUQUNZUVGUVHVEUUHUVPUUJUVNUUHUVSUVPWSUUKUVQUVSUUGUUMUVRUVTUOUWAU
      NZUVGUWBVEWAVFWSUVIUWKUWLUVJYEYJCSDWBVKVLWSYDUWHXSCUUHXBYIRKZXRPKZUWHXSUU
      HUUIUVPUUJUWPUWHUAUWMUWNUVGXBYIXRWCVPUUHUWOXOXRPWSUUKUULUWOXOUAUUGUUMUUNX
      ADWDUOWEWFWGWFWEWSYRUVLUVMUWGYOUAUVKUWCUWDYFYKYLVOVPWHWIWJWSDIVGVHZHZXHTH
      ZYBXKUAZWSDUCUWQUUMWKWLUWRXGTHZCWTUHUWSUWRUXACWTUWRXAWTHZUIZUUIXFTHZUXAUW
      RUUKUULUUIUXBUUKUWRDWMWNXAIDUKUURUOUXCXDTHZUUTUXDUXCYTXCUCHUXEEUWQXAIDUSA
      XCUEUGUXBUUTUWRUXBUVCUVEUUTFXADWOUVFUGVCXDXEVDVEXBXFVDVEVFXGCIDVJVLUWSYTU
      VCUWTEFXHABWPWQVAWSUUKYCYAUAUUMABCDEFWRUNWJ $.
      $( [7-Dec-2005] $)

    $( Lemma for ~ binomi (binomial theorem).  Do the final induction. $)
    binomlem6 $p |- ( N e. NN -> ( ( A + B ) ^ N ) = sum_ k e. ( 0 ... N )
         ( ( N _C k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) ) $=
      ( vx vj caddc co cv cexp cc0 cfz cbc cmin cmul csu wceq c1 opreq2 wcel
      opreq1 opreq2d opreq1d opreq12d adantr sumeq12rdv eqeq12d cc addcli exp1
      ax-mp cuz cfv cz 0z uzid oprex elisseti ax1cn addid2i eqeq2i cn0 1nn0
      bcnn syl6eq subidi exp0 mulid2i sylbi fsump1i bcn0 subid1i mulid1i
      fsum1i mp2an opreq1i eqtr2i opreq2i sumeq1i 3eqtri cn wa nnnn0 expp1
      sylancr simpr binomlem5 3eqtrd ex nnind ) ABIJZGKZLJZMWNNJZWNCKZOJZAWNWQP
      JZLJZBWQLJZQJZQJZCRZSWMTLJZMTNJZTWQOJZATWQPJZLJZXAQJZQJZCRZSWMHKZLJZMXMNJ
      ZXMWQOJZAXMWQPJZLJZXAQJZQJZCRZSZWMXMTIJZLJZMYCNJZYCWQOJZAYCWQPJZLJZXAQJZQ
      JZCRZSZWMDLJZMDNJZDWQOJZADWQPJZLJZXAQJZQJZCRZSGHDWNTSZWOXEXDXLWNTWMLUAUUA
      WPXFXCXKCWNTMNUAUUAXCXKSWQXFUBUUAWRXGXBXJQWNTWQOUCUUAWTXIXAQUUAWSXHALWNTW
      QPUCUDUEUFUGUHUIWNXMSZWOXNXDYAWNXMWMLUAUUBWPXOXCXTCWNXMMNUAUUBXCXTSWQXOUB
      UUBWRXPXBXSQWNXMWQOUCUUBWTXRXAQUUBWSXQALWNXMWQPUCUDUEUFUGUHUIWNYCSZWOYDXD
      YKWNYCWMLUAUUCWPYEXCYJCWNYCMNUAUUCXCYJSWQYEUBUUCWRYFXBYIQWNYCWQOUCUUCWTYH
      XAQUUCWSYGALWNYCWQPUCUDUEUFUGUHUIWNDSZWOYMXDYTWNDWMLUAUUDWPYNXCYSCWNDMNUA
      UUDXCYSSWQYNUBUUDWRYOXBYRQWNDWQOUCUUDWTYQXAQUUDWSYPALWNDWQPUCUDUEUFUGUHUI
      XEWMMMTIJZNJZXKCRZXLWMUJUBZXEWMSABEFUKZWMULUMUUGMMNJXKCRZBIJZWMMMUNUOUBZU
      UGUUKSMUPUBZUULUQMURUMXKBCMMXGXJQUSBUJFUTWQUUESWQTSZXKBSUUETWQTVAVBZVCUUN
      XKTBQJZBUUNXGTXJBQUUNXGTTOJZTWQTTOUATVDUBZUUQTSVETVFUMVGUUNXJUUPBUUNXITXA
      BQUUNXIAMLJZTUUNXHMALUUNXHTTPJMWQTTPUATVAVHVGUDAUJUBZUUSTSEAVIUMVGUUNXABT
      LJZBWQTBLUABUJUBZUVABSFBULUMVGUFBFVJZVGUFUVCVGVKVLUMUUJABIUUTUUMUUJASEUQX
      KAUJCMWQMSZXKTAQJAUVDXGTXJAQUVDXGTMOJZTWQMTOUAUURUVETSVETVMUMVGUVDXJATQJA
      UVDXIAXATQUVDXIATLJZAUVDXHTALUVDXHTMPJTWQMTPUATVAVNVGUDUUTUVFASEAULUMVGUV
      DXABMLJZTWQMBLUAUVBUVGTSFBVIUMVGUFAEVOVGUFAEVJVGVPVQVRVSUUFXFXKCUUETMNUUO
      VTWAWBXMWCUBZYBYLUVHYBWDZYDXNWMQJZYAWMQJZYKUVHYDUVJSZYBUVHUUHXMVDUBUVLUUI
      XMWEWMXMWFWGUGUVIXNYAWMQUVHYBWHUEUVHUVKYKSYBABCXMEFWIUGWJWKWL $.
      $( [7-Dec-2005] $)

    $( The binomial theorem: ` ( A + B ) ^ N ` is the sum from ` k = 0 ` to
       ` N ` of ` ( N _C k ) x. ( ( A ^ k ) x. ( B ^ ( N - k ) ) ` .  Theorem
       15-2.8 of [Gleason] p. 296.  This final piece of the proof combines the
       ` 0 < N ` case of ~ binomlem6 with the ` N = 0 ` case. $)
    binomi $p |- ( N e. NN0 -> ( ( A + B ) ^ N ) = sum_ k e. ( 0 ... N )
         ( ( N _C k ) x. ( ( A ^ ( N - k ) ) x. ( B ^ k ) ) ) ) $=
      ( cn0 wcel cn cc0 wceq wo caddc co cexp cfz cv cbc cmin cmul csu elnn0
      binomlem6 c1 cc cz ax1cn 0z opreq2 0nn0 bcn0 ax-mp syl6eq 0cn subidi
      opreq2d exp0 opreq12d mulid1i fsum1i mp2an eqcomi addcli opreq1 opreq1d
      adantr sumeq12rdv 3eqtr4a jaoi sylbi ) DGHDIHZDJKZLABMNZDONZJDPNZDCQZRNZA
      DVPSNZONZBVPONZTNZTNZCUAZKZDUBVKWDVLABCDEFUCVLUDJJPNZJVPRNZAJVPSNZONZVTTN
      ZTNZCUAZVNWCWKUDUDUEHJUFHWKUDKUGUHWJUDUECJVPJKZWJUDUDTNZUDWLWFUDWIUDTWLWF
      JJRNZUDVPJJRUIJGHWNUDKUJJUKULUMWLWIWMUDWLWHUDVTUDTWLWHAJONZUDWLWGJAOWLWGJ
      JSNJVPJJSUIJUNUOUMUPAUEHWOUDKEAUQULUMWLVTBJONZUDVPJBOUIBUEHWPUDKFBUQULUMU
      RUDUGUSZUMURWQUMUTVAVBVLVNVMJONZUDDJVMOUIVMUEHWRUDKABEFVCVMUQULUMVLVOWEWB
      WJCDJJPUIVLWBWJKVPWEHVLVQWFWAWITDJVPRVDVLVSWHVTTVLVRWGAODJVPSVDUPVEURVFVG
      VHVIVJ $.
      $( [7-Dec-2005] $)
  $}

  ${
    $d k A $.  $d k N $.
    binom1p.1 $e |- A e. CC $.
    $( Special case of the binomial theorem for ` ( 1 + A ) ^ N ` .
       (Contributed by Paul Chapman, 10-May-2007.) $)
    binom1pi $p |- ( N e. NN0 -> ( ( 1 + A ) ^ N ) =
                  sum_ k e. ( 0 ... N ) ( ( N _C k ) x. ( A ^ k ) ) ) $=
      ( cn0 wcel c1 caddc co cexp cc0 cfz cv cbc cmin cmul csu ax1cn binomi wa
      wceq fznn0sub 1exp syl opreq1d elfznn0 cc expcl mpan mulid2 adantl eqtrd
      opreq2d sumeq2dv ) CEFZGAHICJIKCLIZCBMZNIZGCUQOIZJIZAUQJIZPIZPIZBQUPURVAP
      IZBQGABCRDSUOUPVCVDBUOUQUPFZTZVBVAURPVFVBGVAPIZVAVFUTGVAPVFUSEFUTGUAEUQKC
      UBUSUCUDUEVEVGVAUAZUOVEUQEFZVHUQCUFVIVAUGFZVHAUGFVIVJDAUQUHUIVAUJUDUDUKUL
      UMUNUL $.
      $( [10-May-2007] $)
  $}

  ${
    $d M m $.  $d N j k m $.
    $( Lemma for ~ bcxmas . $)
    bcxmaslem1 $p |- ( A = B -> ( ( N + A ) _C A ) = ( ( N + B ) _C B ) ) $=
      ( caddc co wceq cbc opreq2 opreq12 mpancom ) CADEZCBDEZFABFKAGELBGEFABCDH
      KLABGIJ $.
      $( [18-May-2007] $)

    $( Lemma for ~ bcxmas . $)
    bcxmaslem2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                          ( A + ( B + C ) ) = ( ( A + C ) + B ) ) $=
      ( cc wcel w3a caddc co addass add32 eqtr3d ) ADEBDECDEFABGHCGHABCGHGHACGH
      BGHABCIABCJK $.
      $( [18-May-2007] $)

    $( Parallel summation (Christmas Stocking) theorem for Pascal's Triangle.
       (Contributed by Paul Chapman, 18-May-2007.) $)
    bcxmas $p |- ( ( N e. NN0 /\ M e. NN0 ) ->
                     ( ( ( N + 1 ) + M ) _C M ) =
                     sum_ j e. ( 0 ... M ) ( ( N + j ) _C j ) ) $=
      ( vm vk cn0 wcel c1 caddc co cbc cc0 cfz cv csu wceq wi bcxmaslem1
      opreq2 sumeq1d eqeq12d imbi2d 0nn0 wa nn0addcl bcn0 syl mpan2 cz 1nn0
      syl6eqel 0z fsum1i sylancl peano2nn0 jctir 3syl 3eqtr4rd cuz cfv simpr
      elnn0uz sylib oprex fsump1i cc nn0cn adantr adantl ax1cn a1i bcxmaslem2
      syl111anc opreq1d opreq2d eqtrd opreq1 cmin pncan cn cle wbr nn0p1nn
      nnnn0addcl sylan cr nnre simpl nn0addge2 syl11anc breqtrd bcpasc2 eqtr3d
      nnnn0 nnz bccl nn0z addcom peano2cn addass 3eqtr3d 3eqtr2rd ex expcom
      a2d nn0ind impcom ) BFGCFGZCHIJZBIJBKJZLBMJZCANZIJZXLKJZAOZPZXHXIDNZIJXQK
      JZLXQMJZXNAOZPZQXHXILIJZLKJZLLMJZXNAOZPZQXHXIENZIJZYGKJZLYGMJZXNAOZPZQXHX
      IYGHIJZIJZYMKJZLYMMJZXNAOZPZQXHXPQDEBXQLPZYAYFXHYSXRYCXTYEXQLXIRYSXSYDXNA
      XQLLMSTUAUBXQYGPZYAYLXHYTXRYIXTYKXQYGXIRYTXSYJXNAXQYGLMSTUAUBXQYMPZYAYRXH
      UUAXRYOXTYQXQYMXIRUUAXSYPXNAXQYMLMSTUAUBXQBPZYAXPXHUUBXRXJXTXOXQBXIRUUBXS
      XKXNAXQBLMSTUAUBXHCLIJZLKJZHYEYCXHLFGZUUDHPZUCXHUUEUDUUCFGUUFCLUEUUCUFUGU
      HZXHUUDFGLUIGYEUUDPXHUUDHFUUGUJUKULXNUUDFALXLLCRUMUNXHXIFGZUUEUDYBFGYCHPX
      HUUHUUECUOZUCUPXILUEYBUFUQURYGFGZXHYLYRXHUUJYLYRQXHUUJUDZYLYRUUKYLUDYQYKY
      HYMKJZIJZYIUULIJZYOUUKYQUUMPYLUUKYQYKCYMIJZYMKJZIJZUUMUUKYGLUSUTGZYQUUQPU
      UKUUJUURXHUUJVAYGVBVCXNUUPALYGXMXLKVDUUOYMKVDXLYMCRVEUGUUKUUPUULYKIUUKUUO
      YHYMKUUKCVFGZYGVFGZHVFGZUUOYHPXHUUSUUJCVGZVHUUJUUTXHYGVGVIZUVAUUKVJVKZCYG
      HVLVMZVNVOVPVHYLUUNUUMPUUKYIYKUULIVQVIUUKUUNYOPYLUUKUULYIIJZYHHIJZYMKJZUU
      NYOUUKUULYHYMHVRJZKJZIJZUVFUVHUUKUVJYIUULIUUKUVIYGYHKUUKUUTUVAUVIYGPUVCVJ
      YGHVSUNVOVOUUKYHVTGZYMVTGZYMYHWAWBUVKUVHPXHXIVTGUUJUVLCWCXIYGWDWEZUUJUVMX
      HYGWCVIZUUKYMUUOYHWAUUKYMWFGZXHYMUUOWAWBUUKUVMUVPUVOYMWGUGXHUUJWHYMCWIWJU
      VEWKYMYHWLVMWMUUKUULVFGZYIVFGZUVFUUNPUUKUULFGZUVQUUKYHFGZYMUIGZUVSUUKUVLU
      VTUVNYHWNUGUUKUVMUWAUVOYMWOUGYMYHWPWJUULVGUGUUKYIFGZUVRXHUUHUUJUWBUUIUUHU
      UJUDUVTYGUIGZUWBXIYGUEUUJUWCUUHYGWQVIYGYHWPWJWEYIVGUGUULYIWRWJUUKUVGYNYMK
      UUKXIVFGZUUTUVAUVGYNPXHUWDUUJXHUUSUWDUVBCWSUGVHUVCUVDXIYGHWTVMVNXAVHXBXCX
      DXEXFXG $.
      $( [18-May-2007] $)
  $}


