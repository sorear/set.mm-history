$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Supremum on the extended reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Adding minus infinity to a set does not affect the existence of its
       supremum. $)
    xrsupexmnf $p |- ( E. x e. RR* ( A. y e. A -. x < y /\
              A. y e. RR* ( y < x -> E. z e. A y < z ) ) ->
         E. x e. RR* ( A. y e. ( A u. { -oo } ) -. x < y /\
              A. y e. RR* ( y < x -> E. z e. ( A u. { -oo } ) y < z ) ) ) $=
      ( cv clt wbr wn wral wrex wi cxr wa cmnf csn cun wcel wo pm3.27 wceq
      breq2 notbid nltmnf syl5cbir elsn syl5ib adantr jaod elun ex r19.20dv2
      elun1 anim1i r19.22i2 imim2i r19.20si a1i anim12d r19.22i ) AEZBEZFGZHZBD
      IZVAUTFGZVACEZFGZCDJZKZBLIZMVCBDNOZPZIZVEVGCVLJZKZBLIZMALUTLQZVDVMVJVPVQV
      CVCBDVLVQVADQZVCKZVAVLQZVCKVQVSMZVRVAVKQZRVCVTWAVRVCWBVQVSSVQWBVCKVSVQVAN
      TZVCWBWCVCUTNFGZHVQWCVBWDVANUTFUAUBUTUCUDBNUEUFUGUHVADVKUIUFUJUKVJVPKVQVI
      VOBLVHVNVEVGVGCDVLVFDQVFVLQVGVFDVKULUMUNUOUPUQURUS $.
      $( [9-Dec-2005] $) $( [26-Oct-2005] $)

    $( Adding plus infinity to a set does not affect the existence of its
       infimum. $)
    xrinfmexpnf $p |- ( E. x e. RR* ( A. y e. A -. y < x /\
              A. y e. RR* ( x < y -> E. z e. A z < y ) ) ->
         E. x e. RR* ( A. y e. ( A u. { +oo } ) -. y < x /\
              A. y e. RR* ( x < y -> E. z e. ( A u. { +oo } ) z < y ) ) ) $=
      ( cv clt wbr wn wral wrex wi cxr wa cpnf csn cun wcel wo pm3.27 wceq
      breq1 notbid pnfnlt syl5cbir elsn syl5ib adantr jaod elun ex r19.20dv2
      elun1 anim1i r19.22i2 imim2i r19.20si a1i anim12d r19.22i ) BEZAEZFGZHZBD
      IZVAUTFGZCEZUTFGZCDJZKZBLIZMVCBDNOZPZIZVEVGCVLJZKZBLIZMALVALQZVDVMVJVPVQV
      CVCBDVLVQUTDQZVCKZUTVLQZVCKVQVSMZVRUTVKQZRVCVTWAVRVCWBVQVSSVQWBVCKVSVQUTN
      TZVCWBWCVCNVAFGZHVQWCVBWDUTNVAFUAUBVAUCUDBNUEUFUGUHUTDVKUIUFUJUKVJVPKVQVI
      VOBLVHVNVEVGVGCDVLVFDQVFVLQVGVFDVKULUMUNUOUPUQURUS $.
      $( [30-Jan-2006] $) $( [19-Jan-2006] $)

    $( Lemma for ~ xrsupss . $)
    xrsupsslem $p |- ( ( A C_ RR* /\ ( A C_ RR \/ +oo e. A ) ) -> E. x e. RR*
        ( A. y e. A -. x < y /\ A. y e. RR* ( y < x -> E. z e. A y < z ) ) ) $=
      ( cxr wss cr cv clt wbr wn wral wrex wi wa cpnf wcel c0 wceq raleq1
      rexeq1 imbi2d ralbidv anbi12d rexbidv wne cle w3a sup3 rexr anim1i
      r19.22i2 syl cmnf w3o pm3.27 pnfnlt adantr wb breq1 notbid adantl mpbird
      pm2.21d ex ad2antlr wex ssel mnflt syl6 ancld 19.22dv n0 df-rex 3imtr4g
      imp a1d ad2antrr imbi12d 3jaod elxr syl5ib r19.20dv2 anim2d r19.22dva
      3adant3 mpd 3expa letric ord ssel2 sylan an1rs rexnal syl5ibr r19.20dva
      ralnex sylan2br breq2 cbvrexv ralbii sylib imbi1d rcla4ev pnfxr
      r19.21aiv c1 caddc co rcla4va adantrr ancoms peano2re sylan2 ltp1 ltletr
      ancli mpand adantll exp31 a1dd com4r xrltnr ax-mp mtbiri cc0 impcom 0re
      mpan syl5bir exp3a 3jaoi sylbi com13 jca sylancr syldan adantlr
      pm2.61dan mnfxr ral0 nltmnf rgen pm3.2i mp2an a1i pm2.61ne anim12i
      jaodan ) DEFZDGFZAHZBHZIJZKZBDLZUUMUULIJZUUMCHZIJZCDMZNZBELZOZAEMZPDQZUUK
      UVDUUJUUKUVDUUOBRLZUUQUUSCRMZNZBELZOZAEMZDRDRSZUVCUVJAEUVLUUPUVFUVBUVIUUO
      BDRTUVLUVAUVHBEUVLUUTUVGUUQUUSCDRUAUBUCUDUEUUKDRUFZOZUUMUULUGJZBDLZAGMZUV
      DUUKUVMUVQUVDUUKUVMUVQUHZUUPUVABGLZOZAEMZUVDUVRUVTAGMUWAABCDUIUVTUVTAGEUU
      LGQZUULEQZUVTUULUJUKULUMUUKUVMUWAUVDNUVQUVNUVTUVCAEUVNUWCOZUVSUVBUUPUWDUV
      AUVABGEUWDUUMGQZUVANZUUMEQZUVANUWDUWFOZUWEUUMPSZUUMUNSZUOZUVAUWGUWHUWEUVA
      UWIUWJUWDUWFUPUWCUWIUVANUVNUWFUWCUWIUVAUWCUWIOZUUQUUTUWLUUQKZPUULIJZKZUWC
      UWOUWIUULUQURUWIUWMUWOUSUWCUWIUUQUWNUUMPUULIUTVAVBVCVDVEVFUWDUWJUVANUWFUW
      DUWJUVAUWDUWJOUVAUNUULIJZUNUURIJZCDMZNZUVNUWSUWCUWJUVNUWRUWPUUKUVMUWRUUKU
      URDQZCVGUWTUWQOZCVGUVMUWRUUKUWTUXACUUKUWTUWQUUKUWTUURGQZUWQDGUURVHUURVIZV
      JVKVLCDVMUWQCDVNVOVPVQVRUWJUVAUWSUSUWDUWJUUQUWPUUTUWRUUMUNUULIUTUWJUUSUWQ
      CDUUMUNUURIUTUEZVSVBVCVEURVTUUMWAZWBVEWCWDWEWFWGWHUUKUVQKZUVDUVMUUKUXFUUL
      UURUGJZCDMZAGLZUVDUUKUXFOUULUUMUGJZBDMZAGLZUXIUUKUVPKZAGLZUXLUXFUUKUXNUXL
      UUKUXMUXKAGUUKUWBOZUVOKZBDMUXKUXMUXOUXPUXJBDUUKUUMDQZUWBUXPUXJNZUWEUWBUXR
      UUKUXQOUWEUWBOUVOUXJUUMUULWIWJDGUUMWKWLWMWEUVOBDWNWOWPVPUVPAGWQWRUXKUXHAG
      UXJUXGBCDUUMUURUULUGWSWTXAXBPEQZPUUMIJZKZBDLZUUMPIJZUUTNZBELZOZUVDUUKUXIO
      ZUVCUYFAPEUULPSZUUPUYBUVBUYEUYHUUOUYABDUYHUUNUXTUULPUUMIUTVAUCUYHUVAUYDBE
      UYHUUQUYCUUTUULPUUMIWSXCUCUDXDZXEUYGUYBUYEUUKUYBUXIUUKUYABDUUKUXQUWEUYADG
      UUMVHUWEUWGUYAUUMUJUUMUQZUMVJXFURUYGUYDBEUUKUXIUWGUYDNUWGUXIUUKUYDUWGUWKU
      XIUUKUYDNZNZUXEUWEUYLUWIUWJUXIUUKUYCUWEUUTUXIUUKUWEUUTNUYCUXIUUKUWEUUTUXI
      UUKOZUWEOUUMXGXHXIZUURUGJZCDMZUUTUYMUYNGQZUYPUWEUYQUYMUYPUYQUXIUYPUUKUXHU
      YPAUYNGUULUYNSUXGUYOCDUULUYNUURUGUTUEXJXKXLUUMXMZXNUUKUWEUYPUUTNUXIUUKUWE
      OUYOUUSCDUUKUWTUWEUYOUUSNZUXBUWEUYSUUKUWTOZUWEUXBUYSUWEUXBOUUMUYNIJZUYOUU
      SUWEVUAUXBUUMXOURUWEUYQOUXBVUAUYOOUUSNZUWEUWEUYQUXBVUBUUMUYNUURXPWHUWEUYQ
      UYRXQWLXRXLDGUURWKZWLWMWEXSWGXTYAYBUWIUYKUXIUWIUYDUUKUWIUYCUUTUWIUYCPPIJZ
      UXSVUDKXEPYCYDUUMPPIUTYEVDVQVQUWJUXIUUKUYDUWJUYMUUTUYCUWJUUTUWRUYMUXDYFUU
      RUGJZCDMZUUKUWRUXIUUKVUFUWRUUKVUEUWQCDUYTUWQVUEUYTUXBUWQVUCUXCUMVQWEYGYFG
      QUXIVUFYHUXHVUFAYFGUULYFSUXGVUECDUULYFUURUGUTUEXJYIWLYJYAYKYLYMYNVPXFYOYP
      YQYRYSUVKUUKUNEQUNUUMIJZKZBRLZUUMUNIJZUVGNZBELZOZUVKYTVUIVULVUHBUUAVUKBEU
      WGVUJUVGUUMUUBVDUUCUUDUVJVUMAUNEUULUNSZUVFVUIUVIVULVUNUUOVUHBRVUNUUNVUGUU
      LUNUUMIUTVAUCVUNUVHVUKBEVUNUUQVUJUVGUULUNUUMIWSXCUCUDXDUUEUUFUUGVBUXSUYFU
      VDUUJUVEOUYIXEUUJUYBUVEUYEUUJUYABDUUJUXQUWGUYADEUUMVHUYJVJXFUVEUYDBEUVEUY
      DUWGUVEUYCUUTUUSUYCCPDUURPUUMIWSXDVEVQXFUUHYPUUI $.
      $( [19-Jan-2006] $) $( [25-Oct-2005] $)

    $( Lemma for ~ xrinfmss . $)
    xrinfmsslem $p |- ( ( A C_ RR* /\ ( A C_ RR \/ -oo e. A ) ) -> E. x e. RR*
        ( A. y e. A -. y < x /\ A. y e. RR* ( x < y -> E. z e. A z < y ) ) ) $=
      ( cxr wss cr cv clt wbr wn wral wrex wi wa cmnf wcel c0 wceq raleq1
      rexeq1 imbi2d ralbidv anbi12d rexbidv wne cle w3a infm3 rexr anim1i
      r19.22i2 syl cpnf w3o pm3.27 wex ssel ltpnf syl6 ancld 19.22dv n0 df-rex
      3imtr4g imp a1d ad2antrr wb breq2 imbi12d adantl mpbird ex adantr nltmnf
      notbid pm2.21d ad2antlr 3jaod elxr syl5ib r19.20dv2 anim2d r19.22dva
      3adant3 mpd 3expa wo letric ancoms ord ssel2 sylan an1rs rexnal syl5ibr
      r19.20dva ralnex sylan2br breq1 cbvrexv ralbii sylib imbi1d rcla4ev
      mnfxr r19.21aiv c1 cmin co rcla4va adantrr peano2rem sylan2 ltm1 lelttr
      3expb ancri mpan2d adantll exp31 a1dd com4r cc0 impcom 0re mpan syl5bir
      exp3a xrltnr ax-mp mtbiri 3jaoi sylbi com13 jca sylancr syldan adantlr
      pm2.61dan pnfxr ral0 pnfnlt rgen pm3.2i mp2an a1i pm2.61ne anim12i
      jaodan ) DEFZDGFZBHZAHZIJZKZBDLZUUOUUNIJZCHZUUNIJZCDMZNZBELZOZAEMZPDQZUUM
      UVFUULUUMUVFUUQBRLZUUSUVACRMZNZBELZOZAEMZDRDRSZUVEUVLAEUVNUURUVHUVDUVKUUQ
      BDRTUVNUVCUVJBEUVNUVBUVIUUSUVACDRUAUBUCUDUEUUMDRUFZOZUUOUUNUGJZBDLZAGMZUV
      FUUMUVOUVSUVFUUMUVOUVSUHZUURUVCBGLZOZAEMZUVFUVTUWBAGMUWCABCDUIUWBUWBAGEUU
      OGQZUUOEQZUWBUUOUJUKULUMUUMUVOUWCUVFNUVSUVPUWBUVEAEUVPUWEOZUWAUVDUURUWFUV
      CUVCBGEUWFUUNGQZUVCNZUUNEQZUVCNUWFUWHOZUWGUUNUNSZUUNPSZUOZUVCUWIUWJUWGUVC
      UWKUWLUWFUWHUPUWFUWKUVCNUWHUWFUWKUVCUWFUWKOUVCUUOUNIJZUUTUNIJZCDMZNZUVPUW
      QUWEUWKUVPUWPUWNUUMUVOUWPUUMUUTDQZCUQUWRUWOOZCUQUVOUWPUUMUWRUWSCUUMUWRUWO
      UUMUWRUUTGQZUWODGUUTURUUTUSZUTVAVBCDVCUWOCDVDVEVFVGVHUWKUVCUWQVIUWFUWKUUS
      UWNUVBUWPUUNUNUUOIVJUWKUVAUWOCDUUNUNUUTIVJUEZVKVLVMVNVOUWEUWLUVCNUVPUWHUW
      EUWLUVCUWEUWLOZUUSUVBUXCUUSKZUUOPIJZKZUWEUXFUWLUUOVPVOUWLUXDUXFVIUWEUWLUU
      SUXEUUNPUUOIVJVQVLVMVRVNVSVTUUNWAZWBVNWCWDWEWFWGWHUUMUVSKZUVFUVOUUMUXHUUT
      UUOUGJZCDMZAGLZUVFUUMUXHOUUNUUOUGJZBDMZAGLZUXKUUMUVRKZAGLZUXNUXHUUMUXPUXN
      UUMUXOUXMAGUUMUWDOZUVQKZBDMUXMUXOUXQUXRUXLBDUUMUUNDQZUWDUXRUXLNZUWGUWDUXT
      UUMUXSOUWGUWDOUVQUXLUWDUWGUVQUXLWIUUOUUNWJWKWLDGUUNWMWNWOWEUVQBDWPWQWRVFU
      VRAGWSWTUXMUXJAGUXLUXIBCDUUNUUTUUOUGXAXBXCXDPEQZUUNPIJZKZBDLZPUUNIJZUVBNZ
      BELZOZUVFUUMUXKOZUVEUYHAPEUUOPSZUURUYDUVDUYGUYJUUQUYCBDUYJUUPUYBUUOPUUNIV
      JVQUCUYJUVCUYFBEUYJUUSUYEUVBUUOPUUNIXAXEUCUDXFZXGUYIUYDUYGUUMUYDUXKUUMUYC
      BDUUMUXSUWGUYCDGUUNURUWGUWIUYCUUNUJUUNVPZUMUTXHVOUYIUYFBEUUMUXKUWIUYFNUWI
      UXKUUMUYFUWIUWMUXKUUMUYFNZNZUXGUWGUYNUWKUWLUXKUUMUYEUWGUVBUXKUUMUWGUVBNUY
      EUXKUUMUWGUVBUXKUUMOZUWGOUUTUUNXIXJXKZUGJZCDMZUVBUYOUYPGQZUYRUWGUYSUYOUYR
      UYSUXKUYRUUMUXJUYRAUYPGUUOUYPSUXIUYQCDUUOUYPUUTUGVJUEXLXMWKUUNXNZXOUUMUWG
      UYRUVBNUXKUUMUWGOUYQUVACDUUMUWRUWGUYQUVANZUWTUWGVUAUUMUWROZUWTUWGOUYQUYPU
      UNIJZUVAUWGVUCUWTUUNXPVLUWTUYSUWGOUYQVUCOUVANZUWGUWTUYSUWGVUDUUTUYPUUNXQX
      RUWGUYSUYTXSXOXTDGUUTWMZWNWOWEYAWGYBYCYDUWKUXKUUMUYFUWKUYOUVBUYEUWKUVBUWP
      UYOUXBUUTYEUGJZCDMZUUMUWPUXKUUMVUGUWPUUMVUFUWOCDVUBUWOVUFVUBUWTUWOVUEUXAU
      MVGWEYFYEGQUXKVUGYGUXJVUGAYEGUUOYESUXIVUFCDUUOYEUUTUGVJUEXLYHWNYIYCYJUWLU
      YMUXKUWLUYFUUMUWLUYEUVBUWLUYEPPIJZUYAVUHKXGPYKYLUUNPPIVJYMVRVGVGYNYOYPVFX
      HYQYRYSYTUUAUVMUUMUNEQUUNUNIJZKZBRLZUNUUNIJZUVINZBELZOZUVMUUBVUKVUNVUJBUU
      CVUMBEUWIVULUVIUUNUUDVRUUEUUFUVLVUOAUNEUUOUNSZUVHVUKUVKVUNVUPUUQVUJBRVUPU
      UPVUIUUOUNUUNIVJVQUCVUPUVJVUMBEVUPUUSVULUVIUUOUNUUNIXAXEUCUDXFUUGUUHUUIVL
      UYAUYHUVFUULUVGOUYKXGUULUYDUVGUYGUULUYCBDUULUXSUWIUYCDEUUNURUYLUTXHUVGUYF
      BEUVGUYFUWIUVGUYEUVBUVAUYECPDUUTPUUNIXAXFVNVGXHUUJYRUUK $.
      $( [19-May-2006] $) $( [19-Jan-2006] $)

    $( Any subset of extended reals has a supremum. $)
    xrsupss $p |- ( A C_ RR* -> E. x e. RR* (
         A. y e. A -. x < y /\ A. y e. RR* ( y < x -> E. z e. A y < z ) ) ) $=
      ( cxr wss cr cpnf wcel wo cmnf cv clt wbr wn wral wrex wi wa w3o ssxr
      df-3or sylib xrsupsslem csn cdif ssdifss orcom wb mnfxr elisseti snid
      elndif ax-mp biorf 3bitr4i mpdan syl adantr cun wceq snss undif uncom
      eqeq1i 3bitri raleq1 rexeq1 imbi2d ralbidv anbi12d sylbi rexbidv
      xrsupexmnf syl5bi adantl mpd jaodan ) DEFZDGFZHDIZJZKDIZJZALZBLZMNOZBDPZW
      FWEMNZWFCLMNZCDQZRZBEPZSZAEQZVSVTWAWCTWDDUAVTWAWCUBUCVSWBWOWCABCDUDVSWCSW
      GBDKUEZUFZPWIWJCWQQRBEPSAEQZWOVSWRWCVSWQEFZWRDEWPUGWSWQGFZHWQIZJZWRWSWTXA
      KWQIZTZXBWQUAXBXCJXCXBJZXDXBXBXCUHWTXAXCUBXCOZXBXEUIKWPIXFKKEUJUKZULKWPDU
      MUNXCXBUOUNUPUCABCWQUDUQURUSWCWRWORVSWCWGBWQWPUTZPZWIWJCXHQZRZBEPZSZAEQWO
      WRWCXMWNAEWCXHDVAZXMWNUIWCWPDFWPWQUTZDVAXNKDXGVBWPDVCXOXHDWPWQVDVEVFXNXIW
      HXLWMWGBXHDVGXNXKWLBEXNXJWKWIWJCXHDVHVIVJVKVLVMABCWQVNVOVPVQVRUQ $.
      $( [20-Jan-2006] $) $( [25-Oct-2005] $)

    $( Any subset of extended reals has an infimum. $)
    xrinfmss $p |- ( A C_ RR* -> E. x e. RR* (
         A. y e. A -. y < x /\ A. y e. RR* ( x < y -> E. z e. A z < y ) ) ) $=
      ( cxr wss cr cmnf wcel wo cpnf cv clt wbr wn wral wrex wi wa w3o ssxr
      df-3or or23 bitri sylib xrinfmsslem csn cdif ssdifss 3orass wb pnfxr
      elisseti snid elndif ax-mp biorf orbi2i bitr4i mpdan syl adantr cun wceq
      snss undif uncom eqeq1i 3bitri raleq1 rexeq1 imbi2d ralbidv anbi12d
      sylbi rexbidv xrinfmexpnf syl5bi adantl mpd jaodan ) DEFZDGFZHDIZJZKDIZJZ
      BLZALZMNOZBDPZWIWHMNZCLWHMNZCDQZRZBEPZSZAEQZWBWCWFWDTZWGDUAWSWCWFJWDJWGWC
      WFWDUBWCWFWDUCUDUEWBWEWRWFABCDUFWBWFSWJBDKUGZUHZPWLWMCXAQRBEPSAEQZWRWBXBW
      FWBXAEFZXBDEWTUIXCXAGFZHXAIZJZXBXCXDKXAIZXETZXFXAUAXHXDXGXEJZJXFXDXGXEUJX
      EXIXDXGOZXEXIUKKWTIXJKKEULUMZUNKWTDUOUPXGXEUQUPURUSUEABCXAUFUTVAVBWFXBWRR
      WBWFWJBXAWTVCZPZWLWMCXLQZRZBEPZSZAEQWRXBWFXQWQAEWFXLDVDZXQWQUKWFWTDFWTXAV
      CZDVDXRKDXKVEWTDVFXSXLDWTXAVGVHVIXRXMWKXPWPWJBXLDVJXRXOWOBEXRXNWNWLWMCXLD
      VKVLVMVNVOVPABCXAVQVRVSVTWAUT $.
      $( [19-May-2006] $) $( [25-Oct-2005] $)

    $( By quantifying only over reals, we can specify any extended real upper
       bound for any set of extended reals. $)
    xrub $p |- ( ( A C_ RR* /\ B e. RR* ) -> ( A. x e. RR ( x < B ->
          E. y e. A x < y ) <-> A. x e. RR* ( x < B -> E. y e. A x < y ) ) ) $=
      ( vz cxr wss wcel wa cv clt wbr wrex wi cr wral cpnf wceq cmnf w3o
      pm2.27 a1i wn breq1 notbid pnfnlt syl5bir pm2.21 syl6com ad2antlr a1dd
      rexbidv imbi12d c1 cmin co peano2rem rcla4v syl adantl id ltm1 syl5com
      mnflt mnfxr xrlttr mp3an1 rexr ssel2 adantlr sylanc mpand r19.22dva
      3syld 1re ltpnf ax-mp breq2 mpbiri mp3an12 mpani sylan9r syl5 xrltnr
      mtbiri pm2.21d a1d 3jaodan elxr sylan2b imp syl5cbir 3jaod syl5ib com23
      r19.20dv2 ex cbvralv pm2.43d imim1i r19.20i2 impbid1 ) CFGZDFHZIZAJZDKLZX
      FBJZKLZBCMZNZAOPZXKAFPZXEXLXMXEEJZDKLZXNXHKLZBCMZNZEOPZXLXMNZXLXEXSXTXEXS
      IZXKXKAOFYAXFFHZXFOHZXKNZXKYAYCXFQRZXFSRZTYDXKNZYBYAYCYGYEYFYCYGNYAYCXKUA
      UBYAYEXKYDXDYEXKNXCXSYEXDXGUCZXKYEYHQDKLZUCXDYEXGYIXFQDKUDUEDUFUGXGXJUHUI
      UJUKYAYFXKYDYFXKSDKLZSXHKLZBCMZNZYAYFXGYJXJYLXFSDKUDYFXIYKBCXFSXHKUDULUMX
      EXSYMXCDOHZDQRZDSRZTXSYMNZXDXCYNYQYOYPXCYNIZXSYLYJYRXSDUNUOUPZDKLZYSXHKLZ
      BCMZNZUUBYLYNXSUUCNZXCYNYSOHZUUDDUQZXRUUCEYSOXNYSRZXOYTXQUUBXNYSDKUDUUGXP
      UUABCXNYSXHKUDULUMURUSUTYNUUCUUBNXCUUCYTUUBYNUUCVADVBVCUTYRUUAYKBCYRXHCHZ
      IZSYSKLZUUAYKUUIUUEUUJYNUUEXCUUHUUFUJZYSVDUSYSFHZXHFHZUUJUUAIYKNZUUISFHZU
      ULUUMUUNVESYSXHVFVGUUIUUEUULUUKYSVHUSXCUUHUUMYNCFXHVIZVJVKVLVMVNUKXCYOIZX
      SYLYJUUQUNDKLZUNXHKLZBCMZNZYLXSYOUVAUUTXCYLUVAUURUUTYOUVAVAYOUURUNQKLZUNO
      HZUVBVOUNVPVQDQUNKVRVSVCXCUUSYKBCXCUUHIUUMUUSYKNUUPUUMSUNKLZUUSYKUVCUVDVO
      UNVDVQUUOUNFHZUUMUVDUUSIYKNVEUVCUVEVOUNVHVQSUNXHVFVTWAUSVMWBUVCXSUVANVOXR
      UVAEUNOXNUNRZXOUURXQUUTXNUNDKUDUVFXPUUSBCXNUNXHKUDULUMURVQWCUKXCYPIZYMXSU
      VGYJYLYPYJUCXCYPYJSSKLZUUOUVHUCVESWDVQDSSKVRWEUTWFWGWHDWIWJWKWLUKWMXFWIWN
      WOWPWQXKXRAEOXFXNRZXGXOXJXQXFXNDKUDUVIXIXPBCXFXNXHKUDULUMWRWNWSXKXKAFOYCY
      BXKXFVHWTXAXB $.
      $( [10-Apr-2006] $) $( [9-Apr-2006] $)

    $( The supremum of a set of extended reals. $)
    supxr $p |- ( ( ( A C_ RR* /\ B e. RR* ) /\ ( A. x e. A -. B < x /\
    A. x e. RR ( x < B -> E. y e. A x < y ) ) ) -> sup ( A , RR* , < ) = B ) $=
      ( vz cxr wss wcel wa cv clt wbr wn wral wrex wi cr csup wceq xrub biimpd
      anim2d imp crab cuni wreu wb xrsupss anim2i ancoms xrltso supeu breq1
      notbid ralbidv breq2 imbi1d anbi12d reuuni2 3syl biimpa df-sup syl5eq
      syldan ) CFGZDFHZIZDAJZKLZMZACNZVHDKLZVHBJKLBCOZPZAQNZIZVKVNAFNZIZCFKRZDS
      VGVPVRVGVOVQVKVGVOVQABCDTUAUBUCVGVRIEJZVHKLZMZACNZVHVTKLZVMPZAFNZIZEFUDUE
      ZDVSVGVRWHDSZVGVFWGEFOZIZVFWGEFUFZIVRWIUGVFVEWKVEWJVFEABCUHUIUJWJWLVFEABF
      CKUKULUIWGVREFDVTDSZWCVKWFVQWMWBVJACWMWAVIVTDVHKUMUNUOWMWEVNAFWMWDVLVMVTD
      VHKUPUQUOURUSUTVAEABFCKVBVCVD $.
      $( [14-Apr-2006] $) $( [9-Apr-2006] $)

    $( The supremum of a set of extended reals. $)
    supxr2 $p |- ( ( ( A C_ RR* /\ B e. RR* ) /\ ( A. x e. A x <_ B /\
  A. x e. RR ( x < B -> E. y e. A x < y ) ) ) -> sup ( A , RR* , < ) = B ) $=
      ( cxr wss wcel wa cv cle wbr wral clt wrex wi cr wn csup wceq wb xrlenlt
      ssel2 sylan an1rs ralbidva anbi1d biimpa supxr syldan ) CEFZDEGZHZAIZDJKZ
      ACLZUMDMKUMBIMKBCNOAPLZHZDUMMKQZACLZUPHZCEMRDSULUQUTULUOUSUPULUNURACUJUMC
      GZUKUNURTZUMEGUKVBUJVAHUMDUACEUMUBUCUDUEUFUGABCDUHUI $.
      $( [20-Apr-2006] $) $( [9-Apr-2006] $)

    $( The real and extended real suprema match when the real supremum
       exists. $)
    supxrre $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
                  sup ( A , RR* , < ) = sup ( A , RR , < ) ) $=
      ( vz cr wss c0 wne cv cle wbr wral wrex w3a clt wn wi cxr wa crab cuni
      csup wceq wcel rexr imim1i cpnf cmnf w3o pm3.27 pnfnlt adantr wb breq1
      notbid adantl mpbird pm2.21d ex ad2antlr wex ssel mnflt syl6 ancld
      19.22dv n0 df-rex 3imtr4g imp a1d ad2antrr rexbidv imbi12d 3jaod elxr
      syl5ib impbid2 ralbidv2 anbi2d rabbidv unieqd 3adant3 wreu ressxr
      reuuniss mp3an1 sup3 ltso supeu syl weu ax-1 a1i 3simpl3 breq2 anbi1d
      ltpnf biantrurd lenlt ssel2 sylan an1rs ralbidva ralnex syl6rbb bitr3d
      sylan9bbr rexbidva 3ad2antl1 rexanali sylib adantld biimprd sylan9
      dfrex2 bitr3i adantrd imp3a jcad anim1i impbid1 eubidv df-reu 3bitr4g
      cbvralv rexbii ralbidv cbvrexv bitri syl3an3b sylanc eqtr4d df-sup
      3eqtr4g ) CEFZCGHZBIZAIZJKZBCLZAEMZNZYSYROKZPBCLZYRYSOKZYRDIZOKZDCMZQZBRL
      ZSZARTZUAZUUEUUJBELZSZAETUAZCROUBCEOUBUUCUUNUUPARTZUAZUUQYPYQUUNUUSUCUUBY
      PYQSZUUMUURUUTUULUUPARUUTYSRUDZSZUUKUUOUUEUVBUUJUUJBREUVBYRRUDZUUJQZYREUD
      ZUUJQZUVEUVCUUJYRUEUFUVBUVFUVDUVBUVFSZUVEYRUGUCZYRUHUCZUIUUJUVCUVGUVEUUJU
      VHUVIUVBUVFUJUVAUVHUUJQUUTUVFUVAUVHUUJUVAUVHSZUUFUUIUVJUUFPZUGYSOKZPZUVAU
      VMUVHYSUKULUVHUVKUVMUMUVAUVHUUFUVLYRUGYSOUNUOUPUQURUSUTUVBUVIUUJQUVFUVBUV
      IUUJUVBUVISUUJUHYSOKZUHUUGOKZDCMZQZUUTUVQUVAUVIUUTUVPUVNYPYQUVPYPUUGCUDZD
      VAUVRUVOSZDVAYQUVPYPUVRUVSDYPUVRUVOYPUVRUUGEUDZUVOCEUUGVBUUGVCVDVEVFDCVGU
      VODCVHVIVJVKVLUVIUUJUVQUMUVBUVIUUFUVNUUIUVPYRUHYSOUNUVIUUHUVODCYRUHUUGOUN
      VMVNUPUQUSULVOYRVPVQUSVRVSVTWAWBWCUUPAEMZUUPARWDZUUQUUSUCZUUCERFUWAUWBUWC
      WEUUPAERWFWGABDCWHZUUCUWBUUPAEWDZUUCUWAUWEUWDABDECOWIWJWKYPYQUUGYRJKZDCLZ
      BEMZUWBUWEUMUUBYPYQUWHNZUVAUUPSZAWLYSEUDZUUPSZAWLUWBUWEUWIUWJUWLAUWIUWJUW
      LUWIUWJUWKUUPUWIUVAUUPUWKUWIUWKYSUGUCZYSUHUCZUIUUPUWKQZUVAUWIUWKUWOUWMUWN
      UWKUWOQUWIUWKUUPWMWNUWIUWMUWOUWIUWMSZUUOUWKUUEUWPUUOUWKUWPUUFUUIPZSZBEMZU
      UOPUWPUWSUWHYPYQUWHUWMWOYPYQUWMUWSUWHUMUWHYPUWMSUWRUWGBEYPUVEUWMUWRUWGUMU
      WMUWRYRUGOKZUWQSZYPUVESZUWGUWMUUFUWTUWQYSUGYROWPWQUXBUWQUXAUWGUXBUWTUWQUV
      EUWTYPYRWRUPWSUXBUWGUUHPZDCLUWQUXBUWFUXCDCYPUVRUVEUWFUXCUMZUVTUVEUXDYPUVR
      SUUGYRWTCEUUGXAXBXCXDUUHDCXEXFXGXHXCXIXJUQUUFUUIBEXKXLURXMUSYPYQUWNUWOQUW
      HUUTUWNUWOUUTUWNSZUUEUWKUUOUXEUUEUWKUXEYRCUDZUUDSZBVAZUUEPZYPUWNYQUXHYPUW
      NSZYQUXHUXJUXFBVAUXHYQUXJUXFUXGBUXJUXFUUDYPUXFUHYROKZUWNUUDYPUXFUVEUXKCEY
      RVBYRVCVDUWNUUDUXKYSUHYROUNXNXOVEVFBCVGVQVJXCUXHUUDBCMUXIUUDBCVHUUDBCXPXQ
      XLURXRUSWCVOYSVPVQXSUWJUUPQUWIUVAUUPUJWNXTUWKUVAUUPYSUEYAYBYCUUPARYDUUPAE
      YDYEUUBUUGYSJKZDCLZAEMUWHUUAUXMAEYTUXLBDCYRUUGYSJUNYFYGUXMUWGABEYSYRUCUXL
      UWFDCYSYRUUGJWPYHYIYJYKUQYLYMABDRCOYNABDECOYNYO $.
      $( [13-May-2007] $) $( [18-Oct-2005] $)

    $( The supremum of an arbitrary set of extended reals is an extended
       real. $)
    supxrcl $p |- ( A C_ RR* -> sup ( A , RR* , < ) e. RR* ) $=
      ( vx vy vz cxr wss cv clt wbr wn wral wrex wi wa csup wcel xrsupss
      xrltso supcl syl ) AEFBGZCGZHIJCAKUBUAHIUBDGHIDALMCEKNBELAEHOEPBCDAQBCDEA
      HRST $.
      $( [20-Jan-2006] $) $( [24-Oct-2005] $)

    $( The supremum of the union of two sets of extended reals equals the
       largest of their suprema. $)
    supxrun $p |- ( ( A C_ RR* /\ B C_ RR* /\ sup ( A , RR* , < ) <_ sup
   ( B , RR* , < ) ) -> sup ( ( A u. B ) , RR* , < ) = sup ( B , RR* , < ) ) $=
      ( vx vy vz vw cun cxr wss clt csup wcel cv wbr wn wral wrex wi cr wceq
      cle w3a supxr wa unss biimpi 3adant3 supxrcl 3ad2ant2 wo xrsupss xrltso
      supub syl 3ad2ant1 xrlelttr ad2antrr ad2antlr ssel2 adantlr syl3anc
      expdimp con3d exp41 com34 3imp mpdd jaod elun syl5ib r19.21aiv suplub
      rexr sylani elun2 anim1i r19.22i2 syl6 exp3a syl2anc ) ABGZHIZBHJKZHLZWCC
      MZJNZOZCWAPWEWCJNZWEDMZJNZDWAQZRZCSPZWAHJKWCTAHIZBHIZAHJKZWCUANZUBZCDWAWC
      UCWNWOWBWQWNWOUDZWBABHUEUFUGWOWNWDWQBUHZUIWRWGCWAWRWEALZWEBLZUJWGWEWALWRX
      AWGXBWRXAWPWEJNZOZWGWNWOXAXDRZWQWNWIEMZJNOZEAPXFWIJNZXFFMJNZFAQREHPUDDHQX
      EDEFAUKDEFHAWEJULUMUNUOWNWOWQXAXDWGRZRWNWOXAWQXJWNWOXAWQXJWSXAUDZWQUDWFXC
      XKWQWFXCWPHLZWDWEHLZWQWFUDXCRXKWPWCWEUPWNXLWOXAAUHUQWOWDWNXAWTURWNXAXMWOA
      HWEUSUTVAVBVCVDVEVFVGWOWNXBWGRZWQWOXGEBPXHXIFBQREHPUDDHQXNDEFBUKDEFHBWEJU
      LUMUNUIVHWEABVIVJVKWOWNWMWQWOWLCSWOWESLZWHWKWOXOWHUDZWJDBQZWKWOWEXFJNOEBP
      XFWEJNXHDBQREHPUDCHQZXPXQRCEDBUKXRXMWHXQXOCEDHBWEJULVLWEVMVNUNWJWJDBWAWIB
      LWIWALWJWIBAVOVPVQVRVSVKUIVT $.
      $( [23-May-2006] $) $( [19-Jan-2006] $)

    $( The infimum of an arbitrary set of extended reals is an extended
       real. $)
    infmxrcl $p |- ( A C_ RR* -> sup ( A , RR* , `' < ) e. RR* ) $=
      ( vx vy vz cxr wss cv clt ccnv wbr wn wral wrex wi wa csup wcel xrinfmss
      visset brcnv notbii ralbii rexbii imbi12i anbi12i sylibr wor xrltso
      cnvso mpbi supcl syl ) AEFZBGZCGZHIZJZKZCALZUOUNUPJZUODGZUPJZDAMZNZCELZOZ
      BEMZAEUPPEQUMUOUNHJZKZCALZUNUOHJZVAUOHJZDAMZNZCELZOZBEMVGBCDARVFVPBEUSVJV
      EVOURVICAUQVHUNUOHBSZCSZTUAUBVDVNCEUTVKVCVMUOUNHVRVQTVBVLDAUOVAHVRDSTUCUD
      UBUEUCUFBCDEAUPEHUGEUPUGUHEHUIUJUKUL $.
      $( [26-May-2006] $) $( [19-Jan-2006] $)

    $( Adding minus infinity to a set does not affect its supremum. $)
    supxrmnf $p |- ( A C_ RR* ->
                 sup ( ( A u. { -oo } ) , RR* , < ) = sup ( A , RR* , < ) ) $=
      ( cxr wss cmnf csn cun clt csup cle wbr wceq supxrun wcel mnfxr snssi
      ax-mp a1i id supxrcl mnfle syl xrltso supsn syl5eqbr syl3anc uncom
      supeq1 syl5eq ) ABCZDEZAFZBGHZABGHZAUJFZBGHZUJBCZUIUJBGHZUMIJULUMKUIUJALU
      PUIDBMZUPNDBOPQUIRUIDUMUQIUIUMBMDUMIJASUMTUAURUQDKNBDGUBUCPUDUEUNUKKUOULK
      AUJUFBUNUKGUGPUH $.
      $( [10-Jul-2006] $) $( [19-Jan-2006] $)

    $( The supremum of a set of extended reals containing plus infnity is plus
       infinity. $)
    supxrpnf $p |- ( ( A C_ RR* /\ +oo e. A ) -> sup ( A , RR* , < ) = +oo ) $=
      ( vy vz cxr wss cpnf wcel cv clt wbr wn wral wrex wi cr wa csup wceq
      ssel pnfnlt syl6 r19.21aiv breq2 rcla4ev ex a1d anim12i pnfxr supxr
      mpanl2 syldan ) ADEZFAGZFBHZIJKZBALZUNFIJZUNCHZIJZCAMZNZBOLZPZADIQFRZULUP
      UMVBULUOBAULUNAGUNDGUOADUNSUNTUAUBUMVABOUMVAUNOGUMUQUTUSUQCFAURFUNIUCUDUE
      UFUBUGULFDGVCVDUHBCAFUIUJUK $.
      $( [14-Dec-2006] $) $( [15-Oct-2005] $)

    $( The supremum of an unbounded-above set of extended reals is plus
       infinity. $)
    supxrunb1 $p |- ( A C_ RR* -> ( A. x e. RR E. y e. A x <_ y <->
                    sup ( A , RR* , < ) = +oo ) ) $=
      ( vz vw cxr wss cv cle wbr wrex cr wral clt csup cpnf wceq wn wi wa wcel
      ssel pnfnlt syl6 r19.21aiv adantr c1 caddc co breq1 rexbidv rcla4va
      adantrr ancoms peano2re sylan2 ltp1 xrltletr rexr syl3an2 syl3an1 3expa
      ancli sylan mpand ssel2 an1rs r19.22dva adantll mpd exp31 a1dd com4r
      com13 imp jca pnfxr supxr mpanl2 syldan ex xrltso suplub xrsupss
      ad2antrr ad2antlr breq2 ltpnf syl5bir impcom sylc xrltle adantlr sylanc
      syld r19.21adva impbid ) CFGZAHZBHZIJZBCKZALMZCFNOZPQZWRXCXEWRXCPDHZNJRZD
      CMZXFPNJZXFWTNJZBCKZSZDLMZTZXEWRXCTZXHXMWRXHXCWRXGDCWRXFCUAXFFUAZXGCFXFUB
      XFUCUDUEUFXOXLDLWRXCXFLUAZXLSXQXCWRXLXCWRXIXQXKXCWRXQXKSXIXCWRXQXKXCWRTZX
      QTXFUGUHUIZWTIJZBCKZXKXRXSLUAZYAXQYBXRYAYBXCYAWRXBYAAXSLWSXSQXAXTBCWSXSWT
      IUJUKULUMUNXFUOZUPWRXQYAXKSXCWRXQTXTXJBCWRWTCUAZXQXTXJSZWTFUAZXQYEWRYDTXQ
      YFYEXQYFTXFXSNJZXTXJXQYGYFXFUQUFXQYBTYFYGXTTXJSZXQXQYBYFYHXPYBYFYHXQXPXSF
      UAYFYHYBXFXSWTURXSUSUTXFUSVAVBXQYBYCVCVDVEUNCFWTVFZVDVGVHVIVJVKVLVMVNVOUE
      VPWRPFUAXNXEVQDBCPVRVSVTWAWRXEXBALWRWSLUAZTZXEWSWTNJZBCKZXBYKXEYMXFEHZNJR
      ECMYNXFNJYNWTNJBCKSEFMTDFKZWSFUAZWSXDNJZTYMYKXETZDEBFCWSNWBWCWRYOYJXEDEBC
      WDWEYRYPYQYJYPWRXEWSUSZWFYJXEYQWRXEYJYQXEYQWSPNJYJXDPWSNWGWSWHWIWJVIVPWKW
      AYKYLXABCYPYFYLXASYKYDTWSWTWLYJYPWRYDYSWFWRYDYFYJYIWMWNVHWOWPWQ $.
      $( [23-Aug-2006] $) $( [19-Jan-2006] $)

    $( The supremum of an unbounded-above set of extended reals is plus
       infinity. $)
    supxrunb2 $p |- ( A C_ RR* -> ( A. x e. RR E. y e. A x < y <->
                    sup ( A , RR* , < ) = +oo ) ) $=
      ( vz vw cxr wss cv clt wbr wrex cr wral csup cpnf wceq wn wi wa wcel
      ssel pnfnlt syl6 r19.21aiv adantr breq1 rexbidv rcla4va adantrr ancoms
      exp31 a1dd com4r com13 imp jca pnfxr supxr mpanl2 syldan ex xrltso
      suplub xrsupss ad2antrr rexr ad2antlr breq2 ltpnf syl5bir impcom adantll
      sylc com23 r19.21adv impbid ) CFGZAHZBHZIJZBCKZALMZCFINZOPZVQWBWDVQWBODHZ
      IJQZDCMZWEOIJZWEVSIJZBCKZRZDLMZSZWDVQWBSZWGWLVQWGWBVQWFDCVQWECTWEFTWFCFWE
      UAWEUBUCUDUEWNWKDLVQWBWELTZWKRWOWBVQWKWBVQWHWOWJWBVQWOWJRWHWBVQWOWJWOWBVQ
      SWJWOWBWJVQWAWJAWELVRWEPVTWIBCVRWEVSIUFUGUHUIUJUKULUMUNUOUDUPVQOFTWMWDUQD
      BCOURUSUTVAVQWDWAALVQVRLTZWDWAVQWPWDWAWEEHZIJQECMWQWEIJWQVSIJBCKREFMSDFKZ
      VRFTZVRWCIJZSWAVQWPSWDSZDEBFCVRIVBVCVQWRWPWDDEBCVDVEXAWSWTWPWSVQWDVRVFVGW
      PWDWTVQWDWPWTWDWTVROIJWPWCOVRIVHVRVIVJVKVLUPVMUKVNVOVP $.
      $( [26-Feb-2006] $) $( [19-Jan-2006] $)

    $( The supremum of a bounded-above nonempty set of reals is real. $)
    supxrbnd $p |- ( ( A C_ RR /\ A =/= (/) /\ sup ( A , RR* , < ) < +oo ) ->
                    sup ( A , RR* , < ) e. RR ) $=
      ( vy vx cr wss c0 wne cv cle wbr wral wrex cxr clt csup wcel cpnf w3a
      supxrre suprcl eqeltrd wi supxrcl wa pnfxr xrltne mp3an2 necomd ex syl
      wceq wn supxrunb2 wb xrlenlt con2bid ssel2 adantlr rexr ad2antlr sylanc
      rexbidva rexnal syl6bb ralbidva bitr3d ralnex necon2abid sylibrd imp
      ressxr sstr mpan2 sylan 3adant2 syld3an3 ) ADEZAFGZBHZCHZIJZBAKZCDLZAMNOZ
      DPWDQNJZVQVRWCRWDADNODCBASCBATUAVQWEWCVRAMEZWEWCVQWFWEWCWFWEWDQGZWCWFWDMP
      ZWEWGUBAUCWHWEWGWHWEUDQWDWHQMPWEQWDGUEWDQUFUGUHUIUJWFWCWDQWFWDQUKZWBULZCD
      KZWCULWFVTVSNJZBALZCDKWIWKCBAUMWFWMWJCDWFVTDPZUDZWMWAULZBALWJWOWLWPBAVSMP
      ZVTMPZWLWPUNWOVSAPZUDWQWRUDWAWLVSVTUOUPWFWSWQWNAMVSUQURWNWRWFWSVTUSUTVAVB
      WABAVCVDVEVFWBCDVGVDVHVIVJVQDMEWFVKADMVLVMVNVOVP $.
      $( [22-May-2007] $) $( [19-Jan-2006] $)

    $( The supremum of a nonempty set of reals is greater than minus
       infinity. $)
    supxrgtmnf $p |- ( ( A C_ RR /\ A =/= (/) ) ->
                     -oo < sup ( A , RR* , < ) ) $=
      ( cr wss c0 wne wa cxr clt csup wcel cpnf wceq wo cmnf wbr wn supxrbnd
      3expia con3d wb ressxr sstr mpan2 supxrcl syl adantr nltpnft sylibrd
      orrd mnfltxr ) ABCZADEZFZAGHIZBJZUNKLZMNUNHOUMUOUPUMUOPUNKHOZPZUPUMUQUOUK
      ULUQUOAQRSUMUNGJZUPURTUKUSULUKAGCZUSUKBGCUTUAABGUBUCAUDUEUFUNUGUEUHUIUNUJ
      UE $.
      $( [25-May-2007] $) $( [2-Feb-2006] $)

    $( The supremum of a nonempty set of reals is real iff it is less than plus
       infinity. $)
    supxrre1 $p |- ( ( A C_ RR /\ A =/= (/) ) ->
            ( sup ( A , RR* , < ) e. RR <-> sup ( A , RR* , < ) < +oo ) ) $=
      ( cr wss c0 wne wa cxr clt csup wcel cmnf wbr cpnf wb ressxr sstr mpan2
      supxrcl xrrebnd 3syl adantr supxrgtmnf biantrurd bitr4d ) ABCZADEZFZAGHIZ
      BJZKUHHLZUHMHLZFZUKUEUIULNZUFUEAGCZUHGJUMUEBGCUNOABGPQARUHSTUAUGUJUKAUBUC
      UD $.
      $( [26-May-2007] $) $( [5-Feb-2006] $)

    $( The supremum of a nonempty set of reals is real iff it is not plus
       infinity. $)
    supxrre2 $p |- ( ( A C_ RR /\ A =/= (/) ) ->
            ( sup ( A , RR* , < ) e. RR <-> sup ( A , RR* , < ) =/= +oo ) ) $=
      ( cr wss c0 wne wa cxr clt csup wcel cpnf wbr supxrre1 wb wceq wn ressxr
      sstr mpan2 supxrcl nltpnft 3syl necon2abid adantr bitrd ) ABCZADEZFAGHIZB
      JUHKHLZUHKEZAMUFUIUJNUGUFUIUHKUFAGCZUHGJUHKOUIPNUFBGCUKQABGRSATUHUAUBUCUD
      UE $.
      $( [8-Dec-2007] $) $( [5-Feb-2006] $)

    $( The supremum of a bounded-above set of extended reals is less than
       infinity. $)
    supxrbnd1 $p |- ( A C_ RR* -> ( E. x e. RR A. y e. A y < x <->
                    sup ( A , RR* , < ) < +oo ) ) $=
      ( cxr wss cv clt wbr wral cr wrex csup cpnf wn cle wceq wcel wa wb
      xrlenlt ancoms ssel2 rexr syl2an an1rs rexbidva rexnal syl6rbb ralbidva
      ralnex syl5bbr supxrunb1 supxrcl nltpnft syl 3bitrd con4bid ) CDEZBFZAFZG
      HZBCIZAJKZCDGLZMGHZURVCNZUTUSOHZBCKZAJIZVDMPZVENZURVBNZAJIVIVFURVLVHAJURU
      TJQZRZVHVANZBCKVLVNVGVOBCURUSCQZVMVGVOSZUSDQZUTDQZVQURVPRVMVSVRVQUTUSTUAC
      DUSUBUTUCUDUEUFVABCUGUHUIVBAJUJUKABCULURVDDQVJVKSCUMVDUNUOUPUQ $.
      $( [13-Nov-2006] $) $( [30-Jan-2006] $)

    $( The supremum of a bounded-above set of extended reals is less than
       infinity. $)
    supxrbnd2 $p |- ( A C_ RR* -> ( E. x e. RR A. y e. A y <_ x <->
                    sup ( A , RR* , < ) < +oo ) ) $=
      ( cxr wss cv cle wbr wral cr wrex clt csup cpnf wn wceq wcel wa wb
      xrlenlt con2bid ssel2 rexr syl2an an1rs rexbidva rexnal syl6rbb ralbidva
      ralnex syl5bbr supxrunb2 supxrcl nltpnft syl 3bitrd con4bid ) CDEZBFZAFZG
      HZBCIZAJKZCDLMZNLHZURVCOZUTUSLHZBCKZAJIZVDNPZVEOZURVBOZAJIVIVFURVLVHAJURU
      TJQZRZVHVAOZBCKVLVNVGVOBCURUSCQZVMVGVOSZUSDQZUTDQZVQURVPRVMVRVSRVAVGUSUTT
      UACDUSUBUTUCUDUEUFVABCUGUHUIVBAJUJUKABCULURVDDQVJVKSCUMVDUNUOUPUQ $.
      $( [26-Jun-2007] $) $( [30-Jan-2006] $)

    $( The supremum of an empty set under the extended reals is minus
       infinity. $)
    xrsup0 $p |- sup ( (/) , RR* , < ) = -oo $=
      ( vy vz c0 cxr wss cmnf wcel wa cv clt wbr wn wral wrex wi cr csup wceq
      0ss mnfxr pm3.2i ral0 rexr nltmnf syl pm2.21d rgen supxr mp2an ) CDEZFDGZ
      HFAIZJKLZACMZULFJKZULBIJKBCNZOZAPMZHCDJQFRUJUKDSTUAUNURUMAUBUQAPULPGZUOUP
      USULDGUOLULUCULUDUEUFUGUAABCFUHUI $.
      $( [12-Feb-2006] $) $( [15-Oct-2005] $)

    $( A member of a set of extended reals is less than or equal to the set's
       supremum. $)
    supxrub $p |- ( ( A C_ RR* /\ B e. A ) -> B <_ sup ( A , RR* , < ) ) $=
      ( vx vy vz cxr wss wcel wa clt csup cle wbr wn cv wral wrex wi xrsupss
      xrltso supub syl imp wb xrlenlt ssel2 supxrcl adantr sylanc mpbird ) AFGZ
      BAHZIZBAFJKZLMZUNBJMNZUKULUPUKCOZDOZJMNDAPURUQJMUREOJMEAQRDFPICFQULUPRCDE
      ASCDEFABJTUAUBUCBFHUNFHZUOUPUDUMBUNUEAFBUFUKUSULAUGUHUIUJ $.
      $( [26-Feb-2006] $) $( [7-Feb-2006] $)

    $( The supremum of a set of extended reals is less than or equal to an
       upper bound. $)
    supxrleub $p |- ( ( A C_ RR* /\ B e. RR* /\ A. x e. A x <_ B ) ->
                  sup ( A , RR* , < ) <_ B ) $=
      ( vy vz cxr wss wcel cv cle wbr wral clt csup wa wn wb xrlenlt ssel2
      sylan an1rs ralbidva pm5.32da wrex wi xrsupss xrltso supnub syl imp
      supxrcl adantrr mpbird ex sylbid 3impib ) BFGZCFHZAIZCJKZABLZBFMNZCJKZUQU
      RVAOURCUSMKPZABLZOZVCUQURVAVEUQUROUTVDABUQUSBHZURUTVDQZUSFHURVHUQVGOUSCRB
      FUSSTUAUBUCUQVFVCUQVFOVCCVBMKPZUQVFVIUQDIZEIZMKPEBLVKVJMKVKUSMKABUDUEEFLO
      DFUDVFVIUEDEABUFDEAFBCMUGUHUIUJUQURVCVIQZVEVBFHURVLUQVBCRBUKTULUMUNUOUP
      $.
      $( [14-Mar-2006] $) $( [22-Feb-2006] $)
  $}


