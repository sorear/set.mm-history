$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Supremum on the extended reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Adding minus infinity to a set does not affect the existence of its
       supremum. $)
    xrsupexmnf $p |- ( E. x e. RR* ( A. y e. A -. x < y /\
              A. y e. RR* ( y < x -> E. z e. A y < z ) ) ->
         E. x e. RR* ( A. y e. ( A u. { -oo } ) -. x < y /\
              A. y e. RR* ( y < x -> E. z e. ( A u. { -oo } ) y < z ) ) ) $=
      ( cv clt wbr wn wral wrex wi cxr wa cmnf csn cun wcel wo elun simpr wceq
      elsn nltmnf breq2 notbid syl5ibrcom syl5bi adantr jaod ex ralimdv2 elun1
      anim1i reximi2 imim2i ralimi a1i anim12d reximia ) AEZBEZFGZHZBDIZVAUTFGZ
      VACEZFGZCDJZKZBLIZMVCBDNOZPZIZVEVGCVLJZKZBLIZMALUTLQZVDVMVJVPVQVCVCBDVLVQ
      VADQZVCKZVAVLQZVCKVTVRVAVKQZRVQVSMZVCVADVKSWBVRVCWAVQVSTVQWAVCKVSWAVANUAZ
      VQVCBNUBVQVCWCUTNFGZHUTUCWCVBWDVANUTFUDUEUFUGUHUIUGUJUKVJVPKVQVIVOBLVHVNV
      EVGVGCDVLVFDQVFVLQVGVFDVKULUMUNUOUPUQURUS $.
      $( [26-Oct-2005] $)

    $( Adding plus infinity to a set does not affect the existence of its
       infimum. $)
    xrinfmexpnf $p |- ( E. x e. RR* ( A. y e. A -. y < x /\
              A. y e. RR* ( x < y -> E. z e. A z < y ) ) ->
         E. x e. RR* ( A. y e. ( A u. { +oo } ) -. y < x /\
              A. y e. RR* ( x < y -> E. z e. ( A u. { +oo } ) z < y ) ) ) $=
      ( cv clt wbr wn wral wrex wi cxr wa cpnf csn cun wcel wo elun simpr wceq
      elsn pnfnlt breq1 notbid syl5ibrcom syl5bi adantr jaod ex ralimdv2 elun1
      anim1i reximi2 imim2i ralimi a1i anim12d reximia ) BEZAEZFGZHZBDIZVAUTFGZ
      CEZUTFGZCDJZKZBLIZMVCBDNOZPZIZVEVGCVLJZKZBLIZMALVALQZVDVMVJVPVQVCVCBDVLVQ
      UTDQZVCKZUTVLQZVCKVTVRUTVKQZRVQVSMZVCUTDVKSWBVRVCWAVQVSTVQWAVCKVSWAUTNUAZ
      VQVCBNUBVQVCWCNVAFGZHVAUCWCVBWDUTNVAFUDUEUFUGUHUIUGUJUKVJVPKVQVIVOBLVHVNV
      EVGVGCDVLVFDQVFVLQVGVFDVKULUMUNUOUPUQURUS $.
      $( [19-Jan-2006] $)

    $( Lemma for ~ xrsupss . $)
    xrsupsslem $p |- ( ( A C_ RR* /\ ( A C_ RR \/ +oo e. A ) ) -> E. x e. RR*
        ( A. y e. A -. x < y /\ A. y e. RR* ( y < x -> E. z e. A y < z ) ) ) $=
      ( cxr wss cr cv clt wbr wn wral wrex wi wa cpnf wcel c0 wceq raleq rexeq
      imbi2d ralbidv anbi12d rexbidv wne cle w3a sup3 rexr anim1i reximi2 syl
      cmnf w3o elxr simpr pnfnlt adantr wb breq1 notbid adantl mpbird pm2.21d
      ex ad2antlr wex ssel mnflt syl6 ancld eximdv n0 df-rex 3imtr4g imp a1d
      ad2antrr imbi12d 3jaod syl5bi ralimdv2 anim2d reximdva 3adant3 mpd 3expa
      ralnex rexnal ssel2 letric ord sylan an32s syl5bir ralimdva sylan2br
      breq2 cbvrexv ralbii sylib pnfxr r19.21aiv c1 caddc co peano2re rcla4va
      adantrr ancoms sylan2 ltp1 ancli ltletr mpand adantll exp31 a1dd com4r
      xrltnr ax-mp mtbiri cc0 0re mpan impcom syl5ibr exp3a 3jaoi sylbi com13
      jca imbi1d rcla4ev sylancr syldan adantlr pm2.61dan mnfxr ral0 nltmnf
      rgen pm3.2i mp2an a1i pm2.61ne anim12i jaodan ) DEFZDGFZAHZBHZIJZKZBDLZUU
      MUULIJZUUMCHZIJZCDMZNZBELZOZAEMZPDQZUUKUVDUUJUUKUVDUUOBRLZUUQUUSCRMZNZBEL
      ZOZAEMZDRDRSZUVCUVJAEUVLUUPUVFUVBUVIUUOBDRTUVLUVAUVHBEUVLUUTUVGUUQUUSCDRU
      AUBUCUDUEUUKDRUFZOZUUMUULUGJZBDLZAGMZUVDUUKUVMUVQUVDUUKUVMUVQUHZUUPUVABGL
      ZOZAEMZUVDUVRUVTAGMUWAABCDUIUVTUVTAGEUULGQZUULEQZUVTUULUJUKULUMUUKUVMUWAU
      VDNUVQUVNUVTUVCAEUVNUWCOZUVSUVBUUPUWDUVAUVABGEUWDUUMGQZUVANZUUMEQZUVANUWG
      UWEUUMPSZUUMUNSZUOZUWDUWFOZUVAUUMUPZUWKUWEUVAUWHUWIUWDUWFUQUWCUWHUVANUVNU
      WFUWCUWHUVAUWCUWHOZUUQUUTUWMUUQKZPUULIJZKZUWCUWPUWHUULURUSUWHUWNUWPUTUWCU
      WHUUQUWOUUMPUULIVAVBVCVDVEVFVGUWDUWIUVANUWFUWDUWIUVAUWDUWIOUVAUNUULIJZUNU
      URIJZCDMZNZUVNUWTUWCUWIUVNUWSUWQUUKUVMUWSUUKUURDQZCVHUXAUWROZCVHUVMUWSUUK
      UXAUXBCUUKUXAUWRUUKUXAUURGQZUWRDGUURVIUURVJZVKVLVMCDVNUWRCDVOVPVQVRVSUWIU
      VAUWTUTUWDUWIUUQUWQUUTUWSUUMUNUULIVAUWIUUSUWRCDUUMUNUURIVAUEZVTVCVDVFUSWA
      WBVFWCWDWEWFWGWHUUKUVQKZUVDUVMUUKUXFUULUURUGJZCDMZAGLZUVDUUKUXFOUULUUMUGJ
      ZBDMZAGLZUXIUXFUUKUVPKZAGLZUXLUVPAGWIUUKUXNUXLUUKUXMUXKAGUXMUVOKZBDMUUKUW
      BOZUXKUVOBDWJUXPUXOUXJBDUUKUUMDQZUWBUXOUXJNZUUKUXQOUWEUWBUXRDGUUMWKUWEUWB
      OUVOUXJUUMUULWLWMWNWOWEWPWQVQWRUXKUXHAGUXJUXGBCDUUMUURUULUGWSWTXAXBUUKUXI
      OZPEQZPUUMIJZKZBDLZUUMPIJZUUTNZBELZOZUVDXCUXSUYCUYFUUKUYCUXIUUKUYBBDUUKUX
      QUWEUYBDGUUMVIUWEUWGUYBUUMUJUUMURZUMVKXDUSUXSUYEBEUUKUXIUWGUYENUWGUXIUUKU
      YEUWGUWJUXIUUKUYENZNZUWLUWEUYJUWHUWIUXIUUKUYDUWEUUTUXIUUKUWEUUTNUYDUXIUUK
      UWEUUTUXIUUKOZUWEOUUMXEXFXGZUURUGJZCDMZUUTUWEUYKUYLGQZUYNUUMXHZUYOUYKUYNU
      YOUXIUYNUUKUXHUYNAUYLGUULUYLSUXGUYMCDUULUYLUURUGVAUEXIXJXKXLUUKUWEUYNUUTN
      UXIUUKUWEOUYMUUSCDUUKUXAUWEUYMUUSNZUUKUXAOZUXCUWEUYQDGUURWKZUWEUXCUYQUWEU
      XCOUUMUYLIJZUYMUUSUWEUYTUXCUUMXMUSUWEUWEUYOOUXCUYTUYMOUUSNZUWEUYOUYPXNUWE
      UYOUXCVUAUUMUYLUURXOWHWNXPXKWNWOWEXQWGXRXSXTUWHUYIUXIUWHUYEUUKUWHUYDUUTUW
      HUYDPPIJZUXTVUBKXCPYAYBUUMPPIVAYCVEVRVRUWIUXIUUKUYEUWIUYKUUTUYDUYKUUTUWIU
      WSUXIYDUURUGJZCDMZUUKUWSYDGQUXIVUDYEUXHVUDAYDGUULYDSUXGVUCCDUULYDUURUGVAU
      EXIYFUUKVUDUWSUUKVUCUWRCDUYRUWRVUCUYRUXCUWRUYSUXDUMVRWEYGWNUXEYHXSYIYJYKY
      LVQXDYMUVCUYGAPEUULPSZUUPUYCUVBUYFVUEUUOUYBBDVUEUUNUYAUULPUUMIVAVBUCVUEUV
      AUYEBEVUEUUQUYDUUTUULPUUMIWSYNUCUDYOZYPYQYRYSUVKUUKUNEQUNUUMIJZKZBRLZUUMU
      NIJZUVGNZBELZOZUVKYTVUIVULVUHBUUAVUKBEUWGVUJUVGUUMUUBVEUUCUUDUVJVUMAUNEUU
      LUNSZUVFVUIUVIVULVUNUUOVUHBRVUNUUNVUGUULUNUUMIVAVBUCVUNUVHVUKBEVUNUUQVUJU
      VGUULUNUUMIWSYNUCUDYOUUEUUFUUGVCUUJUVEOUXTUYGUVDXCUUJUYCUVEUYFUUJUYBBDUUJ
      UXQUWGUYBDEUUMVIUYHVKXDUVEUYEBEUVEUYEUWGUVEUYDUUTUUSUYDCPDUURPUUMIWSYOVFV
      RXDUUHVUFYPUUI $.
      $( [25-Oct-2005] $)

    $( Lemma for ~ xrinfmss . $)
    xrinfmsslem $p |- ( ( A C_ RR* /\ ( A C_ RR \/ -oo e. A ) ) -> E. x e. RR*
        ( A. y e. A -. y < x /\ A. y e. RR* ( x < y -> E. z e. A z < y ) ) ) $=
      ( cxr wss cr cv clt wbr wn wral wrex wi wa cmnf wcel c0 wceq raleq rexeq
      imbi2d ralbidv anbi12d rexbidv wne cle w3a infm3 rexr anim1i reximi2 syl
      cpnf w3o elxr simpr wex ssel ltpnf syl6 ancld eximdv n0 df-rex 3imtr4g
      imp a1d ad2antrr wb breq2 imbi12d adantl mpbird ex adantr nltmnf notbid
      pm2.21d ad2antlr 3jaod syl5bi ralimdv2 anim2d reximdva 3adant3 mpd 3expa
      ralnex rexnal ssel2 wo letric ancoms ord sylan an32s syl5bir ralimdva
      sylan2br breq1 cbvrexv ralbii sylib mnfxr r19.21aiv c1 cmin co peano2rem
      rcla4va adantrr sylan2 ltm1 ancri lelttr 3expb mpan2d adantll exp31 a1dd
      com4r cc0 0re mpan impcom syl5ibr exp3a xrltnr ax-mp mtbiri 3jaoi sylbi
      com13 jca imbi1d rcla4ev sylancr syldan adantlr pm2.61dan pnfxr ral0
      pnfnlt rgen pm3.2i mp2an a1i pm2.61ne anim12i jaodan ) DEFZDGFZBHZAHZIJZK
      ZBDLZUUOUUNIJZCHZUUNIJZCDMZNZBELZOZAEMZPDQZUUMUVFUULUUMUVFUUQBRLZUUSUVACR
      MZNZBELZOZAEMZDRDRSZUVEUVLAEUVNUURUVHUVDUVKUUQBDRTUVNUVCUVJBEUVNUVBUVIUUS
      UVACDRUAUBUCUDUEUUMDRUFZOZUUOUUNUGJZBDLZAGMZUVFUUMUVOUVSUVFUUMUVOUVSUHZUU
      RUVCBGLZOZAEMZUVFUVTUWBAGMUWCABCDUIUWBUWBAGEUUOGQZUUOEQZUWBUUOUJUKULUMUUM
      UVOUWCUVFNUVSUVPUWBUVEAEUVPUWEOZUWAUVDUURUWFUVCUVCBGEUWFUUNGQZUVCNZUUNEQZ
      UVCNUWIUWGUUNUNSZUUNPSZUOZUWFUWHOZUVCUUNUPZUWMUWGUVCUWJUWKUWFUWHUQUWFUWJU
      VCNUWHUWFUWJUVCUWFUWJOUVCUUOUNIJZUUTUNIJZCDMZNZUVPUWRUWEUWJUVPUWQUWOUUMUV
      OUWQUUMUUTDQZCURUWSUWPOZCURUVOUWQUUMUWSUWTCUUMUWSUWPUUMUWSUUTGQZUWPDGUUTU
      SUUTUTZVAVBVCCDVDUWPCDVEVFVGVHVIUWJUVCUWRVJUWFUWJUUSUWOUVBUWQUUNUNUUOIVKU
      WJUVAUWPCDUUNUNUUTIVKUEZVLVMVNVOVPUWEUWKUVCNUVPUWHUWEUWKUVCUWEUWKOZUUSUVB
      UXDUUSKZUUOPIJZKZUWEUXGUWKUUOVQVPUWKUXEUXGVJUWEUWKUUSUXFUUNPUUOIVKVRVMVNV
      SVOVTWAWBVOWCWDWEWFWGWHUUMUVSKZUVFUVOUUMUXHUUTUUOUGJZCDMZAGLZUVFUUMUXHOUU
      NUUOUGJZBDMZAGLZUXKUXHUUMUVRKZAGLZUXNUVRAGWIUUMUXPUXNUUMUXOUXMAGUXOUVQKZB
      DMUUMUWDOZUXMUVQBDWJUXRUXQUXLBDUUMUUNDQZUWDUXQUXLNZUUMUXSOUWGUWDUXTDGUUNW
      KUWGUWDOUVQUXLUWDUWGUVQUXLWLUUOUUNWMWNWOWPWQWEWRWSVGWTUXMUXJAGUXLUXIBCDUU
      NUUTUUOUGXAXBXCXDUUMUXKOZPEQZUUNPIJZKZBDLZPUUNIJZUVBNZBELZOZUVFXEUYAUYEUY
      HUUMUYEUXKUUMUYDBDUUMUXSUWGUYDDGUUNUSUWGUWIUYDUUNUJUUNVQZUMVAXFVPUYAUYGBE
      UUMUXKUWIUYGNUWIUXKUUMUYGUWIUWLUXKUUMUYGNZNZUWNUWGUYLUWJUWKUXKUUMUYFUWGUV
      BUXKUUMUWGUVBNUYFUXKUUMUWGUVBUXKUUMOZUWGOUUTUUNXGXHXIZUGJZCDMZUVBUWGUYMUY
      NGQZUYPUUNXJZUYQUYMUYPUYQUXKUYPUUMUXJUYPAUYNGUUOUYNSUXIUYOCDUUOUYNUUTUGVK
      UEXKXLWNXMUUMUWGUYPUVBNUXKUUMUWGOUYOUVACDUUMUWSUWGUYOUVANZUUMUWSOZUXAUWGU
      YSDGUUTWKZUXAUWGOUYOUYNUUNIJZUVAUWGVUBUXAUUNXNVMUWGUXAUYQUWGOUYOVUBOUVANZ
      UWGUYQUYRXOUXAUYQUWGVUCUUTUYNUUNXPXQXMXRWPWQWEXSWGXTYAYBUWJUXKUUMUYGUWJUY
      MUVBUYFUYMUVBUWJUWQUXKUUTYCUGJZCDMZUUMUWQYCGQUXKVUEYDUXJVUEAYCGUUOYCSUXIV
      UDCDUUOYCUUTUGVKUEXKYEUUMVUEUWQUUMVUDUWPCDUYTUWPVUDUYTUXAUWPVUAUXBUMVHWEY
      FWPUXCYGYAYHUWKUYKUXKUWKUYGUUMUWKUYFUVBUWKUYFPPIJZUYBVUFKXEPYIYJUUNPPIVKY
      KVSVHVHYLYMYNVGXFYOUVEUYIAPEUUOPSZUURUYEUVDUYHVUGUUQUYDBDVUGUUPUYCUUOPUUN
      IVKVRUCVUGUVCUYGBEVUGUUSUYFUVBUUOPUUNIXAYPUCUDYQZYRYSYTUUAUVMUUMUNEQUUNUN
      IJZKZBRLZUNUUNIJZUVINZBELZOZUVMUUBVUKVUNVUJBUUCVUMBEUWIVULUVIUUNUUDVSUUEU
      UFUVLVUOAUNEUUOUNSZUVHVUKUVKVUNVUPUUQVUJBRVUPUUPVUIUUOUNUUNIVKVRUCVUPUVJV
      UMBEVUPUUSVULUVIUUOUNUUNIXAYPUCUDYQUUGUUHUUIVMUULUVGOUYBUYIUVFXEUULUYEUVG
      UYHUULUYDBDUULUXSUWIUYDDEUUNUSUYJVAXFUVGUYGBEUVGUYGUWIUVGUYFUVBUVAUYFCPDU
      UTPUUNIXAYQVOVHXFUUJVUHYRUUK $.
      $( [19-Jan-2006] $)

    $( Any subset of extended reals has a supremum. $)
    xrsupss $p |- ( A C_ RR* -> E. x e. RR* (
         A. y e. A -. x < y /\ A. y e. RR* ( y < x -> E. z e. A y < z ) ) ) $=
      ( cxr wss cr cpnf wcel wo cmnf cv clt wbr wn wral wrex wi wa w3o ssxr
      df-3or sylib xrsupsslem csn cdif ssdifss orcom wb mnfxr elisseti snid
      elndif ax-mp biorf 3bitr4i mpdan syl adantr cun xrsupexmnf wceq snss
      undif uncom eqeq1i 3bitri raleq rexeq imbi2d ralbidv anbi12d sylbi
      rexbidv syl5ib adantl mpd jaodan ) DEFZDGFZHDIZJZKDIZJZALZBLZMNOZBDPZWFWE
      MNZWFCLMNZCDQZRZBEPZSZAEQZVSVTWAWCTWDDUAVTWAWCUBUCVSWBWOWCABCDUDVSWCSWGBD
      KUEZUFZPWIWJCWQQRBEPSAEQZWOVSWRWCVSWQEFZWRDEWPUGWSWQGFZHWQIZJZWRWSWTXAKWQ
      IZTZXBWQUAXBXCJXCXBJZXDXBXBXCUHWTXAXCUBXCOZXBXEUIKWPIXFKKEUJUKZULKWPDUMUN
      XCXBUOUNUPUCABCWQUDUQURUSWCWRWORVSWRWGBWQWPUTZPZWIWJCXHQZRZBEPZSZAEQWCWOA
      BCWQVAWCXMWNAEWCXHDVBZXMWNUIWCWPDFWPWQUTZDVBXNKDXGVCWPDVDXOXHDWPWQVEVFVGX
      NXIWHXLWMWGBXHDVHXNXKWLBEXNXJWKWIWJCXHDVIVJVKVLVMVNVOVPVQVRUQ $.
      $( [25-Oct-2005] $)

    $( Any subset of extended reals has an infimum. $)
    xrinfmss $p |- ( A C_ RR* -> E. x e. RR* (
         A. y e. A -. y < x /\ A. y e. RR* ( x < y -> E. z e. A z < y ) ) ) $=
      ( cxr wss cr cmnf wcel wo cpnf cv clt wbr wn wral wrex wi wa w3o ssxr
      df-3or or23 bitri sylib xrinfmsslem csn cdif ssdifss 3orass wb pnfxr
      elisseti snid elndif ax-mp biorf orbi2i bitr4i mpdan syl adantr cun
      xrinfmexpnf wceq snss undif uncom eqeq1i 3bitri raleq rexeq imbi2d
      ralbidv anbi12d sylbi rexbidv syl5ib adantl mpd jaodan ) DEFZDGFZHDIZJZKD
      IZJZBLZALZMNOZBDPZWIWHMNZCLWHMNZCDQZRZBEPZSZAEQZWBWCWFWDTZWGDUAWSWCWFJWDJ
      WGWCWFWDUBWCWFWDUCUDUEWBWEWRWFABCDUFWBWFSWJBDKUGZUHZPWLWMCXAQRBEPSAEQZWRW
      BXBWFWBXAEFZXBDEWTUIXCXAGFZHXAIZJZXBXCXDKXAIZXETZXFXAUAXHXDXGXEJZJXFXDXGX
      EUJXEXIXDXGOZXEXIUKKWTIXJKKEULUMZUNKWTDUOUPXGXEUQUPURUSUEABCXAUFUTVAVBWFX
      BWRRWBXBWJBXAWTVCZPZWLWMCXLQZRZBEPZSZAEQWFWRABCXAVDWFXQWQAEWFXLDVEZXQWQUK
      WFWTDFWTXAVCZDVEXRKDXKVFWTDVGXSXLDWTXAVHVIVJXRXMWKXPWPWJBXLDVKXRXOWOBEXRX
      NWNWLWMCXLDVLVMVNVOVPVQVRVSVTWAUT $.
      $( [25-Oct-2005] $)

    $( By quantifying only over reals, we can specify any extended real upper
       bound for any set of extended reals. $)
    xrub $p |- ( ( A C_ RR* /\ B e. RR* ) -> ( A. x e. RR ( x < B ->
          E. y e. A x < y ) <-> A. x e. RR* ( x < B -> E. y e. A x < y ) ) ) $=
      ( vz cxr wss wcel wa cv clt wbr wrex wi cr wral wceq breq1 rexbidv
      imbi12d cbvralv cpnf cmnf w3o elxr pm2.27 a1i wn pnfnlt notbid syl5ibr
      pm2.21 syl6com ad2antlr a1dd c1 cmin co peano2rem rcla4v syl adantl ltm1
      id syl5com mnflt rexr ssel2 adantlr mnfxr xrlttr mp3an1 syl11anc mpand
      reximdva 3syld 1re ax-mp ltpnf breq2 mpbiri mp3an12 mpani sylan9r syl5
      xrltnr mtbiri pm2.21d a1d 3jaodan sylan2b imp syl5ibrcom 3jaod syl5bi
      com23 ralimdv2 ex pm2.43d imim1i ralimi2 impbid1 ) CFGZDFHZIZAJZDKLZXFBJZ
      KLZBCMZNZAOPZXKAFPZXEXLXMXLEJZDKLZXNXHKLZBCMZNZEOPZXEXLXMNZXKXRAEOXFXNQZX
      GXOXJXQXFXNDKRYAXIXPBCXFXNXHKRSTUAXEXSXTXEXSIZXKXKAOFYBXFFHZXFOHZXKNZXKYC
      YDXFUBQZXFUCQZUDYBYEXKNZXFUEYBYDYHYFYGYDYHNYBYDXKUFUGYBYFXKYEXDYFXKNXCXSY
      FXDXGUHZXKXDYIYFUBDKLZUHDUIYFXGYJXFUBDKRUJUKXGXJULUMUNUOYBYGXKYEYBXKYGUCD
      KLZUCXHKLZBCMZNZXEXSYNXDXCDOHZDUBQZDUCQZUDXSYNNZDUEXCYOYRYPYQXCYOIZXSYMYK
      YSXSDUPUQURZDKLZYTXHKLZBCMZNZUUCYMYOXSUUDNZXCYOYTOHZUUEDUSZXRUUDEYTOXNYTQ
      ZXOUUAXQUUCXNYTDKRUUHXPUUBBCXNYTXHKRSTUTVAVBYOUUDUUCNXCYOUUAUUDUUCDVCUUDV
      DVEVBYSUUBYLBCYSXHCHZIZUCYTKLZUUBYLUUJUUFUUKYOUUFXCUUIUUGUNZYTVFVAUUJYTFH
      ZXHFHZUUKUUBIYLNZUUJUUFUUMUULYTVGVAXCUUIUUNYOCFXHVHZVIUCFHZUUMUUNUUOVJUCY
      TXHVKVLVMVNVOVPUOXCYPIZXSYMYKXSUPDKLZUPXHKLZBCMZNZUURYMUPOHZXSUVBNVQXRUVB
      EUPOXNUPQZXOUUSXQUVAXNUPDKRUVDXPUUTBCXNUPXHKRSTUTVRYPUVBUVAXCYMYPUUSUVBUV
      AYPUUSUPUBKLZUVCUVEVQUPVSVRDUBUPKVTWAUVBVDVEXCUUTYLBCXCUUIIUUNUUTYLNUUPUU
      NUCUPKLZUUTYLUVCUVFVQUPVFVRUUQUPFHZUUNUVFUUTIYLNVJUVCUVGVQUPVGVRUCUPXHVKW
      BWCVAVOWDWEUOXCYQIZYNXSUVHYKYMYQYKUHXCYQYKUCUCKLZUUQUVIUHVJUCWFVRDUCUCKVT
      WGVBWHWIWJWKWLYGXGYKXJYMXFUCDKRYGXIYLBCXFUCXHKRSTWMUOWNWOWPWQWRWOWSXKXKAF
      OYDYCXKXFVGWTXAXB $.
      $( [9-Apr-2006] $)

    $( The supremum of a set of extended reals. $)
    supxr $p |- ( ( ( A C_ RR* /\ B e. RR* ) /\ ( A. x e. A -. B < x /\
    A. x e. RR ( x < B -> E. y e. A x < y ) ) ) -> sup ( A , RR* , < ) = B ) $=
      ( vz cxr wss wcel wa cv clt wbr wn wral wrex wi cr csup wceq xrub biimpd
      anim2d imp crab cuni df-sup wreu wb xrsupss anim2i ancoms xrltso supeu
      breq1 notbid ralbidv breq2 imbi1d anbi12d reuuni2 3syl biimpa syl5eq
      syldan ) CFGZDFHZIZDAJZKLZMZACNZVHDKLZVHBJKLBCOZPZAQNZIZVKVNAFNZIZCFKRZDS
      VGVPVRVGVOVQVKVGVOVQABCDTUAUBUCVGVRIVSEJZVHKLZMZACNZVHVTKLZVMPZAFNZIZEFUD
      UEZDEABFCKUFVGVRWHDSZVGVFWGEFOZIZVFWGEFUGZIVRWIUHVFVEWKVEWJVFEABCUIUJUKWJ
      WLVFEABFCKULUMUJWGVREFDVTDSZWCVKWFVQWMWBVJACWMWAVIVTDVHKUNUOUPWMWEVNAFWMW
      DVLVMVTDVHKUQURUPUSUTVAVBVCVD $.
      $( [9-Apr-2006] $)

    $( The supremum of a set of extended reals. $)
    supxr2 $p |- ( ( ( A C_ RR* /\ B e. RR* ) /\ ( A. x e. A x <_ B /\
  A. x e. RR ( x < B -> E. y e. A x < y ) ) ) -> sup ( A , RR* , < ) = B ) $=
      ( cxr wss wcel wa cv cle wbr wral clt wrex wi cr wn csup wceq wb ssel2
      xrlenlt sylan an32s ralbidva anbi1d biimpa supxr syldan ) CEFZDEGZHZAIZDJ
      KZACLZUMDMKUMBIMKBCNOAPLZHZDUMMKQZACLZUPHZCEMRDSULUQUTULUOUSUPULUNURACUJU
      MCGZUKUNURTZUJVAHUMEGUKVBCEUMUAUMDUBUCUDUEUFUGABCDUHUI $.
      $( [9-Apr-2006] $)

    $( The real and extended real suprema match when the real supremum
       exists. $)
    supxrre $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
                  sup ( A , RR* , < ) = sup ( A , RR , < ) ) $=
      ( vz cr wss c0 wne cv cle wbr wral wrex w3a clt wn wi cxr wa crab cuni
      csup wceq wcel rexr imim1i cpnf cmnf w3o elxr simpr pnfnlt adantr wb
      breq1 notbid adantl mpbird pm2.21d ex ad2antlr wex ssel mnflt syl6 ancld
      eximdv n0 df-rex 3imtr4g imp a1d ad2antrr rexbidv imbi12d 3jaod syl5bi
      impbid2 ralbidv2 anbi2d rabbidva unieqd 3adant3 wreu sup3 ltso supeu syl
      cbvralv rexbii breq2 ralbidv cbvrexv bitri weu ax-1 a1i simpl3 anbi1d
      ltpnf biantrurd ssel2 lenlt sylan an32s ralbidva ralnex syl6rbb bitr3d
      sylan9bbr rexbidva 3ad2antl1 rexanali sylib adantld biimprd sylan9
      dfrex2 bitr3i adantrd imp3a jcad anim1i impbid1 eubidv df-reu 3bitr4g
      syl3an3b ressxr reuuniss mp3an1 syl11anc eqtr4d df-sup 3eqtr4g ) CEFZCGHZ
      BIZAIZJKZBCLZAEMZNZYSYROKZPBCLZYRYSOKZYRDIZOKZDCMZQZBRLZSZARTZUAZUUEUUJBE
      LZSZAETUAZCROUBCEOUBUUCUUNUUPARTZUAZUUQYPYQUUNUUSUCUUBYPYQSZUUMUURUUTUULU
      UPARUUTYSRUDZSZUUKUUOUUEUVBUUJUUJBREUVBYRRUDZUUJQZYREUDZUUJQZUVEUVCUUJYRU
      EUFUVBUVFUVDUVCUVEYRUGUCZYRUHUCZUIUVBUVFSZUUJYRUJUVIUVEUUJUVGUVHUVBUVFUKU
      VAUVGUUJQUUTUVFUVAUVGUUJUVAUVGSZUUFUUIUVJUUFPZUGYSOKZPZUVAUVMUVGYSULUMUVG
      UVKUVMUNUVAUVGUUFUVLYRUGYSOUOUPUQURUSUTVAUVBUVHUUJQUVFUVBUVHUUJUVBUVHSUUJ
      UHYSOKZUHUUGOKZDCMZQZUUTUVQUVAUVHUUTUVPUVNYPYQUVPYPUUGCUDZDVBUVRUVOSZDVBY
      QUVPYPUVRUVSDYPUVRUVOYPUVRUUGEUDZUVOCEUUGVCUUGVDVEVFVGDCVHUVODCVIVJVKVLVM
      UVHUUJUVQUNUVBUVHUUFUVNUUIUVPYRUHYSOUOUVHUUHUVODCYRUHUUGOUOVNVOUQURUTUMVP
      VQUTVRVSVTWAWBWCUUCUUPAEMZUUPARWDZUUQUUSUCZABDCWEZUUCUWBUUPAEWDZUUCUWAUWE
      UWDABDECOWFWGWHUUBYPYQUUGYRJKZDCLZBEMZUWBUWEUNUUBUUGYSJKZDCLZAEMUWHUUAUWJ
      AEYTUWIBDCYRUUGYSJUOWIWJUWJUWGABEYSYRUCUWIUWFDCYSYRUUGJWKWLWMWNYPYQUWHNZU
      VAUUPSZAWOYSEUDZUUPSZAWOUWBUWEUWKUWLUWNAUWKUWLUWNUWKUWLUWMUUPUWKUVAUUPUWM
      UVAUWMYSUGUCZYSUHUCZUIUWKUUPUWMQZYSUJUWKUWMUWQUWOUWPUWMUWQQUWKUWMUUPWPWQU
      WKUWOUWQUWKUWOSZUUOUWMUUEUWRUUOUWMUWRUUFUUIPZSZBEMZUUOPUWRUXAUWHYPYQUWHUW
      OWRYPYQUWOUXAUWHUNUWHYPUWOSUWTUWGBEYPUVEUWOUWTUWGUNUWOUWTYRUGOKZUWSSZYPUV
      ESZUWGUWOUUFUXBUWSYSUGYROWKWSUXDUWSUXCUWGUXDUXBUWSUVEUXBYPYRWTUQXAUXDUWGU
      UHPZDCLUWSUXDUWFUXEDCYPUVRUVEUWFUXEUNZYPUVRSUVTUVEUXFCEUUGXBUUGYRXCXDXEXF
      UUHDCXGXHXIXJXEXKXLURUUFUUIBEXMXNUSXOUTYPYQUWPUWQQUWHUUTUWPUWQUUTUWPSZUUE
      UWMUUOUXGUUEUWMUXGYRCUDZUUDSZBVBZUUEPZYPUWPYQUXJYPUWPSZYQUXJYQUXHBVBUXLUX
      JBCVHUXLUXHUXIBUXLUXHUUDYPUXHUHYROKZUWPUUDYPUXHUVEUXMCEYRVCYRVDVEUWPUUDUX
      MYSUHYROUOXPXQVFVGVQVKXEUXJUUDBCMUXKUUDBCVIUUDBCXRXSXNUSXTUTWCVPVQYAUWLUU
      PQUWKUVAUUPUKWQYBUWMUVAUUPYSUEYCYDYEUUPARYFUUPAEYFYGYHURERFUWAUWBUWCYIUUP
      AERYJYKYLYMABDRCOYNABDECOYNYO $.
      $( [13-May-2007] $) $( [18-Oct-2005] $)

    $( The supremum of an arbitrary set of extended reals is an extended
       real. $)
    supxrcl $p |- ( A C_ RR* -> sup ( A , RR* , < ) e. RR* ) $=
      ( vx vy vz cxr wss cv clt wbr wn wral wrex wi wa csup wcel xrsupss
      xrltso supcl syl ) AEFBGZCGZHIJCAKUBUAHIUBDGHIDALMCEKNBELAEHOEPBCDAQBCDEA
      HRST $.
      $( [24-Oct-2005] $)

    $( The supremum of the union of two sets of extended reals equals the
       largest of their suprema. $)
    supxrun $p |- ( ( A C_ RR* /\ B C_ RR* /\ sup ( A , RR* , < ) <_ sup
   ( B , RR* , < ) ) -> sup ( ( A u. B ) , RR* , < ) = sup ( B , RR* , < ) ) $=
      ( vx vy vz vw cxr wss clt csup cle wbr w3a cun wcel cv wn wral wrex wi
      cr wceq wa unss biimpi 3adant3 supxrcl 3ad2ant2 wo elun xrsupss xrltso
      supub syl 3ad2ant1 ad2antrr ad2antlr ssel2 adantlr xrlelttr syl111anc
      expdimp con3d exp41 com34 3imp mpdd jaod syl5bi r19.21aiv rexr suplub
      sylani elun2 anim1i reximi2 syl6 exp3a supxr syl22anc ) AGHZBGHZAGIJZBGIJ
      ZKLZMZABNZGHZWDGOZWDCPZILZQZCWGRWJWDILZWJDPZILZDWGSZTZCUARZWGGIJWDUBWAWBW
      HWEWAWBUCZWHABGUDUEUFWBWAWIWEBUGZUHWFWLCWGWJWGOWJAOZWJBOZUIWFWLWJABUJWFXA
      WLXBWFXAWCWJILZQZWLWAWBXAXDTZWEWAWNEPZILQZEARXFWNILZXFFPILZFASTEGRUCDGSXE
      DEFAUKDEFGAWJIULUMUNUOWAWBWEXAXDWLTZTWAWBXAWEXJWAWBXAWEXJWSXAUCZWEUCWKXCX
      KWEWKXCXKWCGOZWIWJGOZWEWKUCXCTWAXLWBXAAUGUPWBWIWAXAWTUQWAXAXMWBAGWJURUSWC
      WDWJUTVAVBVCVDVEVFVGWBWAXBWLTZWEWBXGEBRXHXIFBSTEGRUCDGSXNDEFBUKDEFGBWJIUL
      UMUNUHVHVIVJWBWAWRWEWBWQCUAWBWJUAOZWMWPWBXOWMUCZWODBSZWPWBWJXFILQEBRXFWJI
      LXHDBSTEGRUCCGSZXPXQTCEDBUKXOXRXMWMXQWJVKCEDGBWJIULVLVMUNWOWODBWGWNBOWNWG
      OWOWNBAVNVOVPVQVRVJUHCDWGWDVSVT $.
      $( [19-Jan-2006] $)

    $( The infimum of an arbitrary set of extended reals is an extended
       real. $)
    infmxrcl $p |- ( A C_ RR* -> sup ( A , RR* , `' < ) e. RR* ) $=
      ( vx vy vz cxr wss cv clt ccnv wbr wn wral wrex wi wa csup wcel xrinfmss
      visset brcnv notbii ralbii rexbii imbi12i anbi12i sylibr wor xrltso
      cnvso mpbi supcl syl ) AEFZBGZCGZHIZJZKZCALZUOUNUPJZUODGZUPJZDAMZNZCELZOZ
      BEMZAEUPPEQUMUOUNHJZKZCALZUNUOHJZVAUOHJZDAMZNZCELZOZBEMVGBCDARVFVPBEUSVJV
      EVOURVICAUQVHUNUOHBSZCSZTUAUBVDVNCEUTVKVCVMUOUNHVRVQTVBVLDAUOVAHVRDSTUCUD
      UBUEUCUFBCDEAUPEHUGEUPUGUHEHUIUJUKUL $.
      $( [19-Jan-2006] $)

    $( Adding minus infinity to a set does not affect its supremum. $)
    supxrmnf $p |- ( A C_ RR* ->
                 sup ( ( A u. { -oo } ) , RR* , < ) = sup ( A , RR* , < ) ) $=
      ( cxr wss cmnf csn cun clt csup uncom supeq1i cle wbr wceq wcel mnfxr
      snssi ax-mp a1i id xrltso supsn supxrcl mnfle syl syl5eqbr supxrun
      syl111anc syl5eq ) ABCZADEZFZBGHUJAFZBGHZABGHZBUKULGAUJIJUIUJBCZUIUJBGHZU
      NKLUMUNMUOUIDBNZUOODBPQRUISUIUPDUNKUQUPDMOBDGTUAQUIUNBNDUNKLAUBUNUCUDUEUJ
      AUFUGUH $.
      $( [19-Jan-2006] $)

    $( The supremum of a set of extended reals containing plus infnity is plus
       infinity. $)
    supxrpnf $p |- ( ( A C_ RR* /\ +oo e. A ) -> sup ( A , RR* , < ) = +oo ) $=
      ( vy vz cxr wss cpnf wcel cv clt wbr wn wral wrex wi cr wa csup wceq
      ssel pnfnlt syl6 r19.21aiv breq2 rcla4ev ex a1d anim12i pnfxr supxr
      mpanl2 syldan ) ADEZFAGZFBHZIJKZBALZUNFIJZUNCHZIJZCAMZNZBOLZPZADIQFRZULUP
      UMVBULUOBAULUNAGUNDGUOADUNSUNTUAUBUMVABOUMVAUNOGUMUQUTUSUQCFAURFUNIUCUDUE
      UFUBUGULFDGVCVDUHBCAFUIUJUK $.
      $( [14-Dec-2006] $) $( [15-Oct-2005] $)

    $( The supremum of an unbounded-above set of extended reals is plus
       infinity. $)
    supxrunb1 $p |- ( A C_ RR* -> ( A. x e. RR E. y e. A x <_ y <->
                    sup ( A , RR* , < ) = +oo ) ) $=
      ( vz vw cxr wss cv cle wbr wrex cr wral clt csup cpnf wceq wn wi wa wcel
      ssel pnfnlt syl6 r19.21aiv adantr c1 caddc co peano2re breq1 rexbidv
      rcla4va adantrr ancoms sylan2 ssel2 ltp1 ancli rexr xrltletr syl3an2
      syl3an1 3expa sylan mpand an32s reximdva adantll mpd exp31 a1dd com4r
      com13 imp jca pnfxr supxr mpanl2 syldan ex xrsupss ad2antrr ad2antlr
      ltpnf breq2 syl5ibr impcom xrltso suplub sylc adantlr xrltle syl11anc
      syld r19.21adva impbid ) CFGZAHZBHZIJZBCKZALMZCFNOZPQZWRXCXEWRXCPDHZNJRZD
      CMZXFPNJZXFWTNJZBCKZSZDLMZTZXEWRXCTZXHXMWRXHXCWRXGDCWRXFCUAXFFUAZXGCFXFUB
      XFUCUDUEUFXOXLDLWRXCXFLUAZXLSXQXCWRXLXCWRXIXQXKXCWRXQXKSXIXCWRXQXKXCWRTZX
      QTXFUGUHUIZWTIJZBCKZXKXQXRXSLUAZYAXFUJZYBXRYAYBXCYAWRXBYAAXSLWSXSQXAXTBCW
      SXSWTIUKULUMUNUOUPWRXQYAXKSXCWRXQTXTXJBCWRWTCUAZXQXTXJSZWRYDTWTFUAZXQYECF
      WTUQZXQYFYEXQYFTXFXSNJZXTXJXQYHYFXFURUFXQXQYBTYFYHXTTXJSZXQYBYCUSXQYBYFYI
      XQXPYBYFYIXFUTYBXPXSFUAYFYIXSUTXFXSWTVAVBVCVDVEVFUOVEVGVHVIVJVKVLVMVNVOUE
      VPWRPFUAXNXEVQDBCPVRVSVTWAWRXEXBALWRWSLUAZTZXEWSWTNJZBCKZXBYKXEYMYKXETZXF
      EHZNJRECMYOXFNJYOWTNJBCKSEFMTDFKZWSFUAZWSXDNJZTYMWRYPYJXEDEBCWBWCYNYQYRYJ
      YQWRXEWSUTZWDYJXEYRWRXEYJYRYJYRXEWSPNJWSWEXDPWSNWFWGWHVIVPDEBFCWSNWIWJWKW
      AYKYLXABCYKYDTYQYFYLXASYJYQWRYDYSWDWRYDYFYJYGWLWSWTWMWNVHWOWPWQ $.
      $( [19-Jan-2006] $)

    $( The supremum of an unbounded-above set of extended reals is plus
       infinity. $)
    supxrunb2 $p |- ( A C_ RR* -> ( A. x e. RR E. y e. A x < y <->
                    sup ( A , RR* , < ) = +oo ) ) $=
      ( vz vw cxr wss cv clt wbr wrex cr wral csup cpnf wceq wn wi wa wcel
      ssel pnfnlt syl6 r19.21aiv adantr breq1 rexbidv rcla4va adantrr ancoms
      exp31 a1dd com4r com13 imp jca pnfxr supxr mpanl2 syldan ex xrsupss
      ad2antrr rexr ad2antlr ltpnf breq2 syl5ibr impcom adantll xrltso suplub
      sylc com23 r19.21adv impbid ) CFGZAHZBHZIJZBCKZALMZCFINZOPZVQWBWDVQWBODHZ
      IJQZDCMZWEOIJZWEVSIJZBCKZRZDLMZSZWDVQWBSZWGWLVQWGWBVQWFDCVQWECTWEFTWFCFWE
      UAWEUBUCUDUEWNWKDLVQWBWELTZWKRWOWBVQWKWBVQWHWOWJWBVQWOWJRWHWBVQWOWJWOWBVQ
      SWJWOWBWJVQWAWJAWELVRWEPVTWIBCVRWEVSIUFUGUHUIUJUKULUMUNUOUDUPVQOFTWMWDUQD
      BCOURUSUTVAVQWDWAALVQVRLTZWDWAVQWPWDWAVQWPSWDSZWEEHZIJQECMWRWEIJWRVSIJBCK
      REFMSDFKZVRFTZVRWCIJZSWAVQWSWPWDDEBCVBVCWQWTXAWPWTVQWDVRVDVEWPWDXAVQWDWPX
      AWPXAWDVROIJVRVFWCOVRIVGVHVIVJUPDEBFCVRIVKVLVMUKVNVOVP $.
      $( [19-Jan-2006] $)

    $( The supremum of a bounded-above nonempty set of reals is real. $)
    supxrbnd $p |- ( ( A C_ RR /\ A =/= (/) /\ sup ( A , RR* , < ) < +oo ) ->
                    sup ( A , RR* , < ) e. RR ) $=
      ( vy vx cr wss c0 wne cxr clt csup cpnf wbr cv cle wral wrex wcel ressxr
      sstr mpan2 wi supxrcl wa pnfxr xrltne mp3an2 necomd ex syl wceq wn
      supxrunb2 wb ssel2 adantlr rexr ad2antlr xrlenlt con2bid syl11anc
      rexbidva rexnal syl6bb ralbidva bitr3d ralnex necon2abid sylibrd imp
      sylan 3adant2 w3a supxrre suprcl eqeltrd syld3an3 ) ADEZAFGZAHIJZKILZBMZC
      MZNLZBAOZCDPZVSDQVQVTWEVRVQAHEZVTWEVQDHEWFRADHSTWFVTWEWFVTVSKGZWEWFVSHQZV
      TWGUAAUBWHVTWGWHVTUCKVSWHKHQVTKVSGUDVSKUEUFUGUHUIWFWEVSKWFVSKUJZWDUKZCDOZ
      WEUKWFWBWAILZBAPZCDOWIWKCBAULWFWMWJCDWFWBDQZUCZWMWCUKZBAPWJWOWLWPBAWOWAAQ
      ZUCWAHQZWBHQZWLWPUMWFWQWRWNAHWAUNUOWNWSWFWQWBUPUQWRWSUCWCWLWAWBURUSUTVAWC
      BAVBVCVDVEWDCDVFVCVGVHVIVJVKVQVRWEVLVSADIJDCBAVMCBAVNVOVP $.
      $( [22-May-2007] $) $( [19-Jan-2006] $)

    $( The supremum of a nonempty set of reals is greater than minus
       infinity. $)
    supxrgtmnf $p |- ( ( A C_ RR /\ A =/= (/) ) ->
                     -oo < sup ( A , RR* , < ) ) $=
      ( cr wss c0 wne wa cxr clt csup wcel cpnf wceq wo cmnf wbr wn supxrbnd
      3expia con3d wb ressxr sstr mpan2 supxrcl syl adantr nltpnft sylibrd
      orrd mnfltxr ) ABCZADEZFZAGHIZBJZUNKLZMNUNHOUMUOUPUMUOPUNKHOZPZUPUMUQUOUK
      ULUQUOAQRSUMUNGJZUPURTUKUSULUKAGCZUSUKBGCUTUAABGUBUCAUDUEUFUNUGUEUHUIUNUJ
      UE $.
      $( [25-May-2007] $) $( [2-Feb-2006] $)

    $( The supremum of a nonempty set of reals is real iff it is less than plus
       infinity. $)
    supxrre1 $p |- ( ( A C_ RR /\ A =/= (/) ) ->
            ( sup ( A , RR* , < ) e. RR <-> sup ( A , RR* , < ) < +oo ) ) $=
      ( cr wss c0 wne wa cxr clt csup wcel cmnf wbr cpnf wb ressxr sstr mpan2
      supxrcl xrrebnd 3syl adantr supxrgtmnf biantrurd bitr4d ) ABCZADEZFZAGHIZ
      BJZKUHHLZUHMHLZFZUKUEUIULNZUFUEAGCZUHGJUMUEBGCUNOABGPQARUHSTUAUGUJUKAUBUC
      UD $.
      $( [26-May-2007] $) $( [5-Feb-2006] $)

    $( The supremum of a nonempty set of reals is real iff it is not plus
       infinity. $)
    supxrre2 $p |- ( ( A C_ RR /\ A =/= (/) ) ->
            ( sup ( A , RR* , < ) e. RR <-> sup ( A , RR* , < ) =/= +oo ) ) $=
      ( cr wss c0 wne wa cxr clt csup wcel cpnf wbr supxrre1 wb wceq wn ressxr
      sstr mpan2 supxrcl nltpnft 3syl necon2abid adantr bitrd ) ABCZADEZFAGHIZB
      JUHKHLZUHKEZAMUFUIUJNUGUFUIUHKUFAGCZUHGJUHKOUIPNUFBGCUKQABGRSATUHUAUBUCUD
      UE $.
      $( [8-Dec-2007] $) $( [5-Feb-2006] $)

    $( The supremum of a bounded-above set of extended reals is less than
       infinity. $)
    supxrbnd1 $p |- ( A C_ RR* -> ( E. x e. RR A. y e. A y < x <->
                    sup ( A , RR* , < ) < +oo ) ) $=
      ( cxr wss cv clt wbr wral cr wrex csup cpnf wn cle wceq ralnex wcel wa
      wb ssel2 rexr xrlenlt ancoms syl2an an32s rexbidva rexnal syl6rbb
      ralbidva syl5bbr supxrunb1 supxrcl nltpnft syl 3bitrd con4bid ) CDEZBFZAF
      ZGHZBCIZAJKZCDGLZMGHZURVCNZUTUSOHZBCKZAJIZVDMPZVENZVFVBNZAJIURVIVBAJQURVL
      VHAJURUTJRZSZVHVANZBCKVLVNVGVOBCURUSCRZVMVGVOTZURVPSUSDRZUTDRZVQVMCDUSUAU
      TUBVSVRVQUTUSUCUDUEUFUGVABCUHUIUJUKABCULURVDDRVJVKTCUMVDUNUOUPUQ $.
      $( [13-Nov-2006] $) $( [30-Jan-2006] $)

    $( The supremum of a bounded-above set of extended reals is less than
       infinity. $)
    supxrbnd2 $p |- ( A C_ RR* -> ( E. x e. RR A. y e. A y <_ x <->
                    sup ( A , RR* , < ) < +oo ) ) $=
      ( cxr wss cv cle wbr wral cr wrex clt csup cpnf wn wceq ralnex wcel wa
      wb ssel2 rexr xrlenlt con2bid syl2an an32s rexbidva rexnal syl6rbb
      ralbidva syl5bbr supxrunb2 supxrcl nltpnft syl 3bitrd con4bid ) CDEZBFZAF
      ZGHZBCIZAJKZCDLMZNLHZURVCOZUTUSLHZBCKZAJIZVDNPZVEOZVFVBOZAJIURVIVBAJQURVL
      VHAJURUTJRZSZVHVAOZBCKVLVNVGVOBCURUSCRZVMVGVOTZURVPSUSDRZUTDRZVQVMCDUSUAU
      TUBVRVSSVAVGUSUTUCUDUEUFUGVABCUHUIUJUKABCULURVDDRVJVKTCUMVDUNUOUPUQ $.
      $( [26-Jun-2007] $) $( [30-Jan-2006] $)

    $( The supremum of an empty set under the extended reals is minus
       infinity. $)
    xrsup0 $p |- sup ( (/) , RR* , < ) = -oo $=
      ( vy vz c0 cxr wss cmnf wcel cv clt wbr wn wral wrex wi cr csup wceq 0ss
      mnfxr ral0 rexr nltmnf syl pm2.21d rgen supxr mp4an ) CDEFDGFAHZIJKZACLUH
      FIJZUHBHIJBCMZNZAOLCDIPFQDRSUIATULAOUHOGZUJUKUMUHDGUJKUHUAUHUBUCUDUEABCFU
      FUG $.
      $( [15-Oct-2005] $)

    $( A member of a set of extended reals is less than or equal to the set's
       supremum. $)
    supxrub $p |- ( ( A C_ RR* /\ B e. A ) -> B <_ sup ( A , RR* , < ) ) $=
      ( vx vy vz cxr wss wcel wa clt csup cle wbr wn cv wral wrex wi xrsupss
      xrltso supub syl imp wb ssel2 supxrcl adantr xrlenlt syl11anc mpbird ) AF
      GZBAHZIZBAFJKZLMZUNBJMNZUKULUPUKCOZDOZJMNDAPURUQJMUREOJMEAQRDFPICFQULUPRC
      DEASCDEFABJTUAUBUCUMBFHUNFHZUOUPUDAFBUEUKUSULAUFUGBUNUHUIUJ $.
      $( [7-Feb-2006] $)

    $( The supremum of a set of extended reals is less than or equal to an
       upper bound. $)
    supxrleub $p |- ( ( A C_ RR* /\ B e. RR* /\ A. x e. A x <_ B ) ->
                  sup ( A , RR* , < ) <_ B ) $=
      ( vy vz cxr wss wcel cv cle wbr wral clt csup wa wn wb ssel2 xrlenlt
      sylan an32s ralbidva pm5.32da wrex wi xrsupss xrltso supnub syl imp
      supxrcl adantrr mpbird ex sylbid 3impib ) BFGZCFHZAIZCJKZABLZBFMNZCJKZUQU
      RVAOURCUSMKPZABLZOZVCUQURVAVEUQUROUTVDABUQUSBHZURUTVDQZUQVGOUSFHURVHBFUSR
      USCSTUAUBUCUQVFVCUQVFOVCCVBMKPZUQVFVIUQDIZEIZMKPEBLVKVJMKVKUSMKABUDUEEFLO
      DFUDVFVIUEDEABUFDEAFBCMUGUHUIUJUQURVCVIQZVEUQVBFHURVLBUKVBCSTULUMUNUOUP
      $.
      $( [22-Feb-2006] $)
  $}


