$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Superior limit (lim sup)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c limsup $.

  $( Extend class notation to include the limsup function. $)
  clsp $a class limsup $.

  ${
    $d k x y z F $.
$(
    df-limsupOLD $a |- limsup = { <. x , y >. | y = sup ( { z |
                  E. k e. ZZ ( ran ( x |` ( ZZ>= ` k ) ) C_ RR* /\
        z = sup ( ran ( x |` ( ZZ>= ` k ) ) , RR* , < ) ) } , RR* , `' < ) } $.

    df-limsupOLD2 $a |- limsup = { <. x , y >. | y = sup ( { z |
   E. k e. ZZ z = sup ( ( x " ( ZZ>= ` k ) ) , RR* , < ) } , RR* , `' < ) } $.
$)

    $( Define the superior limit of an infinite sequence of extended real
       numbers.  Definition 12-4.1 of [Gleason] p. 175.  See ~ limsupval for
       its value. $)
    df-limsup $a |- limsup = { <. x , y >. | y = sup ( { z | E. k e. ZZ
   z = sup ( ( ( x " ( ZZ>= ` k ) ) i^i RR* ) , RR* , < ) } , RR* , `' < ) } $.

    $( The superior limit of an infinite sequence ` F ` of extended real
       numbers, which is the infimum (indicated by ` ``' < ` ) of the set of
       suprema of all upper infinite subsequences of ` F ` .  Definition 12-4.1
       of [Gleason] p. 175. $)
    limsupval $p |- ( F e. A -> ( limsup ` F ) = sup ( { x | E. k e. ZZ x =
       sup ( ( ( F " ( ZZ>= ` k ) ) i^i RR* ) , RR* , < ) } , RR* , `' < ) ) $=
      ( vy vz wcel clsp cfv cv cuz cima cxr cin clt csup wceq cz wrex cab ccnv
      copab df-limsup fveq1i cvv wor xrltso cnvso mpbi supex imaeq1 ineq1d
      supeq1d eqeq2d rexbidv abbidv fvopabg mpan2 syl5eq ) DBGZDHIDEJAJZFJZCJKI
      ZLZMNZMOPZQZCRSZATZMOUAZPZQFEUBZIZVADVCLZMNZMOPZQZCRSZATZMVJPZDHVLFEACUCU
      DUTVTUEGVMVTQMVSVJMOUFMVJUFUGMOUHUIUJFEDVKVTBUEVBDQZMVIVSVJWAVHVRAWAVGVQC
      RWAVFVPVAWAMVEVOOWAVDVNMVBDVCUKULUMUNUOUPUMUQURUS $.
      $( [26-Oct-2005] $)

    $( Closure of the superior limit. $)
    limsupcl $p |- ( F e. A -> ( limsup ` F ) e. RR* ) $=
      ( vx vk wcel clsp cfv cv cuz cima cxr cin clt csup wceq cz wrex cab ccnv
      limsupval wss inss2 supxrcl ax-mp eleq1 mpbiri r19.23aivw abssi infmxrcl
      syl6eqel ) BAEBFGCHZBDHIGJZKLZKMNZOZDPQZCRZKMSNZKCADBTUQKUAURKEUPCKUOUKKE
      ZDPUOUSUNKEZUMKUAUTULKUBUMUCUDUKUNKUEUFUGUHUQUIUDUJ $.
      $( [11-Jun-2007] $) $( [26-Oct-2005] $)
  $}

