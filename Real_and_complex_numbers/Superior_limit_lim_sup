$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Superior limit (lim sup)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c limsup $.

  $( Extend class notation to include the limsup function. $)
  clsp $a class limsup $.

  ${
    $d k x y z F $.
$(
    df-limsupOLD $a |- limsup = { <. x , y >. | y = sup ( { z |
                  E. k e. ZZ ( ran ( x |` ( ZZ> ` k ) ) (_ RR* /\
         z = sup ( ran ( x |` ( ZZ> ` k ) ) , RR* , < ) ) } , RR* , `' < ) } $.

    df-limsupOLD2 $a |- limsup = { <. x , y >. | y = sup ( { z |
   E. k e. ZZ z = sup ( ( x " ( ZZ> ` k ) ) , RR* , < ) } , RR* , `' < ) } $.
$)

    $( Define the superior limit of an infinite sequence of extended real
       numbers.  Definition 12-4.1 of [Gleason] p. 175.  See ~ limsupvalt for
       its value. $)
    df-limsup $a |- limsup = { <. x , y >. | y = sup ( { z | E. k e. ZZ
    z = sup ( ( ( x " ( ZZ> ` k ) ) i^i RR* ) , RR* , < ) } , RR* , `' < ) } $.

    $( The superior limit of an infinite sequence ` F ` of extended real
       numbers, which is the infimum (indicated by ` ``' < ` ) of the set of
       suprema of all upper infinite subsequences of ` F ` .  Definition
       12-4.1 of [Gleason] p. 175. $)
    limsupvalt $p |- ( F e. A -> ( limsup ` F ) = sup ( { x | E. k e. ZZ x =
        sup ( ( ( F " ( ZZ> ` k ) ) i^i RR* ) , RR* , < ) } , RR* , `' < ) ) $=
      ( vy vz wcel cv cuz cfv cima cxr cin clt csup wceq cz wrex cab ccnv
      copab clsp cvv wor xrltso cnvso mpbi supex imaeq1 ineq1d supeq1 syl
      eqeq2d rexbidv abbidv fvopabg mpan2 df-limsup fveq1i syl5eq ) DBGZDEHAHZF
      HZCHIJZKZLMZLNOZPZCQRZASZLNTZOZPFEUAZJZVBDVDKZLMZLNOZPZCQRZASZLVKOZDUBJVA
      WAUCGVNWAPLVTVKLNUDLVKUDUELNUFUGUHFEDVLWABUCVCDPZVJVTPVLWAPWBVIVSAWBVHVRC
      QWBVGVQVBWBVFVPPVGVQPWBVEVOLVCDVDUIUJLVFVPNUKULUMUNUOLVJVTVKUKULUPUQDUBVM
      FEACURUSUT $.
      $( [30-Oct-2005] $) $( [26-Oct-2005] $)

    $( Closure of the superior limit. $)
    limsupclt $p |- ( F e. A -> ( limsup ` F ) e. RR* ) $=
      ( vx vk wcel clsp cfv cv cuz cima cxr cin clt csup wceq cz wrex cab ccnv
      limsupvalt wss wi inss2 supxrcl ax-mp eleq1 mpbiri a1i r19.23aiv abssi
      infmxrcl syl6eqel ) BAEBFGCHZBDHZIGJZKLZKMNZOZDPQZCRZKMSNZKCADBTUTKUAVAKE
      USCKURUMKEZDPURVBUBUNPEURVBUQKEZUPKUAVCUOKUCUPUDUEUMUQKUFUGUHUIUJUTUKUEUL
      $.
      $( [?] $) $( [26-Oct-2005] $)
  $}

