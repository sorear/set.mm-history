$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Nonnegative integers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of nonnegative integers. $)
  df-n0 $a |- NN0 = ( NN u. { 0 } ) $.

  $( Nonnegative integers expressed in terms of naturals and zero.
     (Contributed by Raph Levien, 10-Dec-2002.) $)
  elnn0 $p |- ( A e. NN0 <-> ( A e. NN \/ A = 0 ) ) $=
    ( cn0 wcel cn cc0 csn cun wceq df-n0 eleq2i elun 0cn elisseti elsnc2 orbi2i
    wo cc 3bitri ) ABCADEFZGZCADCZASCZPUAAEHZPBTAIJADSKUBUCUAAEEQLMNOR $.
    $( [10-Dec-2002] $)

  $( Positive naturals are a subset of nonnegative integers.  (Contributed by
     Raph Levien, 10-Dec-2002.) $)
  nnssnn0 $p |- NN C_ NN0 $=
    ( cn cc0 csn cun cn0 ssun1 df-n0 sseqtr4i ) AABCZDEAIFGH $.
    $( [10-Dec-2002] $)

  $( Nonnegative integers are a subset of the reals.  (Contributed by Raph
     Levien, 10-Dec-2002.) $)
  nn0ssre $p |- NN0 C_ RR $=
    ( cn0 cn cc0 csn cun cr df-n0 nnssre wcel wss 0re snssi ax-mp unssi eqsstri
    ) ABCDZEFGBPFHCFIPFJKCFLMNO $.
    $( [10-Dec-2002] $)

  $( Nonnegative integers are a subset of the complex numbers.) $)
  nn0sscn $p |- NN0 C_ CC $=
    ( cn0 cr cc nn0ssre ax-resscn sstri ) ABCDEF $.
    $( [9-May-2004] $)

  $( The set of nonnegative integers exists. $)
  nn0ex $p |- NN0 e. _V $=
    ( cn0 cc ax-cnex nn0sscn ssexi ) ABCDE $.
    $( [18-Jul-2004] $)

  $( A natural number is a nonnegative integer. $)
  nnnn0 $p |- ( A e. NN -> A e. NN0 ) $=
    ( cn cn0 nnssnn0 sseli ) BCADE $.
    $( [9-May-2004] $)

  ${
    nnnn0.1 $e |- N e. NN $.
    $( A natural number is a nonnegative integer. $)
    nnnn0i $p |- N e. NN0 $=
      ( cn wcel cn0 nnnn0 ax-mp ) ACDAEDBAFG $.
      $( [20-Jun-2005] $)
  $}

  $( A nonnegative integer is a real number. $)
  nn0re $p |- ( A e. NN0 -> A e. RR ) $=
    ( cn0 cr nn0ssre sseli ) BCADE $.
    $( [9-May-2004] $)

  $( A nonnegative integer is a complex number. $)
  nn0cn $p |- ( A e. NN0 -> A e. CC ) $=
    ( cn0 cc nn0sscn sseli ) BCADE $.
    $( [9-May-2004] $)

  ${
    nn0re.1 $e |- A e. NN0 $.
    $( A nonnegative integer is a real number. $)
    nn0rei $p |- A e. RR $=
      ( cn0 cr nn0ssre sselii ) CDAEBF $.
      $( [14-May-2003] $)

    $( A nonnegative integer is a complex number. $)
    nn0cni $p |- A e. CC $=
      ( nn0rei recni ) AABCD $.
      $( [14-May-2003] $)
  $}

  $( The set of natural numbers (positive integers) defined in terms of
     nonnegative integers.  (The proof was shortened by Mario Carneiro,
     13-Feb-2013.) $)
  dfn2 $p |- NN = ( NN0 \ { 0 } ) $=
    ( cn0 cc0 csn cdif cn cun df-n0 difeq1i difun2 wcel wceq 0nnn difsn 3eqtrri
    wn ax-mp ) ABCZDEQFZQDEQDZEARQGHEQIBEJOSEKLBEMPN $.
    $( [13-Feb-2013] $) $( [23-Sep-2007] $)

  $( The set of natural numbers (positive integers) defined in terms of
     nonnegative integers. $)
  dfn2OLD $p |- NN = ( NN0 \ { 0 } ) $=
    ( vx cn cn0 cc0 csn cdif cv wcel wne wa nnnn0 nnne0 jca wo elnn0 biimpi ord
    wceq necon1ad imp impbii eldifsn bitr4i eqriv ) ABCDEFZAGZBHZUFCHZUFDIZJZUF
    UEHUGUJUGUHUIUFKUFLMUHUIUGUHUGUFDUHUGUFDRZUHUGUKNUFOPQSTUAUFCDUBUCUD $.
    $( [23-Sep-2007] $)

  $( 0 is a nonnegative integer.  (Contributed by Raph Levien, 10-Dec-2002.) $)
  0nn0 $p |- 0 e. NN0 $=
    ( cc0 wceq cn0 wcel eqid cn wo elnn0 biimpri olcs ax-mp ) AABZACDZAEAFDZLMM
    NLGAHIJK $.
    $( [21-Mar-2007] $) $( [10-Dec-2002] $)

  $( 1 is a nonnegative integer.  (Contributed by Raph Levien, 10-Dec-2002.) $)
  1nn0 $p |- 1 e. NN0 $=
    ( c1 1nn nnnn0i ) ABC $.
    $( [10-Dec-2002] $)

  $( 2 is a nonnegative integer.  (Contributed by Raph Levien, 10-Dec-2002.) $)
  2nn0 $p |- 2 e. NN0 $=
    ( c2 2nn nnnn0i ) ABC $.
    $( [10-Dec-2002] $)

  $( No number less than zero is a nonnegative integer. $)
  lt0nnn0 $p |- ( ( A e. RR /\ A < 0 ) -> -. A e. NN0 ) $=
    ( cr wcel cc0 clt wbr cn0 wn cn wceq wo elnn0 c1 lt01 wa wi 0re 1re axlttrn
    con2d mp3an23 mpan2i lt1nnn ex syld ltnr breq2 notbid syl5ibcom jaod syl5bi
    imp ) ABCZADEFZAGCZHUMUOUNUOAICZADJZKUMUNHZALUMUPURUQUMUNUPUMUNAMEFZUPHZUMU
    NDMEFZUSNUMDBCMBCUNVAOUSPQRADMSUAUBUMUSUTAUCUDUETUMAAEFZHUQURAUFUQVBUNADAEU
    GUHUIUJUKTUL $.
    $( [9-May-2004] $)

  $( A nonnegative integer is greater than or equal to zero. $)
  nn0ge0 $p |- ( N e. NN0 -> 0 <_ N ) $=
    ( cn0 wcel cc0 cle wbr clt wn cr nn0re lt0nnn0 ex con2d mpcom lenlt sylancr
    wb 0re mpbird ) ABCZDAEFZADGFZHZAICZTUCAJZUDUBTUDUBTHAKLMNTDICUDUAUCQRUEDAO
    PS $.
    $( [9-May-2004] $)

  ${
    nn0ge0.1 $e |- N e. NN0 $.
    $( Nonnegative integers are nonnegative.  (Contributed by Raph Levien,
       10-Dec-2002.) $)
    nn0ge0i $p |- 0 <_ N $=
      ( cn0 wcel cc0 cle wbr nn0ge0 ax-mp ) ACDEAFGBAHI $.
      $( [10-Dec-2002] $)
  $}

  $( A nonnegative integer is less than or equal to zero iff it is equal to
     zero. $)
  nn0le0eq0 $p |- ( N e. NN0 -> ( N <_ 0 <-> N = 0 ) ) $=
    ( cn0 wcel cc0 cle wbr wa nn0ge0 biantrud cr wb nn0re letri3 sylancl bitr4d
    wceq 0re ) ABCZADEFZSDAEFZGZADPZRTSAHIRAJCDJCUBUAKALQADMNO $.
    $( [9-Dec-2005] $)

  $( Closure of addition of nonnegative integers.  (Contributed by Raph Levien,
     10-Dec-2002.) $)
  nn0addcl $p |- ( ( M e. NN0 /\ N e. NN0 ) -> ( M + N ) e. NN0 ) $=
    ( cn0 wcel cn cc0 wceq wo caddc co elnn0 wa nnaddcl nnnn0 syl opreq1 addid2
    cc nncn eqeltrd sylan9eq sylan2 adantl opreq2 addid1 sylan9eqr sylan adantr
    opreq12 0cn addid1i syl6eq 0nn0 syl6eqel ccase syl2anb ) ACDZAEDZAFGZHBEDZB
    FGZHABIJZCDZBCDZAKBKURUTUSVAVCURUTLVBEDVCABMVBNOUSUTLVBBCUTUSBRDZVBBGBSUSVE
    VBFBIJBAFBIPBQUAUBUTVDUSBNUCTURVALVBACURARDZVAVBAGASVAVFVBAFIJABFAIUDAUEUFU
    GURUQVAANUHTUSVALZVBFCVGVBFFIJFAFBFIUIFUJUKULUMUNUOUP $.
    $( [10-Dec-2002] $)

  ${
    nn0addcl.1 $e |- M e. NN0 $.
    nn0addcl.2 $e |- N e. NN0 $.
    $( Closure of addition of nonnegative integers, inference form.
       (Contributed by Raph Levien, 10-Dec-2002.) $)
    nn0addcli $p |- ( M + N ) e. NN0 $=
      ( cn0 wcel caddc co nn0addcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [10-Dec-2002] $)

    $( Closure of multiplication of nonnegative integers, inference form.
       (Contributed by Raph Levien, 10-Dec-2002.) $)
    nn0mulcli $p |- ( M x. N ) e. NN0 $=
      ( cn wcel cc0 wceq wo cmul co cn0 elnn0 wa nnmulcl nn0cni syl6eq syl6eqel
      mpbi 0nn0 nnnn0 syl opreq1 mul02i opreq2 mul01i ccase2 mp2an ) AEFZAGHZIZ
      BEFZBGHZIZABJKZLFZALFUKCAMSBLFUNDBMSUIULUJUMUPUIULNUOEFUPABOUOUAUBUJUOGLU
      JUOGBJKGAGBJUCBBDPUDQTRUMUOGLUMUOAGJKGBGAJUEAACPUFQTRUGUH $.
      $( [10-Dec-2002] $)
  $}

  $( Closure of multiplication of nonnegative integers. $)
  nn0mulcl $p |- ( ( M e. NN0 /\ N e. NN0 ) -> ( M x. N ) e. NN0 ) $=
    ( cn0 wcel cmul co cc0 cif wceq opreq1 eleq1d opreq2 0nn0 nn0mulcli dedth2h
    elimel ) ACDZBCDZABEFZCDQAGHZBEFZCDTRBGHZEFZCDABGGATISUACATBEJKBUBIUAUCCBUB
    TELKTUBAGCMPBGCMPNO $.
    $( [22-Jul-2004] $)

  $( Second Peano postulate for nonnegative integers. $)
  peano2nn0 $p |- ( N e. NN0 -> ( N + 1 ) e. NN0 ) $=
    ( cn0 wcel c1 caddc co 1nn0 nn0addcl mpan2 ) ABCDBCADEFBCGADHI $.
    $( [9-May-2004] $)

  $( A natural number plus a nonnegative integer is a natural number. $)
  nnnn0addcl $p |- ( ( M e. NN /\ N e. NN0 ) -> ( M + N ) e. NN ) $=
    ( cn wcel cn0 caddc co cc0 wceq wo elnn0 nnaddcl ex cc nncn addid1 syl ibir
    eleq1d opreq2 syl5ibrcom jaod syl5bi imp ) ACDZBEDZABFGZCDZUFBCDZBHIZJUEUHB
    KUEUIUHUJUEUIUHABLMUEUHUJAHFGZCDZUEULUEUKACUEANDUKAIAOAPQSRUJUGUKCBHAFTSUAU
    BUCUD $.
    $( [20-Apr-2005] $)

  $( A nonnegative integer plus a natural number is a natural number. $)
  nn0nnaddcl $p |- ( ( M e. NN0 /\ N e. NN ) -> ( M + N ) e. NN ) $=
    ( cn wcel cn0 caddc co wa wceq nncn nn0cn addcom syl2an nnnn0addcl eqeltrrd
    cc ancoms ) BCDZAEDZABFGZCDRSHBAFGZTCRBPDAPDUATISBJAKBALMBANOQ $.
    $( [22-Dec-2005] $)

  $( Nonnegative integer ordering relation.  (Contributed by Raph Levien,
     10-Dec-2002.) $)
  nn0ltp1le $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                ( M < N <-> ( M + 1 ) <_ N ) ) $=
    ( cn0 wcel cn cc0 wceq wo clt wbr c1 caddc cle wb wa wi bitr4d wn cr 0re co
    elnn0 nnltp1le breq1 nngt0 ax-1cn addid2i syl5eqbr pm5.74ri sylan9bb opreq1
    nnge1 2th breq1d adantr breq2 nnre ltnsym sylancl mt2d peano2re syl axlttrn
    jca ltp1 mp3an1 sylc ltnle mpan 3syl mpbid pm5.21 syl11anc sylan9bbr adantl
    ltnri ltp1i 1re readdcli ltnlei mpbi 2false syl6bb ccase syl2anb ) ACDAEDZA
    FGZHBEDZBFGZHABIJZAKLUAZBMJZNZBCDAUBBUBWFWHWGWIWMABUCWGWHOWJFKLUAZBMJZWLWGW
    JFBIJZWHWOAFBIUDWHWPWOWHWPPWHWOPBUEWHWNKBMKUFUGBULUHUMUIUJWGWLWONWHWGWKWNBM
    AFKLUKZUNUOQWFWIOWJWKFMJZWLWIWJAFIJZWFWRBFAIUPZWFWSRWRRZWSWRNWFWSFAIJZAUEZW
    FASDZFSDZWSXBRPAUQZTAFURUSUTWFFWKIJZXAWFXDWKSDZOXBAWKIJZOZXGWFXDXHXFWFXDXHX
    FAVAZVBVDWFXBXIXCWFXDXIXFAVEVBVDXEXDXHXJXGPTFAWKVCVFVGWFXDXHXGXANZXFXKXEXHX
    LTFWKVHVIVJVKWSWRVLVMVNWIWLWRNZWFBFWKMUPZVOQWGWIOWJWRWLWIWJWSWGWRWTWGWSWNFM
    JZWRWGWSFFIJZXOAFFIUDXPXOFTVPFWNIJXORFTVQFWNTFKTVRVSVTWAWBWCWGWKWNFMWQUNQVN
    WIXMWGXNVOQWDWE $.
    $( [10-Dec-2002] $)

  $( Nonnegative integer ordering relation.  (Contributed by Raph Levien,
     10-Apr-2004.) $)
  nn0leltp1 $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                ( M <_ N <-> M < ( N + 1 ) ) ) $=
    ( cn0 wa cle wbr c1 caddc co clt cr wb nn0re leadd1 mp3an3 syl2an peano2nn0
    wcel 1re nn0ltp1le sylan2 bitr4d ) ACRZBCRZDABEFZAGHIBGHIZEFZAUFJFZUCAKRZBK
    RZUEUGLZUDAMBMUIUJGKRUKSABGNOPUDUCUFCRUHUGLBQAUFTUAUB $.
    $( [10-Apr-2004] $)

  $( Nonnegative integer ordering relation. $)
  nn0ltlem1 $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                ( M < N <-> M <_ ( N - 1 ) ) ) $=
    ( cn0 wcel wa c1 caddc co cmin cle wbr clt wb cc wceq nn0cn ax-1cn npcan cr
    nn0re sylancl breq2d adantl peano2rem syl 1re leadd1 mp3an3 syl2an 3bitr4rd
    nn0ltp1le ) ACDZBCDZEAFGHZBFIHZFGHZJKZUNBJKZAUOJKZABLKUMUQURMULUMUPBUNJUMBN
    DFNDUPBOBPQBFRUAUBUCULASDZUOSDZUSUQMZUMATUMBSDVABTBUDUEUTVAFSDVBUFAUOFUGUHU
    IABUKUJ $.
    $( [10-May-2004] $)

  $( A number is less than or equal to itself plus a nonnegative integer. $)
  nn0addge1 $p |- ( ( A e. RR /\ N e. NN0 ) -> A <_ ( A + N ) ) $=
    ( cn0 wcel cr cc0 cle wbr wa caddc co nn0re nn0ge0 jca addge01 3expb sylan2
    biimp3a ) BCDZAEDZBEDZFBGHZIAABJKGHZSUAUBBLBMNTUAUBUCTUAUBUCABORPQ $.
    $( [25-Jul-2006] $) $( [10-Mar-2005] $)

  $( A number is less than or equal to itself plus a nonnegative integer. $)
  nn0addge2 $p |- ( ( A e. RR /\ N e. NN0 ) -> A <_ ( N + A ) ) $=
    ( cn0 wcel cr cc0 cle wbr wa caddc co nn0re nn0ge0 jca addge02 3expb sylan2
    biimp3a ) BCDZAEDZBEDZFBGHZIABAJKGHZSUAUBBLBMNTUAUBUCTUAUBUCABORPQ $.
    $( [25-Jul-2006] $) $( [10-Mar-2005] $)

  ${
    nn0addge1.1 $e |- A e. RR $.
    nn0addge1.2 $e |- N e. NN0 $.
    $( A number is less than or equal to itself plus a nonnegative integer. $)
    nn0addge1i $p |- A <_ ( A + N ) $=
      ( cr wcel cn0 caddc co cle wbr nn0addge1 mp2an ) AEFBGFAABHIJKCDABLM $.
      $( [10-Mar-2005] $)

    $( A number is less than or equal to itself plus a nonnegative integer. $)
    nn0addge2i $p |- A <_ ( N + A ) $=
      ( cr wcel cn0 caddc co cle wbr nn0addge2 mp2an ) AEFBGFABAHIJKCDABLM $.
      $( [10-Mar-2005] $)
  $}

  ${
    nn0le2x.1 $e |- N e. NN0 $.
    $( A nonnegative integer is less than or equal to twice itself.
       (Contributed by Raph Levien, 10-Dec-2002.) $)
    nn0le2xi $p |- N <_ ( 2 x. N ) $=
      ( caddc co c2 cmul cle nn0rei nn0addge1i nn0cni 2timesi breqtrri ) AAACDE
      AFDGAAABHBIAABJKL $.
      $( [10-Dec-2002] $)
  $}

  ${
    nn0lele2x.1 $e |- M e. NN0 $.
    nn0lele2x.2 $e |- N e. NN0 $.
    $( 'Less than or equal to' implies 'less than or equal to twice' for
       nonnegative integers.  (Contributed by Raph Levien, 10-Dec-2002.) $)
    nn0lele2xi $p |- ( N <_ M -> N <_ ( 2 x. M ) ) $=
      ( cle wbr c2 cmul co nn0le2xi nn0rei 2re remulcli letri mpan2 ) BAEFAGAHI
      ZEFBPEFACJBAPBDKACKZGALQMNO $.
      $( [10-Dec-2002] $)
  $}

