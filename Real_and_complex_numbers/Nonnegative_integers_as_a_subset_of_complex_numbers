$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Nonnegative integers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of nonnegative integers. $)
  df-n0 $a |- NN0 = ( NN u. { 0 } ) $.

  $( Nonnegative integers expressed in terms of naturals and zero.
     (Contributed by Raph Levien, 10-Dec-2002.) $)
  elnn0 $p |- ( A e. NN0 <-> ( A e. NN \/ A = 0 ) ) $=
    ( cn0 wcel cn cc0 csn cun wo wceq df-n0 eleq2i elun 0cn elexi elsnc2 orbi2i
    cc 3bitri ) ABCADEFZGZCADCZASCZHUAAEIZHBTAJKADSLUBUCUAAEEQMNOPR $.
    $( [10-Dec-2002] $)

  $( Positive naturals are a subset of nonnegative integers.  (Contributed by
     Raph Levien, 10-Dec-2002.) $)
  nnssnn0 $p |- NN C_ NN0 $=
    ( cn cc0 csn cun cn0 ssun1 df-n0 sseqtr4i ) AABCZDEAIFGH $.
    $( [10-Dec-2002] $)

  $( Nonnegative integers are a subset of the reals.  (Contributed by Raph
     Levien, 10-Dec-2002.) $)
  nn0ssre $p |- NN0 C_ RR $=
    ( cn0 cn cc0 csn cun cr df-n0 nnssre wcel wss 0re snssi ax-mp unssi eqsstri
    ) ABCDZEFGBPFHCFIPFJKCFLMNO $.
    $( [10-Dec-2002] $)

  $( Nonnegative integers are a subset of the complex numbers.) $)
  nn0sscn $p |- NN0 C_ CC $=
    ( cn0 cr cc nn0ssre ax-resscn sstri ) ABCDEF $.
    $( [9-May-2004] $)

  $( The set of nonnegative integers exists. $)
  nn0ex $p |- NN0 e. _V $=
    ( cn0 cn cc0 csn cun cvv df-n0 nnex snex unex eqeltri ) ABCDZEFGBLHCIJK $.
    $( [18-Jul-2004] $)

  $( A natural number is a nonnegative integer. $)
  nnnn0 $p |- ( A e. NN -> A e. NN0 ) $=
    ( cn cn0 nnssnn0 sseli ) BCADE $.
    $( [9-May-2004] $)

  ${
    nnnn0.1 $e |- N e. NN $.
    $( A natural number is a nonnegative integer. $)
    nnnn0i $p |- N e. NN0 $=
      ( cn wcel cn0 nnnn0 ax-mp ) ACDAEDBAFG $.
      $( [20-Jun-2005] $)
  $}

  $( A nonnegative integer is a real number. $)
  nn0re $p |- ( A e. NN0 -> A e. RR ) $=
    ( cn0 cr nn0ssre sseli ) BCADE $.
    $( [9-May-2004] $)

  $( A nonnegative integer is a complex number. $)
  nn0cn $p |- ( A e. NN0 -> A e. CC ) $=
    ( cn0 cc nn0sscn sseli ) BCADE $.
    $( [9-May-2004] $)

  ${
    nn0re.1 $e |- A e. NN0 $.
    $( A nonnegative integer is a real number. $)
    nn0rei $p |- A e. RR $=
      ( cn0 cr nn0ssre sselii ) CDAEBF $.
      $( [14-May-2003] $)

    $( A nonnegative integer is a complex number. $)
    nn0cni $p |- A e. CC $=
      ( nn0rei recni ) AABCD $.
      $( [14-May-2003] $)
  $}

  $( The set of natural numbers (positive integers) defined in terms of
     nonnegative integers.  (The proof was shortened by Mario Carneiro,
     13-Feb-2013.) $)
  dfn2 $p |- NN = ( NN0 \ { 0 } ) $=
    ( cn0 cc0 csn cdif cn cun df-n0 difeq1i difun2 wcel wceq 0nnn difsn 3eqtrri
    wn ax-mp ) ABCZDEQFZQDEQDZEARQGHEQIBEJOSEKLBEMPN $.
    $( [13-Feb-2013] $) $( [23-Sep-2007] $)

  $( The set of natural numbers (positive integers) defined in terms of
     nonnegative integers. $)
  dfn2OLD $p |- NN = ( NN0 \ { 0 } ) $=
    ( vx cn cn0 cc0 csn cdif cv wcel wne wa nnnn0 nnne0 jca wo elnn0 biimpi ord
    wceq necon1ad imp impbii eldifsn bitr4i eqriv ) ABCDEFZAGZBHZUFCHZUFDIZJZUF
    UEHUGUJUGUHUIUFKUFLMUHUIUGUHUGUFDUHUGUFDRZUHUGUKNUFOPQSTUAUFCDUBUCUD $.
    $( [23-Sep-2007] $)

  $( 0 is a nonnegative integer.  (Contributed by Raph Levien, 10-Dec-2002.) $)
  0nn0 $p |- 0 e. NN0 $=
    ( cc0 wceq cn0 wcel eqid cn wo elnn0 biimpri olcs ax-mp ) AABZACDZAEAFDZLMM
    NLGAHIJK $.
    $( [21-Mar-2007] $) $( [10-Dec-2002] $)

  $( 1 is a nonnegative integer.  (Contributed by Raph Levien, 10-Dec-2002.) $)
  1nn0 $p |- 1 e. NN0 $=
    ( c1 1nn nnnn0i ) ABC $.
    $( [10-Dec-2002] $)

  $( 2 is a nonnegative integer.  (Contributed by Raph Levien, 10-Dec-2002.) $)
  2nn0 $p |- 2 e. NN0 $=
    ( c2 2nn nnnn0i ) ABC $.
    $( [10-Dec-2002] $)
  $( 3 is a nonnegative integer.  (Contributed by Mario Carneiro,
     18-Feb-2014.) $)
  3nn0 $p |- 3 e. NN0 $=
    ( c3 3nn nnnn0i ) ABC $.
    $( [18-Feb-2014] $)

  $( 4 is a nonnegative integer.  (Contributed by Mario Carneiro,
     18-Feb-2014.) $)
  4nn0 $p |- 4 e. NN0 $=
    ( c4 4nn nnnn0i ) ABC $.
    $( [18-Feb-2014] $)

  $( A nonnegative integer is greater than or equal to zero. $)
  nn0ge0 $p |- ( N e. NN0 -> 0 <_ N ) $=
    ( cn0 wcel cc0 cle wbr wceq wo cn elnn0 nngt0 id eqcomd orim12i sylbi cr wb
    clt 0re nn0re leloe sylancr mpbird ) ABCZDAEFZDARFZDAGZHZUDAICZADGZHUHAJUIU
    FUJUGAKUJADUJLMNOUDDPCAPCUEUHQSATDAUAUBUC $.
    $( [16-May-2014] $) $( [9-May-2004] $)

  $( No number less than zero is a nonnegative integer.  (The proof was
     shortened by Mario Carneiro, 16-May-2014.) $)
  lt0nnn0 $p |- ( A < 0 -> -. A e. NN0 ) $=
    ( cn0 wcel cc0 clt wbr cle wn nn0ge0 cr 0re nn0re lenlt sylancr mpbid con2i
    wb ) ABCZADEFZRDAGFZSHZAIRDJCAJCTUAQKALDAMNOP $.
    $( [16-May-2014] $) $( [9-May-2004] $)

  ${
    nn0ge0.1 $e |- N e. NN0 $.
    $( Nonnegative integers are nonnegative.  (Contributed by Raph Levien,
       10-Dec-2002.) $)
    nn0ge0i $p |- 0 <_ N $=
      ( cn0 wcel cc0 cle wbr nn0ge0 ax-mp ) ACDEAFGBAHI $.
      $( [10-Dec-2002] $)
  $}

  $( A nonnegative integer is less than or equal to zero iff it is equal to
     zero. $)
  nn0le0eq0 $p |- ( N e. NN0 -> ( N <_ 0 <-> N = 0 ) ) $=
    ( cn0 wcel cc0 cle wbr wa nn0ge0 biantrud cr wb nn0re letri3 sylancl bitr4d
    wceq 0re ) ABCZADEFZSDAEFZGZADPZRTSAHIRAJCDJCUBUAKALQADMNO $.
    $( [9-Dec-2005] $)

  $( A natural number plus a nonnegative integer is a natural number.  (The
     proof was shortened by Mario Carneiro, 16-May-2014.) $)
  nnnn0addcl $p |- ( ( M e. NN /\ N e. NN0 ) -> ( M + N ) e. NN ) $=
    ( cn0 wcel cn cc0 wceq wo caddc co elnn0 nnaddcl wa opreq2 addid1 sylan9eqr
    cc nncn syl simpl eqeltrd jaodan sylan2b ) BCDAEDZBEDZBFGZHABIJZEDZBKUDUEUH
    UFABLUDUFMUGAEUFUDUGAFIJZABFAINUDAQDUIAGARAOSPUDUFTUAUBUC $.
    $( [20-Apr-2005] $)

  $( A nonnegative integer plus a natural number is a natural number. $)
  nn0nnaddcl $p |- ( ( M e. NN0 /\ N e. NN ) -> ( M + N ) e. NN ) $=
    ( cn wcel cn0 caddc co wa wceq nncn nn0cn addcom syl2an nnnn0addcl eqeltrrd
    cc ancoms ) BCDZAEDZABFGZCDRSHBAFGZTCRBPDAPDUATISBJAKBALMBANOQ $.
    $( [22-Dec-2005] $)

  $( Closure of addition of nonnegative integers.  (Contributed by Raph Levien,
     10-Dec-2002.)  (The proof was shortened by Mario Carneiro,
     16-May-2014.) $)
  nn0addcl $p |- ( ( M e. NN0 /\ N e. NN0 ) -> ( M + N ) e. NN0 ) $=
    ( cn0 wcel cn cc0 wo caddc co elnn0 wa nn0nnaddcl nnnn0 syl opreq2 cc nn0cn
    wceq addid1 sylan9eqr simpl eqeltrd jaodan sylan2b ) BCDACDZBEDZBFRZGABHIZC
    DZBJUEUFUIUGUEUFKUHEDUIABLUHMNUEUGKUHACUGUEUHAFHIZABFAHOUEAPDUJARAQASNTUEUG
    UAUBUCUD $.
    $( [10-Dec-2002] $)

  $( Closure of multiplication of nonnegative integers.  (Revised by Mario
     Carneiro, 16-May-2014.) $)
  nn0mulcl $p |- ( ( M e. NN0 /\ N e. NN0 ) -> ( M x. N ) e. NN0 ) $=
    ( cn0 wcel cn cc0 wceq wo cmul co elnn0 wi wa nnmulcl nnnn0 syl ex syl6eqel
    cc 0nn0 nncn mul02 opreq1 eleq1d syl5ibr jaoi imp sylanb opreq2 nn0cn mul01
    sylan9eqr jaodan sylan2b ) BCDACDZBEDZBFGZHABIJZCDZBKUOUPUSUQUOAEDZAFGZHZUP
    USAKVBUPUSUTUPUSLVAUTUPUSUTUPMUREDUSABNUROPQUPUSVAFBIJZCDUPVCFCUPBSDVCFGBUA
    BUBPTRVAURVCCAFBIUCUDUEUFUGUHUOUQMURFCUQUOURAFIJZFBFAIUIUOASDVDFGAUJAUKPULT
    RUMUN $.
    $( [22-Jul-2004] $)

  ${
    nn0addcl.1 $e |- M e. NN0 $.
    nn0addcl.2 $e |- N e. NN0 $.
    $( Closure of addition of nonnegative integers, inference form.
       (Contributed by Raph Levien, 10-Dec-2002.) $)
    nn0addcli $p |- ( M + N ) e. NN0 $=
      ( cn0 wcel caddc co nn0addcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [10-Dec-2002] $)

    $( Closure of multiplication of nonnegative integers, inference form.
       (Contributed by Raph Levien, 10-Dec-2002.) $)
    nn0mulcli $p |- ( M x. N ) e. NN0 $=
      ( cn0 wcel cmul co nn0mulcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [10-Dec-2002] $)
  $}

  $( A nonnegative integer plus 1 is a natural number.  (Contributed by Raph
     Levien, 30-Jun-2006.)  (Revised by Mario Carneiro, 16-May-2014.) $)
  nn0p1nn $p |- ( N e. NN0 -> ( N + 1 ) e. NN ) $=
    ( cn0 wcel c1 cn caddc co 1nn nn0nnaddcl mpan2 ) ABCDECADFGECHADIJ $.
    $( [16-May-2014] $) $( [30-Jun-2006] $)

  $( Second Peano postulate for nonnegative integers. $)
  peano2nn0 $p |- ( N e. NN0 -> ( N + 1 ) e. NN0 ) $=
    ( cn0 wcel c1 caddc co 1nn0 nn0addcl mpan2 ) ABCDBCADEFBCGADHI $.
    $( [9-May-2004] $)

  $( A natural number minus 1 is a nonnegative integer.  (Contributed by Jason
     Orendorff, 24-Jan-2007.) $)
  nnm1nn0 $p |- ( N e. NN -> ( N - 1 ) e. NN0 ) $=
    ( cn wcel c1 cmin co cc0 wceq wo nn1m1nn opreq1 ax-1cn subidi syl6eq orim1i
    cn0 syl orcomd elnn0 sylibr ) ABCZADEFZBCZUBGHZIUBPCUAUDUCUAADHZUCIUDUCIAJU
    EUDUCUEUBDDEFGADDEKDLMNOQRUBST $.
    $( [16-May-2014] $) $( [24-Jan-2007] $)

  $( The nonnegative integer property expressed in terms of natural numbers.
     (The proof was shortened by Mario Carneiro, 16-May-2014.) $)
  elnn0nn $p |- ( N e. NN0 <-> ( N e. CC /\ ( N + 1 ) e. NN ) ) $=
    ( cn0 wcel cc c1 caddc co cn nn0cn nn0p1nn jca cmin wceq simpl ax-1cn pncan
    wa sylancl nnm1nn0 adantl eqeltrrd impbii ) ABCZADCZAEFGZHCZQZUCUDUFAIAJKUG
    UEELGZABUGUDEDCUHAMUDUFNOAEPRUFUHBCUDUESTUAUB $.
    $( [16-May-2014] $) $( [10-May-2004] $)

  $( The natural number property expressed in terms of nonnegative integers. $)
  elnnnn0 $p |- ( N e. NN <-> ( N e. CC /\ ( N - 1 ) e. NN0 ) ) $=
    ( cn wcel cc c1 cmin co cn0 nncn caddc wceq ax-1cn npcan mpan2 eleq1d subcl
    wa biantrurd bitr3d elnn0nn syl6bbr biadan2 ) ABCZADCZAEFGZHCZAIUDUCUEDCZUE
    EJGZBCZQZUFUDUIUCUJUDUHABUDEDCZUHAKLAEMNOUDUGUIUDUKUGLAEPNRSUETUAUB $.
    $( [10-May-2004] $)

  $( The natural number property expressed in terms of nonnegative integers. $)
  elnnnn0b $p |- ( N e. NN <-> ( N e. NN0 /\ 0 < N ) ) $=
    ( cn wcel cn0 cc0 clt wbr wa nnnn0 nngt0 jca wceq wo elnn0 ax-1 breq2 ltnri
    wi 0re pm2.21i syl6bi jaoi sylbi imp impbii ) ABCZADCZEAFGZHUFUGUHAIAJKUGUH
    UFUGUFAELZMUHUFRZANUFUJUIUFUHOUIUHEEFGZUFAEEFPUKUFESQTUAUBUCUDUE $.
    $( [1-Sep-2005] $)


  $( The natural number property expressed in terms of nonnegative integers. $)
  elnnnn0c $p |- ( N e. NN <-> ( N e. NN0 /\ 1 <_ N ) ) $=
    ( cn wcel cn0 c1 cle wbr wa nnnn0 nnge1 jca cc0 clt simpl lt01 cr nn0re 0re
    wi 1re ltletr mp3an12 syl mpani imp elnnnn0b sylibr impbii ) ABCZADCZEAFGZH
    ZUIUJUKAIAJKULUJLAMGZHUIULUJUMUJUKNUJUKUMUJLEMGZUKUMOUJAPCZUNUKHUMSZAQLPCEP
    CUOUPRTLEAUAUBUCUDUEKAUFUGUH $.
    $( [10-Jan-2006] $)

  $( A number is less than or equal to itself plus a nonnegative integer. $)
  nn0addge1 $p |- ( ( A e. RR /\ N e. NN0 ) -> A <_ ( A + N ) ) $=
    ( cn0 wcel cr cc0 cle wbr wa caddc co nn0re nn0ge0 jca addge01 3expb sylan2
    biimp3a ) BCDZAEDZBEDZFBGHZIAABJKGHZSUAUBBLBMNTUAUBUCTUAUBUCABORPQ $.
    $( [25-Jul-2006] $) $( [10-Mar-2005] $)

  $( A number is less than or equal to itself plus a nonnegative integer. $)
  nn0addge2 $p |- ( ( A e. RR /\ N e. NN0 ) -> A <_ ( N + A ) ) $=
    ( cn0 wcel cr cc0 cle wbr wa caddc co nn0re nn0ge0 jca addge02 3expb sylan2
    biimp3a ) BCDZAEDZBEDZFBGHZIABAJKGHZSUAUBBLBMNTUAUBUCTUAUBUCABORPQ $.
    $( [25-Jul-2006] $) $( [10-Mar-2005] $)

  ${
    nn0addge1.1 $e |- A e. RR $.
    nn0addge1.2 $e |- N e. NN0 $.
    $( A number is less than or equal to itself plus a nonnegative integer. $)
    nn0addge1i $p |- A <_ ( A + N ) $=
      ( cr wcel cn0 caddc co cle wbr nn0addge1 mp2an ) AEFBGFAABHIJKCDABLM $.
      $( [10-Mar-2005] $)

    $( A number is less than or equal to itself plus a nonnegative integer. $)
    nn0addge2i $p |- A <_ ( N + A ) $=
      ( cr wcel cn0 caddc co cle wbr nn0addge2 mp2an ) AEFBGFABAHIJKCDABLM $.
      $( [10-Mar-2005] $)
  $}

  $( Subtraction of nonnegative integers.  (The proof was shortened by Mario
     Carneiro, 16-May-2014.) $)
  nn0sub $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                ( M <_ N <-> ( N - M ) e. NN0 ) ) $=
    ( cn0 wcel wbr cmin co cn cc0 wceq wo clt cr wb nn0re elnn0 nngt0 cc eleq1d
    bibi12d wa cle leloe syl2an wi nnsub ex nncn subid1 syl eqeltrd 2thd opreq2
    id breq1 syl5ibr jaoi sylbi lt0nnn0 con2i pm2.21d 0re posdif sylancl impbid
    breq2 opreq1 syl5ibrcom jaod syl5bi imp recnd syl2anr eqcom syl6rbb orbi12d
    subeq0 bitrd syl6bbr ) ACDZBCDZUAZABUBEZBAFGZHDZWDIJZKZWDCDWBWCABLEZABJZKZW
    GVTAMDZBMDWCWJNWAAOZBOZABUCUDWBWHWEWIWFVTWAWHWENZWABHDZBIJZKVTWNBPVTWOWNWPV
    TAHDZAIJZKWOWNUEZAPWQWSWRWQWOWNABUFUGWOWNWRIBLEZBIFGZHDZNWOWTXBBQWOXABHWOBR
    DZXABJBUHBUIUJWOUNUKULWRWHWTWEXBAIBLUOWRWDXAHAIBFUMSTUPUQURVTWNWPAILEZIAFGZ
    HDZNVTXDXFVTXDXFXDVTAUSUTVAXFXDVTIXELEZXEQVTWKIMDXDXGNWLVBAIVCVDUPVEWPWHXDW
    EXFBIALVFWPWDXEHBIAFVGSTVHVIVJVKWBWFBAJZWIWAXCARDWFXHNVTWABWMVLVTAWLVLBAVQV
    MBAVNVOVPVRWDPVS $.
    $( [16-May-2014] $) $( [9-May-2004] $)

  ${
    nn0le2x.1 $e |- N e. NN0 $.
    $( A nonnegative integer is less than or equal to twice itself.
       (Contributed by Raph Levien, 10-Dec-2002.) $)
    nn0le2xi $p |- N <_ ( 2 x. N ) $=
      ( caddc co c2 cmul cle nn0rei nn0addge1i nn0cni 2timesi breqtrri ) AAACDE
      AFDGAAABHBIAABJKL $.
      $( [10-Dec-2002] $)
  $}

  ${
    nn0lele2x.1 $e |- M e. NN0 $.
    nn0lele2x.2 $e |- N e. NN0 $.
    $( 'Less than or equal to' implies 'less than or equal to twice' for
       nonnegative integers.  (Contributed by Raph Levien, 10-Dec-2002.) $)
    nn0lele2xi $p |- ( N <_ M -> N <_ ( 2 x. M ) ) $=
      ( cle wbr c2 cmul co nn0le2xi nn0rei 2re remulcli letri mpan2 ) BAEFAGAHI
      ZEFBPEFACJBAPBDKACKZGALQMNO $.
      $( [10-Dec-2002] $)
  $}

