$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Nonnegative integers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of nonnegative integers. $)
  df-n0 $a |- NN0 = ( NN u. { 0 } ) $.

  $( Nonnegative integers expressed in terms of naturals and zero.
     (Contributed by Raph Levien, 10-Dec-02.) $)
  elnn0 $p |- ( A e. NN0 <-> ( A e. NN \/ A = 0 ) ) $=
    ( cn0 wcel cn cc0 csn cun wo wceq df-n0 eleq2i elun cc 0cn elisseti elsnc2
    orbi2i 3bitr ) ABCADEFZGZCADCZASCZHUAAEIZHBTAJKADSLUBUCUAAEEMNOPQR $.
    $( [14-May-2003] $)

  $( Positive naturals are a subset of nonnegative integers.  (Contributed
     by Raph Levien, 10-Dec-02.) $)
  nnssnn0 $p |- NN (_ NN0 $=
    ( cn cc0 csn cun cn0 ssun1 df-n0 sseqtr4 ) AABCZDEAIFGH $.
    $( [14-May-2003] $)

  $( Nonnegative integers are a subset of the reals.  (Contributed by Raph
     Levien, 10-Dec-02.) $)
  nn0ssre $p |- NN0 (_ RR $=
    ( cn0 cn cc0 csn cun cr df-n0 nnssre wcel wss 0re snssi ax-mp unssi eqsstr
    ) ABCDZEFGBPFHCFIPFJKCFLMNO $.
    $( [14-May-2003] $)

  $( Nonnegative integers are a subset of the complex numbers.) $)
  nn0sscn $p |- NN0 (_ CC $=
    ( cn0 cr cc nn0ssre axresscn sstri ) ABCDEF $.
    $( [25-Jun-2004] $) $( [9-May-2004] $)

  $( The set of nonnegative integers exists. $)
  nn0ex $p |- NN0 e. V $=
    ( cn0 cc axcnex nn0sscn ssexi ) ABCDE $.
    $( [24-Jul-2004] $) $( [18-Jul-2004] $)

  $( A natural number is a nonnegative integer. $)
  nnnn0t $p |- ( A e. NN -> A e. NN0 ) $=
    ( cn cn0 nnssnn0 sseli ) BCADE $.
    $( [10-May-2004] $) $( [9-May-2004] $)

  ${
    nnnn0.1 $e |- N e. NN $.
    $( A natural number is a nonnegative integer. $)
    nnnn0 $p |- N e. NN0 $=
      ( cn wcel cn0 nnnn0t ax-mp ) ACDAEDBAFG $.
      $( [21-Jun-2005] $) $( [20-Jun-2005] $)
  $}

  $( A nonnegative integer is a real number. $)
  nn0ret $p |- ( A e. NN0 -> A e. RR ) $=
    ( cn0 cr nn0ssre sseli ) BCADE $.
    $( [25-May-2004] $) $( [9-May-2004] $)

  $( A nonnegative integer is a complex number. $)
  nn0cnt $p |- ( A e. NN0 -> A e. CC ) $=
    ( cn0 cc nn0sscn sseli ) BCADE $.
    $( [3-Jul-2004] $) $( [9-May-2004] $)

  ${
    nn0re.1 $e |- A e. NN0 $.
    $( A nonnegative integer is a real number. $)
    nn0re $p |- A e. RR $=
      ( cn0 cr nn0ssre sselii ) CDAEBF $.
      $( [14-May-2003] $)

    $( A nonnegative integer is a complex number. $)
    nn0cn $p |- A e. CC $=
      ( nn0re recn ) AABCD $.
      $( [14-May-2003] $)
  $}

  $( 0 is a nonnegative integer.  (Contributed by Raph Levien, 10-Dec-02.)
     (Revised by Paul Chapman, 21-Mar-2007.) $)
  0nn0 $p |- 0 e. NN0 $=
    ( cc0 wceq cn0 wcel eqid cn wo elnn0 biimpr olci ax-mp ) AABZACDZAEAFDZLMMN
    LGAHIJK $.
    $( [21-Mar-2007] $) $( [14-May-2003] $)

  $( 1 is a nonnegative integer.  (Contributed by Raph Levien, 10-Dec-02.) $)
  1nn0 $p |- 1 e. NN0 $=
    ( c1 1nn nnnn0 ) ABC $.
    $( [14-May-2003] $)

  $( 2 is a nonnegative integer.  (Contributed by Raph Levien, 10-Dec-02.) $)
  2nn0 $p |- 2 e. NN0 $=
    ( c2 2nn nnnn0 ) ABC $.
    $( [14-May-2003] $)

  $( No number less than zero is a nonnegative integer. $)
  lt0nnn0 $p |- ( ( A e. RR /\ A < 0 ) -> -. A e. NN0 ) $=
    ( cr wcel cc0 clt wbr cn0 wn cn wceq wo c1 lt01 wa wi 0re 1re axlttrn
    mp3an23 mpan2i lt1nnn ex syld con2d breq2 negbid ltnrt syl5bi com12 jaod
    elnn0 syl5ib imp ) ABCZADEFZAGCZHUNUPUOUNAICZADJZKUOHZUPUNUQUSURUNUOUQUNUOA
    LEFZUQHZUNUODLEFZUTMUNDBCLBCUOVBNUTOPQADLRSTUNUTVAAUAUBUCUDURUNUSURAAEFZHUS
    UNURVCUOADAEUEUFAUGUHUIUJAUKULUDUM $.
    $( [27-Jun-2004] $) $( [9-May-2004] $)

  $( A nonnegative integer is greater than or equal to zero. $)
  nn0ge0t $p |- ( N e. NN0 -> 0 <_ N ) $=
    ( cn0 wcel cc0 cle wbr clt wn cr nn0ret lt0nnn0 ex con2d mpcom wb 0re
    lenltt sylan2 mpan mpbird ) ABCZDAEFZADGFZHZAICZUAUDAJZUEUCUAUEUCUAHAKLMNDI
    CZUAUBUDOZPUGUEUHUADAQUFRST $.
    $( [3-Jul-2004] $) $( [9-May-2004] $)

  ${
    nn0ge0.1 $e |- N e. NN0 $.
    $( Nonnegative integers are nonnegative.  (Contributed by Raph Levien,
       10-Dec-02.) $)
    nn0ge0 $p |- 0 <_ N $=
      ( cn0 wcel cc0 cle wbr nn0ge0t ax-mp ) ACDEAFGBAHI $.
      $( [14-May-2003] $)
  $}

  $( A nonnegative integer is less than or equal to zero iff it is equal to
     zero. $)
  nn0le0eq0t $p |- ( N e. NN0 -> ( N <_ 0 <-> N = 0 ) ) $=
    ( cn0 wcel cc0 cle wbr wa wceq nn0ge0t biantrud cr wb nn0ret 0re letri3t
    mpan2 syl bitr4d ) ABCZADEFZTDAEFZGZADHZSUATAIJSAKCZUCUBLZAMUDDKCUENADOPQR
    $.
    $( [30-Dec-2005] $) $( [9-Dec-2005] $)

  $( Closure of addition of nonnegative integers.  (Contributed by Raph Levien,
     10-Dec-02.) $)
  nn0addclt $p |- ( ( M e. NN0 /\ N e. NN0 ) -> ( M + N ) e. NN0 ) $=
    ( cn wcel cc0 wceq wo caddc co cn0 wa nnaddclt nnnn0t syl cc opreq1
    addid2t sylan9eq nncnt sylan2 adantl eqeltrd opreq2 ax0id sylan9eqr sylan
    adantr opreq12 0cn addid1 syl6eq 0nn0 syl6eqel ccase elnn0 syl2anb ) ACDZAE
    FZGBCDZBEFZGABHIZJDZAJDZBJDZUQUSURUTVBUQUSKVACDVBABLVAMNURUSKVABJURBODZVABF
    USURVEVAEBHIBAEBHPBQRBSTUSVDURBMUAUBUQUTKVAAJAODZUTVAAFUQUTVFVAAEHIABEAHUCA
    UDUEASUFUQVCUTAMUGUBURUTKZVAEJVGVAEEHIEAEBEHUHEUIUJUKULUMUNAUOBUOUP $.
    $( [14-May-2003] $)

  ${
    nn0addcl.1 $e |- M e. NN0 $.
    nn0addcl.2 $e |- N e. NN0 $.
    $( Closure of addition of nonnegative integers, inference form.
      (Contributed  by Raph Levien, 10-Dec-02.) $)
    nn0addcl $p |- ( M + N ) e. NN0 $=
      ( cn0 wcel caddc co nn0addclt mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [14-May-2003] $)

    $( Closure of multiplication of nonnegative integers, inference form.
       (Contributed by Raph Levien, 10-Dec-02.) $)
    nn0mulcl $p |- ( M x. N ) e. NN0 $=
      ( cn wcel cc0 wceq wo cmul co cn0 elnn0 mpbi wa nnmulclt nnnn0t syl
      opreq1 nn0cn mul02 syl6eq 0nn0 syl6eqel opreq2 mul01 ccase2 mp2an ) AEFZA
      GHZIZBEFZBGHZIZABJKZLFZALFUKCAMNBLFUNDBMNUIULUJUMUPUIULOUOEFUPABPUOQRUJUO
      GLUJUOGBJKGAGBJSBBDTUAUBUCUDUMUOGLUMUOAGJKGBGAJUEAACTUFUBUCUDUGUH $.
      $( [14-May-2003] $)
  $}

  $( Closure of multiplication of nonnegative integers. $)
  nn0mulclt $p |- ( ( M e. NN0 /\ N e. NN0 ) -> ( M x. N ) e. NN0 ) $=
    ( cn0 wcel cmul co cc0 cif wceq opreq1 eleq1d opreq2 0nn0 elimel nn0mulcl
    dedth2h ) ACDZBCDZABEFZCDQAGHZBEFZCDTRBGHZEFZCDABGGATISUACATBEJKBUBIUAUCCBU
    BTELKTUBAGCMNBGCMNOP $.
    $( [30-Aug-2004] $) $( [22-Jul-2004] $)

  $( Second Peano postulate for nonnegative integers. $)
  peano2nn0 $p |- ( N e. NN0 -> ( N + 1 ) e. NN0 ) $=
    ( cn0 wcel c1 caddc co 1nn0 nn0addclt mpan2 ) ABCDBCADEFBCGADHI $.
    $( [24-May-2004] $) $( [9-May-2004] $)

  $( A natural number plus a nonnegative integer is a natural number. $)
  nnnn0addclt $p |- ( ( M e. NN /\ N e. NN0 ) -> ( M + N ) e. NN ) $=
    ( cn wcel cn0 caddc co cc0 wceq wo nnaddclt ex opreq2 eleq1d cc nncnt
    ax0id syl ibir syl5bir com12 jaod elnn0 syl5ib imp ) ACDZBEDZABFGZCDZUFBCDZ
    BHIZJUIUGUFUJUIUKUFUJUIABKLUKUFUIUKUIAHFGZCDZUFUKUHULCBHAFMNUFUMUFULACUFAOD
    ULAIAPAQRNSTUAUBBUCUDUE $.
    $( [23-Apr-2005] $) $( [20-Apr-2005] $)

  $( A nonnegative integer plus a natural number is a natural number. $)
  nn0nnaddclt $p |- ( ( M e. NN0 /\ N e. NN ) -> ( M + N ) e. NN ) $=
    ( cn wcel cn0 caddc co wa cc wceq axaddcom nncnt nn0cnt syl2an nnnn0addclt
    eqeltrrd ancoms ) BCDZAEDZABFGZCDRSHBAFGZTCBIDAIDUATJRSBAKBLAMNBAOPQ $.
    $( [24-Jan-2006] $) $( [22-Dec-2005] $)

  $( Nonnegative integer ordering relation.  (Contributed by Raph Levien,
     10-Dec-02.) $)
  nn0ltp1let $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                ( M < N <-> ( M + 1 ) <_ N ) ) $=
    ( cn wcel cc0 wceq wo clt wbr c1 caddc co cle wb cn0 nnltp1let wa breq1 wi
    nngt0t nnge1t ax1cn addid2 syl5eqbr 2th pm5.74ri sylan9bb opreq1 breq1d
    adantr bitr4d breq2 wn pm5.21 cr nnret 0re ltnsymt mpan2 syl mt2d axlttrn
    mp3an1 peano2re jca ltp1t sylc ltnlet mpan 3syl mpbid sylanc sylan9bbr
    adantl ltnr ltp1 1re readdcl ltnle mpbi 2false syl6bb ccase elnn0 syl2anb
    ) ACDZAEFZGBCDZBEFZGABHIZAJKLZBMIZNZAODBODWFWHWGWIWMABPWGWHQWJEJKLZBMIZWLWG
    WJEBHIZWHWOAEBHRWHWPWOWHWPSWHWOSBTWHJBWNMBUAJUBUCUDUEUFUGWGWLWONWHWGWKWNBMA
    EJKUHZUIUJUKWFWIQWJWKEMIZWLWIWJAEHIZWFWRBEAHULZWSUMWRUMZWSWRNWFWSWRUNWFWSEA
    HIZATZWFAUODZWSXBUMSZAUPZXDEUODZXEUQAEURUSUTVAWFEWKHIZXAXDWKUODZQXBAWKHIZQZ
    XHWFXGXDXIXKXHSUQEAWKVBVCWFXDXIXFWFXDXIXFAVDZUTVEWFXBXJXCWFXDXJXFAVFUTVEVGW
    FXDXIXHXANZXFXLXGXIXMUQEWKVHVIVJVKVLVMWIWLWRNZWFBEWKMULZVNUKWGWIQWJWRWLWIWJ
    WSWGWRWTWGWSWNEMIZWRWGWSEEHIZXPAEEHRXQXPEUQVOEWNHIXPUMEUQVPEWNUQEJUQVQVRVSV
    TWAWBWGWKWNEMWQUIUKVMWIXNWGXOVNUKWCAWDBWDWE $.
    $( [14-May-2003] $)

  $( Nonnegative integer ordering relation.  (Contributed by Raph Levien,
     10-Apr-04.) $)
  nn0leltp1t $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                ( M <_ N <-> M < ( N + 1 ) ) ) $=
    ( cn0 wcel wa cle wbr c1 caddc co clt cr wb 1re leadd1t mp3an3 nn0ret
    syl2an nn0ltp1let peano2nn0 sylan2 bitr4d ) ACDZBCDZEABFGZAHIJBHIJZFGZAUFKG
    ZALDZBLDZUEUGMZUCUDUIUJHLDUKNABHOPAQBQRUCUFCDUHUGMUDAUFSBTUAUB $.
    $( [25-May-2004] $) $( [9-May-2004] $)

  $( Nonnegative integer ordering relation. $)
  nn0ltlem1t $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                ( M < N <-> M <_ ( N - 1 ) ) ) $=
    ( cn0 wcel wa c1 caddc co cmin cle wbr clt wb cc wceq nn0cnt ax1cn npcant
    mpan2 syl breq2d adantl cr 1re leadd1t mp3an3 nn0ret peano2rem syl2an
    nn0ltp1let 3bitr4rd ) ACDZBCDZEAFGHZBFIHZFGHZJKZUNBJKZAUOJKZABLKUMUQURMULUM
    UPBUNJUMBNDZUPBOZBPUTFNDVAQBFRSTUAUBAUCDZUOUCDZUSUQMZULUMVBVCFUCDVDUDAUOFUE
    UFAUGUMBUCDVCBUGBUHTUIABUJUK $.
    $( [4-Jul-2004] $) $( [10-May-2004] $)

  $( A number is less than or equal to itself plus a nonnegative integer. $)
  nn0addge1t $p |- ( ( A e. RR /\ N e. NN0 ) -> A <_ ( A + N ) ) $=
    ( cr wcel cc0 cle wbr wa caddc co cn0 addge01t biimp3a 3expb nn0ret
    nn0ge0t jca sylan2 ) ACDZBCDZEBFGZHAABIJFGZBKDZSTUAUBSTUAUBABLMNUCTUABOBPQR
    $.
    $( [25-Jul-2006] $) $( [10-Mar-2005] $)

  $( A number is less than or equal to itself plus a nonnegative integer. $)
  nn0addge2t $p |- ( ( A e. RR /\ N e. NN0 ) -> A <_ ( N + A ) ) $=
    ( cr wcel cc0 cle wbr wa caddc co cn0 addge02t biimp3a 3expb nn0ret
    nn0ge0t jca sylan2 ) ACDZBCDZEBFGZHABAIJFGZBKDZSTUAUBSTUAUBABLMNUCTUABOBPQR
    $.
    $( [25-Jul-2006] $) $( [10-Mar-2005] $)

  ${
    nn0addge1.1 $e |- A e. RR $.
    nn0addge1.2 $e |- N e. NN0 $.
    $( A number is less than or equal to itself plus a nonnegative integer. $)
    nn0addge1 $p |- A <_ ( A + N ) $=
      ( cr wcel cn0 caddc co cle wbr nn0addge1t mp2an ) AEFBGFAABHIJKCDABLM $.
      $( [13-Mar-2005] $) $( [10-Mar-2005] $)

    $( A number is less than or equal to itself plus a nonnegative integer. $)
    nn0addge2 $p |- A <_ ( N + A ) $=
      ( cr wcel cn0 caddc co cle wbr nn0addge2t mp2an ) AEFBGFABAHIJKCDABLM $.
      $( [13-Mar-2005] $) $( [10-Mar-2005] $)
  $}

  ${
    nn0le2x.1 $e |- N e. NN0 $.
    $( A nonnegative integer is less than or equal to twice itself.
       (Contributed by Raph Levien, 10-Dec-02.) $)
    nn0le2x $p |- N <_ ( 2 x. N ) $=
      ( caddc co c2 cmul cle nn0re nn0addge1 nn0cn 2times breqtrr ) AAACDEAFDGA
      AABHBIAABJKL $.
      $( [14-May-2003] $)
  $}

  ${
    nn0lele2x.1 $e |- M e. NN0 $.
    nn0lele2x.2 $e |- N e. NN0 $.
    $( 'Less than or equal to' implies 'less than or equal to twice' for
       nonnegative integers.  (Contributed by Raph Levien, 10-Dec-02.) $)
    nn0lele2x $p |- ( N <_ M -> N <_ ( 2 x. M ) ) $=
      ( cle wbr c2 cmul co nn0le2x nn0re 2re remulcl letr mpan2 ) BAEFAGAHIZEFB
      PEFACJBAPBDKACKZGALQMNO $.
      $( [14-May-2003] $)
  $}

