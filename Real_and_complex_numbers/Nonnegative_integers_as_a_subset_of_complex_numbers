$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Nonnegative integers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of nonnegative integers. $)
  df-n0 $a |- NN0 = ( NN u. { 0 } ) $.

  $( Nonnegative integers expressed in terms of naturals and zero.
     (Contributed by Raph Levien, 10-Dec-02.) $)
  elnn0 $p |- ( A e. NN0 <-> ( A e. NN \/ A = 0 ) ) $=
    ( cn0 wcel cn cc0 csn cun wo wceq df-n0 eleq2i elun cc 0cn elisseti elsnc2
    bilor 3bitr ) ABCADEFZGZCADCZASCZHUAAEIZHBTAJKADSLUBUCUAAEEMNOPQR $.
    $( [14-May-03] $)

  $( Positive naturals are a subset of nonnegative integers.  (Contributed
     by Raph Levien, 10-Dec-02.) $)
  nnssnn0 $p |- NN (_ NN0 $=
    ( cn cc0 csn cun cn0 ssun1 df-n0 sseqtr4 ) AABCZDEAIFGH $.
    $( [14-May-03] $)

  $( Nonnegative integers are a subset of the reals.  (Contributed by Raph
     Levien, 10-Dec-02.) $)
  nn0ssre $p |- NN0 (_ RR $=
    ( cn0 cn cc0 csn cun cr df-n0 nnssre wcel wss ax0re snssi ax-mp unssi
    eqsstr ) ABCDZEFGBPFHCFIPFJKCFLMNO $.
    $( [14-May-03] $)

  $( Nonnegative integers are a subset of the complex numbers.) $)
  nn0sscn $p |- NN0 (_ CC $=
    ( cn0 cr cc nn0ssre axresscn sstri ) ABCDEF $.
    $( [25-Jun-04] $) $( [9-May-04] $)

  $( The set of nonnegative integers exists. $)
  nn0ex $p |- NN0 e. V $=
    ( cn0 cc axcnex nn0sscn ssexi ) ABCDE $.
    $( [24-Jul-04] $) $( [18-Jul-04] $)

  $( A natural number is a nonnegative integer. $)
  nnnn0t $p |- ( A e. NN -> A e. NN0 ) $=
    ( cn cn0 nnssnn0 sseli ) BCADE $.
    $( [10-May-04] $) $( [9-May-04] $)

  $( A nonnegative integer is a real number. $)
  nn0ret $p |- ( A e. NN0 -> A e. RR ) $=
    ( cn0 cr nn0ssre sseli ) BCADE $.
    $( [25-May-04] $) $( [9-May-04] $)

  $( A nonnegative integer is a complex number. $)
  nn0cnt $p |- ( A e. NN0 -> A e. CC ) $=
    ( cn0 cc nn0sscn sseli ) BCADE $.
    $( [3-Jul-04] $) $( [9-May-04] $)

  ${
    nn0re.1 $e |- A e. NN0 $.
    $( A nonnegative integer is a real number. $)
    nn0re $p |- A e. RR $=
      ( cn0 cr nn0ssre ssii ) CDAEBF $.
      $( [14-May-03] $)

    $( A nonnegative integer is a complex number. $)
    nn0cn $p |- A e. CC $=
      ( nn0re recn ) AABCD $.
      $( [14-May-03] $)
  $}

  $( 0 is a nonnegative integer.  (Contributed by Raph Levien, 10-Dec-02.) $)
  0nn0 $p |- 0 e. NN0 $=
    ( cc0 cn0 wcel cn wceq wo cleqid olc ax-mp elnn0 mpbir ) ABCADCZAAEZFZMNAGM
    LHIAJK $.
    $( [14-May-03] $)

  $( 1 is a nonnegative integer.  (Contributed by Raph Levien, 10-Dec-02.) $)
  1nn0 $p |- 1 e. NN0 $=
    ( cn cn0 c1 nnssnn0 1nn ssii ) ABCDEF $.
    $( [14-May-03] $)

  $( 2 is a nonnegative integer.  (Contributed by Raph Levien, 10-Dec-02.) $)
  2nn0 $p |- 2 e. NN0 $=
    ( cn cn0 c2 nnssnn0 2nn ssii ) ABCDEF $.
    $( [14-May-03] $)

  $( No number less than zero is a nonnegative integer. $)
  lt0nnn0 $p |- ( ( A e. RR /\ A < 0 ) -> -. A e. NN0 ) $=
    ( cr wcel cc0 clt wbr cn0 wn cn wceq wo c1 lt01 wa wi ax1re ax0re axlttrn
    mp3an2 mpan2 mpan2i lt1nnn exp syld con2d breq2 binegd ltnrt syl5bi com12
    jaod elnn0 syl5ib imp ) ABCZADEFZAGCZHUOUQUPUOAICZADJZKUPHZUQUOURUTUSUOUPUR
    UOUPALEFZURHZUOUPDLEFZVAMUOLBCZUPVCNVAOZPUODBCVDVEQADLRSTUAUOVAVBAUBUCUDUEU
    SUOUTUSAAEFZHUTUOUSVFUPADAEUFUGAUHUIUJUKAULUMUEUN $.
    $( [27-Jun-04] $) $( [9-May-04] $)

  $( A nonnegative integer is greater than or equal to zero. $)
  nn0ge0t $p |- ( A e. NN0 -> 0 <_ A ) $=
    ( cn0 wcel cc0 cle wbr clt wn cr nn0ret lt0nnn0 exp con2d mpcom wb ax0re
    leltt sylan5 mpan mpbird ) ABCZDAEFZADGFZHZAICZUAUDAJZUEUCUAUEUCUAHAKLMNDIC
    ZUAUBUDOZPUGUEUHUADAQUFRST $.
    $( [3-Jul-04] $) $( [9-May-04] $)

  $( Closure of addition of nonnegative integers.  (Contributed by Raph Levien,
     10-Dec-02.) $)
  nn0addclt $p |- ( ( A e. NN0 /\ B e. NN0 ) -> ( A + B ) e. NN0 ) $=
    ( cn wcel cc0 wceq wo caddc co cn0 wa nnaddclt nnnn0t syl cc opreq1
    addid2t sylan9eq nncnt sylan5 adantl eqeltrd opreq2 ax0id sylan9eqr sylan
    adantr opreq12 0cn addid1 syl6eq 0nn0 a1i ccase elnn0 syl2anb ) ACDZAEFZGBC
    DZBEFZGABHIZJDZAJDZBJDZUQUSURUTVBUQUSKVACDVBABLVAMNURUSKVABJURBODZVABFUSURV
    EVAEBHIBAEBHPBQRBSTUSVDURBMUAUBUQUTKVAAJAODZUTVAAFUQUTVFVAAEHIABEAHUCAUDUEA
    SUFUQVCUTAMUGUBURUTKZVAEJVGVAEEHIEAEBEHUHEUIUJUKEJDVGULUMUBUNAUOBUOUP $.
    $( [14-May-03] $)

  ${
    nn0addcl.1 $e |- A e. NN0 $.
    nn0addcl.2 $e |- B e. NN0 $.
    $( Closure of addition of nonnegative integers, inference form.  (Contributed
       by Raph Levien, 10-Dec-02.) $)
    nn0addcl $p |- ( A + B ) e. NN0 $=
      ( cn0 wcel caddc co nn0addclt mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [14-May-03] $)

    $( Closure of multiplication of nonnegative integers, inference form.
       (Contributed by Raph Levien, 10-Dec-02.) $)
    nn0mulcl $p |- ( A x. B ) e. NN0 $=
      ( cn wcel cc0 wceq wo cmulc co cn0 elnn0 mpbi wa nnmulclt nnnn0t syl
      opreq1 nn0cn mulzer2 syl6eq 0nn0 a1i eqeltrd opreq2 mulzer1 ccase2 mp2an
      ) AEFZAGHZIZBEFZBGHZIZABJKZLFZALFULCAMNBLFUODBMNUJUMUKUNUQUJUMOUPEFUQABPU
      PQRUKUPGLUKUPGBJKGAGBJSBBDTUAUBGLFZUKUCUDUEUNUPGLUNUPAGJKGBGAJUFAACTUGUBU
      RUNUCUDUEUHUI $.
      $( [14-May-03] $)
  $}

  $( Closure of multiplication of nonnegative integers. $)
  nn0mulclt $p |- ( ( A e. NN0 /\ B e. NN0 ) -> ( A x. B ) e. NN0 ) $=
    ( cn0 wcel cmulc co cc0 cded wceq opreq1 eleq1d opreq2 0nn0 elimel
    nn0mulcl dedth2h ) ACDZBCDZABEFZCDQAGHZBEFZCDTRBGHZEFZCDABGGATISUACATBEJKBU
    BIUAUCCBUBTELKTUBAGCMNBGCMNOP $.
    $( [30-Aug-04] $) $( [22-Jul-04] $)

  $( Second Peano postulate for nonnegative integers. $)
  peano2nn0 $p |- ( A e. NN0 -> ( A + 1 ) e. NN0 ) $=
    ( cn0 wcel c1 caddc co 1nn0 nn0addclt mpan2 ) ABCDBCADEFBCGADHI $.
    $( [24-May-04] $) $( [9-May-04] $)

  $( Nonnegative integer ordering relation.  (Contributed by Raph Levien,
     10-Dec-02.) $)
  nn0ltle1 $p |- ( ( A e. NN0 /\ B e. NN0 ) ->
                ( A < B <-> ( A + 1 ) <_ B ) ) $=
    ( cn wcel cc0 wceq wo clt wbr c1 caddc co cle wb cn0 nnltle1 wa breq1 wi
    nngt0t nnge1t 1cn addid2 syl5eqbr 2th pm5.74ri sylan9bb opreq1 breq1d
    adantr bitr4d breq2 wn pm5.21 cr nnret ax0re ltnsymt mpan2 syl mt2d
    axlttrn mp3an1 ax1re axaddrcl jca ltplus1t syl13 leltt 3syl bicon2d mpbid
    sylan13 sylan9bbr adantl ltnr ltplus1 addrcl lelt bicon2 mpbi mp2an syl6bb
    ccase elnn0 syl2anb ) ACDZAEFZGBCDZBEFZGABHIZAJKLZBMIZNZAODBODWGWIWHWJWNABP
    WHWIQWKEJKLZBMIZWMWHWKEBHIZWIWPAEBHRWIWQWPWIWQSWIWPSBTWIJBWOMBUAJUBUCUDUEUF
    UGWHWMWPNWIWHWLWOBMAEJKUHZUIUJUKWGWJQWKWLEMIZWMWJWKAEHIZWGWSBEAHULZWTUMWSUM
    ZWTWSNWGWTWSUNWGWTEAHIZATZWGAUODZWTXCUMSZAUPZXEEUODZXFUQAEURUSUTVAWGEWLHIZX
    BXEWLUODZQXCAWLHIZQZXIWGXHXEXJXLXISUQEAWLVBVCWGXEXJXGWGXEXJXGXEJUODXJVDAJVE
    USZUTVFWGXCXKXDWGXEXKXGAVGUTVFVHWGWSXIWGXEXJWSXIUMNZXGXMXJXHXNUQWLEVIUSVJVK
    VLVMVNWJWMWSNZWGBEWLMULZVOUKWHWJQWKWSWMWJWKWTWHWSXAWHWTWOEMIZWSWHWTEEHIZXQA
    EEHRXRUMXQUMZXRXQNEUQVPEWOHIZXSEUQVQXQXTWOEEJUQVDVRUQVSVTWAXRXQUNWBWCWHWLWO
    EMWRUIUKVNWJXOWHXPVOUKWDAWEBWEWF $.
    $( [14-May-03] $)

  $( Nonnegative integer ordering relation.  (Contributed by Raph Levien,
     10-Apr-04.) $)
  nn0lelt1 $p |- ( ( A e. NN0 /\ B e. NN0 ) ->
                ( A <_ B <-> A < ( B + 1 ) ) ) $=
    ( cn0 wcel wa cle wbr c1 caddc co clt cr wb ax1re leadd1t mp3an3 nn0ret
    syl2an nn0ltle1 peano2nn0 sylan5 bitr4d ) ACDZBCDZEABFGZAHIJBHIJZFGZAUFKGZA
    LDZBLDZUEUGMZUCUDUIUJHLDUKNABHOPAQBQRUCUFCDUHUGMUDAUFSBTUAUB $.
    $( [25-May-04] $) $( [9-May-04] $)

  $( Nonnegative integer ordering relation. $)
  nn0ltlem1 $p |- ( ( A e. NN0 /\ B e. NN0 ) ->
                ( A < B <-> A <_ ( B - 1 ) ) ) $=
    ( cn0 wcel wa clt wbr c1 caddc co cle cmin nn0ltle1 cr wb ax1re leadd1t
    mp3an3 nn0ret subrclt mpan2 syl syl2an cc wceq nn0cnt 1cn npcant breq2d
    adantl bitr2d bitrd ) ACDZBCDZEZABFGAHIJZBKGZABHLJZKGZABMUOUSUPURHIJZKGZUQA
    NDZURNDZUSVAOZUMUNVBVCHNDZVDPAURHQRASUNBNDZVCBSVFVEVCPBHTUAUBUCUNVAUQOUMUNU
    TBUPKUNBUDDZUTBUEZBUFVGHUDDVHUGBHUHUAUBUIUJUKUL $.
    $( [4-Jul-04] $) $( [10-May-04] $)

  ${
    $d x A $.
    nn0ge0i.1 $e |- A e. NN0 $.
    $( Nonnegative integers are nonnegative.  (Contributed by Raph Levien,
       10-Dec-02.) $)
    nn0ge0i $p |- 0 <_ A $=
      ( cc0 cle wbr clt wceq wo cn wcel cn0 elnn0 mpbi nngt0t cleqcom biimp
      im2or ax-mp ax0re nn0re leloe mpbir ) CADECAFEZCAGZHZAIJZACGZHZUEAKJUHBAL
      MUFUCUGUDANUGUDACOPQRCASABTUAUB $.
      $( [14-May-03] $)
  $}

  ${
    nn0addge1.1 $e |- A e. NN0 $.
    nn0addge1.2 $e |- B e. NN0 $.
    $( A nonnegative integer is less than or equal to itself plus another.
       (Contributed by Raph Levien, 10-Dec-02.) $)
    nn0addge1 $p |- A <_ ( A + B ) $=
      ( cc0 caddc co cle nn0cn addid1 wbr nn0ge0i ax0re nn0re leadd2 mpbi
      eqbrtrr ) AEFGZAABFGZHAACIJEBHKRSHKBDLEBAMBDNACNOPQ $.
      $( [14-May-03] $)

    $( A nonnegative integer is less than or equal to itself plus another.
      (Contributed by Raph Levien, 10-Dec-02.) $)
    nn0addge2 $p |- B <_ ( A + B ) $=
      ( caddc co cle nn0addge1 nn0cn addcom breqtr ) BBAEFABEFGBADCHBABDIACIJK
      $.
      $( [14-May-03] $)
  $}

  ${
    nn0le2x.1 $e |- A e. NN0 $.
    $( A nonnegative integer is less than or equal to twice itself.
       (Contributed by Raph Levien, 10-Dec-02.) $)
    nn0le2x $p |- A <_ ( 2 x. A ) $=
      ( caddc co c2 cmulc cle nn0addge1 nn0cn 2times breqtrr ) AAACDEAFDGAABBHA
      ABIJK $.
      $( [14-May-03] $)
  $}

  ${
    nn0lele2x.1 $e |- A e. NN0 $.
    nn0lele2x.2 $e |- B e. NN0 $.
    $( 'Less than or equal to' implies 'less than or equal to twice' for
       nonnegative integers.  (Contributed by Raph Levien, 10-Dec-02.) $)
    nn0lele2x $p |- ( B <_ A -> B <_ ( 2 x. A ) ) $=
      ( cle wbr c2 cmulc co nn0le2x nn0re 2re mulrcl letr mpan2 ) BAEFAGAHIZEFB
      PEFACJBAPBDKACKZGALQMNO $.
      $( [14-May-03] $)
  $}

