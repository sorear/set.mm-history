$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Continuous complex functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the new symbol needed for continuous complex functions. $)
  $c -cn-> $.

  $( Extend class notation to include the operation which returns a class of
     continuous complex functions. $)
  ccncf $a class -cn-> $.

  ${
    $d a b f s x y z w $.
    $( Define the operation whose value is a class of continuous complex
       functions. $)
    df-cncf $a |- -cn-> = { <. <. a , b >. , s >. | ( ( a C_ CC /\ b C_ CC ) /\
      s = { f | ( f : a --> b /\ A. x e. a A. y e. RR+
      E. z e. RR+ A. w e. a ( ( abs ` ( x - w ) ) < z ->
      ( abs ` ( ( f ` x ) - ( f ` w ) ) ) < y ) ) } ) } $.
  $}

  ${
    $d A a b f s w x y z $.  $d B a b f s $.  $d F f w x y z $.
    $d w x y z P $.
    $( The value of the continuous complex function operation is the set of
       continuous functions from ` A ` to ` B ` .  (Contributed by Paul
       Chapman, 11-Oct-2007.) $)
    cncfval $p |- ( ( A C_ CC /\ B C_ CC ) ->
      ( A -cn-> B ) = { f | ( f : A --> B /\ A. x e. A A. y e. RR+
      E. z e. RR+ A. w e. A ( ( abs ` ( x - w ) ) < z ->
      ( abs ` ( ( f ` x ) - ( f ` w ) ) ) < y ) ) } ) $=
      ( va vb vs cc wss wa cpw wcel ccncf co cv wf cmin cabs cfv clt wbr wi
      wral crp wrex cab wceq cvv wb axcnex elpw2g anbi12d ax-mp ssexg simpl
      ss2abi mapex sylancr feq2 raleq rexbidv ralbidv raleqbi1dv abbidv feq3
      anbi1d copab2 df-cncf anbi1i oprabbii eqtr4i oprabval2g mpd3an3 sylbir )
      EKLZFKLZMZEKNZOZFWAOZMZEFPQEFGRZSZARZDRZTQUAUBCRUCUDWGWEUBWHWEUBTQUAUBBRU
      CUDUEZDEUFZCUGUHZBUGUFZAEUFZMZGUIZUJZKUKOZWDVTULUMWQWBVRWCVSEKUKUNFKUKUNU
      OUPWBWCWOUKOZWPWOWFGUIZLWSUKOWRWDWOWSUKUQWNWFGWFWMURUSEFWAWAGUTVAHIJEFWAW
      AHRZIRZWESZWIDWTUFZCUGUHZBUGUFZAWTUFZMZGUIZWOPEXAWESZWMMZGUIUKWTEUJZXGXJG
      XKXBXIXFWMWTEXAWEVBXEWLAWTEXKXDWKBUGXKXCWJCUGWIDWTEVCVDVEVFUOVGXAFUJZXJWN
      GXLXIWFWMXAFEWEVHVIVGPWTKLZXAKLZMZJRXHUJZMZHIJVJWTWAOZXAWAOZMZXPMZHIJVJAB
      CDGJHIVKYAXQHIJXTXOXPWQXTXOULUMWQXRXMXSXNWTKUKUNXAKUKUNUOUPVLVMVNVOVPVQ
      $.
      $( [11-Oct-2007] $)

    $( Membership in the set of continuous complex functions from ` A ` to
       ` B ` .  (Contributed by Paul Chapman, 11-Oct-2007.) $)
    elcncf $p |- ( ( A C_ CC /\ B C_ CC ) ->
     ( F e. ( A -cn-> B ) <-> ( F : A --> B /\ A. x e. A A. y e. RR+
     E. z e. RR+ A. w e. A ( ( abs ` ( x - w ) ) < z ->
     ( abs ` ( ( F ` x ) - ( F ` w ) ) ) < y ) ) ) ) $=
      ( vf cc wss wa cvv wcel ccncf co wf cv cmin cabs cfv clt wbr wi wral crp
      wrex wb cab cncfval eleq2d wceq feq1 fveq1 opreq12d fveq2d breq1d imbi2d
      rexralbidv 2ralbidv anbi12d elabg sylan9bb ex wn wo elisset adantl
      axcnex ssex fex adantlr expcom syl adantr imp jaodan con3d ioran pm5.21
      sylbi syl6 pm2.61d ) EIJZFIJZKZGLMZGEFNOZMZEFGPZAQZDQZROSTCQUAUBZWJGTZWKG
      TZROZSTZBQZUAUBZUCZDEUDCUEUFZBUEUDAEUDZKZUGZWEWFXCWEWHGEFHQZPZWLWJXDTZWKX
      DTZROZSTZWQUAUBZUCZDEUDCUEUFZBUEUDAEUDZKZHUHZMWFXBWEWGXOGABCDEFHUIUJXNXBH
      GLXDGUKZXEWIXMXAEFXDGULXPXLWTABEUEXPXKWSCDUEEXPXJWRWLXPXIWPWQUAXPXHWOSXPX
      FWMXGWNRWJXDGUMWKXDGUMUNUOUPUQURUSUTVAVBVCWEWFVDWHXBVEZVDZXCWEXQWFWEXQWFW
      EWHWFXBWHWFWEGWGVFVGWEXBWFWCXBWFUCZWDWCELMZXSEIVHVIXBXTWFWIXTWFXAEFLGVJVK
      VLVMVNVOVPVCVQXRWHVDXBVDKXCWHXBVRWHXBVSVTWAWB $.
      $( [11-Oct-2007] $)

    $( A continuous complex function's domain and codomain.  (Contributed by
       Paul Chapman, 17-Jan-2008.) $)
    cncff $p |- ( ( P C_ CC /\ Q C_ CC /\ F e. ( P -cn-> Q ) ) ->
                  F : P --> Q ) $=
      ( vx vw vz vy cc wss ccncf co wcel w3a wf cv cmin cabs cfv clt wbr wi
      wral crp wrex wa elcncf biimp3a simplld ) AHIZBHIZCABJKLZMABCNZDOZEOZPKQR
      FOSTUMCRUNCRPKQRGOSTUAEAUBFUCUDGUCUBDAUBZUIUJUKULUOUEDGFEABCUFUGUH $.
      $( [17-Jan-2008] $)

    $( MAY BE REVISED - don't delete this until we decide to keep cnff $)
    $( A continuous complex function's value belongs to its codomain.
       (Contributed by Paul Chapman, 21-Jan-2008.) $)
    cncffvelrnOLD $p |- ( ( A C_ CC /\ B C_ CC /\ F e. ( A -cn-> B ) ) ->
                        ( C e. A -> ( F ` C ) e. B ) ) $=
      ( vx vw vz vy cc wss ccncf co wcel w3a wf cfv wi cv cmin cabs clt wbr
      wral crp wrex wa elcncf biimpd 3impia simplld ffvelrn ex syl ) AIJZBIJZDA
      BKLMZNZABDOZCAMZCDPBMZQUQURERZFRZSLTPGRUAUBVADPVBDPSLTPHRUAUBQFAUCGUDUEHU
      DUCEAUCZUNUOUPURVCUFZUNUOUFUPVDEHGFABDUGUHUIUJURUSUTABCDUKULUM $.
      $( [21-Jan-2008] $)
  $}

  $( A continuous complex function's value belongs to its codomain.
     (Contributed by Paul Chapman, 21-Jan-2008.) $)
  cncffvelrn $p |- ( ( A C_ CC /\ B C_ CC /\ F e. ( A -cn-> B ) ) ->
                      ( C e. A -> ( F ` C ) e. B ) ) $=
    ( cc wss ccncf co wcel w3a wf cfv wi cncff ffvelrn ex syl ) AEFBEFDABGHIJAB
    DKZCAIZCDLBIZMABDNRSTABCDOPQ $.
    $( [21-Jan-2008] $)


  ${
    $d A a b w x $.  $d A w x y z $.  $d F a b w x $.  $d F w x y z $.
    $d G w x y z $.
    negfcncf.1 $e |- A C_ CC $.
    negfcncf.2 $e |- F e. ( A -cn-> CC ) $.
    negfcncf.3 $e |- G = { <. a , b >. | ( a e. A /\ b = -u ( F ` a ) ) } $.
    $( The negative of a continuous complex function is continuous.
       (Contributed by Paul Chapman, 21-Jan-2008.) $)
    negfcncfi $p |- G e. ( A -cn-> CC ) $=
      ( vx vw vz vy cc ccncf co wcel wf cv cmin cabs cfv clt wbr wi wral crp
      wrex wss wa wb ssid elcncf mp2an cneg mpbi simpli ffvelrni negcl syl
      rgen wceq fveq2 negeqd eleq1d cbvralv fopab2 simpri negex fvopab4
      opreqan12d neg2sub syl2an eqtrd fveq2d abssub eqtr4d breq1d imbi2d
      ralbidva rexbidv ralbidv ralbiia mpbir mpbir2an ) CAMNOZPZAMCQZIRZJRZSOTU
      AKRUBUCZWHCUAZWICUAZSOZTUAZLRZUBUCZUDZJAUEZKUFUGZLUFUEZIAUEZAMUHZMMUHZWFW
      GXAUIUJFMUKZILKJAMCULUMDRZBUAZUNZMPZDAUEZWGWHBUAZUNZMPZIAUEXIXLIAWHAPZXJM
      PZXLAMWHBAMBQZWJXJWIBUAZSOTUAZWOUBUCZUDZJAUEZKUFUGZLUFUEZIAUEZBWEPZXOYCUI
      ZGXBXCYDYEUJFXDILKJAMBULUMUOZUPZUQZXJURUSUTXLXHIDAWHXEVAZXKXGMYIXJXFWHXEB
      VBVCVDVEUODEAMXGCHVFUOXAYCXOYCYFVGWTYBIAXMWSYALUFXMWRXTKUFXMWQXSJAXMWIAPZ
      UIZWPXRWJYKWNXQWOUBYKWNXPXJSOZTUAZXQYKWMYLTYKWMXKXPUNZSOZYLXMYJWKXKWLYNSD
      EWHXGXKACXEWHVAXFXJXEWHBVBVCHXJVHVIDEWIXGYNACXEWIVAXFXPXEWIBVBVCHXPVHVIVJ
      XNXPMPZYOYLVAXMYJXJXPVKYHAMWIBYGUQZVLVMVNXNYPXQYMVAXMYJXJXPVOYHYQVLVPVQVR
      VSVTWAWBWCWD $.
      $( [21-Jan-2008] $)
  $}

  ${
    $d A w x y z $.  $d F w x y z $.  $d Z w z $.  $d ph w x y $.
    elcncf1d.1 $e |- ( ph -> F : A --> B ) $.
    elcncf1d.2 $e |- ( ph -> ( ( x e. A /\ y e. RR+ ) -> Z e. RR+ ) ) $.
    elcncf1d.3 $e |- ( ph -> ( ( ( x e. A /\ w e. A ) /\ y e. RR+ ) ->
                       ( ( abs ` ( x - w ) ) < Z ->
                         ( abs ` ( ( F ` x ) - ( F ` w ) ) ) < y ) ) ) $.
    $( Membership in the set of continuous complex functions from ` A ` to
       ` B ` .  (Contributed by Paul Chapman, 26-Nov-2007.) $)
    elcncf1di $p |- ( ph ->
                   ( ( A C_ CC /\ B C_ CC ) -> F e. ( A -cn-> B ) ) ) $=
      ( vz cc wss wa ccncf co wcel wf cv cmin cabs cfv clt wbr wi wral crp
      wrex elcncf imp an23 anbi2i anass bitr4i sylbir r19.21aiva wceq breq2
      imbi1d ralbidv rcla4ev syl11anc anassrs jca syl5cbir ) EMNFMNOGEFPQREFGSZ
      BTZDTZUAQUBUCZLTZUDUEZVHGUCVIGUCUAQUBUCCTZUDUEZUFZDEUGZLUHUIZCUHUGZBEUGZO
      ABCLDEFGUJAVGVSIAVRBEAVHERZOVQCUHAVTVMUHRZVQAVTWAOZOZHUHRZVJHUDUEZVNUFZDE
      UGZVQAWBWDJUKWCWFDEWCVIERZOZAVTWHOWAOZOZWFWKAWBWHOZOWIWJWLAVTWHWAULUMAWBW
      HUNUOAWJWFKUKUPUQVPWGLHUHVKHURZVOWFDEWMVLWEVNVKHVJUDUSUTVAVBVCVDUQUQVEVF
      $.
      $( [26-Nov-2007] $)
  $}

  ${
    $d A w x y $.  $d F w x y $.  $d Z w $.  $d ph w x y $.
    elcncf1i.1 $e |- F : A --> B $.
    elcncf1i.2 $e |- ( ( x e. A /\ y e. RR+ ) -> Z e. RR+ ) $.
    elcncf1i.3 $e |- ( ( ( x e. A /\ w e. A ) /\ y e. RR+ ) ->
                       ( ( abs ` ( x - w ) ) < Z ->
                         ( abs ` ( ( F ` x ) - ( F ` w ) ) ) < y ) ) $.
    $( Membership in the set of continuous complex functions from ` A ` to
       ` B ` .  (Contributed by Paul Chapman, 26-Nov-2007.) $)
    elcncf1ii $p |- ( ( A C_ CC /\ B C_ CC ) -> F e. ( A -cn-> B ) ) $=
      ( wph wi cc wss wa ccncf co wcel id wf a1i cv crp cmin cabs cfv clt wbr
      elcncf1di ax-mp ) KKLZDMNEMNOFDEPQRLKSUKABCDEFGDEFTUKHUAAUBZDRZBUBZUCRZOG
      UCRLUKIUAUMCUBZDROUOOULUPUDQUEUFGUGUHULFUFUPFUFUDQUEUFUNUGUHLLUKJUAUIUJ
      $.
      $( [26-Nov-2007] $)
  $}

  ${
    $d A w x y z $.  $d C w x y z $.  $d F w x y z $.
    $( A continuous complex function restricted to a subset is continuous.
       (Contributed by Paul Chapman, 18-Oct-2007.) $)
    rescncf $p |- ( ( A C_ CC /\ B C_ CC /\ C C_ A ) ->
            ( F e. ( A -cn-> B ) -> ( F |` C ) e. ( C -cn-> B ) ) ) $=
      ( vx vw vz vy cc wss w3a wf cv cmin co cabs cfv clt wbr wi wral crp wrex
      wa cres ccncf wcel fssres expcom ssralv fvres opreqan12d fveq2d breq1d
      imbi2d biimprd ralimdvaa sylan9 reximdv ralimdv syld anim12d 3ad2ant3 wb
      elcncf 3adant3 sstr anim1i 3impa 3coml syl 3imtr4d ) AIJZBIJZCAJZKZABDLZE
      MZFMZNOPQGMRSZVRDQZVSDQZNOZPQZHMZRSZTZFAUAZGUBUCZHUBUAZEAUAZUDZCBDCUEZLZV
      TVRWMQZVSWMQZNOZPQZWERSZTZFCUAZGUBUCZHUBUAZECUAZUDZDABUFOUGZWMCBUFOUGZVOV
      MWLXETVNVOVQWNWKXDVQVOWNABCDUHUIVOWKWJECUAXDWJECAUJVOWJXCECVOVRCUGZUDZWIX
      BHUBXIWHXAGUBVOWHWGFCUAXHXAWGFCAUJXHWGWTFCXHVSCUGZUDZWTWGXKWSWFVTXKWRWDWE
      RXKWQWCPXHXJWOWAWPWBNVRCDUKVSCDUKULUMUNUOUPUQURUSUTUQVAVBVCVMVNXFWLVDVOEH
      GFABDVEVFVPCIJZVNUDZXGXEVDVOVMVNXMVOVMVNXMVOVMUDXLVNCAIVGVHVIVJEHGFCBWMVE
      VKVL $.
      $( [18-Oct-2007] $)

    $( Change the codomain of a continuous complex function.  (Contributed by
       Paul Chapman, 18-Oct-2007.) $)
    cncffvrn $p |- ( ( ( A C_ CC /\ B C_ CC /\ C C_ CC ) /\
      A. x e. A ( F ` x ) e. C ) ->
      ( F e. ( A -cn-> B ) -> F e. ( A -cn-> C ) ) ) $=
      ( vw vz vy cc wss w3a cv cfv wcel wral wa wf cmin co cabs clt wbr wi crp
      wrex ccncf crn wfn fnfvrnss ffn sylan fss dffn3 sylib syldan expcom
      anim1d adantl wb elcncf 3adant3 adantr 3adant2 3imtr4d ) BIJZCIJZDIJZKZAL
      ZEMZDNABOZPBCEQZVIFLZRSTMGLUAUBVJVMEMRSTMHLUAUBUCFBOGUDUEHUDOABOZPZBDEQZV
      NPZEBCUFSNZEBDUFSNZVKVOVQUCVHVKVLVPVNVLVKVPVLVKEUGZDJZVPEBUHZVKWAVLABDEUI
      BCEUJZUKBVTEQZWAVPVLBVTDEULVLWBWDWCBEUMUNUKUOUPUQURVHVRVOUSZVKVEVFWEVGAHG
      FBCEUTVAVBVHVSVQUSZVKVEVGWFVFAHGFBDEUTVCVBVD $.
      $( [18-Oct-2007] $)
  $}

  ${
    $d F w x y $.
    abscncflem.1 $e |- A C_ CC $.
    abscncflem.2 $e |- F : CC --> A $.
    abscncflem.3 $e |- ( ( x e. CC /\ w e. CC ) ->
      ( abs ` ( ( F ` x ) - ( F ` w ) ) ) <_ ( abs ` ( x - w ) ) ) $.
    $( Lemma for ~ abscncf , ~ recncf , ~ imcncf , and ~ cjcncf . $)
    abscncflem $p |- F e. ( CC -cn-> A ) $=
      ( vy cc wss ccncf co wcel ssid cv crp simpr wa cmin cabs cfv clt wbr wi
      cle cr lelttr wf fss mp2an ffvelrni anim12i subcl abscl 3syl syl rpre
      syl3an 3anidm12 expcom imp3a ancli anim1i anass sylib syl5 exp3a impcom
      elcncf1ii ) IIJCIJZDICKLMINEAHBICDHOZFAOZIMZVKPMZQVNVMBOZIMZRZVLVOSLZTUAZ
      VKUBUCZVLDUAZVODUAZSLZTUAZVKUBUCZUDVNVQVTWEVNVQWDVSUEUCZVTRZRZWEVQVTRZVNV
      QWGWEVQVNWGWEUDZVQVNWJWDUFMZVSUFMZVKUFMWJVQVQVNWDVSVKUGVQWAIMZWBIMZRWCIMW
      KVMWMVPWNIIVLDICDUHVJIIDUHFEICIDUIUJZUKIIVODWOUKULWAWBUMWCUNUOVQVRIMWLVLV
      OUMVRUNUPVKUQURUSUTVAWIVQWFRZVTRWHVQWPVTVQWFGVBVCVQWFVTVDVEVFVGVHVIUJ $.
      $( [21-Oct-2007] $)
  $}

  ${
    $d w x y z $.
    $( Absolute value is continuous.  (Contributed by Paul Chapman,
       21-Oct-2007.) $)
    abscncf $p |- abs e. ( CC -cn-> RR ) $=
      ( vx vw cr cabs axresscn absf cv abs2difabs abscncflem ) ABCDEFAGBGHI $.
      $( [21-Oct-2007] $)

    $( Real part is continuous.  (Contributed by Paul Chapman, 21-Oct-2007.) $)
    recncf $p |- Re e. ( CC -cn-> RR ) $=
      ( vx vw cr cre axresscn ref cv cc wcel wa cmin co cfv cabs cle resub
      fveq2d wbr subcl absrele syl eqbrtrrd abscncflem ) ABCDEFAGZHIBGZHIJZUDUE
      KLZDMZNMZUDDMUEDMKLZNMUGNMZOUFUHUJNUDUEPQUFUGHIUIUKORUDUESUGTUAUBUC $.
      $( [21-Oct-2007] $)

    $( Imaginary part is continuous.  (Contributed by Paul Chapman,
       21-Oct-2007.) $)
    imcncf $p |- Im e. ( CC -cn-> RR ) $=
      ( vx vw cr cim axresscn imf cv cc wcel wa cmin co cfv cabs cle imsub
      fveq2d wbr subcl absimle syl eqbrtrrd abscncflem ) ABCDEFAGZHIBGZHIJZUDUE
      KLZDMZNMZUDDMUEDMKLZNMUGNMZOUFUHUJNUDUEPQUFUGHIUIUKORUDUESUGTUAUBUC $.
      $( [21-Oct-2007] $)

    $( Complex conjugate is continuous.  (Contributed by Paul Chapman,
       21-Oct-2007.) $)
    cjcncf $p |- * e. ( CC -cn-> CC ) $=
      ( vx vw vy cc ccj ssid cv cre cfv ci cim cmul co cmin df-cj wcel cjval
      cjcl eqeltrrd fopab wa cabs cr wceq cle wbr anim12i subcl abscl 3syl
      cjsub fveq2d abscj syl eqtr3d eqle syl11anc abscncflem ) ABDEDFACDDAGZHIJ
      USKILMNMZEACOUSDPZUSEIZUTDUSQUSRZSTVABGZDPZUAZVBVDEIZNMZUBIZUCPZVIUSVDNMZ
      UBIZUDVIVLUEUFVFVBDPZVGDPZUAVHDPVJVAVMVEVNVCVDRUGVBVGUHVHUIUJVFVKEIZUBIZV
      IVLVFVOVHUBUSVDUKULVFVKDPVPVLUDUSVDUHVKUMUNUOVIVLUPUQUR $.
      $( [21-Oct-2007] $)
  $}

  ${
    $d A v w z $.  $d A w x y z $.  $d F v w z $.
    mulc1cncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( A x. x ) ) } $.
    $( Multiplication by a constant is continuous.  (Contributed by Paul
       Chapman, 28-Nov-2007.) $)
    mulc1cncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( vz vv vw cc wcel wss ccncf co ssid wa wi cc0 wceq c1 wf cv cmul wral
      mulcl r19.21aiva fopab2 sylib adantr crp 1rp a1i12 cmin cabs cfv clt wbr
      w3a opreq1 fveq2d subdi 3expb opreq2 fvopab4g sylan2 anabss7 adantrr
      adantrl opreq12d eqtr4d 0cn eleq1 mpbiri sylan subcl mul02 syl abs0
      syl6eq adantl 3eqtr3d 3adant3 cr elrp simprbi 3ad2ant3 eqbrtrd a1d
      3adant1l 3expib elcncf1di wne cdiv redivcl simplbi abscl abs00 necon3bid
      biimpar jca syl2an divgt0 sylanb absgt0 biimpd imdistani anim1i sylanbrc
      expcom adantld absmul eqtr3d ad2ant2r breq1d wb ltmuldiv2OLD syl3anl
      3anrot df-3an bitri sylanbr anasss ancoms bitr3d exbiri pm2.61dane
      mp2ani ) CIJZIIKZYHDIILMJZINZYJYGYHYHOYIPCQYGCQRZOZFGHIIDSYGIIDTZYKYGCAUA
      ZUBMZIJZAIUCYMYGYPAICYNUDUEABIIYODEUFUGZUHYLFUAZIJZGUAZUIJZOSUIJUJUKYLYSH
      UAZIJZOZUUAYRUUBULMZUMUNZSUOUPZYRDUNZUUBDUNZULMZUMUNZYTUOUPZPZYKUUDUUAUUM
      YGYKUUDUUAUQZUULUUGUUNUUKQYTUOYKUUDUUKQRUUAYKUUDOCUUEUBMZUMUNZQUUEUBMZUMU
      NZUUKQYKUUPUURRUUDYKUUOUUQUMCQUUEUBURUSUHYGUUDUUPUUKRYKYGUUDOZUUOUUJUMUUS
      UUOCYRUBMZCUUBUBMZULMZUUJYGYSUUCUUOUVBRCYRUUBUTVAUUSUUHUUTUUIUVAULYGYSUUH
      UUTRZUUCYGYSUVCYSUUTIJUVCYGYSOABYRYOUUTIIDYNYRCUBVBEVCCYRUDVDVEVFYGUUCUUI
      UVARZYSYGUUCUVDUUCUVAIJUVDYGUUCOABUUBYOUVAIIDYNUUBCUBVBEVCCUUBUDVDVEVGVHV
      IUSZYKYGQIJVJCQIVKVLVMUUDUURQRYKUUDUURQUMUNQUUDUUQQUMUUDUUEIJZUUQQRYRUUBV
      NZUUEVOVPUSVQVRVSVTWAUUAYKQYTUOUPZUUDUUAYTWBJZUVHYTWCZWDWEWFWGWHWIWJYGCQW
      KZOZFGHIIDYTCUMUNZWLMZYGYMUVKYQUHUVLUUAUVNUIJZYSUUAUVLUVOUVOUVNWBJZQUVNUO
      UPZUUAUVLOUVNWCUVIUVMWBJZUVMQWKZOUVPUUAUVLUVIUVRUVSUVPYTUVMWMVAUUAUVIUVHU
      VJWNZUVLUVRUVSYGUVRUVKCWOZUHYGUVSUVKYGUVMQCQCWPWQWRWSWTUUAUVRQUVMUOUPZOZU
      VQUVLUVIUVHOUWCUVQUUAYTUVMXAUVJXBUVLYGUWBOZUWCYGUVKUWBYGUVKUWBCXCXDXEZYGU
      VRUWBUWAXFVPVDXGXHXIUVLUUDUUAOZUULUUFUVNUOUPZUVLUWFOZUVMUUFUBMZYTUOUPZUUL
      UWGUWHUWIUUKYTUOYGUUDUWIUUKRUVKUUAUUSUUPUWIUUKYGUVFUUPUWIRUUDCUUEXJUVGVDU
      VEXKXLXMUWFUVLUWJUWGXNZUWFUWDUWKUVLUWFYGUWBUWKYGUUDUUAUQZUWBUWKUWFYGOZUVR
      UUFWBJZUVIUWBUWKYGUUDUUAUVMUUFYTXOUWAUUDUVFUWNUVGUUEWOVPUVTXPUWLUUDUUAYGU
      QUWMYGUUDUUAXQUUDUUAYGXRXSXTYAUWEVDYBYCYDWJYEYF $.
      $( [28-Nov-2007] $)
  $}

  ${
    $d A x y $.
    divccncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( x / A ) ) } $.
    $( Division by a constant is continuous.  (Contributed by Paul Chapman,
       28-Nov-2007.) $)
    divccncf $p |- ( ( A e. CC /\ A =/= 0 ) -> F e. ( CC -cn-> CC ) ) $=
      ( cc wcel cc0 wne wa cv c1 cdiv co cmul wceq copab ccncf wb w3a divrec2
      eqeq2d 3expb expcom pm5.32d opabbidv syl5eq reccl eqid mulc1cncf syl
      eqeltrd ) CFGZCHIZJZDAKZFGZBKZLCMNZUPONZPZJZABQZFFRNZUOUQURUPCMNZPZJZABQV
      CDUOVGVBABUOUQVFVAUQUOVFVASZUQUMUNVHUQUMUNTVEUTURUPCUAUBUCUDUEUFEUGUOUSFG
      VCVDGCUHABUSVCVCUIUJUKUL $.
      $( [28-Nov-2007] $)
  $}


