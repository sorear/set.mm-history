$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Continuous complex functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the new symbol needed for continuous complex functions. $)
  $c -cn-> $.

  $( Extend class notation to include the operation which returns a class of
     continuous complex functions. $)
  ccncf $a class -cn-> $.

  ${
    $d a b d e f x y $.
    $( Define the operation whose value is a class of continuous complex
       functions. $)
    df-cncf $a |- -cn-> = ( a e. ~P CC , b e. ~P CC |->
      { f e. ( b ^m a ) | A. x e. a A. e e. RR+ E. d e. RR+ A. y e. a
  ( ( abs ` ( x - y ) ) < d -> ( abs ` ( ( f ` x ) - ( f ` y ) ) ) < e ) } ) $.
  $}

  ${
    $d a b f w x y z A $.  $d f w x y z F $.  $d w x y z P $.  $d w x y z Q $.
    $d b f w x y z B $.  $d w x y z C $.  $d w y z R $.
    $( The value of the continuous complex function operation is the set of
       continuous functions from ` A ` to ` B ` .  (Contributed by Paul
       Chapman, 11-Oct-2007.) $)
    cncfval $p |- ( ( A C_ CC /\ B C_ CC ) ->
      ( A -cn-> B ) = { f e. ( B ^m A ) | A. x e. A A. y e. RR+
        E. z e. RR+ A. w e. A ( ( abs ` ( x - w ) ) < z ->
        ( abs ` ( ( f ` x ) - ( f ` w ) ) ) < y ) } ) $=
      ( va vb cc wss wcel co cv cfv wral crp cmap crab wceq cpw ccncf cmin cabs
      clt wbr wi wrex cnex elpw2 oveq2 raleq rexbidv raleqbi1dv rabeqbidv oveq1
      ralbidv rabeq syl df-cncf ovex rabex ovmpt2 syl2anbr ) EJKEJUAZLFVELEFUBM
      ANZDNZUCMUDOCNUEUFVFGNZOVGVHOUCMUDOBNUEUFUGZDEPZCQUHZBQPZAEPZGFERMZSZTFJK
      EJUIUJFJUIUJHIEFVEVEVIDHNZPZCQUHZBQPZAVPPZGINZVPRMZSVOUBVMGWAERMZSZVPETZV
      TVMGWBWCVPEWARUKVSVLAVPEWEVRVKBQWEVQVJCQVIDVPEULUMUQUNUOWAFTWCVNTWDVOTWAF
      ERUPVMGWCVNURUSADBGHICUTVMGVNFERVAVBVCVD $.
      $( [9-Nov-2013] $) $( [11-Oct-2007] $)

    $( Membership in the set of continuous complex functions from ` A ` to
       ` B ` .  (Contributed by Paul Chapman, 11-Oct-2007.) $)
    elcncf $p |- ( ( A C_ CC /\ B C_ CC ) ->
     ( F e. ( A -cn-> B ) <-> ( F : A --> B /\ A. x e. A A. y e. RR+
     E. z e. RR+ A. w e. A ( ( abs ` ( x - w ) ) < z ->
     ( abs ` ( ( F ` x ) - ( F ` w ) ) ) < y ) ) ) ) $=
      ( vf cc wa co wcel cv cmin cabs cfv clt wral crp cvv wss cmap wbr wi wrex
      ccncf wf crab cncfval eleq2d wceq fveq1 oveq12d fveq2d breq12d rexralbidv
      eqidd imbi2d 2ralbidv elrab syl6bb cnex ssex elmapg syl2anr anbi1d bitrd
      wb ) EIUAZFIUAZJZGEFUFKZLZGFEUBKZLZAMZDMZNKOPCMQUCZVPGPZVQGPZNKZOPZBMZQUC
      ZUDZDERCSUEZBSRAERZJZEFGUGZWGJVKVMGVRVPHMZPZVQWJPZNKZOPZWCQUCZUDZDERCSUEZ
      BSRAERZHVNUHZLWHVKVLWSGABCDEFHUIUJWRWGHGVNWJGUKZWQWFABESWTWPWECDSEWTWOWDV
      RWTWNWBWCWCQWTWMWAOWTWKVSWLVTNVPWJGULVQWJGULUMUNWTWCUQUOURUPUSUTVAVKVOWIW
      GVJFTLETLVOWIVHVIFIVBVCEIVBVCFEGTTVDVEVFVG $.
      $( [9-Nov-2013] $) $( [11-Oct-2007] $)
    $( Version of ~ elcncf with arguments commuted.  (Contributed by Mario
       Carneiro, 28-Apr-2014.) $)
    elcncf2 $p |- ( ( A C_ CC /\ B C_ CC ) ->
     ( F e. ( A -cn-> B ) <-> ( F : A --> B /\ A. x e. A A. y e. RR+
     E. z e. RR+ A. w e. A ( ( abs ` ( w - x ) ) < z ->
     ( abs ` ( ( F ` w ) - ( F ` x ) ) ) < y ) ) ) ) $=
      ( cc wa co wcel cv cmin cabs cfv clt wbr wral crp sseldd ccncf wf wi wrex
      elcncf wb wceq simplll simprl simprr abssub syl2anc breq1d simpllr simplr
      wss ffvelrn imbi12d anassrs ralbidva rexbidv ralbidv pm5.32da bitrd ) EHU
      PZFHUPZIZGEFUAJKEFGUBZALZDLZMJNOZCLZPQZVIGOZVJGOZMJNOZBLZPQZUCZDERZCSUDZB
      SRZAERZIVHVJVIMJNOZVLPQZVOVNMJNOZVQPQZUCZDERZCSUDZBSRZAERZIABCDEFGUEVGVHW
      CWLVGVHIZWBWKAEWMVIEKZIZWAWJBSWOVTWICSWOVSWHDEWMWNVJEKZVSWHUFWMWNWPIZIZVM
      WEVRWGWRVKWDVLPWRVIHKVJHKVKWDUGWREHVIVEVFVHWQUHZWMWNWPUIZTWREHVJWSWMWNWPU
      JZTVIVJUKULUMWRVPWFVQPWRVNHKVOHKVPWFUGWRFHVNVEVFVHWQUNZWRVHWNVNFKVGVHWQUO
      ZWTEFVIGUQULTWRFHVOXBWRVHWPVOFKXCXAEFVJGUQULTVNVOUKULUMURUSUTVAVBUTVCVD
      $.
      $( [28-Apr-2014] $)

    $( A continuous complex function's domain and codomain.  (Contributed by
       Paul Chapman, 17-Jan-2008.) $)
    cncff $p |- ( ( P C_ CC /\ Q C_ CC /\ F e. ( P -cn-> Q ) ) ->
                  F : P --> Q ) $=
      ( vx vw vz vy cc wss ccncf co wcel cv cmin cabs cfv clt wbr wral crp wrex
      w3a wf wi wa elcncf biimp3a simpld ) AHIZBHIZCABJKLZUBABCUCZDMZEMZNKOPFMQ
      RUMCPUNCPNKOPGMQRUDEASFTUAGTSDASZUIUJUKULUOUEDGFEABCUFUGUH $.
      $( [17-Jan-2008] $)

    $( MAY BE REVISED - don't delete this until we decide to keep cnff $)
    $( A continuous complex function's value belongs to its codomain.
       (Contributed by Paul Chapman, 21-Jan-2008.) $)
    cncffvelrnOLD $p |- ( ( A C_ CC /\ B C_ CC /\ F e. ( A -cn-> B ) ) ->
                        ( C e. A -> ( F ` C ) e. B ) ) $=
      ( vx vw vz vy cc wss co wcel cfv wi cv cmin cabs clt wbr wral w3a wf wrex
      ccncf crp wa elcncf biimpd 3impia simpld ffvelrn ex syl ) AIJZBIJZDABUDKL
      ZUAZABDUBZCALZCDMBLZNUQUREOZFOZPKQMGORSVADMVBDMPKQMHORSNFATGUEUCHUETEATZU
      NUOUPURVCUFZUNUOUFUPVDEHGFABDUGUHUIUJURUSUTABCDUKULUM $.
      $( [21-Jan-2008] $)
    $( Defining property of a continuous function.  (Contributed by Mario
       Carneiro, 30-Apr-2014.) $)
    cncfi $p |- ( ( ( A C_ CC /\ B C_ CC /\ F e. ( A -cn-> B ) ) /\
                    ( C e. A /\ R e. RR+ ) ) ->
                  E. z e. RR+ A. w e. A ( ( abs ` ( w - C ) ) < z ->
                    ( abs ` ( ( F ` w ) - ( F ` C ) ) ) < R ) ) $=
      ( vx vy co wcel cv cmin cabs cfv clt wbr wi wral crp cc wss ccncf wrex wa
      w3a wf elcncf2 biimp3a simprd oveq2 fveq2d breq1d fveq2 oveq2d rexralbidv
      wceq imbi12d breq2 imbi2d rcla42v mpan9 ) CUAUBZDUAUBZGCDUCJKZUFZBLZHLZMJ
      ZNOZALZPQZVGGOZVHGOZMJZNOZILZPQZRZBCSATUDZITSHCSZECKFTKUEVGEMJZNOZVKPQZVM
      EGOZMJZNOZFPQZRZBCSATUDZVFCDGUGZWAVCVDVEWKWAUEHIABCDGUHUIUJVTWJWDWGVQPQZR
      ZBCSATUDHIEFCTVHEUQZVSWMABTCWNVLWDVRWLWNVJWCVKPWNVIWBNVHEVGMUKULUMWNVPWGV
      QPWNVOWFNWNVNWEVMMVHEGUNUOULUMURUPVQFUQZWMWIABTCWOWLWHWDVQFWGPUSUTUPVAVB
      $.
      $( [30-Apr-2014] $)
  $}

  $( A continuous complex function's value belongs to its codomain.
     (Contributed by Paul Chapman, 21-Jan-2008.) $)
  cncffvelrn $p |- ( ( A C_ CC /\ B C_ CC /\ F e. ( A -cn-> B ) ) ->
                      ( C e. A -> ( F ` C ) e. B ) ) $=
    ( cc wss ccncf co wcel w3a wf cfv wi cncff ffvelrn ex syl ) AEFBEFDABGHIJAB
    DKZCAIZCDLBIZMABDNRSTABCDOPQ $.
    $( [21-Jan-2008] $)


  ${
    $d w x y z A $.  $d w x y z B $.  $d w x y z F $.  $d w x y ph $.
    $d w z Z $.
    elcncf1d.1 $e |- ( ph -> F : A --> B ) $.
    elcncf1d.2 $e |- ( ph -> ( ( x e. A /\ y e. RR+ ) -> Z e. RR+ ) ) $.
    elcncf1d.3 $e |- ( ph -> ( ( ( x e. A /\ w e. A ) /\ y e. RR+ ) ->
                       ( ( abs ` ( x - w ) ) < Z ->
                         ( abs ` ( ( F ` x ) - ( F ` w ) ) ) < y ) ) ) $.
    $( Membership in the set of continuous complex functions from ` A ` to
       ` B ` .  (Contributed by Paul Chapman, 26-Nov-2007.) $)
    elcncf1di $p |- ( ph ->
                   ( ( A C_ CC /\ B C_ CC ) -> F e. ( A -cn-> B ) ) ) $=
      ( vz co wcel wa cv cfv clt wral crp ccncf cc wss wf cmin cabs wbr wi wrex
      imp an32 anbi2i anass bitr4i sylbir ralrimiva wceq imbi1d ralbidv rcla4ev
      breq2 syl2anc anassrs jca elcncf syl5ibrcom ) AGEFUAMNEUBUCFUBUCOEFGUDZBP
      ZDPZUEMUFQZLPZRUGZVHGQVIGQUEMUFQCPZRUGZUHZDESZLTUIZCTSZBESZOAVGVSIAVRBEAV
      HENZOVQCTAVTVMTNZVQAVTWAOZOZHTNZVJHRUGZVNUHZDESZVQAWBWDJUJWCWFDEWCVIENZOZ
      AVTWHOWAOZOZWFWKAWBWHOZOWIWJWLAVTWHWAUKULAWBWHUMUNAWJWFKUJUOUPVPWGLHTVKHU
      QZVOWFDEWMVLWEVNVKHVJRVAURUSUTVBVCUPUPVDBCLDEFGVEVF $.
      $( [26-Nov-2007] $)
  $}

  ${
    $d w x y A $.  $d w x y B $.  $d w x y F $.  $d w Z $.
    elcncf1i.1 $e |- F : A --> B $.
    elcncf1i.2 $e |- ( ( x e. A /\ y e. RR+ ) -> Z e. RR+ ) $.
    elcncf1i.3 $e |- ( ( ( x e. A /\ w e. A ) /\ y e. RR+ ) ->
                       ( ( abs ` ( x - w ) ) < Z ->
                         ( abs ` ( ( F ` x ) - ( F ` w ) ) ) < y ) ) $.
    $( Membership in the set of continuous complex functions from ` A ` to
       ` B ` .  (Contributed by Paul Chapman, 26-Nov-2007.) $)
    elcncf1ii $p |- ( ( A C_ CC /\ B C_ CC ) -> F e. ( A -cn-> B ) ) $=
      ( cc wss wa co wcel wi wtru a1i cv cfv wf crp cmin cabs clt wbr elcncf1di
      ccncf trud ) DKLEKLMFDEUHNOPQABCDEFGDEFUAQHRASZDOZBSZUBOZMGUBOPQIRUKCSZDO
      MUMMUJUNUCNUDTGUEUFUJFTUNFTUCNUDTULUEUFPPQJRUGUI $.
      $( [26-Nov-2007] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d w x y z C $.  $d w x y z F $.
    $( A continuous complex function restricted to a subset is continuous.
       (Contributed by Paul Chapman, 18-Oct-2007.) $)
    rescncf $p |- ( ( A C_ CC /\ B C_ CC /\ C C_ A ) ->
            ( F e. ( A -cn-> B ) -> ( F |` C ) e. ( C -cn-> B ) ) ) $=
      ( vx vw vz vy cc wss cv cmin co cabs cfv clt wral crp wa wcel w3a wf wrex
      wbr cres ccncf fssres expcom ssralv fvres oveqan12d fveq2d breq1d biimprd
      wi imbi2d ralimdva sylan9 reximdv ralimdv syld anim12d 3ad2ant3 wb elcncf
      3adant3 sstr anim1i 3impa 3coml syl 3imtr4d ) AIJZBIJZCAJZUAZABDUBZEKZFKZ
      LMNOGKPUDZVRDOZVSDOZLMZNOZHKZPUDZUOZFAQZGRUCZHRQZEAQZSZCBDCUEZUBZVTVRWMOZ
      VSWMOZLMZNOZWEPUDZUOZFCQZGRUCZHRQZECQZSZDABUFMTZWMCBUFMTZVOVMWLXEUOVNVOVQ
      WNWKXDVQVOWNABCDUGUHVOWKWJECQXDWJECAUIVOWJXCECVOVRCTZSZWIXBHRXIWHXAGRVOWH
      WGFCQXHXAWGFCAUIXHWGWTFCXHVSCTZSZWTWGXKWSWFVTXKWRWDWEPXKWQWCNXHXJWOWAWPWB
      LVRCDUJVSCDUJUKULUMUPUNUQURUSUTUQVAVBVCVMVNXFWLVDVOEHGFABDVEVFVPCIJZVNSZX
      GXEVDVOVMVNXMVOVMVNXMVOVMSXLVNCAIVGVHVIVJEHGFCBWMVEVKVL $.
      $( [18-Oct-2007] $)

    $( Change the codomain of a continuous complex function.  (Contributed by
       Paul Chapman, 18-Oct-2007.) $)
    cncffvrn $p |- ( ( ( A C_ CC /\ B C_ CC /\ C C_ CC ) /\
      A. x e. A ( F ` x ) e. C ) ->
      ( F e. ( A -cn-> B ) -> F e. ( A -cn-> C ) ) ) $=
      ( vw vz vy cc wss cv cfv wcel wral wa wf cmin co cabs clt w3a wbr wi wrex
      crp ccncf crn wfn ffn fnfvrnss sylan dffn3 sylib fss syldan expcom anim1d
      adantl wb elcncf 3adant3 adantr 3adant2 3imtr4d ) BIJZCIJZDIJZUAZAKZELZDM
      ABNZOBCEPZVIFKZQRSLGKTUBVJVMELQRSLHKTUBUCFBNGUEUDHUENABNZOZBDEPZVNOZEBCUF
      RMZEBDUFRMZVKVOVQUCVHVKVLVPVNVLVKVPVLVKEUGZDJZVPVLEBUHZVKWABCEUIZABDEUJUK
      VLBVTEPZWAVPVLWBWDWCBEULUMBVTDEUNUKUOUPUQURVHVRVOUSZVKVEVFWEVGAHGFBCEUTVA
      VBVHVSVQUSZVKVEVGWFVFAHGFBDEUTVCVBVD $.
      $( [18-Oct-2007] $)
  $}

  ${
    $d w x y z $.
    $( Absolute value is continuous.  (Contributed by Paul Chapman,
       21-Oct-2007.) $)
    abscncf $p |- abs e. ( CC -cn-> RR ) $=
      ( vw vx vz vy cabs cc cr ccncf co wcel wf cv cmin cfv clt wbr wi wral crp
      wss wrex wa wb ssid ax-resscn elcncf2 mp2an absf abscn2 rgen2 mpbir2an )
      EFGHIJZFGEKZALZBLZMIENCLOPUNENUOENMIENDLOPQAFRCSUAZDSRBFRZFFTGFTULUMUQUBU
      CFUDUEBDCAFGEUFUGUHUPBDFSDCAUOUIUJUK $.
      $( [28-Apr-2014] $) $( [21-Oct-2007] $)

    $( Real part is continuous.  (Contributed by Paul Chapman, 21-Oct-2007.) $)
    recncf $p |- Re e. ( CC -cn-> RR ) $=
      ( vw vx vz vy cre cc cr ccncf co wcel wf cv cmin cabs cfv clt wbr crp wss
      wral wi wrex wa wb ssid ax-resscn elcncf2 mp2an ref recn2 rgen2 mpbir2an
      ) EFGHIJZFGEKZALZBLZMINOCLPQUOEOUPEOMINODLPQUAAFTCRUBZDRTBFTZFFSGFSUMUNUR
      UCUDFUEUFBDCAFGEUGUHUIUQBDFRDCAUPUJUKUL $.
      $( [28-Apr-2014] $) $( [21-Oct-2007] $)

    $( Imaginary part is continuous.  (Contributed by Paul Chapman,
       21-Oct-2007.) $)
    imcncf $p |- Im e. ( CC -cn-> RR ) $=
      ( vw vx vz vy cim cc cr ccncf co wcel wf cv cmin cabs cfv clt wbr crp wss
      wral wi wrex wa wb ssid ax-resscn elcncf2 mp2an imf imcn2 rgen2 mpbir2an
      ) EFGHIJZFGEKZALZBLZMINOCLPQUOEOUPEOMINODLPQUAAFTCRUBZDRTBFTZFFSGFSUMUNUR
      UCUDFUEUFBDCAFGEUGUHUIUQBDFRDCAUPUJUKUL $.
      $( [28-Apr-2014] $) $( [21-Oct-2007] $)

    $( Complex conjugate is continuous.  (Contributed by Paul Chapman,
       21-Oct-2007.) $)
    cjcncf $p |- * e. ( CC -cn-> CC ) $=
      ( vw vx vz vy ccj cc ccncf co wcel wf cmin cabs cfv clt wbr wral crp wrex
      cv wi wss wa wb ssid elcncf2 mp2an cjf cjcn2 rgen2 mpbir2an ) EFFGHIZFFEJ
      ZASZBSZKHLMCSNOUMEMUNEMKHLMDSNOTAFPCQRZDQPBFPZFFUAZUQUKULUPUBUCFUDZURBDCA
      FFEUEUFUGUOBDFQDCAUNUHUIUJ $.
      $( [28-Apr-2014] $) $( [21-Oct-2007] $)
  $}

  ${
    $d t u v w x y z A $.  $d t u w y z F $.
    mulc1cncf.1 $e |- F = ( x e. CC |-> ( A x. x ) ) $.
    $( Multiplication by a constant is continuous.  (Contributed by Paul
       Chapman, 28-Nov-2007.)  (Revised by Mario Carneiro, 30-Apr-2014.) $)
    mulc1cncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( vu vw cc wcel cv cmin co cabs cfv clt wbr wral crp cmul wa cc0 vy vz vv
      vt wf wi wrex ccncf mulcl fmptd simprr simpl simprl mulcn2 syl3anc fveq2d
      wceq oveq1 breq1d anbi1d oveq1d imbi12d ralbidv rcla4v ad2antrr wb syl6eq
      subid abs0 simprll rpgt0 syl eqbrtrd biantrurd oveq2 ovex simplrl oveq12d
      fvmpt anassrs ralbidva sylibrd reximdva rexlimdva mpd ralrimivva wss ssid
      elcncf2 mp2an sylanbrc ) BGHZGGCUEZEIZUAIZJKLMFIZNOZWNCMZWOCMZJKZLMZUBIZN
      OZUFZEGPZFQUGZUBQPUAGPZCGGUHKHZWLAGBAIZRKZGCBXIUIDUJWLXFUAUBGQWLWOGHZXBQH
      ZSZSZUCIZBJKZLMZUDIZNOZWQSZXOWNRKZBWORKZJKZLMZXBNOZUFZEGPZUCGPZFQUGZUDQUG
      ZXFXNXLWLXKYJWLXKXLUKWLXMULWLXKXLUMUDFEUCXBBWOUNUOXNYIXFUDQXNXRQHZSYHXEFQ
      XNYKWPQHZYHXEUFXNYKYLSZSZYHBBJKZLMZXRNOZWQSZBWNRKZYBJKZLMZXBNOZUFZEGPZXEW
      LYHUUDUFXMYMYGUUDUCBGXOBUQZYFUUCEGUUEXTYRYEUUBUUEXSYQWQUUEXQYPXRNUUEXPYOL
      XOBBJURUPUSUTUUEYDUUAXBNUUEYCYTLUUEYAYSYBJXOBWNRURVAUPUSVBVCVDVEYNXDUUCEG
      XNYMWNGHZXDUUCVFXNYMUUFSZSZWQYRXCUUBUUHYQWQUUHYPTXRNUUHYPTLMTUUHYOTLWLYOT
      UQXMUUGBVHVEUPVIVGUUHYKTXRNOXNYKYLUUFVJXRVKVLVMVNUUHXAUUAXBNUUHWTYTLUUHWR
      YSWSYBJUUHUUFWRYSUQXNYMUUFUKAWNXJYSGCXIWNBRVODBWNRVPVSVLUUHXKWSYBUQWLXKXL
      UUGVQAWOXJYBGCXIWOBRVODBWORVPVSVLVRUPUSVBVTWAWBVTWCWDWEWFGGWGZUUIXHWMXGSV
      FGWHZUUJUAUBFEGGCWIWJWK $.
      $( [30-Apr-2014] $) $( [28-Nov-2007] $)
  $}

  ${
    $d A x $.
    divccncf.1 $e |- F = ( x e. CC |-> ( x / A ) ) $.
    $( Division by a constant is continuous.  (Contributed by Paul Chapman,
       28-Nov-2007.) $)
    divccncf $p |- ( ( A e. CC /\ A =/= 0 ) -> F e. ( CC -cn-> CC ) ) $=
      ( cc wcel cc0 wne wa c1 cdiv co cmul cmpt ccncf wceq divrec2 3expb ancoms
      cv mpteq2dva syl5eq reccl eqid mulc1cncf syl eqeltrd ) BEFZBGHZIZCAEJBKLZ
      ATZMLZNZEEOLZUJCAEULBKLZNUNDUJAEUPUMULEFZUJUPUMPZUQUHUIURULBQRSUAUBUJUKEF
      UNUOFBUCAUKUNUNUDUEUFUG $.
      $( [28-Nov-2007] $)
  $}

  ${
    $d u w x y z A $.  $d u v w z B $.  $d u v w x y z C $.  $d u w x y z ph $.
    $d u v w x y z F $.  $d u v w x y z G $.
    cncfco.1 $e |- ( ph -> A C_ CC ) $.
    cncfco.2 $e |- ( ph -> B C_ CC ) $.
    cncfco.3 $e |- ( ph -> C C_ CC ) $.
    cncfco.4 $e |- ( ph -> F e. ( A -cn-> B ) ) $.
    cncfco.5 $e |- ( ph -> G e. ( B -cn-> C ) ) $.
    $( The composition of two continuous maps on complex numbers is also
       continuous.  (Contributed by Jeff Madsen, 2-Sep-2009.)  (Revised by
       Mario Carneiro, 30-Apr-2014.) $)
    cncfco $p |- ( ph -> ( G o. F ) e. ( A -cn-> C ) ) $=
      ( co wcel cmin cabs cfv clt wi crp wa vw vx vz vy vv vu ccom ccncf wf wbr
      cv wral cc wss wb elcncf2 syl2anc cncff syl3anc fco adantr simprl ffvelrn
      wrex simprr cncfi syl32anc ad2antrr simplrl simpr ad3antrrr fveq2d breq1d
      wceq oveq1 fveq2 oveq1d imbi12d rcla4v syl wfun ffun fvco3 oveq12d imbi2d
      sylibrd imp imim2d anassrs ralimdva reximdva ex mpid rexlimdva ralrimivva
      an32s mpd mpbir2and ) AFEUGZBDUHLMZBDWSUIZUAUKZUBUKZNLOPUCUKZQUJZXBWSPZXC
      WSPZNLZOPZUDUKZQUJZRZUABULZUCSVDZUDSULUBBULZABUMUNZDUMUNZWTXAXOTUOGIUBUDU
      CUABDWSUPUQACDFUIZBCEUIZXAACUMUNZXQFCDUHLMZXRHIKCDFURUSZAXPXTEBCUHLMZXSGH
      JBCEURUSZBCDFEUTUQAXNUBUDBSAXCBMZXJSMZTZTZUEUKZXCEPZNLZOPZUFUKZQUJZYIFPZY
      JFPZNLZOPZXJQUJZRZUECULZUFSVDZXNYHXTXQYAYJCMZYFUUBAXTYGHVAAXQYGIVAAYAYGKV
      AYHXSYEUUCAXSYGYDVAAYEYFVBBCXCEVCUQAYEYFVEUFUECDYJXJFVFVGYHUUAXNUFSYHYMSM
      ZTZUUAXEXBEPZYJNLZOPZYMQUJZRZUABULZUCSVDZXNUUEXPXTYCYEUUDUULAXPYGUUDGVHAX
      TYGUUDHVHAYCYGUUDJVHAYEYFUUDVIZYHUUDVJUCUABCXCYMEVFVGUUEUUAUULXNRUUEUUATZ
      UUKXMUCSUUNXDSMZTUUJXLUABUUNUUOXBBMZUUJXLRUUNUUOUUPTZTUUIXKXEUUEUUQUUAUUI
      XKRZUUEUUQTZUUAUURUUSUUAUUIUUFFPZYPNLZOPZXJQUJZRZUURUUSUUFCMZUUAUVDRUUSXS
      UUPUVEAXSYGUUDUUQYDVKZUUEUUOUUPVEZBCXBEVCUQYTUVDUEUUFCYIUUFVNZYNUUIYSUVCU
      VHYLUUHYMQUVHYKUUGOYIUUFYJNVOVLVMUVHYRUVBXJQUVHYQUVAOUVHYOUUTYPNYIUUFFVPV
      QVLVMVRVSVTUUSXKUVCUUIUUSXIUVBXJQUUSXHUVAOUUSXFUUTXGYPNUUSFWAZXSUUPXFUUTV
      NAUVIYGUUDUUQAXRUVIYBCDFWBVTVKZUVFUVGBCXBFEWCUSUUSUVIXSYEXGYPVNUVJUVFUUEY
      EUUQUUMVABCXCFEWCUSWDVLVMWEWFWGWPWHWIWJWKWLWMWNWQWOWR $.
      $( [30-Apr-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d x y F $.  $d x A $.
    negfcncf.1 $e |- G = ( x e. A |-> -u ( F ` x ) ) $.
    $( The negative of a continuous complex function is continuous.
       (Contributed by Paul Chapman, 21-Jan-2008.) $)
    negfcncf $p |- ( ( A C_ CC /\ F e. ( A -cn-> CC ) ) ->
                      G e. ( A -cn-> CC ) ) $=
      ( vy cc wss ccncf co wcel c1 cneg cv cmpt ccom wceq syl crn negex wa cmul
      cfv wfn ssid cncff mp3an2 ffn dffn5v sylib coeq2d rneqd frn eqsstr3d fvex
      negeq mulm1 mpteq2ia fmptco eqtrd simpl a1i simpr ax-1cn negcli mulc1cncf
      wf eqid ax-mp cncfco eqeltrrd ) BGHZCBGIJZKZUAZFGLMZFNZUBJZOZCPZDVMVOVTVS
      ABANZCUCZOZPZDVOCWCVSVOCBUDZCWCQVOBGCVGZWEVLGGHZVNWFGUEZBGCUFUGZBGCUHRABC
      UIUJZUKVOWCSZGHWDDQVOWKCSZGVOCWCWJULVOWFWLGHWIBGCUMRUNAFBGWBVQMZWBMWCVSDW
      ACUOVQTWBTVQWBUPWCVHFGVRWMVQUQUREUSRUTVOBGGCVSVLVNVAWGVOWHVBZWNVLVNVCVSGG
      IJKZVOVPGKWOLVDVEFVPVSVSVHVFVIVBVJVK $.
      $( [30-Apr-2014] $) $( [21-Jan-2008] $)
  $}


