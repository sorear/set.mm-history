$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Continuous complex functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the new symbol needed for continuous complex functions. $)
  $c -cn-> $.

  $( Extend class notation to include the operation which returns a class of
     continuous complex functions. $)
  ccncf $a class -cn-> $.

  ${
    $d a b d e f x y $.
    $( Define the operation whose value is a class of continuous complex
       functions. $)
    df-cncf $a |- -cn-> = ( a e. ~P CC , b e. ~P CC |->
      { f e. ( b ^m a ) | A. x e. a A. e e. RR+ E. d e. RR+ A. y e. a
  ( ( abs ` ( x - y ) ) < d -> ( abs ` ( ( f ` x ) - ( f ` y ) ) ) < e ) } ) $.
  $}

  ${
    $d a b f w x y z A $.  $d b f B $.  $d f w x y z F $.  $d w x y z P $.
    $( The value of the continuous complex function operation is the set of
       continuous functions from ` A ` to ` B ` .  (Contributed by Paul
       Chapman, 11-Oct-2007.) $)
    cncfval $p |- ( ( A C_ CC /\ B C_ CC ) ->
      ( A -cn-> B ) = { f e. ( B ^m A ) | A. x e. A A. y e. RR+
        E. z e. RR+ A. w e. A ( ( abs ` ( x - w ) ) < z ->
        ( abs ` ( ( f ` x ) - ( f ` w ) ) ) < y ) } ) $=
      ( va vb cc wss wcel co cv cfv wral crp cmap crab wceq cpw ccncf cmin cabs
      clt wbr wi wrex ax-cnex elpw2 opreq2 rexbidv ralbidv raleqbi1dv rabeqbidv
      raleq opreq1 rabeq syl df-cncf oprex rabex ovmpt2 syl2anbr ) EJKEJUAZLFVE
      LEFUBMANZDNZUCMUDOCNUEUFVFGNZOVGVHOUCMUDOBNUEUFUGZDEPZCQUHZBQPZAEPZGFERMZ
      SZTFJKEJUIUJFJUIUJHIEFVEVEVIDHNZPZCQUHZBQPZAVPPZGINZVPRMZSVOUBVMGWAERMZSZ
      VPETZVTVMGWBWCVPEWARUKVSVLAVPEWEVRVKBQWEVQVJCQVIDVPEUPULUMUNUOWAFTWCVNTWD
      VOTWAFERUQVMGWCVNURUSADBGHICUTVMGVNFERVAVBVCVD $.
      $( [9-Nov-2013] $) $( [11-Oct-2007] $)

    $( Membership in the set of continuous complex functions from ` A ` to
       ` B ` .  (Contributed by Paul Chapman, 11-Oct-2007.) $)
    elcncf $p |- ( ( A C_ CC /\ B C_ CC ) ->
     ( F e. ( A -cn-> B ) <-> ( F : A --> B /\ A. x e. A A. y e. RR+
     E. z e. RR+ A. w e. A ( ( abs ` ( x - w ) ) < z ->
     ( abs ` ( ( F ` x ) - ( F ` w ) ) ) < y ) ) ) ) $=
      ( vf cc wa co wcel cv cmin cabs cfv clt wral crp cvv wss cmap wbr wi wrex
      ccncf crab cncfval eleq2d wceq fveq1 opreq12d fveq2d eqidd breq12d imbi2d
      wf rexralbidv 2ralbidv elrab syl6bb wb ax-cnex ssex elmapg syl2anc anbi1d
      bitrd ) EIUAZFIUAZJZGEFUFKZLZGFEUBKZLZAMZDMZNKOPCMQUCZVPGPZVQGPZNKZOPZBMZ
      QUCZUDZDERCSUEZBSRAERZJZEFGUQZWGJVKVMGVRVPHMZPZVQWJPZNKZOPZWCQUCZUDZDERCS
      UEZBSRAERZHVNUGZLWHVKVLWSGABCDEFHUHUIWRWGHGVNWJGUJZWQWFABESWTWPWECDSEWTWO
      WDVRWTWNWBWCWCQWTWMWAOWTWKVSWLVTNVPWJGUKVQWJGUKULUMWTWCUNUOUPURUSUTVAVKVO
      WIWGVJFTLETLVOWIVBVIFIVCVDEIVCVDFEGTTVEVFVGVH $.
      $( [9-Nov-2013] $) $( [11-Oct-2007] $)

    $( A continuous complex function's domain and codomain.  (Contributed by
       Paul Chapman, 17-Jan-2008.) $)
    cncff $p |- ( ( P C_ CC /\ Q C_ CC /\ F e. ( P -cn-> Q ) ) ->
                  F : P --> Q ) $=
      ( vx vw vz vy cc wss ccncf co wcel cv cmin cabs cfv clt wbr wral crp wrex
      w3a wf wi wa elcncf biimp3a simpld ) AHIZBHIZCABJKLZUBABCUCZDMZEMZNKOPFMQ
      RUMCPUNCPNKOPGMQRUDEASFTUAGTSDASZUIUJUKULUOUEDGFEABCUFUGUH $.
      $( [17-Jan-2008] $)

    $( MAY BE REVISED - don't delete this until we decide to keep cnff $)
    $( A continuous complex function's value belongs to its codomain.
       (Contributed by Paul Chapman, 21-Jan-2008.) $)
    cncffvelrnOLD $p |- ( ( A C_ CC /\ B C_ CC /\ F e. ( A -cn-> B ) ) ->
                        ( C e. A -> ( F ` C ) e. B ) ) $=
      ( vx vw vz vy cc wss co wcel cfv wi cv cmin cabs clt wbr wral w3a wf wrex
      ccncf crp wa elcncf biimpd 3impia simpld ffvelrn ex syl ) AIJZBIJZDABUDKL
      ZUAZABDUBZCALZCDMBLZNUQUREOZFOZPKQMGORSVADMVBDMPKQMHORSNFATGUEUCHUETEATZU
      NUOUPURVCUFZUNUOUFUPVDEHGFABDUGUHUIUJURUSUTABCDUKULUM $.
      $( [21-Jan-2008] $)
  $}

  $( A continuous complex function's value belongs to its codomain.
     (Contributed by Paul Chapman, 21-Jan-2008.) $)
  cncffvelrn $p |- ( ( A C_ CC /\ B C_ CC /\ F e. ( A -cn-> B ) ) ->
                      ( C e. A -> ( F ` C ) e. B ) ) $=
    ( cc wss ccncf co wcel w3a wf cfv wi cncff ffvelrn ex syl ) AEFBEFDABGHIJAB
    DKZCAIZCDLBIZMABDNRSTABCDOPQ $.
    $( [21-Jan-2008] $)


  ${
    $d A a b w x $.  $d A w x y z $.  $d F a b w x $.  $d F w x y z $.
    $d G w x y z $.
    negfcncf.1 $e |- A C_ CC $.
    negfcncf.2 $e |- F e. ( A -cn-> CC ) $.
    negfcncf.3 $e |- G = { <. a , b >. | ( a e. A /\ b = -u ( F ` a ) ) } $.
    $( The negative of a continuous complex function is continuous.
       (Contributed by Paul Chapman, 21-Jan-2008.) $)
    negfcncfi $p |- G e. ( A -cn-> CC ) $=
      ( vx vw vz vy cc co wcel cv cmin cfv wral crp ccncf cabs clt wbr wrex wss
      wf wi wa ssid elcncf mp2an cneg mpbi simpli ffvelrni negcl syl rgen fveq2
      wceq negeqd eleq1d cbvralv fopab2 simpri negex fvopab4 opreqan12d neg2sub
      syl2an eqtrd fveq2d abssub eqtr4d breq1d ralbidva rexbidv ralbidv ralbiia
      wb imbi2d mpbir mpbir2an ) CAMUANZOZAMCUGZIPZJPZQNUBRKPUCUDZWHCRZWICRZQNZ
      UBRZLPZUCUDZUHZJASZKTUEZLTSZIASZAMUFZMMUFZWFWGXAUIWAFMUJZILKJAMCUKULDPZBR
      ZUMZMOZDASZWGWHBRZUMZMOZIASXIXLIAWHAOZXJMOZXLAMWHBAMBUGZWJXJWIBRZQNUBRZWO
      UCUDZUHZJASZKTUEZLTSZIASZBWEOZXOYCUIZGXBXCYDYEWAFXDILKJAMBUKULUNZUOZUPZXJ
      UQURUSXLXHIDAWHXEVAZXKXGMYIXJXFWHXEBUTVBVCVDUNDEAMXGCHVEUNXAYCXOYCYFVFWTY
      BIAXMWSYALTXMWRXTKTXMWQXSJAXMWIAOZUIZWPXRWJYKWNXQWOUCYKWNXPXJQNZUBRZXQYKW
      MYLUBYKWMXKXPUMZQNZYLXMYJWKXKWLYNQDEWHXGXKACXEWHVAXFXJXEWHBUTVBHXJVGVHDEW
      IXGYNACXEWIVAXFXPXEWIBUTVBHXPVGVHVIXMXNXPMOZYOYLVAYJYHAMWIBYGUPZXJXPVJVKV
      LVMXMXNYPXQYMVAYJYHYQXJXPVNVKVOVPWBVQVRVSVTWCWD $.
      $( [21-Jan-2008] $)
  $}

  ${
    $d A w x y z $.  $d F w x y z $.  $d Z w z $.  $d ph w x y $.
    elcncf1d.1 $e |- ( ph -> F : A --> B ) $.
    elcncf1d.2 $e |- ( ph -> ( ( x e. A /\ y e. RR+ ) -> Z e. RR+ ) ) $.
    elcncf1d.3 $e |- ( ph -> ( ( ( x e. A /\ w e. A ) /\ y e. RR+ ) ->
                       ( ( abs ` ( x - w ) ) < Z ->
                         ( abs ` ( ( F ` x ) - ( F ` w ) ) ) < y ) ) ) $.
    $( Membership in the set of continuous complex functions from ` A ` to
       ` B ` .  (Contributed by Paul Chapman, 26-Nov-2007.) $)
    elcncf1di $p |- ( ph ->
                   ( ( A C_ CC /\ B C_ CC ) -> F e. ( A -cn-> B ) ) ) $=
      ( vz co wcel wa cv cfv clt wral crp ccncf cc wss wf cmin cabs wbr wi wrex
      imp an32 anbi2i anass bitr4i sylbir ralrimiva wceq imbi1d ralbidv rcla4ev
      breq2 syl11anc anassrs jca elcncf syl5ibrcom ) AGEFUAMNEUBUCFUBUCOEFGUDZB
      PZDPZUEMUFQZLPZRUGZVHGQVIGQUEMUFQCPZRUGZUHZDESZLTUIZCTSZBESZOAVGVSIAVRBEA
      VHENZOVQCTAVTVMTNZVQAVTWAOZOZHTNZVJHRUGZVNUHZDESZVQAWBWDJUJWCWFDEWCVIENZO
      ZAVTWHOWAOZOZWFWKAWBWHOZOWIWJWLAVTWHWAUKULAWBWHUMUNAWJWFKUJUOUPVPWGLHTVKH
      UQZVOWFDEWMVLWEVNVKHVJRVAURUSUTVBVCUPUPVDBCLDEFGVEVF $.
      $( [26-Nov-2007] $)
  $}

  ${
    $d A w x y $.  $d F w x y $.  $d Z w $.  $d ph w x y $.
    elcncf1i.1 $e |- F : A --> B $.
    elcncf1i.2 $e |- ( ( x e. A /\ y e. RR+ ) -> Z e. RR+ ) $.
    elcncf1i.3 $e |- ( ( ( x e. A /\ w e. A ) /\ y e. RR+ ) ->
                       ( ( abs ` ( x - w ) ) < Z ->
                         ( abs ` ( ( F ` x ) - ( F ` w ) ) ) < y ) ) $.
    $( Membership in the set of continuous complex functions from ` A ` to
       ` B ` .  (Contributed by Paul Chapman, 26-Nov-2007.) $)
    elcncf1ii $p |- ( ( A C_ CC /\ B C_ CC ) -> F e. ( A -cn-> B ) ) $=
      ( wph wi cc wss wa co wcel a1i cv cfv ccncf id wf crp cmin cabs elcncf1di
      clt wbr ax-mp ) KKLZDMNEMNOFDEUAPQLKUBUKABCDEFGDEFUCUKHRASZDQZBSZUDQZOGUD
      QLUKIRUMCSZDQOUOOULUPUEPUFTGUHUIULFTUPFTUEPUFTUNUHUILLUKJRUGUJ $.
      $( [26-Nov-2007] $)
  $}

  ${
    $d A w x y z $.  $d C w x y z $.  $d F w x y z $.
    $( A continuous complex function restricted to a subset is continuous.
       (Contributed by Paul Chapman, 18-Oct-2007.) $)
    rescncf $p |- ( ( A C_ CC /\ B C_ CC /\ C C_ A ) ->
            ( F e. ( A -cn-> B ) -> ( F |` C ) e. ( C -cn-> B ) ) ) $=
      ( vx vw vz vy cc wss cv cmin co cabs cfv clt wral crp wa wcel w3a wf wrex
      wbr cres ccncf fssres expcom ssralv fvres opreqan12d fveq2d breq1d imbi2d
      wi biimprd ralimdva sylan9 reximdv ralimdv syld anim12d wb elcncf 3adant3
      3ad2ant3 sstr anim1i 3impa 3coml syl 3imtr4d ) AIJZBIJZCAJZUAZABDUBZEKZFK
      ZLMNOGKPUDZVRDOZVSDOZLMZNOZHKZPUDZUOZFAQZGRUCZHRQZEAQZSZCBDCUEZUBZVTVRWMO
      ZVSWMOZLMZNOZWEPUDZUOZFCQZGRUCZHRQZECQZSZDABUFMTZWMCBUFMTZVOVMWLXEUOVNVOV
      QWNWKXDVQVOWNABCDUGUHVOWKWJECQXDWJECAUIVOWJXCECVOVRCTZSZWIXBHRXIWHXAGRVOW
      HWGFCQXHXAWGFCAUIXHWGWTFCXHVSCTZSZWTWGXKWSWFVTXKWRWDWEPXKWQWCNXHXJWOWAWPW
      BLVRCDUJVSCDUJUKULUMUNUPUQURUSUTUQVAVBVFVMVNXFWLVCVOEHGFABDVDVEVPCIJZVNSZ
      XGXEVCVOVMVNXMVOVMVNXMVOVMSXLVNCAIVGVHVIVJEHGFCBWMVDVKVL $.
      $( [18-Oct-2007] $)

    $( Change the codomain of a continuous complex function.  (Contributed by
       Paul Chapman, 18-Oct-2007.) $)
    cncffvrn $p |- ( ( ( A C_ CC /\ B C_ CC /\ C C_ CC ) /\
      A. x e. A ( F ` x ) e. C ) ->
      ( F e. ( A -cn-> B ) -> F e. ( A -cn-> C ) ) ) $=
      ( vw vz vy cc wss cv cfv wcel wral wa wf cmin co cabs clt w3a wbr wi wrex
      crp ccncf crn wfn ffn fnfvrnss sylan dffn3 sylib fss syldan expcom anim1d
      adantl wb elcncf 3adant3 adantr 3adant2 3imtr4d ) BIJZCIJZDIJZUAZAKZELZDM
      ABNZOBCEPZVIFKZQRSLGKTUBVJVMELQRSLHKTUBUCFBNGUEUDHUENABNZOZBDEPZVNOZEBCUF
      RMZEBDUFRMZVKVOVQUCVHVKVLVPVNVLVKVPVLVKEUGZDJZVPVLEBUHZVKWABCEUIZABDEUJUK
      VLBVTEPZWAVPVLWBWDWCBEULUMBVTDEUNUKUOUPUQURVHVRVOUSZVKVEVFWEVGAHGFBCEUTVA
      VBVHVSVQUSZVKVEVGWFVFAHGFBDEUTVCVBVD $.
      $( [18-Oct-2007] $)
  $}

  ${
    $d F w x y $.
    abscncflem.1 $e |- A C_ CC $.
    abscncflem.2 $e |- F : CC --> A $.
    abscncflem.3 $e |- ( ( x e. CC /\ w e. CC ) ->
      ( abs ` ( ( F ` x ) - ( F ` w ) ) ) <_ ( abs ` ( x - w ) ) ) $.
    $( Lemma for ~ abscncf , ~ recncf , ~ imcncf , and ~ cjcncf . $)
    abscncflem $p |- F e. ( CC -cn-> A ) $=
      ( vy cc wss co wcel cv wa cmin cabs cfv clt wbr cr ccncf crp simpr wi cle
      ssid ancli anim1i anass sylib fss mp2an ffvelrni anim12i subcl abscl 3syl
      syl rpre lelttr syl3an 3anidm12 expcom imp3a syl5 exp3a impcom elcncf1ii
      wf ) IIJCIJZDICUAKLIUFEAHBICDHMZFAMZILZVKUBLZUCVNVMBMZILZNZVLVOOKZPQZVKRS
      ZVLDQZVODQZOKZPQZVKRSZUDVNVQVTWEVQVTNZVQWDVSUESZVTNZNZVNWEWFVQWGNZVTNWIVQ
      WJVTVQWGGUGUHVQWGVTUIUJVNVQWHWEVQVNWHWEUDZVQVNWKVQWDTLZVQVSTLZVNVKTLWKVQW
      AILZWBILZNWCILWLVMWNVPWOIIVLDICDVIVJIIDVIFEICIDUKULZUMIIVODWPUMUNWAWBUOWC
      UPUQVQVRILWMVLVOUOVRUPURVKUSWDVSVKUTVAVBVCVDVEVFVGVHUL $.
      $( [21-Oct-2007] $)
  $}

  ${
    $d w x y z $.
    $( Absolute value is continuous.  (Contributed by Paul Chapman,
       21-Oct-2007.) $)
    abscncf $p |- abs e. ( CC -cn-> RR ) $=
      ( vx vw cr cabs ax-resscn absf cv abs2difabs abscncflem ) ABCDEFAGBGHI $.
      $( [21-Oct-2007] $)

    $( Real part is continuous.  (Contributed by Paul Chapman, 21-Oct-2007.) $)
    recncf $p |- Re e. ( CC -cn-> RR ) $=
      ( vx vw cr cre ax-resscn ref cv cc wcel wa cmin cfv cabs cle resub fveq2d
      co wbr subcl absrele syl eqbrtrrd abscncflem ) ABCDEFAGZHIBGZHIJZUDUEKQZD
      LZMLZUDDLUEDLKQZMLUGMLZNUFUHUJMUDUEOPUFUGHIUIUKNRUDUESUGTUAUBUC $.
      $( [21-Oct-2007] $)

    $( Imaginary part is continuous.  (Contributed by Paul Chapman,
       21-Oct-2007.) $)
    imcncf $p |- Im e. ( CC -cn-> RR ) $=
      ( vx vw cr cim ax-resscn imf cv cc wcel wa cmin cfv cabs cle imsub fveq2d
      co wbr subcl absimle syl eqbrtrrd abscncflem ) ABCDEFAGZHIBGZHIJZUDUEKQZD
      LZMLZUDDLUEDLKQZMLUGMLZNUFUHUJMUDUEOPUFUGHIUIUKNRUDUESUGTUAUBUC $.
      $( [21-Oct-2007] $)

    $( Complex conjugate is continuous.  (Contributed by Paul Chapman,
       21-Oct-2007.) $)
    cjcncf $p |- * e. ( CC -cn-> CC ) $=
      ( vx vw cc ccj ssid cjf cv wcel wa cfv cmin co cabs wceq cle cjcl anim12i
      cr wbr subcl abscl 3syl cjsub fveq2d abscj syl eqtr3d syl11anc abscncflem
      eqle ) ABCDCEFAGZCHZBGZCHZIZUKDJZUMDJZKLZMJZRHZUSUKUMKLZMJZNUSVBOSUOUPCHZ
      UQCHZIURCHUTULVCUNVDUKPUMPQUPUQTURUAUBUOVADJZMJZUSVBUOVEURMUKUMUCUDUOVACH
      VFVBNUKUMTVAUEUFUGUSVBUJUHUI $.
      $( [7-Nov-2013] $) $( [21-Oct-2007] $)
  $}

  ${
    $d A v w z $.  $d A w x y z $.  $d F v w z $.
    mulc1cncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( A x. x ) ) } $.
    $( Multiplication by a constant is continuous.  (Contributed by Paul
       Chapman, 28-Nov-2007.) $)
    mulc1cncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( cc wcel co wa cc0 wceq cv cmul adantr cmin cabs cfv clt wbr sylan2 ssid
      vz vv vw wss ccncf wi c1 wf wral mulcl ralrimiva fopab2 sylib crp 1rp w3a
      a1i12 opreq1 fveq2d 0cn eleq1 mpbiri subdi 3expb fvopab4g anabss7 adantrr
      opreq2 adantrl opreq12d eqtr4d sylan subcl syl abs0 syl6eq adantl 3eqtr3d
      mul02 3adant3 elrp simprbi 3ad2ant3 eqbrtrd a1d 3adant1l 3expib elcncf1di
      cr wne simplbi abscl abs00 necon3bid biimpar redivcl syl2an absgt0 biimpd
      cdiv imdistani anim1i divgt0 sylanb sylanbrc expcom adantld absmul eqtr3d
      ad2ant2r breq1d wb 3anrot df-3an bitri ltmuldiv2OLD syl3anl anasss ancoms
      jca sylanbr bitr3d exbiri pm2.61dane mp2ani ) CFGZFFUEZYHDFFUFHGZFUAZYJYG
      YHYHIYIUGCJYGCJKZIZUBUCUDFFDUHYGFFDUIZYKYGCALZMHZFGZAFUJYMYGYPAFCYNUKULAB
      FFYODEUMUNZNYLUBLZFGZUCLZUOGZIUHUOGUPURYLYSUDLZFGZIZUUAYRUUBOHZPQZUHRSZYR
      DQZUUBDQZOHZPQZYTRSZUGZYKUUDUUAUUMYGYKUUDUUAUQZUULUUGUUNUUKJYTRYKUUDUUKJK
      UUAYKUUDICUUEMHZPQZJUUEMHZPQZUUKJYKUUPUURKUUDYKUUOUUQPCJUUEMUSUTNYKYGUUDU
      UPUUKKYKYGJFGVACJFVBVCYGUUDIZUUOUUJPUUSUUOCYRMHZCUUBMHZOHZUUJYGYSUUCUUOUV
      BKCYRUUBVDVEUUSUUHUUTUUIUVAOYGYSUUHUUTKZUUCYGYSUVCYGYSIYSUUTFGUVCCYRUKABY
      RYOUUTFFDYNYRCMVIEVFTVGVHYGUUCUUIUVAKZYSYGUUCUVDYGUUCIUUCUVAFGUVDCUUBUKAB
      UUBYOUVAFFDYNUUBCMVIEVFTVGVJVKVLUTZVMUUDUURJKYKUUDUURJPQJUUDUUQJPUUDUUEFG
      ZUUQJKYRUUBVNZUUEVTVOUTVPVQVRVSWAUUAYKJYTRSZUUDUUAYTWJGZUVHYTWBZWCWDWEWFW
      GWHWIYGCJWKZIZUBUCUDFFDYTCPQZXAHZYGYMUVKYQNUVLUUAUVNUOGZYSUUAUVLUVOUUAUVL
      IUVNWJGZJUVNRSZUVOUUAUVIUVMWJGZUVMJWKZIUVPUVLUUAUVIUVHUVJWLZUVLUVRUVSYGUV
      RUVKCWMZNYGUVSUVKYGUVMJCJCWNWOWPYAUVIUVRUVSUVPYTUVMWQVEWRUVLUUAUVRJUVMRSZ
      IZUVQUVLYGUWBIZUWCYGUVKUWBYGUVKUWBCWSWTXBZYGUVRUWBUWAXCVOUUAUVIUVHIUWCUVQ
      UVJYTUVMXDXETUVNWBXFXGXHUVLUUDUUAIZUULUUFUVNRSZUVLUWFIZUVMUUFMHZYTRSZUULU
      WGUWHUWIUUKYTRYGUUDUWIUUKKUVKUUAUUSUUPUWIUUKUUDYGUVFUUPUWIKUVGCUUEXITUVEX
      JXKXLUWFUVLUWJUWGXMZUVLUWFUWDUWKUWEUWFYGUWBUWKUWFYGIZYGUUDUUAUQZUWBUWKUWM
      UUDUUAYGUQUWLYGUUDUUAXNUUDUUAYGXOXPYGUVRUUDUUFWJGZUUAUVIUWBUWKUWAUUDUVFUW
      NUVGUUEWMVOUVTUVMUUFYTXQXRYBXSTXTYCYDWIYEYF $.
      $( [28-Nov-2007] $)
  $}

  ${
    $d A x y $.
    divccncf.1 $e |- F = { <. x , y >. | ( x e. CC /\ y = ( x / A ) ) } $.
    $( Division by a constant is continuous.  (Contributed by Paul Chapman,
       28-Nov-2007.) $)
    divccncf $p |- ( ( A e. CC /\ A =/= 0 ) -> F e. ( CC -cn-> CC ) ) $=
      ( cc wcel cc0 wne wa cv c1 cdiv co cmul wceq copab ccncf wb w3a mulc1cncf
      divrec2 eqeq2d 3expb expcom pm5.32d opabbidv syl5eq reccl eqid eqeltrd
      syl ) CFGZCHIZJZDAKZFGZBKZLCMNZUPONZPZJZABQZFFRNZUODUQURUPCMNZPZJZABQVCEU
      OVGVBABUOUQVFVAUQUOVFVASZUQUMUNVHUQUMUNTVEUTURUPCUBUCUDUEUFUGUHUOUSFGVCVD
      GCUIABUSVCVCUJUAULUK $.
      $( [28-Nov-2007] $)
  $}


