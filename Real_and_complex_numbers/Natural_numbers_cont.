$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Natural numbers (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.  $d x y z A $.  $d x ps $.  $d x ch $.  $d x z th $.
    $d y z ph $.
    $( Substitutions. $)
    nn1suc.1 $e |- ( x = 1 -> ( ph <-> ps ) ) $.
    nn1suc.3 $e |- ( x = ( y + 1 ) -> ( ph <-> ch ) ) $.
    nn1suc.4 $e |- ( x = A -> ( ph <-> th ) ) $.
    $( Hypothesis showing it holds for 1. $)
    nn1suc.5 $e |- ps $.
    $( Hypothesis showing it holds for a successor. $)
    nn1suc.6 $e |- ( y e. NN -> ch ) $.
    $( If a statement holds for 1 and also holds for a successor, it holds for
       all natural numbers.  The first three hypotheses give us the
       substitution instances we need; the last two show that it holds for 1
       and for a successor.  $)
    nn1suc $p |- ( A e. NN -> th ) $=
      ( vz cn wcel wi cv wsbc c1 caddc co dfsbcq sbequ wceq wb wex elex ax-17
      hbsbc1 hbbi sbceq1a bitr3d imbi2d 19.23ai syl pm5.74rd pm2.43i sylan9bbr
      expcom pm5.74d sb19.21 syl5bb 1nn elisseti isseti hbsbc1v mpbiri mpbid
      ax-mp a1i cvv sbc19.21g mpbir oprex sylib a1d sylibr nnind ) GNOZDVSAPZEM
      QZRZVTESRZVTEFQZRZVTEWDSTUAZRZVSDPZMFGVTEWASUBVTMFEUCVTEWAWFUBWAGUDZVSAEW
      ARZPWHWBWIVSWJDVSWIWJDUEWIWJAEGRZVSDAEWAGUBVSWKDUEVSVSWKDVSEQZGUDZEUFVSWK
      PZWHUEZEGNUGWMWOEWNWHEAEMGNWAGOEUHUIWHEUHUJWMWKDVSWMAWKDAEGUKJULUMUNUOUPU
      QURUSUTVSAEMVSEUHZVAVBWCVSAESRZPZWQVSWLSUDZEUFWQESSNVCVDZVEWSWQEAESWTVFWS
      AWQWSABKHVGAESUKVHUNVIVJSVKOWCWRUEWTVSAESVKWPVLVIVMWDNOZWGWEXAVSAEWFRZPZW
      GXAXBVSXACXBLWLWFUDZEUFCXBUEZEWFWDSTVNZVEXDXEECXBECEUHAEWFXFVFUJXDACXBIAE
      WFUKULUNVIVOVPWFVKOWGXCUEXFVSAEWFVKWPVLVIVQVPVRUQ $.
      $( [17-Oct-2004] $) $( [11-Oct-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Closure of addition of natural numbers, proved by induction on the
       second addend. $)
    nnaddclt $p |- ( ( A e. NN /\ B e. NN ) -> ( A + B ) e. NN ) $=
      ( vx vy cn wcel caddc co cv wi c1 wceq opreq2 eleq1d imbi2d peano2nn wa
      cc ax1cn axaddass mp3an3 nncnt syl2an syl5bi expcom a2d nnind impcom ) BE
      FAEFZABGHZEFZUIACIZGHZEFZJUIAKGHZEFZJUIADIZGHZEFZJUIAUQKGHZGHZEFZJUIUKJCD
      BULKLZUNUPUIVCUMUOEULKAGMNOULUQLZUNUSUIVDUMUREULUQAGMNOULUTLZUNVBUIVEUMVA
      EULUTAGMNOULBLZUNUKUIVFUMUJEULBAGMNOAPUQEFZUIUSVBUIVGUSVBJUIVGQZURKGHZEFV
      BUSVHVIVAEARFZUQRFZVIVALZUIVGVJVKKRFVLSAUQKTUAAUBUQUBUCNURPUDUEUFUGUH $.
      $( [12-Jan-1997] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Closure of multiplication of natural numbers. $)
    nnmulclt $p |- ( ( A e. NN /\ B e. NN ) -> ( A x. B ) e. NN ) $=
      ( vx vy cn wcel cmul co cv wi c1 caddc wceq opreq2 eleq1d imbi2d cc
      nncnt ax1id biimprd mpcom wa ax1cn axdistr mp3an3 opreq2d adantr eqtrd
      syl2an nnaddclt ancoms syl5bir exp4b pm2.43b a2d nnind impcom ) BEFAEFZAB
      GHZEFZURACIZGHZEFZJURAKGHZEFZJURADIZGHZEFZJURAVFKLHZGHZEFZJURUTJCDBVAKMZV
      CVEURVLVBVDEVAKAGNOPVAVFMZVCVHURVMVBVGEVAVFAGNOPVAVIMZVCVKURVNVBVJEVAVIAG
      NOPVABMZVCUTURVOVBUSEVABAGNOPAQFZURVEARZVPVEURVPVDAEASZOTUAVFEFZURVHVKVSU
      RVHVKJURVSURVHVKURVSUBZVKVGALHZEFZURVHUBVTVJWAEVPVFQFZVJWAMURVSVPWCUBVJVG
      VDLHZWAVPWCKQFVJWDMUCAVFKUDUEVPWDWAMWCVPVDAVGLVRUFUGUHVQVFRUIOVHURWBVGAUJ
      UKULUMUNUOUPUQ $.
      $( [12-Jan-1997] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( There exists a natural number greater than or equal to any two
       others. $)
    nn2get $p |- ( ( A e. NN /\ B e. NN ) ->
                E. x e. NN ( A <_ x /\ B <_ x ) ) $=
      ( cn wcel wa cle wbr wo cv wrex cr letrit nnret syl2an wi leidt biantrud
      biimpd syl anc2li wceq breq2 anbi12d rcla4ev syl6 adantl biantrurd
      adantr jaod mpd ) BDEZCDEZFZBCGHZCBGHZIZBAJZGHZCURGHZFZADKZBLEZCLEZUQULUM
      BCMBNZCNZOUNUOVBUPUMUOVBPULUMUOUMUOCCGHZFZFVBUMUOVHUMVDUOVHPVFVDUOVHVDVGU
      OCQRSTUAVAVHACDURCUBUSUOUTVGURCBGUCURCCGUCUDUEUFUGULUPVBPUMULUPULBBGHZUPF
      ZFVBULUPVJULVCUPVJPVEVCUPVJVCVIUPBQUHSTUAVAVJABDURBUBUSVIUTUPURBBGUCURBCG
      UCUDUEUFUIUJUK $.
      $( [18-Aug-1999] $)
  $}

  ${
    $d x y A $.
    $( A natural number is one or greater. $)
    nnge1t $p |- ( A e. NN -> 1 <_ A ) $=
      ( vx vy c1 cv cle wbr caddc co breq2 1re leid cn wcel cr wi nnret cc0 cc
      wceq recnt ax0id syl breq2d clt wn lt01 0re axltadd mp3an12 mpi wa
      axlttrn mp3an3 axaddrcl mpan2 peano2re sylanc mpand con3d wb jctil
      lenltt 3imtr4d sylbird nnind ) DBEZFGDDFGDCEZFGZDVHDHIZFGZDAFGBCAVGDDFJVG
      VHDFJVGVJDFJVGADFJDKLVHMNVHONZVIVKPVHQVLVIDVHRHIZFGZVKVLVMVHDFVLVHSNVMVHT
      VHUAVHUBUCUDVLVMDUEGZUFZVJDUEGZUFZVNVKVLVQVOVLVMVJUEGZVQVOVLRDUEGZVSUGRON
      ZDONZVLVTVSPUHKRDVHUIUJUKVMONZVJONZVSVQULVOPZVLWCWDWBWEKVMVJDUMUNVLWAWCUH
      VHRUOUPZVHUQZURUSUTVLWBWCULVNVPVAVLWCWBWFKVBDVMVCUCVLWBWDULVKVRVAVLWDWBWG
      KVBDVJVCUCVDVEUCVF $.
      $( [25-Aug-1999] $)
  $}

  $( A natural number is greater than one iff it is not equal to one. $)
  nngt1ne1t $p |- ( A e. NN -> ( 1 < A <-> -. A = 1 ) ) $=
    ( cn wcel c1 clt wbr wceq wn cr cle wb 1re leltnet mp3an1 nnret nnge1t
    sylanc eqcom negbii syl6bb ) ABCZDAEFZDAGZHZADGZHAICZDAJFZUBUDKZUADICUFUGUH
    LDAMNAOAPQUCUEDARST $.
    $( [17-Oct-2004] $) $( [7-Oct-2004] $)

  $( A natural number is less than or equal to one iff it is equal to one. $)
  nnle1eq1t $p |- ( A e. NN -> ( A <_ 1 <-> A = 1 ) ) $=
    ( cn wcel c1 cle wbr wa wceq nnge1t biantrud cr wb nnret 1re letri3t mpan2
    syl bitr4d ) ABCZADEFZTDAEFZGZADHZSUATAIJSAKCZUCUBLZAMUDDKCUENADOPQR $.
    $( [22-Apr-2005] $) $( [3-Apr-2005] $)

  $( A natural number is positive. $)
  nngt0t $p |- ( A e. NN -> 0 < A ) $=
    ( cr wcel c1 cle wbr cc0 clt cn lt01 wa wi 0re 1re ltletrt mp3an12 mpani
    nnret nnge1t sylc ) ABCZDAEFZGAHFZAICUAGDHFZUBUCJGBCDBCUAUDUBKUCLMNGDAOPQAR
    AST $.
    $( [26-Sep-1999] $)

  $( A number less than one is not a natural number. $)
  lt1nnn $p |- ( ( A e. RR /\ A < 1 ) -> -. A e. NN ) $=
    ( cr wcel c1 clt wbr cn wn cle wb 1re ltnlet mpan2 nnge1t con3i syl6bi imp
    ) ABCZADEFZAGCZHZRSDAIFZHZUARDBCSUCJKADLMTUBANOPQ $.
    $( [18-Jan-2004] $) $( [18-Jan-2004] $)

  $( Zero is not a natural number. $)
  0nnn $p |- -. 0 e. NN $=
    ( cc0 cr wcel c1 clt wbr cn wn 0re lt01 lt1nnn mp2an ) ABCADEFAGCHIJAKL $.
    $( [25-Aug-1999] $)

  $( A natural number is non-zero. $)
  nnne0t $p |- ( A e. NN -> A =/= 0 ) $=
    ( cn wcel cc0 wceq 0nnn eleq1 mtbiri necon2ai ) ABCZADADEJDBCFADBGHI $.
    $( [27-Sep-1999] $)

  ${
    nngt0.1 $e |- A e. NN $.
    $( A natural number is positive (inference version). $)
    nngt0 $p |- 0 < A $=
      ( cn wcel cc0 clt wbr nngt0t ax-mp ) ACDEAFGBAHI $.
      $( [17-Sep-1999] $)

    $( A natural number is non-zero (inference version). $)
    nnne0 $p |- A =/= 0 $=
      ( nnre nngt0 gt0ne0i ) AABCABDE $.
      $( [25-Aug-1999] $)
  $}

  $( The reciprocal of a natural number is positive. $)
  nnrecgt0t $p |- ( A e. NN -> 0 < ( 1 / A ) ) $=
    ( cn wcel c1 cle wbr cc0 cdiv co clt nnge1t lt01 cr wa wi nnret 0re 1re
    ltletrt mp3an12 recgt0t ex syld syl mpani mpd ) ABCZDAEFZGDAHIJFZAKUGGDJFZU
    HUILUGAMCZUJUHNZUIOAPUKULGAJFZUIGMCDMCUKULUMOQRGDASTUKUMUIAUAUBUCUDUEUF $.
    $( [25-Aug-1999] $)

  ${
    $d x y z w v A $.  $d x y z w v B $.
    $( Natural number ordering relation. $)
    nnleltp1t $p |- ( ( A e. NN /\ B e. NN ) ->
                  ( A <_ B <-> A < ( B + 1 ) ) ) $=
      ( vz vx vy vw vv cn wcel wa cle wbr clt wceq wo c1 caddc co cr wb leloet
      nnret syl2an wi cc0 lt01 0re 1re pm3.2i lt2addt an4s mpan2 mpan2i cc
      pm3.26 recnt ax0id 3syl breq1d sylibd breq1 ltp1t syl5bir com12 adantl
      jaod cv wral eqeq1 orbi12d imbi12d rcla4v opreq1 breq2d breq2 eqeq2
      ralbidv wn eqid a1i orri ltadd1t mp3an23 syl nnge1t lenltt mpan mpbid
      pm2.21d sylbird nn1suc rgen imbi2d nngt0t mp3an13 ax1cn addid2 syl5eqbrr
      pm2.21nd orrd a1d adantr rcla4va adantlr mp3an3 peano2nn w3a 3anim123i
      3anrot sylibr 3simpc 3simp1 jca anandir sylib axaddcom eqeqan12d addcant
      bitr2d 3imtr3d exp31 imp3a exp3a com3l r19.21adv cbvralv syl5ib nnind
      syl5 imp impbid bitrd ) AHIZBHIZJZABKLZABMLZABNZOZABPQRZMLZASIZBSIZYPYSTY
      MYNABUAAUBZBUBZUCYOYSUUAUUBUUCYSUUAUDYMYNUUBUUCJZYQUUAYRUUFYQAUEQRZYTMLZU
      UAUUFYQUEPMLZUUHUFUUFUESIZPSIZJYQUUIJUUHUDZUUJUUKUGUHUIUUBUUJUUCUUKUULAUE
      BPUJUKULUMUUFUUGAYTMUUFUUBAUNIUUGANUUBUUCUOAUPAUQURUSUTUUCYRUUAUDUUBYRUUC
      UUAYRUUABYTMLUUCABYTMVABVBVCVDVEVFUUDUUEUCYMYNUUAYSUDZYMCVGZYTMLZUUNBMLZU
      UNBNZOZUDZCHVHZUUMYNUUSUUMCAHUUNANZUUOUUAUURYSUUNAYTMVAUVAUUPYQUUQYRUUNAB
      MVAUUNABVIVJVKVLUUNDVGZPQRZMLZUUNUVBMLZUUNUVBNZOZUDZCHVHUUNPPQRZMLZUUNPML
      ZUUNPNZOZUDZCHVHUUNEVGZPQRZMLZUUNUVOMLZUUNUVONZOZUDZCHVHZUUNUVPPQRZMLZUVQ
      UUNUVPNZOZUDZCHVHZUUTDEBUVBPNZUVHUVNCHUWIUVDUVJUVGUVMUWIUVCUVIUUNMUVBPPQV
      MVNUWIUVEUVKUVFUVLUVBPUUNMVOUVBPUUNVPVJVKVQUVBUVONZUVHUWACHUWJUVDUVQUVGUV
      TUWJUVCUVPUUNMUVBUVOPQVMVNUWJUVEUVRUVFUVSUVBUVOUUNMVOUVBUVOUUNVPVJVKVQUVB
      UVPNZUVHUWGCHUWKUVDUWDUVGUWFUWKUVCUWCUUNMUVBUVPPQVMVNUWKUVEUVQUVFUWEUVBUV
      PUUNMVOUVBUVPUUNVPVJVKVQUVBBNZUVHUUSCHUWLUVDUUOUVGUURUWLUVCYTUUNMUVBBPQVM
      VNUWLUVEUUPUVFUUQUVBBUUNMVOUVBBUUNVPVJVKVQUVNCHUVBUVIMLZUVBPMLZUWIOZUDPUV
      IMLZPPMLZPPNZOZUDUVPUVIMLZUVPPMLZUVPPNZOZUDUVNDEUUNUWIUWMUWPUWOUWSUVBPUVI
      MVAUWIUWNUWQUWIUWRUVBPPMVAUVBPPVIVJVKUWKUWMUWTUWOUXCUVBUVPUVIMVAUWKUWNUXA
      UWIUXBUVBUVPPMVAUVBUVPPVIVJVKUVBUUNNZUWMUVJUWOUVMUVBUUNUVIMVAUXDUWNUVKUWI
      UVLUVBUUNPMVAUVBUUNPVIVJVKUWSUWPUWQUWRUWRUWQVRPVSVTWAVTUVOHIZUWTUVOPMLZUX
      CUXEUVOSIZUXFUWTTZUVOUBZUXGUUKUUKUXHUHUHUVOPPWBWCWDUXEUXFUXCUXEPUVOKLZUXF
      VRZUVOWEUXEUXGUXJUXKTZUXIUUKUXGUXLUHPUVOWFWGWDWHWIWJWKWLUXEFVGZUVPMLZUXMU
      VOMLZUXMUVONZOZUDZFHVHZUWHUWBUXEUXSUWGCHUUNHIZUXEUXSUWGUXTUXEUXSUWGUXEUXS
      JZUVBUWCMLZUVBUVPMLZUWKOZUDZUDUYAPUWCMLZPUVPMLZPUVPNZOZUDZUDUYAGVGZPQRZUW
      CMLZUYLUVPMLZUYLUVPNZOZUDZUDUYAUWGUDDGUUNUWIUYEUYJUYAUWIUYBUYFUYDUYIUVBPU
      WCMVAUWIUYCUYGUWKUYHUVBPUVPMVAUVBPUVPVIVJVKWMUVBUYLNZUYEUYQUYAUYRUYBUYMUY
      DUYPUVBUYLUWCMVAUYRUYCUYNUWKUYOUVBUYLUVPMVAUVBUYLUVPVIVJVKWMUXDUYEUWGUYAU
      XDUYBUWDUYDUWFUVBUUNUWCMVAUXDUYCUVQUWKUWEUVBUUNUVPMVAUVBUUNUVPVIVJVKWMUXE
      UYJUXSUXEUYIUYFUXEUYGUYHUXEUYGUYHUXEUEPQRZUVPPMUXEUEUVOMLZUYSUVPMLZUVOWNU
      XEUXGUYTVUATZUXIUUJUXGUUKVUBUGUHUEUVOPWBWOWDWHPWPWQWRWSWTXAXBUYKHIZUXEUXS
      UYQVUCUXEUXSUYQVUCUXEJZUXSJUYKUVPMLZUYKUVOMLZUYKUVONZOZUYMUYPVUCUXSVUEVUH
      UDZUXEUXRVUIFUYKHUXMUYKNZUXNVUEUXQVUHUXMUYKUVPMVAVUJUXOVUFUXPVUGUXMUYKUVO
      MVAUXMUYKUVOVIVJVKXCXDVUDVUEUYMTZUXSUYKSIZUVPSIZVUKVUCUXEVULVUMUUKVUKUHUY
      KUVPPWBXEUYKUBZUXEUVPHIVUMUVOXFUVPUBWDUCXBVUDVUHUYPTZUXSVULUXGVUOVUCUXEVU
      LUXGUUKVUOUHVULUXGUUKXGZVUFUYNVUGUYOUYKUVOPWBVUPPUNIZUYKUNIZUVOUNIZXGZVUG
      UYOTVUPVURVUSVUQXGVUTVULVURUXGVUSUUKVUQUYKUPUVOUPPUPXHVUQVURVUSXIXJVUTUYO
      PUYKQRZPUVOQRZNZVUGVUTVURVUQJZVUSVUQJZJZUYOVVCTVUTVURVUSJZVUQJVVFVUTVVGVU
      QVUQVURVUSXKVUQVURVUSXLXMVURVUSVUQXNXOVVDVVEUYLVVAUVPVVBUYKPXPUVOPXPXQWDP
      UYKUVOXRXSWDVJXEVUNUXIUCXBXTYAYBWKYCYDYEUWAUXRCFHUUNUXMNZUVQUXNUVTUXQUUNU
      XMUVPMVAVVHUVRUXOUVSUXPUUNUXMUVOMVAUUNUXMUVOVIVJVKYFYGYHYIYJYKYL $.
      $( [13-Aug-2001] $)
  $}

  $( Natural number ordering relation. $)
  nnltp1let $p |- ( ( A e. NN /\ B e. NN ) ->
                ( A < B <-> ( A + 1 ) <_ B ) ) $=
    ( cn wcel wa clt wbr c1 caddc co cle cr wb 1re ltadd1t mp3an3 nnret syl2an
    nnleltp1t peano2nn sylan bitr4d ) ACDZBCDZEABFGZAHIJZBHIJFGZUFBKGZALDZBLDZU
    EUGMZUCUDUIUJHLDUKNABHOPAQBQRUFCDUDUHUGMUCUFBSATUAUB $.
    $( [19-Aug-2001] $)

  ${
    $d x y A $.  $d x y B $.
    nnsub.1 $e |- A e. NN $.
    nnsub.2 $e |- B e. NN $.
    $( Subtraction of natural numbers. $)
    nnsub $p |- ( A < B <-> ( B - A ) e. NN ) $=
      ( vx vy clt wbr cmin co cn wcel wi cv c1 caddc wceq breq2 opreq1 eleq1d
      imbi12d cle wn nnge1t ax-mp 1re nnre lenlt mpbi pm2.21i wo cr wa wb
      nnret jctil leloet syl nnleltp1t mpan bitr3d cc nncnt nncn jctir ax1cn
      addsubt mp3an2 peano2nn syl5bir imim2d com23 opreq1d cc0 addsub subid
      opreq1i addid2 3eqtr 1nn eqeltr syl6eqelr a1d a1i jaod sylbird nnind
      nngt0t posdif sylibr impbi ) ABGHZBAIJZKLZBKLWLWNMZDAENZGHZWPAIJZKLZMAOGH
      ZOAIJZKLZMAFNZGHZXCAIJZKLZMZAXCOPJZGHZXHAIJZKLZMWOEFBWPOQZWQWTWSXBWPOAGRX
      LWRXAKWPOAISTUAWPXCQZWQXDWSXFWPXCAGRXMWRXEKWPXCAISTUAWPXHQZWQXIWSXKWPXHAG
      RXNWRXJKWPXHAISTUAWPBQZWQWLWSWNWPBAGRXOWRWMKWPBAISTUAWTXBOAUBHZWTUCAKLZXP
      CAUDUEOAUFACUGZUHUIUJXCKLZXIXGXKXSXIXDAXCQZUKZXGXKMZXSAXCUBHZYAXIXSAULLZX
      CULLZUMYCYAUNXSYEYDXCUOXRUPAXCUQURXQXSYCXIUNCAXCUSUTVAXSXDYBXTXSXGXDXKXSX
      FXKXDXSXKXEOPJZKLXFXSXJYFKXSXCVBLZAVBLZUMXJYFQZXSYGYHXCVCACVDZVEYGOVBLYHY
      IVFXCOAVGVHURTXEVIVJVKVLXTYBMXSXTXKXGXTXJAOPJZAIJZKXTYKXHAIAXCOPSVMYLOKYL
      AAIJZOPJVNOPJOAOAYJVFYJVOYMVNOPAYJVPVQOVFVRVSVTWAWBWCWDWEWFVLWGUEWNVNWMGH
      WLWMWHABXRBDUGWIWJWK $.
      $( [19-Aug-2001] $)
  $}

  $( Subtraction of natural numbers. $)
  nnsubt $p |- ( ( A e. NN /\ B e. NN ) -> ( A < B <-> ( B - A ) e. NN ) ) $=
    ( cn wcel clt wbr cmin co wb c1 cif wceq breq1 opreq2 eleq1d bibi12d breq2
    opreq1 1nn elimel nnsub dedth2h ) ACDZBCDZABEFZBAGHZCDZIUCAJKZBEFZBUHGHZCDZ
    IUHUDBJKZEFZULUHGHZCDZIABJJAUHLZUEUIUGUKAUHBEMUPUFUJCAUHBGNOPBULLZUIUMUKUOB
    ULUHEQUQUJUNCBULUHGROPUHULAJCSTBJCSTUAUB $.
    $( [20-Aug-2001] $)

  $( Closure of addition of natural numbers minus one. $)
  nnaddm1clt $p |- ( ( A e. NN /\ B e. NN ) -> ( ( A + B ) - 1 ) e. NN ) $=
    ( cn wcel wa c1 caddc co clt wbr cmin cr cle wi 1re readdcl ltletrt
    mp3an12 axaddrcl nnret syl2an a1i mpan syl adantl nnge1t wb leadd2t
    mp3an13 mpbid adantr leadd1t mp3an1 letrd ltp1 jctil sylc nnaddclt 1nn
    nnsubt ) ACDZBCDZEZFABGHZIJZVDFKHCDZVDLDZFFFGHZIJZVHVDMJZEZVEVCFLDZVHLDZVGV
    KVENOFFOOPZFVHVDQRALDZBLDZVGVAVBABSATZBTZUAZVCVJVIVCVHFBGHZVDVMVCVNUBVBVTLD
    ZVAVBVPWAVRVLVPWAOFBSUCUDUEVSVBVHVTMJZVAVBFBMJZWBBUFVBVPWCWBUGZVRVLVPVLWDOO
    FBFUHUIUDUJUEVCFAMJZVTVDMJZVAWEVBAUFUKVOVPWEWFUGZVAVBVLVOVPWGOFABULUMVQVRUA
    UJUNFOUOUPUQVCVDCDZVEVFUGZABURFCDWHWIUSFVDUTUCUDUJ $.
    $( [6-Aug-2003] $)

  ${
    $d x A $.  $d x B $.
    $( Two ways to express " ` A ` divides ` B ` " for natural numbers. $)
    nndivt $p |- ( ( A e. NN /\ B e. NN ) ->
                ( E. x e. NN ( A x. x ) = B <-> ( B / A ) e. NN ) ) $=
      ( cn wcel wa cc0 wne cv cmul co wceq wrex cdiv wb nnne0t adantr cc wi
      w3a divcan3t 3com23 3expa nncnt sylan2 3adantl2 eqcomd opreq1 sylan9eq
      simplr eqeltrrd exp31 r19.23adv divcan2t opreq2 eqeq1d rcla4ev expcom
      syl impbid 3exp imp syl2an mpd ) BDEZCDEZFBGHZBAIZJKZCLZADMZCBNKZDEZOZVEV
      GVFBPQBREZCREZVGVNSZVEVFVOVPVQVOVPVGVNVOVPVGTZVKVMVRVJVMADVRVHDEZVJVMVRVS
      FZVJFVHVLDVTVJVHVIBNKZVLVTWAVHVOVGVSWAVHLZVPVOVGFVHREZWBVSVOVGWCWBVOWCVGW
      BBVHUAUBUCVHUDUEUFUGVICBNUHUIVRVSVJUJUKULUMVRBVLJKZCLZVMVKSBCUNVMWEVKVJWE
      AVLDVHVLLVIWDCVHVLBJUOUPUQURUSUTVAVBBUDCUDVCVD $.
      $( [20-Feb-2004] $) $( [3-Feb-2004] $)
  $}

  $( Transitive property of divisibility:  if ` A ` divides ` B ` and ` B `
     divides ` C ` , then ` A ` divides ` C ` . $)
  nndivtrt $p |- ( ( ( A e. NN /\ B e. NN /\ C e. CC ) /\
               ( ( B / A ) e. NN /\ ( C / B ) e. NN ) ) -> ( C / A ) e. NN ) $=
    ( cn wcel cc w3a cdiv co wa cmul c1 cc0 wne wceq divmul24t nncnt anim12i
    ancoms 3adant3 anim2i 3adant1 jca nnne0t sylanc dividt opreq1d 3ad2ant2
    divclt 3expb sylan2 mulid2t syl 3adant2 3eqtrd eleq1d nnmulclt syl5bi imp
    ) ADEZBDEZCFEZGZBAHIZDECBHIZDEJZCAHIZDEZVCVDVEKIZDEVHVFVCVIVGDVCVIBBHIZVGKI
    ZLVGKIZVGBFEZAFEZJZVBVMJZJAMNZBMNZJZVIVKOVCBACBPVCVOVPUTVAVOVBVAUTVOVAVMUTV
    NBQZAQZRSTVAVBVPUTVBVAVPVAVMVBVTUASUBUCUTVAVSVBUTVQVAVRAUDZBUDZRTUEVAUTVKVL
    OVBVAVJLVGKVMVRVJLOVABUFVTWCUEUGUHUTVBVLVGOZVAUTVBJVGFEZWDVBUTWEVBVNVQJWEUT
    VBVNVQWECAUIUJUTVNVQWAWBUCUKSVGULUMUNUOUPVDVEUQURUS $.
    $( [6-May-2005] $) $( [3-May-2005] $)

