$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Natural numbers (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.  $d x y z A $.  $d x ps $.  $d x ch $.  $d x z th $.
    $d y z ph $.
    $( Substitutions. $)
    nn1suc.1 $e |- ( x = 1 -> ( ph <-> ps ) ) $.
    nn1suc.3 $e |- ( x = ( y + 1 ) -> ( ph <-> ch ) ) $.
    nn1suc.4 $e |- ( x = A -> ( ph <-> th ) ) $.
    $( Hypothesis showing it holds for 1. $)
    nn1suc.5 $e |- ps $.
    $( Hypothesis showing it holds for a successor. $)
    nn1suc.6 $e |- ( y e. NN -> ch ) $.
    $( If a statement holds for 1 and also holds for a successor, it holds for
       all natural numbers.  The first three hypotheses give us the
       substitution instances we need; the last two show that it holds for 1
       and for a successor. $)
    nn1suc $p |- ( A e. NN -> th ) $=
      ( vz cn wcel wi cv wsbc c1 caddc co dfsbcq sbequ wceq ax-17 sb19.21 wb
      wex elex hbsbc1 hbbi sbceq1a bitr3d imbi2d 19.23ai syl pm5.74rd pm2.43i
      sylan9bbr expcom pm5.74d syl5bb 1nn elisseti isseti hbsbc1v mpbiri mpbid
      ax-mp a1i cvv sbc19.21g mpbir oprex sylib a1d sylibr nnind ) GNOZDVSAPZEM
      QZRZVTESRZVTEFQZRZVTEWDSTUAZRZVSDPZMFGVTEWASUBVTMFEUCVTEWAWFUBWBVSAEWARZP
      WAGUDZWHVSAEMVSEUEZUFWJVSWIDVSWJWIDUGWJWIAEGRZVSDAEWAGUBVSWLDUGVSVSWLDVSE
      QZGUDZEUHVSWLPZWHUGZEGNUIWNWPEWOWHEAEMGNWAGOEUEUJWHEUEUKWNWLDVSWNAWLDAEGU
      LJUMUNUOUPUQURUSUTVAVBWCVSAESRZPZWQVSWMSUDZEUHWQESSNVCVDZVEWSWQEAESWTVFWS
      AWQWSABKHVGAESULVHUOVIVJSVKOWCWRUGWTVSAESVKWKVLVIVMWDNOZWGWEXAVSAEWFRZPZW
      GXAXBVSXACXBLWMWFUDZEUHCXBUGZEWFWDSTVNZVEXDXEECXBECEUEAEWFXFVFUKXDACXBIAE
      WFULUMUOVIVOVPWFVKOWGXCUGXFVSAEWFVKWKVLVIVQVPVRUR $.
      $( [11-Oct-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Closure of addition of natural numbers, proved by induction on the
       second addend. $)
    nnaddcl $p |- ( ( A e. NN /\ B e. NN ) -> ( A + B ) e. NN ) $=
      ( vx vy cn wcel caddc co cv wi c1 wceq opreq2 eleq1d imbi2d peano2nn wa
      cc nncn ax-1cn addass mp3an3 syl2an syl5ib expcom a2d nnind impcom ) BEFA
      EFZABGHZEFZUIACIZGHZEFZJUIAKGHZEFZJUIADIZGHZEFZJUIAUQKGHZGHZEFZJUIUKJCDBU
      LKLZUNUPUIVCUMUOEULKAGMNOULUQLZUNUSUIVDUMUREULUQAGMNOULUTLZUNVBUIVEUMVAEU
      LUTAGMNOULBLZUNUKUIVFUMUJEULBAGMNOAPUQEFZUIUSVBUIVGUSVBJUSURKGHZEFUIVGQZV
      BURPVIVHVAEUIARFZUQRFZVHVALZVGASUQSVJVKKRFVLTAUQKUAUBUCNUDUEUFUGUH $.
      $( [12-Jan-1997] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Closure of multiplication of natural numbers. $)
    nnmulcl $p |- ( ( A e. NN /\ B e. NN ) -> ( A x. B ) e. NN ) $=
      ( vx vy cn wcel cmul co cv wi c1 caddc wceq opreq2 eleq1d imbi2d cc nncn
      mulid1 biimprd mpcom wa nnaddcl ancoms ax-1cn adddi mp3an3 opreq2d
      adantr eqtrd syl2an syl5ibr exp4b pm2.43b a2d nnind impcom ) BEFAEFZABGHZ
      EFZURACIZGHZEFZJURAKGHZEFZJURADIZGHZEFZJURAVFKLHZGHZEFZJURUTJCDBVAKMZVCVE
      URVLVBVDEVAKAGNOPVAVFMZVCVHURVMVBVGEVAVFAGNOPVAVIMZVCVKURVNVBVJEVAVIAGNOP
      VABMZVCUTURVOVBUSEVABAGNOPAQFZURVEARZVPVEURVPVDAEASZOTUAVFEFZURVHVKVSURVH
      VKJURVSURVHVKURVHUBVKURVSUBZVGALHZEFZVHURWBVGAUCUDVTVJWAEURVPVFQFZVJWAMVS
      VQVFRVPWCUBVJVGVDLHZWAVPWCKQFVJWDMUEAVFKUFUGVPWDWAMWCVPVDAVGLVRUHUIUJUKOU
      LUMUNUOUPUQ $.
      $( [12-Jan-1997] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( There exists a natural number greater than or equal to any two
       others. $)
    nn2ge $p |- ( ( A e. NN /\ B e. NN ) ->
                E. x e. NN ( A <_ x /\ B <_ x ) ) $=
      ( cn wcel wa cv cle wbr wrex cr nnre adantr adantl leid biantrud biimpa
      sylan wceq breq2 anbi12d rcla4ev syldan adantll anim1i adantlr lecasei )
      BDEZCDEZFBAGZHIZCUJHIZFZADJZBCUHBKEZUIBLZMUICKEZUHCLZNUIBCHIZUNUHUIUSUSCC
      HIZFZUNUIUQUSVAURUQUSVAUQUTUSCOPQRUMVAACDUJCSUKUSULUTUJCBHTUJCCHTUAUBUCUD
      UHCBHIZUNUIUHVBBBHIZVBFZUNUHUOVBVDUPUOVCVBBOUERUMVDABDUJBSUKVCULVBUJBBHTU
      JBCHTUAUBUCUFUG $.
      $( [18-Aug-1999] $)
  $}

  ${
    $d x y A $.
    $( A natural number is one or greater. $)
    nnge1 $p |- ( A e. NN -> 1 <_ A ) $=
      ( vx vy c1 cv cle wbr caddc co breq2 1re leidi cn wcel cr wi nnre cc0 cc
      wceq recn addid1 syl breq2d clt wn lt01 0re axltadd mp3an12 mpi wa
      readdcl mpan2 peano2re axlttrn mp3an3 syl11anc mpand con3d wb lenlt
      sylancr 3imtr4d sylbird nnind ) DBEZFGDDFGDCEZFGZDVHDHIZFGZDAFGBCAVGDDFJV
      GVHDFJVGVJDFJVGADFJDKLVHMNVHONZVIVKPVHQVLVIDVHRHIZFGZVKVLVMVHDFVLVHSNVMVH
      TVHUAVHUBUCUDVLVMDUEGZUFZVJDUEGZUFZVNVKVLVQVOVLVMVJUEGZVQVOVLRDUEGZVSUGRO
      NZDONZVLVTVSPUHKRDVHUIUJUKVLVMONZVJONZVSVQULVOPZVLWAWCUHVHRUMUNZVHUOZWCWD
      WBWEKVMVJDUPUQURUSUTVLWBWCVNVPVAKWFDVMVBVCVLWBWDVKVRVAKWGDVJVBVCVDVEUCVF
      $.
      $( [25-Aug-1999] $)
  $}

  $( A natural number is greater than one iff it is not equal to one. $)
  nngt1ne1 $p |- ( A e. NN -> ( 1 < A <-> A =/= 1 ) ) $=
    ( cn wcel cr c1 cle wbr clt wne wb nnre nnge1 1re leltne mp3an1 syl11anc )
    ABCADCZEAFGZEAHGAEIJZAKALEDCQRSMEANOP $.
    $( [20-Jul-2007] $) $( [7-Oct-2004] $)

  $( A natural number is less than or equal to one iff it is equal to one. $)
  nnle1eq1 $p |- ( A e. NN -> ( A <_ 1 <-> A = 1 ) ) $=
    ( cn wcel c1 cle wbr wa wceq nnge1 biantrud cr wb nnre 1re letri3 sylancl
    bitr4d ) ABCZADEFZSDAEFZGZADHZRTSAIJRAKCDKCUBUALAMNADOPQ $.
    $( [3-Apr-2005] $)

  $( A natural number is positive. $)
  nngt0 $p |- ( A e. NN -> 0 < A ) $=
    ( cn wcel cr c1 cle wbr cc0 clt nnre nnge1 lt01 wa wi 0re 1re ltletr
    mp3an12 mpani sylc ) ABCADCZEAFGZHAIGZAJAKUAHEIGZUBUCLHDCEDCUAUDUBMUCNOPHEA
    QRST $.
    $( [26-Sep-1999] $)

  $( A number less than one is not a natural number. $)
  lt1nnn $p |- ( ( A e. RR /\ A < 1 ) -> -. A e. NN ) $=
    ( cr wcel c1 clt wbr cn wn cle wb 1re ltnle mpan2 nnge1 con3i syl6bi imp )
    ABCZADEFZAGCZHZRSDAIFZHZUARDBCSUCJKADLMTUBANOPQ $.
    $( [18-Jan-2004] $)

  $( Zero is not a natural number. $)
  0nnn $p |- -. 0 e. NN $=
    ( cc0 cr wcel c1 clt wbr cn wn 0re lt01 lt1nnn mp2an ) ABCADEFAGCHIJAKL $.
    $( [25-Aug-1999] $)

  $( A natural number is nonzero. $)
  nnne0 $p |- ( A e. NN -> A =/= 0 ) $=
    ( cn wcel cc0 wceq 0nnn eleq1 mtbiri necon2ai ) ABCZADADEJDBCFADBGHI $.
    $( [27-Sep-1999] $)

  ${
    nngt0.1 $e |- A e. NN $.
    $( A natural number is positive (inference version). $)
    nngt0i $p |- 0 < A $=
      ( cn wcel cc0 clt wbr nngt0 ax-mp ) ACDEAFGBAHI $.
      $( [17-Sep-1999] $)

    $( A natural number is nonzero (inference version). $)
    nnne0i $p |- A =/= 0 $=
      ( nnrei nngt0i gt0ne0ii ) AABCABDE $.
      $( [25-Aug-1999] $)
  $}

  $( The quotient of a real and a natural number is real. $)
  nndivre $p |- ( ( A e. RR /\ N e. NN ) -> ( A / N ) e. RR ) $=
    ( cn wcel cr cc0 wne wa cdiv co nnre nnne0 jca redivcl 3expb sylan2 ) BCDZA
    EDZBEDZBFGZHABIJEDZQSTBKBLMRSTUAABNOP $.
    $( [28-Nov-2008] $)

  $( The reciprocal of a natural number is real. $)
  nnrecre $p |- ( N e. NN -> ( 1 / N ) e. RR ) $=
    ( c1 cr wcel cn cdiv co 1re nndivre mpan ) BCDAEDBAFGCDHBAIJ $.
    $( [8-Feb-2008] $)

  $( The reciprocal of a natural number is positive. $)
  nnrecgt0 $p |- ( A e. NN -> 0 < ( 1 / A ) ) $=
    ( cn wcel c1 cle wbr cc0 cdiv co clt nnge1 lt01 cr wa wi nnre 0re 1re
    ltletr mp3an12 recgt0 ex syld syl mpani mpd ) ABCZDAEFZGDAHIJFZAKUGGDJFZUHU
    ILUGAMCZUJUHNZUIOAPUKULGAJFZUIGMCDMCUKULUMOQRGDASTUKUMUIAUAUBUCUDUEUF $.
    $( [25-Aug-1999] $)

  ${
    $d x y z w v A $.  $d x y z w v B $.
    $( Natural number ordering relation. $)
    nnleltp1 $p |- ( ( A e. NN /\ B e. NN ) ->
                  ( A <_ B <-> A < ( B + 1 ) ) ) $=
      ( vz vx vy vw vv cn wcel wa cle wbr clt wceq wo c1 caddc co cr wb nnre
      leloe syl2an wi cc0 lt01 0re 1re lt2add an4s mpanr12 mpan2i cc simpl
      recn addid1 3syl breq1d sylibd ltp1 breq1 syl5ibrcom adantl jaod cv wral
      opreq1 breq2d breq2 eqeq2 orbi12d imbi12d ralbidv eqeq1 eqid olci a1i
      ltadd1 mp3an23 syl wn nnge1 lenlt sylancr mpbid pm2.21d sylbird nn1suc
      rgen cbvralv imbi2d ax-1cn addid2i nngt0 mp3an13 syl5eqbrr orcd a1d
      adantr rcla4va adantlr peano2nn mp3an3 w3a 3anim123i 3anrot sylibr
      3simpc simp1 jca anandir sylib addcom eqeqan12d addcan bitr2d 3imtr3d
      expl exp3a com3l r19.21adv syl5bi nnind rcla4v syl5 imp impbid bitrd ) AH
      IZBHIZJZABKLZABMLZABNZOZABPQRZMLZYIASIZBSIZYLYOTYJAUAZBUAZABUBUCYKYOYQYIY
      RYSYOYQUDYJYTUUAYRYSJZYMYQYNUUBYMAUEQRZYPMLZYQUUBYMUEPMLZUUDUFUUBUESIZPSI
      ZYMUUEJUUDUDZUGUHYRUUFYSUUGUUHAUEBPUIUJUKULUUBUUCAYPMUUBYRAUMIUUCANYRYSUN
      AUOAUPUQURUSYSYNYQUDYRYSYQYNBYPMLBUTABYPMVAVBVCVDUCYIYJYQYOUDZYJCVEZYPMLZ
      UUJBMLZUUJBNZOZUDZCHVFZYIUUIUUJDVEZPQRZMLZUUJUUQMLZUUJUUQNZOZUDZCHVFUUJPP
      QRZMLZUUJPMLZUUJPNZOZUDZCHVFUUJEVEZPQRZMLZUUJUVJMLZUUJUVJNZOZUDZCHVFZUUJU
      VKPQRZMLZUVLUUJUVKNZOZUDZCHVFZUUPDEBUUQPNZUVCUVICHUWDUUSUVEUVBUVHUWDUURUV
      DUUJMUUQPPQVGVHUWDUUTUVFUVAUVGUUQPUUJMVIUUQPUUJVJVKVLVMUUQUVJNZUVCUVPCHUW
      EUUSUVLUVBUVOUWEUURUVKUUJMUUQUVJPQVGVHUWEUUTUVMUVAUVNUUQUVJUUJMVIUUQUVJUU
      JVJVKVLVMUUQUVKNZUVCUWBCHUWFUUSUVSUVBUWAUWFUURUVRUUJMUUQUVKPQVGVHUWFUUTUV
      LUVAUVTUUQUVKUUJMVIUUQUVKUUJVJVKVLVMUUQBNZUVCUUOCHUWGUUSUUKUVBUUNUWGUURYP
      UUJMUUQBPQVGVHUWGUUTUULUVAUUMUUQBUUJMVIUUQBUUJVJVKVLVMUVICHUUQUVDMLZUUQPM
      LZUWDOZUDPUVDMLZPPMLZPPNZOZUDUVKUVDMLZUVKPMLZUVKPNZOZUDUVIDEUUJUWDUWHUWKU
      WJUWNUUQPUVDMVAUWDUWIUWLUWDUWMUUQPPMVAUUQPPVNVKVLUWFUWHUWOUWJUWRUUQUVKUVD
      MVAUWFUWIUWPUWDUWQUUQUVKPMVAUUQUVKPVNVKVLUUQUUJNZUWHUVEUWJUVHUUQUUJUVDMVA
      UWSUWIUVFUWDUVGUUQUUJPMVAUUQUUJPVNVKVLUWNUWKUWMUWLPVOVPVQUVJHIZUWOUVJPMLZ
      UWRUWTUVJSIZUXAUWOTZUVJUAZUXBUUGUUGUXCUHUHUVJPPVRVSVTUWTUXAUWRUWTPUVJKLZU
      XAWAZUVJWBUWTUUGUXBUXEUXFTUHUXDPUVJWCWDWEWFWGWHWIUVQFVEZUVKMLZUXGUVJMLZUX
      GUVJNZOZUDZFHVFZUWTUWCUVPUXLCFHUUJUXGNZUVLUXHUVOUXKUUJUXGUVKMVAUXNUVMUXIU
      VNUXJUUJUXGUVJMVAUUJUXGUVJVNVKVLWJUWTUXMUWBCHUUJHIZUWTUXMUWBUXOUWTUXMUWBU
      WTUXMJZUUQUVRMLZUUQUVKMLZUWFOZUDZUDUXPPUVRMLZPUVKMLZPUVKNZOZUDZUDUXPGVEZP
      QRZUVRMLZUYGUVKMLZUYGUVKNZOZUDZUDUXPUWBUDDGUUJUWDUXTUYEUXPUWDUXQUYAUXSUYD
      UUQPUVRMVAUWDUXRUYBUWFUYCUUQPUVKMVAUUQPUVKVNVKVLWKUUQUYGNZUXTUYLUXPUYMUXQ
      UYHUXSUYKUUQUYGUVRMVAUYMUXRUYIUWFUYJUUQUYGUVKMVAUUQUYGUVKVNVKVLWKUWSUXTUW
      BUXPUWSUXQUVSUXSUWAUUQUUJUVRMVAUWSUXRUVLUWFUVTUUQUUJUVKMVAUUQUUJUVKVNVKVL
      WKUWTUYEUXMUWTUYDUYAUWTUYBUYCUWTPUEPQRZUVKMPWLWMUWTUEUVJMLZUYNUVKMLZUVJWN
      UWTUXBUYOUYPTZUXDUUFUXBUUGUYQUGUHUEUVJPVRWOVTWEWPWQWRWSUYFHIZUWTUXMUYLUYR
      UWTJZUXMJUYFUVKMLZUYFUVJMLZUYFUVJNZOZUYHUYKUYRUXMUYTVUCUDZUWTUXLVUDFUYFHU
      XGUYFNZUXHUYTUXKVUCUXGUYFUVKMVAVUEUXIVUAUXJVUBUXGUYFUVJMVAUXGUYFUVJVNVKVL
      WTXAUYSUYTUYHTZUXMUYRUYFSIZUVKSIZVUFUWTUYFUAZUWTUVKHIVUHUVJXBUVKUAVTVUGVU
      HUUGVUFUHUYFUVKPVRXCUCWSUYSVUCUYKTZUXMUYRVUGUXBVUJUWTVUIUXDVUGUXBUUGVUJUH
      VUGUXBUUGXDZVUAUYIVUBUYJUYFUVJPVRVUKPUMIZUYFUMIZUVJUMIZXDZVUBUYJTVUKVUMVU
      NVULXDVUOVUGVUMUXBVUNUUGVULUYFUOUVJUOPUOXEVULVUMVUNXFXGVUOUYJPUYFQRZPUVJQ
      RZNZVUBVUOVUMVULJZVUNVULJZJZUYJVURTVUOVUMVUNJZVULJVVAVUOVVBVULVULVUMVUNXH
      VULVUMVUNXIXJVUMVUNVULXKXLVUSVUTUYGVUPUVKVUQUYFPXMUVJPXMXNVTPUYFUVJXOXPVT
      VKXCUCWSXQXRWHXSXTYAYBYCUUOUUICAHUUJANZUUKYQUUNYOUUJAYPMVAVVCUULYMUUMYNUU
      JABMVAUUJABVNVKVLYDYEYFYGYH $.
      $( [13-Aug-2001] $)
  $}

  $( Natural number ordering relation. $)
  nnltp1le $p |- ( ( A e. NN /\ B e. NN ) ->
                ( A < B <-> ( A + 1 ) <_ B ) ) $=
    ( cn wcel wa clt wbr c1 caddc co cle cr wb nnre 1re ltadd1 mp3an3 syl2an
    peano2nn nnleltp1 sylan bitr4d ) ACDZBCDZEABFGZAHIJZBHIJFGZUFBKGZUCALDZBLDZ
    UEUGMZUDANBNUIUJHLDUKOABHPQRUCUFCDUDUHUGMASUFBTUAUB $.
    $( [19-Aug-2001] $)

  ${
    $d x y A $.  $d x y B $.
    nnsub.1 $e |- A e. NN $.
    nnsub.2 $e |- B e. NN $.
    $( Subtraction of natural numbers. $)
    nnsubi $p |- ( A < B <-> ( B - A ) e. NN ) $=
      ( vx vy clt wbr cmin co cn wcel wi cv c1 caddc wceq breq2 opreq1 eleq1d
      imbi12d cle wn nnge1 ax-mp 1re nnrei lenlti mpbi pm2.21i wo cr wb nnre
      leloe sylancr nnleltp1 mpan bitr3d peano2nn cc nncn nncni ax-1cn addsub
      mp3an2 sylancl syl5ibr imim2d com23 opreq1d cc0 addsubi subidi opreq1i
      addid2i 3eqtri 1nn eqeltri syl6eqelr a1d a1i jaod sylbird nnind nngt0
      posdifi sylibr impbii ) ABGHZBAIJZKLZBKLWJWLMZDAENZGHZWNAIJZKLZMAOGHZOAIJ
      ZKLZMAFNZGHZXAAIJZKLZMZAXAOPJZGHZXFAIJZKLZMWMEFBWNOQZWOWRWQWTWNOAGRXJWPWS
      KWNOAISTUAWNXAQZWOXBWQXDWNXAAGRXKWPXCKWNXAAISTUAWNXFQZWOXGWQXIWNXFAGRXLWP
      XHKWNXFAISTUAWNBQZWOWJWQWLWNBAGRXMWPWKKWNBAISTUAWRWTOAUBHZWRUCAKLZXNCAUDU
      EOAUFACUGZUHUIUJXAKLZXGXEXIXQXGXBAXAQZUKZXEXIMZXQAXAUBHZXSXGXQAULLXAULLYA
      XSUMXPXAUNAXAUOUPXOXQYAXGUMCAXAUQURUSXQXBXTXRXQXEXBXIXQXDXIXBXDXIXQXCOPJZ
      KLXCUTXQXHYBKXQXAVALZAVALZXHYBQZXAVBACVCZYCOVALYDYEVDXAOAVEVFVGTVHVIVJXRX
      TMXQXRXIXEXRXHAOPJZAIJZKXRYGXFAIAXAOPSVKYHOKYHAAIJZOPJVLOPJOAOAYFVDYFVMYI
      VLOPAYFVNVOOVDVPVQVRVSVTWAWBWCWDVJWEUEWLVLWKGHWJWKWFABXPBDUGWGWHWI $.
      $( [19-Aug-2001] $)
  $}

  $( Subtraction of natural numbers. $)
  nnsub $p |- ( ( A e. NN /\ B e. NN ) -> ( A < B <-> ( B - A ) e. NN ) ) $=
    ( cn wcel clt wbr cmin co wb c1 cif wceq breq1 opreq2 eleq1d bibi12d breq2
    opreq1 1nn elimel nnsubi dedth2h ) ACDZBCDZABEFZBAGHZCDZIUCAJKZBEFZBUHGHZCD
    ZIUHUDBJKZEFZULUHGHZCDZIABJJAUHLZUEUIUGUKAUHBEMUPUFUJCAUHBGNOPBULLZUIUMUKUO
    BULUHEQUQUJUNCBULUHGROPUHULAJCSTBJCSTUAUB $.
    $( [20-Aug-2001] $)

  $( Closure of addition of natural numbers minus one. $)
  nnaddm1cl $p |- ( ( A e. NN /\ B e. NN ) -> ( ( A + B ) - 1 ) e. NN ) $=
    ( cn wcel wa c1 caddc co clt wbr cmin cr cle nnre readdcl syl2an 1re
    readdcli a1i sylancr adantl nnge1 wb leadd2 mp3an13 syl mpbid adantr
    leadd1 mp3an1 letrd ltp1i jctil wi ltletr mp3an12 sylc 1nn nnaddcl nnsub )
    ACDZBCDZEZFABGHZIJZVDFKHCDZVCVDLDZFFFGHZIJZVHVDMJZEZVEVAALDZBLDZVGVBANZBNZA
    BOPZVCVJVIVCVHFBGHZVDVHLDZVCFFQQRZSVBVQLDZVAVBFLDZVMVTQVOFBOTUAVPVBVHVQMJZV
    AVBFBMJZWBBUBVBVMWCWBUCZVOWAVMWAWDQQFBFUDUEUFUGUAVCFAMJZVQVDMJZVAWEVBAUBUHV
    AVLVMWEWFUCZVBVNVOWAVLVMWGQFABUIUJPUGUKFQULUMWAVRVGVKVEUNQVSFVHVDUOUPUQVCFC
    DVDCDVEVFUCURABUSFVDUTTUG $.
    $( [6-Aug-2003] $)

  ${
    $d x A $.  $d x B $.
    $( Two ways to express " ` A ` divides ` B ` " for natural numbers. $)
    nndiv $p |- ( ( A e. NN /\ B e. NN ) ->
                ( E. x e. NN ( A x. x ) = B <-> ( B / A ) e. NN ) ) $=
      ( cn wcel wa cc0 wne cv cmul co wceq wrex cdiv wb nnne0 adantr cc wi
      nncn w3a divcan3 3coml 3expa sylan2 3adantl2 opreq1 sylan9req simplr
      eqeltrrd exp31 r19.23adv divcan2 3com12 opreq2 eqeq1d rcla4ev expcom syl
      impbid 3expia syl2an mpd ) BDEZCDEZFBGHZBAIZJKZCLZADMZCBNKZDEZOZVDVFVEBPQ
      VDBREZCREZVFVMSVEBTCTVNVOVFVMVNVOVFUAZVJVLVPVIVLADVPVGDEZVIVLVPVQFZVIFVGV
      KDVRVIVGVHBNKZVKVNVFVQVSVGLZVOVQVNVFFVGREZVTVGTVNVFWAVTWAVNVFVTVGBUBUCUDU
      EUFVHCBNUGUHVPVQVIUIUJUKULVPBVKJKZCLZVLVJSVOVNVFWCCBUMUNVLWCVJVIWCAVKDVGV
      KLVHWBCVGVKBJUOUPUQURUSUTVAVBVC $.
      $( [3-Feb-2004] $)
  $}

  $( Transitive property of divisibility: if ` A ` divides ` B ` and ` B `
     divides ` C ` , then ` A ` divides ` C ` .  Typically ` C ` would be an
     integer, although the theorem holds for complex ` C ` . $)
  nndivtr $p |- ( ( ( A e. NN /\ B e. NN /\ C e. CC ) /\
               ( ( B / A ) e. NN /\ ( C / B ) e. NN ) ) -> ( C / A ) e. NN ) $=
    ( cn wcel cc w3a cdiv co wa cmul nnmulcl c1 cc0 wne wceq nncn 3ad2ant2
    simp3 nnne0 jca 3ad2ant1 divmul24 syl22anc divid syl11anc opreq1d divcl
    3expb sylan2 ancoms mulid2 syl 3adant2 3eqtrd eleq1d syl5ib imp ) ADEZBDEZC
    FEZGZBAHIZDECBHIZDEJZCAHIZDEZVEVCVDKIZDEVBVGVCVDLVBVHVFDVBVHBBHIZVFKIZMVFKI
    ZVFVBBFEZVAAFEZANOZJZVLBNOZJZVHVJPUTUSVLVABQZRUSUTVASUSUTVOVAUSVMVNAQATUAZU
    BUTUSVQVAUTVLVPVRBTZUARBCABUCUDUTUSVJVKPVAUTVIMVFKUTVLVPVIMPVRVTBUEUFUGRUSV
    AVKVFPZUTUSVAJVFFEZWAVAUSWBUSVAVOWBVSVAVMVNWBCAUHUIUJUKVFULUMUNUOUPUQUR $.
    $( [3-May-2005] $)

