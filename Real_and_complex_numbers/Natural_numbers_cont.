$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Natural numbers (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.  $d x y z A $.  $d x ps $.  $d x ch $.  $d x z th $.
    $d y z ph $.
    $( Substitutions. $)
    nn1suc.1 $e |- ( x = 1 -> ( ph <-> ps ) ) $.
    nn1suc.3 $e |- ( x = ( y + 1 ) -> ( ph <-> ch ) ) $.
    nn1suc.4 $e |- ( x = A -> ( ph <-> th ) ) $.
    $( Hypothesis showing it holds for 1. $)
    nn1suc.5 $e |- ps $.
    $( Hypothesis showing it holds for a successor. $)
    nn1suc.6 $e |- ( y e. NN -> ch ) $.
    $( If a statement holds for 1 and also holds for a successor, it holds for
       all natural numbers.  The first three hypotheses give us the
       substitution instances we need; the last two show that it holds for 1
       and for a successor. $)
    nn1suc $p |- ( A e. NN -> th ) $=
      ( vz cn wcel wi cv wsbc c1 caddc co dfsbcq sbequ wceq wb wex elex ax-17
      hbsbc1 hbbi sbceq1a bitr3d imbi2d 19.23ai syl pm5.74rd pm2.43i sylan9bbr
      expcom pm5.74d sb19.21 syl5bb 1nn elisseti isseti hbsbc1v mpbiri mpbid
      ax-mp a1i cvv sbc19.21g mpbir oprex sylib a1d sylibr nnind ) GNOZDVSAPZEM
      QZRZVTESRZVTEFQZRZVTEWDSTUAZRZVSDPZMFGVTEWASUBVTMFEUCVTEWAWFUBWAGUDZVSAEW
      ARZPWHWBWIVSWJDVSWIWJDUEWIWJAEGRZVSDAEWAGUBVSWKDUEVSVSWKDVSEQZGUDZEUFVSWK
      PZWHUEZEGNUGWMWOEWNWHEAEMGNWAGOEUHUIWHEUHUJWMWKDVSWMAWKDAEGUKJULUMUNUOUPU
      QURUSUTVSAEMVSEUHZVAVBWCVSAESRZPZWQVSWLSUDZEUFWQESSNVCVDZVEWSWQEAESWTVFWS
      AWQWSABKHVGAESUKVHUNVIVJSVKOWCWRUEWTVSAESVKWPVLVIVMWDNOZWGWEXAVSAEWFRZPZW
      GXAXBVSXACXBLWLWFUDZEUFCXBUEZEWFWDSTVNZVEXDXEECXBECEUHAEWFXFVFUJXDACXBIAE
      WFUKULUNVIVOVPWFVKOWGXCUEXFVSAEWFVKWPVLVIVQVPVRUQ $.
      $( [17-Oct-2004] $) $( [11-Oct-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Closure of addition of natural numbers, proved by induction on the
       second addend. $)
    nnaddcl $p |- ( ( A e. NN /\ B e. NN ) -> ( A + B ) e. NN ) $=
      ( vx vy cn wcel caddc co cv wi c1 wceq opreq2 eleq1d imbi2d peano2nn wa
      cc ax1cn addass mp3an3 nncn syl2an syl5bi expcom a2d nnind impcom ) BEFAE
      FZABGHZEFZUIACIZGHZEFZJUIAKGHZEFZJUIADIZGHZEFZJUIAUQKGHZGHZEFZJUIUKJCDBUL
      KLZUNUPUIVCUMUOEULKAGMNOULUQLZUNUSUIVDUMUREULUQAGMNOULUTLZUNVBUIVEUMVAEUL
      UTAGMNOULBLZUNUKUIVFUMUJEULBAGMNOAPUQEFZUIUSVBUIVGUSVBJUIVGQZURKGHZEFVBUS
      VHVIVAEARFZUQRFZVIVALZUIVGVJVKKRFVLSAUQKTUAAUBUQUBUCNURPUDUEUFUGUH $.
      $( [12-Jan-1997] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Closure of multiplication of natural numbers. $)
    nnmulcl $p |- ( ( A e. NN /\ B e. NN ) -> ( A x. B ) e. NN ) $=
      ( vx vy cn wcel cmul co cv wi c1 caddc wceq opreq2 eleq1d imbi2d cc nncn
      ax1id biimprd mpcom wa ax1cn adddi mp3an3 opreq2d adantr eqtrd syl2an
      nnaddcl ancoms syl5bir exp4b pm2.43b a2d nnind impcom ) BEFAEFZABGHZEFZUR
      ACIZGHZEFZJURAKGHZEFZJURADIZGHZEFZJURAVFKLHZGHZEFZJURUTJCDBVAKMZVCVEURVLV
      BVDEVAKAGNOPVAVFMZVCVHURVMVBVGEVAVFAGNOPVAVIMZVCVKURVNVBVJEVAVIAGNOPVABMZ
      VCUTURVOVBUSEVABAGNOPAQFZURVEARZVPVEURVPVDAEASZOTUAVFEFZURVHVKVSURVHVKJUR
      VSURVHVKURVSUBZVKVGALHZEFZURVHUBVTVJWAEVPVFQFZVJWAMURVSVPWCUBVJVGVDLHZWAV
      PWCKQFVJWDMUCAVFKUDUEVPWDWAMWCVPVDAVGLVRUFUGUHVQVFRUIOVHURWBVGAUJUKULUMUN
      UOUPUQ $.
      $( [12-Jan-1997] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( There exists a natural number greater than or equal to any two
       others. $)
    nn2ge $p |- ( ( A e. NN /\ B e. NN ) ->
                E. x e. NN ( A <_ x /\ B <_ x ) ) $=
      ( cn wcel wa cv cle wbr wrex cr nnre adantr adantl leid biantrud biimpa
      sylan wceq breq2 anbi12d rcla4ev syldan adantll anim1i adantlr lecasei )
      BDEZCDEZFBAGZHIZCUJHIZFZADJZBCUHBKEZUIBLZMUICKEZUHCLZNUIBCHIZUNUHUIUSUSCC
      HIZFZUNUQUSVAUIUQUSVAUQUTUSCOPQURRUMVAACDUJCSUKUSULUTUJCBHTUJCCHTUAUBUCUD
      UHCBHIZUNUIUHVBBBHIZVBFZUNUOVBVDUHUOVCVBBOUEUPRUMVDABDUJBSUKVCULVBUJBBHTU
      JBCHTUAUBUCUFUG $.
      $( [18-Aug-1999] $)
  $}

  ${
    $d x y A $.
    $( A natural number is one or greater. $)
    nnge1 $p |- ( A e. NN -> 1 <_ A ) $=
      ( vx vy c1 cv cle wbr caddc co breq2 1re leidi cn wcel cr wi nnre cc0 cc
      wceq recn addid1 syl breq2d clt wn lt01 0re axltadd mp3an12 mpi wa
      axlttrn mp3an3 readdcl mpan2 peano2re sylanc mpand con3d wb lenlt
      sylancr 3imtr4d sylbird nnind ) DBEZFGDDFGDCEZFGZDVHDHIZFGZDAFGBCAVGDDFJV
      GVHDFJVGVJDFJVGADFJDKLVHMNVHONZVIVKPVHQVLVIDVHRHIZFGZVKVLVMVHDFVLVHSNVMVH
      TVHUAVHUBUCUDVLVMDUEGZUFZVJDUEGZUFZVNVKVLVQVOVLVMVJUEGZVQVOVLRDUEGZVSUGRO
      NZDONZVLVTVSPUHKRDVHUIUJUKVMONZVJONZVSVQULVOPZVLWCWDWBWEKVMVJDUMUNVLWAWCU
      HVHRUOUPZVHUQZURUSUTWBWCVNVPVAVLDVMVBKWFVCWBWDVKVRVAVLDVJVBKWGVCVDVEUCVF
      $.
      $( [25-Aug-1999] $)
  $}

  $( A natural number is greater than one iff it is not equal to one. $)
  nngt1ne1 $p |- ( A e. NN -> ( 1 < A <-> A =/= 1 ) ) $=
    ( cr wcel c1 cle wbr clt wne wb cn 1re leltne mp3an1 nnre nnge1 sylanc ) AB
    CZDAEFZDAGFADHIZAJCDBCQRSKDALMANAOP $.
    $( [20-Jul-2007] $) $( [7-Oct-2004] $)

  $( A natural number is less than or equal to one iff it is equal to one. $)
  nnle1eq1 $p |- ( A e. NN -> ( A <_ 1 <-> A = 1 ) ) $=
    ( cn wcel c1 cle wbr wa wceq nnge1 biantrud cr wb letri3 nnre 1re sylancl
    bitr4d ) ABCZADEFZSDAEFZGZADHZRTSAIJAKCDKCUBUALRADMANOPQ $.
    $( [22-Apr-2005] $) $( [3-Apr-2005] $)

  $( A natural number is positive. $)
  nngt0 $p |- ( A e. NN -> 0 < A ) $=
    ( cr wcel c1 cle wbr cc0 clt cn lt01 wa wi 0re 1re ltletr mp3an12 mpani
    nnre nnge1 sylc ) ABCZDAEFZGAHFZAICUAGDHFZUBUCJGBCDBCUAUDUBKUCLMNGDAOPQARAS
    T $.
    $( [26-Sep-1999] $)

  $( A number less than one is not a natural number. $)
  lt1nnn $p |- ( ( A e. RR /\ A < 1 ) -> -. A e. NN ) $=
    ( cr wcel c1 clt wbr cn wn cle wb 1re ltnle mpan2 nnge1 con3i syl6bi imp )
    ABCZADEFZAGCZHZRSDAIFZHZUARDBCSUCJKADLMTUBANOPQ $.
    $( [18-Jan-2004] $) $( [18-Jan-2004] $)

  $( Zero is not a natural number. $)
  0nnn $p |- -. 0 e. NN $=
    ( cc0 cr wcel c1 clt wbr cn wn 0re lt01 lt1nnn mp2an ) ABCADEFAGCHIJAKL $.
    $( [25-Aug-1999] $)

  $( A natural number is nonzero. $)
  nnne0 $p |- ( A e. NN -> A =/= 0 ) $=
    ( cn wcel cc0 wceq 0nnn eleq1 mtbiri necon2ai ) ABCZADADEJDBCFADBGHI $.
    $( [27-Sep-1999] $)

  ${
    nngt0.1 $e |- A e. NN $.
    $( A natural number is positive (inference version). $)
    nngt0i $p |- 0 < A $=
      ( cn wcel cc0 clt wbr nngt0 ax-mp ) ACDEAFGBAHI $.
      $( [17-Sep-1999] $)

    $( A natural number is nonzero (inference version). $)
    nnne0i $p |- A =/= 0 $=
      ( nnrei nngt0i gt0ne0ii ) AABCABDE $.
      $( [25-Aug-1999] $)
  $}

  $( The quotient of a real and a natural number is real. $)
  nndivre $p |- ( ( A e. RR /\ N e. NN ) -> ( A / N ) e. RR ) $=
    ( cr wcel cc0 wne wa cdiv co cn redivcl 3expb nnre nnne0 jca sylan2 ) ACDZB
    CDZBEFZGABHICDZBJDZQRSTABKLUARSBMBNOP $.
    $( [1-Dec-2008] $) $( [28-Nov-2008] $)

  $( The reciprocal of a natural number is real. $)
  nnrecre $p |- ( N e. NN -> ( 1 / N ) e. RR ) $=
    ( c1 cr wcel cn cdiv co 1re nndivre mpan ) BCDAEDBAFGCDHBAIJ $.
    $( [16-Aug-2008] $) $( [8-Feb-2008] $)

  $( The reciprocal of a natural number is positive. $)
  nnrecgt0 $p |- ( A e. NN -> 0 < ( 1 / A ) ) $=
    ( cn wcel c1 cle wbr cc0 cdiv co clt nnge1 lt01 cr wa wi nnre 0re 1re
    ltletr mp3an12 recgt0 ex syld syl mpani mpd ) ABCZDAEFZGDAHIJFZAKUGGDJFZUHU
    ILUGAMCZUJUHNZUIOAPUKULGAJFZUIGMCDMCUKULUMOQRGDASTUKUMUIAUAUBUCUDUEUF $.
    $( [25-Aug-1999] $)

  ${
    $d x y z w v A $.  $d x y z w v B $.
    $( Natural number ordering relation. $)
    nnleltp1 $p |- ( ( A e. NN /\ B e. NN ) ->
                  ( A <_ B <-> A < ( B + 1 ) ) ) $=
      ( vz vx vy vw vv cn wcel wa cle wbr clt wceq wo c1 caddc co cr wb leloe
      nnre syl2an wi cc0 lt01 0re 1re lt2add an4s mpanr12 mpan2i cc pm3.26
      recn addid1 3syl breq1d sylibd breq1 ltp1 syl5cbir adantl jaod cv wral
      eqeq1 orbi12d imbi12d rcla4v opreq1 breq2d breq2 eqeq2 ralbidv eqid olci
      a1i ltadd1 mp3an23 syl wn nnge1 lenlt sylancr mpbid pm2.21d sylbird
      nn1suc rgen imbi2d nngt0 mp3an13 ax1cn addid2i syl5eqbrr orcd a1d adantr
      rcla4va adantlr mp3an3 peano2nn w3a 3anim123i 3anrot sylibr 3simpc
      3simp1 jca anandir sylib addcom eqeqan12d addcan bitr2d 3imtr3d expl
      exp3a com3l r19.21adv cbvralv syl5ib nnind syl5 imp impbid bitrd ) AHIZBH
      IZJZABKLZABMLZABNZOZABPQRZMLZASIZBSIZYLYOTYIYJABUAAUBZBUBZUCYKYOYQYRYSYOY
      QUDYIYJYRYSJZYMYQYNUUBYMAUEQRZYPMLZYQUUBYMUEPMLZUUDUFUUBUESIZPSIZYMUUEJUU
      DUDZUGUHYRUUFYSUUGUUHAUEBPUIUJUKULUUBUUCAYPMUUBYRAUMIUUCANYRYSUNAUOAUPUQU
      RUSYSYNYQUDYRYNYQBYPMLYSABYPMUTBVAVBVCVDYTUUAUCYIYJYQYOUDZYICVEZYPMLZUUJB
      MLZUUJBNZOZUDZCHVFZUUIYJUUOUUICAHUUJANZUUKYQUUNYOUUJAYPMUTUUQUULYMUUMYNUU
      JABMUTUUJABVGVHVIVJUUJDVEZPQRZMLZUUJUURMLZUUJUURNZOZUDZCHVFUUJPPQRZMLZUUJ
      PMLZUUJPNZOZUDZCHVFUUJEVEZPQRZMLZUUJUVKMLZUUJUVKNZOZUDZCHVFZUUJUVLPQRZMLZ
      UVMUUJUVLNZOZUDZCHVFZUUPDEBUURPNZUVDUVJCHUWEUUTUVFUVCUVIUWEUUSUVEUUJMUURP
      PQVKVLUWEUVAUVGUVBUVHUURPUUJMVMUURPUUJVNVHVIVOUURUVKNZUVDUVQCHUWFUUTUVMUV
      CUVPUWFUUSUVLUUJMUURUVKPQVKVLUWFUVAUVNUVBUVOUURUVKUUJMVMUURUVKUUJVNVHVIVO
      UURUVLNZUVDUWCCHUWGUUTUVTUVCUWBUWGUUSUVSUUJMUURUVLPQVKVLUWGUVAUVMUVBUWAUU
      RUVLUUJMVMUURUVLUUJVNVHVIVOUURBNZUVDUUOCHUWHUUTUUKUVCUUNUWHUUSYPUUJMUURBP
      QVKVLUWHUVAUULUVBUUMUURBUUJMVMUURBUUJVNVHVIVOUVJCHUURUVEMLZUURPMLZUWEOZUD
      PUVEMLZPPMLZPPNZOZUDUVLUVEMLZUVLPMLZUVLPNZOZUDUVJDEUUJUWEUWIUWLUWKUWOUURP
      UVEMUTUWEUWJUWMUWEUWNUURPPMUTUURPPVGVHVIUWGUWIUWPUWKUWSUURUVLUVEMUTUWGUWJ
      UWQUWEUWRUURUVLPMUTUURUVLPVGVHVIUURUUJNZUWIUVFUWKUVIUURUUJUVEMUTUWTUWJUVG
      UWEUVHUURUUJPMUTUURUUJPVGVHVIUWOUWLUWNUWMPVPVQVRUVKHIZUWPUVKPMLZUWSUXAUVK
      SIZUXBUWPTZUVKUBZUXCUUGUUGUXDUHUHUVKPPVSVTWAUXAUXBUWSUXAPUVKKLZUXBWBZUVKW
      CUUGUXCUXFUXGTUXAPUVKWDUHUXEWEWFWGWHWIWJUXAFVEZUVLMLZUXHUVKMLZUXHUVKNZOZU
      DZFHVFZUWDUVRUXAUXNUWCCHUUJHIZUXAUXNUWCUXOUXAUXNUWCUXAUXNJZUURUVSMLZUURUV
      LMLZUWGOZUDZUDUXPPUVSMLZPUVLMLZPUVLNZOZUDZUDUXPGVEZPQRZUVSMLZUYGUVLMLZUYG
      UVLNZOZUDZUDUXPUWCUDDGUUJUWEUXTUYEUXPUWEUXQUYAUXSUYDUURPUVSMUTUWEUXRUYBUW
      GUYCUURPUVLMUTUURPUVLVGVHVIWKUURUYGNZUXTUYLUXPUYMUXQUYHUXSUYKUURUYGUVSMUT
      UYMUXRUYIUWGUYJUURUYGUVLMUTUURUYGUVLVGVHVIWKUWTUXTUWCUXPUWTUXQUVTUXSUWBUU
      RUUJUVSMUTUWTUXRUVMUWGUWAUURUUJUVLMUTUURUUJUVLVGVHVIWKUXAUYEUXNUXAUYDUYAU
      XAUYBUYCUXAUEPQRZUVLPMUXAUEUVKMLZUYNUVLMLZUVKWLUXAUXCUYOUYPTZUXEUUFUXCUUG
      UYQUGUHUEUVKPVSWMWAWFPWNWOWPWQWRWSUYFHIZUXAUXNUYLUYRUXAJZUXNJUYFUVLMLZUYF
      UVKMLZUYFUVKNZOZUYHUYKUYRUXNUYTVUCUDZUXAUXMVUDFUYFHUXHUYFNZUXIUYTUXLVUCUX
      HUYFUVLMUTVUEUXJVUAUXKVUBUXHUYFUVKMUTUXHUYFUVKVGVHVIWTXAUYSUYTUYHTZUXNUYF
      SIZUVLSIZVUFUYRUXAVUGVUHUUGVUFUHUYFUVLPVSXBUYFUBZUXAUVLHIVUHUVKXCUVLUBWAU
      CWSUYSVUCUYKTZUXNVUGUXCVUJUYRUXAVUGUXCUUGVUJUHVUGUXCUUGXDZVUAUYIVUBUYJUYF
      UVKPVSVUKPUMIZUYFUMIZUVKUMIZXDZVUBUYJTVUKVUMVUNVULXDVUOVUGVUMUXCVUNUUGVUL
      UYFUOUVKUOPUOXEVULVUMVUNXFXGVUOUYJPUYFQRZPUVKQRZNZVUBVUOVUMVULJZVUNVULJZJ
      ZUYJVURTVUOVUMVUNJZVULJVVAVUOVVBVULVULVUMVUNXHVULVUMVUNXIXJVUMVUNVULXKXLV
      USVUTUYGVUPUVLVUQUYFPXMUVKPXMXNWAPUYFUVKXOXPWAVHXBVUIUXEUCWSXQXRWIXSXTYAU
      VQUXMCFHUUJUXHNZUVMUXIUVPUXLUUJUXHUVLMUTVVCUVNUXJUVOUXKUUJUXHUVKMUTUUJUXH
      UVKVGVHVIYBYCYDYEYFYGYH $.
      $( [13-Aug-2001] $)
  $}

  $( Natural number ordering relation. $)
  nnltp1le $p |- ( ( A e. NN /\ B e. NN ) ->
                ( A < B <-> ( A + 1 ) <_ B ) ) $=
    ( cn wcel wa clt wbr c1 caddc co cle cr wb 1re ltadd1 mp3an3 nnre syl2an
    nnleltp1 peano2nn sylan bitr4d ) ACDZBCDZEABFGZAHIJZBHIJFGZUFBKGZALDZBLDZUE
    UGMZUCUDUIUJHLDUKNABHOPAQBQRUFCDUDUHUGMUCUFBSATUAUB $.
    $( [19-Aug-2001] $)

  ${
    $d x y A $.  $d x y B $.
    nnsub.1 $e |- A e. NN $.
    nnsub.2 $e |- B e. NN $.
    $( Subtraction of natural numbers. $)
    nnsubi $p |- ( A < B <-> ( B - A ) e. NN ) $=
      ( vx vy clt wbr cmin co cn wcel wi cv c1 caddc wceq breq2 opreq1 eleq1d
      imbi12d cle wn nnge1 ax-mp 1re nnrei lenlti mpbi pm2.21i wo cr wb leloe
      nnre sylancr nnleltp1 mpan bitr3d cc ax1cn addsub mp3an2 nncn nncni
      sylancl peano2nn syl5bir imim2d com23 opreq1d cc0 addsubi subidi opreq1i
      addid2i 3eqtri 1nn eqeltri syl6eqelr a1d a1i jaod sylbird nnind nngt0
      posdifi sylibr impbii ) ABGHZBAIJZKLZBKLWJWLMZDAENZGHZWNAIJZKLZMAOGHZOAIJ
      ZKLZMAFNZGHZXAAIJZKLZMZAXAOPJZGHZXFAIJZKLZMWMEFBWNOQZWOWRWQWTWNOAGRXJWPWS
      KWNOAISTUAWNXAQZWOXBWQXDWNXAAGRXKWPXCKWNXAAISTUAWNXFQZWOXGWQXIWNXFAGRXLWP
      XHKWNXFAISTUAWNBQZWOWJWQWLWNBAGRXMWPWKKWNBAISTUAWRWTOAUBHZWRUCAKLZXNCAUDU
      EOAUFACUGZUHUIUJXAKLZXGXEXIXQXGXBAXAQZUKZXEXIMZXQAXAUBHZXSXGAULLXAULLYAXS
      UMXQAXAUNXPXAUOUPXOXQYAXGUMCAXAUQURUSXQXBXTXRXQXEXBXIXQXDXIXBXQXIXCOPJZKL
      XDXQXHYBKXAUTLZAUTLZXHYBQZXQYCOUTLYDYEVAXAOAVBVCXAVDACVEZVFTXCVGVHVIVJXRX
      TMXQXRXIXEXRXHAOPJZAIJZKXRYGXFAIAXAOPSVKYHOKYHAAIJZOPJVLOPJOAOAYFVAYFVMYI
      VLOPAYFVNVOOVAVPVQVRVSVTWAWBWCWDVJWEUEWLVLWKGHWJWKWFABXPBDUGWGWHWI $.
      $( [19-Aug-2001] $)
  $}

  $( Subtraction of natural numbers. $)
  nnsub $p |- ( ( A e. NN /\ B e. NN ) -> ( A < B <-> ( B - A ) e. NN ) ) $=
    ( cn wcel clt wbr cmin co wb c1 cif wceq breq1 opreq2 eleq1d bibi12d breq2
    opreq1 1nn elimel nnsubi dedth2h ) ACDZBCDZABEFZBAGHZCDZIUCAJKZBEFZBUHGHZCD
    ZIUHUDBJKZEFZULUHGHZCDZIABJJAUHLZUEUIUGUKAUHBEMUPUFUJCAUHBGNOPBULLZUIUMUKUO
    BULUHEQUQUJUNCBULUHGROPUHULAJCSTBJCSTUAUB $.
    $( [20-Aug-2001] $)

  $( Closure of addition of natural numbers minus one. $)
  nnaddm1cl $p |- ( ( A e. NN /\ B e. NN ) -> ( ( A + B ) - 1 ) e. NN ) $=
    ( cn wcel wa c1 caddc co clt wbr cmin cr cle wi 1re readdcli ltletr
    mp3an12 readdcl nnre syl2an a1i sylancr adantl nnge1 wb leadd2 mp3an13 syl
    mpbid adantr leadd1 mp3an1 letrd ltp1i jctil sylc nnsub 1nn nnaddcl ) ACDZB
    CDZEZFABGHZIJZVDFKHCDZVDLDZFFFGHZIJZVHVDMJZEZVEVCFLDZVHLDZVGVKVENOFFOOPZFVH
    VDQRALDZBLDZVGVAVBABSATZBTZUAZVCVJVIVCVHFBGHZVDVMVCVNUBVBVTLDZVAVLVPWAVBFBS
    OVRUCUDVSVBVHVTMJZVAVBFBMJZWBBUEVBVPWCWBUFZVRVLVPVLWDOOFBFUGUHUIUJUDVCFAMJZ
    VTVDMJZVAWEVBAUEUKVOVPWEWFUFZVAVBVLVOVPWGOFABULUMVQVRUAUJUNFOUOUPUQFCDVDCDV
    EVFUFVCFVDURUSABUTUCUJ $.
    $( [6-Aug-2003] $)

  ${
    $d x A $.  $d x B $.
    $( Two ways to express " ` A ` divides ` B ` " for natural numbers. $)
    nndiv $p |- ( ( A e. NN /\ B e. NN ) ->
                ( E. x e. NN ( A x. x ) = B <-> ( B / A ) e. NN ) ) $=
      ( cn wcel wa cc0 wne cv cmul co wceq wrex cdiv wb nnne0 adantr cc wi w3a
      divcan3 3coml 3expa nncn sylan2 3adantl2 opreq1 sylan9req simplr
      eqeltrrd exp31 r19.23adv divcan2 3com12 opreq2 eqeq1d rcla4ev expcom syl
      impbid 3expia syl2an mpd ) BDEZCDEZFBGHZBAIZJKZCLZADMZCBNKZDEZOZVDVFVEBPQ
      BREZCREZVFVMSVDVEVNVOVFVMVNVOVFTZVJVLVPVIVLADVPVGDEZVIVLVPVQFZVIFVGVKDVRV
      IVGVHBNKZVKVNVFVQVSVGLZVOVNVFFVGREZVTVQVNVFWAVTWAVNVFVTVGBUAUBUCVGUDUEUFV
      HCBNUGUHVPVQVIUIUJUKULVPBVKJKZCLZVLVJSVOVNVFWCCBUMUNVLWCVJVIWCAVKDVGVKLVH
      WBCVGVKBJUOUPUQURUSUTVABUDCUDVBVC $.
      $( [20-Feb-2004] $) $( [3-Feb-2004] $)
  $}

  $( Transitive property of divisibility: if ` A ` divides ` B ` and ` B `
     divides ` C ` , then ` A ` divides ` C ` .  Typically ` C ` would be an
     integer, although the theorem holds for complex ` C ` . $)
  nndivtr $p |- ( ( ( A e. NN /\ B e. NN /\ C e. CC ) /\
               ( ( B / A ) e. NN /\ ( C / B ) e. NN ) ) -> ( C / A ) e. NN ) $=
    ( cn wcel cc w3a cdiv co wa cmul c1 cc0 wne wceq divmul24 nncn 3ad2ant2
    3simp3 nnne0 jca 3ad2ant1 syl2anc divid sylanc opreq1d divcl 3expb sylan2
    ancoms mulid2 syl 3adant2 3eqtrd eleq1d nnmulcl syl5bi imp ) ADEZBDEZCFEZGZ
    BAHIZDECBHIZDEJZCAHIZDEZVBVCVDKIZDEVGVEVBVHVFDVBVHBBHIZVFKIZLVFKIZVFBFEZVAA
    FEZAMNZJZVLBMNZJZVHVJOVBBCABPUTUSVLVABQZRUSUTVASUSUTVOVAUSVMVNAQATUAZUBUTUS
    VQVAUTVLVPVRBTZUARUCUTUSVJVKOVAUTVILVFKVLVPVILOUTBUDVRVTUEUFRUSVAVKVFOZUTUS
    VAJVFFEZWAVAUSWBVAVOWBUSVAVMVNWBCAUGUHVSUIUJVFUKULUMUNUOVCVDUPUQUR $.
    $( [6-May-2005] $) $( [3-May-2005] $)

