$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The exponential, sine, and cosine functions (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A j k x y $.  $d F k x $.  $d M j k x y $.
    abspef01tlub.1 $e |- F = { <. j , y >. | ( j e. NN0 /\
                               y = ( ( ( _i x. A ) ^ j ) / ( ! ` j ) ) ) } $.
    abspef01tlub.2 $e |- ( P = Re \/ P = Im ) $.
    $( An upper bound on the absolute value of the infinite tail of the series
       expansion of the exponential function on the punctured closed unit disc
       projected onto the real or imaginary axis.  (Contributed by Paul
       Chapman, 19-Jan-2008.) $)
    abspef01tlubi $p |- ( ( A e. ( 0 (,] 1 ) /\ M e. NN ) ->
                       ( abs ` ( P ` sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) ) <_
                       ( ( A ^ M ) x. ( ( M + 1 ) / ( ( ! ` M ) x. M ) ) ) ) $=
      ( c1 co wcel cfv cabs cmul cc cr wceq wbr cle cc0 cioc cn wa cuz csu cexp
      cv caddc cfa cdiv cre cim wo wi fveq1 adantr ci ax-icn clt w3a wb 0re 1re
      elioc2 mp2an simp1bi recnd mulcl sylancr eftlcl sylan recl adantl eqeltrd
      syl ex imcl jaoi ax-mp abscl cn0 reexpcl syl2an eftlubcl remulcl syl11anc
      nnnn0 absrele fveq2d breq1d syl5ibr absimle absi opreq1i a1i simp2bi ltle
      absmul mpan sylc absid mulid2 3eqtrd eqtrd jca absef01tlubi 3expa opreq1d
      id breq2d mpbid letrd ) BUAJUBKZLZGUCLZUDZGUEMEUHFMEUFZCMZNMZXRNMZBGUGKZG
      JUIKGUJMGOKUKKZOKZXQXSPLXTQLXQXSCULRZCUMRZUNZXQXSQLZUOZIYEYIYFYEXQYHYEXQU
      DXSXRULMZQYEXSYJRXQXRCULUPZUQXQYJQLZYEXQXRPLZYLXOURBOKZPLZXPYMXOURPLZBPLZ
      YOUSXOBXOBQLZUABUTSZBJTSZUAQLZJQLXOYRYSYTVAVBVCVDUAJBVEVFZVGZVHZURBVIVJZA
      YNDEFGHVKVLZXRVMVPVNVOVQYFXQYHYFXQUDXSXRUMMZQYFXSUUGRXQXRCUMUPZUQXQUUGQLZ
      YFXQYMUUIUUFXRVRVPVNVOVQVSVTVHXSWAVPXQYMYAQLUUFXRWAVPXQYBQLZYCQLZYDQLXOYR
      GWBLUUJXPUUCGWHBGWCWDXPUUKXOGWEVNYBYCWFWGYGXQXTYATSZUOZIYEUUMYFXQUULYEYJN
      MZYATSZXQYMUUOUUFXRWIVPYEXTUUNYATYEXSYJNYKWJWKWLXQUULYFUUGNMZYATSZXQYMUUQ
      UUFXRWMVPYFXTUUPYATYFXSUUGNUUHWJWKWLVSVTXQYAYNNMZGUGKZYCOKZTSZYAYDTSZXOYO
      UURXNLZUDXPUVAXOYOUVCUUEXOUURBXNXOUURBNMZBXOUURURNMZUVDOKZJUVDOKZUVDXOYPY
      QUURUVFRUSUUDURBWSVJUVFUVGRXOUVEJUVDOWNWOWPXOUVDPLUVGUVDRXOUVDBPXOYRUABTS
      ZUVDBRUUCXOYRYSUVHUUCXOYRYSYTUUBWQUUAYRYSUVHUOVCUABWRWTXABXBWGZUUDVOUVDXC
      VPXDUVIXEZXOXJVOXFYOUVCXPUVAAYNDEFGHXGXHVLXOUVAUVBVBXPXOUUTYDYATXOUUSYBYC
      OXOUURBGUGUVJXIXIXKUQXLXM $.
      $( [19-Jan-2008] $)
  $}

  ${
    $d A j x y $.  $d F k x $.  $d M k x $.
    efsep.1 $e |- F = { <. j , y >. |
                    ( j e. NN0 /\ y = ( ( A ^ j ) / ( ! ` j ) ) ) } $.
    efsep.2 $e |- A e. CC $.
    efsep.3 $e |- M e. NN0 $.
    efsep.4 $e |- B e. CC $.
    efsep.5 $e |- ( exp ` A ) = ( B + sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) $.
    efsep.6 $e |- ( F ` M ) = C $.
    efsep.7 $e |- N = ( M + 1 ) $.
    efsep.8 $e |- D = ( B + C ) $.
    $( Separate out the next term of the power series expansion of the
       exponential function.  The last hypothesis allows the separated terms to
       be rearranged as desired.  (Contributed by Paul Chapman,
       23-Nov-2007.) $)
    efsepi $p |- ( exp ` A ) = ( D + sum_ k e. ( ZZ>= ` N ) ( F ` k ) ) $=
      ( caddc wcel vx ce cfv co c1 cuz cv csu cz cc wral cop cseqz cli wbr wceq
      wex cn0 nn0z ax-mp cc0 wss nn0uz eleqtri uzss sseli wf cexp cfa cdiv mpan
      eftcl fopab feq2i mpbi ffvelrni syl rgen cn nn0p1nn eftlex mp2an fopabex2
      nn0ex isum1p mp3an opreq2i nnz isumcl addassi 3eqtr4i eqcomi eqtri fveq2i
      sumeq1i opreq12i eqtr4i ) BUBUCZCIHUCZSUDZIUESUDZUFUCZGUGZHUCZGUHZSUDZEJU
      FUCZXDGUHZSUDCIUFUCZXDGUHZSUDCWSXESUDZSUDWRXFXJXKCSIUITZXDUJTZGXIUKXASULH
      UMUDUAUGUNUOUAUQZXJXKUPIURTZXLMIUSUTXMGXIXCXITXCVAUFUCZTXMXIXPXCIXPTZXIXP
      VBIURXPMVCVDZVAIVEUTVFXPUJXCHURUJHVGXPUJHVGFAURUJBFUGZVHUDXSVIUCVJUDZHKBU
      JTZXSURTXTUJTLBXSVLVKVMURXPUJHVCVNVOZVPVQVRYAXAVSTZXNLXOYCMIVTUTZUAABFHXA
      KWAWBZUAGHIFAURXTHWDKWCZWEWFWGOCWSXENXQWSUJTXRXPUJIHYBVPUTXAUITZXNXEUJTYC
      YGYDXAWHUTYEUAGHXAYFWIWBWJWKEWTXHXESECDSUDWTRDWSCSWSDPWLWGWMXGXBXDGJXAUFQ
      WNWOWPWQ $.
      $( [23-Nov-2007] $)
  $}

  ${
    $d A j k y $.  $d F k $.  $d N k $.
    effsumle.1 $e |- F =
             { <. j , y >. | ( j e. NN0 /\ y = ( ( A ^ j ) / ( ! ` j ) ) ) } $.
    effsumle.2 $e |- A e. RR $.
    effsumle.3 $e |- N e. NN0 $.
    $( The partial sums of the series expansion of the exponential function of
       a nonnegative real number are bounded by the value of the function.
       (Contributed by Paul Chapman, 21-Aug-2007.) $)
    effsumlei $p |- ( 0 <_ A -> ( ( + seq0 F ) ` N ) <_ ( exp ` A ) ) $=
      ( vk cc0 cle wbr cfv caddc co wcel cn0 cr mpan adantr cv cuz wral elnn0uz
      cseq0 ce wa cexp cfa cdiv clt reexpcl expge0 mp3an1 faccl nnre syl divge0
      cn nngt0 syl22anc eftval breqtrrd sylanbr expcom ralrimiv cop cseqz addex
      wceq nn0ex fopabex2 seq0seqz fveq1i nn0uz eleqtri fvex cli cc recni efcvg
      ax-mp eqbrtrri wf reeftcl fopab feq2i mpbi climserzlei syl5eqbr ) JBKLZJI
      UAZDMZKLZIJUBMZUCZENDUEOZMZBUFMZKLWKWNIWOWLWOPZWKWNWTWLQPZWKWNWLUDXAWKUGZ
      JBWLUHOZWLUIMZUJOZWMKXBXCRPZJXCKLZXDRPZJXDUKLZJXEKLXAXFWKBRPZXAXFGBWLULST
      XJXAWKXGGBWLUMUNXAXHWKXAXDUSPZXHWLUOZXDUPUQTXAXIWKXAXKXIXLXDUTUQTXCXDURVA
      XAWMXEVJWKABCDWLFVBTVCVDVEVFWPWREJNVGDVHOZMZWSKEWQXMNDVICAQBCUAZUHOXOUIMU
      JOZDVKFVLZVMZVNEWOPWPXNWSKLEQWOHVOVPWSIDJEXQBUFVQWQXMWSVRXRBVSPWQWSVRLBGV
      TABCDFWAWBWCQRDWDWORDWDCAQRXPDFXJXOQPXPRPGBXOWESWFQWORDVOWGWHWISWJUQ $.
      $( [21-Aug-2007] $)
  $}

  ${
    $d A j y $.
    eft0val.1 $e |- F =
             { <. j , y >. | ( j e. NN0 /\ y = ( ( A ^ j ) / ( ! ` j ) ) ) } $.
    eft0val.2 $e |- A e. CC $.
    $( The value of the first term of the series expansion of the exponential
       function is 1.  (Contributed by Paul Chapman, 21-Aug-2007.) $)
    eft0vali $p |- ( F ` 0 ) = 1 $=
      ( cc0 cfv cexp co cfa cdiv c1 cn0 wcel wceq 0nn0 eftval ax-mp cc opreq12i
      exp0 fac0 ax-1cn div1i 3eqtri ) GDHZBGIJZGKHZLJZMMLJMGNOUGUJPQABCDGERSUHM
      UIMLBTOUHMPFBUBSUCUAMUDUEUF $.
      $( [21-Aug-2007] $)
  $}

  ${
    $d A j k x y $.  $d F k $.
    ef4p.1 $e |- F = { <. j , y >. | ( j e. NN0 /\
                       y = ( ( A ^ j ) / ( ! ` j ) ) ) } $.

    ${
      ef4p.2 $e |- A e. CC $.
      $( Separate out the first four terms of the infinite series expansion of
         the exponential function.  (Contributed by Paul Chapman,
         19-Jan-2008.) $)
      ef4pi $p |- ( exp ` A ) = ( ( ( ( 1 + A ) + ( ( A ^ 2 ) / 2 ) ) +
                 ( ( A ^ 3 ) / 6 ) ) + sum_ k e. ( ZZ>= ` 4 ) ( F ` k ) ) $=
        ( c1 caddc co c2 cexp cdiv c3 cc0 cfv cn0 wcel wceq ax-mp c6 3nn nnnn0i
        c4 ax-1cn addcli sqcli 2cn 2ne0 divcli 2nn0 1nn0 0nn0 0cn ce cuz cv csu
        cfa cc efval eftval sumeq2i nn0uz sumeq1i 3eqtr2i efcl eqeltrri addid2i
        eqtr4i eft0vali eqcomi efsepi exp1 fac1 opreq12i div1i 3eqtri df-2 eqid
        fac2 opreq2i eqtri df-3 fac3 df-4 ) ABHBIJZBKLJZKMJZIJZBNLJZUAMJZWJWLIJ
        ZCDENUDFGNUBUCZWGWIHBUEGUFZWHKBGUGUHUIUJUFABWGWIWJCDEKNFGUKWOABHBWGCDEH
        KFGULUEABOHHCDEOHFGUMUNBUOPZOUPPZDUQZEPZDURZOWTIJWPQBWRLJWRUSPMJZDURZQW
        SDURWTBUTRZWPXBSGBDVATQWSXADABCEWRFVBVCQWQWSDVDVEVFZWTWPWTUTXDXCWPUTRGB
        VGTVHVIVJABCEFGVKOHIJHHUEVIVLZXEVMHEPZBHLJZHUSPZMJZBHMJBHQRXFXISULABCEH
        FVBTXGBXHHMXCXGBSGBVNTVOVPBGVQVRVSWGVTVMKEPZWHKUSPZMJZWIKQRXJXLSUKABCEK
        FVBTXKKWHMWAWBWCWDWJVTVMNEPZWKNUSPZMJZWLNQRXMXOSWNABCENFVBTXNUAWKMWEWBW
        CWFWMVTVM $.
        $( [19-Jan-2008] $)
    $}

    $( Separate out the first four terms of the infinite series expansion of
       the exponential function.  (Contributed by Paul Chapman,
       19-Jan-2008.) $)
    ef4p $p |- ( A e. CC -> ( exp ` A ) =
                ( ( ( ( 1 + A ) + ( ( A ^ 2 ) / 2 ) ) + ( ( A ^ 3 ) / 6 ) ) +
                  sum_ k e. ( ZZ>= ` 4 ) ( F ` k ) ) ) $=
      ( ce cfv c1 caddc co c2 cexp cdiv c3 c6 cv wceq cc0 opreq1 cc wcel c4 cuz
      csu cif cn0 wa copab fveq2 opreq2 opreq1d opreq12d eqeq2d anbi2d opabbidv
      cfa syl5eq fveq1d sumeq2sdv eqeq12d eqid 0cn elimel ef4pi dedth ) BUAUBZB
      GHZIBJKZBLMKZLNKZJKZBOMKZPNKZJKZUCUDHZDQZEHZDUEZJKZRVGBSUFZGHZIWAJKZWALMK
      ZLNKZJKZWAOMKZPNKZJKZVPVQCQZUGUBZAQZWAWJMKZWJUQHZNKZRZUHZCAUIZHZDUEZJKZRB
      SBWARZVHWBVTXABWAGUJXBVOWIVSWTJXBVLWFVNWHJXBVIWCVKWEJBWAIJUKXBVJWDLNBWALM
      TULUMXBVMWGPNBWAOMTULUMXBVPVRWSDXBVQEWRXBEWKWLBWJMKZWNNKZRZUHZCAUIWRFXBXF
      WQCAXBXEWPWKXBXDWOWLXBXCWMWNNBWAWJMTULUNUOUPURUSUTUMVAAWACDWRWRVBBSUAVCVD
      VEVF $.
      $( [19-Jan-2008] $)
  $}

  ${
    $d A j y $.
    efge1.1 $e |- A e. RR $.
    $( The exponential function of a nonnegative real number is greater than or
       equal to 1.  (Contributed by Paul Chapman, 21-Aug-2007.) $)
    efge1i $p |- ( 0 <_ A -> 1 <_ ( exp ` A ) ) $=
      ( vj vy cc0 cle wbr c1 caddc cv cn0 wcel cexp co cfa cfv cdiv wceq copab
      wa cseq0 ce addex nn0ex opabex2 seq00 eqid eft0vali eqtr2i 0nn0 effsumlei
      recni syl5eqbr ) EAFGHEICJZKLDJAUNMNUNOPQNZRTCDSZUANPZAUBPFUQEUPPHIUPUCCD
      KUOUDUEUFDACUPUPUGZABULUHUIDACUPEURBUJUKUM $.
      $( [21-Aug-2007] $)

    $( The exponential function of a nonnegative real number is greater than or
       equal to 1 plus that number.  (Contributed by Paul Chapman,
       18-Oct-2007.) $)
    efge1pi $p |- ( 0 <_ A -> ( 1 + A ) <_ ( exp ` A ) ) $=
      ( vj vy cc0 cle c1 caddc co cv cn0 wcel cexp cfa cfv cdiv wceq 1nn0 ax-mp
      opreq12i wbr wa copab cseq0 addex nn0ex opabex2 seq01 eqid recni eft0vali
      ce eftval cc exp1 fac1 div1i 3eqtri eqtr2i effsumlei syl5eqbr ) EAFUAGAHI
      ZGHCJZKLDJAVCMIVCNOPIZQUBCDUCZUDIOZAULOFVFEVEOZGVEOZHIVBHVEUECDKVDUFUGUHV
      GGVHAHDACVEVEUIZABUJZUKVHAGMIZGNOZPIZAGPIAGKLVHVMQRDACVEGVIUMSVKAVLGPAUNL
      VKAQVJAUOSUPTAVJUQURTUSDACVEGVIBRUTVA $.
      $( [18-Oct-2007] $)

    $( The exponential function of a positive real number is greater than 1.
       (Contributed by Paul Chapman, 21-Aug-2007.) $)
    efgt1i $p |- ( 0 < A -> 1 < ( exp ` A ) ) $=
      ( cc0 clt wbr c1 caddc co ce cfv cle 1re ltaddposi biimpi 0re efge1pi syl
      ltlei readdcli reefcli ltletri syl11anc ) CADEZFFAGHZDEZUDAIJZKEZFUFDEUCU
      EAFBLMNUCCAKEUGCAOBRABPQFUDUFLFALBSABTUAUB $.
      $( [21-Aug-2007] $)
    $( The exponential function of a positive real number is greater than 1
       plus that number.  (Contributed by Mario Carneiro, 14-Mar-2014.) $)
    efgt1pi $p |- ( 0 < A -> ( 1 + A ) < ( exp ` A ) ) $=
      ( vj vy cc0 clt wbr c1 caddc co cexp cdiv cfv cle cn0 wcel cfa wceq ax-mp
      c2 wne gt0ne0i sqgt0i syl 2pos resqcli 2re divgt0i 2ne0 redivcli readdcli
      ce sylancl 1re ltaddposi sylib 0re ltlei cv copab cseq0 df-2 fveq2i addex
      wa 1nn0 nn0ex opabex2 seq0p1 seq01 eqid recni eft0vali eftval cc opreq12i
      exp1 fac1 div1i eqtri 2nn0 fac2 opreq2i 3eqtr3i effsumlei reefcli ltletri
      3eqtri syl5eqbrr syl11anc ) EAFGZHAIJZWLATKJZTLJZIJZFGZWOAULMZNGZWLWQFGWK
      EWNFGZWPWKEWMFGZETFGWSWKAEUAWTABUBABUCUDUEWMTABUFZUGUHUMWNWLWMTXAUGUIUJZH
      AUNBUKZUOUPWKEANGZWREAUQBURXDWOTICUSZOPDUSAXEKJXEQMLJZRVECDUTZVAJZMZWQNXI
      HHIJZXHMZHXHMZXJXGMZIJZWOTXJXHVBVCHOPZXKXNRVFIXGHVDCDOXFVGVHZVISXLWLXMWNI
      XLEXGMZHXGMZIJWLIXGVDXPVJXQHXRAIDACXGXGVKZABVLZVMXRAHKJZHQMZLJZAHLJAXOXRY
      CRVFDACXGHXSVNSYAAYBHLAVOPYAARXTAVQSVRVPAXTVSWHVPVTTXGMZWMTQMZLJZXMWNTOPY
      DYFRWADACXGTXSVNSTXJXGVBVCYETWMLWBWCWDVPWHDACXGTXSBWAWEWIUDWLWOWQXCWLWNXC
      XBUKABWFWGWJ $.
      $( [14-Mar-2014] $)

    $( The exponential function of a real number is greater than 0.
       (Contributed by Paul Chapman, 21-Aug-2007.) $)
    efgt0i $p |- 0 < ( exp ` A ) $=
      ( cc0 clt wbr wceq wo ce cfv cle cr wcel 0re c1 lt01 efgt1i reefcli lttri
      1re ax-mp lelttric mp2an leloei orbi1i sylancr fveq2 ef0 syl6eq syl5breqr
      mpbi eqcoms jaoi cneg wb lt0neg1 cmul co wa renegcli jctil ltlei cc recni
      3syl efcan breqtrri prodgt02 mpanl12 sylancl sylbi ) CADEZCAFZGZACDEZGZCA
      HIZDEZCAJEZVNGZVOCKLAKLZVSMBCAUAUBVRVMVNCAMBUCUDUJVMVQVNVKVQVLVKCNDEZNVPD
      EVQOABPCNVPMSABQZRUEVQACACFZCNVPDOWCVPCHINACHUFUGUHUIUKULVNCAUMZDEZVQVTVN
      WEUNBAUOTWECWDHIZJEZCVPWFUPUQZDEZVQWEWANWFDEZURCWFDEWGWEWJWAWDABUSZPOUTCN
      WFMSWDWKQZRCWFMWLVAVDCNWHDOAVBLWHNFABVCAVETVFVPKLWFKLWGWIURVQWBWLVPWFVGVH
      VIVJULT $.
      $( [21-Aug-2007] $)
  $}

  $( The exponential function of a real number is greater than 0.  (Contributed
     by Paul Chapman, 13-Sep-2007.) $)
  efgt0 $p |- ( A e. RR -> 0 < ( exp ` A ) ) $=
    ( cr wcel cc0 ce cfv clt wbr cif wceq fveq2 breq2d 0re elimel efgt0i dedth
    ) ABCZDAEFZGHDQADIZEFZGHADASJRTDGASEKLSADBMNOP $.
    $( [13-Sep-2007] $)

  $( The exponential function of a real number is a positive real. $)
  rpefcl $p |- ( A e. RR -> ( exp ` A ) e. RR+ ) $=
    ( cr wcel ce cfv cc0 clt wbr crp reefcl efgt0 elrp sylanbrc ) ABCADEZBCFNGH
    NICAJAKNLM $.
    $( [10-Nov-2013] $)

  ${
    eflt.1 $e |- A e. RR $.
    eflt.2 $e |- B e. RR $.
    $( The exponential function on the reals is strictly monotonic.
       (Contributed by Paul Chapman, 21-Aug-2007.) $)
    eflti $p |- ( A < B -> ( exp ` A ) < ( exp ` B ) ) $=
      ( clt wbr cc0 cmin co ce cfv posdifi c1 reefcli recni mulid1i resubcli cr
      cmul wcel efgt1i wa wb 1re efgt0i pm3.2i ltmul2 mp3an sylib caddc pncan3i
      fveq2i efaddi eqtr3i syl6breqr syl5eqbrr sylbi ) ABEFGBAHIZEFZAJKZBJKZEFA
      BCDLUSUTUTMSIZVAEUTUTACNZOPUSVBUTURJKZSIZVAEUSMVDEFZVBVEEFZURBADCQZUAMRTV
      DRTUTRTZGUTEFZUBVFVGUCUDURVHNVIVJVCACUEUFMVDUTUGUHUIAURUJIZJKVAVEVKBJABAC
      OZBDOUKULAURVLURVHOUMUNUOUPUQ $.
      $( [21-Aug-2007] $)

    $( The exponential function on the reals is strictly monotonic.
       (Contributed by Steve Rodriguez, 22-Aug-2007.) $)
    efltbi $p |- ( A < B <-> ( exp ` A ) < ( exp ` B ) ) $=
      ( clt wbr ce cfv eflti cle wn wceq wo fveq2 orim12i leloei reefcli lenlti
      3imtr4i 3imtr3i con4i impbii ) ABEFZAGHZBGHZEFZABCDIUCUFBAJFZUEUDJFZUCKUF
      KBAEFZBALZMUEUDEFZUEUDLZMUGUHUIUKUJULBADCIBAGNOBADCPUEUDBDQZACQZPSBADCRUE
      UDUMUNRTUAUB $.
      $( [21-Aug-2007] $)

  $}

  $( The exponential function on the reals is strictly monotonic.  (Contributed
     by Mario Carneiro, 11-Mar-2014.) $)
  eflt $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A < B <-> ( exp ` A ) < ( exp ` B ) ) ) $=
    ( cr wcel clt wbr ce cfv wb cc0 cif breq1 fveq2 breq1d bibi12d breq2 breq2d
    wceq 0re elimel efltbi dedth2h ) ACDZBCDZABEFZAGHZBGHZEFZIUCAJKZBEFZUIGHZUG
    EFZIUIUDBJKZEFZUKUMGHZEFZIABJJAUIRZUEUJUHULAUIBELUQUFUKUGEAUIGMNOBUMRZUJUNU
    LUPBUMUIEPURUGUOUKEBUMGMQOUIUMAJCSTBJCSTUAUB $.
    $( [11-Mar-2014] $)

  $( The exponential function on the reals is strictly monotonic.  (Contributed
     by Mario Carneiro, 11-Mar-2014.) $)
  efle $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A <_ B <-> ( exp ` A ) <_ ( exp ` B ) ) ) $=
    ( cr wcel wa clt wbr wn ce cfv cle eflt ancoms notbid reefcl syl2an 3bitr4d
    wb lenlt ) ACDZBCDZEZBAFGZHBIJZAIJZFGZHZABKGUEUDKGZUBUCUFUATUCUFRBALMNABSTU
    ECDUDCDUHUGRUAAOBOUEUDSPQ $.
    $( [11-Mar-2014] $)

  $( The exponential function on real numbers is one-to-one. $)
  reef11 $p |- ( ( A e. RR /\ B e. RR )
        -> ( ( exp ` A ) = ( exp ` B ) <-> A = B ) ) $=
    ( cr wcel wa cle wbr ce cfv wceq efle ancoms anbi12d letri3 reefcl 3bitr4rd
    wb syl2an ) ACDZBCDZEZABFGZBAFGZEAHIZBHIZFGZUEUDFGZEZABJUDUEJZUAUBUFUCUGABK
    TSUCUGQBAKLMABNSUDCDUECDUIUHQTAOBOUDUENRP $.
    $( [11-Mar-2014] $) $( [21-Aug-2008] $)

  ${
    $d x y $.
    $( The exponential function maps real arguments one-to-one to positive
       reals.  (Contributed by Steve Rodriguez, 25-Aug-2007.) $)
    reeff1 $p |- ( exp |` RR ) : RR -1-1-> RR+ $=
      ( vx vy cr crp ce cres wf1 wf cv cfv wceq wi wral dff13 wfn wcel ffnfv cc
      fvres mpbir2an wss eff ffn ax-mp ax-resscn fnssres rpefcl eqeltrd rgen wa
      mp2an eqeqan12d reef11 biimpd sylbid rgen2a ) CDECFZGCDUQHZAIZUQJZBIZUQJZ
      KZUSVAKZLZBCMACMABCDUQNURUQCOZUTDPZACMACDUQQEROZCRUAVFRREHVHUBRREUCUDUERC
      EUFUKVGACUSCPZUTUSEJZDUSCESZUSUGUHUITVEABCVIVACPZUJZVCVJVAEJZKZVDVIVLUTVJ
      VBVNVKVACESULVMVOVDUSVAUMUNUOUPT $.
      $( [10-Nov-2013] $) $( [25-Aug-2007] $)
  $}

  ${
    $d A j y $.
    efm1lim.1 $e |- F = { <. j , y >. |
                          ( j e. NN0 /\ y = ( ( A ^ j ) / ( ! ` j ) ) ) } $.
    efm1lim.2 $e |- A e. CC $.
    $( Series convergence to the exponential function minus 1.  (Contributed by
       Paul Chapman, 11-Sep-2007.) $)
    efm1limi $p |- ( <. 1 , + >. seqz F ) ~~> ( ( exp ` A ) - 1 ) $=
      ( cc0 c1 caddc co cop cseqz ce cfv cmin cli wcel cn0 cc ax-mp cuz eleqtri
      wbr 0nn0 nn0uz cv cexp cfa cdiv nn0ex fopabex2 cseq0 addex seq0seqz efcvg
      fvex eqbrtrri wf eftcl mpan fopab feq2i clim2serzi ax-1cn addid2i opreq1i
      mpbi opeq1i cz wceq 0z seqz1 eft0vali eqtri opreq2i 3brtr3i ) GHIJZIKZDLJ
      ZBMNZGGIKDLJZNZOJZHIKZDLJVTHOJPGGUANZQVSWCPUCGRWEUDUEUBVTDGGCARBCUFZUGJWF
      UHNUIJZDUJEUKZBMUPIDULJZWAVTPIDUMWHUNBSQZWIVTPUCFABCDEUOTUQRSDURWESDURCAR
      SWGDEWJWFRQWGSQFBWFUSUTVARWESDUEVBVGVCTVRWDDLVQHIHVDVEVHVFWBHVTOWBGDNZHGV
      IQWBWKVJVKIDGUMWHVLTABCDEFVMVNVOVP $.
      $( [11-Sep-2007] $)
  $}

  ${
    $d A j k y $.
    absefm1le.1 $e |- A e. CC $.
    $( The absolute value of the exponential function minus 1 is less than or
       equal to the exponential function minus 1 of the absolute value.
       (Contributed by Paul Chapman, 11-Sep-2007.) $)
    absefm1lei $p |- ( abs ` ( ( exp ` A ) - 1 ) ) <_
                    ( ( exp ` ( abs ` A ) ) - 1 ) $=
      ( vk vj vy cfv c1 cmin co cabs cv cn0 wcel cexp cdiv wceq cc mpan syl cc0
      ce cfa wa copab oprex nn0ex opabex2 1z cn elnnuz nnnn0 sylbir eqid eftval
      cuz eftcl eqeltrd expcl cr faccl nnre recn facne0 absdiv syl111anc absexp
      wne 3syl cle wbr clt nngt0 wi 0re ltle sylc absid syl11anc opreq12d eqtrd
      fveq2d 3eqtr4rd jca efm1limi abscli recni iserzabsi ) AUAFZGHIAJFZUAFZGHI
      CDKZLMZEKZAWKNIWKUBFZOIZPUCDEUDZWLWMWIWKNIWNOIZPUCDEUDZGWHGHUEWJGHUEDELWO
      UFUGDELWQUFUGUHCKZGUOFMZWSWPFZQMZWSWRFZXAJFZPZWTWSLMZXBWTWSUIMXFWSUJWSUKU
      LZXFXAAWSNIZWSUBFZOIZQEADWPWSWPUMZUNZAQMZXFXJQMBAWSUPRUQSWTXFXEXGXFXJJFZW
      IWSNIZXIOIZXDXCXFXNXHJFZXIJFZOIZXPXFXHQMZXIQMZXITVGXNXSPXMXFXTBAWSURRXFXI
      UIMZXIUSMZYAWSUTZXIVAZXIVBVHWSVCXHXIVDVEXFXQXOXRXIOXMXFXQXOPBAWSVFRXFYCTX
      IVIVJZXRXIPXFYBYCYDYESZXFYCTXIVKVJZYFYGXFYBYHYDXIVLSTUSMYCYHYFVMVNTXIVORV
      PXIVQVRVSVTXFXAXJJXLWAEWIDWRWSWRUMZUNWBSWCEADWPXKBWDEWIDWRYIWIABWEWFWDWG
      $.
      $( [11-Sep-2007] $)
  $}

  ${
    eflegeolem1.1 $e |- A e. RR $.
    eflegeolem1.2 $e |- 0 <_ A $.
    $( Lemma for ~ eflegeoi . $)
    eflegeolem1 $p |- ( K e. NN0 -> ( ( A ^ K ) / ( ! ` K ) ) <_ ( A ^ K ) ) $=
      ( cn0 wcel cexp co cfa cfv cdiv cle wbr cmul cr cc0 c1 reexpcl mpan syl
      cn faccl nnre expge0 mp3an13 nnge1 lemulge11 syl22anc clt wb ledivmul2OLD
      nngt0 syl31anc mpbird ) BEFZABGHZBIJZKHUPLMZUPUPUQNHLMZUOUPOFZUQOFZPUPLMZ
      QUQLMZUSAOFZUOUTCABRSZUOUQUAFZVABUBZUQUCTZVDUOPALMVBCDABUDUEUOVFVCVGUQUFT
      UPUQUGUHUOUTVAUTPUQUIMZURUSUJVEVHVEUOVFVIVGUQULTUPUQUPUKUMUN $.
      $( [11-Sep-2007] $)
  $}

  ${
    $d A j y $.  $d F k n $.  $d G k n $.  $d j k y $.
    eflegeolem2.1 $e |- A e. RR $.
    eflegeolem2.2 $e |- ( 0 <_ A /\ A < 1 ) $.
    eflegeolem2.3 $e |- F = { <. j , y >. | ( j e. NN0 /\
                           y = ( ( A ^ j ) / ( ! ` j ) ) ) } $.
    eflegeolem2.4 $e |- G = { <. j , y >. | ( j e. NN0 /\
                           y = ( A ^ j ) ) } $.
    $( Lemma for ~ eflegeoi . $)
    eflegeolem2 $p |- ( exp ` A ) <_ ( 1 / ( 1 - A ) ) $=
      ( vk cc0 caddc co cfv cli wbr c1 wcel cr cle vn cop cseqz ce cmin cdiv cz
      cv w3a cuz wral cseq0 addex cn0 cexp cfa nn0ex fopabex2 seq0seqz cc recni
      efcvg ax-mp eqbrtrri cabs clt cneg 1re abslti lt01 wb lt0neg2 mpbi simpli
      renegcli 0re ltletri mp2an simpri mpbir2an geolimi 0z cfz elfznn0 reeftcl
      eftval mpan eqeltrd syl serzrecl mpan2 opreq2 fvopab4 reexpcl eflegeolem1
      rgen oprex 3brtr4d 3jca serzcmp fvex climcmp mp4an ) KLUBZDUCMZBUDNZOPXDE
      UCMZQQBUEMZUFMZOPKUGRUAUHZXENZSRZXJXGNZSRZXKXMTPZUIZUAKUJNZUKXFXITPLDULMZ
      XEXFOLDUMCAUNBCUHZUOMZXSUPNUFMDUQHURZUSBUTRXRXFOPBFVAZABCDHVBVCVDLEULMXGX
      IOLEUMCAUNXTEUQIURZUSABCEIYBBVENQVFPQVGZBVFPZBQVFPZBQFVHVIYDKVFPZKBTPZYEK
      QVFPZYGVJQSRYIYGVKVHQVLVCVMYHYFGVNZYDKBQVHVOVPFVQVRYHYFGVSVTWAVDWBXPUAXQX
      JXQRZXLXNXOYKJUHZDNZSRZJKXJWCMZUKXLYNJYOYLYORZYLUNRZYNYLXJWDZYQYMBYLUOMZY
      LUPNUFMZSABCDYLHWFZBSRZYQYTSRFBYLWEWGWHWIZWPJDKXJYAWJWKYKYLENZSRZJYOUKXNU
      UEJYOYPYQUUEYRYQUUDYSSCAYLXTYSUNEXSYLBUOWLIBYLUOWQWMZUUBYQYSSRFBYLWNWGWHW
      IZWPJEKXJYCWJWKYKYNUUEYMUUDTPZUIZJYOUKXOUUIJYOYPYNUUEUUHUUCUUGYPYQUUHYRYQ
      YTYSYMUUDTBYLFYJWOUUAUUFWRWIWSWPJDEKXJYAYCWTWKWSWPXFXIUAXEXGKXDDUCWQXDEUC
      WQBUDXAQXHUFWQXBXC $.
      $( [11-Sep-2007] $)
  $}

  ${
    $d A j y $.
    eflegeo.1 $e |- A e. RR $.
    eflegeo.2 $e |- ( 0 <_ A /\ A < 1 ) $.
    $( The exponential function on the reals between 0 and 1 lies below the
       comparable geometric series sum.  (Contributed by Paul Chapman,
       11-Sep-2007.) $)
    eflegeoi $p |- ( exp ` A ) <_ ( 1 / ( 1 - A ) ) $=
      ( vy vj cv cn0 wcel cexp co cfa cfv cdiv wceq wa copab eqid eflegeolem2 )
      DAEEFZGHZDFZASIJZSKLMJNOEDPZTUAUBNOEDPZBCUCQUDQR $.
      $( [11-Sep-2007] $)
  $}

  $( The exponential function on the reals between 0 and 1 lies below the
     comparable geometric series sum.  (Contributed by Paul Chapman,
     11-Sep-2007.) $)
  eflegeo $p |- ( ( A e. RR /\ 0 <_ A /\ A < 1 ) ->
                   ( exp ` A ) <_ ( 1 / ( 1 - A ) ) ) $=
    ( cr wcel cc0 cle wbr c1 clt w3a ce cfv cmin co cdiv wceq eleq1 breq2 breq1
    3anbi123d 0re cif fveq2 opreq2 opreq2d breq12d leidi 3pm3.2i elimhyp simp1i
    lt01 simp2i simp3i pm3.2i eflegeoi dedth ) ABCZDAEFZAGHFZIZAJKZGGALMZNMZEFU
    SADUAZJKZGGVCLMZNMZEFADAVCOZUTVDVBVFEAVCJUBVGVAVEGNAVCGLUCUDUEVCVCBCZDVCEFZ
    VCGHFZUSVHVIVJIDBCZDDEFZDGHFZIADVGUPVHUQVIURVJAVCBPAVCDEQAVCGHRSDVCOVKVHVLV
    IVMVJDVCBPDVCDEQDVCGHRSVKVLVMTDTUFUJUGUHZUIVIVJVHVIVJVNUKVHVIVJVNULUMUNUO
    $.
    $( [11-Sep-2007] $)

  ${
    efm1legeo.1 $e |- A e. RR $.
    efm1legeo.2 $e |- ( 0 <_ A /\ A < 1 ) $.
    $( One less than the exponential function on the reals between 0 and 1 lies
       below the comparable geometric series sum.  (Contributed by Paul
       Chapman, 15-Sep-2007.) $)
    efm1legeoi $p |- ( ( exp ` A ) - 1 ) <_ ( A / ( 1 - A ) ) $=
      ( c1 cmin co cdiv cle wbr cr wcel wb 1re cc0 clt ax-1cn mp3an mpbi wceq
      cc cfv eflegeoi reefcli resubcli subidi simpri eqbrtrri gt0ne0ii rereccli
      ce ltsub2 lesub1 wne wa recni pm3.2i divsubdir nncan mp2an opreq1i dividi
      opreq2i 3eqtr3i breqtrri ) AUJUAZDEFZDDAEFZGFZDEFZAVGGFZHVEVHHIZVFVIHIZAB
      CUBVEJKVHJKDJKZVKVLLABUCVGDAMBUDZVGVNDDEFZNVGODPUEADOIZVOVGOIZNAHIVPCUFAJ
      KVMVMVPVQLBMMADDUKQRUGUHZUIMVEVHDULQRDVGEFZVGGFZVHVGVGGFZEFZVJVIDTKZVGTKZ
      WDVGNUMZUNVTWBSPVGVNUOZWDWEWFVRUPDVGVGUQQVSAVGGWCATKVSASPABUODAURUSUTWADV
      HEVGWFVRVAVBVCVD $.
      $( [15-Sep-2007] $)
  $}

  $( One less than the exponential function on the reals between 0 and 1 lies
     below the comparable geometric series sum.  (Contributed by Paul Chapman,
     15-Sep-2007.) $)
  efm1legeo $p |- ( ( A e. RR /\ 0 <_ A /\ A < 1 ) ->
                     ( ( exp ` A ) - 1 ) <_ ( A / ( 1 - A ) ) ) $=
    ( cr wcel cc0 cle wbr c1 clt w3a ce cfv cmin co cdiv wceq eleq1 breq2 breq1
    3anbi123d 0reALT cif fveq2 opreq1d id opreq2 opreq12d breq12d leidi 3pm3.2i
    lt01 elimhyp simp1i simp2i simp3i pm3.2i efm1legeoi dedth ) ABCZDAEFZAGHFZI
    ZAJKZGLMZAGALMZNMZEFVAADUAZJKZGLMZVFGVFLMZNMZEFADAVFOZVCVHVEVJEVKVBVGGLAVFJ
    UBUCVKAVFVDVINVKUDAVFGLUEUFUGVFVFBCZDVFEFZVFGHFZVAVLVMVNIDBCZDDEFZDGHFZIADV
    KURVLUSVMUTVNAVFBPAVFDEQAVFGHRSDVFOVOVLVPVMVQVNDVFBPDVFDEQDVFGHRSVOVPVQTDTU
    HUJUIUKZULVMVNVLVMVNVRUMVLVMVNVRUNUOUPUQ $.
    $( [15-Sep-2007] $)

  ${
    efcnlem1.1 $e |- A e. RR $.
    efcnlem1.2 $e |- X e. RR $.
    efcnlem1.3 $e |- Y e. RR $.
    efcnlem1.4 $e |- 0 < X $.
    efcnlem1.5 $e |- 0 < Y $.
    $( Lemma for ~ efcn . $)
    efcnlem1 $p |- ( A < ( Y / ( X + Y ) ) -> ( A < 1 /\ ( 1 - A ) =/= 0 /\
                       ( X x. ( A / ( 1 - A ) ) ) < Y ) ) $=
      ( co clt wbr c1 cc0 cmul recni cr wcel wb 1re remulcli cdiv cmin readdcli
      caddc wne mulid1i breq2i addgt0ii ltdivmulOLD mpan2 mp3an ltaddpos2 mp2an
      w3a 3bitr4ri mpbi gt0ne0ii redivcli lttri 0reALT ltaddsubi addid2i breq1i
      bitr3i sylibr resubcli gt0ne0i syl wceq divasszi mulcomi adddii ltmuldivi
      biimpri syl5eqbrr mulid2i syl6breqr ax-1cn subdiri 3pm3.2i sylancr mpbird
      ax-mp sylib eqbrtrrd 3jca ) ACBCUDIZUAIZJKZALJKZLAUBIZMUEZBAWKUAINIZCJKWI
      WHLJKZWJMBJKZWNGCWGLNIZJKZCWGJKZWNWOWPWGCJWGWGBCEFUCZOUFUGCPQZWGPQZLPQZWN
      WQRZFWSSWTXAXBUNMWGJKZXCBCEFGHUHZCWGLUIUJUKBPQWTWOWRREFBCULUMUOUPAWHLDCWG
      FWSWGWSXEUQURSUSUJZWIMWKJKZWLWIWJXGXFXGMAUDIZLJKWJMALUTDSVAXHALJAADOZVBVC
      VDVEZWKLASDVFZVGVHZWIBANIZWKUAIZWMCJWIWLXNWMVIXLBAWKBEOZXIWKXKOVJVHWIXNCJ
      KZXMWKCNIZJKZWIXMABNIZXQJABXIXOVKWIXSLCNIZACNIZUBIZXQJWIXSYAUDIZXTJKXSYBJ
      KWIYCCXTJWIYCAWGNIZCJABCXIXOCFOZVLYDCJKZWIXDYFWIRXEACWGDFWSVMWCVNVOCYEVPV
      QXSYAXTABDETACDFTLCSFTVAWDLACVRXIYEVSVQVOWIXMPQZWKPQZWTUNXGXPXRRYGYHWTBAE
      DTXKFVTXJXMWKCUIWAWBWEWF $.
      $( [15-Sep-2007] $)
  $}

  ${
    efcnlem2.1 $e |- X e. CC $.
    efcnlem2.2 $e |- W e. CC $.
    efcnlem2.3 $e |- Y e. RR $.
    efcnlem2.4 $e |- 0 < Y $.
    efcnlem2.5 $e |- S = ( abs ` ( exp ` X ) ) $.
    efcnlem2.6 $e |- H = ( abs ` ( X - W ) ) $.
    efcnlem2.7 $e |- G = ( H / ( 1 - H ) ) $.
    efcnlem2.8 $e |- D = ( Y / ( S + Y ) ) $.
    $( Lemma for ~ efcn . $)
    efcnlem2 $p |- ( H < D -> ( abs ` ( ( exp ` X ) - ( exp ` W ) ) ) < Y ) $=
      ( clt wbr cfv co wcel ce cmin cabs cc efcl ax-mp abssubi c1 subcli ax-1cn
      absmuli subdii caddc efaddi pncan3i fveq2i eqtr3i mulid1i opreq12i eqtr2i
      cmul opreq1i 3eqtr4i cr abscli eqeltri remulcli a1i cc0 wne cdiv w3a cneg
      negsubdi2i absnegi 3eqtr2i efne0 absgt0i mpbi breqtrri efcnlem1 sylbi 1re
      breq2i simp2d resubcli redivclzi syl5eqel syl remulcl sylancr cle reefcli
      absefm1lei simp1d absge0i efm1legeo mp3an12 syl6breqr letrd pm3.2i lemul2
      wb wa mp3an3 mpbid opreq2i simp3d syl5eqbr lelttrd ) DAPQZFUARZEUARZUBSUC
      RXMXLUBSZUCRZGPXLXMFUDTZXLUDTHFUEUFZEUDTXMUDTIEUEUFUGXKXOBEFUBSZUARZUHUBS
      ZUCRZVASZGPXLXTVASZUCRXLUCRZYAVASXOYBXLXTXQXSUHXRUDTXSUDTEFIHUIZXRUEUFZUJ
      UIZUKXNYCUCYCXLXSVASZXLUHVASZUBSXNXLXSUHXQYFUJULYHXMYIXLUBFXRUMSZUARYHXMF
      XRHYEUNYJEUAFEHIUOUPUQXLXQURUSUTUPBYDYAVALVBVCXKYBBCVASZGYBVDTXKBYABYDVDL
      XLXQVEVFZXTYGVEZVGVHXKBVDTZCVDTZYKVDTYLXKUHDUBSZVIVJZYOXKDUHPQZYQBDYPVKSZ
      VASZGPQZXKDGBGUMSVKSZPQYRYQUUAVLAUUBDPOWDDBGDXRUCRZVDDFEUBSZUCRXRVMZUCRUU
      CMUUEUUDUCEFIHVNUPXRYEVOVPZXRYEVEVFZYLJVIYDBPXLVIVJZVIYDPQXPUUHHFVQUFXLXQ
      VRVSLVTZKWAWBZWEYQCYSVDNDYPUUGUHDWCUUGWFWGWHWIZBCWJWKGVDTXKJVHXKYACWLQZYB
      YKWLQZXKYADUARZUHUBSZCYAVDTZXKYMVHUUOVDTXKUUNUHDUUGWMWCWFVHUUKYAUUOWLQXKY
      AUUCUARZUHUBSUUOWLXRYEWNUUNUUQUHUBDUUCUAUUFUPVBVTVHXKUUOYSCWLXKYRUUOYSWLQ
      ZXKYRYQUUAUUJWODVDTVIDWLQYRUURUUGVIUUCDWLXRYEWPUUFVTDWQWRWINWSWTXKUUPYOUU
      LUUMXCZYMUUKUUPYOYNVIBPQZXDUUSYNUUTYLUUIXAYACBXBXEWKXFXKYKYTGPCYSBVANXGXK
      YRYQUUAUUJXHXIXJXIXI $.
      $( [15-Sep-2007] $)
  $}

  ${
    efcnlem3.1 $e |- X e. CC $.
    efcnlem3.2 $e |- W e. CC $.
    efcnlem3.3 $e |- Y e. RR $.
    efcnlem3.4 $e |- 0 < Y $.
    $( Lemma for ~ efcn . $)
    efcnlem3 $p |- ( ( abs ` ( X - W ) ) < ( Y / ( ( abs ` ( exp ` X ) ) + Y )
            ) -> ( abs ` ( ( exp ` X ) - ( exp ` W ) ) ) < Y ) $=
      ( ce cfv cabs caddc co cdiv cmin c1 eqid efcnlem2 ) CBHIJIZCKLMLZRBANLJIZ
      OTNLMLZTABCDEFGRPTPUAPSPQ $.
      $( [15-Sep-2007] $)
  $}

  $( Lemma for ~ efcn . $)
  efcnlem4 $p |- ( ( X e. CC /\ W e. CC /\ ( Y e. RR /\ 0 < Y ) ) ->
             ( ( abs ` ( X - W ) ) < ( Y / ( ( abs ` ( exp ` X ) ) + Y ) ) ->
                     ( abs ` ( ( exp ` X ) - ( exp ` W ) ) ) < Y ) ) $=
    ( cc wcel cr cc0 clt wbr cmin co cabs cfv ce caddc cdiv wi c1 wceq fveq2 wa
    opreq1 fveq2d 3syl opreq2d breq12d breq1d imbi12d opreq2 id opreq12d breq2d
    cif breq2 0cn elimel anbi12d 1re lt01 pm3.2i elimhyp simpli simpri efcnlem3
    eleq1 dedth3h ) BDEZADEZCFEZGCHIZUAZBAJKZLMZCBNMZLMZCOKZPKZHIZVNANMZJKZLMZC
    HIZQVGBGUMZAJKZLMZCWCNMZLMZCOKZPKZHIZWFVSJKZLMZCHIZQWCVHAGUMZJKZLMZWIHIZWFW
    NNMZJKZLMZCHIZQWPVKCRUMZWGXBOKZPKZHIZWTXBHIZQBACGGRBWCSZVRWJWBWMXGVMWEVQWIH
    XGVLWDLBWCAJUBUCXGVPWHCPXGVNWFSZVOWGSVPWHSBWCNTZVNWFLTVOWGCOUBUDUEUFXGWAWLC
    HXGXHVTWKSWAWLSXIVNWFVSJUBVTWKLTUDUGUHAWNSZWJWQWMXAXJWEWPWIHXJWDWOLAWNWCJUI
    UCUGXJWLWTCHXJVSWRSWKWSSWLWTSAWNNTVSWRWFJUIWKWSLTUDUGUHCXBSZWQXEXAXFXKWIXDW
    PHXKCXBWHXCPXKUJCXBWGOUIUKULCXBWTHUNUHWNWCXBBGDUOUPAGDUOUPXBFEZGXBHIZVKXLXM
    UARFEZGRHIZUACRXKVIXLVJXMCXBFVECXBGHUNUQRXBSXNXLXOXMRXBFVERXBGHUNUQXNXOURUS
    UTVAZVBXLXMXPVCVDVF $.
    $( [15-Sep-2007] $)

  ${
    $d w x y z $.
    $( The exponential function is continuous.  (Contributed by Paul Chapman,
       15-Sep-2007.) $)
    efcn $p |- exp e. ( CC -cn-> CC ) $=
      ( vx vw vz vy ce cc co wcel cv cabs cfv clt wbr wi wral crp wa wb cr cc0
      ccncf cmin wrex wss ssid elcncf mp2an eff caddc cdiv wne simpr elrp sylib
      wf simpld efcl abscl syl adantr syl11anc efne0 absgt0 mpbid simprd addgt0
      readdcl syl22anc gt0ne0 redivcl syl111anc divgt0 sylanbrc 3ancoma 3anbi3i
      w3a 3anass 3bitr3i efcnlem4 sylbir expcom ralrimiv wceq breq2 imbi1 imbi2
      3syl ralbidv2 rcla4ev rgen2 mpbir2an ) EFFUAGHZFFEUOZAIZBIZUBGJKZCIZLMZWN
      EKZWOEKUBGJKDIZLMZNZBFOZCPUCZDPOAFOZFFUDZXFWLWMXEQRFUEZXGADCBFFEUFUGUHXDA
      DFPWNFHZWTPHZQZWTWSJKZWTUIGZUJGZPHZWPXMLMZXANZBFOZXDXJXMSHZTXMLMZXNXJWTSH
      ZXLSHZXLTUKZXRXJXTTWTLMZXJXIXTYCQZXHXIULWTUMZUNZUPZXJXKSHZXTYAXHYHXIXHWSF
      HZYHWNUQZWSURUSUTZYGXKWTVGVAZXJYATXLLMZYBYLXJYHXTTXKLMZYCYMYKYGXHYNXIXHWS
      TUKZYNWNVBXHYIYOYNRYJWSVCUSVDUTXJXTYCYFVEZXKWTVFVHZXLVIVAWTXLVJVKXJXTYCYA
      YMXSYGYPYLYQWTXLVLVHXMUMVMXJXPBFWOFHZXJXPYRXJQZXHYRYDVPZXPXHYRXIVPYRXHXIV
      PYTYSXHYRXIVNXIYDXHYRYEVOYRXHXIVQVRWOWNWTVSVTWAWBXCXQCXMPWQXMWCZXBXPBFFUU
      AWRXORXBXPRYRXBNYRXPNRWQXMWPLWDWRXOXAWEXBXPYRWFWGWHWIVAWJWK $.
      $( [15-Sep-2007] $)
  $}

  ${
    $d C y $.  $d U x y $.
    reeff1olem1.1 $e |- U e. RR $.
    reeff1olem1.2 $e |- 1 < U $.
    reeff1olem1.3 $e |- S = { x e. ( 0 [,] U ) | ( exp ` x ) = U } $.
    reeff1olem1.4 $e |- C = sup ( S , RR , < ) $.
    $( Lemma for ~ reeff1o . $)
    reeff1olem1 $p |- E. y e. RR ( exp ` y ) = U $=
      ( cr wcel ce cfv cc0 co 0re c1 clt wbr cle wceq wa cv wrex cioo 1re lttri
      cc lt01 mp2an cicc w3a wb elicc2 simp1bi ssriv ax-resscn ssid efcn reefcl
      sstri syl eqbrtri caddc ltp1i ax-1cn recni addcomi efge1pi ax-mp eqbrtrri
      ef0 ltleii readdcli reefcli ltletri pm3.2i isupivthi elioore anim1i fveq2
      eqeq1d rcla4ev ) CJKZCLMZEUAZUBZBUCZLMZEUAZBJUDCNEUEOKZWFUBWGANECUHDELPFF
      NQRSQERSNERSUIGNQEPUFFUGUJZNEUKOZJUHAWMJAUCZWMKZWNJKZNWNTSZWNETSZNJKEJKWO
      WPWQWRULUMPFNEWNUNUJUOZUPUQVAUHURUSWOWPWNLMJKWSWNUTVBHNLMZERSEELMZRSZWTQE
      RVLGVCEEQVDOZRSXCXATSXBEFVEQEVDOZXCXATQEVFEFVGVHNETSXDXATSNEPFWLVMEFVIVJV
      KEXCXAFEQFUFVNEFVOVPUJVQIVRWKWDWFCNEVSVTVJWJWFBCJWHCUAWIWEEWHCLWAWBWCVJ
      $.
      $( [18-Oct-2007] $)
  $}

  ${
    $d U x y $.
    $( Lemma for ~ reeff1o . $)
    reeff1olem2 $p |- ( ( U e. RR /\ 1 < U ) -> E. x e. RR ( exp ` x ) = U ) $=
      ( vy cr wcel c1 clt wbr wa cv ce cfv wceq wrex c2 cif eleq1 breq2 anbi12d
      eqid eqeq2 rexbidv cc0 cicc co crab csup 2re pm3.2i elimhyp simpli simpri
      1lt2 reeff1olem1 dedth ) BDEZFBGHZIZAJKLZBMZADNUSURBOPZMZADNBOBVAMZUTVBAD
      BVAUSUAUBCACJKLVAMCUCVAUDUEUFZDGUGZVDVAVADEZFVAGHZURVFVGIODEZFOGHZIBOVCUP
      VFUQVGBVADQBVAFGRSOVAMVHVFVIVGOVADQOVAFGRSVHVIUHUMUIUJZUKVFVGVJULVDTVETUN
      UO $.
      $( [18-Oct-2007] $)
  $}

  ${
    $d x y z $.
    $( The real exponential function is one-to-one onto.  (Contributed by Paul
       Chapman, 18-Oct-2007.) $)
    reeff1o $p |- ( exp |` RR ) : RR -1-1-onto-> RR+ $=
      ( vx vy cr crp ce reeff1 wceq cv wcel cfv c1 clt wbr syl cc0 wa wb eqeq1d
      wrex cc vz cres wf1o wf1 wfo df-f1o wfn crn df-fo wf f1f ffn mp2b wss frn
      w3o rpre 1re lttri4 mpan2 cdiv co elrp reclt1 sylbi cneg rereccl syl11anc
      wne rpne0 reeff1olem2 sylan wi eqcom rpcn jca recn efcl efne0 rec11r cmul
      syl2an eqcomd ax-1cn divmul2 mp3an1 syl12anc mpbird adantl syl5bbr biimpd
      efcan negcl bitrd reximdva adantr renegcl infm3lem fveq2 rexxfr sylibr ex
      mpd sylbid imp ef0 eqeq2i 0re rcla4ev eqcoms sylbir 3jaodan mpdan rexbiia
      mpan fvres fvelrnb ax-mp ssriv eqssi mpbir2an ) CDECUBZUCCDYBUDZCDYBUEZCD
      YBUFFYDYBCUGZYBUHZDGCDYBUIYCCDYBUJZYEFCDYBUKZCDYBULUMZYFDYCYGYFDUNFYHCDYB
      UOUMUADYFUAHZDIZAHZYBJZYJGZACSZYJYFIZYKYLEJZYJGZACSZYOYKYJKLMZYJKGZKYJLMZ
      UPZYSYKYJCIZUUCYJUQZUUDKCIUUCURYJKUSUTNYKYTYSUUAUUBYKYTYSYKYTKKYJVAVBZLMZ
      YSYKUUDOYJLMPYTUUGQYJVCYJVDVEYKUUGYSYKUUGPZBHZVFZEJZYJGZBCSZYSUUHUUIEJZUU
      FGZBCSZUUMYKUUFCIZUUGUUPYKUUDYJOVIZUUQUUEYJVJZYJVGVHBUUFVKVLYKUUPUUMVMUUG
      YKUUOUULBCYKUUICIZPZUUOUULUUOUUFUUNGZUVAUULUUFUUNVNUVAUVBKUUNVAVBZYJGZUUL
      YKYJTIZUURPUUNTIZUUNOVIZPZUVBUVDQUUTYKUVEUURYJVOUUSVPUUTUVFUVGUUTUUITIZUV
      FUUIVQZUUIVRZNUUTUVIUVGUVJUUIVSZNVPYJUUNVTWBUUTUVDUULQYKUUTUVCUUKYJUUTUVI
      UVCUUKGZUVJUVIUVMKUUNUUKWAVBZGZUVIUVNKUUIWLWCUVIUUKTIZUVFUVGUVMUVOQZUVIUU
      JTIUVPUUIWMUUJVRNUVKUVLKTIUVPUVHUVQWDKUUKUUNWEWFWGWHNRWIWNWJWKWOWPXCYRUUL
      ABUUJCUUIWQABWRYLUUJGYQUUKYJYLUUJEWSRWTXAXBXDXEUUAYSYKUUAYJOEJZGYSUVRKYJX
      FXGYSUVRYJOCIUVRYJGZYSXHYRUVSAOCYLOGYQUVRYJYLOEWSRXIXOXJXKWIYKUUDUUBYSUUE
      AYJVKVLXLXMYNYRACYLCIYMYQYJYLCEXPRXNXAYEYPYOQYIACYJYBXQXRXAXSXTYAYA $.
      $( [10-Nov-2013] $) $( [18-Oct-2007] $)
  $}

  ${
    $d x y $.
    $( The exponential function on the reals determines an isomorphism from
       reals onto positive reals.  (Contributed by Steve Rodriguez,
       25-Nov-2007.)  (Revised by Mario Carneiro, 11-Mar-2014.) $)
    reefiso $p |- ( exp |` RR ) Isom < , < ( RR , RR+ ) $=
      ( vx vy cr crp clt ce cres wiso wf1o cv wbr cfv wb wral df-iso reeff1o wa
      wcel eflt fvres breqan12d bitr4d rgen2a mpbir2an ) CDEEFCGZHCDUEIAJZBJZEK
      ZUFUELZUGUELZEKZMZBCNACNABCDEEUEOPULABCUFCRZUGCRZQUHUFFLZUGFLZEKUKUFUGSUM
      UNUIUOUJUPEUFCFTUGCFTUAUBUCUD $.
      $( [11-Mar-2014] $) $( [25-Nov-2007] $)
  $}

  ${
    $d x A $.
    $( Value of the sine function. $)
    sinval $p |- ( A e. CC -> ( sin ` A ) =
    ( ( ( exp ` ( _i x. A ) ) - ( exp ` ( -u _i x. A ) ) ) / ( 2 x. _i ) ) ) $=
      ( vx ci cv cmul co ce cfv cneg cmin cdiv csin wceq opreq2 fveq2d opreq12d
      c2 cc opreq1d df-sin oprex fvmpt ) BACBDZEFZGHZCIZUCEFZGHZJFZQCEFZKFCAEFZ
      GHZUFAEFZGHZJFZUJKFRLUCAMZUIUOUJKUPUEULUHUNJUPUDUKGUCACENOUPUGUMGUCAUFENO
      PSBTUOUJKUAUB $.
      $( [10-Nov-2013] $) $( [14-Mar-2005] $)

    $( Value of the cosine function. $)
    cosval $p |- ( A e. CC -> ( cos ` A ) =
       ( ( ( exp ` ( _i x. A ) ) + ( exp ` ( -u _i x. A ) ) ) / 2 ) ) $=
      ( vx ci cv cmul co ce cfv cneg caddc c2 cdiv cc ccos wceq opreq2 opreq12d
      fveq2d opreq1d df-cos oprex fvmpt ) BACBDZEFZGHZCIZUCEFZGHZJFZKLFCAEFZGHZ
      UFAEFZGHZJFZKLFMNUCAOZUIUNKLUOUEUKUHUMJUOUDUJGUCACEPRUOUGULGUCAUFEPRQSBTU
      NKLUAUB $.
      $( [10-Nov-2013] $) $( [14-Mar-2005] $)
  $}

  $( Closure of the sine function. $)
  sincl $p |- ( A e. CC -> ( sin ` A ) e. CC ) $=
    ( cc wcel csin cfv ci cmul co ce cneg cmin c2 cdiv sinval ax-icn mulcl mpan
    efcl syl 2cn wa negcli jca subcl cc0 mulcli 2ne0 ine0 mulne0i divcl mp3an23
    wne 3syl eqeltrd ) ABCZADEFAGHZIEZFJZAGHZIEZKHZLFGHZMHZBANUOUQBCZUTBCZUAVAB
    CZVCBCZUOVDVEUOUPBCZVDFBCUOVHOFAPQUPRSUOUSBCZVEURBCUOVIFOUBURAPQUSRSUCUQUTU
    DVFVBBCVBUEULVGLFTOUFLFTOUGUHUIVAVBUJUKUMUN $.
    $( [28-Apr-2005] $)

  $( Closure of the cosine function with a complex argument. $)
  coscl $p |- ( A e. CC -> ( cos ` A ) e. CC ) $=
    ( cc wcel ccos ci cmul co ce cneg caddc c2 cdiv cosval wa ax-icn mulcl mpan
    cfv efcl syl negcli jca addcl halfcl 3syl eqeltrd ) ABCZADREAFGZHRZEIZAFGZH
    RZJGZKLGZBAMUGUIBCZULBCZNUMBCUNBCUGUOUPUGUHBCZUOEBCUGUQOEAPQUHSTUGUKBCZUPUJ
    BCUGUREOUAUJAPQUKSTUBUIULUCUMUDUEUF $.
    $( [28-Apr-2005] $)

  $( The sine of a real number in terms of the exponential function. $)
  resinval $p |- ( A e. RR -> ( sin ` A ) = ( Im ` ( exp ` ( _i x. A ) ) ) ) $=
    ( cr wcel ci cmul co ce cfv cneg cmin c2 cdiv ccj csin wceq sylancr opreq2d
    cc ax-icn syl cim recn cjmul cji cjre syl5eq eqtrd fveq2d mulcl efcj eqtr3d
    opreq1i opreq1d sinval efcl imcj 3eqtr4d ) ABCZDAEFZGHZDIZAEFZGHZJFZKDEFZLF
    ZUTUTMHZJFZVELFZANHZUTUAHZURVDVHVELURVCVGUTJURUSMHZGHZVCVGURVLVBGURVLDMHZAM
    HZEFZVBURDRCZARCZVLVPOSAUBZDAUCPURVPVAVOEFVBVNVAVOEUDULURVOAVAEAUEQUFUGUHUR
    USRCZVMVGOURVQVRVTSVSDAUIPZUSUJTUKQUMURVRVJVFOVSAUNTURUTRCZVKVIOURVTWBWAUSU
    OTUTUPTUQ $.
    $( [23-Feb-2006] $) $( [30-Apr-2005] $)

  $( The cosine of a real number in terms of the exponential function. $)
  recosval $p |- ( A e. RR -> ( cos ` A ) = ( Re ` ( exp ` ( _i x. A ) ) ) ) $=
    ( cr wcel ci cmul co ce cfv cneg caddc c2 cdiv ccj ccos wceq ax-icn sylancr
    cc opreq2d syl cre recn cjmul opreq1i cjre syl5eq eqtrd fveq2d mulcl eqtr3d
    cji efcj opreq1d cosval efcl recj 3eqtr4d ) ABCZDAEFZGHZDIZAEFZGHZJFZKLFZUT
    UTMHZJFZKLFZANHZUTUAHZURVDVGKLURVCVFUTJURUSMHZGHZVCVFURVKVBGURVKDMHZAMHZEFZ
    VBURDRCZARCZVKVOOPAUBZDAUCQURVOVAVNEFVBVMVAVNEUKUDURVNAVAEAUESUFUGUHURUSRCZ
    VLVFOURVPVQVSPVRDAUIQZUSULTUJSUMURVQVIVEOVRAUNTURUTRCZVJVHOURVSWAVTUSUOTUTU
    PTUQ $.
    $( [27-Feb-2006] $) $( [30-Apr-2005] $)

  ${
    $d A j k x y $.  $d F k x $.
    efit4pt.1 $e |- F = { <. j , y >. | ( j e. NN0 /\
                          y = ( ( ( _i x. A ) ^ j ) / ( ! ` j ) ) ) } $.
    $( Separate out the first four terms of the infinite series expansion of
       the exponential function of a pure imaginary number.  (Contributed by
       Paul Chapman, 19-Jan-2008.) $)
    efi4p $p |- ( A e. RR -> ( exp ` ( _i x. A ) ) =
        ( ( ( 1 - ( ( A ^ 2 ) / 2 ) ) + ( _i x. ( A - ( ( A ^ 3 ) / 6 ) ) ) ) +
          sum_ k e. ( ZZ>= ` 4 ) ( F ` k ) ) ) $=
      ( wcel ci cmul co c1 caddc c2 cexp cdiv c3 c6 cc wceq syl cr ce cfv c4 cv
      cuz csu cmin ax-icn recn mulcl sylancr ef4p ax-1cn addcl sqcl cc0 wne 2cn
      2ne0 divcl mp3an23 cn0 3nn nnnn0i expcl sylancl 6re recni gt0ne0ii addass
      6pos syl111anc a1i add4 syl22anc cneg 2nn0 mulexp mp3an13 i2 mulm1 3eqtrd
      opreq1i opreq1d divneg eqtr4d opreq2d negsub eqtrd negcli pm3.2i mulneg12
      i3 wa divass negcl adddi mp3an1 syl11anc 3eqtr2d opreq12d ) BUAGZHBIJZUBU
      CZKXDLJZXDMNJZMOJZLJXDPNJZQOJZLJZUDUFUCDUEEUCDUGZLJZKBMNJZMOJZUHJZHBBPNJZ
      QOJZUHJZIJZLJZXLLJXCXDRGZXEXMSXCHRGZBRGZYBUIBUJZHBUKULZAXDCDEFUMTXCXKYAXL
      LXCXKXFXHXJLJLJZKXHLJZXDXJLJZLJZYAXCXFRGZXHRGZXJRGZXKYGSXCKRGZYBYKUNYFKXD
      UOULXCXGRGZYLXCYBYOYFXDUPTYOMRGZMUQURZYLUSUTXGMVAVBTZXCXIRGZYMXCYBPVCGZYS
      YFPVDVEZXDPVFVGYSQRGZQUQURZYMQVHVIZQVHVLVJZXIQVAVBTZXFXHXJVKVMXCYNYBYLYMY
      GYJSYNXCUNVNYFYRUUFKXDXHXJVOVPXCYHXPYIXTLXCYHKXOVQZLJZXPXCXHUUGKLXCXHXNVQ
      ZMOJZUUGXCXGUUIMOXCXGHMNJZXNIJZKVQZXNIJZUUIXCYDXGUULSZYEYCYDMVCGUUOUIVRHB
      MVSVTTUULUUNSXCUUKUUMXNIWAWDVNXCXNRGZUUNUUISXCYDUUPYEBUPTZXNWBTWCWEXCUUPU
      UGUUJSZUUQUUPYPYQUURUSUTXNMWFVBTWGWHXCYNXORGZUUHXPSUNXCUUPUUSUUQUUPYPYQUU
      SUSUTXNMVAVBTKXOWIULWJXCYIXDHXRVQZIJZLJZHBUUTLJZIJZXTXCXJUVAXDLXCXJHVQZXQ
      IJZQOJZUVEXRIJZUVAXCXIUVFQOXCXIHPNJZXQIJZUVFXCYDXIUVJSZYEYCYDYTUVKUIUUAHB
      PVSVTTUVJUVFSXCUVIUVEXQIWNWDVNWJWEXCXQRGZUVGUVHSZXCYDYTUVLYEUUABPVFVGZUVE
      RGUVLUUBUUCWOUVMHUIWKUUBUUCUUDUUEWLUVEXQQWPVTTXCYCXRRGZUVHUVASUIXCUVLUVOU
      VNUVLUUBUUCUVOUUDUUEXQQVAVBTZHXRWMULWCWHXCYDUUTRGZUVDUVBSZYEXCUVOUVQUVPXR
      WQTYCYDUVQUVRUIHBUUTWRWSWTXCUVCXSHIXCYDUVOUVCXSSYEUVPBXRWIWTWHXAXBWCWEWJ
      $.
      $( [19-Jan-2008] $)

    $( Separate out the first four terms of the infinite series expansion of
       the sine of a real number.  (Contributed by Paul Chapman,
       19-Jan-2008.) $)
    resin4p $p |- ( A e. RR -> ( sin ` A ) = ( ( A - ( ( A ^ 3 ) / 6 ) ) +
                       ( Im ` sum_ k e. ( ZZ>= ` 4 ) ( F ` k ) ) ) ) $=
      ( cr wcel cfv ci co cim c3 c6 c4 caddc c1 cc sylancr syl11anc csin ce cuz
      cmul cexp cdiv cmin cv csu resinval efi4p fveq2d wceq 1re resqcl rehalfcl
      c2 syl resubcl recnd ax-icn cn0 3nn nnnn0i reexpcl mpan2 cc0 wne 6re 6pos
      gt0ne0ii redivcl mp3an23 mpdan mulcl addcl cn recn 4nn sylancl imadd crim
      eftlcl opreq1d 3eqtrd eqtrd ) BGHZBUAIJBUDKZUBIZLIZBBMUEKZNUFKZUGKZOUCIDU
      HEIDUIZLIZPKZBUJWGWJQBUQUEKZUQUFKZUGKZJWMUDKZPKZWNPKZLIZXALIZWOPKZWPWGWIX
      BLABCDEFUKULWGXARHZWNRHZXCXEUMWGWSRHWTRHZXFWGWSWGQGHWRGHZWSGHZUNWGWQGHXIB
      UOWQUPURQWRUSSZUTWGJRHZWMRHXHVAWGWMWGWLGHZWMGHZWGWKGHZXMWGMVBHXOMVCVDBMVE
      VFXONGHNVGVHXMVINVIVJVKWKNVLVMURBWLUSVNZUTJWMVOSWSWTVPTWGWHRHZOVQHXGWGXLB
      RHXQVABVRJBVOSVSAWHCDEOFWCVTXAWNWATWGXDWMWOPWGXJXNXDWMUMXKXPWSWMWBTWDWEWF
      $.
      $( [19-Jan-2008] $)

    $( Separate out the first four terms of the infinite series expansion of
       the cosine of a real number.  (Contributed by Paul Chapman,
       19-Jan-2008.) $)
    recos4p $p |- ( A e. RR -> ( cos ` A ) = ( ( 1 - ( ( A ^ 2 ) / 2 ) ) +
                       ( Re ` sum_ k e. ( ZZ>= ` 4 ) ( F ` k ) ) ) ) $=
      ( cr wcel cfv ci co cre c1 c4 caddc c3 c6 cc sylancr syl11anc ccos ce cuz
      cmul c2 cexp cdiv cmin csu recosval efi4p fveq2d wceq 1re resqcl rehalfcl
      cv syl resubcl recnd ax-icn cn0 3nn nnnn0i reexpcl mpan2 cc0 wne 6re 6pos
      gt0ne0ii redivcl mp3an23 mpdan mulcl addcl cn recn 4nn sylancl readd crre
      eftlcl opreq1d 3eqtrd eqtrd ) BGHZBUAIJBUDKZUBIZLIZMBUEUFKZUEUGKZUHKZNUCI
      DUQEIDUIZLIZOKZBUJWGWJWMJBBPUFKZQUGKZUHKZUDKZOKZWNOKZLIZXALIZWOOKZWPWGWIX
      BLABCDEFUKULWGXARHZWNRHZXCXEUMWGWMRHWTRHZXFWGWMWGMGHWLGHZWMGHZUNWGWKGHXIB
      UOWKUPURMWLUSSZUTWGJRHZWSRHXHVAWGWSWGWRGHZWSGHZWGWQGHZXMWGPVBHXOPVCVDBPVE
      VFXOQGHQVGVHXMVIQVIVJVKWQQVLVMURBWRUSVNZUTJWSVOSWMWTVPTWGWHRHZNVQHXGWGXLB
      RHXQVABVRJBVOSVSAWHCDENFWCVTXAWNWATWGXDWMWOOWGXJXNXDWMUMXKXPWMWSWBTWDWEWF
      $.
      $( [19-Jan-2008] $)
  $}

  $( The sine of a real number is real. $)
  resincl $p |- ( A e. RR -> ( sin ` A ) e. RR ) $=
    ( cr wcel csin cfv ci cmul co ce resinval cc ax-icn recn mulcl sylancr efcl
    cim syl imcl eqeltrd ) ABCZADEFAGHZIEZQEZBAJUAUCKCZUDBCUAUBKCZUEUAFKCAKCUFL
    AMFANOUBPRUCSRT $.
    $( [24-Feb-2006] $) $( [30-Apr-2005] $)

  $( The cosine of a real number is real. $)
  recoscl $p |- ( A e. RR -> ( cos ` A ) e. RR ) $=
    ( cr wcel ccos cfv ci cmul co ce recosval cc ax-icn recn mulcl sylancr efcl
    cre syl recl eqeltrd ) ABCZADEFAGHZIEZQEZBAJUAUCKCZUDBCUAUBKCZUEUAFKCAKCUFL
    AMFANOUBPRUCSRT $.
    $( [28-Feb-2006] $) $( [30-Apr-2005] $)

  ${
    $d x y $.
    $( Domain and codomain of the sine function.  (Contributed by Paul Chapman,
       22-Oct-2007.) $)
    sinf $p |- sin : CC --> CC $=
      ( vx ci cv cmul co ce cneg cmin c2 cdiv cc wcel wral csin wf sinval sincl
      cfv eqeltrrd rgen df-sin fmpt mpbi ) BACZDEFRBGUDDEFRHEIBDEJEZKLZAKMKKNOU
      FAKUDKLUDNRUEKUDPUDQSTAKKUENAUAUBUC $.
      $( [10-Nov-2013] $) $( [22-Oct-2007] $)

    $( Domain and codomain of the sine function.  (Contributed by Paul Chapman,
       22-Oct-2007.) $)
    cosf $p |- cos : CC --> CC $=
      ( vx ci cv cmul co ce cfv cneg caddc c2 cdiv cc wcel wral wf cosval coscl
      ccos eqeltrrd rgen df-cos fmpt mpbi ) BACZDEFGBHUDDEFGIEJKEZLMZALNLLROUFA
      LUDLMUDRGUELUDPUDQSTALLUERAUAUBUC $.
      $( [10-Nov-2013] $) $( [22-Oct-2007] $)
  $}

  $( The sine of a negative is the negative of the sine. $)
  sinneg $p |- ( A e. CC -> ( sin ` -u A ) = -u ( sin ` A ) ) $=
    ( cc wcel ci cneg cmul co ce cfv cmin c2 cdiv csin wceq ax-icn fveq2d mulcl
    mpan efcl syl mulneg12 eqcomd mul2neg opreq12d negcli syl11anc eqtr4d negcl
    negsubdi2 opreq1d sinval negeqd cc0 wne 2cn mulcli 2ne0 ine0 mulne0i divneg
    subcl mp3an23 eqtrd 3eqtr4d ) ABCZDAEZFGZHIZDEZVFFGZHIZJGZKDFGZLGZDAFGZHIZV
    IAFGZHIZJGZEZVMLGZVFMIZAMIZEZVEVLVTVMLVEVLVRVPJGZVTVEVHVRVKVPJVEVGVQHVEVQVG
    DBCZVEVQVGNODAUARUBPVEVJVOHWFVEVJVONODAUCRPUDVEVPBCZVRBCZVTWENVEVOBCZWGWFVE
    WIODAQRVOSTZVEVQBCZWHVIBCVEWKDOUEVIAQRVQSTZVPVRUIUFUGUJVEVFBCWBVNNAUHVFUKTV
    EWDVSVMLGZEZWAVEWCWMAUKULVEVSBCZWNWANZVEWGWHWOWJWLVPVRVAUFWOVMBCVMUMUNWPKDU
    OOUPKDUOOUQURUSVSVMUTVBTVCVD $.
    $( [30-Apr-2005] $)

  $( The cosines of a number and its negative are the same. $)
  cosneg $p |- ( A e. CC -> ( cos ` -u A ) = ( cos ` A ) ) $=
    ( cc wcel ci cneg cmul co ce cfv caddc c2 cdiv ccos wceq ax-icn mpan fveq2d
    mulcl efcl syl mulneg12 eqcomd mul2neg opreq12d negcli addcom eqtrd opreq1d
    syl11anc negcl cosval 3eqtr4d ) ABCZDAEZFGZHIZDEZUNFGZHIZJGZKLGZDAFGZHIZUQA
    FGZHIZJGZKLGUNMIZAMIUMUTVFKLUMUTVEVCJGZVFUMUPVEUSVCJUMUOVDHUMVDUODBCZUMVDUO
    NODAUAPUBQUMURVBHVIUMURVBNODAUCPQUDUMVEBCZVCBCZVHVFNUMVDBCZVJUQBCUMVLDOUEUQ
    ARPVDSTUMVBBCZVKVIUMVMODARPVBSTVEVCUFUIUGUHUMUNBCVGVANAUJUNUKTAUKUL $.
    $( [20-Feb-2006] $) $( [30-Apr-2005] $)

  $( Value of the sine function at 0.  (Contributed by Steve Rodriguez,
     5-Jul-2006.) $)
  sin0 $p |- ( sin ` 0 ) = 0 $=
    ( cc0 csin cfv ci cmul co ce cim c1 cr wcel wceq 0re resinval ax-icn mul01i
    ax-mp fveq2i ef0 eqtri im1 3eqtri ) ABCZDAEFZGCZHCZIHCAAJKUCUFLMANQUEIHUEAG
    CIUDAGDOPRSTRUAUB $.
    $( [5-Jul-2006] $)

  $( Value of the sine function at 0. $)
  sin0ALT $p |- ( sin ` 0 ) = 0 $=
    ( cc0 csin cfv cneg wceq neg0 fveq2i cc wcel 0cn sinneg ax-mp eqtr3i eqnegi
    sincl mpbi ) ABCZQDZEQAEADZBCZQRSABFGAHIZTREJAKLMQUAQHIJAOLNP $.
    $( [26-Apr-2008] $) $( [14-Mar-2005] $)

  $( Value of the cosine function at 0. $)
  cos0 $p |- ( cos ` 0 ) = 1 $=
    ( cc0 ccos cfv ci cmul co ce cre c1 cr wcel wceq 0re recosval ax-icn mul01i
    ax-mp fveq2i ef0 eqtri re1 3eqtri ) ABCZDAEFZGCZHCZIHCIAJKUCUFLMANQUEIHUEAG
    CIUDAGDOPRSTRUAUB $.
    $( [28-Feb-2006] $) $( [30-Apr-2005] $)

  $( The exponential function in terms of sine and cosine. $)
  efival $p |- ( A e. CC -> ( exp ` ( _i x. A ) ) =
                ( ( cos ` A ) + ( _i x. ( sin ` A ) ) ) ) $=
    ( cc wcel ci cmul co ce cfv caddc c2 cdiv wceq ax-icn syl syl11anc 2cn 2ne0
    3eqtr4d ine0 c1 cneg cmin ccos csin mulcl mpan efcl negcli addcl cc0 wne wa
    pm3.2i divdir mp3an3 pncan3 opreq2d addass syl111anc 2times opreq1d divcan3
    mp3an23 eqtr2d cosval mulcli mulne0i mp3an13 sinval mulid2i opreq1i opreq2i
    subcl divrec dividi ax-1cn divmuldivi reccli mulid1i 3eqtr3i eqtr3i syl6eqr
    div12 opreq12d ) ABCZDAEFZGHZDUAZAEFZGHZIFZWGWJUBFZIFZJKFZWKJKFZWLJKFZIFZWG
    AUCHZDAUDHZEFZIFWEWKBCZWLBCZWNWQLZWEWGBCZWJBCZXAWEWFBCZXDDBCZWEXFMDAUEUFWFU
    GNZWEWIBCZXEWHBCWEXIDMUHWHAUEUFWIUGNZWGWJUIOWEXDXEXBXHXJWGWJVMOZXAXBJBCZJUJ
    UKZULXCXLXMPQUMWKWLJUNUOOWEWNJWGEFZJKFZWGWEWMXNJKWEWGWJWLIFZIFZWGWGIFZWMXNW
    EXPWGWGIWEXEXDXPWGLXJXHWJWGUPOUQWEXDXEXBWMXQLXHXJXKWGWJWLURUSWEXDXNXRLXHWGU
    TNRVAWEXDXOWGLZXHXDXLXMXSPQWGJVBVCNVDWEWRWOWTWPIAVEWEDWLJDEFZKFZEFZWLDXTKFZ
    EFZWTWPWEXBYBYDLZXKXGXBXTBCZXTUJUKZULYEMYFYGJDPMVFJDPMQSVGUMDWLXTWCVHNWEWSY
    ADEAVIUQWEWPWLTJKFZEFZYDWEXBWPYILZXKXBXLXMYJPQWLJVNVCNYCYHWLETDEFZXTKFZYCYH
    YKDXTKDMVJVKYHDDKFZEFYHTEFYLYHYMTYHEDMSVOVLTJDDVPPMMQSVQYHJPQVRVSVTWAVLWBRW
    DR $.
    $( [30-Apr-2005] $)

  $( The exponential function in terms of sine and cosine. $)
  efmival $p |- ( A e. CC -> ( exp ` ( -u _i x. A ) ) =
                ( ( cos ` A ) - ( _i x. ( sin ` A ) ) ) ) $=
    ( cc wcel ci cneg cmul co ce ccos csin caddc cmin wceq ax-icn mulneg12 mpan
    cfv fveq2d sylancr eqtrd negcl efival cosneg opreq2d sincl mulneg2 opreq12d
    syl sinneg coscl mulcl negsub syl11anc 3eqtrd ) ABCZDEAFGZHQDAEZFGZHQZUQIQZ
    DUQJQZFGZKGZAIQZDAJQZFGZLGZUOUPURHDBCZUOUPURMNDAOPRUOUQBCUSVCMAUAUQUBUHUOVC
    VDVFEZKGZVGUOUTVDVBVIKAUCUOVBDVEEZFGZVIUOVAVKDFAUIUDUOVHVEBCZVLVIMNAUEZDVEU
    FSTUGUOVDBCVFBCZVJVGMAUJUOVHVMVONVNDVEUKSVDVFULUMTUN $.
    $( [14-Jan-2006] $)

  $( Eulerian representation of the complex exponential.  (Suggested by Jeffrey
     Hankins, 3-Jul-2006.) $)
  efeul $p |- ( A e. CC -> ( exp ` A ) = ( ( exp ` ( Re ` A ) ) x.
               ( ( cos ` ( Im ` A ) ) + ( _i x. ( sin ` ( Im ` A ) ) ) ) ) ) $=
    ( cc wcel ce cfv cre ci cim cmul co caddc ccos csin replim fveq2d wceq recl
    recnd ax-icn imcl mulcl sylancr efadd syl11anc efival syl opreq2d 3eqtrd )
    ABCZADEAFEZGAHEZIJZKJZDEZUJDEZULDEZIJZUOUKLEGUKMEIJKJZIJUIAUMDANOUIUJBCULBC
    ZUNUQPUIUJAQRUIGBCUKBCZUSSUIUKATRZGUKUAUBUJULUCUDUIUPURUOIUIUTUPURPVAUKUEUF
    UGUH $.
    $( [25-Jul-2008] $) $( [4-Jul-2006] $)

  $( The exponentials of two imaginary numbers are equal iff their sine and
     cosine components are equal.  (Contributed by Paul Chapman,
     15-Mar-2008.) $)
  efieq $p |- ( ( A e. RR /\ B e. RR ) ->
       ( ( exp ` ( _i x. A ) ) = ( exp ` ( _i x. B ) ) <->
         ( ( cos ` A ) = ( cos ` B ) /\ ( sin ` A ) = ( sin ` B ) ) ) ) $=
    ( cr wcel wa ci cmul co ce wceq ccos csin caddc cc wb efival syl2an recoscl
    cfv recn eqeqan12d resincl jca cru bitrd ) ACDZBCDZEFAGHISZFBGHISZJZAKSZFAL
    SZGHMHZBKSZFBLSZGHMHZJZUKUNJULUOJEZUFANDZBNDZUJUQOUGATBTUSUTUHUMUIUPAPBPUAQ
    UFUKCDZULCDZEUNCDZUOCDZEUQUROUGUFVAVBARAUBUCUGVCVDBRBUBUCUKULUNUOUDQUE $.
    $( [15-Mar-2008] $)

  ${
    sinadd.1 $e |- A e. CC $.
    sinadd.2 $e |- B e. CC $.
    $( Sine addition formula for complex arguments.  Equation 14 of [Gleason]
       p. 310. $)
    sinaddi $p |- ( sin ` ( A + B ) ) =
       ( ( ( sin ` A ) x. ( cos ` B ) ) + ( ( cos ` A ) x. ( sin ` B ) ) ) $=
      ( caddc co cfv ci cmul ce cmin c2 cc wcel wceq addcli ax-mp mulcli ax-icn
      eqtri csin cneg cdiv ccos sinval coscl sincl pnncani adddii fveq2i efaddi
      efival muladdi 3eqtri negcli efmival mulsub mp4an 2timesi 3eqtr4i opreq1i
      opreq12i mulassi mul12i mulcomi opreq2i 2ne0 ine0 mulne0i divmuli addcomi
      2cn mpbir eqtr4i ) ABEFZUAGZHVOIFZJGZHUBZVOIFZJGZKFZLHIFZUCFZLAUDGZHBUAGZ
      IFZIFZBUDGZHAUAGZIFZIFZEFZIFZWCUCFZWJWIIFZWEWFIFZEFZVOMNVPWDOABCDPVOUEQWB
      WNWCUCWEWIIFZWGWKIFZEFZWMEFZXAWMKFZKFWMWMEFWBWNXAWMWMWSWTWEWIAMNZWEMNZCAU
      FQZBMNZWIMNZDBUFQZRWGWKHWFSXGWFMNDBUGQZRZHWJSXDWJMNCAUGQZRZRPWHWLWEWGXFXK
      RWIWKXIXMRPZXNUHVRXBWAXCKVRHAIFZHBIFZEFZJGXOJGZXPJGZIFZXBVQXQJHABSCDUIUJX
      OXPHASCRHBSDRUKXTWEWKEFZWIWGEFZIFXBXRYAXSYBIXDXRYAOCAULQXGXSYBODBULQVBWEW
      KWIWGXFXMXIXKUMTUNWAVSAIFZVSBIFZEFZJGYCJGZYDJGZIFZXCVTYEJVSABHSUOZCDUIUJY
      CYDVSAYICRVSBYIDRUKYHWEWKKFZWIWGKFZIFZXCYFYJYGYKIXDYFYJOCAUPQXGYGYKODBUPQ
      VBXEWKMNXHWGMNYLXCOXFXMXIXKWEWKWIWGUQURTUNVBWMXNUSUTVAWOWQWPEFZWRWOYMOWCY
      MIFZWNOYNLHYMIFZIFWNLHYMVLSWQWPWEWFXFXJRZWJWIXLXIRZPZVCYOWMLIYOHWQIFZHWPI
      FZEFWMHWQWPSYPYQUIYSWHYTWLEHWEWFSXFXJVDYTHWIWJIFZIFWLWPUUAHIWJWIXLXIVEVFH
      WIWJSXIXLVDTVBTVFTWNWCYMLWMVLXNRLHVLSRYRLHVLSVGVHVIVJVMWPWQYQYPVKVNUN $.
      $( [14-Jan-2006] $)

    $( Addition formula for cosine.  Equation 15 of [Gleason] p. 310. $)
    cosaddi $p |- ( cos ` ( A + B ) ) =
         ( ( ( cos ` A ) x. ( cos ` B ) ) - ( ( sin ` A ) x. ( sin ` B ) ) ) $=
      ( caddc co cfv ci cmul ce c2 cmin cc wcel wceq ax-mp mulcli ax-icn 3eqtri
      opreq12i ccos addcli cosval coscl sincl ppncan mp3an adddii fveq2i efaddi
      cneg cdiv csin efival muladdi eqtri negcli efmival mulsub 2timesi 3eqtr4i
      mp4an opreq1i 2cn 2ne0 divcan3i mul4i ixi mulcomi mulm1i opreq2i negsubi
      c1 ) ABEFZUAGZHVNIFZJGZHUKZVNIFZJGZEFZKULFZKAUAGZBUAGZIFZHBUMGZIFZHAUMGZI
      FZIFZEFZIFZKULFZWEWHWFIFZLFZVNMNVOWBOABCDUBVNUCPWAWLKULWKWCWGIFZWDWIIFZEF
      ZEFZWKWRLFZEFZWKWKEFZWAWLWKMNZWRMNXCXAXBOWEWJWCWDAMNZWCMNZCAUDPZBMNZWDMNZ
      DBUDPZQZWGWIHWFRXGWFMNDBUEPZQZHWHRXDWHMNCAUEPZQZQUBZWPWQWCWGXFXLQWDWIXIXN
      QUBXOWKWRWKUFUGVQWSVTWTEVQHAIFZHBIFZEFZJGXPJGZXQJGZIFZWSVPXRJHABRCDUHUIXP
      XQHARCQHBRDQUJYAWCWIEFZWDWGEFZIFWSXSYBXTYCIXDXSYBOCAUNPXGXTYCODBUNPTWCWIW
      DWGXFXNXIXLUOUPSVTVRAIFZVRBIFZEFZJGYDJGZYEJGZIFZWTVSYFJVRABHRUQZCDUHUIYDY
      EVRAYJCQVRBYJDQUJYIWCWILFZWDWGLFZIFZWTYGYKYHYLIXDYGYKOCAURPXGYHYLODBURPTX
      EWIMNXHWGMNYMWTOXFXNXIXLWCWIWDWGUSVBUPSTWKXOUTVAVCWMWKWEWNUKZEFWOWKKXOVDV
      EVFWJYNWEEWJHHIFZWFWHIFZIFVMUKZWNIFYNHWFHWHRXKRXMVGYOYQYPWNIVHWFWHXKXMVIT
      WNWHWFXMXKQZVJSVKWEWNXJYRVLSS $.
      $( [15-Jan-2006] $)
  $}

  $( Addition formula for sine.  Equation 14 of [Gleason] p. 310.  (Contributed
     by Steve Rodriguez, 10-Nov-2006.) $)
  sinadd $p |- ( ( A e. CC /\ B e. CC ) -> ( sin ` ( A + B ) ) =
       ( ( ( sin ` A ) x. ( cos ` B ) ) + ( ( cos ` A ) x. ( sin ` B ) ) ) ) $=
    ( cc wcel caddc co csin cfv ccos cmul cc0 cif fveq2d fveq2 opreq1d opreq12d
    wceq eqeq12d opreq2d 0cn opreq1 opreq2 elimel sinaddi dedth2h ) ACDZBCDZABE
    FZGHZAGHZBIHZJFZAIHZBGHZJFZEFZQUFAKLZBEFZGHZUQGHZUKJFZUQIHZUNJFZEFZQUQUGBKL
    ZEFZGHZUTVEIHZJFZVBVEGHZJFZEFZQABKKAUQQZUIUSUPVDVMUHURGAUQBEUAMVMULVAUOVCEV
    MUJUTUKJAUQGNOVMUMVBUNJAUQINOPRBVEQZUSVGVDVLVNURVFGBVEUQEUBMVNVAVIVCVKEVNUK
    VHUTJBVEINSVNUNVJVBJBVEGNSPRUQVEAKCTUCBKCTUCUDUE $.
    $( [10-Nov-2006] $)

  $( Addition formula for cosine.  Equation 15 of [Gleason] p. 310. $)
  cosadd $p |- ( ( A e. CC /\ B e. CC ) -> ( cos ` ( A + B ) ) =
       ( ( ( cos ` A ) x. ( cos ` B ) ) - ( ( sin ` A ) x. ( sin ` B ) ) ) ) $=
    ( cc wcel caddc co ccos cfv cmul csin cmin wceq cc0 fveq2d opreq1d opreq12d
    cif fveq2 eqeq12d opreq2d opreq1 opreq2 0cn elimel cosaddi dedth2h ) ACDZBC
    DZABEFZGHZAGHZBGHZIFZAJHZBJHZIFZKFZLUGAMQZBEFZGHZURGHZULIFZURJHZUOIFZKFZLUR
    UHBMQZEFZGHZVAVFGHZIFZVCVFJHZIFZKFZLABMMAURLZUJUTUQVEVNUIUSGAURBEUANVNUMVBU
    PVDKVNUKVAULIAURGROVNUNVCUOIAURJROPSBVFLZUTVHVEVMVOUSVGGBVFUREUBNVOVBVJVDVL
    KVOULVIVAIBVFGRTVOUOVKVCIBVFJRTPSURVFAMCUCUDBMCUCUDUEUF $.
    $( [15-Jan-2006] $)

  $( Sine of difference.  (Contributed by Paul Chapman, 12-Oct-2007.) $)
  sinsub $p |- ( ( A e. CC /\ B e. CC ) -> ( sin ` ( A - B ) ) =
                  ( ( ( sin ` A ) x. ( cos ` B ) ) -
                    ( ( cos ` A ) x. ( sin ` B ) ) ) ) $=
    ( cc wcel cneg caddc co csin cfv ccos cmul cmin negsub adantl opreq2d coscl
    wceq sincl syl2an eqtrd wa negcl sinadd sylan2 fveq2d cosneg sinneg mulneg2
    opreq12d mulcl syl11anc 3eqtr3d ) ACDZBCDZUAZABEZFGZHIZAHIZUPJIZKGZAJIZUPHI
    ZKGZFGZABLGZHIUSBJIZKGZVBBHIZKGZLGZUNUMUPCDURVEQBUBAUPUCUDUOUQVFHABMUEUOVEV
    HVJEZFGZVKUOVAVHVDVLFUOUTVGUSKUNUTVGQUMBUFNOUOVDVBVIEZKGZVLUOVCVNVBKUNVCVNQ
    UMBUGNOUMVBCDZVICDZVOVLQUNAPZBRZVBVIUHSTUIUOVHCDZVJCDZVMVKQUMUSCDVGCDVTUNAR
    BPUSVGUJSUMVPVQWAUNVRVSVBVIUJSVHVJMUKTUL $.
    $( [12-Oct-2007] $)

  $( Cosine of difference.  (Contributed by Paul Chapman, 12-Oct-2007.) $)
  cossub $p |- ( ( A e. CC /\ B e. CC ) -> ( cos ` ( A - B ) ) =
                  ( ( ( cos ` A ) x. ( cos ` B ) ) +
                    ( ( sin ` A ) x. ( sin ` B ) ) ) ) $=
    ( cc wcel cneg caddc co ccos cfv cmul csin cmin adantl opreq2d sincl syl2an
    wceq eqtrd coscl mulcl wa cosadd sylan2 negsub fveq2d cosneg sinneg mulneg2
    negcl opreq12d subneg syl11anc 3eqtr3d ) ACDZBCDZUAZABEZFGZHIZAHIZUQHIZJGZA
    KIZUQKIZJGZLGZABLGZHIUTBHIZJGZVCBKIZJGZFGZUOUNUQCDUSVFQBUIAUQUBUCUPURVGHABU
    DUEUPVFVIVKEZLGZVLUPVBVIVEVMLUPVAVHUTJUOVAVHQUNBUFMNUPVEVCVJEZJGZVMUPVDVOVC
    JUOVDVOQUNBUGMNUNVCCDZVJCDZVPVMQUOAOZBOZVCVJUHPRUJUPVICDZVKCDZVNVLQUNUTCDVH
    CDWAUOASBSUTVHTPUNVQVRWBUOVSVTVCVJTPVIVKUKULRUM $.
    $( [12-Oct-2007] $)

  $( Sum of sines.  (Contributed by Paul Chapman, 12-Oct-2007.) $)
  addsin $p |- ( ( A e. CC /\ B e. CC ) -> ( ( sin ` A ) + ( sin ` B ) ) =
                    ( 2 x. ( ( sin ` ( ( A + B ) / 2 ) ) x.
                             ( cos ` ( ( A - B ) / 2 ) ) ) ) ) $=
    ( cc wcel c2 caddc cdiv csin cfv cmin ccos cmul wceq halfcl syl sincl coscl
    co mulcl syl11anc wa addcl subcl 2times sinadd sinsub opreq12d ppncan eqtrd
    syl111anc halfaddsub simpld fveq2d simprd 3eqtr2rd ) ACDBCDUAZEABFRZEGRZHIZ
    ABJRZEGRZKIZLRZLRZVCVCFRZURVAFRZHIZURVAJRZHIZFRZAHIZBHIZFRUPVCCDZVDVEMUPUSC
    DZVBCDZVMUPURCDZVNUPUQCDVPABUBUQNOZURPOUPVACDZVOUPUTCDVRABUCUTNOZVAQOUSVBST
    ZVCUDOUPVJVCURKIZVAHIZLRZFRZVCWCJRZFRZVEUPVGWDVIWEFUPVPVRVGWDMVQVSURVAUETUP
    VPVRVIWEMVQVSURVAUFTUGUPVMWCCDZVMWFVEMVTUPWACDZWBCDZWGUPVPWHVQURQOUPVRWIVSV
    APOWAWBSTVTVCWCVCUHUJUIUPVGVKVIVLFUPVFAHUPVFAMZVHBMZABUKZULUMUPVHBHUPWJWKWL
    UNUMUGUO $.
    $( [12-Oct-2007] $)

  $( Difference of sines.  (Contributed by Paul Chapman, 12-Oct-2007.) $)
  subsin $p |- ( ( A e. CC /\ B e. CC ) -> ( ( sin ` A ) - ( sin ` B ) ) =
                    ( 2 x. ( ( cos ` ( ( A + B ) / 2 ) ) x.
                             ( sin ` ( ( A - B ) / 2 ) ) ) ) ) $=
    ( cc wcel wa c2 caddc cdiv ccos cfv cmin csin cmul coscl sincl mulcl syl2an
    co wceq syl halfaddsubcl 2times sinadd sinsub opreq12d syl111anc halfaddsub
    pnncan eqtrd simpld fveq2d simprd 3eqtr2rd ) ACDBCDEZFABGRFHRZIJZABKRFHRZLJ
    ZMRZMRZUSUSGRZUOUQGRZLJZUOUQKRZLJZKRZALJZBLJZKRUNUSCDZUTVASUNUOCDZUQCDZEZVI
    ABUAZVJUPCDURCDVIVKUONUQOUPURPQTZUSUBTUNVFUOLJZUQIJZMRZUSGRZVQUSKRZKRZVAUNV
    LVFVTSVMVLVCVRVEVSKUOUQUCUOUQUDUETUNVQCDZVIVIVTVASUNVLWAVMVJVOCDVPCDWAVKUOO
    UQNVOVPPQTVNVNVQUSUSUHUFUIUNVCVGVEVHKUNVBALUNVBASZVDBSZABUGZUJUKUNVDBLUNWBW
    CWDULUKUEUM $.
    $( [12-Oct-2007] $)

  $( Sum of cosines.  (Contributed by Paul Chapman, 12-Oct-2007.) $)
  addcos $p |- ( ( A e. CC /\ B e. CC ) -> ( ( cos ` A ) + ( cos ` B ) ) =
                    ( 2 x. ( ( cos ` ( ( A + B ) / 2 ) ) x.
                             ( cos ` ( ( A - B ) / 2 ) ) ) ) ) $=
    ( cc wcel wa ccos cfv caddc co c2 cdiv cmin cmul wceq coscl syl2an opreq12d
    fveq2d csin syl addcom halfaddsub simprd halfaddsubcl mulcl sincl syl111anc
    simpld ppncan cossub cosadd 2times 3eqtr4d 3eqtr2d ) ACDZBCDZEZAFGZBFGZHIZU
    SURHIZABHIJKIZABLIJKIZLIZFGZVBVCHIZFGZHIZJVBFGZVCFGZMIZMIZUOURCDUSCDUTVANUP
    AOBOURUSUAPUQVEUSVGURHUQVDBFUQVFANZVDBNZABUBZUCRUQVFAFUQVMVNVOUHRQUQVKVBSGZ
    VCSGZMIZHIZVKVRLIZHIZVKVKHIZVHVLUQVKCDZVRCDZWCWAWBNUQVBCDZVCCDZEZWCABUDZWEV
    ICDVJCDWCWFVBOVCOVIVJUEPTZUQWGWDWHWEVPCDVQCDWDWFVBUFVCUFVPVQUEPTWIVKVRVKUIU
    GUQWGVHWANWHWGVEVSVGVTHVBVCUJVBVCUKQTUQWCVLWBNWIVKULTUMUN $.
    $( [12-Oct-2007] $)

  $( Difference of cosines.  (Contributed by Paul Chapman, 12-Oct-2007.) $)
  subcos $p |- ( ( A e. CC /\ B e. CC ) -> ( ( cos ` A ) - ( cos ` B ) ) =
                  ( -u 2 x. ( ( sin ` ( ( A + B ) / 2 ) ) x.
                              ( sin ` ( ( A - B ) / 2 ) ) ) ) ) $=
    ( cc wcel wa ccos cfv cmin co c2 caddc cdiv csin cmul cneg mulcl syl2an syl
    wceq coscl halfaddsubcl sincl 2times cossub cosadd opreq12d syl111anc eqtrd
    pnncan halfaddsub simprd fveq2d simpld negsubdi2 eqtr4d 3eqtr2d 2cn sylancr
    wb subcl negcon2 syl11anc mpbid mulneg1 ) ACDZBCDZEZAFGZBFGZHIZJABKIJLIZMGZ
    ABHIJLIZMGZNIZNIZOZJOVONIZVGVPVJOZSZVJVQSZVGVPVOVOKIZVKVMHIZFGZVKVMKIZFGZHI
    ZVSVGVOCDZVPWBSVGVKCDZVMCDZEZWHABUAZWIVLCDVNCDWHWJVKUBVMUBVLVNPQRZVOUCRVGWG
    VKFGZVMFGZNIZVOKIZWPVOHIZHIZWBVGWKWGWSSWLWKWDWQWFWRHVKVMUDVKVMUEUFRVGWPCDZW
    HWHWSWBSVGWKWTWLWIWNCDWOCDWTWJVKTVMTWNWOPQRWMWMWPVOVOUIUGUHVGWGVIVHHIZVSVGW
    DVIWFVHHVGWCBFVGWEASZWCBSZABUJZUKULVGWEAFVGXBXCXDUMULUFVEVHCDZVICDZVSXASVFA
    TZBTZVHVIUNQUOUPVGVPCDZVJCDZVTWAUSVGJCDZWHXIUQWMJVOPURVEXEXFXJVFXGXHVHVIUTQ
    VPVJVAVBVCVGXKWHVRVQSUQWMJVOVDURUO $.
    $( [12-Oct-2007] $)

  $( Sine squared plus cosine squared is 1.  Equation 17 of [Gleason] p. 311.
     Note that this holds for non-real arguments, even though individually each
     term is unbounded. $)
  sincossq $p |- ( A e. CC ->
                  ( ( ( sin ` A ) ^ 2 ) + ( ( cos ` A ) ^ 2 ) ) = 1 ) $=
    ( cc wcel cneg caddc co ccos cfv cmul csin c1 c2 cexp wceq sincl sqcl coscl
    cc0 syl syl11anc negcl cosadd mpdan negid fveq2d syl6eq addcom sqval cosneg
    cmin cos0 opreq2d eqtr4d sinneg negeqd eqtrd mulneg2 3eqtr2d opreq12d mulcl
    negneg negsub 3eqtrrd 3eqtr3rd ) ABCZAADZEFZGHZAGHZVFGHZIFZAJHZVFJHZIFZUJFZ
    KVLLMFZVILMFZEFZVEVFBCZVHVONAUAZAVFUBUCVEVHRGHKVEVGRGAUDUEUKUFVEVRVQVPEFZVK
    VNDZEFZVOVEVPBCZVQBCZVRWANVEVLBCZWDAOZVLPSVEVIBCZWEAQZVIPSVPVQUGTVEVQVKVPWB
    EVEVQVIVIIFZVKVEWHVQWJNWIVIUHSVEVJVIVIIAUIULUMVEVPVLVLIFZVLVMDZIFZWBVEWFVPW
    KNWGVLUHSVEWLVLVLIVEWLVLDZDZVLVEVMWNAUNUOVEWFWOVLNWGVLVASUPULVEWFVMBCZWMWBN
    WGVEVSWPVTVFOSZVLVMUQTURUSVEVKBCZVNBCZWCVONVEWHVJBCZWRWIVEVSWTVTVFQSVIVJUTT
    VEWFWPWSWGWQVLVMUTTVKVNVBTVCVD $.
    $( [15-Jan-2006] $)

  $( Double-angle formula for sine.  (Contributed by Paul Chapman,
     17-Jan-2008.) $)
  sin2t $p |- ( A e. CC ->
      ( sin ` ( 2 x. A ) ) = ( 2 x. ( ( sin ` A ) x. ( cos ` A ) ) ) ) $=
    ( cc wcel c2 cmul csin cfv caddc ccos 2times fveq2d wceq coscl sincl mulcom
    co syl11anc opreq2d sinadd anidms mulcl syl 3eqtr4d eqtrd ) ABCZDAEPZFGAAHP
    ZFGZDAFGZAIGZEPZEPZUEUFUGFAJKUEUKUJUIEPZHPZUKUKHPZUHULUEUMUKUKHUEUJBCZUIBCZ
    UMUKLAMZANZUJUIOQRUEUHUNLAASTUEUKBCZULUOLUEUQUPUTUSURUIUJUAQUKJUBUCUD $.
    $( [17-Jan-2008] $)

  $( Double-angle formula for cosine.  (Contributed by Paul Chapman,
     24-Jan-2008.) $)
  cos2t $p |- ( A e. CC -> ( cos ` ( 2 x. A ) ) =
                           ( ( 2 x. ( ( cos ` A ) ^ 2 ) ) - 1 ) ) $=
    ( cc wcel ccos c2 cexp co c1 cmin csin cmul wceq caddc sqcl syl11anc ax-1cn
    cfv syl 2times sqval sincl coscl addcom eqtr3d subadd mp3an1 mpbird opreq2d
    sincossq wb opreq1d subsub3 mp3an2 eqtr4d cosadd opreq12d 3eqtr4d 3eqtr4rd
    anidms fveq2d ) ABCZADQZEFGZHVCIGZIGZVCAJQZEFGZIGZEVCKGZHIGZEAKGZDQZVAVDVGV
    CIVAVDVGLZVCVGMGZHLZVAVGVCMGZVNHVAVGBCZVCBCZVPVNLVAVFBCZVQAUAZVFNRZVAVBBCZV
    RAUBZVBNRZVGVCUCOAUIUDVAVRVQVMVOUJZWDWAHBCZVRVQWEPHVCVGUEUFOUGUHVAVJVCVCMGZ
    HIGZVEVAVIWGHIVAVRVIWGLWDVCSRUKVAVRVRVEWHLZWDWDVRWFVRWIPVCHVCULUMOUNVAAAMGZ
    DQZVBVBKGZVFVFKGZIGZVLVHVAWKWNLAAUOUSVAVKWJDASUTVAVCWLVGWMIVAWBVCWLLWCVBTRV
    AVSVGWMLVTVFTRUPUQUR $.
    $( [24-Jan-2008] $)

  $( Double-angle formula for cosine in terms of sine. $)
  cos2tsin $p |- ( A e. CC -> ( cos ` ( 2 x. A ) ) =
                           ( 1 - ( 2 x. ( ( sin ` A ) ^ 2 ) ) ) ) $=
    ( cc wcel c2 cmul co ccos cfv cexp c1 cmin wceq caddc sqcl syl 2cn syl11anc
    mp3an1 syl6eq ax-1cn csin cos2t sincl coscl sincossq opreq2d eqtr3d mulid1i
    adddi wb mulcl sylancr subadd mpbird opreq1d sub32 mp3an13 eqcomi subaddrii
    df-2 opreq1i 3eqtr2d ) ABCZDAEFGHDAGHZDIFZEFZJKFDDAUAHZDIFZEFZKFZJKFZJVIKFZ
    AUBVCVJVFJKVCVJVFLZVIVFMFZDLZVCVNDJEFZDVCDVHVEMFZEFZVNVPVCVHBCZVEBCZVRVNLZV
    CVGBCVSAUCVGNOZVCVDBCVTAUDVDNOZDBCZVSVTWAPDVHVEUIRQVCVQJDEAUEUFUGDPUHSVCVIB
    CZVFBCZVMVOUJZVCWDVSWEPWBDVHUKULZVCWDVTWFPWCDVEUKULWDWEWFWGPDVIVFUMRQUNUOVC
    VKDJKFZVIKFZVLVCWEVKWJLZWHWDWEJBCWKPTDVIJUPUQOWIJVIKDJJPTTDJJMFUTURUSVASVB
    $.
    $( [12-Sep-2008] $)

  ${
    sin2t.1 $e |- A e. CC $.
    $( Double-angle formula for cosine.  (Contributed by Paul Chapman,
       25-Nov-2007.) $)
    cos2OLD $p |- ( cos ` ( 2 x. A ) ) =
                ( ( 2 x. ( ( cos ` A ) ^ 2 ) ) - 1 ) $=
      ( c2 cmul co ccos cfv cexp csin cmin c1 caddc 2timesi cc wcel sqvali wceq
      ax-mp ax-1cn sqcli cosaddi coscl sincl opreq12i 3eqtr4i sincossq subadd2i
      fveq2i mpbir opreq2i subsub3 mp3an opreq1i eqtr4i 3eqtr2i ) CADEZFGZAFGZC
      HEZAIGZCHEZJEZUSKUSJEZJEZCUSDEZKJEZAALEZFGURURDEZUTUTDEZJEUQVBAABBUAUPVGF
      ABMUHUSVHVAVIJURANOZURNOBAUBRZPUTVJUTNOBAUCRZPUDUEVCVAUSJVCVAQVAUSLEKQZVJ
      VMBAUFRKUSVASURVKTZUTVLTUGUIUJVDUSUSLEZKJEZVFUSNOZKNOVQVDVPQVNSVNUSKUSUKU
      LVEVOKJUSVNMUMUNUO $.
      $( [25-Nov-2007] $)
  $}

  $( The sine of a real number lies between -1 and 1.  Equation 18 of [Gleason]
     p. 311. $)
  sinbnd $p |- ( A e. RR -> ( -u 1 <_ ( sin ` A ) /\ ( sin ` A ) <_ 1 ) ) $=
    ( cr wcel csin cfv c1 cle wbr cneg c2 cexp co wa ccos cc0 syl wb resqcl 1re
    mpbid caddc recoscl sqge0 resincl addge01 syl11anc cc wceq sincossq syl6eqr
    recn sq1 breqtrd lt01 ltleii lenegsq mp3an23 lenegcon1 anbi2d bitr3d ancomd
    0re mpan2 ) ABCZADEZFGHZFIVEGHZVDVEJKLZFJKLZGHZVFVGMZVDVHVHANEZJKLZUALZVIGV
    DOVMGHZVHVNGHZVDVLBCZVOAUBZVLUCPVDVHBCZVMBCZVOVPQVDVEBCZVSAUDZVERPVDVQVTVRV
    LRPVHVMUEUFTVDVNFVIVDAUGCVNFUHAUKAUIPULUJUMVDWAVJVKQWBWAVFVEIFGHZMZVJVKWAFB
    CZOFGHWDVJQSOFVBSUNUOVEFUPUQWAWCVGVFWAWEWCVGQSVEFURVCUSUTPTVA $.
    $( [16-Jan-2006] $)

  $( The cosine of a real number lies between -1 and 1.  Equation 18 of
     [Gleason] p. 311. $)
  cosbnd $p |- ( A e. RR -> ( -u 1 <_ ( cos ` A ) /\ ( cos ` A ) <_ 1 ) ) $=
    ( cr wcel ccos cfv c1 cle wbr cneg c2 cexp co wa csin cc0 syl wb resqcl 1re
    mpbid caddc resincl sqge0 recoscl addge02 syl11anc cc wceq sincossq syl6eqr
    recn sq1 breqtrd lt01 ltleii lenegsq mp3an23 lenegcon1 anbi2d bitr3d ancomd
    0re mpan2 ) ABCZADEZFGHZFIVEGHZVDVEJKLZFJKLZGHZVFVGMZVDVHANEZJKLZVHUALZVIGV
    DOVMGHZVHVNGHZVDVLBCZVOAUBZVLUCPVDVHBCZVMBCZVOVPQVDVEBCZVSAUDZVERPVDVQVTVRV
    LRPVHVMUEUFTVDVNFVIVDAUGCVNFUHAUKAUIPULUJUMVDWAVJVKQWBWAVFVEIFGHZMZVJVKWAFB
    CZOFGHWDVJQSOFVBSUNUOVEFUPUQWAWCVGVFWAWEWCVGQSVEFURVCUSUTPTVA $.
    $( [10-Feb-2007] $) $( [16-Jan-2006] $)

  $( Lemma for ~ sin01bnd and ~ cos01bnd . $)
  sin01bndlem1 $p |- ( 5 / ( ( ! ` 4 ) x. 4 ) ) < ( 1 / 6 ) $=
    ( c5 c6 c2 c4 cexp co cmul cdiv cfa cfv c1 clt wbr c3 cle caddc 5re opreq2i
    wcel 6re cc0 3pos 0re 3re ltadd1i mpbi recni addid2i c8 cu2 5p3e8 3nn nncni
    addcomi 3eqtr2ri 3brtr3i cn 2nn nnge1 ax-mp cr lep1 breqtrri cn0 w3a wa 2re
    df-4 nnnn0i 4nn 3pm3.2i expwordi mp2an 8re eqeltri nnexpcl ltletri remulcli
    mpan nnrei 6pos nngt0i mulgt0ii ltdiv1ii fveq2i facp1 eqtr2i 3eqtri opreq1i
    wceq sq2 cc fac3 4re mulassi 3eqtr3i 2p2e4 2nn0 expadd mp3an eqtr3i 3eqtr2i
    2cn nnne0i dividi ax-1cn gt0ne0ii divmuldivi reccli mulid1i mulid2i 3brtr4i
    ) ABCDEFZGFZHFZXMXNHFZADIJZDGFZHFKBHFZLAXMLMZXOXPLMACNEFZLMYAXMOMZXTUAAPFZN
    APFZAYALUANLMYCYDLMUBUANAUCUDQUEUFAAQUGZUHYAUIANPFYDUJUKANYENULUMUNUOUPKCOM
    ZNDOMZYBCUQSZYFURCUSUTNNKPFZDONVASNYIOMUDNVBUTVHVCCVASZNVDSZDVDSZVEYFYGVFYB
    YJYKYLVGNULVIZDVJVIZVKCNDVLVSVMAYAXMQYAUIVAUJVNVOXMYHYLXMUQSURYNCDVPVMZVTZV
    QVMAXMXNQYPBXMTYPVRBXMTYPWAXMYOWBWCWDUFXRXNAHXRNIJZCCEFZYRGFZGFZYQXMGFXNXQY
    RGFYQYRGFZYRGFXRYTXQUUAYRGXQYIIJZYQYIGFZUUADYIIVHWEYKUUBUUCWJYMNWFUTYIYRYQG
    YRDYIWKVHWGRWHWIYRDXQGWKRYQYRYRYQBWLWMBTUGZVOYRDWLWKDWNUGVOZUUEWOWPXMYSYQGC
    CCPFZEFZXMYSUUFDCEWQRCWLSCVDSZUUHUUGYSWJXCWRWRCCCWSWTXARYQBXMGWMWIXBRKXMGFZ
    XNHFZXSXPXSXMXMHFZGFXSKGFUUJXSUUKKXSGXMXMYOUMZXMYOXDZXERKBXMXMXFUUDUULUULBT
    WAXGZUUMXHXSBUUDUUNXIXJWPUUIXMXNHXMUULXKWIXAXL $.
    $( [19-Jan-2008] $)

  ${
    $d A j k x y $.  $d F k x $.
    sin01bndlem2.1 $e |- F = { <. j , y >. | ( j e. NN0 /\
                               y = ( ( ( _i x. A ) ^ j ) / ( ! ` j ) ) ) } $.
    $( Lemma for ~ sin01bnd . $)
    sin01bndlem2 $p |- ( A e. ( 0 (,] 1 ) ->
                         ( abs ` ( Im ` sum_ k e. ( ZZ>= ` 4 ) ( F ` k ) ) ) <
                         ( ( A ^ 3 ) / 6 ) ) $=
      ( cc0 c1 co wcel c4 cim cmul c3 c6 cc cr clt wbr cle cioc cuz cfv cv cabs
      csu cexp c5 cfa cdiv ci cn ax-icn w3a wb 0re 1re elioc2 mp2an recnd mulcl
      simp1bi sylancr 4nn eftlcl sylancl imcl abscl 3syl cn0 nnnn0i mpan2 caddc
      reexpcl df-5 opreq1i eftlubcl ax-mp eqeltri remulcl 3nn wne 6pos gt0ne0ii
      6re redivcl mp3an23 wceq cre eqid abspef01tlubi opreq2i syl6breqr simp2bi
      olci rereccli expgt0 mp3an2 syl11anc wa sin01bndlem1 ltmul2 mpbii mp3an12
      wi 3re ltp1i df-4 breqtrri expword2i expcom mp3an3 com12 3impib sylbi jca
      recgt0ii pm3.2i lemul1 mpbid recni divrec breqtrrd ltletrd lelttrd ) BGHU
      AIJZKUBUCDUDEUCDUFZLUCZUEUCZBKUGIZUHKUIUCKMIZUJIZMIZBNUGIZOUJIZYFYGPJZYHP
      JYIQJYFUKBMIZPJZKULJZYPYFUKPJBPJYRUMYFBYFBQJZGBRSZBHTSZGQJHQJYFYTUUAUUBUN
      ZUOUPUQGHBURUSZVBZUTUKBVAVCVDAYQCDEKFVEVFYPYHYGVGUTYHVHVIYFYTYJQJZYMQJZUU
      EYTKVJJZUUFKVDVKZBKVNZVLZUUFYLQJZUUGYLKHVMIZYKUJIZQUHUUMYKUJVOVPZYSUUNQJV
      DKVQVRVSZYJYLVTVLVIZYFYTYNQJZYOQJZUUEYTNVJJZUURNWAVKZBNVNVLZUUROQJOGWBZUU
      SWEOWEWCWDZYNOWFWGVIZYFYIYJUUNMIZYMTYFYSYIUVFTSVDABLCDEKFLLWHLWIWHLWJWOWK
      VLYLUUNYJMUUOWLWMYFYMYJHOUJIZMIZYOUUQYFUUFUVGQJZUVHQJYFYTUUHUUFUUEUUIUUJV
      FZOWEUVDWPZYJUVGVTVFUVEYFUUFGYJRSZYMUVHRSZUVJYFYTUUAUVLUUEYFYTUUAUUBUUDWN
      YTUUHUUAUVLUUIBKWQWRWSUULUVIUUFUVLWTZUVMUUPUVKUULUVIUVNUNYLUVGRSUVMXAYLUV
      GYJXBXCXDWSYFUVHYNUVGMIZYOTYFYJYNTSZUVHUVOTSZYFUUCUVPUUDYTUUAUUBUVPYTUUTU
      UHUUAUUBWTZUVPXEUVAUUIUVRYTUUTUUHUNZUVPUUAUUBNKRSZUVSUVPXENNHVMIKRNXFXGXH
      XIUVSUUAUUBUVTUNUVPBNKXJXKXLXMWGXNXOYFYTUUFUURWTUVPUVQUOZUUEYTUUFUURUUKUV
      BXPUUFUURUVIGUVGRSZWTUWAUVIUWBUVKOWEWCXQXRYJYNUVGXSXLVIXTYFYTYNPJZYOUVOWH
      ZUUEYTYNUVBUTUWCOPJUVCUWDOWEYAUVDYNOYBWGVIYCYDYE $.
      $( [19-Jan-2008] $)

    $( Lemma for ~ sin01bnd . $)
    sin01bndlem3 $p |- ( A e. ( 0 (,] 1 ) ->
        ( ( A - ( ( A ^ 3 ) / 3 ) ) < ( sin ` A ) /\ ( sin ` A ) < A ) ) $=
      ( cc0 c1 co wcel c3 cdiv clt wbr c6 cc wceq cr syl c2 cmul cioc cexp cmin
      vk csin cfv cneg caddc cle w3a 0re 1re elioc2 mp2an simp1bi recnd cn0 3nn
      wb nnnn0i wa reexpcl wne 6re 6pos gt0ne0ii redivcl mp3an23 sylancl subsub
      renegcl syl111anc subneg syl11anc 2times 2cn mulid1i recni mulcomi eqtr3i
      3t2e6 opreq12i 3pos 2ne0 ax-1cn divcan5 mp3an1 mp4an opreq2i pm3.2i div12
      3re mp3an13 divrec 3eqtr4a 3eqtr2d opreq2d eqtr3d c4 cuz csu sin01bndlem2
      cv cim cabs ci cn ax-icn mulcl sylancr 4nn eftlcl imcl abslt mpbid simpld
      resubcl ltadd2 resin4p breqtrrd eqbrtrrd simprd eqbrtrd npcan breqtrd jca
      ) BFGUAHIZBBJUBHZJKHZUCHZBUEUFZLMYKBLMYGBYHNKHZUCHZYLUGZUHHZYJYKLYGBYLYNU
      CHZUCHZYOYJYGBOIZYLOIZYNOIYQYOPYGBYGBQIZFBLMZBGUIMZFQIGQIYGYTUUAUUBUJUSUK
      ULFGBUMUNUOZUPZYGYLYGYTJUQIZYLQIZUUCJURUTZYTUUEVAYHQIZUUFBJVBZUUHNQINFVCZ
      UUFVDNVDVEVFZYHNVGVHRVIZUPZYGYNYGUUFYNQIZUULYLVKRZUPBYLYNVJVLYGYPYIBUCYGY
      PYLYLUHHZSYLTHZYIYGYSYSYPUUPPUUMUUMYLYLVMVNYGYSUUQUUPPUUMYLVORYGYHSNKHZTH
      ZYHGJKHZTHZUUQYIUURUUTYHTSGTHZSJTHZKHZUURUUTUVBSUVCNKSVPVQJSTHUVCNJSJWLVR
      ZVPVSWAVTWBJOIZJFVCZSOIZSFVCZUVDUUTPZUVEJWLWCVFZVPWDGOIUVFUVGVAUVHUVIVAUV
      JWEGJSWFWGWHVTWIYGYHOIZUUQUUSPZYGYHYGYTUUEUUHUUCUUGUUIVIUPZUVHUVLNOIZUUJV
      AUVMVPUVOUUJNVDVRUUKWJSYHNWKWMRYGUVLYIUVAPZUVNUVLUVFUVGUVPUVEUVKYHJWNVHRW
      OWPWQWRYGYOYMWSWTUFUDXCDUFUDXAZXDUFZUHHZYKLYGYNUVRLMZYOUVSLMZYGUVTUVRYLLM
      ZYGUVRXEUFYLLMZUVTUWBVAZABCUDDEXBYGUVRQIZUUFUWCUWDUSYGUVQOIZUWEYGXFBTHZOI
      ZWSXGIUWFYGXFOIYRUWHXHUUDXFBXIXJXKAUWGCUDDWSEXLVIUVQXMRZUULUVRYLXNVNXOZXP
      YGUUNUWEYMQIZUVTUWAUSUUOUWIYGYTUUFUWKUUCUULBYLXQVNZYNUVRYMXRVLXOYGYTYKUVS
      PUUCABCUDDEXSRZXTYAYGYKYMYLUHHZBLYGYKUVSUWNLUWMYGUWBUVSUWNLMZYGUVTUWBUWJY
      BYGUWEUUFUWKUWBUWOUSUWIUULUWLUVRYLYMXRVLXOYCYGYRYSUWNBPUUDUUMBYLYDVNYEYF
      $.
      $( [19-Jan-2008] $)

    $( Lemma for ~ cos01bnd . $)
    cos01bndlem2 $p |- ( A e. ( 0 (,] 1 ) ->
                         ( abs ` ( Re ` sum_ k e. ( ZZ>= ` 4 ) ( F ` k ) ) ) <
                         ( ( A ^ 2 ) / 6 ) ) $=
      ( cc0 c1 co wcel c4 cre cmul c2 c6 cc cr clt wbr cle cioc cuz cfv cv cabs
      csu cexp c5 cfa cdiv ci cn ax-icn w3a wb 0re 1re elioc2 mp2an recnd mulcl
      simp1bi sylancr 4nn eftlcl sylancl recl abscl 3syl cn0 nnnn0i mpan2 caddc
      reexpcl df-5 opreq1i eftlubcl ax-mp eqeltri remulcl wne 6re 6pos gt0ne0ii
      resqcl redivcl mp3an23 wceq cim eqid orci abspef01tlubi opreq2i syl6breqr
      rereccli simp2bi expgt0 mp3an2 syl11anc sin01bndlem1 ltmul2 mpbii mp3an12
      wa 2nn0 2pos 2re ltadd1i mpbi 2cn addid2i 3brtr3i expword2i expcom mp3an3
      wi 2p2e4 com12 3impib sylbi jca recgt0ii pm3.2i lemul1 mpbid recni divrec
      breqtrrd ltletrd lelttrd ) BGHUAIJZKUBUCDUDEUCDUFZLUCZUEUCZBKUGIZUHKUIUCK
      MIZUJIZMIZBNUGIZOUJIZYKYLPJZYMPJYNQJYKUKBMIZPJZKULJZUUAYKUKPJBPJUUCUMYKBY
      KBQJZGBRSZBHTSZGQJHQJYKUUEUUFUUGUNZUOUPUQGHBURUSZVBZUTUKBVAVCVDAUUBCDEKFV
      EVFUUAYMYLVGUTYMVHVIYKUUEYOQJZYRQJZUUJUUEKVJJZUUKKVDVKZBKVNZVLZUUKYQQJZUU
      LYQKHVMIZYPUJIZQUHUURYPUJVOVPZUUDUUSQJVDKVQVRVSZYOYQVTVLVIZYKUUEYSQJZYTQJ
      ZUUJBWEZUVCOQJOGWAZUVDWBOWBWCWDZYSOWFWGVIZYKYNYOUUSMIZYRTYKUUDYNUVITSVDAB
      LCDEKFLLWHLWIWHLWJWKWLVLYQUUSYOMUUTWMWNYKYRYOHOUJIZMIZYTUVBYKUUKUVJQJZUVK
      QJYKUUEUUMUUKUUJUUNUUOVFZOWBUVGWOZYOUVJVTVFUVHYKUUKGYORSZYRUVKRSZUVMYKUUE
      UUFUVOUUJYKUUEUUFUUGUUIWPUUEUUMUUFUVOUUNBKWQWRWSUUQUVLUUKUVOXDZUVPUVAUVNU
      UQUVLUVQUNYQUVJRSUVPWTYQUVJYOXAXBXCWSYKUVKYSUVJMIZYTTYKYOYSTSZUVKUVRTSZYK
      UUHUVSUUIUUEUUFUUGUVSUUENVJJZUUMUUFUUGXDZUVSXPXEUUNUWBUUEUWAUUMUNZUVSUUFU
      UGNKRSZUWCUVSXPGNVMIZNNVMIZNKRGNRSUWEUWFRSXFGNNUPXGXGXHXINXJXKXQXLUWCUUFU
      UGUWDUNUVSBNKXMXNXOXRWGXSXTYKUUEUUKUVCXDUVSUVTUOZUUJUUEUUKUVCUUPUVEYAUUKU
      VCUVLGUVJRSZXDUWGUVLUWHUVNOWBWCYBYCYOYSUVJYDXOVIYEYKUUEYSPJZYTUVRWHZUUJUU
      EYSUVEUTUWIOPJUVFUWJOWBYFUVGYSOYGWGVIYHYIYJ $.
      $( [19-Jan-2008] $)

    $( Lemma for ~ cos01bnd . $)
    cos01bndlem3 $p |- ( A e. ( 0 (,] 1 ) ->
                         ( ( 1 - ( 2 x. ( ( A ^ 2 ) / 3 ) ) ) < ( cos ` A ) /\
                           ( cos ` A ) < ( 1 - ( ( A ^ 2 ) / 3 ) ) ) ) $=
      ( cc0 c1 co wcel c2 c3 cmul cmin clt wbr c6 cc wceq cr syl cioc cexp cdiv
      vk ccos cfv caddc cle w3a wb 0re 1re elioc2 mp2an simp1bi resqcl rehalfcl
      cneg recnd cn0 2nn0 reexpcl wne 6re 6pos gt0ne0ii redivcl mp3an23 sylancl
      wa renegcl ax-1cn subsub mp3an1 syl11anc subneg 2cn divrec recni opreq12d
      2ne0 halfpm6th simpri opreq2i reccli adddi syl5eqr 3re 3pos div12 3eqtr2d
      pm3.2i eqtrd opreq2d eqtr3d c4 cuz cv csu cre cabs cos01bndlem2 ci ax-icn
      cn recn mulcl sylancr 4nn eftlcl recl abslt mpbid simpld ltadd2 syl111anc
      resubcl recos4p breqtrrd eqbrtrrd simprd eqbrtrd simpli subdi breqtrd jca
      3eqtr4rd ) BFGUAHIZGJBJUBHZKUCHZLHZMHZBUEUFZNOYMGYJMHZNOYHGYIJUCHZMHZYIPU
      CHZURZUGHZYLYMNYHGYOYRMHZMHZYSYLYHYOQIZYRQIZUUAYSRZYHYOYHYISIZYOSIZYHBSIZ
      UUEYHUUGFBNOZBGUHOZFSIGSIZYHUUGUUHUUIUIUJUKULFGBUMUNUOZBUPTZYIUQTZUSZYHYR
      YHYQSIZYRSIZYHUUGJUTIZUUOUUKVAUUGUUQVJUUEUUOBJVBUUEPSIPFVCZUUOVDPVDVEVFZY
      IPVGVHTVIZYQVKTZUSGQIZUUBUUCUUDVLGYOYRVMVNVOYHYTYKGMYHYTYOYQUGHZYKYHUUBYQ
      QIZYTUVCRUUNYHYQUUTUSZYOYQVPVOYHYIQIZUVCYKRYHYIUULUSZUVFUVCYIGJUCHZLHZYIG
      PUCHZLHZUGHZYIJKUCHZLHZYKUVFYOUVIYQUVKUGUVFJQIZJFVCYOUVIRVQWAYIJVRVHZUVFP
      QIUURYQUVKRPVDVSZUUSYIPVRVHZVTUVFUVNYIUVHUVJUGHZLHZUVLUVSUVMYILUVHUVJMHZG
      KUCHZRZUVSUVMRZWBWCWDUVFUVHQIZUVJQIZUVTUVLRJVQWAWEZPUVQUUSWEZYIUVHUVJWFVH
      WGUVFUVOKQIZKFVCZVJUVNYKRVQUWIUWJKWHVSZKWHWIVFZWLYIJKWJVHWKTWMWNWOYHYSYPW
      PWQUFUDWRDUFUDWSZWTUFZUGHZYMNYHYRUWNNOZYSUWONOZYHUWPUWNYQNOZYHUWNXAUFYQNO
      ZUWPUWRVJZABCUDDEXBYHUWNSIZUUOUWSUWTUJYHUWMQIZUXAYHXCBLHZQIZWPXEIUXBYHXCQ
      IBQIZUXDXDYHUUGUXEUUKBXFTXCBXGXHXIAUXCCUDDWPEXJVIUWMXKTZUUTUWNYQXLVOXMZXN
      YHUUPUXAYPSIZUWPUWQUJUVAUXFYHUUJUUFUXHULUUMGYOXQXHZYRUWNYPXOXPXMYHUUGYMUW
      ORUUKABCUDDEXRTZXSXTYHYMYPYQUGHZYNNYHYMUWOUXKNUXJYHUWRUWOUXKNOZYHUWPUWRUX
      GYAYHUXAUUOUXHUWRUXLUJUXFUUTUXIUWNYQYPXOXPXMYBYHGYOYQMHZMHZUXKYNYHUUBUVDU
      XNUXKRZUUNUVEUVBUUBUVDUXOVLGYOYQVMVNVOYHUXMYJGMYHUVFUXMYJRUVGUVFYIUWBLHZU
      VIUVKMHZYJUXMUVFUXPYIUWALHZUXQUWAUWBYILUWCUWDWBYCWDUVFUWEUWFUXRUXQRUWGUWH
      YIUVHUVJYDVHWGUVFUWIUWJYJUXPRUWKUWLYIKVRVHUVFYOUVIYQUVKMUVPUVRVTYGTWNWOYE
      YF $.
      $( [19-Jan-2008] $)
  $}

  ${
    $d A j y $.
    $( Bounds on the sine of a positive real number less than or equal to 1.
       (Contributed by Paul Chapman, 19-Jan-2008.) $)
    sin01bnd $p |- ( A e. ( 0 (,] 1 ) ->
        ( ( A - ( ( A ^ 3 ) / 3 ) ) < ( sin ` A ) /\ ( sin ` A ) < A ) ) $=
      ( vy vj cv cn0 wcel ci cmul co cexp cfa cdiv wceq copab eqid sin01bndlem3
      cfv wa ) BACCDZEFBDGAHISJISKQLIMRCBNZTOP $.
      $( [19-Jan-2008] $)

    $( Bounds on the cosine of a positive real number less than or equal to 1.
       (Contributed by Paul Chapman, 19-Jan-2008.) $)
    cos01bnd $p |- ( A e. ( 0 (,] 1 ) ->
                     ( ( 1 - ( 2 x. ( ( A ^ 2 ) / 3 ) ) ) < ( cos ` A ) /\
                       ( cos ` A ) < ( 1 - ( ( A ^ 2 ) / 3 ) ) ) ) $=
      ( vy vj cv cn0 wcel ci cmul co cexp cfa cdiv wceq copab eqid cos01bndlem3
      cfv wa ) BACCDZEFBDGAHISJISKQLIMRCBNZTOP $.
      $( [19-Jan-2008] $)
  $}

  $( Bounds on the cosine of 1.  (Contributed by Paul Chapman, 19-Jan-2008.) $)
  cos1bnd $p |- ( ( 1 / 3 ) < ( cos ` 1 ) /\ ( cos ` 1 ) < ( 2 / 3 ) ) $=
    ( c1 c3 cdiv co clt wbr c2 cmul cmin opreq1i opreq2i 2cn ax-1cn caddc eqtri
    3nn cr wcel cc0 1re ccos cfv cexp nncni nnne0i divreci eqtr4i divcli reccli
    sq1 df-3 dividi divdiri 3eqtr3ri subaddrii cle wa lt01 leidi cioc wb elioc2
    w3a 0re mp2an cos01bnd sylbir mp3an simpli eqbrtrri simpri subadd2i breqtri
    wceq mpbir pm3.2i ) ABCDZAUAUBZEFVRGBCDZEFAGAGUCDZBCDZHDZIDZVQVREWCAVSIDVQW
    BVSAIWBGVQHDVSWAVQGHVTABCUJJZKGBLBPUDZBPUEZUFUGKAVSVQMGBLWEWFUHZBWEWFUIZBBC
    DGANDZBCDAVSVQNDZBWIBCUKJBWEWFULGABLMWEWFUMUNZUOOWCVREFZVRAWAIDZEFZAQRZSAEF
    ZAAUPFZWLWNUQZTURATUSWOWPWQVCZASAUTDRZWRSQRWOWTWSVAVDTSAAVBVEAVFVGVHZVIVJVR
    WMVSEWLWNXAVKWMAVQIDZVSWAVQAIWDKXBVSVNWJAVNWKAVQVSMWHWGVLVOOVMVP $.
    $( [19-Jan-2008] $)

  $( Bounds on the cosine of 2.  (Contributed by Paul Chapman, 19-Jan-2008.) $)
  cos2bnd $p |- ( -u ( 7 / 9 ) < ( cos ` 2 ) /\
                     ( cos ` 2 ) < -u ( 1 / 9 ) ) $=
    ( c7 c9 cdiv co cneg c2 clt wbr c1 cexp cmin wcel cc0 wceq 2cn caddc ax-1cn
    c8 c3 c4 ccos cfv cmul cc wne wa recni 9pos gt0ne0ii pm3.2i divsubdir mp3an
    9re negsubdi2i 7re addassi df-8 opreq1i df-2 opreq2i 3eqtr4ri df-9 subadd2i
    eqtr4i mpbir negeqi eqtr3i dividi 3eqtr3ri divneg divassi 3re nnne0i sqdivi
    mulid1i 3nn sq1 sq3 opreq12i eqtri cos1bnd simpli cle wb nn0ge0i nngt0i 1re
    1nn0 divge0i mp2an 0re cr recoscl ax-mp rereccli lelttri ltleii lt2sqi mpbi
    eqbrtrri resqcli 2re ltmul2i redivcli remulcli ltsub1 fveq2i cos2t breqtrri
    2pos simpri 2nn0 sq2 breqtri 4re mulcomi 4t2e8 8re subaddi 3eqtr2ri eqbrtri
    eqcomi ) ABCDEZFUAUBZGHYDIBCDZEZGHYCFIUAUBZFJDZUCDZIKDZYDGFBCDZIKDZYCYJGYLA
    EZBCDZYCFBKDZBCDZYKBBCDZKDZYNYLFUDLBUDLZYSBMUEZUFZYPYRNOBUMUGZYSYTUUBBUMUHU
    IZUJZFBBUKULYOYMBCBFKDZEYOYMBFUUBOUNUUEAUUEANAFPDZBNUUFRIPDZBAIPDZIPDAIIPDZ
    PDUUGUUFAIIAUOUGZQQUPRUUHIPUQURFUUIAPUSUTVAVBVDBFAUUBOUUJVCVEVFVGURYQIYKKBU
    UBUUCVHZUTVIAUDLYSYTYCYNNUUJUUBUUCABVJULVDYKYIGHZYLYJGHZFYEUCDZYKYIGFIUCDZB
    CDUUNYKFIBOQUUBUUCVKUUOFBCFOVOZURVGYEYHGHZUUNYIGHZISCDZFJDZYEYHGUUTIFJDZSFJ
    DZCDYEISQSVLUGZSVPVMZVNUVAIUVBBCVQVRVSVTUUSYGGHZUUTYHGHZUVEYGFSCDZGHZWAWBZM
    UUSWCHZMYGWCHZUVEUVFWDMIWCHMSGHZUVJIWHWESVPWFZISWGVLWIWJZMYGWKIWLLZYGWLLWGI
    WMWNZUVJUVEMYGGHUVNUVIMUUSYGWKSVLUVDWOZUVPWPWJWQZUUSYGUVQUVPWRWJWSWTMFGHZUU
    QUURWDXJYEYHFBUMUUCWOYGUVPXAZXBXCWNWSWTYKWLLYIWLLZUVOUULUUMWDFBXBUMUUCXDFYH
    XBUVTXEZWGYKYIIXFULWSWTUUOUAUBZYDYJUUOFUAUUPXGIUDLZUWCYJNQIXHWNVGZXIYDYJYFG
    UWEYJRBCDZIKDZYFGYIUWFGHZYJUWGGHZYIFTBCDZUCDZUWFGYHUWJGHZYIUWKGHZYHUVGFJDZU
    WJGUVHYHUWNGHZUVEUVHWAXKUVKMUVGWCHZUVHUWOWDUVRMFWCHUVLUWPFXLWEUVMFSXBVLWIWJ
    YGUVGUVPFSXBVLUVDXDWRWJWSUWNFFJDZUVBCDUWJFSOUVCUVDVNUWQTUVBBCXMVRVSVTXNUVSU
    WLUWMWDXJYHUWJFUVTTBXOUMUUCXDXBXCWNWSFTUCDZBCDUWKUWFFTBOTXOUGZUUBUUCVKUWRRB
    CUWRTFUCDRFTOUWSXPXQVTURVGXNUWAUWFWLLUVOUWHUWIWDUWBRBXRUMUUCXDWGYIUWFIXFULW
    SUWFYQKDZUWGYFYQIUWFKUUKUTYFIEZBCDZRBKDZBCDZUWTUWDYSYTYFUXBNQUUBUUCIBVJULUX
    CUXABCBRKDZEUXCUXABRUUBRXRUGZUNUXEIUXEINUUGBNBUUGVBYBBRIUUBUXFQXSVEVFVGURRU
    DLYSUUAUXDUWTNUXFUUBUUDRBBUKULXTVGXNYAUJ $.
    $( [19-Jan-2008] $)

  $( The sine of a positive real number less than or equal to 1 is positive.
     (Contributed by Paul Chapman, 19-Jan-2008.) $)
  sin01gt0 $p |- ( A e. ( 0 (,] 1 ) -> 0 < ( sin ` A ) ) $=
    ( cc0 c1 co wcel c3 cexp cdiv clt wbr cr cle w3a 0re 1re syl wi wa syl11anc
    c2 cioc cmin cfv cn0 wb elioc2 mp2an simp1bi 3nn nnnn0i reexpcl sylancl wne
    csin 3re nnne0i redivcl mp3an23 expgt0 mp3an2 3adant3 sylbi lt01 3pos caddc
    2pos ltadd1i mpbi ax-1cn addid2i eqcomi 3brtr3i ltdiv2 mpbii expcom mp3an12
    2re df-3 com12 sylc cc wceq recnd div1 breqtrd 1nn0 expword2i mp3an3 3impib
    ltletrd posdif mpbid sin01bnd simpld resubcl resincl axlttrn mp3an1 mp2and
    exp1 ) ABCUADEZBAAFGDZFHDZUBDZIJZXDAUNUCZIJZBXFIJZXAXCAIJZXEXAXCXBAXAXBKEZX
    CKEZXAAKEZFUDEZXJXAXLBAIJZACLJZBKEZCKEZXAXLXNXOMZUENOBCAUFUGZUHZFUIUJZAFUKU
    LZXJFKEZFBUMXKUOFUIUPXBFUQURPZYBXTXAXCXBCHDZXBIXAXJBXBIJZXCYEIJZYBXAXRYFXSX
    LXNYFXOXLXMXNYFYAAFUSUTVAVBXQYCXJYFYGQOUOYFXQYCXJMZYGBCIJZBFIJZYFYHYGQVCVDY
    HYIYJYFMZYGYHYKRCFIJZYGBCVEDZTCVEDZCFIBTIJYMYNIJVFBTCNVQOVGVHCVIVJFYNVRVKVL
    ZCFXBVMVNVOVPVSVPVTXAXBWAEYEXBWBXAXBYBWCXBWDPWEXAXBACGDZALXAXRXBYPLJZXSXLXN
    XOYQXLCUDEZXMXNXORZYQQWFYAYSXLYRXMMZYQXNXOYLYTYQQYOYTXNXOYLMYQACFWGVOWHVSUR
    WIVBXAAWAEYPAWBXAAXTWCAWTPWEWJXAXKXLXIXEUEYDXTXCAWKSWLXAXGXFAIJAWMWNXAXDKEZ
    XFKEZXEXGRXHQZXAXLXKUUAXTYDAXCWOSXAXLUUBXTAWPPXPUUAUUBUUCNBXDXFWQWRSWS $.
    $( [19-Jan-2008] $)

  $( The cosine of a positive real number less than or equal to 1 is positive.
     (Contributed by Paul Chapman, 19-Jan-2008.) $)
  cos01gt0 $p |- ( A e. ( 0 (,] 1 ) -> 0 < ( cos ` A ) ) $=
    ( cc0 c1 co wcel c2 c3 cdiv cmul clt wbr cle cc cr wb 1re syl wa 3re 2re wi
    cioc cexp cmin ccos cfv w3a 0re elioc2 mp2an simp1bi resqcl recnd wne recni
    wceq 2cn 3nn nnne0i pm3.2i div12 mp3an13 cn0 2nn0 expgt0 mp3an2 sylbi caddc
    3adant3 ltp1i df-3 breqtrri ltdiv1ii dividi breqtri redivcli ltmul2 mp3an12
    3pos mpbi syl11anc mulid1 breqtrd eqbrtrd ltle mpan imdistani le2sq2 mpanr1
    mpbii sylan 3impa sq1 syl6breq redivcl mp3an23 remulcl ltletr mp3an3 mp2and
    sylancr posdif sylancl mpbid cos01bnd simpld resubcl recoscl axlttrn mp3an1
    ) ABCUBDEZBCFAFUCDZGHDZIDZUDDZJKZXOAUEUFZJKZBXQJKZXKXNCJKZXPXKXNXLJKZXLCLKZ
    XTXKXNXLFGHDZIDZXLJXKXLMEZXNYDUPZXKXLXKANEZXLNEZXKYGBAJKZACLKZBNEZCNEZXKYGY
    IYJUGZOUHPBCAUIUJZUKZAULQZUMZFMEYEGMEZGBUNZRYFUQYRYSGSUOZGURUSZUTFXLGVAVBQX
    KYDXLCIDZXLJXKYHBXLJKZYDUUBJKZYPXKYMUUCYNYGYIUUCYJYGFVCEYIUUCVDAFVEVFVIVGYH
    UUCRZYCCJKZUUDYCGGHDZCJFGJKYCUUGJKFFCVHDGJFTVJVKVLFGGTSSVSVMVTGYTUUAVNVOYCN
    EYLUUEUUFUUDOFGTSUUAVPPYCCXLVQVRWJWAXKYEUUBXLUPYQXLWBQWCWDXKXLCFUCDZCLXKYMX
    LUUHLKZYNYGYIYJUUIYGYIRYGBALKZRZYJUUIYGYIUUJYKYGYIUUJUAUHBAWEWFWGUUKYLYJUUI
    PACWHWIWKWLVGWMWNXKXNNEZYHYAYBRXTUAZXKFNEXMNEZUULTXKYHUUNYPYHGNEYSUUNSUUAXL
    GWOWPQFXMWQXAZYPUULYHYLUUMPXNXLCWRWSWAWTXKUULYLXTXPOUUOPXNCXBXCXDXKXRXQCXMU
    DDJKAXEXFXKXONEZXQNEZXPXRRXSUAZXKYLUULUUPPUUOCXNXGXAXKYGUUQYOAXHQYKUUPUUQUU
    RUHBXOXQXIXJWAWT $.
    $( [19-Jan-2008] $)

  $( The sine of a positive real number less than or equal to 2 is positive.
     (Contributed by Paul Chapman, 19-Jan-2008.) $)
  sin02gt0 $p |- ( A e. ( 0 (,] 2 ) -> 0 < ( sin ` A ) ) $=
    ( cc0 c2 co wcel cmul csin cfv clt wbr c1 cr cle wa 2re 2pos wb syl wi cc
    cioc cdiv w3a rehalfcl 3ad2ant1 divgt0 mpanr12 3adant3 pm3.2i lediv1 biimpa
    ccos mp3an23 2cn 2ne0 dividi syl6breq 3adant2 3jca 0re elioc2 mp2an 3imtr4i
    1re sin01gt0 cos01gt0 resincl recoscl axmulgt0 syl11anc mp2and remulcl mpan
    sylbi mpani 3syl mpd wceq recnd breqtrrd simp1bi wne divcan2 fveq2d breqtrd
    sin2t ) ABCUADEZBCACUBDZFDZGHZAGHIWGBCWHGHZWHULHZFDZFDZWJIWGBWMIJZBWNIJZWGB
    WKIJZBWLIJZWOWGWHBKUADEZWQALEZBAIJZACMJZUCZWHLEZBWHIJZWHKMJZUCZWGWSXCXDXEXF
    WTXAXDXBAUDUEZWTXAXEXBWTXANCLEZBCIJZXEOPACUFUGUHWTXBXFXAWTXBNWHCCUBDZKMWTXB
    WHXKMJZWTXIXIXJNXBXLQOXIXJOPUIACCUJUMUKCUNUOUPUQURUSBLEZXIWGXCQUTOBCAVAVBZX
    MKLEWSXGQUTVDBKWHVAVBVCZWHVERWGWSWRXOWHVFRWGXDWQWRNWOSZWGXCXDXNXHVNZXDWKLEZ
    WLLEZXPWHVGZWHVHZWKWLVIVJRVKWGXDWMLEZWOWPSXQXDXRXSYBXTYAWKWLVLVJYBXJWOWPPXI
    YBXJWONWPSOCWMVIVMVOVPVQWGWHTEWJWNVRWGWHXQVSWHWFRVTWGWIAGWGATEZWIAVRZWGAWGW
    TXAXBXNWAVSYCCTECBWBYDUNUOACWCUMRWDWE $.
    $( [19-Jan-2008] $)

  $( The signs of the sine and cosine of 1.  (Contributed by Paul Chapman,
     19-Jan-2008.) $)
  sincos1sgn $p |- ( 0 < ( sin ` 1 ) /\ 0 < ( cos ` 1 ) ) $=
    ( c1 cc0 cioc co wcel csin cfv clt wbr ccos wa cr cle 1re lt01 leidi w3a wb
    0re elioc2 mp2an biimpri mp3an sin01gt0 cos01gt0 jca ax-mp ) ABACDEZBAFGHIZ
    BAJGHIZKALEZBAHIZAAMIZUHNOANPUHUKULUMQZBLEUKUHUNRSNBAATUAUBUCUHUIUJAUDAUEUF
    UG $.
    $( [19-Jan-2008] $)

  $( The signs of the sine and cosine of 2.  (Contributed by Paul Chapman,
     19-Jan-2008.) $)
  sincos2sgn $p |- ( 0 < ( sin ` 2 ) /\ ( cos ` 2 ) < 0 ) $=
    ( cc0 c2 csin cfv clt wbr ccos co wcel cr 2re 0re mp2an ax-mp cdiv cneg 9re
    wb c9 9pos cioc cle 2pos leidi w3a elioc2 biimpri mp3an sin02gt0 c1 cos2bnd
    c7 simpri recgt0ii gt0ne0ii rereccli lt0neg2 recoscl renegcli lttri pm3.2i
    mpbi ) ABCDEFZBGDZAEFZBABUAHIZVCBJIZABEFZBBUBFZVFKUCBKUDVFVGVHVIUEZAJIVGVFV
    JRLKABBUFMUGUHBUINVDUJSOHZPZEFZVLAEFZVEULSOHPVDEFVMUKUMAVKEFZVNSQTUNVKJIVOV
    NRSQSQTUOUPZVKUQNVBVDVLAVGVDJIKBURNVKVPUSLUTMVA $.
    $( [19-Jan-2008] $)

  $( The sine of 4 is negative.  (Contributed by Paul Chapman, 19-Jan-2008.) $)
  sin4lt0 $p |- ( sin ` 4 ) < 0 $=
    ( c4 csin cfv c2 cmul co cc0 clt wcel 2cn ax-mp wbr sincos2sgn cr wa wb 2re
    0re pm3.2i ltmul2 ccos 2t2e4 fveq2i wceq sin2t eqtr3i simpri recoscl simpli
    cc resincl mp3an mpbi recni mul01i breqtri remulcli 2pos eqbrtri ) ABCZDDBC
    ZDUACZEFZEFZGHDDEFZBCZUTVDVEABUBUCDUJIVFVDUDJDUEKUFVDDGEFZGHVCGHLZVDVGHLZVC
    VAGEFZGHVBGHLZVCVJHLZGVAHLZVKMUGVBNIZGNIZVANIZVMOVKVLPDNIZVNQDUHKZRVPVMVQVP
    QDUKKZVMVKMUISVBGVATULUMVAVAVSUNUOUPVCNIVOVQGDHLZOVHVIPVAVBVSVRUQRVQVTQURSV
    CGDTULUMDJUOUPUS $.
    $( [19-Jan-2008] $)

  $( The absolute value of the exponential function of an imaginary number is
     one.  Equation 48 of [Rudin] p. 167.  (Contributed by Jason Orendorff,
     9-Feb-2007.) $)
  absefi $p |- ( A e. RR -> ( abs ` ( exp ` ( _i x. A ) ) ) = 1 ) $=
    ( cr wcel ci cmul co cfv cabs caddc c2 cexp csqr c1 cc wceq fveq2d syl11anc
    syl resqcl recnd ccos csin recn efival recoscl resincl absreim addcom eqtrd
    ce sincossq sqr1 syl6eq 3eqtrd ) ABCZDAEFUJGZHGAUAGZDAUBGZEFIFZHGZUQJKFZURJ
    KFZIFZLGZMUOUPUSHUOANCZUPUSOAUCZAUDRPUOUQBCZURBCZUTVDOAUEZAUFZUQURUGQUOVDML
    GMUOVCMLUOVCVBVAIFZMUOVANCVBNCVCVKOUOVAUOVGVABCVIUQSRTUOVBUOVHVBBCVJURSRTVA
    VBUHQUOVEVKMOVFAUKRUIPULUMUN $.
    $( [9-Feb-2007] $)

  $( The absolute value of the exponential function is the exponential function
     of the real part.  (Contributed by Paul Chapman, 13-Sep-2007.) $)
  absef $p |- ( A e. CC -> ( abs ` ( exp ` A ) ) = ( exp ` ( Re ` A ) ) ) $=
    ( cc wcel ce cfv cabs c1 cmul co ci wceq fveq2 syl cr recn sylancr syl11anc
    eqtrd cc0 wbr cre cim caddc replim recl ax-icn imcl mulcl efadd reefcl efcl
    absmul absefi opreq2 abscl 3syl mulid1 cle clt efgt0 0reALT ltle mpd absid
    wi ) ABCZADEZFEZAUAEZDEZFEZVJVFVHVKGHIZVKVFVHVKJAUBEZHIZDEZFEZHIZVLVFVHVJVO
    HIZFEZVQVFVGVRKVHVSKVFVGVIVNUCIZDEZVRVFAVTKVGWAKAUDAVTDLMVFVIBCZVNBCZWAVRKV
    FVINCZWBAUEZVIOMVFJBCVMBCZWCUFVFVMNCZWFAUGZVMOMJVMUHPZVIVNUIQRVGVRFLMVFVJBC
    ZVOBCZVSVQKVFVJNCZWJVFWDWLWEVIUJMZVJOMZVFWCWKWIVNUKMVJVOULQRVFVPGKZVQVLKVFW
    GWOWHVMUMMVPGVKHUNMRVFVKBCZVLVKKVFWJVKNCWPWNVJUOVKOUPVKUQMRVFWLSVJURTZVKVJK
    WMVFSVJUSTZWQVFWDWRWEVIUTMVFSNCWLWRWQVEVAWMSVJVBPVCVJVDQR $.
    $( [13-Sep-2007] $)

  $( A number is real iff its imaginary exponential has absolute value one. $)
  absefib $p |- ( A e. CC
          -> ( A e. RR <-> ( abs ` ( exp ` ( _i x. A ) ) ) = 1 ) ) $=
    ( cc wcel ci cmul co ce cfv c1 wceq cneg cr cre ax-icn mulcl caddc syl11anc
    syl eqtrd cc0 cabs mpan absef replim recl recnd imcl sylancr addcom opreq2d
    cim adddi mp3an1 mulass mp3an12 opreq1i syl5eq eqtr3d opreq1d 3eqtrd fveq2d
    ixi mulm1 renegcl eqeq1d wb negeq0 reim0b ef0 eqeq2i reef11 sylancl syl5bbr
    crre 0re 3bitr4rd bitr2d ) ABCZDAEFZGHUAHZIJAUKHZKZGHZIJZALCZVRVTWCIVRVTVSM
    HZGHZWCVRVSBCZVTWGJDBCZVRWHNDAOUBVSUCRVRWFWBGVRWFWBDAMHZEFZPFZMHZWBVRVSWLMV
    RVSDDWAEFZWJPFZEFZDWNEFZWKPFZWLVRAWODEVRAWJWNPFZWOAUDVRWJBCZWNBCZWSWOJVRWJA
    UEZUFZVRWIWABCZXANVRWAAUGZUFZDWAOUHZWJWNUIQSUJVRXAWTWPWRJZXGXCWIXAWTXHNDWNW
    JULUMQVRWQWBWKPVRDDEFZWAEFZWQWBVRXDXJWQJZXFWIWIXDXKNNDDWAUNUORVRXJIKZWAEFZW
    BXIXLWAEVBUPVRXDXMWBJXFWAVCRUQURUSUTVAVRWBLCZWJLCWMWBJVRWALCXNXEWAVDRZXBWBW
    JVNQSVASVEVRWATJZWBTJZWEWDVRXDXPXQVFXFWAVGRAVHWDWCTGHZJZVRXQXRIWCVIVJVRXNTL
    CXSXQVFXOVOWBTVKVLVMVPVQ $.
    $( [25-Mar-2009] $) $( [21-Aug-2008] $)

  $( MAY BE REVISED - make obsolete $)
  $( A number whose imaginary exponential is one is real. $)
  efieq1re $p |- ( ( A e. CC /\ ( exp ` ( _i x. A ) ) = 1 ) -> A e. RR ) $=
    ( cc wcel ci cmul co ce cfv c1 wceq cr cabs caddc recnd ax-icn syl11anc syl
    cc0 wbr 0re cim cneg cre replim opreq2d recl imcl mulcl adddi mp3an1 mulass
    sylancr mp3an12 ixi opreq1i mulm1 syl5eq eqtr3d 3eqtrd fveq2d renegcl efadd
    eqtrd eqeq1d efcl absmul absefi cle reefcl efgt0 wi ltle mpan sylc opreq12d
    clt absid mulid2 3eqtrrd fveq2 sylan9eq ex sylbid wb negeq0 reim0b ef0 lt01
    1re ltleii absidi eqtr4i eqeq2i reef11 sylancl syl5bbr 3bitr4rd sylibd imp
    ax-mp ) ABCZDAEFZGHZIJZAKCZXAXDAUAHZUBZGHZILHZJZXEXAXDDAUCHZEFZGHZXHEFZIJZX
    JXAXCXNIXAXCXLXGMFZGHZXNXAXBXPGXAXBDXKDXFEFZMFZEFZXLDXREFZMFZXPXAAXSDEAUDUE
    XAXKBCZXRBCZXTYBJZXAXKAUFZNZXADBCZXFBCZYDOXAXFAUGZNZDXFUHULYHYCYDYEODXKXRUI
    UJPXAYAXGXLMXADDEFZXFEFZYAXGXAYIYMYAJZYKYHYHYIYNOODDXFUKUMQXAYMIUBZXFEFZXGY
    LYOXFEUNUOXAYIYPXGJYKXFUPQUQURUEUSUTXAXLBCZXGBCZXQXNJXAYHYCYQOYGDXKUHULZXAX
    GXAXFKCXGKCZYJXFVAQZNZXLXGVBPVCVDXAXOXJXAXOXHXNLHZXIXAUUCXMLHZXHLHZEFZIXHEF
    ZXHXAXMBCZXHBCZUUCUUFJXAYQUUHYSXLVEQXAYRUUIUUBXGVEQZXMXHVFPXAUUDIUUEXHEXAXK
    KCUUDIJYFXKVGQXAXHKCZRXHVHSZUUEXHJXAYTUUKUUAXGVIQZXAUUKRXHVPSZUULUUMXAYTUUN
    UUAXGVJQRKCZUUKUUNUULVKTRXHVLVMVNXHVQPVOXAUUIUUGXHJUUJXHVRQVSXNILVTWAWBWCXA
    XFRJZXGRJZXEXJXAYIUUPUUQWDYKXFWEQAWFXJXHRGHZJZXAUUQUURXIXHUURIXIWGRIVHSXIIJ
    RITWIWHWJIWIWKWTWLWMXAYTUUOUUSUUQWDUUATXGRWNWOWPWQWRWS $.
    $( [25-Mar-2009] $) $( [21-Aug-2008] $)

  ${
    $d x k $.  $d x N $.  $d A x $.  $d A k $.
    $( De Moivre's Formula.  Proof by induction given at
       ~ http://en.wikipedia.org/wiki/De&#x5F;Moivre&#x27;s&#x5F;formula , but
       restricted to nonnegative integer powers.  (Contributed by Steve
       Rodriguez, 10-Nov-2006.) $)
    demoivre $p |- ( ( A e. CC /\ N e. NN0 ) ->
                  ( ( ( cos ` A ) + ( _i x. ( sin ` A ) ) ) ^ N ) =
                 ( ( cos ` ( N x. A ) ) + ( _i x. ( sin ` ( N x. A ) ) ) ) ) $=
      ( wcel cc ccos cfv ci csin cmul co caddc cexp wceq cc0 c1 fveq2d opreq12d
      opreq2d ax-icn syl11anc vx vk cn0 cv wi opreq2 opreq1 eqeq12d coscl sincl
      imbi2d mulcl sylancr addcl exp0 syl mul02 cos0 syl6eq sin0 mul01i addid1i
      ax-1cn eqtr4d wa expp1 sylan ancoms adantr adantl sinadd sylancom opreq1d
      nn0cn mulcom addcom 3eqtr2d w3a adddir mulid2 3ad2ant3 eqtrd syl3an1 cmin
      mp3an2 cneg mpan 3syl jca muladd syl21anc jctil mul4 opreq1i mul12 mp3an1
      ixi adddi 3eqtrd mulm1 negsub cosadd 3eqtr4rd exp31 a2d nn0ind impcom ) B
      UCCADCZAEFZGAHFZIJZKJZBLJZBAIJZEFZGXNHFZIJZKJZMZXHXLUAUDZLJZXTAIJZEFZGYBH
      FZIJZKJZMZUEXHXLNLJZNAIJZEFZGYIHFZIJZKJZMZUEXHXLUBUDZLJZYOAIJZEFZGYQHFZIJ
      ZKJZMZUEXHXLYOOKJZLJZUUCAIJZEFZGUUEHFZIJZKJZMZUEXHXSUEUAUBBXTNMZYGYNXHUUK
      YAYHYFYMXTNXLLUFUUKYCYJYEYLKUUKYBYIEXTNAIUGZPUUKYDYKGIUUKYBYIHUULPRQUHUKX
      TYOMZYGUUBXHUUMYAYPYFUUAXTYOXLLUFUUMYCYRYEYTKUUMYBYQEXTYOAIUGZPUUMYDYSGIU
      UMYBYQHUUNPRQUHUKXTUUCMZYGUUJXHUUOYAUUDYFUUIXTUUCXLLUFUUOYCUUFYEUUHKUUOYB
      UUEEXTUUCAIUGZPUUOYDUUGGIUUOYBUUEHUUPPRQUHUKXTBMZYGXSXHUUQYAXMYFXRXTBXLLU
      FUUQYCXOYEXQKUUQYBXNEXTBAIUGZPUUQYDXPGIUUQYBXNHUURPRQUHUKXHYHOYMXHXLDCZYH
      OMXHXIDCZXKDCZUUSAUIZXHGDCZXJDCZUVASAUJZGXJULUMZXIXKUNTZXLUOUPXHYMONKJOXH
      YJOYLNKXHYJNEFOXHYINEAUQZPURUSXHYLGNIJNXHYKNGIXHYKNHFNXHYINHUVHPUTUSRGSVA
      USQOVCVBUSVDYOUCCZXHUUBUUJUVIXHUUBUUJUVIXHVEZUUBVEUUDYPXLIJZUUAXLIJZUUIUV
      JUUDUVKMZUUBXHUVIUVMXHUUSUVIUVMUVGXLYOVFVGVHVIUUBUVKUVLMUVJYPUUAXLIUGVJUV
      JUVLUUIMUUBUVJYQAKJZEFZGUVNHFZIJZKJUVOGYRXJIJZXIYSIJZKJZIJZKJZUUIUVLUVJUV
      QUWAUVOKUVJUVPUVTGIUVJUVPYSXIIJZUVRKJZUVSUVRKJZUVTUVIXHYQDCZUVPUWDMUVIYOD
      CZXHUWFYOVNZYOAULVGZYQAVKVLUVJUVSUWCUVRKUVJUUTYSDCZUVSUWCMXHUUTUVIUVBVJZU
      VJUWFUWJUWIYQUJZUPZXIYSVOTVMUVJUVSDCZUVRDCZUWEUVTMUVJUUTUWJUWNUWKUWMXIYSU
      LTZUVJYRDCZUVDUWOUVJUWFUWQUWIYQUIUPZXHUVDUVIUVEVJZYRXJULTZUVSUVRVPTVQRRUV
      JUUFUVOUUHUVQKUVJUUEUVNEUVIODCZXHUUEUVNMZVCUVIUWGUXAXHUXBUWHUWGUXAXHVRUUE
      YQOAIJZKJZUVNYOOAVSXHUWGUXDUVNMUXAXHUXCAYQKAVTRWAWBWCWEZPUVJUUGUVPGIUVJUU
      EUVNHUXEPRQUVJUVLYRXIIJZXJYSIJZWDJZUWAKJZUWBUVJUVLUXFOWFZUXGIJZKJZUWAKJZU
      XFUXGWFZKJZUWAKJUXIUVJUVLUXFXKYTIJZKJZYRXKIJZXIYTIJZKJZKJZUXLUXTKJUXMUVJU
      WQYTDCZUUTUVAVEZUVLUYAMUWRUVJUWFUWJUYBUWIUWLUVCUWJUYBSGYSULWGWHXHUYCUVIXH
      UUTUVAUVBUVFWIVJYRYTXIXKWJWKUVJUXQUXLUXTKUVJUXPUXKUXFKUVJUVCUVDVEZUVCUWJV
      EZUXPUXKMUVJUVDUVCUWSSWLUVJUWJUVCUWMSWLUYDUYEVEUXPGGIJZUXGIJUXKGXJGYSWMUY
      FUXJUXGIWQWNUSTRVMUVJUXTUWAUXLKUVJUXTGUVRIJZGUVSIJZKJZUWAUVJUXRUYGUXSUYHK
      UVJUWQUVDUXRUYGMZUWRUWSUWQUVCUVDUYJSYRGXJWOWETUVJUUTUWJUXSUYHMZUWKUWMUUTU
      VCUWJUYKSXIGYSWOWETQUVJUWOUWNUWAUYIMZUWTUWPUVCUWOUWNUYLSGUVRUVSWRWPTVDRWS
      UVJUXLUXOUWAKUVJUXKUXNUXFKUVJUXGDCZUXKUXNMUVJUVDUWJUYMUWSUWMXJYSULTZUXGWT
      UPRVMUVJUXOUXHUWAKUVJUXFDCZUYMUXOUXHMUVJUWQUUTUYOUWRUWKYRXIULTUYNUXFUXGXA
      TVMWSUVJUVOUXHUWAKUVJUVOUXFYSXJIJZWDJZUXHUVIXHUWFUVOUYQMUWIYQAXBVLUVJUYPU
      XGUXFWDUVJUWJUVDUYPUXGMUWMUWSYSXJVOTRWBVMVDXCVIWSXDXEXFXG $.
      $( [9-Nov-2006] $)

    $( Shorter proof of ~ demoivre using the exponential function. $)
    demoivreALT $p |- ( ( A e. CC /\ N e. NN0 ) ->
                  ( ( ( cos ` A ) + ( _i x. ( sin ` A ) ) ) ^ N ) =
                 ( ( cos ` ( N x. A ) ) + ( _i x. ( sin ` ( N x. A ) ) ) ) ) $=
      ( cc wcel cn0 wa ci cmul co ce cfv cexp ccos csin caddc wceq ax-icn mulcl
      mpan efival efexp sylan nn0cn mul12 mp3an2 fveq2d syl eqtrd ancoms sylan2
      opreq1d adantr 3eqtr3rd ) ACDZBEDZFBGAHIZHIZJKZUPJKZBLIZBAHIZMKGVANKHIOIZ
      AMKGANKHIOIZBLIZUNUPCDZUOURUTPGCDZUNVEQGARSUPBUAUBUOUNBCDZURVBPZBUCVGUNVH
      VGUNFZURGVAHIZJKZVBVIUQVJJVGVFUNUQVJPQBGAUDUEUFVIVACDVKVBPBARVATUGUHUIUJU
      NUTVDPUOUNUSVCBLATUKULUM $.
      $( [24-Jul-2007] $)
  $}

