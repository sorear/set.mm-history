$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The exponential, sine, and cosine functions (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d k n A $.  $d k F $.  $d k n M $.  $d k n N $.  $d k ph $.
    efsep.1 $e |- F = ( n e. NN0 |-> ( ( A ^ n ) / ( ! ` n ) ) ) $.
    efsep.2 $e |- N = ( M + 1 ) $.
    efsep.3 $e |- M e. NN0 $.
    efsep.4 $e |- ( ph -> A e. CC ) $.
    efsep.5 $e |- ( ph -> B e. CC ) $.
    efsep.6 $e |- ( ph -> ( exp ` A ) =
                    ( B + sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) ) $.
    efsep.7 $e |- ( ph -> ( B + ( ( A ^ M ) / ( ! ` M ) ) ) = D ) $.
    $( Separate out the next term of the power series expansion of the
       exponential function.  The last hypothesis allows the separated terms to
       be rearranged as desired.  (Contributed by Paul Chapman, 23-Nov-2007.)
       (Revised by Mario Carneiro, 29-Apr-2014.) $)
    efsep $p |- ( ph -> ( exp ` A ) =
                  ( D + sum_ k e. ( ZZ>= ` N ) ( F ` k ) ) ) $=
      ( cfv caddc co wcel ce cuz cv csu cexp cfa cdiv c1 eqid cz nn0zi wa eqidd
      a1i cn0 cc eluznn0 mpan wceq eftval adantl eftcl sylan eqeltrd sylan2 cli
      cseq eftlcvg sylancl isum1p eqcomi fveq2i sumeq1i opreq12i syl6eq opreq2d
      cdm ax-mp peano2nn0 eqeltri eftlcl addass syl3anc opreq1d 3eqtr2rd eqtr4d
      ) ABUAQCHUBQZEUCZGQZEUDZRSZDIUBQZWIEUDZRSZOAWKCBHUESHUFQUGSZWMRSZRSZCWORS
      ZWMRSZWNAWJWPCRAWJHGQZHUHRSZUBQZWIEUDZRSWPAWIEGHWGWGUIHUJTAHLUKUNAWHWGTZU
      LWIUMXDAWHUOTZWIUPTHUOTZXDXELWHHUQURAXEULWIBWHUESWHUFQUGSZUPXEWIXGUSABFGW
      HJUTVAABUPTZXEXGUPTMBWHVBVCVDVEAXHXFRGHVGVFVQTMLBFGHJVHVIVJWTWOXCWMRXFWTW
      OUSLBFGHJUTVRXBWLWIEXAIUBIXAKVKVLVMVNVOVPACUPTWOUPTZWMUPTZWSWQUSNAXHXFXIM
      LBHVBVIAXHIUOTXJMIXAUOKXFXAUOTLHVSVRVTBEFGIJWAVICWOWMWBWCAWRDWMRPWDWEWF
      $.
      $( [29-Apr-2014] $) $( [23-Nov-2007] $)
  $}

  ${
    $d k n A $.  $d k F $.  $d k N $.  $d k ph $.
    effsumlt.1 $e |- F = ( n e. NN0 |-> ( ( A ^ n ) / ( ! ` n ) ) ) $.
    effsumlt.2 $e |- ( ph -> A e. RR+ ) $.
    effsumlt.3 $e |- ( ph -> N e. NN0 ) $.
    $( The partial sums of the series expansion of the exponential function of
       a positive real number are bounded by the value of the function.
       (Contributed by Paul Chapman, 21-Aug-2007.)  (Revised by Mario Carneiro,
       29-Apr-2014.) $)
    effsumlt $p |- ( ph -> ( seq 0 ( + , F ) ` N ) < ( exp ` A ) ) $=
      ( vk caddc cc0 cfv c1 co csu cr wcel crp cn0 syl cseq cuz cv ce clt nn0uz
      wbr wf cz 0z a1i wa cexp cfa cdiv wceq eftval adantl rpre reeftcl eqeltrd
      sylan serfre ffvelrn syl2anc eqid peano2nn0 eqidd rpexpcl syl2an cn faccl
      nn0z nnrp rpdivcl cli cdm recnd efcllem isumrpcl ltaddrp efval2 isumsplit
      cc cfz nn0cn ax-1cn pncan sylancl opreq2d sumeq1d syl6eleq elfznn0 sylan2
      cmin fsumser eqtrd opreq1d breqtrrd ) AEJDKUAZLZXAEMJNZUBLZIUCZDLZIOZJNZB
      UDLZUEAXAPQZXFRQXAXGUEUGASPWTUHESQZXIAIDKSUFKUIQAUJUKAXDSQZULZXEBXDUMNZXD
      UNLZUONZPXKXEXOUPABCDXDFUQURZABPQZXKXOPQABRQZXQGBUSTZBXDUTVBVAZVCHSPEWTVD
      VEAXEIDKXBXCSUFXCVFZAXJXBSQHEVGTZXLXEVHZXLXEXORXPXLXMRQZXNRQZXORQAXRXDUIQ
      YDXKGXDVMBXDVIVJXLXNVKQZYEXKYFAXDVLURXNVNTXMXNVOVEVAABWDQZWTVPVQQABXSVRZB
      CDFVSTZVTXAXFWAVEAXHSXEIOZXGAYGXHYJUPYHBICDFWBTAYJKXBMWONZWENZXEIOZXFJNXG
      AXEIDKXBXCSUFYAYBYCXLXEXTVRZYIWCAYMXAXFJAYMKEWENZXEIOXAAYLYOXEIAYKEKWEAEW
      DQZMWDQYKEUPAXJYPHEWFTWGEMWHWIWJWKAXEIDKEAXDYOQZULXEVHAESKUBLHUFWLYQAXKXE
      WDQXDEWMYNWNWPWQWRWQWQWS $.
      $( [29-Apr-2014] $) $( [21-Aug-2007] $)
  $}

  $( The value of the first term of the series expansion of the exponential
     function is 1.  (Contributed by Paul Chapman, 21-Aug-2007.)  (Revised by
     Mario Carneiro, 29-Apr-2014.) $)
  eft0val $p |- ( A e. CC -> ( ( A ^ 0 ) / ( ! ` 0 ) ) = 1 ) $=
    ( cc wcel cc0 cexp co cfa cfv cdiv c1 exp0 opreq1d fac0 opreq2i div1i eqtri
    ax-1cn syl6eq ) ABCZADEFZDGHZIFJUAIFZJSTJUAIAKLUBJJIFJUAJJIMNJQOPR $.
    $( [29-Apr-2014] $) $( [21-Aug-2007] $)

  ${
    $d k n A $.  $d k F $.
    ef4p.1 $e |- F = ( n e. NN0 |-> ( ( A ^ n ) / ( ! ` n ) ) ) $.
    $( Separate out the first four terms of the infinite series expansion of
       the exponential function.  (Contributed by Paul Chapman,
       19-Jan-2008.) $)
    ef4p $p |- ( A e. CC -> ( exp ` A ) =
                ( ( ( ( 1 + A ) + ( ( A ^ 2 ) / 2 ) ) + ( ( A ^ 3 ) / 6 ) ) +
                  sum_ k e. ( ZZ>= ` 4 ) ( F ` k ) ) ) $=
      ( cc wcel c1 caddc co c2 cexp cdiv c3 a1i cc0 cfv wceq cfa efsep c6 c4 id
      df-4 3nn0 ax-1cn mpan sqcl halfcl syl syl2anc df-3 2nn0 df-2 1nn0 addid2i
      addcl eqcomi 0nn0 0cn cuz cv csu cn0 efval2 nn0uz sumeq1i syl6req opreq2d
      ce efcl addid2 eqtr2d eft0val syl6eq exp1 fac1 opreq12d div1 fac2 opreq2i
      eqtrd fac3 ) AFGZAHAIJZAKLJZKMJZIJZWHANLJZUAMJZIJZBCDNUBEUDUEWDUCZWDWEFGZ
      WGFGZWHFGHFGZWDWMUFHAUQUGZWDWFFGWNAUHWFUIUJWEWGUQUKWDAWEWHBCDKNEULUMWLWPW
      DAHWEBCDHKEUNUOWLWOWDUFOWDAPHBCDPHEPHIJZHHUFUPZURUSWLPFGWDUTOWDPPVAQZBVBD
      QZBVCZIJPAVJQZIJZXBWDXAXBPIWDXBVDWTBVCXAABCDEVEVDWSWTBVFVGVHVIWDXBFGXCXBR
      AVKXBVLUJVMWDPAPLJPSQMJZIJWQHWDXDHPIAVNVIWRVOTWDAHLJZHSQZMJZAHIWDXGAHMJAW
      DXEAXFHMAVPXFHRWDVQOVRAVSWBVITWEWFKSQZMJZIJWHRWDXIWGWEIXHKWFMVTWAWAOTWHWI
      NSQZMJZIJWKRWDXKWJWHIXJUAWIMWCWAWAOT $.
      $( [29-Apr-2014] $) $( [19-Jan-2008] $)
  $}

  ${
    $d A n $.
    $( The exponential function of a positive real number is greater than 1
       plus that number.  (Contributed by Mario Carneiro, 14-Mar-2014.) $)
    efgt1p $p |- ( A e. RR+ -> ( 1 + A ) < ( exp ` A ) ) $=
      ( vn crp wcel c1 caddc cn0 cv cexp co cfa cfv cdiv wceq 0nn0 eftval ax-mp
      cc0 syl5eq 1nn0 cmpt cseq ce clt cc rpcn nn0uz ax-1cn addid2i eqcomi eqid
      0z eft0val seq1i fac1 opreq2i exp1 opreq1d div1 eqtrd seqp1i syl effsumlt
      id a1i eqbrtrrd ) ACDZEFBGABHZIJVHKLMJUAZRUBLZEAFJZAUCLUDVGAUEDZVJVKNAUFV
      LEAFVIERRGUGOREFJEEUHUIUJVLEFVIRULVLRVILZARIJRKLMJZERGDVMVNNOABVIRVIUKZPQ
      AUMSUNVLEVILZAEIJZEKLZMJZAEGDZVPVSNTABVIEVOPQVLVSVQEMJZAVREVQMUOUPVLWAAEM
      JAVLVQAEMAUQURAUSUTSSVAVBVGABVIEVOVGVDVTVGTVEVCVF $.
      $( [30-Apr-2014] $) $( [14-Mar-2014] $)

    $( The exponential function of a positive real number is greater than 1.
       (Contributed by Paul Chapman, 21-Aug-2007.)  (Revised by Mario Carneiro,
       30-Apr-2014.) $)
    efgt1 $p |- ( A e. RR+ -> 1 < ( exp ` A ) ) $=
      ( crp wcel c1 caddc co ce cfv 1re a1i rpre readdcl sylancr reefcl syl clt
      cr wbr ltaddrp mpan efgt1p lttrd ) ABCZDDAEFZAGHZDQCZUCIJUCUFAQCZUDQCIAKZ
      DALMUCUGUEQCUHANOUFUCDUDPRIDASTAUAUB $.
      $( [30-Apr-2014] $) $( [21-Aug-2007] $)
  $}

  ${
    eflt.1 $e |- ( ph -> A e. RR ) $.
    eflt.2 $e |- ( ph -> B e. RR ) $.
    $( The exponential function on the reals is strictly monotonic.
       (Contributed by Paul Chapman, 21-Aug-2007.)  (Revised by Mario Carneiro,
       30-Apr-2014.) $)
    eflti $p |- ( ph -> ( A < B -> ( exp ` A ) < ( exp ` B ) ) ) $=
      ( clt wbr ce cfv co wcel cr cc0 adantr syl2anc wb syl reefcl cc recnd crp
      wa cmin cmul c1 resubcl posdif biimpa elrp sylanbrc efgt1 efgt0 ltmulgt11
      syl3anc mpbid caddc wceq efadd pncan3 fveq2d eqtr3d breqtrd ex ) ABCFGZBH
      IZCHIZFGAVDUBZVEVECBUCJZHIZUDJZVFFVGUEVIFGZVEVJFGZVGVHUAKZVKVGVHLKZMVHFGZ
      VMVGCLKZBLKZVNAVPVDENZAVQVDDNZCBUFOZAVDVOAVQVPVDVOPDEBCUGOUHVHUIUJVHUKQVG
      VELKZVILKZMVEFGZVKVLPVGVQWAVSBRQVGVNWBVTVHRQVGVQWCVSBULQVEVIUMUNUOVGBVHUP
      JZHIZVJVFVGBSKZVHSKWEVJUQVGBVSTZVGVHVTTBVHUROVGWDCHVGWFCSKWDCUQWGVGCVRTBC
      USOUTVAVBVC $.
      $( [21-Aug-2007] $)

  $}

  $( The exponential function on the reals is strictly monotonic.  (Contributed
     by Mario Carneiro, 11-Mar-2014.) $)
  eflt $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A < B <-> ( exp ` A ) < ( exp ` B ) ) ) $=
    ( cr wcel wa clt wbr ce cfv simpl simpr eflti wo wn wi fveq2 reefcl axlttri
    wceq a1i orim12d con3d wb syl2an 3imtr4d impbid ) ACDZBCDZEZABFGZAHIZBHIZFG
    ZUIABUGUHJZUGUHKZLUIUKULSZULUKFGZMZNZABSZBAFGZMZNUMUJUIVBURUIUTUPVAUQUTUPOU
    IABHPTUIBAUOUNLUAUBUGUKCDULCDUMUSUCUHAQBQUKULRUDABRUEUF $.
    $( [11-Mar-2014] $)

  $( The exponential function on the reals is strictly monotonic.  (Contributed
     by Mario Carneiro, 11-Mar-2014.) $)
  efle $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A <_ B <-> ( exp ` A ) <_ ( exp ` B ) ) ) $=
    ( cr wcel wa clt wbr wn ce cfv cle eflt ancoms notbid reefcl syl2an 3bitr4d
    wb lenlt ) ACDZBCDZEZBAFGZHBIJZAIJZFGZHZABKGUEUDKGZUBUCUFUATUCUFRBALMNABSTU
    ECDUDCDUHUGRUAAOBOUEUDSPQ $.
    $( [11-Mar-2014] $)

  $( The exponential function on real numbers is one-to-one. $)
  reef11 $p |- ( ( A e. RR /\ B e. RR )
        -> ( ( exp ` A ) = ( exp ` B ) <-> A = B ) ) $=
    ( cr wcel wa cle wbr ce cfv wceq efle ancoms anbi12d letri3 reefcl 3bitr4rd
    wb syl2an ) ACDZBCDZEZABFGZBAFGZEAHIZBHIZFGZUEUDFGZEZABJUDUEJZUAUBUFUCUGABK
    TSUCUGQBAKLMABNSUDCDUECDUIUHQTAOBOUDUENRP $.
    $( [11-Mar-2014] $) $( [21-Aug-2008] $)

  ${
    $d x y $.
    $( The exponential function maps real arguments one-to-one to positive
       reals.  (Contributed by Steve Rodriguez, 25-Aug-2007.) $)
    reeff1 $p |- ( exp |` RR ) : RR -1-1-> RR+ $=
      ( vx vy cr crp ce cres wf1 wf cv cfv wceq wi wral dff13 wfn wcel ffnfv cc
      fvres mpbir2an wss eff ffn ax-mp ax-resscn fnssres rpefcl eqeltrd rgen wa
      mp2an eqeqan12d reef11 biimpd sylbid rgen2a ) CDECFZGCDUQHZAIZUQJZBIZUQJZ
      KZUSVAKZLZBCMACMABCDUQNURUQCOZUTDPZACMACDUQQEROZCRUAVFRREHVHUBRREUCUDUERC
      EUFUKVGACUSCPZUTUSEJZDUSCESZUSUGUHUITVEABCVIVACPZUJZVCVJVAEJZKZVDVIVLUTVJ
      VBVNVKVACESULVMVOVDUSVAUMUNUOUPT $.
      $( [10-Nov-2013] $) $( [25-Aug-2007] $)
  $}

  ${
    $d A k n $.  $d k ph $.
    eflegeo.1 $e |- ( ph -> A e. RR ) $.
    eflegeo.2 $e |- ( ph -> 0 <_ A ) $.
    eflegeo.3 $e |- ( ph -> A < 1 ) $.
    $( The exponential function on the reals between 0 and 1 lies below the
       comparable geometric series sum.  (Contributed by Paul Chapman,
       11-Sep-2007.) $)
    eflegeo $p |- ( ph -> ( exp ` A ) <_ ( 1 / ( 1 - A ) ) ) $=
      ( vk vn cn0 cexp co cfv cdiv c1 cle cc0 wcel wceq cr wbr syl cfa csu cmin
      cv ce cmpt nn0uz cz 0z a1i eftval adantl reeftcl sylan opreq2 oprex fvmpt
      eqid reexpcl wa cmul cn faccl adantr simpr expge0 syl3anc nnge1 lemulge12
      nnre syl22anc clt wb nngt0 ledivmul syl112anc mpbird caddc cseq cli recnd
      cc cdm efcllem cabs absid syl2anc eqbrtrd geolim seqex breldm isumcmp cvv
      efval expcl isumclim eqcomd 3brtr4d ) AHBFUDZIJZWSUAKZLJZFUBZHWTFUBZBUEKZ
      MMBUCJZLJZNAXBWTFGHBGUDZIJZXHUAKLJUFZGHXIUFZOHUGOUHPAUIUJZWSHPZWSXJKXBQAB
      GXJWSXJURZUKULABRPZXMXBRPCBWSUMUNXMWSXKKWTQAGWSXIWTHXKXHWSBIUOXKURBWSIUPU
      QULZAXOXMWTRPZCBWSUSUNZAXMUTZXBWTNSZWTXAWTVAJNSZXSXQXARPZOWTNSZMXANSZYAXR
      XSXAVBPZYBXMYEAWSVCULZXAVJTZXSXOXMOBNSZYCAXOXMCVDAXMVEAYHXMDVDBWSVFVGXSYE
      YDYFXAVHTWTXAVIVKXSXQXQYBOXAVLSZXTYAVMXRXRYGXSYEYIYFXAVNTWTWTXAVOVPVQABWB
      PZVRXJOVSVTWCZPABCWAZBGXJXNWDTAVRXKOVSZXGVTSYMYKPABFXKYLABWEKZBMVLAXOYHYN
      BQCDBWFWGEWHXPWIZYMXGVTVRXKOWJWKTWLAYJXEXCQYLBFWNTAXDXGAWTXGFXKOWMHUGXLXP
      AYJXMWTWBPYLBWSWOUNXGWMPAMXFLUPUJYOWPWQWR $.
      $( [11-Sep-2007] $)
  $}

  ${
    $d k n A $.  $d k n B $.  $d k ph $.
    efcnlem.1 $e |- ( ph -> A e. CC ) $.
    efcnlem.2 $e |- ( ph -> B e. CC ) $.
    efcnlem.3 $e |- ( ph -> R e. RR+ ) $.
    efcnlem.4 $e |- ( ph -> ( abs ` ( A - B ) ) <
                      ( R / ( 2 x. ( abs ` ( exp ` A ) ) ) ) ) $.
    efcnlem.5 $e |- ( ph -> ( abs ` ( A - B ) ) < 1 ) $.
    $( Lemma for ~ efcn . $)
    efcnlem $p |- ( ph -> ( abs ` ( ( exp ` A ) - ( exp ` B ) ) ) < R ) $=
      ( cfv co c2 cmul cc wcel cr syl cc0 c1 wceq vk vn ce cmin cabs efcl subcl
      syl2anc abscl crp cn 2nn nnrp ax-mp efne0 absrpcl rpmulcl sylancr remulcl
      wne rpre cle wbr ax-1cn sylancl 2re absge0 jca cuz cn0 cexp cfa cdiv cmpt
      wa csu caddc eqid 1nn a1i abssub clt 1re ltle mpd eqbrtrrd eftlub addid2i
      cv wi eqcomi 0nn0 0cn addid2 efval2 sumeq1i syl6eq opreq2d eqtr3d eft0val
      nn0uz efsep eqcomd 1nn0 eftlcl subadd syl3anc mpbird fveq2d opreq1d recnd
      wb exp1 df-2 fac1 opreq1i mulid1i eqtr2i opreq12i 2cn div1i eqtr3i eqtr4d
      opreq12d 3brtr4d syl31anc absmul subdir mulid2 efadd npcan eqtrd 3eqtr3rd
      lemul1a mulass 3brtr3d rpgt0 ltmuldiv syl112anc lelttrd ) ABUCJZCUCJZUDKZ
      UEJZBCUDKZUEJZLUUAUEJZMKZMKZDAUUCNOZUUDPOAUUANOZUUBNOZUUJABNOZUUKEBUFQZAC
      NOZUULFCUFQUUAUUBUGUHUUCUIQAUUFPOZUUHPOZUUIPOAUUENOZUUPAUUMUUOUUREFBCUGUH
      UUEUIQZAUUHUJOZUUQALUJOZUUGUJOZUUTLUKOUVAULLUMUNAUUKUUARUTZUVBUUNAUUMUVCE
      BUOQUUAUPUHLUUGUQURZUUHVAQZUUFUUHUSUHADUJODPOZGDVAQZACBUDKZUCJZSUDKZUEJZU
      UGMKZUUFLMKZUUGMKZUUDUUIVBAUVKPOZUVMPOZUUGPOZRUUGVBVCZVOUVKUVMVBVCUVLUVNV
      BVCAUVJNOZUVOAUVINOZSNOZUVSAUVHNOZUVTAUUOUUMUWBFECBUGUHZUVHUFQZVDUVISUGVE
      UVJUIQAUUPLPOUVPUUSVFUUFLUSVEAUVQUVRAUUKUVQUUNUUAUIQZAUUKUVRUUNUUAVGQVHAS
      VIJUAWIUBVJUVHUBWIZVKKUWFVLJZVMKVNZJZUAVPZUEJUVHUEJZSVKKZSSVQKZSVLJZSMKZV
      MKZMKZUVKUVMVBAUVHUAUBUWHUBVJUWKUWFVKKUWGVMKVNZUBVJUWLUWNVMKSUWMVMKUWFVKK
      MKVNZSUWHVRZUWRVRUWSVRSUKOAVSVTUWCAUUFUWKSVBAUUMUUOUUFUWKTEFBCWAUHZAUUFSW
      BVCZUUFSVBVCZIAUUPSPOUXBUXCWJUUSWCUUFSWDVEWEWFWGAUVJUWJUEAUVJUWJTZSUWJVQK
      ZUVITZAUVIUXEAUVHRSUAUBUWHRSUWTRSVQKZSSVDWHZWKWLUWCRNOAWMVTARUVIVQKZUVIRR
      VIJZUWIUAVPZVQKAUVTUXIUVITUWDUVIWNQAUVIUXKRVQAUVIVJUWIUAVPZUXKAUWBUVIUXLT
      UWCUVHUAUBUWHUWTWOQVJUXJUWIUAXAWPWQWRWSARUVHRVKKRVLJVMKZVQKUXGSAUXMSRVQAU
      WBUXMSTUWCUVHWTQWRUXHWQXBXCAUVTUWAUWJNOZUXDUXFXLUWDUWAAVDVTZAUWBSVJOUXNUW
      CXDUVHUAUBUWHSUWTXEVEUVISUWJXFXGXHXIAUVMUWKLMKUWQAUUFUWKLMUXAXJAUWLUWKUWP
      LMAUWKNOUWLUWKTAUWKAUWBUWKPOUWCUVHUIQXKUWKXMQUWPLTALSVMKUWPLLUWMSUWOVMXNU
      WOSSMKSUWNSSMXOXPSVDXQXRXSLXTYAYBVTYDYCYEUVKUVMUUGYNYFASUVIUDKZUUAMKZUEJZ
      UXPUEJZUUGMKZUUDUVLAUXPNOZUUKUXRUXTTAUWAUVTUYAVDUWDSUVIUGURUUNUXPUUAYGUHA
      UXQUUCUEAUXQSUUAMKZUVIUUAMKZUDKZUUCAUWAUVTUUKUXQUYDTUXOUWDUUNSUVIUUAYHXGA
      UYBUUAUYCUUBUDAUUKUYBUUATUUNUUAYIQAUVHBVQKZUCJZUYCUUBAUWBUUMUYFUYCTUWCEUV
      HBYJUHAUYECUCAUUOUUMUYECTFECBYKUHXIWSYDYLXIAUXSUVKUUGMAUWAUVTUXSUVKTUXOUW
      DSUVIWAUHXJYMAUUFNOLNOZUUGNOUVNUUITAUUFUUSXKUYGAXTVTAUUGUWEXKUUFLUUGYOXGY
      PAUUIDWBVCZUUFDUUHVMKWBVCZHAUUPUVFUUQRUUHWBVCZUYHUYIXLUUSUVGUVEAUUTUYJUVD
      UUHYQQUUFDUUHYRYSXHYT $.
      $( [15-Sep-2007] $)
  $}

  ${
    $d w x y z $.
    $( The exponential function is continuous.  (Contributed by Paul Chapman,
       15-Sep-2007.) $)
    efcn $p |- exp e. ( CC -cn-> CC ) $=
      ( vx vz vy cc ce co wcel cv c2 cfv cabs c1 cle wbr crp wa sylancl syl 1re
      cr wss ccncf ssid cmul cdiv cif eff id cn 2nn nnrp ax-mp cc0 efcl absrpcl
      wne syl2anc rpmulcl sylancr rpdivcl syl2anr 1rp ifcl cmin simplll simpllr
      efne0 clt simplr subcl abscl ad2antrr rpre adantlr simpr min1 ltletrd a1i
      adantr min2 efcnlem ex elcncf1ii mp2an ) DDUAZWEEDDUBFGDUCZWFABCDDEBHZIAH
      ZEJZKJZUDFZUEFZLMNZWLLUFZUGWHDGZWGOGZPZWLOGZLOGWNOGZWPWPWKOGZWRWOWPUHWOIO
      GZWJOGZWTIUIGXAUJIUKULWOWIDGWIUMUPXBWHUNWHVGWIUOUQIWJURUSWGWKUTVAZVBWMWLL
      OVCQZWOCHZDGZPZWPPZWHXEVDFZKJZWNVHNZWIXEEJVDFKJWGVHNXHXKPZWHXEWGWOXFWPXKV
      EWOXFWPXKVFXGWPXKVIXLXJWNWLXGXJTGZWPXKXGXIDGXMWHXEVJXIVKRVLZXHWNTGZXKWOWP
      XOXFWQWSXOXDWNVMRVNVSZXHWLTGZXKWOWPXQXFWQWRXQXCWLVMRVNVSZXHXKVOZXLXQLTGZW
      NWLMNXRSWLLVPQVQXLXJWNLXNXPXTXLSVRXSXLXQXTWNLMNXRSWLLVTQVQWAWBWCWD $.
      $( [15-Sep-2007] $)
  $}

  ${
    $d U x y $.
    $( Lemma for ~ reeff1o . $)
    reeff1olem $p |- ( ( U e. RR /\ 1 < U ) -> E. x e. RR ( exp ` x ) = U ) $=
      ( vy cr wcel c1 clt wbr wa cc0 co wss cv ce cfv wceq 0re adantr cc a1i wi
      cioo wrex cicc ioossicc iccssre mpan syl5ss simpl lt01 lttr mp3an12 mpani
      1re imp ax-resscn syl6ss ssid ccncf efcn ssel2 reefcl syl sylan ef0 simpr
      syl5eqbr caddc peano2re ltp1 recnd ax-1cn addcom crp elrp sylanbrc efgt1p
      sylancl eqbrtrd lttrd jca ivth ssrexv sylc ) BDEZFBGHZIZJBUBKZDLAMNOBPZAW
      HUCWIADUCWGWHJBUDKZDJBUEWEWJDLZWFJDEZWEWKQJBUFUGRZUHWGCJBSBNAWLWGQTWEWFUI
      ZWNWEWFJBGHZWEJFGHZWFWOUJWLFDEWEWPWFIWOUAQUNJFBUKULUMUOZWGWJDSWMUPUQSSLWG
      SURTNSSUSKEWGUTTWGWKCMZWJEZWRNODEZWMWKWSIWRDEWTWJDWRVAWRVBVCVDWGJNOZBGHBB
      NOZGHWGXAFBGVEWEWFVFVGWGBBFVHKZXBWNWEXCDEWFBVIRWEXBDEWFBVBRWEBXCGHWFBVJRW
      GXCFBVHKZXBGWGBSEFSEXCXDPWGBWNVKVLBFVMVRWGBVNEZXDXBGHWGWEWOXEWNWQBVOVPBVQ
      VCVSVTWAWBWIAWHDWCWD $.
      $( [30-Apr-2014] $) $( [18-Oct-2007] $)
  $}

  ${
    $d x y z $.
    $( The real exponential function is one-to-one onto.  (Contributed by Paul
       Chapman, 18-Oct-2007.)  (Revised by Mario Carneiro, 30-Apr-2014.) $)
    reeff1o $p |- ( exp |` RR ) : RR -1-1-onto-> RR+ $=
      ( vx vy cr crp ce reeff1 wceq cv wcel cfv c1 clt wbr syl cc0 wa wb eqeq1d
      wrex cc vz cres wf1o wf1 wfo df-f1o wfn crn df-fo wf f1f ffn mp2b wss frn
      w3o rpre 1re lttri4 mpan2 cdiv elrp reclt1 sylbi cneg wne rereccl syl2anc
      co rpne0 reeff1olem sylan wi eqcom rpcn jca recn efcl efne0 rec11r syl2an
      cmul efcan eqcomd negcl ax-1cn divmul2 mp3an1 mpbird adantl bitrd syl5bbr
      syl12anc biimpd reximdva adantr mpd renegcl infm3lem rexxfr sylibr sylbid
      fveq2 imp ef0 eqeq2i 0re rcla4ev mpan eqcoms sylbir 3jaodan mpdan rexbiia
      ex fvres fvelrnb ax-mp ssriv eqssi mpbir2an ) CDECUBZUCCDYBUDZCDYBUEZCDYB
      UFFYDYBCUGZYBUHZDGCDYBUIYCCDYBUJZYEFCDYBUKZCDYBULUMZYFDYCYGYFDUNFYHCDYBUO
      UMUADYFUAHZDIZAHZYBJZYJGZACSZYJYFIZYKYLEJZYJGZACSZYOYKYJKLMZYJKGZKYJLMZUP
      ZYSYKYJCIZUUCYJUQZUUDKCIUUCURYJKUSUTNYKYTYSUUAUUBYKYTYSYKYTKKYJVAVIZLMZYS
      YKUUDOYJLMPYTUUGQYJVBYJVCVDYKUUGYSYKUUGPZBHZVEZEJZYJGZBCSZYSUUHUUIEJZUUFG
      ZBCSZUUMYKUUFCIZUUGUUPYKUUDYJOVFZUUQUUEYJVJZYJVGVHBUUFVKVLYKUUPUUMVMUUGYK
      UUOUULBCYKUUICIZPZUUOUULUUOUUFUUNGZUVAUULUUFUUNVNUVAUVBKUUNVAVIZYJGZUULYK
      YJTIZUURPUUNTIZUUNOVFZPZUVBUVDQUUTYKUVEUURYJVOUUSVPUUTUVFUVGUUTUUITIZUVFU
      UIVQZUUIVRZNUUTUVIUVGUVJUUIVSZNVPYJUUNVTWAUUTUVDUULQYKUUTUVCUUKYJUUTUVIUV
      CUUKGZUVJUVIUVMKUUNUUKWBVIZGZUVIUVNKUUIWCWDUVIUUKTIZUVFUVGUVMUVOQZUVIUUJT
      IUVPUUIWEUUJVRNUVKUVLKTIUVPUVHUVQWFKUUKUUNWGWHWMWINRWJWKWLWNWOWPWQYRUULAB
      UUJCUUIWRABWSYLUUJGYQUUKYJYLUUJEXCRWTXAXOXBXDUUAYSYKUUAYJOEJZGYSUVRKYJXEX
      FYSUVRYJOCIUVRYJGZYSXGYRUVSAOCYLOGYQUVRYJYLOEXCRXHXIXJXKWJYKUUDUUBYSUUEAY
      JVKVLXLXMYNYRACYLCIYMYQYJYLCEXPRXNXAYEYPYOQYIACYJYBXQXRXAXSXTYAYA $.
      $( [10-Nov-2013] $) $( [18-Oct-2007] $)
  $}

  ${
    $d x y $.
    $( The exponential function on the reals determines an isomorphism from
       reals onto positive reals.  (Contributed by Steve Rodriguez,
       25-Nov-2007.)  (Revised by Mario Carneiro, 11-Mar-2014.) $)
    reefiso $p |- ( exp |` RR ) Isom < , < ( RR , RR+ ) $=
      ( vx vy cr crp clt ce cres wiso wf1o cv wbr cfv wb wral df-iso reeff1o wa
      wcel eflt fvres breqan12d bitr4d rgen2a mpbir2an ) CDEEFCGZHCDUEIAJZBJZEK
      ZUFUELZUGUELZEKZMZBCNACNABCDEEUEOPULABCUFCRZUGCRZQUHUFFLZUGFLZEKUKUFUGSUM
      UNUIUOUJUPEUFCFTUGCFTUAUBUCUD $.
      $( [11-Mar-2014] $) $( [25-Nov-2007] $)
  $}

  ${
    $d x A $.
    $( Value of the sine function. $)
    sinval $p |- ( A e. CC -> ( sin ` A ) =
    ( ( ( exp ` ( _i x. A ) ) - ( exp ` ( -u _i x. A ) ) ) / ( 2 x. _i ) ) ) $=
      ( vx ci cv cmul co ce cfv cneg cmin cdiv csin wceq opreq2 fveq2d opreq12d
      c2 cc opreq1d df-sin oprex fvmpt ) BACBDZEFZGHZCIZUCEFZGHZJFZQCEFZKFCAEFZ
      GHZUFAEFZGHZJFZUJKFRLUCAMZUIUOUJKUPUEULUHUNJUPUDUKGUCACENOUPUGUMGUCAUFENO
      PSBTUOUJKUAUB $.
      $( [10-Nov-2013] $) $( [14-Mar-2005] $)

    $( Value of the cosine function. $)
    cosval $p |- ( A e. CC -> ( cos ` A ) =
       ( ( ( exp ` ( _i x. A ) ) + ( exp ` ( -u _i x. A ) ) ) / 2 ) ) $=
      ( vx ci cv cmul co ce cfv cneg caddc c2 cdiv cc ccos wceq opreq2 opreq12d
      fveq2d opreq1d df-cos oprex fvmpt ) BACBDZEFZGHZCIZUCEFZGHZJFZKLFCAEFZGHZ
      UFAEFZGHZJFZKLFMNUCAOZUIUNKLUOUEUKUHUMJUOUDUJGUCACEPRUOUGULGUCAUFEPRQSBTU
      NKLUAUB $.
      $( [10-Nov-2013] $) $( [14-Mar-2005] $)
  $}

  ${
    $d x y $.
    $( Domain and codomain of the sine function.  (Contributed by Paul Chapman,
       22-Oct-2007.) $)
    sinf $p |- sin : CC --> CC $=
      ( vx ci cv cmul co ce cfv cneg cmin c2 cdiv cc wcel csin ax-icn mpan efcl
      mulcl syl 2cn wral wf negcli subcl syl2anc cc0 wne mulcli 2ne0 ine0 divcl
      mulne0i mp3an23 rgen df-sin fmpt mpbi ) BACZDEZFGZBHZURDEZFGZIEZJBDEZKEZL
      MZALUALLNUBVGALURLMZVDLMZVGVHUTLMZVCLMZVIVHUSLMZVJBLMVHVLOBURRPUSQSVHVBLM
      ZVKVALMVHVMBOUCVAURRPVBQSUTVCUDUEVIVELMVEUFUGVGJBTOUHJBTOUIUJULVDVEUKUMSU
      NALLVFNAUOUPUQ $.
      $( [30-Apr-2014] $) $( [22-Oct-2007] $)

    $( Domain and codomain of the sine function.  (Contributed by Paul Chapman,
       22-Oct-2007.) $)
    cosf $p |- cos : CC --> CC $=
      ( vx ci cv cmul co ce cfv cneg caddc c2 cdiv cc wcel wral ccos mulcl mpan
      ax-icn efcl syl wf negcli addcl syl2anc cc0 wne 2ne0 divcl mp3an23 df-cos
      2cn rgen fmpt mpbi ) BACZDEZFGZBHZUODEZFGZIEZJKEZLMZALNLLOUAVCALUOLMZVALM
      ZVCVDUQLMZUTLMZVEVDUPLMZVFBLMVDVHRBUOPQUPSTVDUSLMZVGURLMVDVIBRUBURUOPQUSS
      TUQUTUCUDVEJLMJUEUFVCUKUGVAJUHUITULALLVBOAUJUMUN $.
      $( [30-Apr-2014] $) $( [22-Oct-2007] $)
  $}

  $( Closure of the sine function. $)
  sincl $p |- ( A e. CC -> ( sin ` A ) e. CC ) $=
    ( cc csin sinf ffvelrni ) BBACDE $.
    $( [30-Apr-2014] $) $( [28-Apr-2005] $)

  $( Closure of the cosine function with a complex argument. $)
  coscl $p |- ( A e. CC -> ( cos ` A ) e. CC ) $=
    ( cc ccos cosf ffvelrni ) BBACDE $.
    $( [30-Apr-2014] $) $( [28-Apr-2005] $)

  ${
    $d x y A $.
    $( Value of the tangent function. $)
    tanval $p |- ( ( A e. CC /\ ( cos ` A ) =/= 0 ) ->
                   ( tan ` A ) = ( ( sin ` A ) / ( cos ` A ) ) ) $=
      ( vx cc wcel ccos cfv cc0 wne wa ccnv cdif cima ctan csin cdiv wceq simpl
      csn co fveq2 coscl anim1i eldifsn sylibr wf wfn wb cosf ffn elpreima mp2b
      sylanbrc cv opreq12d df-tan oprex fvmpt syl ) ACDZAEFZGHZIZAEJCGRKZLZDZAM
      FANFZUTOSZPVBUSUTVCDZVEUSVAQVBUTCDZVAIVHUSVIVAAUAUBUTCGUCUDCCEUEECUFVEUSV
      HIUGUHCCEUICAVCEUJUKULBABUMZNFZVJEFZOSVGVDMVJAPVKVFVLUTOVJANTVJAETUNBUOVF
      UTOUPUQUR $.
      $( [14-Mar-2014] $)
  $}

  $( The closure of the tangent function with a complex argument.  (Contributed
     by David A. Wheeler, 15-Mar-2014.) $)
  tancl $p |- ( ( A e. CC /\ ( cos ` A ) =/= 0 ) -> ( tan ` A ) e. CC ) $=
    ( cc wcel ccos cfv cc0 wa ctan csin cdiv co tanval sincl adantr coscl simpr
    wne divcl syl3anc eqeltrd ) ABCZADEZFQZGZAHEAIEZUBJKZBALUDUEBCZUBBCZUCUFBCU
    AUGUCAMNUAUHUCAONUAUCPUEUBRST $.
    $( [15-Mar-2014] $)

  $( The sine of a real number in terms of the exponential function. $)
  resinval $p |- ( A e. RR -> ( sin ` A ) = ( Im ` ( exp ` ( _i x. A ) ) ) ) $=
    ( cr wcel ci cmul co ce cfv cneg cmin c2 cdiv ccj csin wceq sylancr opreq2d
    cc ax-icn syl cim recn cjmul cji cjre syl5eq eqtrd fveq2d mulcl efcj eqtr3d
    opreq1i opreq1d sinval efcl imcj 3eqtr4d ) ABCZDAEFZGHZDIZAEFZGHZJFZKDEFZLF
    ZUTUTMHZJFZVELFZANHZUTUAHZURVDVHVELURVCVGUTJURUSMHZGHZVCVGURVLVBGURVLDMHZAM
    HZEFZVBURDRCZARCZVLVPOSAUBZDAUCPURVPVAVOEFVBVNVAVOEUDULURVOAVAEAUEQUFUGUHUR
    USRCZVMVGOURVQVRVTSVSDAUIPZUSUJTUKQUMURVRVJVFOVSAUNTURUTRCZVKVIOURVTWBWAUSU
    OTUTUPTUQ $.
    $( [23-Feb-2006] $) $( [30-Apr-2005] $)

  $( The cosine of a real number in terms of the exponential function. $)
  recosval $p |- ( A e. RR -> ( cos ` A ) = ( Re ` ( exp ` ( _i x. A ) ) ) ) $=
    ( cr wcel ci cmul co ce cfv cneg caddc c2 cdiv ccj ccos wceq ax-icn sylancr
    cc opreq2d syl cre recn cjmul opreq1i cjre syl5eq eqtrd fveq2d mulcl eqtr3d
    cji efcj opreq1d cosval efcl recj 3eqtr4d ) ABCZDAEFZGHZDIZAEFZGHZJFZKLFZUT
    UTMHZJFZKLFZANHZUTUAHZURVDVGKLURVCVFUTJURUSMHZGHZVCVFURVKVBGURVKDMHZAMHZEFZ
    VBURDRCZARCZVKVOOPAUBZDAUCQURVOVAVNEFVBVMVAVNEUKUDURVNAVAEAUESUFUGUHURUSRCZ
    VLVFOURVPVQVSPVRDAUIQZUSULTUJSUMURVQVIVEOVRAUNTURUTRCZVJVHOURVSWAVTUSUOTUTU
    PTUQ $.
    $( [27-Feb-2006] $) $( [30-Apr-2005] $)

  ${
    $d A k n $.  $d F k $.
    efi4p.1 $e |- F = ( n e. NN0 |-> ( ( ( _i x. A ) ^ n ) / ( ! ` n ) ) ) $.
    $( Separate out the first four terms of the infinite series expansion of
       the exponential function of an imaginary number.  (Contributed by Paul
       Chapman, 19-Jan-2008.)  (Revised by Mario Carneiro, 30-Apr-2014.) $)
    efi4p $p |- ( A e. CC -> ( exp ` ( _i x. A ) ) =
        ( ( ( 1 - ( ( A ^ 2 ) / 2 ) ) + ( _i x. ( A - ( ( A ^ 3 ) / 6 ) ) ) ) +
          sum_ k e. ( ZZ>= ` 4 ) ( F ` k ) ) ) $=
      ( cc wcel ci cmul co c1 caddc c2 cexp cdiv c3 c6 wceq ax-icn syl ce c4 cv
      cfv cuz csu cmin mulcl mpan ef4p ax-1cn addcl sylancr sqcl cc0 2ne0 divcl
      wne 2cn mp3an23 cn0 3nn nnnn0i expcl sylancl 6pos gt0ne0ii addass syl3anc
      6re recni a1i add4 syl22anc cneg 2nn0 mulexp mp3an13 opreq1i mulm1 3eqtrd
      i2 opreq1d divneg eqtr4d opreq2d negsub eqtrd i3 mpan2 wa negcli mulneg12
      pm3.2i divass negcl adddi mp3an1 mpdan 3eqtr2d opreq12d ) AFGZHAIJZUAUDZK
      XCLJZXCMNJZMOJZLJXCPNJZQOJZLJZUBUEUDBUCDUDBUFZLJZKAMNJZMOJZUGJZHAAPNJZQOJ
      ZUGJZIJZLJZXKLJXBXCFGZXDXLRHFGZXBYASHAUHUIZXCBCDEUJTXBXJXTXKLXBXJXEXGXILJ
      LJZKXGLJZXCXILJZLJZXTXBXEFGZXGFGZXIFGZXJYDRXBKFGZYAYHUKYCKXCULUMXBXFFGZYI
      XBYAYLYCXCUNTYLMFGZMUOURZYIUSUPXFMUQUTTZXBXHFGZYJXBYAPVAGZYPYCPVBVCZXCPVD
      VEYPQFGZQUOURZYJQVJVKZQVJVFVGZXHQUQUTTZXEXGXIVHVIXBYKYAYIYJYDYGRYKXBUKVLY
      CYOUUCKXCXGXIVMVNXBYEXOYFXSLXBYEKXNVOZLJZXOXBXGUUDKLXBXGXMVOZMOJZUUDXBXFU
      UFMOXBXFHMNJZXMIJZKVOZXMIJZUUFYBXBMVAGXFUUIRSVPHAMVQVRUUIUUKRXBUUHUUJXMIW
      BVSVLXBXMFGZUUKUUFRAUNZXMVTTWAWCXBUULUUDUUGRZUUMUULYMYNUUNUSUPXMMWDUTTWEW
      FXBYKXNFGZUUEXORUKXBUULUUOUUMUULYMYNUUOUSUPXMMUQUTTKXNWGUMWHXBYFXCHXQVOZI
      JZLJZHAUUPLJZIJZXSXBXIUUQXCLXBXIHVOZXPIJZQOJZUVAXQIJZUUQXBXHUVBQOXBXHHPNJ
      ZXPIJZUVBYBXBYQXHUVFRSYRHAPVQVRUVFUVBRXBUVEUVAXPIWIVSVLWHWCXBXPFGZUVCUVDR
      ZXBYQUVGYRAPVDWJZUVAFGUVGYSYTWKUVHHSWLYSYTUUAUUBWNUVAXPQWOVRTXBYBXQFGZUVD
      UUQRSXBUVGUVJUVIUVGYSYTUVJUUAUUBXPQUQUTTZHXQWMUMWAWFXBUUPFGZUUTUURRZXBUVJ
      UVLUVKXQWPTYBXBUVLUVMSHAUUPWQWRWSXBUUSXRHIXBUVJUUSXRRUVKAXQWGWSWFWTXAWAWC
      WH $.
      $( [30-Apr-2014] $) $( [19-Jan-2008] $)

    $( Separate out the first four terms of the infinite series expansion of
       the sine of a real number.  (Contributed by Paul Chapman,
       19-Jan-2008.) $)
    resin4p $p |- ( A e. RR -> ( sin ` A ) = ( ( A - ( ( A ^ 3 ) / 6 ) ) +
                       ( Im ` sum_ k e. ( ZZ>= ` 4 ) ( F ` k ) ) ) ) $=
      ( cr wcel cfv ci co cim c3 c6 c4 caddc c1 cc wceq syl sylancr csin ce cuz
      cmul cexp cdiv cmin cv csu resinval recn efi4p fveq2d 1re resqcl rehalfcl
      c2 resubcl recnd ax-icn cn0 3nn nnnn0i reexpcl mpan2 cc0 wne 6re gt0ne0ii
      6pos redivcl mp3an23 mpdan mulcl addcl syl2anc 4nn0 sylancl imadd opreq1d
      eftlcl crim 3eqtrd eqtrd ) AFGZAUAHIAUDJZUBHZKHZAALUEJZMUFJZUGJZNUCHBUHDH
      BUIZKHZOJZAUJWEWHPAUQUEJZUQUFJZUGJZIWKUDJZOJZWLOJZKHZWSKHZWMOJZWNWEWGWTKW
      EAQGZWGWTRAUKZABCDEULSUMWEWSQGZWLQGZXAXCRWEWQQGWRQGZXFWEWQWEPFGWPFGZWQFGZ
      UNWEWOFGXIAUOWOUPSPWPURTZUSWEIQGZWKQGXHUTWEWKWEWJFGZWKFGZWEWIFGZXMWELVAGX
      OLVBVCALVDVEXOMFGMVFVGXMVHMVHVJVIWIMVKVLSAWJURVMZUSIWKVNTWQWRVOVPWEWFQGZN
      VAGXGWEXLXDXQUTXEIAVNTVQWFBCDNEWAVRWSWLVSVPWEXBWKWMOWEXJXNXBWKRXKXPWQWKWB
      VPVTWCWD $.
      $( [30-Apr-2014] $) $( [19-Jan-2008] $)

    $( Separate out the first four terms of the infinite series expansion of
       the cosine of a real number.  (Contributed by Paul Chapman,
       19-Jan-2008.) $)
    recos4p $p |- ( A e. RR -> ( cos ` A ) = ( ( 1 - ( ( A ^ 2 ) / 2 ) ) +
                       ( Re ` sum_ k e. ( ZZ>= ` 4 ) ( F ` k ) ) ) ) $=
      ( cr wcel cfv ci co cre c1 c4 caddc c3 c6 cc wceq syl sylancr ccos ce cuz
      cmul c2 cexp cdiv cmin csu recosval recn efi4p fveq2d 1re resqcl rehalfcl
      cv resubcl recnd ax-icn cn0 3nn nnnn0i reexpcl mpan2 cc0 wne 6re gt0ne0ii
      6pos redivcl mp3an23 mpdan mulcl addcl syl2anc 4nn0 sylancl readd opreq1d
      eftlcl crre 3eqtrd eqtrd ) AFGZAUAHIAUDJZUBHZKHZLAUEUFJZUEUGJZUHJZMUCHBUQ
      DHBUIZKHZNJZAUJWEWHWKIAAOUFJZPUGJZUHJZUDJZNJZWLNJZKHZWSKHZWMNJZWNWEWGWTKW
      EAQGZWGWTRAUKZABCDEULSUMWEWSQGZWLQGZXAXCRWEWKQGWRQGZXFWEWKWELFGWJFGZWKFGZ
      UNWEWIFGXIAUOWIUPSLWJURTZUSWEIQGZWQQGXHUTWEWQWEWPFGZWQFGZWEWOFGZXMWEOVAGX
      OOVBVCAOVDVEXOPFGPVFVGXMVHPVHVJVIWOPVKVLSAWPURVMZUSIWQVNTWKWRVOVPWEWFQGZM
      VAGXGWEXLXDXQUTXEIAVNTVQWFBCDMEWAVRWSWLVSVPWEXBWKWMNWEXJXNXBWKRXKXPWKWQWB
      VPVTWCWD $.
      $( [30-Apr-2014] $) $( [19-Jan-2008] $)
  $}

  $( The sine of a real number is real. $)
  resincl $p |- ( A e. RR -> ( sin ` A ) e. RR ) $=
    ( cr wcel csin cfv ci cmul co ce resinval cc ax-icn recn mulcl sylancr efcl
    cim syl imcl eqeltrd ) ABCZADEFAGHZIEZQEZBAJUAUCKCZUDBCUAUBKCZUEUAFKCAKCUFL
    AMFANOUBPRUCSRT $.
    $( [24-Feb-2006] $) $( [30-Apr-2005] $)

  $( The cosine of a real number is real. $)
  recoscl $p |- ( A e. RR -> ( cos ` A ) e. RR ) $=
    ( cr wcel ccos cfv ci cmul co ce recosval cc ax-icn recn mulcl sylancr efcl
    cre syl recl eqeltrd ) ABCZADEFAGHZIEZQEZBAJUAUCKCZUDBCUAUBKCZUEUAFKCAKCUFL
    AMFANOUBPRUCSRT $.
    $( [28-Feb-2006] $) $( [30-Apr-2005] $)

  $( The closure of the tangent function with a real argument.  (Contributed by
     David A. Wheeler, 15-Mar-2014.) $)
  retancl $p |- ( ( A e. RR /\ ( cos ` A ) =/= 0 ) -> ( tan ` A ) e. RR ) $=
    ( cr wcel ccos cfv cc0 wne wa ctan csin cdiv co cc wceq recn tanval recoscl
    sylan resincl redivcl syl3an1 syl3an2 3anidm12 eqeltrd ) ABCZADEZFGZHAIEZAJ
    EZUFKLZBUEAMCUGUHUJNAOAPRUEUGUJBCZUEUEUFBCZUGUKAQUEUIBCULUGUKASUIUFTUAUBUCU
    D $.
    $( [15-Mar-2014] $)


  $( The sine of a negative is the negative of the sine. $)
  sinneg $p |- ( A e. CC -> ( sin ` -u A ) = -u ( sin ` A ) ) $=
    ( cc wcel ci cneg cmul co ce cfv cmin c2 cdiv csin wceq ax-icn fveq2d mulcl
    mpan efcl syl mulneg12 eqcomd mul2neg negcli negsubdi2 syl2anc eqtr4d negcl
    opreq12d opreq1d sinval negeqd subcl cc0 wne 2cn mulcli 2ne0 mulne0i divneg
    ine0 mp3an23 eqtrd 3eqtr4d ) ABCZDAEZFGZHIZDEZVFFGZHIZJGZKDFGZLGZDAFGZHIZVI
    AFGZHIZJGZEZVMLGZVFMIZAMIZEZVEVLVTVMLVEVLVRVPJGZVTVEVHVRVKVPJVEVGVQHVEVQVGD
    BCZVEVQVGNODAUARUBPVEVJVOHWFVEVJVONODAUCRPUIVEVPBCZVRBCZVTWENVEVOBCZWGWFVEW
    IODAQRVOSTZVEVQBCZWHVIBCVEWKDOUDVIAQRVQSTZVPVRUEUFUGUJVEVFBCWBVNNAUHVFUKTVE
    WDVSVMLGZEZWAVEWCWMAUKULVEVSBCZWNWANZVEWGWHWOWJWLVPVRUMUFWOVMBCVMUNUOWPKDUP
    OUQKDUPOURVAUSVSVMUTVBTVCVD $.
    $( [30-Apr-2005] $)

  $( The cosines of a number and its negative are the same. $)
  cosneg $p |- ( A e. CC -> ( cos ` -u A ) = ( cos ` A ) ) $=
    ( cc wcel ci cneg cmul co ce cfv caddc c2 cdiv ccos wceq ax-icn mpan fveq2d
    mulcl efcl syl mulneg12 eqcomd mul2neg opreq12d negcli addcom syl2anc eqtrd
    opreq1d negcl cosval 3eqtr4d ) ABCZDAEZFGZHIZDEZUNFGZHIZJGZKLGZDAFGZHIZUQAF
    GZHIZJGZKLGUNMIZAMIUMUTVFKLUMUTVEVCJGZVFUMUPVEUSVCJUMUOVDHUMVDUODBCZUMVDUON
    ODAUAPUBQUMURVBHVIUMURVBNODAUCPQUDUMVEBCZVCBCZVHVFNUMVDBCZVJUQBCUMVLDOUEUQA
    RPVDSTUMVBBCZVKVIUMVMODARPVBSTVEVCUFUGUHUIUMUNBCVGVANAUJUNUKTAUKUL $.
    $( [20-Feb-2006] $) $( [30-Apr-2005] $)

  $( The tangent of a negative is the negative of the tangent.  (Contributed by
     David A. Wheeler, 23-Mar-2014.) $)
  tanneg $p |- ( ( A e. CC /\ ( cos ` A ) =/= 0 ) ->
                      ( tan ` -u A ) = -u ( tan ` A ) ) $=
    ( cc wcel ccos cfv cc0 wne wa csin cdiv cneg ctan wceq coscl divneg syl3an1
    co sincl tanval adantr syl3an2 3anidm12 negeqd cosneg eqnetrd syldan sinneg
    simpr negcl sylan opreq12d eqtrd 3eqtr4rd ) ABCZADEZFGZHZAIEZUOJQZKZURKZUOJ
    QZALEZKAKZLEZUNUPUTVBMZUNUNUOBCZUPVFANUNURBCVGUPVFARURUOOPUAUBUQVCUSASUCUQV
    EVDIEZVDDEZJQZVBUNUPVIFGZVEVJMZUQVIUOFUNVIUOMUPAUDZTUNUPUHUEUNVDBCVKVLAUIVD
    SUJUFUNVJVBMUPUNVHVAVIUOJAUGVMUKTULUM $.
    $( [23-Mar-2014] $)

  $( Value of the sine function at 0.  (Contributed by Steve Rodriguez,
     5-Jul-2006.) $)
  sin0 $p |- ( sin ` 0 ) = 0 $=
    ( cc0 csin cfv cneg wceq neg0 fveq2i cc wcel 0cn sinneg ax-mp eqtr3i eqnegi
    sincl mpbi ) ABCZQDZEQAEADZBCZQRSABFGAHIZTREJAKLMQUAQHIJAOLNP $.
    $( [26-Apr-2008] $) $( [14-Mar-2005] $)

  $( Value of the cosine function at 0. $)
  cos0 $p |- ( cos ` 0 ) = 1 $=
    ( cc0 ccos cfv ci cmul co ce cre c1 cr wcel wceq 0re recosval ax-icn mul01i
    ax-mp fveq2i ef0 eqtri re1 3eqtri ) ABCZDAEFZGCZHCZIHCIAJKUCUFLMANQUEIHUEAG
    CIUDAGDOPRSTRUAUB $.
    $( [28-Feb-2006] $) $( [30-Apr-2005] $)

  $( The value of the tangent function at zero is zero.  (Contributed by David
     A. Wheeler, 16-Mar-2014.) $)
  tan0 $p |- ( tan ` 0 ) = 0 $=
    ( cc0 ctan cfv csin ccos cdiv co cc wcel wne wceq c1 ax-1ne0 eqnetri tanval
    0cn cos0 mp2an sin0 opreq1i ax-1cn eqeltri div0i 3eqtri ) ABCZADCZAECZFGZAU
    GFGAAHIUGAJUEUHKPUGLAQMNZAORUFAUGFSTUGUGLHQUAUBUIUCUD $.
    $( [16-Mar-2014] $)

  $( The exponential function in terms of sine and cosine. $)
  efival $p |- ( A e. CC -> ( exp ` ( _i x. A ) ) =
                ( ( cos ` A ) + ( _i x. ( sin ` A ) ) ) ) $=
    ( cc wcel ci cmul co ce cfv caddc cdiv wceq ax-icn syl syl2anc 2ne0 3eqtr4d
    c2 2cn ine0 c1 cneg cmin ccos csin mulcl mpan negcli addcl subcl cc0 wne wa
    efcl pm3.2i divdir mp3an3 pncan3 opreq2d addass 2times eqtr2d cosval mulcli
    syl3anc opreq1d divcan3 mp3an23 mulne0i div12 mp3an13 sinval divrec mulid2i
    opreq1i dividi opreq2i ax-1cn divmuldivi mulid1i 3eqtr3i syl6eqr opreq12d
    reccli eqtr3i ) ABCZDAEFZGHZDUAZAEFZGHZIFZWGWJUBFZIFZQJFZWKQJFZWLQJFZIFZWGA
    UCHZDAUDHZEFZIFWEWKBCZWLBCZWNWQKZWEWGBCZWJBCZXAWEWFBCZXDDBCZWEXFLDAUEUFWFUM
    MZWEWIBCZXEWHBCWEXIDLUGWHAUEUFWIUMMZWGWJUHNWEXDXEXBXHXJWGWJUINZXAXBQBCZQUJU
    KZULXCXLXMROUNWKWLQUOUPNWEWNQWGEFZQJFZWGWEWMXNQJWEWGWJWLIFZIFZWGWGIFZWMXNWE
    XPWGWGIWEXEXDXPWGKXJXHWJWGUQNURWEXDXEXBWMXQKXHXJXKWGWJWLUSVDWEXDXNXRKXHWGUT
    MPVEWEXDXOWGKZXHXDXLXMXSROWGQVFVGMVAWEWRWOWTWPIAVBWEDWLQDEFZJFZEFZWLDXTJFZE
    FZWTWPWEXBYBYDKZXKXGXBXTBCZXTUJUKZULYELYFYGQDRLVCQDRLOSVHUNDWLXTVIVJMWEWSYA
    DEAVKURWEWPWLTQJFZEFZYDWEXBWPYIKZXKXBXLXMYJROWLQVLVGMYCYHWLETDEFZXTJFZYCYHY
    KDXTJDLVMVNYHDDJFZEFYHTEFYLYHYMTYHEDLSVOVPTQDDVQRLLOSVRYHQROWCVSVTWDVPWAPWB
    P $.
    $( [30-Apr-2005] $)

  $( The exponential function in terms of sine and cosine. $)
  efmival $p |- ( A e. CC -> ( exp ` ( -u _i x. A ) ) =
                ( ( cos ` A ) - ( _i x. ( sin ` A ) ) ) ) $=
    ( cc wcel ci cneg cmul co ce ccos csin caddc cmin wceq ax-icn mulneg12 mpan
    cfv fveq2d sylancr eqtrd negcl efival cosneg opreq2d sincl mulneg2 opreq12d
    syl sinneg coscl mulcl negsub syl2anc 3eqtrd ) ABCZDEAFGZHQDAEZFGZHQZUQIQZD
    UQJQZFGZKGZAIQZDAJQZFGZLGZUOUPURHDBCZUOUPURMNDAOPRUOUQBCUSVCMAUAUQUBUHUOVCV
    DVFEZKGZVGUOUTVDVBVIKAUCUOVBDVEEZFGZVIUOVAVKDFAUIUDUOVHVEBCZVLVIMNAUEZDVEUF
    STUGUOVDBCVFBCZVJVGMAUJUOVHVMVONVNDVEUKSVDVFULUMTUN $.
    $( [14-Jan-2006] $)

  $( Eulerian representation of the complex exponential.  (Suggested by Jeffrey
     Hankins, 3-Jul-2006.) $)
  efeul $p |- ( A e. CC -> ( exp ` A ) = ( ( exp ` ( Re ` A ) ) x.
               ( ( cos ` ( Im ` A ) ) + ( _i x. ( sin ` ( Im ` A ) ) ) ) ) ) $=
    ( cc wcel ce cfv cre ci cim cmul co caddc ccos csin replim fveq2d wceq recl
    recnd ax-icn imcl mulcl sylancr efadd syl2anc efival syl opreq2d 3eqtrd ) A
    BCZADEAFEZGAHEZIJZKJZDEZUJDEZULDEZIJZUOUKLEGUKMEIJKJZIJUIAUMDANOUIUJBCULBCZ
    UNUQPUIUJAQRUIGBCUKBCZUSSUIUKATRZGUKUAUBUJULUCUDUIUPURUOIUIUTUPURPVAUKUEUFU
    GUH $.
    $( [25-Jul-2008] $) $( [4-Jul-2006] $)

  $( The exponentials of two imaginary numbers are equal iff their sine and
     cosine components are equal.  (Contributed by Paul Chapman,
     15-Mar-2008.) $)
  efieq $p |- ( ( A e. RR /\ B e. RR ) ->
       ( ( exp ` ( _i x. A ) ) = ( exp ` ( _i x. B ) ) <->
         ( ( cos ` A ) = ( cos ` B ) /\ ( sin ` A ) = ( sin ` B ) ) ) ) $=
    ( cr wcel wa ci cmul co ce wceq ccos csin caddc cc wb efival syl2an recoscl
    cfv recn eqeqan12d resincl jca cru bitrd ) ACDZBCDZEFAGHISZFBGHISZJZAKSZFAL
    SZGHMHZBKSZFBLSZGHMHZJZUKUNJULUOJEZUFANDZBNDZUJUQOUGATBTUSUTUHUMUIUPAPBPUAQ
    UFUKCDZULCDZEUNCDZUOCDZEUQUROUGUFVAVBARAUBUCUGVCVDBRBUBUCUKULUNUOUDQUE $.
    $( [15-Mar-2008] $)

  $( Addition formula for sine.  Equation 14 of [Gleason] p. 310.  (Contributed
     by Steve Rodriguez, 10-Nov-2006.)  (Revised by Mario Carneiro,
     30-Apr-2014.) $)
  sinadd $p |- ( ( A e. CC /\ B e. CC ) -> ( sin ` ( A + B ) ) =
       ( ( ( sin ` A ) x. ( cos ` B ) ) + ( ( cos ` A ) x. ( sin ` B ) ) ) ) $=
    ( cc wcel caddc co cfv ci cmul ce cmin c2 wceq mulcl syl2anc ax-icn sylancr
    syl3anc a1i eqtrd csin cneg cdiv addcl sinval syl coscl adantr adantl sincl
    wa ccos pnncan simpl simpr adddi fveq2d efadd efival opreq12d muladd 3eqtrd
    syl22anc negcli efmival mulsub 2times 3eqtr4d opreq1d mulass mulcom opreq2d
    2cn mul12 cc0 wne wb mulcli 2ne0 ine0 divmul syl112anc mpbird addcom eqtr4d
    mulne0i ) ACDZBCDZUKZABEFZUAGZHWJIFZJGZHUBZWJIFZJGZKFZLHIFZUCFZLAULGZHBUAGZ
    IFZIFZBULGZHAUAGZIFZIFZEFZIFZWRUCFZXEXDIFZWTXAIFZEFZWIWJCDWKWSMABUDWJUEUFWI
    WQXIWRUCWIWTXDIFZXBXFIFZEFZXHEFZXPXHKFZKFZXHXHEFZWQXIWIXPCDZXHCDZYBXSXTMWIX
    NCDZXOCDZYAWIWTCDZXDCDZYCWGYEWHAUGUHZWHYFWGBUGUIZWTXDNOWIXBCDZXFCDZYDWIHCDZ
    XACDZYIPWHYLWGBUJUIZHXANQZWIYKXECDZYJPWGYOWHAUJUHZHXENQZXBXFNOXNXOUDOWIXCCD
    ZXGCDZYBWIYEYIYRYGYNWTXBNOWIYFYJYSYHYQXDXFNOXCXGUDOZYTXPXHXHUMRWIWMXQWPXRKW
    IWMHAIFZHBIFZEFZJGZUUAJGZUUBJGZIFZXQWIWLUUCJWIYKWGWHWLUUCMYKWIPSZWGWHUNZWGW
    HUOZHABUPRUQWIUUACDZUUBCDZUUDUUGMWIYKWGUUKPUUIHANQWIYKWHUULPUUJHBNQUUAUUBUR
    OWIUUGWTXFEFZXDXBEFZIFZXQWIUUEUUMUUFUUNIWGUUEUUMMWHAUSUHWHUUFUUNMWGBUSUIUTW
    IYEYJYFYIUUOXQMYGYQYHYNWTXFXDXBVAVCTVBWIWPWNAIFZWNBIFZEFZJGZUUPJGZUUQJGZIFZ
    XRWIWOUURJWIWNCDZWGWHWOUURMUVCWIHPVDZSUUIUUJWNABUPRUQWIUUPCDZUUQCDZUUSUVBMW
    IUVCWGUVEUVDUUIWNANQWIUVCWHUVFUVDUUJWNBNQUUPUUQUROWIUVBWTXFKFZXDXBKFZIFZXRW
    IUUTUVGUVAUVHIWGUUTUVGMWHAVEUHWHUVAUVHMWGBVEUIUTWIYEYJYFYIUVIXRMYGYQYHYNWTX
    FXDXBVFVCTVBUTWIYBXIXTMYTXHVGUFVHVIWIXJXLXKEFZXMWIXJUVJMZWRUVJIFZXIMZWIUVLL
    HUVJIFZIFZXIWILCDZYKUVJCDZUVLUVOMUVPWIVMSUUHWIXLCDZXKCDZUVQWIYEYLUVRYGYMWTX
    ANOZWIYOYFUVSYPYHXEXDNOZXLXKUDOZLHUVJVJRWIUVNXHLIWIUVNHXLIFZHXKIFZEFZXHWIYK
    UVRUVSUVNUWEMUUHUVTUWAHXLXKUPRWIUWCXCUWDXGEWIYKYEYLUWCXCMUUHYGYMHWTXAVNRWIU
    WDHXDXEIFZIFZXGWIXKUWFHIWIYOYFXKUWFMYPYHXEXDVKOVLWIYKYFYOUWGXGMUUHYHYPHXDXE
    VNRTUTTVLTWIXICDZUVQWRCDZWRVOVPZUVKUVMVQWIUVPYBUWHVMYTLXHNQUWBUWIWILHVMPVRS
    UWJWILHVMPVSVTWFSXIUVJWRWAWBWCWIUVSUVRXMUVJMUWAUVTXKXLWDOWEVB $.
    $( [30-Apr-2014] $) $( [10-Nov-2006] $)

  $( Addition formula for cosine.  Equation 15 of [Gleason] p. 310.  (Revised
     by Mario Carneiro, 30-Apr-2014.) $)
  cosadd $p |- ( ( A e. CC /\ B e. CC ) -> ( cos ` ( A + B ) ) =
       ( ( ( cos ` A ) x. ( cos ` B ) ) - ( ( sin ` A ) x. ( sin ` B ) ) ) ) $=
    ( cc wcel caddc co cfv ci cmul ce cmin wceq syl adantr mulcl syl2anc ax-icn
    c2 sylancr 3eqtrd wa ccos cneg cdiv addcl cosval coscl adantl sincl syl3anc
    csin ppncan adddi mp3an1 fveq2d simpl simpr efival opreq12d muladd syl22anc
    efadd negcli efmival mulsub 2times 3eqtr4d opreq1d cc0 wne 2cn 2ne0 divcan3
    eqtrd mp3an23 c1 a1i mul4 ixi opreq1i mulcom opreq2d syl5eq mulm1 negsub )
    ACDZBCDZUAZABEFZUBGZHWIIFZJGZHUCZWIIFZJGZEFZRUDFZRAUBGZBUBGZIFZHBUKGZIFZHAU
    KGZIFZIFZEFZIFZRUDFZWTXCXAIFZKFZWHWICDWJWQLABUEWIUFMWHWPXGRUDWHXFWRXBIFZWSX
    DIFZEFZEFZXFXMKFZEFZXFXFEFZWPXGWHXFCDZXMCDZXRXPXQLWHWTCDZXECDZXRWHWRCDZWSCD
    ZXTWFYBWGAUGNZWGYCWFBUGUHZWRWSOPZWHXBCDZXDCDZYAWHHCDZXACDZYGQWGYJWFBUIUHZHX
    AOSZWHYIXCCDZYHQWFYMWGAUINZHXCOSZXBXDOPWTXEUEPZWHXKCDZXLCDZXSWHYBYGYQYDYLWR
    XBOPWHYCYHYRYEYOWSXDOPXKXLUEPYPXFXMXFULUJWHWLXNWOXOEWHWLHAIFZHBIFZEFZJGZYSJ
    GZYTJGZIFZXNWHWKUUAJYIWFWGWKUUALQHABUMUNUOWHYSCDZYTCDZUUBUUELWHYIWFUUFQWFWG
    UPZHAOSWHYIWGUUGQWFWGUQZHBOSYSYTVBPWHUUEWRXDEFZWSXBEFZIFZXNWHUUCUUJUUDUUKIW
    FUUCUUJLWGAURNWGUUDUUKLWFBURUHUSWHYBYHYCYGUULXNLYDYOYEYLWRXDWSXBUTVAVNTWHWO
    WMAIFZWMBIFZEFZJGZUUMJGZUUNJGZIFZXOWHWNUUOJWMCDZWFWGWNUUOLHQVCZWMABUMUNUOWH
    UUMCDZUUNCDZUUPUUSLWHUUTWFUVBUVAUUHWMAOSWHUUTWGUVCUVAUUIWMBOSUUMUUNVBPWHUUS
    WRXDKFZWSXBKFZIFZXOWHUUQUVDUURUVEIWFUUQUVDLWGAVDNWGUURUVELWFBVDUHUSWHYBYHYC
    YGUVFXOLYDYOYEYLWRXDWSXBVEVAVNTUSWHXRXGXQLYPXFVFMVGVHWHXHXFWTXIUCZEFZXJWHXR
    XHXFLZYPXRRCDRVIVJUVIVKVLXFRVMVOMWHXEUVGWTEWHXEHHIFZXAXCIFZIFZVPUCZXIIFZUVG
    WHYIYJYIYMXEUVLLYIWHQVQZYKUVOYNHXAHXCVRVAWHUVLUVMUVKIFUVNUVJUVMUVKIVSVTWHUV
    KXIUVMIWHYJYMUVKXILYKYNXAXCWAPWBWCWHXICDZUVNUVGLWHYMYJUVPYNYKXCXAOPZXIWDMTW
    BWHXTUVPUVHXJLYFUVQWTXIWEPTT $.
    $( [30-Apr-2014] $) $( [15-Jan-2006] $)

  $( Sine of difference.  (Contributed by Paul Chapman, 12-Oct-2007.) $)
  sinsub $p |- ( ( A e. CC /\ B e. CC ) -> ( sin ` ( A - B ) ) =
                  ( ( ( sin ` A ) x. ( cos ` B ) ) -
                    ( ( cos ` A ) x. ( sin ` B ) ) ) ) $=
    ( cc wcel cneg caddc co csin cfv ccos cmul cmin negsub adantl opreq2d coscl
    wceq sincl syl2an eqtrd wa negcl sinadd sylan2 fveq2d cosneg sinneg mulneg2
    opreq12d mulcl syl2anc 3eqtr3d ) ACDZBCDZUAZABEZFGZHIZAHIZUPJIZKGZAJIZUPHIZ
    KGZFGZABLGZHIUSBJIZKGZVBBHIZKGZLGZUNUMUPCDURVEQBUBAUPUCUDUOUQVFHABMUEUOVEVH
    VJEZFGZVKUOVAVHVDVLFUOUTVGUSKUNUTVGQUMBUFNOUOVDVBVIEZKGZVLUOVCVNVBKUNVCVNQU
    MBUGNOUMVBCDZVICDZVOVLQUNAPZBRZVBVIUHSTUIUOVHCDZVJCDZVMVKQUMUSCDVGCDVTUNARB
    PUSVGUJSUMVPVQWAUNVRVSVBVIUJSVHVJMUKTUL $.
    $( [12-Oct-2007] $)

  $( Cosine of difference.  (Contributed by Paul Chapman, 12-Oct-2007.) $)
  cossub $p |- ( ( A e. CC /\ B e. CC ) -> ( cos ` ( A - B ) ) =
                  ( ( ( cos ` A ) x. ( cos ` B ) ) +
                    ( ( sin ` A ) x. ( sin ` B ) ) ) ) $=
    ( cc wcel cneg caddc co ccos cfv cmul csin cmin adantl opreq2d sincl syl2an
    wceq eqtrd coscl mulcl wa cosadd sylan2 negsub fveq2d cosneg sinneg mulneg2
    negcl opreq12d subneg syl2anc 3eqtr3d ) ACDZBCDZUAZABEZFGZHIZAHIZUQHIZJGZAK
    IZUQKIZJGZLGZABLGZHIUTBHIZJGZVCBKIZJGZFGZUOUNUQCDUSVFQBUIAUQUBUCUPURVGHABUD
    UEUPVFVIVKEZLGZVLUPVBVIVEVMLUPVAVHUTJUOVAVHQUNBUFMNUPVEVCVJEZJGZVMUPVDVOVCJ
    UOVDVOQUNBUGMNUNVCCDZVJCDZVPVMQUOAOZBOZVCVJUHPRUJUPVICDZVKCDZVNVLQUNUTCDVHC
    DWAUOASBSUTVHTPUNVQVRWBUOVSVTVCVJTPVIVKUKULRUM $.
    $( [12-Oct-2007] $)

  $( Sum of sines.  (Contributed by Paul Chapman, 12-Oct-2007.) $)
  addsin $p |- ( ( A e. CC /\ B e. CC ) -> ( ( sin ` A ) + ( sin ` B ) ) =
                    ( 2 x. ( ( sin ` ( ( A + B ) / 2 ) ) x.
                             ( cos ` ( ( A - B ) / 2 ) ) ) ) ) $=
    ( cc wcel c2 caddc cdiv csin cfv cmin ccos cmul wceq halfcl syl sincl coscl
    co mulcl syl2anc wa addcl subcl 2times sinadd sinsub opreq12d syl3anc eqtrd
    ppncan halfaddsub simpld fveq2d simprd 3eqtr2rd ) ACDBCDUAZEABFRZEGRZHIZABJ
    RZEGRZKIZLRZLRZVCVCFRZURVAFRZHIZURVAJRZHIZFRZAHIZBHIZFRUPVCCDZVDVEMUPUSCDZV
    BCDZVMUPURCDZVNUPUQCDVPABUBUQNOZURPOUPVACDZVOUPUTCDVRABUCUTNOZVAQOUSVBSTZVC
    UDOUPVJVCURKIZVAHIZLRZFRZVCWCJRZFRZVEUPVGWDVIWEFUPVPVRVGWDMVQVSURVAUETUPVPV
    RVIWEMVQVSURVAUFTUGUPVMWCCDZVMWFVEMVTUPWACDZWBCDZWGUPVPWHVQURQOUPVRWIVSVAPO
    WAWBSTVTVCWCVCUJUHUIUPVGVKVIVLFUPVFAHUPVFAMZVHBMZABUKZULUMUPVHBHUPWJWKWLUNU
    MUGUO $.
    $( [12-Oct-2007] $)

  $( Difference of sines.  (Contributed by Paul Chapman, 12-Oct-2007.) $)
  subsin $p |- ( ( A e. CC /\ B e. CC ) -> ( ( sin ` A ) - ( sin ` B ) ) =
                    ( 2 x. ( ( cos ` ( ( A + B ) / 2 ) ) x.
                             ( sin ` ( ( A - B ) / 2 ) ) ) ) ) $=
    ( cc wcel wa c2 caddc cdiv ccos cfv cmin csin cmul coscl sincl mulcl syl2an
    co wceq syl halfaddsubcl 2times sinadd sinsub opreq12d pnncan syl3anc eqtrd
    halfaddsub simpld fveq2d simprd 3eqtr2rd ) ACDBCDEZFABGRFHRZIJZABKRFHRZLJZM
    RZMRZUSUSGRZUOUQGRZLJZUOUQKRZLJZKRZALJZBLJZKRUNUSCDZUTVASUNUOCDZUQCDZEZVIAB
    UAZVJUPCDURCDVIVKUONUQOUPURPQTZUSUBTUNVFUOLJZUQIJZMRZUSGRZVQUSKRZKRZVAUNVLV
    FVTSVMVLVCVRVEVSKUOUQUCUOUQUDUETUNVQCDZVIVIVTVASUNVLWAVMVJVOCDVPCDWAVKUOOUQ
    NVOVPPQTVNVNVQUSUSUFUGUHUNVCVGVEVHKUNVBALUNVBASZVDBSZABUIZUJUKUNVDBLUNWBWCW
    DULUKUEUM $.
    $( [12-Oct-2007] $)

  $( Sum of cosines.  (Contributed by Paul Chapman, 12-Oct-2007.) $)
  addcos $p |- ( ( A e. CC /\ B e. CC ) -> ( ( cos ` A ) + ( cos ` B ) ) =
                    ( 2 x. ( ( cos ` ( ( A + B ) / 2 ) ) x.
                             ( cos ` ( ( A - B ) / 2 ) ) ) ) ) $=
    ( cc wcel wa ccos cfv caddc co c2 cdiv cmin cmul wceq coscl syl2an opreq12d
    fveq2d csin syl addcom halfaddsub simprd simpld halfaddsubcl ppncan syl3anc
    mulcl sincl cossub cosadd 2times 3eqtr4d 3eqtr2d ) ACDZBCDZEZAFGZBFGZHIZUSU
    RHIZABHIJKIZABLIJKIZLIZFGZVBVCHIZFGZHIZJVBFGZVCFGZMIZMIZUOURCDUSCDUTVANUPAO
    BOURUSUAPUQVEUSVGURHUQVDBFUQVFANZVDBNZABUBZUCRUQVFAFUQVMVNVOUDRQUQVKVBSGZVC
    SGZMIZHIZVKVRLIZHIZVKVKHIZVHVLUQVKCDZVRCDZWCWAWBNUQVBCDZVCCDZEZWCABUEZWEVIC
    DVJCDWCWFVBOVCOVIVJUHPTZUQWGWDWHWEVPCDVQCDWDWFVBUIVCUIVPVQUHPTWIVKVRVKUFUGU
    QWGVHWANWHWGVEVSVGVTHVBVCUJVBVCUKQTUQWCVLWBNWIVKULTUMUN $.
    $( [12-Oct-2007] $)

  $( Difference of cosines.  (Contributed by Paul Chapman, 12-Oct-2007.)
     (Revised by Mario Carneiro, 10-May-2014.) $)
  subcos $p |- ( ( A e. CC /\ B e. CC ) -> ( ( cos ` B ) - ( cos ` A ) ) =
                  ( 2 x. ( ( sin ` ( ( A + B ) / 2 ) ) x.
                           ( sin ` ( ( A - B ) / 2 ) ) ) ) ) $=
    ( cc wcel wa c2 caddc co cdiv csin cfv cmin cmul ccos wceq sincl syl2an syl
    mulcl opreq12d halfaddsubcl 2times cossub cosadd coscl pnncan syl3anc eqtrd
    halfaddsub simprd fveq2d simpld 3eqtr2rd ) ACDBCDEZFABGHFIHZJKZABLHFIHZJKZM
    HZMHZUSUSGHZUOUQLHZNKZUOUQGHZNKZLHZBNKZANKZLHUNUSCDZUTVAOUNUOCDZUQCDZEZVIAB
    UAZVJUPCDURCDVIVKUOPUQPUPURSQRZUSUBRUNVFUONKZUQNKZMHZUSGHZVQUSLHZLHZVAUNVLV
    FVTOVMVLVCVRVEVSLUOUQUCUOUQUDTRUNVQCDZVIVIVTVAOUNVLWAVMVJVOCDVPCDWAVKUOUEUQ
    UEVOVPSQRVNVNVQUSUSUFUGUHUNVCVGVEVHLUNVBBNUNVDAOZVBBOZABUIZUJUKUNVDANUNWBWC
    WDULUKTUM $.
    $( [10-May-2014] $) $( [12-Oct-2007] $)

  $( Sine squared plus cosine squared is 1.  Equation 17 of [Gleason] p. 311.
     Note that this holds for non-real arguments, even though individually each
     term is unbounded. $)
  sincossq $p |- ( A e. CC ->
                  ( ( ( sin ` A ) ^ 2 ) + ( ( cos ` A ) ^ 2 ) ) = 1 ) $=
    ( cc wcel cneg caddc co ccos cfv cmul csin c1 c2 cexp wceq sincl sqcl coscl
    cc0 syl syl2anc negcl cosadd mpdan negid fveq2d syl6eq addcom sqval opreq2d
    cmin cos0 cosneg eqtr4d sinneg negeqd negneg eqtrd mulneg2 3eqtr2d opreq12d
    mulcl negsub 3eqtrrd 3eqtr3rd ) ABCZAADZEFZGHZAGHZVFGHZIFZAJHZVFJHZIFZUJFZK
    VLLMFZVILMFZEFZVEVFBCZVHVONAUAZAVFUBUCVEVHRGHKVEVGRGAUDUEUKUFVEVRVQVPEFZVKV
    NDZEFZVOVEVPBCZVQBCZVRWANVEVLBCZWDAOZVLPSVEVIBCZWEAQZVIPSVPVQUGTVEVQVKVPWBE
    VEVQVIVIIFZVKVEWHVQWJNWIVIUHSVEVJVIVIIAULUIUMVEVPVLVLIFZVLVMDZIFZWBVEWFVPWK
    NWGVLUHSVEWLVLVLIVEWLVLDZDZVLVEVMWNAUNUOVEWFWOVLNWGVLUPSUQUIVEWFVMBCZWMWBNW
    GVEVSWPVTVFOSZVLVMURTUSUTVEVKBCZVNBCZWCVONVEWHVJBCZWRWIVEVSWTVTVFQSVIVJVATV
    EWFWPWSWGWQVLVMVATVKVNVBTVCVD $.
    $( [15-Jan-2006] $)

  $( Double-angle formula for sine.  (Contributed by Paul Chapman,
     17-Jan-2008.) $)
  sin2t $p |- ( A e. CC ->
      ( sin ` ( 2 x. A ) ) = ( 2 x. ( ( sin ` A ) x. ( cos ` A ) ) ) ) $=
    ( cc wcel c2 cmul csin cfv caddc ccos 2times fveq2d wceq coscl sincl mulcom
    co syl2anc opreq2d sinadd anidms mulcl syl 3eqtr4d eqtrd ) ABCZDAEPZFGAAHPZ
    FGZDAFGZAIGZEPZEPZUEUFUGFAJKUEUKUJUIEPZHPZUKUKHPZUHULUEUMUKUKHUEUJBCZUIBCZU
    MUKLAMZANZUJUIOQRUEUHUNLAASTUEUKBCZULUOLUEUQUPUTUSURUIUJUAQUKJUBUCUD $.
    $( [17-Jan-2008] $)

  $( Double-angle formula for cosine.  (Contributed by Paul Chapman,
     24-Jan-2008.) $)
  cos2t $p |- ( A e. CC -> ( cos ` ( 2 x. A ) ) =
                           ( ( 2 x. ( ( cos ` A ) ^ 2 ) ) - 1 ) ) $=
    ( cc wcel ccos cfv c2 cexp co c1 cmin csin cmul wceq caddc sqcl syl syl2anc
    ax-1cn 2times sqval sincl coscl addcom sincossq eqtr3d subadd mp3an1 mpbird
    opreq2d opreq1d subsub3 mp3an2 eqtr4d cosadd anidms fveq2d opreq12d 3eqtr4d
    wb 3eqtr4rd ) ABCZADEZFGHZIVCJHZJHZVCAKEZFGHZJHZFVCLHZIJHZFALHZDEZVAVDVGVCJ
    VAVDVGMZVCVGNHZIMZVAVGVCNHZVNIVAVGBCZVCBCZVPVNMVAVFBCZVQAUAZVFOPZVAVBBCZVRA
    UBZVBOPZVGVCUCQAUDUEVAVRVQVMVOUSZWDWAIBCZVRVQWERIVCVGUFUGQUHUIVAVJVCVCNHZIJ
    HZVEVAVIWGIJVAVRVIWGMWDVCSPUJVAVRVRVEWHMZWDWDVRWFVRWIRVCIVCUKULQUMVAAANHZDE
    ZVBVBLHZVFVFLHZJHZVLVHVAWKWNMAAUNUOVAVKWJDASUPVAVCWLVGWMJVAWBVCWLMWCVBTPVAV
    SVGWMMVTVFTPUQURUT $.
    $( [24-Jan-2008] $)

  $( Double-angle formula for cosine in terms of sine. $)
  cos2tsin $p |- ( A e. CC -> ( cos ` ( 2 x. A ) ) =
                           ( 1 - ( 2 x. ( ( sin ` A ) ^ 2 ) ) ) ) $=
    ( cc wcel c2 cmul co ccos cfv cexp c1 cmin wceq sqcl syl 2cn mp3an1 syl2anc
    caddc syl6eq ax-1cn cos2t sincl coscl adddi sincossq opreq2d eqtr3d mulid1i
    csin wb mulcl sylancr subadd mpbird opreq1d sub32 mp3an13 subaddrii opreq1i
    df-2 eqcomi 3eqtr2d ) ABCZDAEFGHDAGHZDIFZEFZJKFDDAUIHZDIFZEFZKFZJKFZJVIKFZA
    UAVCVJVFJKVCVJVFLZVIVFRFZDLZVCVNDJEFZDVCDVHVERFZEFZVNVPVCVHBCZVEBCZVRVNLZVC
    VGBCVSAUBVGMNZVCVDBCVTAUCVDMNZDBCZVSVTWAODVHVEUDPQVCVQJDEAUEUFUGDOUHSVCVIBC
    ZVFBCZVMVOUJZVCWDVSWEOWBDVHUKULZVCWDVTWFOWCDVEUKULWDWEWFWGODVIVFUMPQUNUOVCV
    KDJKFZVIKFZVLVCWEVKWJLZWHWDWEJBCWKOTDVIJUPUQNWIJVIKDJJOTTDJJRFUTVAURUSSVB
    $.
    $( [12-Sep-2008] $)


  $( The sine of a real number lies between -1 and 1.  Equation 18 of [Gleason]
     p. 311. $)
  sinbnd $p |- ( A e. RR -> ( -u 1 <_ ( sin ` A ) /\ ( sin ` A ) <_ 1 ) ) $=
    ( cr wcel csin cfv c1 cle wbr cneg c2 cexp co wa ccos cc0 syl wb resqcl 1re
    mpbid caddc recoscl sqge0 resincl addge01 syl2anc cc wceq recn sincossq sq1
    syl6eqr breqtrd ltleii lenegsq mp3an23 lenegcon1 mpan2 anbi2d bitr3d ancomd
    0re lt01 ) ABCZADEZFGHZFIVEGHZVDVEJKLZFJKLZGHZVFVGMZVDVHVHANEZJKLZUALZVIGVD
    OVMGHZVHVNGHZVDVLBCZVOAUBZVLUCPVDVHBCZVMBCZVOVPQVDVEBCZVSAUDZVERPVDVQVTVRVL
    RPVHVMUEUFTVDVNFVIVDAUGCVNFUHAUIAUJPUKULUMVDWAVJVKQWBWAVFVEIFGHZMZVJVKWAFBC
    ZOFGHWDVJQSOFVBSVCUNVEFUOUPWAWCVGVFWAWEWCVGQSVEFUQURUSUTPTVA $.
    $( [16-Jan-2006] $)

  $( The cosine of a real number lies between -1 and 1.  Equation 18 of
     [Gleason] p. 311. $)
  cosbnd $p |- ( A e. RR -> ( -u 1 <_ ( cos ` A ) /\ ( cos ` A ) <_ 1 ) ) $=
    ( cr wcel ccos cfv c1 cle wbr cneg c2 cexp co wa csin cc0 syl wb resqcl 1re
    mpbid caddc resincl sqge0 recoscl addge02 syl2anc cc wceq recn sincossq sq1
    syl6eqr breqtrd ltleii lenegsq mp3an23 lenegcon1 mpan2 anbi2d bitr3d ancomd
    0re lt01 ) ABCZADEZFGHZFIVEGHZVDVEJKLZFJKLZGHZVFVGMZVDVHANEZJKLZVHUALZVIGVD
    OVMGHZVHVNGHZVDVLBCZVOAUBZVLUCPVDVHBCZVMBCZVOVPQVDVEBCZVSAUDZVERPVDVQVTVRVL
    RPVHVMUEUFTVDVNFVIVDAUGCVNFUHAUIAUJPUKULUMVDWAVJVKQWBWAVFVEIFGHZMZVJVKWAFBC
    ZOFGHWDVJQSOFVBSVCUNVEFUOUPWAWCVGVFWAWEWCVGQSVEFUQURUSUTPTVA $.
    $( [10-Feb-2007] $) $( [16-Jan-2006] $)

  $( The sine of a real number is in the closed interval from -1 to 1.
     (Contributed by Mario Carneiro, 12-May-2014.) $)
  sinbnd2 $p |- ( A e. RR -> ( sin ` A ) e. ( -u 1 [,] 1 ) ) $=
    ( cr wcel csin cfv c1 cneg cle wbr cicc co resincl sinbnd simpld simprd w3a
    wb 1re renegcli elicc2 mp2an syl3anbrc ) ABCZADEZBCZFGZUDHIZUDFHIZUDUFFJKCZ
    ALUCUGUHAMZNUCUGUHUJOUFBCFBCUIUEUGUHPQFRSRUFFUDTUAUB $.
    $( [12-May-2014] $)

  $( The cosine of a real number is in the closed interval from -1 to 1.
     (Contributed by Mario Carneiro, 12-May-2014.) $)
  cosbnd2 $p |- ( A e. RR -> ( cos ` A ) e. ( -u 1 [,] 1 ) ) $=
    ( cr wcel ccos cfv c1 cneg cle wbr cicc co recoscl cosbnd simpld simprd w3a
    wb 1re renegcli elicc2 mp2an syl3anbrc ) ABCZADEZBCZFGZUDHIZUDFHIZUDUFFJKCZ
    ALUCUGUHAMZNUCUGUHUJOUFBCFBCUIUEUGUHPQFRSRUFFUDTUAUB $.
    $( [12-May-2014] $)

  ${
    $d k n A $.  $d k F $.
    ef01bnd.1 $e |- F = ( n e. NN0 |-> ( ( ( _i x. A ) ^ n ) / ( ! ` n ) ) ) $.
    $( Lemma for ~ sin01bnd and ~ cos01bnd . $)
    ef01bndlem $p |- ( A e. ( 0 (,] 1 ) ->
        ( abs ` sum_ k e. ( ZZ>= ` 4 ) ( F ` k ) ) < ( ( A ^ 4 ) / 6 ) ) $=
      ( cc0 c1 co wcel c4 cfv cmul cdiv c6 cr clt wbr c5 c2 c3 cioc cuz cv cabs
      csu cexp caddc cfa cc ci cn0 ax-icn cle w3a wb 0re 1re elioc2 mp2an recnd
      simp1bi mulcl sylancr 4nn nnnn0i eftlcl sylancl abscl reexpcl cn readdcli
      syl 4re faccl nnmulcli nndivre remulcl 6nn cmpt eqid a1i wceq absmul absi
      ax-mp opreq1i crp simp2bi elrp sylanbrc rpre rpge0 syl2anc opreq2d syl5eq
      mulid2 3eqtrd simp3bi eqbrtrd eftlub opreq1d breqtrd 3pos 3re 5re ltadd1i
      absid mpbi recni addid2i cu2 5p3e8 3nn nncni addcomi 3eqtr2ri 3brtr3i 2re
      c8 2nn nnge1 cz nnzi eluz1i 3lt4 ltleii mpbir2an expwordi 8re eqeltri 6re
      mp3an 6pos df-4 sq2 opreq2i fac3 3eqtr3i 2nn0 eqtr3i nnexpcl nnrei nngt0i
      ltletri remulcli mulgt0ii ltdiv1ii df-5 fveq2i facp1 eqtr2i mulassi 2p2e4
      3eqtri 2cn expadd opreq12i mulid2i nnne0i dividi ax-1cn gt0ne0ii rereccli
      divmuldivi mulid1i rpexpcl ltmul2 mp3an12 sylbi mpbii wne divrec breqtrrd
      wa mp3an23 lelttrd ) AFGUAHIZJUBKBUCDKBUEZUDKZAJUFHZJGUGHZJUHKZJLHZMHZLHZ
      UVTNMHZUVQUVRUIIZUVSOIUVQUJALHZUIIZJUKIZUWGUVQUJUIIZAUIIZUWIULUVQAUVQAOIZ
      FAPQZAGUMQZFOIGOIUVQUWMUWNUWOUNUOUPUQFGAURUSZVAZUTZUJAVBVCZJVDVEZUWHBCDJE
      VFVGUVRVHVLUVQUVTOIZUWDOIZUWEOIUVQUWMUWJUXAUWQUWTAJVIVGZUWAOIUWCVJIUXBJGV
      MUQVKUWBJUWJUWBVJIUWTJVNWEVDVOUWAUWCVPUSZUVTUWDVQVGUVQUXANVJIUWFOIUXCVRUV
      TNVPVGUVQUVSUWHUDKZJUFHZUWDLHUWEUMUVQUWHBCDCUKUXECUCZUFHUXGUHKMHVSZCUKUXF
      UWBMHGUWAMHUXGUFHLHVSZJEUXHVTUXIVTJVJIUVQVDWAUWSUVQUXEAGUMUVQUXEUJUDKZAUD
      KZLHZGALHZAUVQUWKUWLUXEUXLWBULUWRUJAWCVCUVQUXLGUXKLHUXMUXJGUXKLWDWFUVQUXK
      AGLUVQAWGIZUXKAWBZUVQUWMUWNUXNUWQUVQUWMUWNUWOUWPWHAWIWJZUXNUWMFAUMQUXOAWK
      AWLAXGWMVLWNWOUVQUWLUXMAWBUWRAWPVLWQZUVQUWMUWNUWOUWPWRWSWTUVQUXFUVTUWDLUV
      QUXEAJUFUXQXAXAXBUVQUWEUVTGNMHZLHZUWFPUVQUWDUXRPQZUWEUXSPQZRNSJUFHZLHZMHZ
      UYBUYCMHZUWDUXRPRUYBPQZUYDUYEPQRSTUFHZPQUYGUYBUMQZUYFFRUGHZTRUGHZRUYGPFTP
      QUYIUYJPQXCFTRUPXDXEXFXHRRXEXIZXJUYGXSRTUGHUYJXKXLRTUYKTXMXNXOXPXQSOIGSUM
      QZJTUBKIZUYHXRSVJIZUYLXTSYAWEUYMJYBIZTJUMQTJTXMYCYDJVDYCZTJXDVMYEYFYGSTJY
      HYLRUYGUYBXEUYGXSOXKYIYJUYBUYNUWJUYBVJIXTUWTSJUUAUSZUUBZUUDUSRUYBUYCXEUYR
      NUYBYKUYRUUENUYBYKUYRYMUYBUYQUUCUUFUUGXHRUWAUYCUWCMUUHUWCTUHKZSSUFHZUYTLH
      ZLHZUYSUYBLHUYCUWBUYTLHUYSUYTLHZUYTLHUWCVUBUWBVUCUYTLUWBTGUGHZUHKZUYSVUDL
      HZVUCJVUDUHYNUUITUKIVUEVUFWBTXMVETUUJWEVUDUYTUYSLUYTJVUDYOYNUUKYPUUNWFUYT
      JUWBLYOYPUYSUYTUYTUYSNUIYQNYKXIZYJUYTJUIYOJVMXIYJZVUHUULYRUYBVUAUYSLSSSUG
      HZUFHZUYBVUAVUIJSUFUUMYPSUIISUKIZVUKVUJVUAWBUUOYSYSSSSUUPYLYTYPUYSNUYBLYQ
      WFXPUUQGUYBLHZUYCMHZUYEUXRVULUYBUYCMUYBUYBUYQXNZUURWFUXRUYBUYBMHZLHUXRGLH
      VUMUXRVUOGUXRLUYBVUNUYBUYQUUSZUUTYPGNUYBUYBUVAVUGVUNVUNNYKYMUVBZVUPUVDUXR
      UXRNYKVUQUVCZXIUVEYRYTXQUVQUVTWGIZUXTUYAUOZUVQUXNUYOVUSUXPUYPAJUVFVGVUSUX
      AFUVTPQUVNZVUTUVTWIUXBUXROIVVAVUTUXDVURUWDUXRUVTUVGUVHUVIVLUVJUVQUVTUIIZU
      WFUXSWBZUVQUVTUXCUTVVBNUIINFUVKVVCVUGVUQUVTNUVLUVOVLUVMUVP $.
      $( [19-Jan-2008] $)
  $}

  ${
    $d A k n $.
    $( Bounds on the sine of a positive real number less than or equal to 1.
       (Contributed by Paul Chapman, 19-Jan-2008.)  (Revised by Mario Carneiro,
       30-Apr-2014.) $)
    sin01bnd $p |- ( A e. ( 0 (,] 1 ) ->
        ( ( A - ( ( A ^ 3 ) / 3 ) ) < ( sin ` A ) /\ ( sin ` A ) < A ) ) $=
      ( vk cc0 co wcel cfv c3 c6 cdiv cmin clt wbr c4 wceq cr cle recnd sylancl
      syl cc vn c1 cioc csin cexp cabs wa cuz cv cn0 ci cmul cfa cmpt csu caddc
      cim w3a wb 0re 1re elioc2 mp2an simp1bi resin4p eqcomd resincl cn reexpcl
      eqid 3nn0 nndivre resubcl syl2anc ax-icn mulcl sylancr 4nn0 eftlcl subadd
      6nn imcl syl3anc mpbird fveq2d abscl absimle ef01bndlem a1i cz 3nn eluz1i
      nnzi 4nn 3re 4re 3lt4 ltleii mpbir2an simp2bi ltle mpd expword2i syl32anc
      wi simp3bi 6pos lediv1 syl112anc ltletrd lelttrd eqbrtrd absdiflt subsub4
      6re mpbid c2 wne 3cn 3ne0 pm3.2i 2cn 2ne0 divdiv1 mp3an23 opreq2i syl6req
      3t2e6 opreq12d 2halves eqtrd opreq2d breq1d npcan breq2d anbi12d bitrd )
      ACUBUCDEZAUDFZAAGUEDZHIDZJDZJDZUFFZUUAKLZAYTGIDZJDZYSKLZYSAKLZUGZYRUUDMUH
      FBUIUAUJUKAULDZUAUIZUEDUULUMFIDUNZFBUOZUQFZUFFZUUAKYRUUCUUOUFYRUUCUUONZUU
      BUUOUPDZYSNZYRYSUURYRAOEZYSUURNYRUUTCAKLZAUBPLZCOEZUBOEYRUUTUVAUVBURUSUTV
      ACUBAVBVCZVDZABUAUUMUUMVJZVESVFYRYSTEUUBTEUUOTEZUUQUUSUSYRYSYRUUTYSOEZUVE
      AVGSZQYRUUBYRUUTUUAOEZUUBOEZUVEYRYTOEZHVHEZUVJYRUUTGUJEZUVLUVEVKAGVIRZWAY
      THVLRZAUUAVMVNZQYRUUOYRUUNTEZUUOOEYRUUKTEZMUJEZUVRYRUKTEATEZUVSVOYRAUVEQZ
      UKAVPVQVRUUKBUAUUMMUVFVSRZUUNWBSQZYSUUBUUOVTWCWDWEYRUUPUUNUFFZUUAYRUVGUUP
      OEUWDUUOWFSYRUVRUWEOEUWCUUNWFSZUVPYRUVRUUPUWEPLUWCUUNWGSYRUWEAMUEDZHIDZUU
      AUWFYRUWGOEZUVMUWHOEYRUUTUVTUWIUVEVRAMVIRZWAUWGHVLRUVPABUAUUMUVFWHYRUWGYT
      PLZUWHUUAPLZYRUUTUVNMGUHFEZCAPLZUVBUWKUVEUVNYRVKWIUWMYRUWMMWJEGMPLGMGWKWM
      WLMWNWMGMWOWPWQWRWSWIYRUVAUWNYRUUTUVAUVBUVDWTYRUVCUUTUVAUWNXEUTUVECAXAVQX
      BYRUUTUVAUVBUVDXFAGMXCXDYRUWIUVLHOEZCHKLZUWKUWLUSUWJUVOUWOYRXOWIUWPYRXGWI
      UWGYTHXHXIXPXJXKXLYRUUEUUBUUAJDZYSKLZYSUUBUUAUPDZKLZUGZUUJYRUVHUVKUVJUUEU
      XAUSUVIUVQUVPYSUUBUUAXMWCYRUWRUUHUWTUUIYRUWQUUGYSKYRUWQAUUAUUAUPDZJDZUUGY
      RUWAUUATEZUXDUWQUXCNUWBYRUUAUVPQZUXEAUUAUUAXNWCYRUXBUUFAJYRUXBUUFXQIDZUXF
      UPDZUUFYRUUAUXFUUAUXFUPYRUXFYTGXQULDZIDZUUAYRYTTEZUXFUXINZYRYTUVOQUXJGTEZ
      GCXRZUGXQTEZXQCXRZUGUXKUXLUXMXSXTYAUXNUXOYBYCYAYTGXQYDYESUXHHYTIYHYFYGZUX
      PYIYRUUFTEUXGUUFNYRUUFYRUVLGVHEUUFOEUVOWKYTGVLRQUUFYJSYKYLYKYMYRUWSAYSKYR
      UWAUXDUWSANUWBUXEAUUAYNVNYOYPYQXP $.
      $( [30-Apr-2014] $) $( [19-Jan-2008] $)

    $( Bounds on the cosine of a positive real number less than or equal to 1.
       (Contributed by Paul Chapman, 19-Jan-2008.)  (Revised by Mario Carneiro,
       30-Apr-2014.) $)
    cos01bnd $p |- ( A e. ( 0 (,] 1 ) ->
                     ( ( 1 - ( 2 x. ( ( A ^ 2 ) / 3 ) ) ) < ( cos ` A ) /\
                       ( cos ` A ) < ( 1 - ( ( A ^ 2 ) / 3 ) ) ) ) $=
      ( cc0 c1 co wcel cfv c2 cdiv cmin c6 clt wbr c3 cmul c4 wceq cr syl recnd
      cc vk vn cioc ccos cexp cabs wa cuz cv cn0 cfa cmpt csu cre caddc cle w3a
      ci wb 0re 1re elioc2 mp2an simp1bi recos4p eqcomd recoscl resqcl rehalfcl
      eqid resubcl sylancr ax-icn mulcl 4nn0 eftlcl sylancl recl subadd syl3anc
      mpbird fveq2d abscl cn 6nn nndivre absrele reexpcl ef01bndlem 2nn0 a1i cz
      2z eluz1i 4nn nnzi 2re 4re 2lt4 ltleii mpbir2an simp2bi wi ltle expword2i
      mpd simp3bi syl32anc 6pos lediv1 syl112anc mpbid ltletrd lelttrd absdiflt
      6re eqbrtrd ax-1cn subsub4 halfpm6th simpri opreq2i 2cn 2ne0 reccli nncni
      nnne0i adddi mp3an23 syl5eqr wne 3cn pm3.2i div12 mp3an13 divrec opreq12d
      3ne0 3eqtr4rd opreq2d eqtrd breq1d subsub simpli eqtr3d breq2d anbi12d
      subdi bitrd ) ABCUCDEZAUDFZCAGUEDZGHDZIDZIDZUFFZUULJHDZKLZCGUULMHDZNDZIDZ
      UUKKLZUUKCUUSIDZKLZUGZUUJUUPOUHFUAUIUBUJURANDZUBUIZUEDUVGUKFHDULZFUAUMZUN
      FZUFFZUUQKUUJUUOUVJUFUUJUUOUVJPZUUNUVJUODZUUKPZUUJUUKUVMUUJAQEZUUKUVMPUUJ
      UVOBAKLZACUPLZBQEZCQEZUUJUVOUVPUVQUQUSUTVABCAVBVCZVDZAUAUBUVHUVHVJZVERVFU
      UJUUKTEUUNTEUVJTEZUVLUVNUSUUJUUKUUJUVOUUKQEZUWAAVGRZSUUJUUNUUJUVSUUMQEZUU
      NQEZVAUUJUULQEZUWFUUJUVOUWHUWAAVHRZUULVIRZCUUMVKVLZSUUJUVJUUJUVITEZUVJQEU
      UJUVFTEZOUJEZUWLUUJURTEATEUWMVMUUJAUWASURAVNVLVOUVFUAUBUVHOUWBVPVQZUVIVRR
      SZUUKUUNUVJVSVTWAWBUUJUVKUVIUFFZUUQUUJUWCUVKQEUWPUVJWCRUUJUWLUWQQEUWOUVIW
      CRZUUJUWHJWDEZUUQQEZUWIWEUULJWFVQZUUJUWLUVKUWQUPLUWOUVIWGRUUJUWQAOUEDZJHD
      ZUUQUWRUUJUXBQEZUWSUXCQEUUJUVOUWNUXDUWAVOAOWHVQZWEUXBJWFVQUXAAUAUBUVHUWBW
      IUUJUXBUULUPLZUXCUUQUPLZUUJUVOGUJEZOGUHFEZBAUPLZUVQUXFUWAUXHUUJWJWKUXIUUJ
      UXIOWLEGOUPLGOWMWNOWOWPGOWQWRWSWTXAWKUUJUVPUXJUUJUVOUVPUVQUVTXBUUJUVRUVOU
      VPUXJXCUTUWABAXDVLXFUUJUVOUVPUVQUVTXGAGOXEXHUUJUXDUWHJQEZBJKLZUXFUXGUSUXE
      UWIUXKUUJXPWKUXLUUJXIWKUXBUULJXJXKXLXMXNXQUUJUURUUNUUQIDZUUKKLZUUKUUNUUQU
      ODZKLZUGZUVEUUJUWDUWGUWTUURUXQUSUWEUWKUXAUUKUUNUUQXOVTUUJUXNUVBUXPUVDUUJU
      XMUVAUUKKUUJUXMCUUMUUQUODZIDZUVAUUJCTEZUUMTEZUUQTEZUXMUXSPUXTUUJXRWKZUUJU
      UMUWJSZUUJUUQUXASZCUUMUUQXSVTUUJUXRUUTCIUUJUULGMHDZNDZUULCGHDZNDZUULCJHDZ
      NDZUODZUUTUXRUUJUYGUULUYHUYJUODZNDZUYLUYMUYFUULNUYHUYJIDZCMHDZPZUYMUYFPZX
      TYAYBUUJUULTEZUYNUYLPZUUJUULUWISZUYSUYHTEZUYJTEZUYTGYCYDYEZJJWEYFZJWEYGZY
      EZUULUYHUYJYHYIRYJUUJUYSUUTUYGPZVUAGTEZUYSMTEZMBYKZUGVUHYCVUJVUKYLYRYMGUU
      LMYNYORUUJUUMUYIUUQUYKUOUUJUYSUUMUYIPZVUAUYSVUIGBYKVULYCYDUULGYPYIRZUUJUY
      SUUQUYKPZVUAUYSJTEJBYKVUNVUEVUFUULJYPYIRZYQYSYTUUAUUBUUJUXOUVCUUKKUUJCUUM
      UUQIDZIDZUXOUVCUUJUXTUYAUYBVUQUXOPUYCUYDUYECUUMUUQUUCVTUUJVUPUUSCIUUJUULU
      YPNDZUYIUYKIDZUUSVUPUUJVURUULUYONDZVUSUYOUYPUULNUYQUYRXTUUDYBUUJUYSVUTVUS
      PZVUAUYSVUBVUCVVAVUDVUGUULUYHUYJUUHYIRYJUUJUYSUUSVURPZVUAUYSVUJVUKVVBYLYR
      UULMYPYIRUUJUUMUYIUUQUYKIVUMVUOYQYSYTUUEUUFUUGUUIXL $.
      $( [30-Apr-2014] $) $( [19-Jan-2008] $)
  $}

  $( Bounds on the cosine of 1.  (Contributed by Paul Chapman, 19-Jan-2008.) $)
  cos1bnd $p |- ( ( 1 / 3 ) < ( cos ` 1 ) /\ ( cos ` 1 ) < ( 2 / 3 ) ) $=
    ( c1 c3 cdiv co clt wbr c2 cmul cmin opreq1i opreq2i 2cn ax-1cn caddc eqtri
    3nn cr wcel cc0 1re ccos cfv cexp nncni nnne0i divreci eqtr4i divcli reccli
    sq1 df-3 dividi divdiri 3eqtr3ri subaddrii cle wa lt01 leidi cioc wb elioc2
    w3a 0re mp2an cos01bnd sylbir mp3an simpli eqbrtrri simpri subadd2i breqtri
    wceq mpbir pm3.2i ) ABCDZAUAUBZEFVRGBCDZEFAGAGUCDZBCDZHDZIDZVQVREWCAVSIDVQW
    BVSAIWBGVQHDVSWAVQGHVTABCUJJZKGBLBPUDZBPUEZUFUGKAVSVQMGBLWEWFUHZBWEWFUIZBBC
    DGANDZBCDAVSVQNDZBWIBCUKJBWEWFULGABLMWEWFUMUNZUOOWCVREFZVRAWAIDZEFZAQRZSAEF
    ZAAUPFZWLWNUQZTURATUSWOWPWQVCZASAUTDRZWRSQRWOWTWSVAVDTSAAVBVEAVFVGVHZVIVJVR
    WMVSEWLWNXAVKWMAVQIDZVSWAVQAIWDKXBVSVNWJAVNWKAVQVSMWHWGVLVOOVMVP $.
    $( [19-Jan-2008] $)

  $( Bounds on the cosine of 2.  (Contributed by Paul Chapman, 19-Jan-2008.) $)
  cos2bnd $p |- ( -u ( 7 / 9 ) < ( cos ` 2 ) /\
                     ( cos ` 2 ) < -u ( 1 / 9 ) ) $=
    ( c7 c9 cdiv co cneg c2 clt wbr c1 cexp cmin wcel cc0 wceq 2cn caddc ax-1cn
    c8 c3 c4 ccos cfv cmul cc wne wa recni 9pos gt0ne0ii pm3.2i divsubdir mp3an
    9re negsubdi2i 7re addassi df-8 opreq1i df-2 opreq2i 3eqtr4ri df-9 subadd2i
    eqtr4i mpbir negeqi eqtr3i dividi 3eqtr3ri divneg divassi 3re nnne0i sqdivi
    mulid1i 3nn sq1 sq3 opreq12i eqtri cos1bnd simpli cle wb nn0ge0i nngt0i 1re
    1nn0 divge0i mp2an 0re cr recoscl ax-mp rereccli lelttri ltleii lt2sqi mpbi
    eqbrtrri resqcli 2re ltmul2i redivcli remulcli ltsub1 fveq2i cos2t breqtrri
    2pos simpri 2nn0 sq2 breqtri 4re mulcomi 4t2e8 8re subaddi 3eqtr2ri eqbrtri
    eqcomi ) ABCDEZFUAUBZGHYDIBCDZEZGHYCFIUAUBZFJDZUCDZIKDZYDGFBCDZIKDZYCYJGYLA
    EZBCDZYCFBKDZBCDZYKBBCDZKDZYNYLFUDLBUDLZYSBMUEZUFZYPYRNOBUMUGZYSYTUUBBUMUHU
    IZUJZFBBUKULYOYMBCBFKDZEYOYMBFUUBOUNUUEAUUEANAFPDZBNUUFRIPDZBAIPDZIPDAIIPDZ
    PDUUGUUFAIIAUOUGZQQUPRUUHIPUQURFUUIAPUSUTVAVBVDBFAUUBOUUJVCVEVFVGURYQIYKKBU
    UBUUCVHZUTVIAUDLYSYTYCYNNUUJUUBUUCABVJULVDYKYIGHZYLYJGHZFYEUCDZYKYIGFIUCDZB
    CDUUNYKFIBOQUUBUUCVKUUOFBCFOVOZURVGYEYHGHZUUNYIGHZISCDZFJDZYEYHGUUTIFJDZSFJ
    DZCDYEISQSVLUGZSVPVMZVNUVAIUVBBCVQVRVSVTUUSYGGHZUUTYHGHZUVEYGFSCDZGHZWAWBZM
    UUSWCHZMYGWCHZUVEUVFWDMIWCHMSGHZUVJIWHWESVPWFZISWGVLWIWJZMYGWKIWLLZYGWLLWGI
    WMWNZUVJUVEMYGGHUVNUVIMUUSYGWKSVLUVDWOZUVPWPWJWQZUUSYGUVQUVPWRWJWSWTMFGHZUU
    QUURWDXJYEYHFBUMUUCWOYGUVPXAZXBXCWNWSWTYKWLLYIWLLZUVOUULUUMWDFBXBUMUUCXDFYH
    XBUVTXEZWGYKYIIXFULWSWTUUOUAUBZYDYJUUOFUAUUPXGIUDLZUWCYJNQIXHWNVGZXIYDYJYFG
    UWEYJRBCDZIKDZYFGYIUWFGHZYJUWGGHZYIFTBCDZUCDZUWFGYHUWJGHZYIUWKGHZYHUVGFJDZU
    WJGUVHYHUWNGHZUVEUVHWAXKUVKMUVGWCHZUVHUWOWDUVRMFWCHUVLUWPFXLWEUVMFSXBVLWIWJ
    YGUVGUVPFSXBVLUVDXDWRWJWSUWNFFJDZUVBCDUWJFSOUVCUVDVNUWQTUVBBCXMVRVSVTXNUVSU
    WLUWMWDXJYHUWJFUVTTBXOUMUUCXDXBXCWNWSFTUCDZBCDUWKUWFFTBOTXOUGZUUBUUCVKUWRRB
    CUWRTFUCDRFTOUWSXPXQVTURVGXNUWAUWFWLLUVOUWHUWIWDUWBRBXRUMUUCXDWGYIUWFIXFULW
    SUWFYQKDZUWGYFYQIUWFKUUKUTYFIEZBCDZRBKDZBCDZUWTUWDYSYTYFUXBNQUUBUUCIBVJULUX
    CUXABCBRKDZEUXCUXABRUUBRXRUGZUNUXEIUXEINUUGBNBUUGVBYBBRIUUBUXFQXSVEVFVGURRU
    DLYSUUAUXDUWTNUXFUUBUUDRBBUKULXTVGXNYAUJ $.
    $( [19-Jan-2008] $)

  $( The sine of a positive real number less than or equal to 1 is positive.
     (Contributed by Paul Chapman, 19-Jan-2008.) $)
  sin01gt0 $p |- ( A e. ( 0 (,] 1 ) -> 0 < ( sin ` A ) ) $=
    ( cc0 c1 co wcel c3 cexp clt wbr cr cle w3a 0re 1re 3nn 3re syl wi syl2anc
    c2 cioc cdiv cmin csin cfv cn0 wb elioc2 simp1bi nnnn0i reexpcl sylancl wne
    mp2an nnne0i redivcl mp3an23 cz nnzi expgt0 mp3an2 3adant3 sylbi lt01 caddc
    3pos 2pos 2re ltadd1i mpbi ax-1cn addid2i df-3 eqcomi 3brtr3i ltdiv2 expcom
    wa mpbii mp3an12 com12 sylc cc wceq div1 breqtrd cuz 1nn0 a1i eluz1i ltleii
    recnd mpbir2an simp2bi ltle sylancr simp3bi expword2i syl32anc exp1 ltletrd
    1z mpd posdif mpbid sin01bnd simpld resubcl resincl axlttrn mp3an1 mp2and )
    ABCUADEZBAAFGDZFUBDZUCDZHIZXPAUDUEZHIZBXRHIZXMXOAHIZXQXMXOXNAXMXNJEZXOJEZXM
    AJEZFUFEYBXMYDBAHIZACKIZBJEZCJEZXMYDYEYFLZUGMNBCAUHUNZUIZFOUJAFUKULZYBFJEZF
    BUMYCPFOUOXNFUPUQQZYLYKXMXOXNCUBDZXNHXMYBBXNHIZXOYOHIZYLXMYIYPYJYDYEYPYFYDF
    UREZYEYPFOUSZAFUTVAVBVCYHYMYBYPYQRNPYPYHYMYBLZYQBCHIZBFHIZYPYTYQRVDVFYTUUAU
    UBYPLZYQYTUUCVRCFHIYQBCVEDZTCVEDZCFHBTHIUUDUUEHIVGBTCMVHNVIVJCVKVLFUUEVMVNV
    OZCFXNVPVSVQVTWAVTWBXMXNWCEYOXNWDXMXNYLWLXNWEQWFXMXNACGDZAKXMYDCUFEZFCWGUEE
    ZBAKIZYFXNUUGKIYKUUHXMWHWIUUIXMUUIYRCFKICFXBWJYSCFNPUUFWKWMWIXMYEUUJXMYDYEY
    FYJWNXMYGYDYEUUJRMYKBAWOWPXCXMYDYEYFYJWQACFWRWSXMAWCEUUGAWDXMAYKWLAWTQWFXAX
    MYCYDYAXQUGYNYKXOAXDSXEXMXSXRAHIAXFXGXMXPJEZXRJEZXQXSVRXTRZXMYDYCUUKYKYNAXO
    XHSXMYDUULYKAXIQYGUUKUULUUMMBXPXRXJXKSXL $.
    $( [19-Jan-2008] $)

  $( The cosine of a positive real number less than or equal to 1 is positive.
     (Contributed by Paul Chapman, 19-Jan-2008.) $)
  cos01gt0 $p |- ( A e. ( 0 (,] 1 ) -> 0 < ( cos ` A ) ) $=
    ( cc0 c1 co wcel c2 c3 cdiv cmul clt wbr cle cc cr wb 1re syl wa 3re 2re cz
    cioc cexp cmin ccos cfv w3a 0re elioc2 mp2an simp1bi resqcl recnd wne recni
    wceq 2cn 3nn nnne0i pm3.2i div12 mp3an13 2z expgt0 mp3an2 sylbi caddc ltp1i
    3adant3 df-3 breqtrri ltdiv1ii dividi breqtri redivcli ltmul2 mp3an12 mpbii
    3pos mpbi syl2anc mulid1 breqtrd eqbrtrd ltle imdistani le2sq2 mpanr1 sylan
    wi mpan 3impa syl6breq redivcl mp3an23 remulcl sylancr ltletr mp3an3 mp2and
    sq1 posdif sylancl mpbid cos01bnd simpld resubcl recoscl axlttrn mp3an1 ) A
    BCUBDEZBCFAFUCDZGHDZIDZUDDZJKZXOAUEUFZJKZBXQJKZXKXNCJKZXPXKXNXLJKZXLCLKZXTX
    KXNXLFGHDZIDZXLJXKXLMEZXNYDUPZXKXLXKANEZXLNEZXKYGBAJKZACLKZBNEZCNEZXKYGYIYJ
    UGZOUHPBCAUIUJZUKZAULQZUMZFMEYEGMEZGBUNZRYFUQYRYSGSUOZGURUSZUTFXLGVAVBQXKYD
    XLCIDZXLJXKYHBXLJKZYDUUBJKZYPXKYMUUCYNYGYIUUCYJYGFUAEYIUUCVCAFVDVEVIVFYHUUC
    RZYCCJKZUUDYCGGHDZCJFGJKYCUUGJKFFCVGDGJFTVHVJVKFGGTSSVSVLVTGYTUUAVMVNYCNEYL
    UUEUUFUUDOFGTSUUAVOPYCCXLVPVQVRWAXKYEUUBXLUPYQXLWBQWCWDXKXLCFUCDZCLXKYMXLUU
    HLKZYNYGYIYJUUIYGYIRYGBALKZRZYJUUIYGYIUUJYKYGYIUUJWJUHBAWEWKWFUUKYLYJUUIPAC
    WGWHWIWLVFXAWMXKXNNEZYHYAYBRXTWJZXKFNEXMNEZUULTXKYHUUNYPYHGNEYSUUNSUUAXLGWN
    WOQFXMWPWQZYPUULYHYLUUMPXNXLCWRWSWAWTXKUULYLXTXPOUUOPXNCXBXCXDXKXRXQCXMUDDJ
    KAXEXFXKXONEZXQNEZXPXRRXSWJZXKYLUULUUPPUUOCXNXGWQXKYGUUQYOAXHQYKUUPUUQUURUH
    BXOXQXIXJWAWT $.
    $( [19-Jan-2008] $)

  $( The sine of a positive real number less than or equal to 2 is positive.
     (Contributed by Paul Chapman, 19-Jan-2008.) $)
  sin02gt0 $p |- ( A e. ( 0 (,] 2 ) -> 0 < ( sin ` A ) ) $=
    ( cc0 c2 co wcel cmul csin cfv clt wbr c1 cr cle wa 2re 2pos wb syl wi cc
    cioc cdiv w3a rehalfcl 3ad2ant1 divgt0 mpanr12 3adant3 pm3.2i lediv1 biimpa
    ccos mp3an23 2cn 2ne0 dividi syl6breq 3adant2 3jca 0re elioc2 mp2an 3imtr4i
    1re sin01gt0 cos01gt0 sylbi resincl recoscl axmulgt0 syl2anc mp2and remulcl
    mpan mpani 3syl mpd recnd sin2t breqtrrd simp1bi wne divcan2 fveq2d breqtrd
    wceq ) ABCUADEZBCACUBDZFDZGHZAGHIWGBCWHGHZWHULHZFDZFDZWJIWGBWMIJZBWNIJZWGBW
    KIJZBWLIJZWOWGWHBKUADEZWQALEZBAIJZACMJZUCZWHLEZBWHIJZWHKMJZUCZWGWSXCXDXEXFW
    TXAXDXBAUDUEZWTXAXEXBWTXANCLEZBCIJZXEOPACUFUGUHWTXBXFXAWTXBNWHCCUBDZKMWTXBW
    HXKMJZWTXIXIXJNXBXLQOXIXJOPUIACCUJUMUKCUNUOUPUQURUSBLEZXIWGXCQUTOBCAVAVBZXM
    KLEWSXGQUTVDBKWHVAVBVCZWHVERWGWSWRXOWHVFRWGXDWQWRNWOSZWGXCXDXNXHVGZXDWKLEZW
    LLEZXPWHVHZWHVIZWKWLVJVKRVLWGXDWMLEZWOWPSXQXDXRXSYBXTYAWKWLVMVKYBXJWOWPPXIY
    BXJWONWPSOCWMVJVNVOVPVQWGWHTEWJWNWFWGWHXQVRWHVSRVTWGWIAGWGATEZWIAWFZWGAWGWT
    XAXBXNWAVRYCCTECBWBYDUNUOACWCUMRWDWE $.
    $( [19-Jan-2008] $)

  $( The signs of the sine and cosine of 1.  (Contributed by Paul Chapman,
     19-Jan-2008.) $)
  sincos1sgn $p |- ( 0 < ( sin ` 1 ) /\ 0 < ( cos ` 1 ) ) $=
    ( c1 cc0 cioc co wcel csin cfv clt wbr ccos wa cr cle 1re lt01 leidi w3a wb
    0re elioc2 mp2an biimpri mp3an sin01gt0 cos01gt0 jca ax-mp ) ABACDEZBAFGHIZ
    BAJGHIZKALEZBAHIZAAMIZUHNOANPUHUKULUMQZBLEUKUHUNRSNBAATUAUBUCUHUIUJAUDAUEUF
    UG $.
    $( [19-Jan-2008] $)

  $( The signs of the sine and cosine of 2.  (Contributed by Paul Chapman,
     19-Jan-2008.) $)
  sincos2sgn $p |- ( 0 < ( sin ` 2 ) /\ ( cos ` 2 ) < 0 ) $=
    ( cc0 c2 csin cfv clt wbr ccos co wcel cr 2re 0re mp2an ax-mp cdiv cneg 9re
    wb c9 9pos cioc cle 2pos leidi w3a elioc2 biimpri mp3an sin02gt0 c1 cos2bnd
    c7 simpri recgt0ii gt0ne0ii rereccli lt0neg2 recoscl renegcli lttri pm3.2i
    mpbi ) ABCDEFZBGDZAEFZBABUAHIZVCBJIZABEFZBBUBFZVFKUCBKUDVFVGVHVIUEZAJIVGVFV
    JRLKABBUFMUGUHBUINVDUJSOHZPZEFZVLAEFZVEULSOHPVDEFVMUKUMAVKEFZVNSQTUNVKJIVOV
    NRSQSQTUOUPZVKUQNVBVDVLAVGVDJIKBURNVKVPUSLUTMVA $.
    $( [19-Jan-2008] $)

  $( The sine of 4 is negative.  (Contributed by Paul Chapman, 19-Jan-2008.) $)
  sin4lt0 $p |- ( sin ` 4 ) < 0 $=
    ( c4 csin cfv c2 cmul co cc0 clt wcel 2cn ax-mp wbr sincos2sgn cr wa wb 2re
    0re pm3.2i ltmul2 ccos 2t2e4 fveq2i wceq sin2t eqtr3i simpri recoscl simpli
    cc resincl mp3an mpbi recni mul01i breqtri remulcli 2pos eqbrtri ) ABCZDDBC
    ZDUACZEFZEFZGHDDEFZBCZUTVDVEABUBUCDUJIVFVDUDJDUEKUFVDDGEFZGHVCGHLZVDVGHLZVC
    VAGEFZGHVBGHLZVCVJHLZGVAHLZVKMUGVBNIZGNIZVANIZVMOVKVLPDNIZVNQDUHKZRVPVMVQVP
    QDUKKZVMVKMUISVBGVATULUMVAVAVSUNUOUPVCNIVOVQGDHLZOVHVIPVAVBVSVRUQRVQVTQURSV
    CGDTULUMDJUOUPUS $.
    $( [19-Jan-2008] $)

  $( The absolute value of the exponential function of an imaginary number is
     one.  Equation 48 of [Rudin] p. 167.  (Contributed by Jason Orendorff,
     9-Feb-2007.) $)
  absefi $p |- ( A e. RR -> ( abs ` ( exp ` ( _i x. A ) ) ) = 1 ) $=
    ( cr wcel ci cmul co cfv cabs caddc c2 cexp csqr c1 wceq syl fveq2d syl2anc
    cc resqcl recnd ce ccos csin efival recoscl resincl absreim addcom sincossq
    recn eqtrd sqr1 syl6eq 3eqtrd ) ABCZDAEFUAGZHGAUBGZDAUCGZEFIFZHGZUQJKFZURJK
    FZIFZLGZMUOUPUSHUOARCZUPUSNAUJZAUDOPUOUQBCZURBCZUTVDNAUEZAUFZUQURUGQUOVDMLG
    MUOVCMLUOVCVBVAIFZMUOVARCVBRCVCVKNUOVAUOVGVABCVIUQSOTUOVBUOVHVBBCVJURSOTVAV
    BUHQUOVEVKMNVFAUIOUKPULUMUN $.
    $( [9-Feb-2007] $)

  $( The absolute value of the exponential function is the exponential function
     of the real part.  (Contributed by Paul Chapman, 13-Sep-2007.) $)
  absef $p |- ( A e. CC -> ( abs ` ( exp ` A ) ) = ( exp ` ( Re ` A ) ) ) $=
    ( cc wcel ce cfv cabs c1 cmul co ci wceq fveq2 syl cr sylancr syl2anc eqtrd
    recn cc0 wbr cre cim caddc replim recl ax-icn imcl mulcl reefcl efcl absmul
    efadd absefi opreq2 abscl 3syl mulid1 cle clt efgt0 0reALT ltle mpd absid
    wi ) ABCZADEZFEZAUAEZDEZFEZVJVFVHVKGHIZVKVFVHVKJAUBEZHIZDEZFEZHIZVLVFVHVJVO
    HIZFEZVQVFVGVRKVHVSKVFVGVIVNUCIZDEZVRVFAVTKVGWAKAUDAVTDLMVFVIBCZVNBCZWAVRKV
    FVINCZWBAUEZVIRMVFJBCVMBCZWCUFVFVMNCZWFAUGZVMRMJVMUHOZVIVNULPQVGVRFLMVFVJBC
    ZVOBCZVSVQKVFVJNCZWJVFWDWLWEVIUIMZVJRMZVFWCWKWIVNUJMVJVOUKPQVFVPGKZVQVLKVFW
    GWOWHVMUMMVPGVKHUNMQVFVKBCZVLVKKVFWJVKNCWPWNVJUOVKRUPVKUQMQVFWLSVJURTZVKVJK
    WMVFSVJUSTZWQVFWDWRWEVIUTMVFSNCWLWRWQVEVAWMSVJVBOVCVJVDPQ $.
    $( [13-Sep-2007] $)

  $( A number is real iff its imaginary exponential has absolute value one. $)
  absefib $p |- ( A e. CC
          -> ( A e. RR <-> ( abs ` ( exp ` ( _i x. A ) ) ) = 1 ) ) $=
    ( cc wcel ci cmul co ce cfv c1 wceq cneg cre ax-icn mulcl syl caddc syl2anc
    cr eqtrd cc0 cabs mpan absef replim recl recnd sylancr addcom opreq2d adddi
    cim mp3an1 mulass mp3an12 opreq1i mulm1 syl5eq eqtr3d opreq1d 3eqtrd fveq2d
    imcl ixi renegcl crre eqeq1d wb negeq0 reim0b ef0 eqeq2i 0re reef11 sylancl
    syl5bbr 3bitr4rd bitr2d ) ABCZDAEFZGHUAHZIJAUKHZKZGHZIJZARCZVRVTWCIVRVTVSLH
    ZGHZWCVRVSBCZVTWGJDBCZVRWHMDANUBVSUCOVRWFWBGVRWFWBDALHZEFZPFZLHZWBVRVSWLLVR
    VSDDWAEFZWJPFZEFZDWNEFZWKPFZWLVRAWODEVRAWJWNPFZWOAUDVRWJBCZWNBCZWSWOJVRWJAU
    EZUFZVRWIWABCZXAMVRWAAVBZUFZDWANUGZWJWNUHQSUIVRXAWTWPWRJZXGXCWIXAWTXHMDWNWJ
    UJULQVRWQWBWKPVRDDEFZWAEFZWQWBVRXDXJWQJZXFWIWIXDXKMMDDWAUMUNOVRXJIKZWAEFZWB
    XIXLWAEVCUOVRXDXMWBJXFWAUPOUQURUSUTVAVRWBRCZWJRCWMWBJVRWARCXNXEWAVDOZXBWBWJ
    VEQSVASVFVRWATJZWBTJZWEWDVRXDXPXQVGXFWAVHOAVIWDWCTGHZJZVRXQXRIWCVJVKVRXNTRC
    XSXQVGXOVLWBTVMVNVOVPVQ $.
    $( [25-Mar-2009] $) $( [21-Aug-2008] $)

  $( MAY BE REVISED - make obsolete $)
  $( A number whose imaginary exponential is one is real. $)
  efieq1re $p |- ( ( A e. CC /\ ( exp ` ( _i x. A ) ) = 1 ) -> A e. RR ) $=
    ( cc wcel ci cmul co ce cfv c1 wceq cabs caddc recnd ax-icn syl2anc syl cc0
    cr wbr 0re cim cneg cre replim opreq2d recl imcl mulcl sylancr adddi mp3an1
    mulass mp3an12 ixi opreq1i mulm1 syl5eq eqtr3d 3eqtrd fveq2d renegcl eqeq1d
    efadd eqtrd efcl absmul absefi cle reefcl clt efgt0 wi ltle mpan sylc absid
    opreq12d mulid2 3eqtrrd fveq2 sylan9eq ex sylbid negeq0 reim0b ef0 1re lt01
    wb ltleii absidi ax-mp eqtr4i eqeq2i reef11 sylancl syl5bbr 3bitr4rd sylibd
    imp ) ABCZDAEFZGHZIJZARCZXAXDAUAHZUBZGHZIKHZJZXEXAXDDAUCHZEFZGHZXHEFZIJZXJX
    AXCXNIXAXCXLXGLFZGHZXNXAXBXPGXAXBDXKDXFEFZLFZEFZXLDXREFZLFZXPXAAXSDEAUDUEXA
    XKBCZXRBCZXTYBJZXAXKAUFZMZXADBCZXFBCZYDNXAXFAUGZMZDXFUHUIYHYCYDYENDXKXRUJUK
    OXAYAXGXLLXADDEFZXFEFZYAXGXAYIYMYAJZYKYHYHYIYNNNDDXFULUMPXAYMIUBZXFEFZXGYLY
    OXFEUNUOXAYIYPXGJYKXFUPPUQURUEUSUTXAXLBCZXGBCZXQXNJXAYHYCYQNYGDXKUHUIZXAXGX
    AXFRCXGRCZYJXFVAPZMZXLXGVCOVDVBXAXOXJXAXOXHXNKHZXIXAUUCXMKHZXHKHZEFZIXHEFZX
    HXAXMBCZXHBCZUUCUUFJXAYQUUHYSXLVEPXAYRUUIUUBXGVEPZXMXHVFOXAUUDIUUEXHEXAXKRC
    UUDIJYFXKVGPXAXHRCZQXHVHSZUUEXHJXAYTUUKUUAXGVIPZXAUUKQXHVJSZUULUUMXAYTUUNUU
    AXGVKPQRCZUUKUUNUULVLTQXHVMVNVOXHVPOVQXAUUIUUGXHJUUJXHVRPVSXNIKVTWAWBWCXAXF
    QJZXGQJZXEXJXAYIUUPUUQWIYKXFWDPAWEXJXHQGHZJZXAUUQUURXIXHUURIXIWFQIVHSXIIJQI
    TWGWHWJIWGWKWLWMWNXAYTUUOUUSUUQWIUUATXGQWOWPWQWRWSWT $.
    $( [25-Mar-2009] $) $( [21-Aug-2008] $)

  ${
    $d x k $.  $d x N $.  $d A x $.  $d A k $.
    $( De Moivre's Formula.  Shorter proof of ~ demoivreALT using the
       exponential function. $)
    demoivre $p |- ( ( A e. CC /\ N e. ZZ ) ->
                  ( ( ( cos ` A ) + ( _i x. ( sin ` A ) ) ) ^ N ) =
                 ( ( cos ` ( N x. A ) ) + ( _i x. ( sin ` ( N x. A ) ) ) ) ) $=
      ( cc wcel cz wa ci cmul co ce cexp ccos csin caddc wceq ax-icn mulcl mpan
      cfv efival efexp sylan zcn mul12 mp3an2 fveq2d eqtrd ancoms sylan2 adantr
      syl opreq1d 3eqtr3rd ) ACDZBEDZFBGAHIZHIZJSZUPJSZBKIZBAHIZLSGVAMSHINIZALS
      GAMSHINIZBKIZUNUPCDZUOURUTOGCDZUNVEPGAQRUPBUAUBUOUNBCDZURVBOZBUCVGUNVHVGU
      NFZURGVAHIZJSZVBVIUQVJJVGVFUNUQVJOPBGAUDUEUFVIVACDVKVBOBAQVATUKUGUHUIUNUT
      VDOUOUNUSVCBKATULUJUM $.
      $( [24-Jul-2007] $)
    $( De Moivre's Formula.  Proof by induction given at
       ~ http://en.wikipedia.org/wiki/De&#x5F;Moivre&#x27;s&#x5F;formula , but
       restricted to nonnegative integer powers.  (Contributed by Steve
       Rodriguez, 10-Nov-2006.) $)
    demoivreALT $p |- ( ( A e. CC /\ N e. NN0 ) ->
                  ( ( ( cos ` A ) + ( _i x. ( sin ` A ) ) ) ^ N ) =
                 ( ( cos ` ( N x. A ) ) + ( _i x. ( sin ` ( N x. A ) ) ) ) ) $=
      ( wcel cc ccos cfv ci csin cmul co caddc cexp wceq cc0 c1 fveq2d opreq12d
      opreq2d ax-icn syl2anc vx vk cn0 cv wi opreq2 opreq1 eqeq12d imbi2d coscl
      sincl mulcl sylancr addcl exp0 syl cos0 syl6eq sin0 mul01i ax-1cn addid1i
      mul02 eqtr4d expp1 sylan ancoms adantr adantl nn0cn sinadd mulcom opreq1d
      wa sylancom addcom 3eqtr2d w3a adddir mulid2 3ad2ant3 syl3an1 mp3an2 cmin
      eqtrd cneg mpan 3syl jca muladd syl21anc jctil opreq1i mul12 adddi mp3an1
      mul4 ixi 3eqtrd mulm1 negsub cosadd 3eqtr4rd exp31 a2d nn0ind impcom ) BU
      CCADCZAEFZGAHFZIJZKJZBLJZBAIJZEFZGXNHFZIJZKJZMZXHXLUAUDZLJZXTAIJZEFZGYBHF
      ZIJZKJZMZUEXHXLNLJZNAIJZEFZGYIHFZIJZKJZMZUEXHXLUBUDZLJZYOAIJZEFZGYQHFZIJZ
      KJZMZUEXHXLYOOKJZLJZUUCAIJZEFZGUUEHFZIJZKJZMZUEXHXSUEUAUBBXTNMZYGYNXHUUKY
      AYHYFYMXTNXLLUFUUKYCYJYEYLKUUKYBYIEXTNAIUGZPUUKYDYKGIUUKYBYIHUULPRQUHUIXT
      YOMZYGUUBXHUUMYAYPYFUUAXTYOXLLUFUUMYCYRYEYTKUUMYBYQEXTYOAIUGZPUUMYDYSGIUU
      MYBYQHUUNPRQUHUIXTUUCMZYGUUJXHUUOYAUUDYFUUIXTUUCXLLUFUUOYCUUFYEUUHKUUOYBU
      UEEXTUUCAIUGZPUUOYDUUGGIUUOYBUUEHUUPPRQUHUIXTBMZYGXSXHUUQYAXMYFXRXTBXLLUF
      UUQYCXOYEXQKUUQYBXNEXTBAIUGZPUUQYDXPGIUUQYBXNHUURPRQUHUIXHYHOYMXHXLDCZYHO
      MXHXIDCZXKDCZUUSAUJZXHGDCZXJDCZUVASAUKZGXJULUMZXIXKUNTZXLUOUPXHYMONKJOXHY
      JOYLNKXHYJNEFOXHYINEAVCZPUQURXHYLGNIJNXHYKNGIXHYKNHFNXHYINHUVHPUSURRGSUTU
      RQOVAVBURVDYOUCCZXHUUBUUJUVIXHUUBUUJUVIXHVNZUUBVNUUDYPXLIJZUUAXLIJZUUIUVJ
      UUDUVKMZUUBXHUVIUVMXHUUSUVIUVMUVGXLYOVEVFVGVHUUBUVKUVLMUVJYPUUAXLIUGVIUVJ
      UVLUUIMUUBUVJYQAKJZEFZGUVNHFZIJZKJUVOGYRXJIJZXIYSIJZKJZIJZKJZUUIUVLUVJUVQ
      UWAUVOKUVJUVPUVTGIUVJUVPYSXIIJZUVRKJZUVSUVRKJZUVTUVIXHYQDCZUVPUWDMUVIYODC
      ZXHUWFYOVJZYOAULVFZYQAVKVOUVJUVSUWCUVRKUVJUUTYSDCZUVSUWCMXHUUTUVIUVBVIZUV
      JUWFUWJUWIYQUKZUPZXIYSVLTVMUVJUVSDCZUVRDCZUWEUVTMUVJUUTUWJUWNUWKUWMXIYSUL
      TZUVJYRDCZUVDUWOUVJUWFUWQUWIYQUJUPZXHUVDUVIUVEVIZYRXJULTZUVSUVRVPTVQRRUVJ
      UUFUVOUUHUVQKUVJUUEUVNEUVIODCZXHUUEUVNMZVAUVIUWGUXAXHUXBUWHUWGUXAXHVRUUEY
      QOAIJZKJZUVNYOOAVSXHUWGUXDUVNMUXAXHUXCAYQKAVTRWAWEWBWCZPUVJUUGUVPGIUVJUUE
      UVNHUXEPRQUVJUVLYRXIIJZXJYSIJZWDJZUWAKJZUWBUVJUVLUXFOWFZUXGIJZKJZUWAKJZUX
      FUXGWFZKJZUWAKJUXIUVJUVLUXFXKYTIJZKJZYRXKIJZXIYTIJZKJZKJZUXLUXTKJUXMUVJUW
      QYTDCZUUTUVAVNZUVLUYAMUWRUVJUWFUWJUYBUWIUWLUVCUWJUYBSGYSULWGWHXHUYCUVIXHU
      UTUVAUVBUVFWIVIYRYTXIXKWJWKUVJUXQUXLUXTKUVJUXPUXKUXFKUVJUVCUVDVNZUVCUWJVN
      ZUXPUXKMUVJUVDUVCUWSSWLUVJUWJUVCUWMSWLUYDUYEVNUXPGGIJZUXGIJUXKGXJGYSWQUYF
      UXJUXGIWRWMURTRVMUVJUXTUWAUXLKUVJUXTGUVRIJZGUVSIJZKJZUWAUVJUXRUYGUXSUYHKU
      VJUWQUVDUXRUYGMZUWRUWSUWQUVCUVDUYJSYRGXJWNWCTUVJUUTUWJUXSUYHMZUWKUWMUUTUV
      CUWJUYKSXIGYSWNWCTQUVJUWOUWNUWAUYIMZUWTUWPUVCUWOUWNUYLSGUVRUVSWOWPTVDRWSU
      VJUXLUXOUWAKUVJUXKUXNUXFKUVJUXGDCZUXKUXNMUVJUVDUWJUYMUWSUWMXJYSULTZUXGWTU
      PRVMUVJUXOUXHUWAKUVJUXFDCZUYMUXOUXHMUVJUWQUUTUYOUWRUWKYRXIULTUYNUXFUXGXAT
      VMWSUVJUVOUXHUWAKUVJUVOUXFYSXJIJZWDJZUXHUVIXHUWFUVOUYQMUWIYQAXBVOUVJUYPUX
      GUXFWDUVJUWJUVDUYPUXGMUWMUWSYSXJVLTRWEVMVDXCVHWSXDXEXFXG $.
      $( [9-Nov-2006] $)

  $}

