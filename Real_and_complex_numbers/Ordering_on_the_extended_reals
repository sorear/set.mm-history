$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Ordering on the extended reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Membership in the set of extended reals. $)
  elxr $p |- ( A e. RR* <-> ( A e. RR \/ A = +oo \/ A = -oo ) ) $=
    ( cxr wcel cr cpnf cmnf cpr cun wo wceq w3o df-xr eleq2i elun pnfxr
    elisseti mnfxr elpr2 orbi2i 3orass bitr4 3bitr ) ABCADEFGZHZCADCZAUCCZIZUEA
    EJZAFJZKZBUDALMADUCNUGUEUHUIIZIUJUFUKUEAEFEBOPFBQPRSUEUHUITUAUB $.
    $( [17-Oct-2005] $) $( [14-Oct-2005] $)

  $( Plus and minus infinity are distinguished elements of ` RR* ` . $)
  pnfnemnf $p |- +oo =/= -oo $=
    ( cpnf cmnf wne wceq wn cc csn wcel eirr axcnex snid eleq2 mpbiri mto
    df-pnf df-mnf eqeq12i mtbir df-ne mpbir ) ABCABDZEUAFFGZDZUCFFHZFIUCUDFUBHF
    JKFUBFLMNAFBUBOPQRABST $.
    $( [18-Oct-2005] $) $( [14-Oct-2005] $)

  $( No (finite) real equals plus infinity. $)
  renepnft $p |- ( A e. RR -> A =/= +oo ) $=
    ( cr wcel cpnf wceq wn wne wnel pnfnre df-nel mpbi nelneq mpan2 df-ne
    sylibr ) ABCZADEFZADGPDBCFZQDBHRIDBJKADBLMADNO $.
    $( [16-Oct-2005] $) $( [14-Oct-2005] $)

  $( No real equals minus infinity. $)
  renemnft $p |- ( A e. RR -> A =/= -oo ) $=
    ( cr wcel cmnf wceq wn wne wnel minfnre df-nel mpbi nelneq mpan2 df-ne
    sylibr ) ABCZADEFZADGPDBCFZQDBHRIDBJKADBLMADNO $.
    $( [26-Oct-2005] $) $( [14-Oct-2005] $)

  $( The reals and the infinities are disjoint. $)
  renfdisj $p |- ( RR i^i { +oo , -oo } ) = (/) $=
    ( cr cpnf cmnf cpr cin csn cun c0 df-pr ineq2i wceq wa wcel wn eqid wne
    renepnft df-ne sylib mt2 disjsn mpbir renemnft pm3.2i undisj2 mpbi eqtr )
    ABCDZEABFZCFZGZEZHUHUKABCIJAUIEHKZAUJEHKZLULHKUMUNUMBAMZNUOBBKZBOUOBBPUPNBQ
    BBRSTABUAUBUNCAMZNUQCCKZCOUQCCPURNCUCCCRSTACUAUBUDAUIUJUEUFUG $.
    $( [27-Oct-2005] $) $( [25-Oct-2005] $)

  ${
    $d x A $.
    $( The three (non-exclusive) possibilities implied by a subset of extended
       reals. $)
    ssxr $p |- ( A (_ RR* -> ( A (_ RR \/ +oo e. A \/ -oo e. A ) ) $=
      ( vx cxr wss cr cpnf wcel cmnf wo w3o wn cv cpr wral cin c0 wceq cun
      disjssun df-xr uncom eqtr sseq2i syl5bb biimpcd disj syl5ibr con3d wrex
      dfrex2 visset elpr rexbii bitr3 r19.43 wa wex df-rex exancom pnfxr
      elisseti eleq1 ceqsexv 3bitr mnfxr orbi12i syl6ib orrd 3orass sylibr ) AC
      DZAEDZFAGZHAGZIZIVLVMVNJVKVLVOVKVLKBLZFHMZGZKBANZKZVOVKVSVLVKAVQOPQZVLVSW
      AVKVLWAAVQERZDVLVKAVQESCWBACEVQRWBTEVQUAUBUCUDUEBAVQUFUGUHVTVPFQZVPHQZIZB
      AUIZWCBAUIZWDBAUIZIVOVTVRBAUIWFVRBAUJVRWEBAVPFHBUKULUMUNWCWDBAUOWGVMWHVNW
      GVPAGZWCUPBUQWCWIUPBUQVMWCBAURWIWCBUSWIVMBFFCUTVAVPFAVBVCVDWHWIWDUPBUQWDW
      IUPBUQVNWDBAURWIWDBUSWIVNBHHCVEVAVPHAVBVCVDVFVDVGVHVLVMVNVIVJ $.
      $( [10-Nov-2005] $) $( [25-Oct-2005] $)
  $}

  $( The extended real 'less than' is irreflexive. $)
  xrltnrt $p |- ( A e. RR* -> -. A < A ) $=
    ( cxr wcel cr cpnf wceq cmnf w3o clt wbr wn elxr ltnrt wa cltrr wo wnel
    pnfnre df-nel mpbi intnan intnanr wne pnfnemnf df-ne pm3.2ni wb pnfxr
    ltxrt mp2an mtbir breq12 anidms mtbiri minfnre necom mnfxr 3jaoi sylbi ) AB
    CADCZAEFZAGFZHAAIJZKZALUTVDVAVBAMVAVCEEIJZVEEDCZVFNZEEOJZNZEGFZEEFZNZPZVFVK
    NZVJVFNZPZPZVMVPVIVLVGVHVFVFEDQVFKREDSTZUAUBVJVKEGUCZVJKUDEGUETUBUFVNVOVFVK
    VRUBVFVJVRUAUFUFEBCZVTVEVQUGUHUHEEUIUJUKVAVCVEUGAEAEIULUMUNVBVCGGIJZWAGDCZW
    BNZGGOJZNZGGFZGEFZNZPZWBWGNZWFWBNZPZPZWIWLWEWHWCWDWBWBGDQWBKUOGDSTZUAUBWGWF
    GEUCZWGKVSWOUDEGUPTGEUETUAUFWJWKWBWGWNUBWBWFWNUAUFUFGBCZWPWAWMUGUQUQGGUIUJU
    KVBVCWAUGAGAGIULUMUNURUS $.
    $( [18-Oct-2005] $) $( [14-Oct-2005] $)

  $( Any (finite) real is less than plus infinity. $)
  ltpnft $p |- ( A e. RR -> A < +oo ) $=
    ( cr wcel cpnf clt wbr wa cltrr cmnf wceq wo eqid jctr orc olc 3syl cxr wb
    rexrt pnfxr ltxrt mpan2 syl mpbird ) ABCZADEFZUEDBCZGADHFGAIJZDDJZGKZUEUIGZ
    UHUGGZKZKZUEUKUMUNUEUIDLMUKULNUMUJOPUEAQCZUFUNRZASUODQCUPTADUAUBUCUD $.
    $( [19-Oct-2005] $) $( [14-Oct-2005] $)

  $( Minus infinity is less than any (finite) real. $)
  mnfltt $p |- ( A e. RR -> -oo < A ) $=
    ( cr wcel cmnf clt wbr wa cltrr wceq cpnf wo eqid jctl olc 3syl cxr wb
    rexrt mnfxr ltxrt mpan syl mpbird ) ABCZDAEFZDBCZUDGDAHFGDDIZAJIZGKZUFUHGZU
    GUDGZKZKZUDUKULUMUDUGDLMUKUJNULUINOUDAPCZUEUMQZARDPCUNUOSDATUAUBUC $.
    $( [17-Oct-2005] $) $( [14-Oct-2005] $)

  $( Minus infinity is less than plus infinity. $)
  mnfltpnf $p |- -oo < +oo $=
    ( cmnf cpnf clt wbr cr wcel wa cltrr wceq wo eqid olc mp2an orc ax-mp cxr
    wb mnfxr pnfxr ltxrt mpbir ) ABCDZAEFZBEFZGABHDGZAAIZBBIZGZJZUCUGGUFUDGJZJZ
    UIUKUFUGUIAKBKUHUELMUIUJNOAPFBPFUBUKQRSABTMUA $.
    $( [19-Oct-2005] $) $( [14-Oct-2005] $)

  $( Minus infinity is less than an extended real that is either real or plus
     infinity. $)
  mnfltxrt $p |- ( ( A e. RR \/ A = +oo ) -> -oo < A ) $=
    ( cr wcel cmnf clt wbr cpnf wceq mnfltt mnfltpnf breq2 mpbiri jaoi ) ABCDAE
    FZAGHZAIONDGEFJAGDEKLM $.
    $( [26-Feb-2006] $) $( [2-Feb-2006] $)

  $( No extended real is greater than plus infinity. $)
  pnfnltt $p |- ( A e. RR* -> -. +oo < A ) $=
    ( cxr wcel cpnf clt wbr cr wa cltrr cmnf wceq wo wnel wn pnfnre df-nel
    mpbi intnanr wne pnfnemnf df-ne pm3.2ni wb pnfxr ltxrt mpan mtbiri ) ABCZDA
    EFZDGCZAGCZHZDAIFZHZDJKZADKZHZLZUJUPHZUOUKHZLZLZURVAUNUQULUMUJUKDGMUJNODGPQ
    ZRRUOUPDJSUONTDJUAQZRUBUSUTUJUPVCRUOUKVDRUBUBDBCUHUIVBUCUDDAUEUFUG $.
    $( [19-Oct-2005] $) $( [15-Oct-2005] $)

  $( No extended real is less than minus infinity. $)
  nltmnft $p |- ( A e. RR* -> -. A < -oo ) $=
    ( cxr wcel cmnf clt wbr cr wa cltrr wceq cpnf wo wnel wn minfnre df-nel
    mpbi intnan intnanr wne pnfnemnf necom df-ne pm3.2ni wb mnfxr ltxrt mpan2
    mtbiri ) ABCZADEFZAGCZDGCZHZADIFZHZADJZDKJZHZLZULURHZUQUMHZLZLZUTVCUPUSUNUO
    UMULDGMUMNODGPQZRSURUQDKTZURNKDTVFUAKDUBQDKUCQZRUDVAVBURULVGRUMUQVERUDUDUJD
    BCUKVDUEUFADUGUHUI $.
    $( [20-Oct-2005] $) $( [15-Oct-2005] $)

  $( Plus infinity is an upper bound for extended reals. $)
  pnfget $p |- ( A e. RR* -> A <_ +oo ) $=
    ( cxr wcel cpnf cle wbr clt wn pnfnltt wb pnfxr xrlenltt mpan2 mpbird ) ABC
    ZADEFZDAGFHZAIODBCPQJKADLMN $.
    $( [18-Feb-2006] $) $( [30-Jan-2006] $)

  $( Minus infinity is less than or equal to any extended real. $)
  mnflet $p |- ( A e. RR* -> -oo <_ A ) $=
    ( cxr wcel cmnf cle wbr clt wn nltmnft wb mnfxr xrlenltt mpan mpbird ) ABCZ
    DAEFZADGFHZAIDBCOPQJKDALMN $.
    $( [25-Jan-2006] $) $( [19-Jan-2006] $)

  $( Ordering on the extended reals is not symmetric. $)
  xrltnsymt $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A < B -> -. B < A ) ) $=
    ( cr wcel cpnf wceq cmnf w3o clt wbr wn wi cxr ltnsymt wa rexrt pnfnltt
    syl adantr wb breq1 adantl mtbird a1d nltmnft breq2 pm2.21d 3jaodan elxr
    sylan2br mnfxr ax-mp breq12 mtbiri ancoms xrltnrt 3jaoian syl2anb ) ACDZAEF
    ZAGFZHBCDZBEFZBGFZHZABIJZBAIJZKZLZAMDZBMDZUSVEVIUTVAUSVBVIVCVDABNUSVCOZVHVF
    VLVGEAIJZUSVMKZVCUSVJVNAPZAQRSVCVGVMTUSBEAIUAUBUCUDUSVDOZVFVHVPVFAGIJZUSVQK
    ZVDUSVJVRVOAUERSVDVFVQTUSBGAIUFUBUCUGUHUTVKVIVEUTVKOZVFVHVSVFEBIJZVKVTKUTBQ
    UBUTVFVTTVKAEBIUASUCUGBUIZUJVAVBVIVCVDVAVBOZVHVFWBVGBGIJZVBWCKZVAVBVKWDBPBU
    ERUBVAVGWCTVBAGBIUFSUCUDVAVCOVHVFVCVAVHVCVAOVGEGIJZGMDZWEKUKGQULBEAGIUMUNUO
    UDVAVDOZVFVHWGVFGGIJZWFWHKUKGUPULAGBGIUMUNUGUHUQAUIWAUR $.
    $( [21-Oct-2005] $) $( [15-Oct-2005] $)

  $( 'Less than' is antisymmetric and irreflexive for extended reals. $)
  xrltnsym2t $p |- ( ( A e. RR* /\ B e. RR* ) -> -. ( A < B /\ B < A ) ) $=
    ( cxr wcel wa clt wbr wn wi xrltnsymt imnan sylib ) ACDBCDEABFGZBAFGZHIMNEH
    ABJMNKL $.
    $( [?] $) $( [6-Feb-2007] $)

  $( Ordering on the extended reals satisfies strict trichotomy. $)
  xrlttrit $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A < B <-> -. ( A = B \/ B < A ) ) ) $=
    ( cxr wcel wa wceq clt wbr wo wn wi xrltnrt adantr wb breq2 adantl mtbid
    ex xrltnsymt ancoms jaod cr cpnf cmnf w3o axlttri biimprd con1d ltpnft
    mpbird pm2.21nd mnfltt breq1 olc syl a1d 3jaodan eqtr3t orc mnfltpnf
    breq12 mpbiri 3jaoian elxr syl2anb impbid con2bid ) ACDZBCDZEZABFZBAGHZIZAB
    GHZVJVMVNJZVJVKVOVLVHVKVOKVIVHVKVOVHVKEAAGHZVNVHVPJVKALMVKVPVNNVHABAGOPQRMV
    IVHVLVOKBASTUAAUBDZAUCFZAUDFZUEBUBDZBUCFZBUDFZUEZVOVMKZVHVIVQWCWDVRVSVQVTWD
    WAWBVQVTEZVMVNWEVNVMJABUFUGUHVQWAEZVNVMWFVNAUCGHZVQWGWAAUIMWAVNWGNVQBUCAGOP
    UJUKVQWBEZVMVOWHVLVMWHVLUDAGHZVQWIWBAULMWBVLWINVQBUDAGUMPUJVLVKUNZUOUPUQVRV
    TWDWAWBVRVTEZVMVOWKVLVMWKVLBUCGHZVTWLVRBUIPVRVLWLNVTAUCBGOMUJWJUOUPVRWAEZVM
    VOWMVKVMABUCURVKVLUSZUOUPVRWBEZVMVOWOVLVMWBVRVLWBVREVLUDUCGHZUTBUDAUCGVAVBT
    WJUOUPUQVSVTWDWAWBVSVTEZVNVMWQVNUDBGHZVTWRVSBULPVSVNWRNVTAUDBGUMMUJUKVSWAEZ
    VNVMWSVNWPUTAUDBUCGVAVBUKVSWBEZVMVOWTVKVMABUDURWNUOUPUQVCAVDBVDVEVFVG $.
    $( [22-Oct-2005] $) $( [14-Oct-2005] $)

  $( Ordering on the extended reals is transitive. $)
  xrlttrt $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
             ( ( A < B /\ B < C ) -> A < C ) ) $=
    ( cr wcel cpnf wceq cmnf w3o cxr clt wbr wa wi axlttrn 3expa an1rs wn
    rexrt pnfnltt syl adantr wb breq1 adantl mtbird pm2.21d adantll adantld
    nltmnft breq2 adantlr adantrd 3jaodan elxr sylan2b ltpnft mpbird a1d
    anasss adantrr mnfltt mnfltpnf breq12 mpbiri 3jaoian 3impb syl3an3b
    syl3an1b ) ADEZAFGZAHGZIZBJEZCJEZABKLZBCKLZMZACKLZNZAJEZVMVNCDEZCFGZCHGZIZV
    TVOVMVNWEVTVJVNWEMVTVKVLVJVNWEVTVJVNMZWBVTWCWDVJWBVNVTVJWBMZBDEZBFGZBHGZIVT
    VNWGWHVTWIWJVJWHWBVTVJWHWBVTABCOPQWGWIMVQVSVPWBWIVQVSNVJWBWIMZVQVSWKVQFCKLZ
    WBWLRZWIWBVOWMCSCTUAUBWIVQWLUCWBBFCKUDUEUFUGUHUIWGWJMVPVSVQVJWJVPVSNWBVJWJM
    ZVPVSWNVPAHKLZVJWORZWJVJWAWPASAUJUAUBWJVPWOUCVJBHAKUKUEUFUGULUMUNBUOUPQWFWC
    MVSVRVJWCVSVNVJWCMVSAFKLZVJWQWCAUQUBWCVSWQUCVJCFAKUKUEURULUSVNWDVTVJVNWDMZV
    QVSVPWRVQVSWRVQBHKLZVNWSRWDBUJUBWDVQWSUCVNCHBKUKUEUFUGUIZUHUNUTVKVNVTWEVKVN
    MZVPVSVQXAVPVSXAVPFBKLZVNXBRVKBTUEVKVPXBUCVNAFBKUDUBUFUGUMVAVLVNWEVTVLVNMWB
    VTWCWDVLWBVTVNVLWBMZVSVRXCVSHCKLZWBXDVLCVBUEVLVSXDUCWBAHCKUDUBURUSULVLWCVTV
    NVLWCMZVSVRXEVSHFKLVCAHCFKVDVEUSULVNWDVTVLWTUHUNUTVFVGCUOVHAUOVI $.
    $( [23-Oct-2005] $) $( [15-Oct-2005] $)

  ${
    $d x y z A $.
    $( 'Less than' is a strict ordering on the extended reals. $)
    xrltso $p |- < Or RR* $=
      ( vx vy vz cxr clt cv wcel w3a wbr wceq wo wn wb wa wi xrlttrit 3adant3
      xrlttrt jca so ) ABCDEAFZDGZBFZDGZCFZDGZHUAUCEIZUAUCJUCUAEIKLMZUGUCUEEINU
      AUEEIOUBUDUHUFUAUCPQUAUCUERST $.
      $( [24-Oct-2005] $) $( [15-Oct-2005] $)
  $}

  $( Trichotomy law for 'less than' for extended reals. $)
  xrlttri3t $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A = B <-> ( -. A < B /\ -. B < A ) ) ) $=
    ( cxr clt wor wcel wa wceq wbr wn wb xrltso sotrieq2 mpan ) CDEACFBCFGABHAB
    DIJBADIJGKLCABDMN $.
    $( [9-Mar-2006] $) $( [9-Feb-2006] $)

  $( 'Less than or equal' expressed in terms of 'less than' or 'equals', for
     extended reals. $)
  xrleloet $p |- ( ( A e. RR* /\ B e. RR* ) ->
               ( A <_ B <-> ( A < B \/ A = B ) ) ) $=
    ( cxr wcel wa cle wbr clt wn wceq wo xrlenltt wb xrlttrit ancoms con2bid
    eqcom orbi1i orcom bitr syl5rbbr bitrd ) ACDZBCDZEZABFGBAHGZIZABHGZABJZKZAB
    LUEBAJZUHKZUGUJUEUFULUDUCUFULIMBANOPULUIUHKUJUKUIUHBAQRUIUHSTUAUB $.
    $( [27-Jan-2006] $) $( [19-Jan-2006] $)

  $( 'Less than or equal to' implies 'less than' is not 'equals', for extended
     reals. $)
  xrleltnet $p |- ( ( A e. RR* /\ B e. RR* /\ A <_ B ) ->
                ( A < B <-> -. A = B ) ) $=
    ( cxr wcel cle wbr clt wceq wn wb wa wi xrlttri3t pm3.26 syl6bi adantr wo
    xrleloet biimpa ord impbid con2bid 3impa ) ACDZBCDZABEFZABGFZABHZIJUDUEKZUF
    KZUHUGUJUHUGIZUIUHUKLUFUIUHUKBAGFIZKUKABMUKULNOPUJUGUHUIUFUGUHQABRSTUAUBUC
    $.
    $( [10-Mar-2006] $) $( [9-Feb-2006] $)

  $( 'Less than' implies 'less than or equal' for extended reals. $)
  xrltlet $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A < B -> A <_ B ) ) $=
    ( cxr wcel wa cle wbr clt wceq wo xrleloet orc syl5bir ) ACDBCDEABFGABHGZAB
    IZJNABKNOLM $.
    $( [27-Feb-2006] $) $( [19-Jan-2006] $)

  $( 'Less than or equal to' is reflexive for extended reals. $)
  xrleidt $p |- ( A e. RR* -> A <_ A ) $=
    ( cxr wcel cle wbr wa clt wceq wo wn eqid a1i orri xrleloet mpbiri anidms
    ) ABCZAADEZQQFRAAGEZAAHZISTTSJAKLMAANOP $.
    $( [8-Feb-2007] $) $( [7-Feb-2007] $)

  $( Trichotomy law for extended reals. $)
  xrletrit $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A <_ B \/ B <_ A ) ) $=
    ( cxr wcel wa cle wbr wn clt wb xrltnlet ancoms wi xrltlet sylbird orrd )
    ACDZBCDZEZABFGZBAFGZSTHZBAIGZUARQUCUBJBAKLRQUCUAMBANLOP $.
    $( [8-Feb-2007] $) $( [7-Feb-2007] $)

  $( Transitive law for ordering on extended reals. $)
  xrlelttrt $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
             ( ( A <_ B /\ B < C ) -> A < C ) ) $=
    ( cxr wcel w3a cle wbr clt wceq wo wi wb xrleloet 3adant3 xrlttrt exp3a
    breq1 biimprd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACIHZUGUHABIH
    ZABJZKZUIUJLZUDUEUHUMMUFABNOUGUKUNULUGUKUIUJABCPQULUNLUGULUJUIABCIRSTUAUBUC
    $.
    $( [1-Mar-2006] $) $( [19-Jan-2006] $)

  $( Transitive law for ordering on extended reals. $)
  xrltletrt $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
             ( ( A < B /\ B <_ C ) -> A < C ) ) $=
    ( cxr wcel w3a clt wbr cle wceq wo wi wb xrleloet 3adant1 xrlttrt exp3a
    com23 breq2 biimpd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACGHZUHU
    JUIUKUHUJBCGHZBCJZKZUIUKLZUFUGUJUNMUEBCNOUHULUOUMUHUIULUKUHUIULUKABCPQRUMUO
    LUHUMUIUKBCAGSTUAUBUCRUD $.
    $( [21-Mar-2006] $) $( [19-Jan-2006] $)

  $( Transitive law for ordering on extended reals. $)
  xrletrt $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
             ( ( A <_ B /\ B <_ C ) -> A <_ C ) ) $=
    ( cxr wcel w3a cle wbr wi wa clt wceq wo wb xrleloet 3adant1 adantr
    xrlelttrt xrltlet 3adant2 syld exp3a imp breq2 biimpcd adantl jaod sylbid
    ex imp3a ) ADEZBDEZCDEZFZABGHZBCGHZACGHZUNUOUPUQIUNUOJZUPBCKHZBCLZMZUQUNUPV
    ANZUOULUMVBUKBCOPQURUSUQUTUNUOUSUQIUNUOUSUQUNUOUSJACKHZUQABCRUKUMVCUQIULACS
    TUAUBUCUOUTUQIUNUTUOUQBCAGUDUEUFUGUHUIUJ $.
    $( [3-Mar-2006] $) $( [9-Feb-2006] $)

  $( 'Less than' implies not equal for extended reals. $)
  xrltnet $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A < B -> -. A = B ) ) $=
    ( cxr wcel wa clt wbr wo wceq wn wor wb xrltso sotrieq mpan con2bid orc
    syl5bi ) ACDBCDEZABFGZBAFGZHZABIZJTSUCUBCFKSUCUBJLMCABFNOPTUAQR $.
    $( [28-Feb-2006] $) $( [20-Jan-2006] $)

  $( An extended real is not less than plus infinity iff they are equal. $)
  nltpnftt $p |- ( A e. RR* -> ( A = +oo <-> -. A < +oo ) ) $=
    ( cxr wcel cpnf wceq clt wbr wn pnfxr xrltnrt ax-mp breq1 mtbiri cle wo
    pnfget wb xrleloet mpan2 mpbid ord impbid2 ) ABCZADEZADFGZHUDUEDDFGZDBCZUFH
    IDJKADDFLMUCUEUDUCADNGZUEUDOZAPUCUGUHUIQIADRSTUAUB $.
    $( [3-Mar-2006] $) $( [30-Jan-2006] $)

  $( An extended real is not greater than minus infinity iff they are equal. $)
  ngtmnftt $p |- ( A e. RR* -> ( A = -oo <-> -. -oo < A ) ) $=
    ( cxr wcel cmnf wceq clt wbr wn mnfxr xrltnrt ax-mp breq2 mtbiri cle wo
    mnflet wb xrleloet mpan mpbid ord eqcom syl6ib impbid2 ) ABCZADEZDAFGZHZUFU
    GDDFGZDBCZUIHIDJKADDFLMUEUHDAEZUFUEUGUKUEDANGZUGUKOZAPUJUEULUMQIDARSTUADAUB
    UCUD $.
    $( [5-Mar-2006] $) $( [2-Feb-2006] $)

  $( An extended real is real iff it is strictly bounded by infinities. $)
  xrrebndt $p |- ( A e. RR* -> ( A e. RR <-> ( -oo < A /\ A < +oo ) ) ) $=
    ( cxr wcel cr cmnf clt wbr cpnf wa mnfltt ltpnft jca wceq wo wn nltpnftt
    ngtmnftt orbi12d ianor orcom bitr2 syl6bb con2bid w3o elxr biimp 3orass
    bitr sylib ord sylbid impbid2 ) ABCZADCZEAFGZAHFGZIZUNUOUPAJAKLUMUQAHMZAEMZ
    NZOUNUMUTUQUMUTUPOZUOOZNZUQOZUMURVAUSVBAPAQRVDVBVANVCUOUPSVBVATUAUBUCUMUTUN
    UMUNURUSUDZUTUNNZUMVEAUEUFVEUNUTNVFUNURUSUGUNUTTUHUIUJUKUL $.
    $( [5-Mar-2006] $) $( [2-Feb-2006] $)

  $( A way of proving that an extended real is real. $)
  xrret $p |- ( ( ( A e. RR* /\ B e. RR ) /\ ( -oo < A /\ A <_ B ) ) ->
              A e. RR ) $=
    ( cxr wcel cr wa cmnf clt wbr cle cpnf simprl ltpnft adantl wi pnfxr
    xrlelttrt mp3an3 rexrt sylan2 mpan2d imp adantrl jca wb xrrebndt ad2antrr
    mpbird ) ACDZBEDZFZGAHIZABJIZFZFZAEDZULAKHIZFZUOULUQUKULUMLUKUMUQULUKUMUQUK
    UMBKHIZUQUJUSUIBMNUIBCDZUMUSFUQOZUJUIUTKCDVAPABKQRBSTUAUBUCUDUIUPURUEUJUNAU
    FUGUH $.
    $( [10-Mar-2005] $) $( [9-Mar-2006] $)

  $( An extended real between two others is real. $)
  xrre2t $p |- ( ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\
              ( A < B /\ B < C ) ) -> B e. RR ) $=
    ( cxr wcel w3a clt wbr wa cr cmnf cpnf wi cle mnflet adantr mnfxr
    xrlelttrt mp3an1 mpand 3adant3 pnfget adantl pnfxr xrltletrt mp3an3 mpan2d
    3adant1 anim12d wb xrrebndt 3ad2ant2 sylibrd imp ) ADEZBDEZCDEZFZABGHZBCGHZ
    IZBJEZURVAKBGHZBLGHZIZVBURUSVCUTVDUOUPUSVCMUQUOUPIKANHZUSVCUOVFUPAOPKDEUOUP
    VFUSIVCMQKABRSTUAUPUQUTVDMUOUPUQIUTCLNHZVDUQVGUPCUBUCUPUQLDEUTVGIVDMUDBCLUE
    UFUGUHUIUPUOVBVEUJUQBUKULUMUN $.
    $( [6-Feb-2007] $) $( [6-Feb-2007] $)

