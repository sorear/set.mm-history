$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Ordering on the extended reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Membership in the set of extended reals. $)
  elxr $p |- ( A e. RR* <-> ( A e. RR \/ A = +oo \/ A = -oo ) ) $=
    ( cxr wcel cr cpnf cmnf cpr cun wo wceq w3o df-xr eleq2i elun pnfxr
    elisseti mnfxr elpr2 orbi2i 3orass bitr4i 3bitri ) ABCADEFGZHZCADCZAUCCZIZU
    EAEJZAFJZKZBUDALMADUCNUGUEUHUIIZIUJUFUKUEAEFEBOPFBQPRSUEUHUITUAUB $.
    $( [17-Oct-2005] $) $( [14-Oct-2005] $)

  $( Plus and minus infinity are distinguished elements of ` RR* ` . $)
  pnfnemnf $p |- +oo =/= -oo $=
    ( cpnf cmnf wne cpw cxr wcel pnfxr pwne ax-mp necom mpbir df-mnf neeq2i )
    ABCAADZCZONACZAEFPGAEHIANJKBNALMK $.
    $( [6-May-2008] $) $( [14-Oct-2005] $)

  $( No (finite) real equals plus infinity. $)
  renepnf $p |- ( A e. RR -> A =/= +oo ) $=
    ( cr wcel cpnf wceq wn wne wnel pnfnre df-nel mpbi nelneq mpan2 df-ne
    sylibr ) ABCZADEFZADGPDBCFZQDBHRIDBJKADBLMADNO $.
    $( [16-Oct-2005] $) $( [14-Oct-2005] $)

  $( No real equals minus infinity. $)
  renemnf $p |- ( A e. RR -> A =/= -oo ) $=
    ( cr wcel cmnf wceq wn wne wnel mnfnre df-nel mpbi nelneq mpan2 df-ne
    sylibr ) ABCZADEFZADGPDBCFZQDBHRIDBJKADBLMADNO $.
    $( [26-Oct-2005] $) $( [14-Oct-2005] $)

  $( The reals and the infinities are disjoint. $)
  renfdisj $p |- ( RR i^i { +oo , -oo } ) = (/) $=
    ( cr cpnf cmnf cpr cin csn cun c0 df-pr ineq2i wceq wa wcel wn eqid wne
    renepnf df-ne sylib mt2 disjsn mpbir renemnf pm3.2i undisj2 mpbi eqtri ) AB
    CDZEABFZCFZGZEZHUHUKABCIJAUIEHKZAUJEHKZLULHKUMUNUMBAMZNUOBBKZBOUOBBPUPNBQBB
    RSTABUAUBUNCAMZNUQCCKZCOUQCCPURNCUCCCRSTACUAUBUDAUIUJUEUFUG $.
    $( [27-Oct-2005] $) $( [25-Oct-2005] $)

  ${
    $d x A $.
    $( The three (non-exclusive) possibilities implied by a subset of extended
       reals. $)
    ssxr $p |- ( A (_ RR* -> ( A (_ RR \/ +oo e. A \/ -oo e. A ) ) $=
      ( vx cxr wss cr cpnf wcel cmnf wo w3o wn cv cpr wral cin c0 wceq cun
      disjssun df-xr uncom eqtri sseq2i syl5bb biimpcd disj syl5ibr con3d wrex
      dfrex2 visset elpr rexbii bitr3i r19.43 wa wex df-rex exancom pnfxr
      elisseti eleq1 ceqsexv 3bitri mnfxr orbi12i syl6ib orrd 3orass sylibr )
      ACDZAEDZFAGZHAGZIZIVLVMVNJVKVLVOVKVLKBLZFHMZGZKBANZKZVOVKVSVLVKAVQOPQZVLV
      SWAVKVLWAAVQERZDVLVKAVQESCWBACEVQRWBTEVQUAUBUCUDUEBAVQUFUGUHVTVPFQZVPHQZI
      ZBAUIZWCBAUIZWDBAUIZIVOVTVRBAUIWFVRBAUJVRWEBAVPFHBUKULUMUNWCWDBAUOWGVMWHV
      NWGVPAGZWCUPBUQWCWIUPBUQVMWCBAURWIWCBUSWIVMBFFCUTVAVPFAVBVCVDWHWIWDUPBUQW
      DWIUPBUQVNWDBAURWIWDBUSWIVNBHHCVEVAVPHAVBVCVDVFVDVGVHVLVMVNVIVJ $.
      $( [10-Nov-2005] $) $( [25-Oct-2005] $)
  $}

  $( The extended real 'less than' is irreflexive. $)
  xrltnr $p |- ( A e. RR* -> -. A < A ) $=
    ( cxr wcel cr cpnf wceq cmnf w3o clt wbr wn elxr ltnr wa cltrr wo wnel
    pnfnre df-nel mpbi intnan intnanr wne pnfnemnf df-ne pm3.2ni wb pnfxr ltxr
    mp2an mtbir breq12 anidms mtbiri mnfnre necom mnfxr 3jaoi sylbi ) ABCADCZAE
    FZAGFZHAAIJZKZALUTVDVAVBAMVAVCEEIJZVEEDCZVFNZEEOJZNZEGFZEEFZNZPZVFVKNZVJVFN
    ZPZPZVMVPVIVLVGVHVFVFEDQVFKREDSTZUAUBVJVKEGUCZVJKUDEGUETUBUFVNVOVFVKVRUBVFV
    JVRUAUFUFEBCZVTVEVQUGUHUHEEUIUJUKVAVCVEUGAEAEIULUMUNVBVCGGIJZWAGDCZWBNZGGOJ
    ZNZGGFZGEFZNZPZWBWGNZWFWBNZPZPZWIWLWEWHWCWDWBWBGDQWBKUOGDSTZUAUBWGWFGEUCZWG
    KVSWOUDEGUPTGEUETUAUFWJWKWBWGWNUBWBWFWNUAUFUFGBCZWPWAWMUGUQUQGGUIUJUKVBVCWA
    UGAGAGIULUMUNURUS $.
    $( [18-Oct-2005] $) $( [14-Oct-2005] $)

  $( Any (finite) real is less than plus infinity. $)
  ltpnf $p |- ( A e. RR -> A < +oo ) $=
    ( cr wcel cpnf clt wbr wa cltrr cmnf wceq wo eqid jctr orc olc 3syl cxr wb
    rexr pnfxr ltxr mpan2 syl mpbird ) ABCZADEFZUEDBCZGADHFGAIJZDDJZGKZUEUIGZUH
    UGGZKZKZUEUKUMUNUEUIDLMUKULNUMUJOPUEAQCZUFUNRZASUODQCUPTADUAUBUCUD $.
    $( [19-Oct-2005] $) $( [14-Oct-2005] $)

  $( Minus infinity is less than any (finite) real. $)
  mnflt $p |- ( A e. RR -> -oo < A ) $=
    ( cr wcel cmnf clt wbr wa cltrr wceq cpnf wo eqid jctl olc 3syl cxr wb
    rexr mnfxr ltxr mpan syl mpbird ) ABCZDAEFZDBCZUDGDAHFGDDIZAJIZGKZUFUHGZUGU
    DGZKZKZUDUKULUMUDUGDLMUKUJNULUINOUDAPCZUEUMQZARDPCUNUOSDATUAUBUC $.
    $( [17-Oct-2005] $) $( [14-Oct-2005] $)

  $( Minus infinity is less than plus infinity. $)
  mnfltpnf $p |- -oo < +oo $=
    ( cmnf cpnf clt wbr cr wcel wa cltrr wceq wo eqid olc mp2an orci cxr wb
    mnfxr pnfxr ltxr mpbir ) ABCDZAEFZBEFZGABHDGZAAIZBBIZGZJZUBUFGUEUCGJZJZUHUI
    UEUFUHAKBKUGUDLMNAOFBOFUAUJPQRABSMT $.
    $( [19-Oct-2005] $) $( [14-Oct-2005] $)

  $( Minus infinity is less than an extended real that is either real or plus
     infinity. $)
  mnfltxr $p |- ( ( A e. RR \/ A = +oo ) -> -oo < A ) $=
    ( cr wcel cmnf clt wbr cpnf wceq mnflt mnfltpnf breq2 mpbiri jaoi ) ABCDAEF
    ZAGHZAIONDGEFJAGDEKLM $.
    $( [26-Feb-2006] $) $( [2-Feb-2006] $)

  $( No extended real is greater than plus infinity. $)
  pnfnlt $p |- ( A e. RR* -> -. +oo < A ) $=
    ( cxr wcel cpnf clt wbr cr wa cltrr cmnf wceq wo wnel wn pnfnre df-nel
    mpbi intnanr wne pnfnemnf df-ne pm3.2ni wb pnfxr ltxr mpan mtbiri ) ABCZDAE
    FZDGCZAGCZHZDAIFZHZDJKZADKZHZLZUJUPHZUOUKHZLZLZURVAUNUQULUMUJUKDGMUJNODGPQZ
    RRUOUPDJSUONTDJUAQZRUBUSUTUJUPVCRUOUKVDRUBUBDBCUHUIVBUCUDDAUEUFUG $.
    $( [19-Oct-2005] $) $( [15-Oct-2005] $)

  $( No extended real is less than minus infinity. $)
  nltmnf $p |- ( A e. RR* -> -. A < -oo ) $=
    ( cxr wcel cmnf clt wbr cr wa cltrr wceq cpnf wo wnel wn mnfnre df-nel
    mpbi intnan intnanr wne pnfnemnf necom df-ne pm3.2ni wb mnfxr ltxr mpan2
    mtbiri ) ABCZADEFZAGCZDGCZHZADIFZHZADJZDKJZHZLZULURHZUQUMHZLZLZUTVCUPUSUNUO
    UMULDGMUMNODGPQZRSURUQDKTZURNKDTVFUAKDUBQDKUCQZRUDVAVBURULVGRUMUQVERUDUDUJD
    BCUKVDUEUFADUGUHUI $.
    $( [20-Oct-2005] $) $( [15-Oct-2005] $)

  $( Plus infinity is an upper bound for extended reals. $)
  pnfge $p |- ( A e. RR* -> A <_ +oo ) $=
    ( cxr wcel cpnf cle wbr clt wn pnfnlt wb pnfxr xrlenlt mpan2 mpbird ) ABCZA
    DEFZDAGFHZAIODBCPQJKADLMN $.
    $( [18-Feb-2006] $) $( [30-Jan-2006] $)

  $( Minus infinity is less than or equal to any extended real. $)
  mnfle $p |- ( A e. RR* -> -oo <_ A ) $=
    ( cxr wcel cmnf cle wbr clt wn nltmnf wb mnfxr xrlenlt mpan mpbird ) ABCZDA
    EFZADGFHZAIDBCOPQJKDALMN $.
    $( [25-Jan-2006] $) $( [19-Jan-2006] $)

  $( Ordering on the extended reals is not symmetric. $)
  xrltnsym $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A < B -> -. B < A ) ) $=
    ( cr wcel cpnf wceq cmnf w3o clt wbr wn wi cxr ltnsym wa rexr pnfnlt syl
    adantr wb breq1 adantl mtbird a1d nltmnf breq2 pm2.21d 3jaodan elxr
    sylan2br mnfxr ax-mp breq12 mtbiri ancoms xrltnr 3jaoian syl2anb ) ACDZAEFZ
    AGFZHBCDZBEFZBGFZHZABIJZBAIJZKZLZAMDZBMDZUSVEVIUTVAUSVBVIVCVDABNUSVCOZVHVFV
    LVGEAIJZUSVMKZVCUSVJVNAPZAQRSVCVGVMTUSBEAIUAUBUCUDUSVDOZVFVHVPVFAGIJZUSVQKZ
    VDUSVJVRVOAUERSVDVFVQTUSBGAIUFUBUCUGUHUTVKVIVEUTVKOZVFVHVSVFEBIJZVKVTKUTBQU
    BUTVFVTTVKAEBIUASUCUGBUIZUJVAVBVIVCVDVAVBOZVHVFWBVGBGIJZVBWCKZVAVBVKWDBPBUE
    RUBVAVGWCTVBAGBIUFSUCUDVAVCOVHVFVCVAVHVCVAOVGEGIJZGMDZWEKUKGQULBEAGIUMUNUOU
    DVAVDOZVFVHWGVFGGIJZWFWHKUKGUPULAGBGIUMUNUGUHUQAUIWAUR $.
    $( [21-Oct-2005] $) $( [15-Oct-2005] $)

  $( 'Less than' is antisymmetric and irreflexive for extended reals. $)
  xrltnsym2 $p |- ( ( A e. RR* /\ B e. RR* ) -> -. ( A < B /\ B < A ) ) $=
    ( cxr wcel wa clt wbr wn wi xrltnsym imnan sylib ) ACDBCDEABFGZBAFGZHIMNEHA
    BJMNKL $.
    $( [8-Jun-2007] $) $( [6-Feb-2007] $)

  $( Ordering on the extended reals satisfies strict trichotomy. $)
  xrlttri $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A < B <-> -. ( A = B \/ B < A ) ) ) $=
    ( cxr wcel wa wceq clt wbr wo wn wi xrltnr adantr wb breq2 adantl mtbid ex
    xrltnsym ancoms jaod cr cpnf cmnf w3o axlttri biimprd con1d ltpnf mpbird
    pm2.24d mnflt breq1 olcd a1d 3jaodan eqtr3 orcd mnfltpnf breq12 mpbiri
    3jaoian elxr syl2anb impbid con2bid ) ACDZBCDZEZABFZBAGHZIZABGHZVIVLVMJZVIV
    JVNVKVGVJVNKVHVGVJVNVGVJEAAGHZVMVGVOJVJALMVJVOVMNVGABAGOPQRMVHVGVKVNKBASTUA
    AUBDZAUCFZAUDFZUEBUBDZBUCFZBUDFZUEZVNVLKZVGVHVPWBWCVQVRVPVSWCVTWAVPVSEZVLVM
    WDVMVLJABUFUGUHVPVTEZVMVLWEVMAUCGHZVPWFVTAUIMVTVMWFNVPBUCAGOPUJUKVPWAEZVLVN
    WGVKVJWGVKUDAGHZVPWHWAAULMWAVKWHNVPBUDAGUMPUJUNUOUPVQVSWCVTWAVQVSEZVLVNWIVK
    VJWIVKBUCGHZVSWJVQBUIPVQVKWJNVSAUCBGOMUJUNUOVQVTEZVLVNWKVJVKABUCUQURUOVQWAE
    ZVLVNWLVKVJWAVQVKWAVQEVKUDUCGHZUSBUDAUCGUTVATUNUOUPVRVSWCVTWAVRVSEZVMVLWNVM
    UDBGHZVSWOVRBULPVRVMWONVSAUDBGUMMUJUKVRVTEZVMVLWPVMWMUSAUDBUCGUTVAUKVRWAEZV
    LVNWQVJVKABUDUQURUOUPVBAVCBVCVDVEVF $.
    $( [22-Oct-2005] $) $( [14-Oct-2005] $)

  $( Ordering on the extended reals is transitive. $)
  xrlttr $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
             ( ( A < B /\ B < C ) -> A < C ) ) $=
    ( cr wcel cpnf wceq cmnf w3o cxr clt wbr wa wi axlttrn 3expa an1rs wn rexr
    pnfnlt syl adantr wb breq1 adantl mtbird pm2.21d adantll adantld nltmnf
    breq2 adantlr adantrd 3jaodan elxr sylan2b ltpnf mpbird a1d anasss adantrr
    mnflt mnfltpnf breq12 mpbiri 3jaoian 3impb syl3an3b syl3an1b ) ADEZAFGZAHGZ
    IZBJEZCJEZABKLZBCKLZMZACKLZNZAJEZVMVNCDEZCFGZCHGZIZVTVOVMVNWEVTVJVNWEMVTVKV
    LVJVNWEVTVJVNMZWBVTWCWDVJWBVNVTVJWBMZBDEZBFGZBHGZIVTVNWGWHVTWIWJVJWHWBVTVJW
    HWBVTABCOPQWGWIMVQVSVPWBWIVQVSNVJWBWIMZVQVSWKVQFCKLZWBWLRZWIWBVOWMCSCTUAUBW
    IVQWLUCWBBFCKUDUEUFUGUHUIWGWJMVPVSVQVJWJVPVSNWBVJWJMZVPVSWNVPAHKLZVJWORZWJV
    JWAWPASAUJUAUBWJVPWOUCVJBHAKUKUEUFUGULUMUNBUOUPQWFWCMVSVRVJWCVSVNVJWCMVSAFK
    LZVJWQWCAUQUBWCVSWQUCVJCFAKUKUEURULUSVNWDVTVJVNWDMZVQVSVPWRVQVSWRVQBHKLZVNW
    SRWDBUJUBWDVQWSUCVNCHBKUKUEUFUGUIZUHUNUTVKVNVTWEVKVNMZVPVSVQXAVPVSXAVPFBKLZ
    VNXBRVKBTUEVKVPXBUCVNAFBKUDUBUFUGUMVAVLVNWEVTVLVNMWBVTWCWDVLWBVTVNVLWBMZVSV
    RXCVSHCKLZWBXDVLCVBUEVLVSXDUCWBAHCKUDUBURUSULVLWCVTVNVLWCMZVSVRXEVSHFKLVCAH
    CFKVDVEUSULVNWDVTVLWTUHUNUTVFVGCUOVHAUOVI $.
    $( [23-Oct-2005] $) $( [15-Oct-2005] $)

  ${
    $d x y z A $.
    $( 'Less than' is a strict ordering on the extended reals. $)
    xrltso $p |- < Or RR* $=
      ( vx vy vz cxr clt cv wcel w3a wbr wceq wo wn wb wa wi xrlttri 3adant3
      xrlttr jca so ) ABCDEAFZDGZBFZDGZCFZDGZHUAUCEIZUAUCJUCUAEIKLMZUGUCUEEINUA
      UEEIOUBUDUHUFUAUCPQUAUCUERST $.
      $( [24-Oct-2005] $) $( [15-Oct-2005] $)
  $}

  $( Trichotomy law for 'less than' for extended reals. $)
  xrlttri2 $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A =/= B <-> ( A < B \/ B < A ) ) ) $=
    ( cxr wcel wa clt wbr wo wceq wn wor wb xrltso sotrieq mpan bicomd
    necon1abid ) ACDBCDEZABFGBAFGHZABRABIZSJZCFKRTUALMCABFNOPQ $.
    $( [2-Jan-2008] $) $( [10-Dec-2007] $)

  $( Trichotomy law for 'less than' for extended reals. $)
  xrlttri3 $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A = B <-> ( -. A < B /\ -. B < A ) ) ) $=
    ( cxr clt wor wcel wa wceq wbr wn wb xrltso sotrieq2 mpan ) CDEACFBCFGABHAB
    DIJBADIJGKLCABDMN $.
    $( [9-Mar-2006] $) $( [9-Feb-2006] $)

  $( 'Less than or equal' expressed in terms of 'less than' or 'equals', for
     extended reals. $)
  xrleloe $p |- ( ( A e. RR* /\ B e. RR* ) ->
               ( A <_ B <-> ( A < B \/ A = B ) ) ) $=
    ( cxr wcel wa cle wbr clt wn wceq wo xrlenlt wb xrlttri ancoms con2bid
    eqcom orbi1i orcom bitri syl5rbbr bitrd ) ACDZBCDZEZABFGBAHGZIZABHGZABJZKZA
    BLUEBAJZUHKZUGUJUEUFULUDUCUFULIMBANOPULUIUHKUJUKUIUHBAQRUIUHSTUAUB $.
    $( [27-Jan-2006] $) $( [19-Jan-2006] $)

  $( 'Less than or equal to' implies 'less than' is not 'equals', for extended
     reals. $)
  xrleltne $p |- ( ( A e. RR* /\ B e. RR* /\ A <_ B ) ->
                ( A < B <-> B =/= A ) ) $=
    ( cxr wcel cle wbr clt wne wb wa wceq wn wi xrlttri3 pm3.26 syl6bi adantr
    wo xrleloe biimpa ord impbid necon2abid necom syl6bbr 3impa ) ACDZBCDZABEFZ
    ABGFZBAHZIUGUHJZUIJZUJABHUKUMUJABUMABKZUJLZULUNUOMUIULUNUOBAGFLZJUOABNUOUPO
    PQUMUJUNULUIUJUNRABSTUAUBUCBAUDUEUF $.
    $( [22-Jul-2007] $) $( [9-Feb-2006] $)

  $( 'Less than' implies 'less than or equal' for extended reals. $)
  xrltle $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A < B -> A <_ B ) ) $=
    ( cxr wcel wa cle wbr clt wceq wo xrleloe orc syl5bir ) ACDBCDEABFGABHGZABI
    ZJNABKNOLM $.
    $( [27-Feb-2006] $) $( [19-Jan-2006] $)

  $( 'Less than or equal to' is reflexive for extended reals. $)
  xrleid $p |- ( A e. RR* -> A <_ A ) $=
    ( cxr wcel cle wbr wa clt wceq wo eqid olci xrleloe mpbiri anidms ) ABCZAAD
    EZOOFPAAGEZAAHZIRQAJKAALMN $.
    $( [8-Feb-2007] $) $( [7-Feb-2007] $)

  $( Trichotomy law for extended reals. $)
  xrletri $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A <_ B \/ B <_ A ) ) $=
    ( cxr wcel wa cle wbr wn clt wb xrltnle ancoms wi xrltle sylbird orrd ) ACD
    ZBCDZEZABFGZBAFGZSTHZBAIGZUARQUCUBJBAKLRQUCUAMBANLOP $.
    $( [8-Feb-2007] $) $( [7-Feb-2007] $)

  $( Transitive law for ordering on extended reals. $)
  xrlelttr $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
             ( ( A <_ B /\ B < C ) -> A < C ) ) $=
    ( cxr wcel w3a cle wbr clt wceq wo wi wb xrleloe 3adant3 xrlttr exp3a
    breq1 biimprd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACIHZUGUHABIH
    ZABJZKZUIUJLZUDUEUHUMMUFABNOUGUKUNULUGUKUIUJABCPQULUNLUGULUJUIABCIRSTUAUBUC
    $.
    $( [1-Mar-2006] $) $( [19-Jan-2006] $)

  $( Transitive law for ordering on extended reals. $)
  xrltletr $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
             ( ( A < B /\ B <_ C ) -> A < C ) ) $=
    ( cxr wcel w3a clt wbr cle wceq wo wi wb xrleloe 3adant1 xrlttr exp3a
    com23 breq2 biimpd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACGHZUHU
    JUIUKUHUJBCGHZBCJZKZUIUKLZUFUGUJUNMUEBCNOUHULUOUMUHUIULUKUHUIULUKABCPQRUMUO
    LUHUMUIUKBCAGSTUAUBUCRUD $.
    $( [21-Mar-2006] $) $( [19-Jan-2006] $)

  $( Transitive law for ordering on extended reals. $)
  xrletr $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
             ( ( A <_ B /\ B <_ C ) -> A <_ C ) ) $=
    ( cxr wcel w3a cle wbr wa clt wceq wo wb xrleloe 3adant1 adantr xrlelttr
    wi xrltle 3adant2 syld expdimp breq2 biimpcd adantl jaod sylbid expimpd )
    ADEZBDEZCDEZFZABGHZBCGHZACGHZULUMIZUNBCJHZBCKZLZUOULUNUSMZUMUJUKUTUIBCNOPUP
    UQUOURULUMUQUOULUMUQIACJHZUOABCQUIUKVAUORUJACSTUAUBUMURUORULURUMUOBCAGUCUDU
    EUFUGUH $.
    $( [3-Mar-2006] $) $( [9-Feb-2006] $)

  $( 'Less than' implies not equal for extended reals. $)
  xrltne $p |- ( ( A e. RR* /\ B e. RR* /\ A < B ) -> B =/= A ) $=
    ( cxr wcel clt wbr w3a wne wa wo wor wceq wn wb xrltso sotrieq mpan
    necon2abid orc syl5bi 3impia necomd ) ACDZBCDZABEFZGABUCUDUEABHZUCUDIZUEBAE
    FZJZUFUEUGUIABCEKUGABLUIMNOCABEPQRUEUHSTUAUB $.
    $( [22-Aug-2007] $) $( [20-Jan-2006] $)

  $( An extended real is not less than plus infinity iff they are equal. $)
  nltpnft $p |- ( A e. RR* -> ( A = +oo <-> -. A < +oo ) ) $=
    ( cxr wcel cpnf wceq clt wbr wn pnfxr xrltnr ax-mp breq1 mtbiri cle wo
    pnfge wb xrleloe mpan2 mpbid ord impbid2 ) ABCZADEZADFGZHUDUEDDFGZDBCZUFHID
    JKADDFLMUCUEUDUCADNGZUEUDOZAPUCUGUHUIQIADRSTUAUB $.
    $( [3-Mar-2006] $) $( [30-Jan-2006] $)

  $( An extended real is not greater than minus infinity iff they are equal. $)
  ngtmnft $p |- ( A e. RR* -> ( A = -oo <-> -. -oo < A ) ) $=
    ( cxr wcel cmnf wceq clt wbr wn mnfxr xrltnr ax-mp breq2 mtbiri cle wo
    mnfle wb xrleloe mpan mpbid ord eqcom syl6ib impbid2 ) ABCZADEZDAFGZHZUFUGD
    DFGZDBCZUIHIDJKADDFLMUEUHDAEZUFUEUGUKUEDANGZUGUKOZAPUJUEULUMQIDARSTUADAUBUC
    UD $.
    $( [5-Mar-2006] $) $( [2-Feb-2006] $)

  $( An extended real is real iff it is strictly bounded by infinities. $)
  xrrebnd $p |- ( A e. RR* -> ( A e. RR <-> ( -oo < A /\ A < +oo ) ) ) $=
    ( cxr wcel cr cmnf clt wbr cpnf wa mnflt ltpnf jca wceq wo wn nltpnft
    ngtmnft orbi12d ianor orcom bitr2i syl6bb con2bid w3o elxr biimpi 3orass
    bitri sylib ord sylbid impbid2 ) ABCZADCZEAFGZAHFGZIZUNUOUPAJAKLUMUQAHMZAEM
    ZNZOUNUMUTUQUMUTUPOZUOOZNZUQOZUMURVAUSVBAPAQRVDVBVANVCUOUPSVBVATUAUBUCUMUTU
    NUMUNURUSUDZUTUNNZUMVEAUEUFVEUNUTNVFUNURUSUGUNUTTUHUIUJUKUL $.
    $( [5-Mar-2006] $) $( [2-Feb-2006] $)

  $( A way of proving that an extended real is real. $)
  xrre $p |- ( ( ( A e. RR* /\ B e. RR ) /\ ( -oo < A /\ A <_ B ) ) ->
              A e. RR ) $=
    ( cxr wcel cr wa cmnf clt wbr cle cpnf simprl ltpnf adantl wi pnfxr
    xrlelttr mp3an3 rexr sylan2 mpan2d imp adantrl jca wb xrrebnd ad2antrr
    mpbird ) ACDZBEDZFZGAHIZABJIZFZFZAEDZULAKHIZFZUOULUQUKULUMLUKUMUQULUKUMUQUK
    UMBKHIZUQUJUSUIBMNUIBCDZUMUSFUQOZUJUIUTKCDVAPABKQRBSTUAUBUCUDUIUPURUEUJUNAU
    FUGUH $.
    $( [10-Mar-2005] $) $( [9-Mar-2006] $)

  $( An extended real between two others is real. $)
  xrre2 $p |- ( ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\
              ( A < B /\ B < C ) ) -> B e. RR ) $=
    ( cxr wcel w3a clt wbr wa cr cmnf cpnf wi cle mnfle adantr mnfxr xrlelttr
    mp3an1 mpand 3adant3 pnfge adantl pnfxr xrltletr mp3an3 mpan2d 3adant1
    anim12d wb xrrebnd 3ad2ant2 sylibrd imp ) ADEZBDEZCDEZFZABGHZBCGHZIZBJEZURV
    AKBGHZBLGHZIZVBURUSVCUTVDUOUPUSVCMUQUOUPIKANHZUSVCUOVFUPAOPKDEUOUPVFUSIVCMQ
    KABRSTUAUPUQUTVDMUOUPUQIUTCLNHZVDUQVGUPCUBUCUPUQLDEUTVGIVDMUDBCLUEUFUGUHUIU
    PUOVBVEUJUQBUKULUMUN $.
    $( [6-Feb-2007] $) $( [6-Feb-2007] $)

