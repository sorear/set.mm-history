$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Ordering on the extended reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The set of extended reals exists. $)
  xrex $p |- RR* e. _V $=
    ( cxr cr cpnf cmnf cpr cun cvv df-xr reex prex unex eqeltri ) ABCDEZFGHBMIC
    DJKL $.
    $( [24-Dec-2006] $)

  $( Plus infinity belongs to the set of extended reals.  (The proof was
     shortened by Anthony Hart, 29-Aug-2011.) $)
  pnfxr $p |- +oo e. RR* $=
    ( cpnf cr cmnf cpr cun cxr ssun2 cc cuni cpw df-pnf cnex uniex pwex eqeltri
    cvv prid1 sselii df-xr eleqtrri ) ABACDZEZFUAUBAUABGACAHIZJPKUCHLMNOQRST $.
    $( [29-Aug-2011] $) $( [13-Oct-2005] $)

  $( Minus infinity belongs to the set of extended reals.  (The proof was
     shortened by Anthony Hart, 29-Aug-2011.)  (The proof was shortened by
     Andrew Salmon, 19-Nov-2011.) $)
  mnfxr $p |- -oo e. RR* $=
    ( cmnf cr cpnf cpr cun cxr wcel cpw cvv df-mnf pnfxr elisseti eqeltri prid2
    pwex elun2 ax-mp df-xr eleqtrri ) ABCADZEZFATGAUAGCAACHIJCCFKLOMNATBPQRS $.
    $( [19-Nov-2011] $) $( [13-Oct-2005] $)

  ${
    $d x y A $.  $d x y B $.
    $( The 'less than' binary relation on the set of extended reals.
       Definition 12-3.1 of [Gleason] p. 173. $)
    ltxr $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A < B <->
              ( ( ( ( A e. RR /\ B e. RR ) /\ A <RR B ) \/
                   ( A = -oo /\ B = +oo ) ) \/ ( ( A e. RR /\ B = +oo )
                  \/ ( A = -oo /\ B e. RR ) ) ) ) ) $=
      ( vx vy clt wbr cr wcel cltrr w3a cmnf cpnf csn cun wo cxr wa wceq syl5bb
      elun cop copab cxp df-br df-ltxr eleq2i 3bitri eleq1 breq1 3anbi13d breq2
      cv 3anbi23d df-3an syl6bb opelopabg elsnc wb pnfxr opthgg orbi12d opelxpg
      opex mp3an3 elsncg anbi2d bitrd adantl anbi1d sylan9bbr ) ABEFZABUAZCULZG
      HZDULZGHZVMVOIFZJZCDUBZKLUAZMZNZHZVLGLMZUCZKMZGUCZNZHZOZAPHZBPHZQZAGHZBGH
      ZQABIFZQZAKRZBLRZQZOZWNWSQZWRWOQZOZOVKVLEHVLWBWHNZHWJABEUDEXEVLCDUEUFVLWB
      WHTUGWMWCXAWIXDWCVLVSHZVLWAHZOWMXAVLVSWATWMXFWQXGWTVRWNVPAVOIFZJZWQCDABPP
      VMARVNWNVQXHVPVMAGUHVMAVOIUIUJVOBRZXIWNWOWPJWQXJVPWOXHWPWNVOBGUHVOBAIUKUM
      WNWOWPUNUOUPXGVLVTRZWMWTVLVTABVCUQWKWLLPHXKWTURUSABKLPPPUTVDSVASWIVLWEHZV
      LWGHZOWMXDVLWEWGTWMXLXBXMXCWLXLXBURWKWLXLWNBWDHZQXBABGWDPVBWLXNWSWNBLPVEV
      FVGVHWLXMAWFHZWOQWKXCABWFGPVBWKXOWRWOAKPVEVIVJVASVAS $.
      $( [14-Oct-2005] $)
  $}

  $( Membership in the set of extended reals. $)
  elxr $p |- ( A e. RR* <-> ( A e. RR \/ A = +oo \/ A = -oo ) ) $=
    ( cxr wcel cr cpnf cmnf cpr cun wo wceq w3o df-xr elun pnfxr elisseti mnfxr
    eleq2i elpr2 orbi2i 3orass bitr4i 3bitri ) ABCADEFGZHZCADCZAUCCZIZUEAEJZAFJ
    ZKZBUDALQADUCMUGUEUHUIIZIUJUFUKUEAEFEBNOFBPORSUEUHUITUAUB $.
    $( [14-Oct-2005] $)

  $( Plus and minus infinity are distinguished elements of ` RR* ` . $)
  pnfnemnf $p |- +oo =/= -oo $=
    ( cpnf cpw cmnf cxr wcel wne pnfxr pwne ax-mp necomi df-mnf neeqtrri ) AABZ
    CMAADEMAFGADHIJKL $.
    $( [6-May-2008] $) $( [14-Oct-2005] $)

  $( The extended real 'less than' is irreflexive. $)
  xrltnr $p |- ( A e. RR* -> -. A < A ) $=
    ( cxr wcel cr cpnf wceq cmnf clt wbr wn wa cltrr wo wnel df-nel mpbi intnan
    intnanr pm3.2ni wb w3o elxr ltnr pnfnre wne pnfnemnf df-ne pnfxr ltxr mp2an
    mtbir breq12 anidms mtbiri mnfnre necomi mnfxr 3jaoi sylbi ) ABCADCZAEFZAGF
    ZUAAAHIZJZAUBUTVDVAVBAUCVAVCEEHIZVEEDCZVFKZEELIZKZEGFZEEFZKZMZVFVKKZVJVFKZM
    ZMZVMVPVIVLVGVHVFVFEDNVFJUDEDOPZQRVJVKEGUEVJJUFEGUGPRSVNVOVFVKVRRVFVJVRQSSE
    BCZVSVEVQTUHUHEEUIUJUKVAVCVETAEAEHULUMUNVBVCGGHIZVTGDCZWAKZGGLIZKZGGFZGEFZK
    ZMZWAWFKZWEWAKZMZMZWHWKWDWGWBWCWAWAGDNWAJUOGDOPZQRWFWEGEUEWFJEGUFUPGEUGPQSW
    IWJWAWFWMRWAWEWMQSSGBCZWNVTWLTUQUQGGUIUJUKVBVCVTTAGAGHULUMUNURUS $.
    $( [14-Oct-2005] $)

  $( Any (finite) real is less than plus infinity. $)
  ltpnf $p |- ( A e. RR -> A < +oo ) $=
    ( cr wcel cpnf clt wbr wa cltrr cmnf wceq wo eqid jctr orc olc 3syl wb rexr
    cxr pnfxr ltxr sylancl mpbird ) ABCZADEFZUDDBCZGADHFGAIJZDDJZGKZUDUHGZUGUFG
    ZKZKZUDUJULUMUDUHDLMUJUKNULUIOPUDASCDSCUEUMQARTADUAUBUC $.
    $( [14-Oct-2005] $)

  $( Minus infinity is less than any (finite) real. $)
  mnflt $p |- ( A e. RR -> -oo < A ) $=
    ( cr wcel cmnf clt wbr wa cltrr wceq cpnf wo eqid jctl olc 3syl cxr wb rexr
    mnfxr ltxr sylancr mpbird ) ABCZDAEFZDBCZUCGDAHFGDDIZAJIZGKZUEUGGZUFUCGZKZK
    ZUCUJUKULUCUFDLMUJUINUKUHNOUCDPCAPCUDULQSARDATUAUB $.
    $( [14-Oct-2005] $)

  $( Minus infinity is less than plus infinity. $)
  mnfltpnf $p |- -oo < +oo $=
    ( cmnf cpnf clt wbr cr wcel wa cltrr wceq wo eqid olc mp2an cxr mnfxr pnfxr
    orci wb ltxr mpbir ) ABCDZAEFZBEFZGABHDGZAAIZBBIZGZJZUBUFGUEUCGJZJZUHUIUEUF
    UHAKBKUGUDLMQANFBNFUAUJROPABSMT $.
    $( [14-Oct-2005] $)

  $( Minus infinity is less than an extended real that is either real or plus
     infinity. $)
  mnfltxr $p |- ( ( A e. RR \/ A = +oo ) -> -oo < A ) $=
    ( cr wcel cmnf clt wbr cpnf wceq mnflt mnfltpnf breq2 mpbiri jaoi ) ABCDAEF
    ZAGHZAIONDGEFJAGDEKLM $.
    $( [2-Feb-2006] $)

  $( No extended real is greater than plus infinity. $)
  pnfnlt $p |- ( A e. RR* -> -. +oo < A ) $=
    ( cxr wcel cpnf clt wbr cr wa cltrr cmnf wceq wo wnel wn pnfnre df-nel mpbi
    intnanr wne pm3.2ni pnfnemnf df-ne wb pnfxr ltxr mpan mtbiri ) ABCZDAEFZDGC
    ZAGCZHZDAIFZHZDJKZADKZHZLZUJUPHZUOUKHZLZLZURVAUNUQULUMUJUKDGMUJNODGPQZRRUOU
    PDJSUONUADJUBQZRTUSUTUJUPVCRUOUKVDRTTDBCUHUIVBUCUDDAUEUFUG $.
    $( [15-Oct-2005] $)

  $( No extended real is less than minus infinity. $)
  nltmnf $p |- ( A e. RR* -> -. A < -oo ) $=
    ( cxr wcel cmnf clt wbr cr wa cltrr wceq cpnf wo wnel wn mnfnre df-nel mpbi
    intnan intnanr pm3.2ni wne pnfnemnf necomi df-ne wb mnfxr ltxr mpan2 mtbiri
    ) ABCZADEFZAGCZDGCZHZADIFZHZADJZDKJZHZLZULURHZUQUMHZLZLZUTVCUPUSUNUOUMULDGM
    UMNODGPQZRSURUQDKUAURNKDUBUCDKUDQZRTVAVBURULVFRUMUQVERTTUJDBCUKVDUEUFADUGUH
    UI $.
    $( [15-Oct-2005] $)

  $( Plus infinity is an upper bound for extended reals. $)
  pnfge $p |- ( A e. RR* -> A <_ +oo ) $=
    ( cxr wcel cpnf cle wbr clt wn pnfnlt wb pnfxr xrlenlt mpan2 mpbird ) ABCZA
    DEFZDAGFHZAIODBCPQJKADLMN $.
    $( [30-Jan-2006] $)

  $( Minus infinity is less than or equal to any extended real. $)
  mnfle $p |- ( A e. RR* -> -oo <_ A ) $=
    ( cxr wcel cmnf cle wbr clt wn nltmnf wb mnfxr xrlenlt mpan mpbird ) ABCZDA
    EFZADGFHZAIDBCOPQJKDALMN $.
    $( [19-Jan-2006] $)

  $( Ordering on the extended reals is not symmetric. $)
  xrltnsym $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A < B -> -. B < A ) ) $=
    ( cxr wcel cr cpnf wceq cmnf clt wbr wn pnfnlt syl adantr adantl mtbird a1d
    wa wb pm2.21d wi elxr ltnsym rexr breq1 nltmnf breq2 3jaodan sylan2br mnfxr
    w3o ax-mp breq12 mtbiri ancoms xrltnr 3jaoian syl2anb ) ACDZAEDZAFGZAHGZUKB
    EDZBFGZBHGZUKZABIJZBAIJZKZUAZBCDZAUBBUBZUTVFVJVAVBUTVCVJVDVEABUCUTVDRZVIVGV
    MVHFAIJZUTVNKZVDUTUSVOAUDZALMNVDVHVNSUTBFAIUEOPQUTVERZVGVIVQVGAHIJZUTVRKZVE
    UTUSVSVPAUFMNVEVGVRSUTBHAIUGOPTUHVFVAVKVJVLVAVKRZVGVIVTVGFBIJZVKWAKVABLOVAV
    GWASVKAFBIUENPTUIVBVCVJVDVEVBVCRZVIVGWBVHBHIJZVCWCKZVBVCVKWDBUDBUFMOVBVHWCS
    VCAHBIUGNPQVBVDRVIVGVDVBVIVDVBRVHFHIJZHCDZWEKUJHLULBFAHIUMUNUOQVBVERZVGVIWG
    VGHHIJZWFWHKUJHUPULAHBHIUMUNTUHUQUR $.
    $( [15-Oct-2005] $)

  $( 'Less than' is antisymmetric and irreflexive for extended reals. $)
  xrltnsym2 $p |- ( ( A e. RR* /\ B e. RR* ) -> -. ( A < B /\ B < A ) ) $=
    ( cxr wcel wa clt wbr wn wi xrltnsym imnan sylib ) ACDBCDEABFGZBAFGZHIMNEHA
    BJMNKL $.
    $( [6-Feb-2007] $)

  $( Ordering on the extended reals satisfies strict trichotomy. $)
  xrlttri $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A < B <-> -. ( A = B \/ B < A ) ) ) $=
    ( wcel wa wceq clt wbr wn wi adantr wb adantl cpnf cmnf mpbird pm2.24d olcd
    breq2 a1d 3jaodan cxr wo xrltnr mtbid xrltnsym ancoms jaod w3o elxr axlttri
    ex cr biimprd con1d ltpnf mnflt breq1 eqtr3 mnfltpnf breq12 3jaoian syl2anb
    orcd mpbiri impbid con2bid ) AUACZBUACZDZABEZBAFGZUBZABFGZVIVLVMHZVIVJVNVKV
    GVJVNIVHVGVJVNVGVJDAAFGZVMVGVOHVJAUCJVJVOVMKVGABAFRLUDUKJVHVGVKVNIBAUEUFUGV
    GAULCZAMEZANEZUHBULCZBMEZBNEZUHZVNVLIZVHAUIBUIVPWBWCVQVRVPVSWCVTWAVPVSDZVLV
    MWDVMVLHABUJUMUNVPVTDZVMVLWEVMAMFGZVPWFVTAUOJVTVMWFKVPBMAFRLOPVPWADZVLVNWGV
    KVJWGVKNAFGZVPWHWAAUPJWAVKWHKVPBNAFUQLOQSTVQVSWCVTWAVQVSDZVLVNWIVKVJWIVKBMF
    GZVSWJVQBUOLVQVKWJKVSAMBFRJOQSVQVTDZVLVNWKVJVKABMURVCSVQWADZVLVNWLVKVJWAVQV
    KWAVQDVKNMFGZUSBNAMFUTVDUFQSTVRVSWCVTWAVRVSDZVMVLWNVMNBFGZVSWOVRBUPLVRVMWOK
    VSANBFUQJOPVRVTDZVMVLWPVMWMUSANBMFUTVDPVRWADZVLVNWQVJVKABNURVCSTVAVBVEVF $.
    $( [14-Oct-2005] $)

  $( Ordering on the extended reals is transitive. $)
  xrlttr $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
             ( ( A < B /\ B < C ) -> A < C ) ) $=
    ( cxr wcel cr cpnf wceq cmnf w3o clt wbr wa wn adantr adantl mtbird pm2.21d
    wb adantlr wi elxr axlttrn 3expa an32s rexr pnfnlt syl breq1 adantll nltmnf
    adantld breq2 adantrd 3jaodan sylan2b ltpnf mpbird a1d anasss adantrr mnflt
    mnfltpnf breq12 mpbiri 3jaoian 3impb syl3an3b syl3an1b ) ADEZAFEZAGHZAIHZJZ
    BDEZCDEZABKLZBCKLZMZACKLZUAZAUBVPVNVOCFEZCGHZCIHZJZWACUBVNVOWEWAVKVOWEMWAVL
    VMVKVOWEWAVKVOMZWBWAWCWDVKWBVOWAVOVKWBMZBFEZBGHZBIHZJWABUBWGWHWAWIWJVKWHWBW
    AVKWHWBWAABCUCUDUEWGWIMVRVTVQWBWIVRVTUAVKWBWIMZVRVTWKVRGCKLZWBWLNZWIWBVPWMC
    UFCUGUHOWIVRWLSWBBGCKUIPQRUJULWGWJMVQVTVRVKWJVQVTUAWBVKWJMZVQVTWNVQAIKLZVKW
    ONZWJVKVJWPAUFAUKUHOWJVQWOSVKBIAKUMPQRTUNUOUPUEWFWCMVTVSVKWCVTVOVKWCMVTAGKL
    ZVKWQWCAUQOWCVTWQSVKCGAKUMPURTUSVOWDWAVKVOWDMZVRVTVQWRVRVTWRVRBIKLZVOWSNWDB
    UKOWDVRWSSVOCIBKUMPQRULZUJUOUTVLVOWAWEVLVOMZVQVTVRXAVQVTXAVQGBKLZVOXBNVLBUG
    PVLVQXBSVOAGBKUIOQRUNVAVMVOWEWAVMVOMWBWAWCWDVMWBWAVOVMWBMZVTVSXCVTICKLZWBXD
    VMCVBPVMVTXDSWBAICKUIOURUSTVMWCWAVOVMWCMZVTVSXEVTIGKLVCAICGKVDVEUSTVOWDWAVM
    WTUJUOUTVFVGVHVI $.
    $( [15-Oct-2005] $)

  ${
    $d x y z A $.
    $( 'Less than' is a strict ordering on the extended reals. $)
    xrltso $p |- < Or RR* $=
      ( vx vy vz cxr clt cv wcel w3a wceq wo wn wb wa wi xrlttri 3adant3 xrlttr
      wbr jca so ) ABCDEAFZDGZBFZDGZCFZDGZHUAUCERZUAUCIUCUAERJKLZUGUCUEERMUAUEE
      RNUBUDUHUFUAUCOPUAUCUEQST $.
      $( [15-Oct-2005] $)
  $}

  $( Trichotomy law for 'less than' for extended reals. $)
  xrlttri2 $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A =/= B <-> ( A < B \/ B < A ) ) ) $=
    ( cxr wcel wa clt wbr wo wceq wn wor xrltso sotrieq mpan bicomd necon1abid
    wb ) ACDBCDEZABFGBAFGHZABRABIZSJZCFKRTUAQLCABFMNOP $.
    $( [10-Dec-2007] $)

  $( Trichotomy law for 'less than' for extended reals. $)
  xrlttri3 $p |- ( ( A e. RR* /\ B e. RR* ) ->
                ( A = B <-> ( -. A < B /\ -. B < A ) ) ) $=
    ( cxr clt wor wcel wa wceq wbr wn wb xrltso sotrieq2 mpan ) CDEACFBCFGABHAB
    DIJBADIJGKLCABDMN $.
    $( [9-Feb-2006] $)

  $( 'Less than or equal' expressed in terms of 'less than' or 'equals', for
     extended reals. $)
  xrleloe $p |- ( ( A e. RR* /\ B e. RR* ) ->
               ( A <_ B <-> ( A < B \/ A = B ) ) ) $=
    ( cxr wcel wa cle wbr clt wn wo xrlenlt eqcom orbi1i orcom bitri wb xrlttri
    wceq ancoms con2bid syl5rbbr bitrd ) ACDZBCDZEZABFGBAHGZIZABHGZABRZJZABKUJB
    ARZUHJZUEUGULUIUHJUJUKUIUHBALMUIUHNOUEUFULUDUCUFULIPBAQSTUAUB $.
    $( [19-Jan-2006] $)

  $( 'Less than or equal to' implies 'less than' is not 'equals', for extended
     reals. $)
  xrleltne $p |- ( ( A e. RR* /\ B e. RR* /\ A <_ B ) ->
                ( A < B <-> B =/= A ) ) $=
    ( cxr wcel cle wbr clt wne wb wa wceq wn wi xrlttri3 simpl syl6bi adantr wo
    xrleloe biimpa ord impbid necon2abid necom syl6bbr 3impa ) ACDZBCDZABEFZABG
    FZBAHZIUGUHJZUIJZUJABHUKUMUJABUMABKZUJLZULUNUOMUIULUNUOBAGFLZJUOABNUOUPOPQU
    MUJUNULUIUJUNRABSTUAUBUCBAUDUEUF $.
    $( [22-Jul-2007] $) $( [9-Feb-2006] $)

  $( 'Less than' implies 'less than or equal' for extended reals. $)
  xrltle $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A < B -> A <_ B ) ) $=
    ( clt wbr cle cxr wcel wa wceq wo orc xrleloe syl5ibr ) ABCDZABEDAFGBFGHNAB
    IZJNOKABLM $.
    $( [19-Jan-2006] $)

  $( 'Less than or equal to' is reflexive for extended reals. $)
  xrleid $p |- ( A e. RR* -> A <_ A ) $=
    ( cxr wcel cle wbr wa clt wceq wo eqid olci xrleloe mpbiri anidms ) ABCZAAD
    EZOOFPAAGEZAAHZIRQAJKAALMN $.
    $( [7-Feb-2007] $)

  $( Trichotomy law for extended reals. $)
  xrletri $p |- ( ( A e. RR* /\ B e. RR* ) -> ( A <_ B \/ B <_ A ) ) $=
    ( cxr wcel wa cle wbr wn clt wb xrltnle ancoms wi xrltle sylbird orrd ) ACD
    ZBCDZEZABFGZBAFGZSTHZBAIGZUARQUCUBJBAKLRQUCUAMBANLOP $.
    $( [7-Feb-2007] $)

  $( Transitive law for ordering on extended reals. $)
  xrlelttr $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
             ( ( A <_ B /\ B < C ) -> A < C ) ) $=
    ( cxr wcel w3a cle wbr clt wceq wo wi wb xrleloe 3adant3 xrlttr exp3a breq1
    biimprd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACIHZUGUHABIHZABJZK
    ZUIUJLZUDUEUHUMMUFABNOUGUKUNULUGUKUIUJABCPQULUNLUGULUJUIABCIRSTUAUBUC $.
    $( [19-Jan-2006] $)

  $( Transitive law for ordering on extended reals. $)
  xrltletr $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
             ( ( A < B /\ B <_ C ) -> A < C ) ) $=
    ( cxr wcel w3a clt wbr cle wceq wo wi wb xrleloe 3adant1 xrlttr exp3a com23
    breq2 biimpd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACGHZUHUJUIUKU
    HUJBCGHZBCJZKZUIUKLZUFUGUJUNMUEBCNOUHULUOUMUHUIULUKUHUIULUKABCPQRUMUOLUHUMU
    IUKBCAGSTUAUBUCRUD $.
    $( [19-Jan-2006] $)

  $( Transitive law for ordering on extended reals. $)
  xrletr $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
             ( ( A <_ B /\ B <_ C ) -> A <_ C ) ) $=
    ( cxr wcel w3a cle wbr wa clt wceq wo wb xrleloe 3adant1 adantr xrlelttr wi
    xrltle 3adant2 syld expdimp breq2 biimpcd adantl jaod sylbid expimpd ) ADEZ
    BDEZCDEZFZABGHZBCGHZACGHZULUMIZUNBCJHZBCKZLZUOULUNUSMZUMUJUKUTUIBCNOPUPUQUO
    URULUMUQUOULUMUQIACJHZUOABCQUIUKVAUORUJACSTUAUBUMURUORULURUMUOBCAGUCUDUEUFU
    GUH $.
    $( [9-Feb-2006] $)

  $( 'Less than' implies not equal for extended reals. $)
  xrltne $p |- ( ( A e. RR* /\ B e. RR* /\ A < B ) -> B =/= A ) $=
    ( cxr wcel clt wbr w3a wne wo wa orc wor wceq wn wb sotrieq mpan necon2abid
    xrltso syl5ib 3impia necomd ) ACDZBCDZABEFZGABUCUDUEABHZUEUEBAEFZIZUCUDJZUF
    UEUGKUIUHABCELUIABMUHNOSCABEPQRTUAUB $.
    $( [22-Aug-2007] $) $( [20-Jan-2006] $)

  $( An extended real is not less than plus infinity iff they are equal. $)
  nltpnft $p |- ( A e. RR* -> ( A = +oo <-> -. A < +oo ) ) $=
    ( cxr wcel cpnf wceq clt wbr wn pnfxr xrltnr ax-mp breq1 mtbiri wo pnfge wb
    cle xrleloe mpan2 mpbid ord impbid2 ) ABCZADEZADFGZHUDUEDDFGZDBCZUFHIDJKADD
    FLMUCUEUDUCADQGZUEUDNZAOUCUGUHUIPIADRSTUAUB $.
    $( [30-Jan-2006] $)

  $( An extended real is not greater than minus infinity iff they are equal. $)
  ngtmnft $p |- ( A e. RR* -> ( A = -oo <-> -. -oo < A ) ) $=
    ( cxr wcel cmnf wceq clt wbr wn mnfxr xrltnr ax-mp breq2 mtbiri wo mnfle wb
    cle xrleloe mpan mpbid ord eqcom syl6ib impbid2 ) ABCZADEZDAFGZHZUFUGDDFGZD
    BCZUIHIDJKADDFLMUEUHDAEZUFUEUGUKUEDAQGZUGUKNZAOUJUEULUMPIDARSTUADAUBUCUD $.
    $( [2-Feb-2006] $)

  $( An extended real is real iff it is strictly bounded by infinities. $)
  xrrebnd $p |- ( A e. RR* -> ( A e. RR <-> ( -oo < A /\ A < +oo ) ) ) $=
    ( cxr wcel cr cmnf clt wbr cpnf wa mnflt ltpnf wceq nltpnft ngtmnft orbi12d
    jca wo wn ianor orcom bitr2i syl6bb con2bid w3o elxr biimpi bitri sylib ord
    3orass sylbid impbid2 ) ABCZADCZEAFGZAHFGZIZUNUOUPAJAKPUMUQAHLZAELZQZRUNUMU
    TUQUMUTUPRZUORZQZUQRZUMURVAUSVBAMANOVDVBVAQVCUOUPSVBVATUAUBUCUMUTUNUMUNURUS
    UDZUTUNQZUMVEAUEUFVEUNUTQVFUNURUSUJUNUTTUGUHUIUKUL $.
    $( [2-Feb-2006] $)

  $( A way of proving that an extended real is real. $)
  xrre $p |- ( ( ( A e. RR* /\ B e. RR ) /\ ( -oo < A /\ A <_ B ) ) ->
              A e. RR ) $=
    ( cxr wcel cr wa cmnf clt wbr cle cpnf xrrebnd ad2antrr simprl ltpnf adantl
    wb wi rexr pnfxr xrlelttr mp3an3 sylan2 mpan2d imp adantrl mpbir2and ) ACDZ
    BEDZFZGAHIZABJIZFZFAEDZUKAKHIZUHUNUKUOFQUIUMALMUJUKULNUJULUOUKUJULUOUJULBKH
    IZUOUIUPUHBOPUIUHBCDZULUPFUORZBSUHUQKCDURTABKUAUBUCUDUEUFUG $.
    $( [10-Mar-2005] $) $( [9-Mar-2006] $)

  $( An extended real between two others is real. $)
  xrre2 $p |- ( ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) /\
              ( A < B /\ B < C ) ) -> B e. RR ) $=
    ( cxr wcel w3a clt wbr wa cr cmnf wi cle mnfle adantr mnfxr xrlelttr mp3an1
    cpnf mpand 3adant3 pnfge adantl pnfxr mp3an3 mpan2d 3adant1 anim12d xrrebnd
    xrltletr wb 3ad2ant2 sylibrd imp ) ADEZBDEZCDEZFZABGHZBCGHZIZBJEZURVAKBGHZB
    SGHZIZVBURUSVCUTVDUOUPUSVCLUQUOUPIKAMHZUSVCUOVFUPANOKDEUOUPVFUSIVCLPKABQRTU
    AUPUQUTVDLUOUPUQIUTCSMHZVDUQVGUPCUBUCUPUQSDEUTVGIVDLUDBCSUJUEUFUGUHUPUOVBVE
    UKUQBUIULUMUN $.
    $( [6-Feb-2007] $)

  $( An extended real is less than or equal to the maximum of it and
     another. $)
  xrmax1 $p |- ( ( A e. RR* /\ B e. RR* ) -> A <_ if ( A <_ B , B , A ) ) $=
    ( cxr wcel cle wbr wn xrleid iffalse breq2d syl5ibrcom id breqtrrd pm2.61d2
    cif iftrue adantr ) ACDZAABEFZBAOZEFZBCDRSUARUASGZAAEFAHUBTAAESBAIJKSABTESL
    SBAPMNQ $.
    $( [7-Feb-2007] $)

  $( An extended real is less than or equal to the maximum of it and
     another. $)
  xrmax2 $p |- ( ( A e. RR* /\ B e. RR* ) -> B <_ if ( A <_ B , B , A ) ) $=
    ( cxr wcel wa cle wbr cif xrleid ad2antlr iftrue adantl breqtrrd wn xrletri
    wceq orcanai iffalse pm2.61dan ) ACDZBCDZEZABFGZBUCBAHZFGUBUCEBBUDFUABBFGTU
    CBIJUCUDBPUBUCBAKLMUBUCNZEBAUDFUBUCBAFGABOQUEUDAPUBUCBARLMS $.
    $( [7-Feb-2007] $)

  $( The minimum of two extended reals is less than or equal to one of them. $)
  xrmin1 $p |- ( ( A e. RR* /\ B e. RR* ) -> if ( A <_ B , A , B ) <_ A ) $=
    ( cxr wcel wa cle wbr wceq iftrue adantl xrleid ad2antrr eqbrtrd wn iffalse
    cif xrletri orcanai pm2.61dan ) ACDZBCDZEZABFGZUCABPZAFGUBUCEUDAAFUCUDAHUBU
    CABIJTAAFGUAUCAKLMUBUCNZEUDBAFUEUDBHUBUCABOJUBUCBAFGABQRMS $.
    $( [7-Feb-2007] $)

  $( The minimum of two extended reals is less than or equal to one of them. $)
  xrmin2 $p |- ( ( A e. RR* /\ B e. RR* ) -> if ( A <_ B , A , B ) <_ B ) $=
    ( cxr wcel cle wbr cif wn xrleid iffalse breq1d syl5ibrcom eqbrtrd pm2.61d2
    iftrue id adantl ) BCDZABEFZABGZBEFZACDRSUARUASHZBBEFBIUBTBBESABJKLSTABESAB
    OSPMNQ $.
    $( [7-Feb-2007] $)

  $( Two ways of saying the maximum of two extended reals is less than a
     third. $)
  xrmaxlt $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
               ( if ( A <_ B , B , A ) < C <-> ( A < C /\ B < C ) ) ) $=
    ( cxr wcel w3a cle wbr cif clt xrmax1 3adant3 ifcl ancoms xrlelttr syld3an2
    wa wi mpand breq1 xrmax2 simp2 simp3 syl111anc jcad ifboth impbid1 ) ADEZBD
    EZCDEZFZABGHZBAIZCJHZACJHZBCJHZQUKUNUOUPUKAUMGHZUNUOUHUIUQUJABKLUHUMDEZUIUJ
    UQUNQUORUHUIURUJUIUHURULBADMNLZAUMCOPSUKBUMGHZUNUPUHUIUTUJABUALUKUIURUJUTUN
    QUPRUHUIUJUBUSUHUIUJUCBUMCOUDSUEUPUOUNULUPUOUNBABUMCJTAUMCJTUFNUG $.
    $( [7-Feb-2007] $)

  $( Two ways of saying an extended real is less than the minimum of two
     others. $)
  xrltmin $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
               ( A < if ( B <_ C , B , C ) <-> ( A < B /\ A < C ) ) ) $=
    ( cxr wcel w3a cle wbr cif clt wa xrmin1 3adant1 wi simp1 ifcl simp2 mpan2d
    xrltletr breq2 syl111anc xrmin2 syld3an2 jcad ifboth impbid1 ) ADEZBDEZCDEZ
    FZABCGHZBCIZJHZABJHZACJHZKUJUMUNUOUJUMULBGHZUNUHUIUPUGBCLMUJUGULDEZUHUMUPKU
    NNUGUHUIOUHUIUQUGUKBCDPMZUGUHUIQAULBSUARUJUMULCGHZUOUHUIUSUGBCUBMUGUQUHUIUM
    USKUONURAULCSUCRUDUKUNUOUMBCBULAJTCULAJTUEUF $.
    $( [7-Feb-2007] $)

  $( A number is less than or equal to the maximum of it and another. $)
  max1 $p |- ( ( A e. RR /\ B e. RR ) -> A <_ if ( A <_ B , B , A ) ) $=
    ( cr wcel cxr cle wbr cif rexr xrmax1 syl2an ) ACDAEDBEDAABFGBAHFGBCDAIBIAB
    JK $.
    $( [3-Apr-2005] $)

  $( A number is less than or equal to the maximum of it and another. $)
  max1ALT $p |- ( A e. RR -> A <_ if ( A <_ B , B , A ) ) $=
    ( cr wcel cle wbr cif wn iffalse breq2d syl5ibrcom iftrue breqtrrd pm2.61d2
    leid id ) ACDZABEFZARBAGZEFZQTRHZAAEFAOUASAAERBAIJKRABSERPRBALMN $.
    $( [3-Apr-2005] $)

  $( A number is less than or equal to the maximum of it and another. $)
  max2 $p |- ( ( A e. RR /\ B e. RR ) -> B <_ if ( A <_ B , B , A ) ) $=
    ( cr wcel cxr cle wbr cif rexr xrmax2 syl2an ) ACDAEDBEDBABFGBAHFGBCDAIBIAB
    JK $.
    $( [3-Apr-2005] $)

  $( Two ways of saying the maximum of two numbers is less than or equal to a
     third. $)
  maxle $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
               ( if ( A <_ B , B , A ) <_ C <-> ( A <_ C /\ B <_ C ) ) ) $=
    ( cr wcel w3a cle wbr cif wa max1 3adant3 wi ifcl ancoms letr syld3an2 max2
    mpand breq1 simp2 simp3 syl111anc jcad ifboth impbid1 ) ADEZBDEZCDEZFZABGHZ
    BAIZCGHZACGHZBCGHZJUJUMUNUOUJAULGHZUMUNUGUHUPUIABKLUGULDEZUHUIUPUMJUNMUGUHU
    QUIUHUGUQUKBADNOLZAULCPQSUJBULGHZUMUOUGUHUSUIABRLUJUHUQUIUSUMJUOMUGUHUIUAUR
    UGUHUIUBBULCPUCSUDUOUNUMUKUOUNUMBABULCGTAULCGTUEOUF $.
    $( [29-Sep-2005] $)

  $( The minimum of two numbers is less than or equal to the first. $)
  min1 $p |- ( ( A e. RR /\ B e. RR ) -> if ( A <_ B , A , B ) <_ A ) $=
    ( cr wcel cxr cle wbr cif rexr xrmin1 syl2an ) ACDAEDBEDABFGABHAFGBCDAIBIAB
    JK $.
    $( [3-Aug-2007] $)

  $( The minimum of two numbers is less than or equal to the second. $)
  min2 $p |- ( ( A e. RR /\ B e. RR ) -> if ( A <_ B , A , B ) <_ B ) $=
    ( cr wcel cxr cle wbr cif rexr xrmin2 syl2an ) ACDAEDBEDABFGABHBFGBCDAIBIAB
    JK $.
    $( [3-Aug-2007] $)

  $( Two ways of saying a number is less than or equal to the minimum of two
     others. $)
  lemin $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
               ( A <_ if ( B <_ C , B , C ) <-> ( A <_ B /\ A <_ C ) ) ) $=
    ( cr wcel w3a cle wbr cif wa min1 3adant1 simp1 ifcl simp2 syl111anc mpan2d
    wi letr breq2 min2 syld3an2 jcad ifboth impbid1 ) ADEZBDEZCDEZFZABCGHZBCIZG
    HZABGHZACGHZJUIULUMUNUIULUKBGHZUMUGUHUOUFBCKLUIUFUKDEZUGULUOJUMRUFUGUHMUGUH
    UPUFUJBCDNLZUFUGUHOAUKBSPQUIULUKCGHZUNUGUHURUFBCUALUFUPUGUHULURJUNRUQAUKCSU
    BQUCUJUMUNULBCBUKAGTCUKAGTUDUE $.
    $( [3-Aug-2007] $)

  $( Two ways of saying the maximum of two numbers is less than a third. $)
  maxlt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
               ( if ( A <_ B , B , A ) < C <-> ( A < C /\ B < C ) ) ) $=
    ( cr wcel w3a cle wbr cif clt wa max1 3adant3 wi ifcl ancoms syld3an2 mpand
    lelttr breq1 max2 simp2 simp3 syl111anc jcad ifboth impbid1 ) ADEZBDEZCDEZF
    ZABGHZBAIZCJHZACJHZBCJHZKUKUNUOUPUKAUMGHZUNUOUHUIUQUJABLMUHUMDEZUIUJUQUNKUO
    NUHUIURUJUIUHURULBADOPMZAUMCSQRUKBUMGHZUNUPUHUIUTUJABUAMUKUIURUJUTUNKUPNUHU
    IUJUBUSUHUIUJUCBUMCSUDRUEUPUOUNULUPUOUNBABUMCJTAUMCJTUFPUG $.
    $( [3-Aug-2007] $)

  $( Two ways of saying a number is less than the minimum of two others. $)
  ltmin $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
               ( A < if ( B <_ C , B , C ) <-> ( A < B /\ A < C ) ) ) $=
    ( cr wcel cxr cle wbr cif clt wa wb rexr xrltmin syl3an ) ADEAFEBDEBFECDECF
    EABCGHBCIJHABJHACJHKLAMBMCMABCNO $.
    $( [1-Sep-2006] $)

  ${
    $d k M $.  $d k N $.
    $( There exists an integer greater than or equal to any two others. $)
    z2ge $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                E. k e. ZZ ( M <_ k /\ N <_ k ) ) $=
      ( cz wcel wa cle wbr cif cv wrex ifcl ancoms cr zre max1 jca syl2an breq2
      max2 wceq anbi12d rcla4ev syl11anc ) BDEZCDEZFBCGHZCBIZDEZBUHGHZCUHGHZFZB
      AJZGHZCUMGHZFZADKUFUEUIUGCBDLMUEBNEZCNEZULUFBOCOUQURFUJUKBCPBCTQRUPULAUHD
      UMUHUAUNUJUOUKUMUHBGSUMUHCGSUBUCUD $.
      $( [28-Aug-2005] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( The rational numbers are dense in ` RR ` : any two real numbers have a
       rational between them.  Exercise 6 of [Apostol] p. 28. $)
    qbtwnre $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
                  E. x e. QQ ( A < x /\ x < B ) ) $=
      ( vy vz cr wcel clt wbr cv wa cq wrex c1 co wi sylan cz wb cc cdiv cn cc0
      cmin posdif resubcl nnrecl ancoms sylbid cmul cle caddc wreu nnre remulcl
      peano2rem syl adantrl zbtwnre reurex 3syl anass zre wceq nncn recn syl3an
      subdi 3com23 3expb breq2d nngt0 ltdivmulOLD mp3anl1 exp31 com23 sylc syl5
      ex 1re ltsub13 mp3an3 syl2an anandis 3bitr4d adantr anbi1d adantrr ltletr
      imp 3expa ltmuldiv2OLD 3exp1 com4r mpcom adantrd imp31 sylibd mulcl npcan
      jca ax-1cn sylancl 3ad2ant2 syl3anl2 mpdan bitr4d biimpd adantlrl anim12d
      w3a 3coml sylan2 adantlr jctild breq2 breq1 anbi12d rcla4ev syl5bir exp4b
      znq syl6 com34 rexlimdv mpd expcom syld 3impia ) BFGZCFGZBCHIZBAJZHIZYMCH
      IZKZALMZYJYKKZYLNDJZUAOCBUDOZHIZDUBMZYQYRYLUCYTHIZUUBBCUEYKYJUUCUUBPYKYJK
      ZUUCUUBUUDYTFGZUUCUUBCBUFZYTDUGQVSUHUIYRUUAYQDUBYSUBGZYRUUAYQPZUUGYRKZYSC
      UJOZNUDOZEJZUKIZUULUUKNULOZHIZKZERMZUUHUUIUUKFGZUUPERUMUUQUUGYKUURYJUUGYK
      KZUUJFGZUURUUGYSFGZYKUUTYSUNZYSCUOZQUUJUPUQURZEUUKUSUUPERUTVAUUIUUPUUHERU
      UIUULRGZUUAUUPYQUUIUVEUUAUUPYQUUAUUPKUUAUUMKZUUOKZUUIUVEKZYQUUAUUMUUOVBUV
      HUVGUULYSUAOZLGZBUVIHIZUVICHIZKZKYQUVHUVGUVMUVJUVEUUIUULFGZUVGUVMPUULVCUU
      IUVNKZUVFUVKUUOUVLUVOUVFYSBUJOZUULHIZUVKUVOUVFUVPUUKHIZUUMKZUVQUVOUUAUVRU
      UMUUIUUAUVRSUVNUUINYSYTUJOZHIZNUUJUVPUDOZHIZUUAUVRUUIUVTUWBNHUUGYJYKUVTUW
      BVDZUUGYKYJUWDUUGYSTGZYKCTGZYJBTGUWDYSVEZCVFZBVFYSCBVHVGVIVJVKUUGYRUUAUWA
      SZYRUUEUUGUWIYKYJUUEUUFUHUUGUVAUCYSHIZUUEUWIPUVBYSVLZUVAUUEUWJUWIUVAUUEUW
      JUWINFGZUVAUUEUWJUWIVTNYSYTVMVNVOVPVQVRWJUUGUVAYRUVRUWCSZUVBUVAYJYKUWMUVA
      YJKUVPFGZUUTUWMUVAYKKYSBUOZUVCUWNUUTUWLUWMVTUVPUUJNWAWBWCWDQWEWFWGUUIUWNU
      URKUVNUVSUVQPZUUIUWNUURUUGYJUWNYKUUGUVAYJUWNUVBUWOQWHUVDXAUWNUURUVNUWPUVP
      UUKUULWIWKQUIUUGYRUVNUVQUVKSZUUGYJUVNUWQPZYKUWJUUGYJUWRPUWKUUGYJUVNUWJUWQ
      UUGUVAYJUVNUWJUWQPPPUVBUVAYJUVNUWJUWQYSBUULWLWMUQWNWOWPWQWRUUGYKUVNUUOUVL
      PYJUUSUVNKZUUOUVLUWSUUOUULUUJHIZUVLUWSUUNUUJUULHUUSUUNUUJVDZUVNUUSUUJTGZN
      TGUXAUUGUWEUWFUXBYKUWGUWHYSCWSWCXBUUJNWTXCWFVKUUGYKUVNUVLUWTSZUVNUUGYKUXC
      UVNUUGYKXKUWJUXCUUGUVNUWJYKUWKXDUUGUVNUVAYKUWJUXCUVBUULYSCVMXEXFXLWKXGXHX
      IXJXMUUGUVEUVJYRUVEUUGUVJUULYSYBUHXNXOYPUVMAUVILYMUVIVDYNUVKYOUVLYMUVIBHX
      PYMUVICHXQXRXSYCXTYAYDYEYFYGYEYHYI $.
      $( [15-Feb-2007] $) $( [18-Nov-2004] $)

    $( The rational numbers are dense in ` RR* ` : any two extended real
       numbers have a rational between them. $)
    qbtwnxr $p |- ( ( A e. RR* /\ B e. RR* /\ A < B )
                   -> E. x e. QQ ( A < x /\ x < B ) ) $=
      ( cxr wcel clt wbr wa cq wrex cr cpnf wceq cmnf c1 syl adantr rexr adantl
      wb cv w3o wi elxr qbtwnre 3expia caddc peano2re ltp1 mpd3an23 ltpnf pnfxr
      co qre xrlttr mp3an3 syl11anc mpan2d anim2d reximdva breq2 anbi2d rexbidv
      mpd mpbird a1d wn nltmnf notbid pm2.21d 3jaodan sylan2b pnfnlt breq1 cmin
      peano2rem id ltm1 syl111anc mnflt mnfxr mp3an1 syl2an mpand anim1d anbi1d
      ax-mp mpbiri cz 1z zq anbi12d rcla4ev breq12 mtbiri 3jaoian sylanb 3impia
      1re mpan ) BDEZCDEZBCFGZBAUAZFGZXDCFGZHZAIJZXABKEZBLMZBNMZUBXBXCXHUCZBUDX
      IXBXLXJXKXBXICKEZCLMZCNMZUBZXLCUDZXIXMXLXNXOXIXMXCXHABCUEUFXIXNHZXHXCXRXH
      XEXDLFGZHZAIJZXIYAXNXIXEXDBOUGUMZFGZHZAIJZYAXIYBKEZBYBFGYEBUHZBUIABYBUEUJ
      XIYDXTAIXIXDIEZHZYCXSXEYIYCYBLFGZXSXIYJYHXIYFYJYGYBUKPQYIXDDEZYBDEZYCYJHX
      SUCZYHYKXIYHXDKEYKXDUNXDRPZSXIYLYHXIYFYLYGYBRPQYKYLLDEYMULXDYBLUOUPUQURUS
      UTVDQXNXHYATXIXNXGXTAIXNXFXSXECLXDFVAVBVCSVEVFXIXOHZXCXHYOXCVGZBNFGZVGZXI
      YRXOXIXAYRBRBVHPQXOYPYRTXIXOXCYQCNBFVAVISVEVJVKVLXJXBHZXCXHYSYPLCFGZVGZXB
      UUAXJCVMSXJYPUUATXBXJXCYTBLCFVNVIQVEVJXBXKXPXLXQXKXMXLXNXOXKXMHZXHXCUUBXH
      NXDFGZXFHZAIJZXMUUEXKXMCOVOUMZXDFGZXFHZAIJZUUEXMUUFKEZXMUUFCFGUUICVPZXMVQ
      CVRAUUFCUEVSXMUUHUUDAIXMYHHZUUGUUCXFUULNUUFFGZUUGUUCXMUUMYHXMUUJUUMUUKUUF
      VTPQXMUUFDEZYKUUMUUGHUUCUCZYHXMUUJUUNUUKUUFRPYNNDEZUUNYKUUOWANUUFXDUOWBWC
      WDWEUTVDSXKXHUUETXMXKXGUUDAIXKXEUUCXFBNXDFVNWFVCQVEVFXKXNHXHXCXKBOFGZOCFG
      ZXHXNXKUUQNOFGZOKEZUUSWSOVTWGBNOFVNWHXNUUROLFGZUUTUVAWSOUKWGCLOFVAWHOIEZU
      UQUURHZXHOWIEUVBWJOWKWGXGUVCAOIXDOMXEUUQXFUURXDOBFVAXDOCFVNWLWMWTWCVFXKXO
      HZXCXHUVDXCNNFGZUUPUVEVGWANVHWGBNCNFWNWOVJVKVLWPWQWR $.
      $( [6-Feb-2007] $)
  $}

  ${
    $d x A $.
    $( If a nonnegative real is less than any positive rational, it is zero. $)
    qsqueeze $p |- ( ( A e. RR /\ 0 <_ A /\ A. x e. QQ ( 0 < x -> A < x ) ) ->
                   A = 0 ) $=
      ( cr wcel cc0 cle wbr cv clt wi cq wral w3a wceq wn wa wrex wb 0re ltnle
      mpan qbtwnre mp3an1 qre ltnsym con2d sylan2 anim2d reximdva syld rexanali
      sylbird syl6ib con4d imp 3adant2 letri3 mpan2 iba bicomd sylan9bb 3adant3
      ex mpbird ) BCDZEBFGZEAHZIGZBVGIGZJAKLZMBENZBEFGZVEVJVLVFVEVJVLVEVLVJVEVL
      OZVHVIOZPZAKQZVJOVEVMEBIGZVPECDZVEVQVMRSEBTUAVEVQVHVGBIGZPZAKQZVPVEVQWAVR
      VEVQWASAEBUBUCVCVEVTVOAKVEVGKDZPVSVNVHWBVEVGCDZVSVNJVGUDVEWCPVIVSBVGUEUFU
      GUHUIUJULVHVIAKUKUMUNUOUPVEVFVKVLRVJVEVKVLVFPZVFVLVEVRVKWDRSBEUQURVFVLWDV
      FVLUSUTVAVBVD $.
      $( [6-Feb-2007] $)
  $}

