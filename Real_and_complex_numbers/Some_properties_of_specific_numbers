$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Some properties of specific numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Two plus two equals four.  For more information, see "2+2=4 Trivia" on the
     Metamath Proof Explorer Home Page:
     ~ http://us.metamath.org/mpegif/mmset.html#trivia . $)
  2p2e4 $p |- ( 2 + 2 ) = 4 $=
    ( c2 caddc co c1 c4 df-2 oveq2i c3 df-4 oveq1i ax-1cn addassi 3eqtri eqtr4i
    df-3 2cn ) AABCADDBCZBCZEAQABFGEHDBCADBCZDBCRIHSDBOJADDPKKLMN $.
    $( [27-May-1999] $)

  ${
    2times.1 $e |- A e. CC $.
    $( Two times a number. $)
    2timesi $p |- ( 2 x. A ) = ( A + A ) $=
      ( c2 cmul co c1 caddc df-2 oveq1i 1p1timesi eqtri ) CADEFFGEZADEAAGECLADH
      IABJK $.
      $( [1-Aug-1999] $)
  $}

  $( Two times a number. $)
  2times $p |- ( A e. CC -> ( 2 x. A ) = ( A + A ) ) $=
    ( cc wcel c2 cmul co caddc wceq cc0 cif oveq2 id oveq12d eqeq12d 0cn elimel
    2timesi dedth ) ABCZDAEFZAAGFZHDSAIJZEFZUBUBGFZHAIAUBHZTUCUAUDAUBDEKUEAUBAU
    BGUELZUFMNUBAIBOPQR $.
    $( [10-Oct-2004] $)

  $( A number times 2. $)
  times2 $p |- ( A e. CC -> ( A x. 2 ) = ( A + A ) ) $=
    ( cc wcel c2 cmul co caddc wceq 2cn mulcom mpan2 2times eqtrd ) ABCZADEFZDA
    EFZAAGFNDBCOPHIADJKALM $.
    $( [16-Oct-2007] $)

  ${
    times2.1 $e |- A e. CC $.
    $( A number times 2. $)
    times2i $p |- ( A x. 2 ) = ( A + A ) $=
      ( cc wcel c2 cmul co caddc wceq times2 ax-mp ) ACDAEFGAAHGIBAJK $.
      $( [11-May-2004] $)
  $}

  $( 3 + 2 = 5. $)
  3p2e5 $p |- ( 3 + 2 ) = 5 $=
    ( c4 c1 caddc co c3 c5 c2 df-4 oveq1i df-5 df-2 oveq2i recni ax-1cn addassi
    3re eqtr4i 3eqtr4ri ) ABCDEBCDZBCDZFEGCDZASBCHIJUAEBBCDZCDTGUBECKLEBBEPMNNO
    QR $.
    $( [11-May-2004] $)

  $( 3 + 3 = 6. $)
  3p3e6 $p |- ( 3 + 3 ) = 6 $=
    ( c3 c2 caddc co c1 c5 c6 3p2e5 oveq1i df-3 oveq2i 3re recni ax-1cn addassi
    2cn eqtr4i df-6 3eqtr4i ) ABCDZECDZFECDAACDZGTFECHIUBABECDZCDUAAUCACJKABEAL
    MPNOQRS $.
    $( [11-May-2004] $)

  $( 4 + 2 = 6. $)
  4p2e6 $p |- ( 4 + 2 ) = 6 $=
    ( c5 c1 caddc co c4 c6 c2 df-5 oveq1i df-6 df-2 oveq2i recni ax-1cn addassi
    4re eqtr4i 3eqtr4ri ) ABCDEBCDZBCDZFEGCDZASBCHIJUAEBBCDZCDTGUBECKLEBBEPMNNO
    QR $.
    $( [11-May-2004] $)

  $( 4 + 3 = 7. $)
  4p3e7 $p |- ( 4 + 3 ) = 7 $=
    ( c4 c2 caddc co c1 c6 c3 c7 4p2e6 oveq1i df-3 oveq2i 4re recni 2cn addassi
    ax-1cn eqtr4i df-7 3eqtr4i ) ABCDZECDZFECDAGCDZHUAFECIJUCABECDZCDUBGUDACKLA
    BEAMNOQPRST $.
    $( [11-May-2004] $)

  $( 4 + 4 = 8. $)
  4p4e8 $p |- ( 4 + 4 ) = 8 $=
    ( c4 c3 caddc co c1 c7 c8 4p3e7 oveq1i df-4 oveq2i 4re recni ax-1cn addassi
    3re eqtr4i df-8 3eqtr4i ) ABCDZECDZFECDAACDZGTFECHIUBABECDZCDUAAUCACJKABEAL
    MBPMNOQRS $.
    $( [11-May-2004] $)

  $( 5 + 2 = 7. $)
  5p2e7 $p |- ( 5 + 2 ) = 7 $=
    ( c6 c1 caddc co c5 c7 c2 df-6 oveq1i df-7 df-2 oveq2i recni ax-1cn addassi
    5re eqtr4i 3eqtr4ri ) ABCDEBCDZBCDZFEGCDZASBCHIJUAEBBCDZCDTGUBECKLEBBEPMNNO
    QR $.
    $( [11-May-2004] $)

  $( 5 + 3 = 8. $)
  5p3e8 $p |- ( 5 + 3 ) = 8 $=
    ( c5 c2 caddc co c1 c7 c3 c8 5p2e7 oveq1i df-3 oveq2i 5re recni 2cn addassi
    ax-1cn eqtr4i df-8 3eqtr4i ) ABCDZECDZFECDAGCDZHUAFECIJUCABECDZCDUBGUDACKLA
    BEAMNOQPRST $.
    $( [11-May-2004] $)

  $( 5 + 4 = 9. $)
  5p4e9 $p |- ( 5 + 4 ) = 9 $=
    ( c5 c3 caddc co c1 c8 c4 c9 5p3e8 oveq1i df-4 oveq2i 5re recni 3re addassi
    ax-1cn eqtr4i df-9 3eqtr4i ) ABCDZECDZFECDAGCDZHUAFECIJUCABECDZCDUBGUDACKLA
    BEAMNBONQPRST $.
    $( [11-May-2004] $)

  $( 5 + 5 = 10. $)
  5p5e10 $p |- ( 5 + 5 ) = 10 $=
    ( c5 c4 caddc co c1 c9 c10 5p4e9 oveq1i oveq2i 5re recni 4re ax-1cn addassi
    df-5 eqtr4i df-10 3eqtr4i ) ABCDZECDZFECDAACDZGTFECHIUBABECDZCDUAAUCACPJABE
    AKLBMLNOQRS $.
    $( [5-Feb-2007] $)

  $( 6 + 2 = 8. $)
  6p2e8 $p |- ( 6 + 2 ) = 8 $=
    ( c7 c1 caddc co c6 c8 c2 df-7 oveq1i df-8 df-2 oveq2i recni ax-1cn addassi
    6re eqtr4i 3eqtr4ri ) ABCDEBCDZBCDZFEGCDZASBCHIJUAEBBCDZCDTGUBECKLEBBEPMNNO
    QR $.
    $( [11-May-2004] $)

  $( 6 + 3 = 9. $)
  6p3e9 $p |- ( 6 + 3 ) = 9 $=
    ( c6 c2 caddc co c1 c8 c3 c9 6p2e8 oveq1i df-3 oveq2i 6re recni 2cn addassi
    ax-1cn eqtr4i df-9 3eqtr4i ) ABCDZECDZFECDAGCDZHUAFECIJUCABECDZCDUBGUDACKLA
    BEAMNOQPRST $.
    $( [11-May-2004] $)

  $( 6 + 4 = 10. $)
  6p4e10 $p |- ( 6 + 4 ) = 10 $=
    ( c6 c3 caddc co c1 c9 c4 c10 6p3e9 oveq1i df-4 oveq2i 6re recni 3re ax-1cn
    addassi eqtr4i df-10 3eqtr4i ) ABCDZECDZFECDAGCDZHUAFECIJUCABECDZCDUBGUDACK
    LABEAMNBONPQRST $.
    $( [5-Feb-2007] $)

  $( 7 + 2 = 9. $)
  7p2e9 $p |- ( 7 + 2 ) = 9 $=
    ( c8 c1 caddc co c7 c9 c2 df-8 oveq1i df-9 df-2 oveq2i recni ax-1cn addassi
    7re eqtr4i 3eqtr4ri ) ABCDEBCDZBCDZFEGCDZASBCHIJUAEBBCDZCDTGUBECKLEBBEPMNNO
    QR $.
    $( [11-May-2004] $)

  $( 7 + 3 = 10. $)
  7p3e10 $p |- ( 7 + 3 ) = 10 $=
    ( c7 c2 caddc co c1 c9 c3 c10 7p2e9 oveq1i df-3 oveq2i 7re recni 2cn ax-1cn
    addassi eqtr4i df-10 3eqtr4i ) ABCDZECDZFECDAGCDZHUAFECIJUCABECDZCDUBGUDACK
    LABEAMNOPQRST $.
    $( [5-Feb-2007] $)

  $( 8 + 2 = 10. $)
  8p2e10 $p |- ( 8 + 2 ) = 10 $=
    ( c9 c1 caddc co c8 c10 c2 df-9 oveq1i df-10 df-2 oveq2i 8re ax-1cn addassi
    recni eqtr4i 3eqtr4ri ) ABCDEBCDZBCDZFEGCDZASBCHIJUAEBBCDZCDTGUBECKLEBBEMPN
    NOQR $.
    $( [5-Feb-2007] $)

  $( 2 times 2 equals 4. $)
  2t2e4 $p |- ( 2 x. 2 ) = 4 $=
    ( c2 cmul co caddc c4 2cn 2timesi 2p2e4 eqtri ) AABCAADCEAFGHI $.
    $( [11-May-2004] $) $( [1-Aug-1999] $)

  $( 3 times 2 equals 6. $)
  3t2e6 $p |- ( 3 x. 2 ) = 6 $=
    ( c3 c2 cmul co caddc c6 3re recni times2i 3p3e6 eqtri ) ABCDAAEDFAAGHIJK
    $.
    $( [2-Aug-2004] $)

  $( 3 times 3 equals 9. $)
  3t3e9 $p |- ( 3 x. 3 ) = 9 $=
    ( c3 cmul co c2 c1 caddc c6 df-3 oveq2i 3re recni 2cn ax-1cn adddii mulid1i
    c9 3t2e6 oveq12i eqtri 6p3e9 3eqtri ) AABCADEFCZBCZGAFCZPAUBABHIUCADBCZAEBC
    ZFCUDADEAJKZLMNUEGUFAFQAUGORSTUA $.
    $( [11-May-2004] $)

  $( 4 times 2 equals 8. $)
  4t2e8 $p |- ( 4 x. 2 ) = 8 $=
    ( c4 c2 cmul co caddc c8 4re recni times2i 4p4e8 eqtri ) ABCDAAEDFAAGHIJK
    $.
    $( [2-Aug-2004] $)

  $( 5 times 2 equals 10. $)
  5t2e10 $p |- ( 5 x. 2 ) = 10 $=
    ( c5 c2 cmul co caddc c10 5re recni times2i 5p5e10 eqtri ) ABCDAAEDFAAGHIJK
    $.
    $( [5-Feb-2007] $)

  $( One half of four is two. $)
  4d2e2 $p |- ( 4 / 2 ) = 2 $=
    ( c4 c2 cdiv co wceq cmul 2t2e4 4re recni 2cn 2ne0 divmuli mpbir ) ABCDBEBB
    FDAEGABBAHIJJKLM $.
    $( [3-Sep-1999] $)

  $( 2 is a natural number. $)
  2nn $p |- 2 e. NN $=
    ( c2 c1 caddc co cn df-2 wcel 1nn peano2nn ax-mp eqeltri ) ABBCDZEFBEGLEGHB
    IJK $.
    $( [20-Aug-2001] $)

  $( 3 is a natural number. $)
  3nn $p |- 3 e. NN $=
    ( c3 c2 c1 caddc co cn df-3 wcel 2nn peano2nn ax-mp eqeltri ) ABCDEZFGBFHMF
    HIBJKL $.
    $( [8-Jan-2006] $)

  $( 4 is a natural number. $)
  4nn $p |- 4 e. NN $=
    ( c4 c3 c1 caddc co cn df-4 wcel 3nn peano2nn ax-mp eqeltri ) ABCDEZFGBFHMF
    HIBJKL $.
    $( [8-Jan-2006] $)

  $( 5 is a natural number. $)
  5nn $p |- 5 e. NN $=
    ( c5 c4 c1 caddc co cn df-5 wcel 4nn peano2nn ax-mp eqeltri ) ABCDEZFGBFHMF
    HIBJKL $.
    $( [15-Sep-2013] $)

  $( 6 is a natural number. $)
  6nn $p |- 6 e. NN $=
    ( c6 c5 c1 caddc co cn df-6 wcel 5nn peano2nn ax-mp eqeltri ) ABCDEZFGBFHMF
    HIBJKL $.
    $( [15-Sep-2013] $)

  $( 7 is a natural number. $)
  7nn $p |- 7 e. NN $=
    ( c7 c6 c1 caddc co cn df-7 wcel 6nn peano2nn ax-mp eqeltri ) ABCDEZFGBFHMF
    HIBJKL $.
    $( [15-Sep-2013] $)

  $( 8 is a natural number. $)
  8nn $p |- 8 e. NN $=
    ( c8 c7 c1 caddc co cn df-8 wcel 7nn peano2nn ax-mp eqeltri ) ABCDEZFGBFHMF
    HIBJKL $.
    $( [15-Sep-2013] $)

  $( 9 is a natural number. $)
  9nn $p |- 9 e. NN $=
    ( c9 c8 c1 caddc co cn df-9 wcel 8nn peano2nn ax-mp eqeltri ) ABCDEZFGBFHMF
    HIBJKL $.
    $( [21-Oct-2012] $)

  $( 10 is a natural number. $)
  10nn $p |- 10 e. NN $=
    ( c10 c9 c1 caddc co cn df-10 wcel 9nn peano2nn ax-mp eqeltri ) ABCDEZFGBFH
    MFHIBJKL $.
    $( [8-Nov-2012] $)

  $( 1 is less than 2. $)
  1lt2 $p |- 1 < 2 $=
    ( c1 caddc co c2 clt 1re ltp1i df-2 breqtrri ) AAABCDEAFGHI $.
    $( [24-Feb-2005] $)

  $( 2 is less than 3. $)
  2lt3 $p |- 2 < 3 $=
    ( c2 c1 caddc co c3 clt 2re ltp1i df-3 breqtrri ) AABCDEFAGHIJ $.
    $( [26-Sep-2010] $)

  $( 1 is less than 3. $)
  1lt3 $p |- 1 < 3 $=
    ( c1 c2 clt wbr c3 1lt2 2lt3 1re 2re 3re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [26-Sep-2010] $)

  $( 3 is less than 4. $)
  3lt4 $p |- 3 < 4 $=
    ( c3 c1 caddc co c4 clt 3re ltp1i df-4 breqtrri ) AABCDEFAGHIJ $.
    $( [15-Sep-2013] $)

  $( 2 is less than 4. $)
  2lt4 $p |- 2 < 4 $=
    ( c2 c3 clt wbr c4 2lt3 3lt4 2re 3re 4re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 1 is less than 4. $)
  1lt4 $p |- 1 < 4 $=
    ( c1 c2 clt wbr c4 1lt2 2lt4 1re 2re 4re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 4 is less than 5. $)
  4lt5 $p |- 4 < 5 $=
    ( c4 c1 caddc co c5 clt 4re ltp1i df-5 breqtrri ) AABCDEFAGHIJ $.
    $( [15-Sep-2013] $)

  $( 3 is less than 5. $)
  3lt5 $p |- 3 < 5 $=
    ( c3 c4 clt wbr c5 3lt4 4lt5 3re 4re 5re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 2 is less than 5. $)
  2lt5 $p |- 2 < 5 $=
    ( c2 c4 clt wbr c5 2lt4 4lt5 2re 4re 5re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 1 is less than 5. $)
  1lt5 $p |- 1 < 5 $=
    ( c1 c4 clt wbr c5 1lt4 4lt5 1re 4re 5re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 5 is less than 6. $)
  5lt6 $p |- 5 < 6 $=
    ( c5 c1 caddc co c6 clt 5re ltp1i df-6 breqtrri ) AABCDEFAGHIJ $.
    $( [15-Sep-2013] $)

  $( 4 is less than 6. $)
  4lt6 $p |- 4 < 6 $=
    ( c4 c5 clt wbr c6 4lt5 5lt6 4re 5re 6re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 3 is less than 6. $)
  3lt6 $p |- 3 < 6 $=
    ( c3 c4 clt wbr c6 3lt4 4lt6 3re 4re 6re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 2 is less than 6. $)
  2lt6 $p |- 2 < 6 $=
    ( c2 c3 clt wbr c6 2lt3 3lt6 2re 3re 6re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 1 is less than 6. $)
  1lt6 $p |- 1 < 6 $=
    ( c1 c2 clt wbr c6 1lt2 2lt6 1re 2re 6re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [19-Oct-2012] $)

  $( 6 is less than 7. $)
  6lt7 $p |- 6 < 7 $=
    ( c6 c1 caddc co c7 clt 6re ltp1i df-7 breqtrri ) AABCDEFAGHIJ $.
    $( [15-Sep-2013] $)

  $( 5 is less than 7. $)
  5lt7 $p |- 5 < 7 $=
    ( c5 c6 clt wbr c7 5lt6 6lt7 5re 6re 7re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 4 is less than 7. $)
  4lt7 $p |- 4 < 7 $=
    ( c4 c5 clt wbr c7 4lt5 5lt7 4re 5re 7re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 3 is less than 7. $)
  3lt7 $p |- 3 < 7 $=
    ( c3 c4 clt wbr c7 3lt4 4lt7 3re 4re 7re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 2 is less than 7. $)
  2lt7 $p |- 2 < 7 $=
    ( c2 c3 clt wbr c7 2lt3 3lt7 2re 3re 7re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 1 is less than 7. $)
  1lt7 $p |- 1 < 7 $=
    ( c1 c2 clt wbr c7 1lt2 2lt7 1re 2re 7re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 7 is less than 8. $)
  7lt8 $p |- 7 < 8 $=
    ( c7 c1 caddc co c8 clt 7re ltp1i df-8 breqtrri ) AABCDEFAGHIJ $.
    $( [15-Sep-2013] $)

  $( 6 is less than 8. $)
  6lt8 $p |- 6 < 8 $=
    ( c6 c7 clt wbr c8 6lt7 7lt8 6re 7re 8re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 5 is less than 8. $)
  5lt8 $p |- 5 < 8 $=
    ( c5 c6 clt wbr c8 5lt6 6lt8 5re 6re 8re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 4 is less than 8. $)
  4lt8 $p |- 4 < 8 $=
    ( c4 c5 clt wbr c8 4lt5 5lt8 4re 5re 8re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 3 is less than 8. $)
  3lt8 $p |- 3 < 8 $=
    ( c3 c4 clt wbr c8 3lt4 4lt8 3re 4re 8re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 2 is less than 8. $)
  2lt8 $p |- 2 < 8 $=
    ( c2 c3 clt wbr c8 2lt3 3lt8 2re 3re 8re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 1 is less than 8. $)
  1lt8 $p |- 1 < 8 $=
    ( c1 c2 clt wbr c8 1lt2 2lt8 1re 2re 8re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [15-Sep-2013] $)

  $( 8 is less than 9. $)
  8lt9 $p |- 8 < 9 $=
    ( c8 c1 caddc co c9 clt 8re ltp1i df-9 breqtrri ) AABCDEFAGHIJ $.
    $( [19-Feb-2014] $)

  $( 1 is less than 9. $)
  1lt9 $p |- 1 < 9 $=
    ( cc0 c1 caddc co clt wbr 8pos 0re 8re 1re ltadd1i mpbi ax-1cn addid2i df-9
    c8 c9 eqcomi 3brtr3i ) ABCDZPBCDZBQEAPEFTUAEFGAPBHIJKLBMNQUAORS $.
    $( [19-Oct-2012] $)

  $( 1 is less than 10. $)
  1lt10 $p |- 1 < 10 $=
    ( cc0 c1 caddc co c9 c10 clt wbr 9pos 0re 9re 1re ltadd1i mpbi ax-1cn df-10
    addid2i eqcomi 3brtr3i ) ABCDZEBCDZBFGAEGHTUAGHIAEBJKLMNBOQFUAPRS $.
    $( [7-Nov-2012] $)

  $( 1 is not equal to 2. $)
  1ne2 $p |- 1 =/= 2 $=
    ( c2 c1 1re 2re 1lt2 ltneii necomi ) ABBACDEFG $.
    $( [19-Oct-2012] $)

  $( One-half is greater than zero. $)
  halfgt0 $p |- 0 < ( 1 / 2 ) $=
    ( c2 2re 2pos recgt0ii ) ABCD $.
    $( [24-Feb-2005] $)

  $( One-half is less than one. $)
  halflt1 $p |- ( 1 / 2 ) < 1 $=
    ( c1 cdiv co clt wbr ax-1cn div1i 1lt2 eqbrtri 1re lt01 2pos ltdiv23ii mpbi
    c2 2re ) AABCZODEAOBCADEQAODAFGHIAAOJJPKLMN $.
    $( [24-Feb-2005] $)

  $( An eighth of four thirds is a sixth.  (Contributed by Paul Chapman,
     24-Nov-2007.) $)
  8th4div3 $p |- ( ( 1 / 8 ) x. ( 4 / 3 ) ) = ( 1 / 6 ) $=
    ( c1 c8 cdiv co c4 c3 c6 ax-1cn 8re recni 4re 3re gt0ne0ii oveq1i c2 oveq2i
    cmul 2cn cc wcel 8pos divmuldivi mulcomi mul32i 4t2e8 mulassi 3eqtr3i 3t2e6
    3pos eqtri 3eqtri cc0 wne wceq 4pos 6re 6pos divcan5 mp3an1 mpanl12 mp2an
    wa ) ABCDEFCDQDZEAQDZEGQDZCDZAGCDZVCAEQDZBFQDZCDVDVICDVFABEFHBIJEKJZFLJZBIU
    AMFLUIMUBVHVDVICAEHVJUCNVIVEVDCVIEFOQDZQDZVEEOQDZFQDEFQDOQDVIVMEOFVJRVKUDVN
    BFQUENEFOVJVKRUFUGVLGEQUHPUJPUKESTZEULUMZVFVGUNZVJEKUOMGSTZGULUMZVOVPVBZVQG
    UPJGUPUQMASTVRVSVBVTVQHAGEURUSUTVAUJ $.
    $( [24-Nov-2007] $)

  $( One half plus or minus one sixth.  (Contributed by Paul Chapman,
     17-Jan-2008.) $)
  halfpm6th $p |- ( ( ( 1 / 2 ) - ( 1 / 6 ) ) = ( 1 / 3 ) /\
                       ( ( 1 / 2 ) + ( 1 / 6 ) ) = ( 2 / 3 ) ) $=
    ( c1 c2 cdiv co c6 cmin c3 wceq caddc cmul ax-1cn divmuldivi oveq1i mulid1i
    2cn 2ne0 3t2e6 oveq12i cc wcel 3nn nncni nnne0i dividi 2re rereccli mulid2i
    recni eqtri 3eqtr3i cc0 wne wa 6re 6pos gt0ne0ii divsubdir mp3an df-3 pncan
    pm3.2i mp2an oveq2i reccli 3eqtr2i c4 divdiri df-4 3eqtr4ri 2t2e4 divcli )
    ABCDZAECDZFDZAGCDZHVLVMIDZBGCDZHVNGECDZVMFDZGAFDZECDZVOVLVRVMFGGCDZVLJDZGAJ
    DZGBJDZCDVLVRGGABGUAUBZWFKOGUAUCZPLWCAVLJDVLWBAVLJGWFWGUDMVLVLBUEPUFUHUGUIW
    DGWEECGWFNQRUJZMGSTASTZESTZEUKULZUMWAVSHWFKWJWKEUNUHZEUNUOUPZVAGAEUQURWABEC
    DABJDZWECDZVOVTBECVTBAIDZAFDZBGWPAFUSMBSTWIWQBHOKBAUTVBUIMWNBWEECBOUGQRVOBB
    CDZJDVOAJDWOVOWRAVOJBOPUDZVCAGBBKWFOOWGPLVOGWFWGVDNUJVEVEVPVFECDZBBJDZWECDZ
    VQGAIDZECDVRVMIDWTVPGAEWFKWLWMVGVFXCECVHMVLVRVMIWHMVIXAVFWEECVJQRVQWRJDVQAJ
    DXBVQWRAVQJWSVCBGBBOWFOOWGPLVQBGOWFWGVKNUJVEVA $.
    $( [17-Jan-2008] $)

  $( Closure of half of a number (frequently used special case). $)
  halfcl $p |- ( A e. CC -> ( A / 2 ) e. CC ) $=
    ( cc wcel c2 cc0 wne cdiv co 2cn 2ne0 divcl mp3an23 ) ABCDBCDEFADGHBCIJADKL
    $.
    $( [1-Jan-2006] $)

  $( Real closure of half. $)
  rehalfcl $p |- ( A e. RR -> ( A / 2 ) e. RR ) $=
    ( cr wcel c2 cc0 wne cdiv co 2re 2ne0 redivcl mp3an23 ) ABCDBCDEFADGHBCIJAD
    KL $.
    $( [1-Jan-2006] $)

  $( Half of a number is zero iff the number is zero. $)
  half0 $p |- ( A e. CC -> ( ( A / 2 ) = 0 <-> A = 0 ) ) $=
    ( cc wcel c2 cc0 wne cdiv co wceq wb 2cn 2ne0 diveq0 mp3an23 ) ABCDBCDEFADG
    HEIAEIJKLADMN $.
    $( [20-Apr-2006] $)

  $( A positive number is greater than its half. $)
  halfpos $p |- ( A e. RR -> ( 0 < A <-> ( A / 2 ) < A ) ) $=
    ( cr wcel cc0 clt wbr c1 caddc co cdiv c2 wb cif wceq breq2 breq12d bibi12d
    oveq1 id 0re elimel halfposi dedth df-2 oveq2i breq1i syl6bbr ) ABCZDAEFZAG
    GHIZJIZAEFZAKJIZAEFUHUIULLDUHADMZEFZUNUJJIZUNEFZLADAUNNZUIUOULUQAUNDEOURUKU
    PAUNEAUNUJJRURSPQUNADBTUAUBUCUMUKAEKUJAJUDUEUFUG $.
    $( [28-Oct-2004] $)

  $( A number is positive iff its half is positive. $)
  halfpos2 $p |- ( A e. RR -> ( 0 < A <-> 0 < ( A / 2 ) ) ) $=
    ( cr wcel c2 cc0 clt wbr cdiv co wb 2re 2pos gt0div mp3an23 ) ABCDBCEDFGEAF
    GEADHIFGJKLADMN $.
    $( [10-Apr-2005] $)

  $( A number is nonnegative iff its half is nonnegative. $)
  halfnneg2 $p |- ( A e. RR -> ( 0 <_ A <-> 0 <_ ( A / 2 ) ) ) $=
    ( cr wcel c2 cc0 clt wbr cle cdiv co wb 2re 2pos ge0div mp3an23 ) ABCDBCEDF
    GEAHGEADIJHGKLMADNO $.
    $( [9-Dec-2005] $)

  $( Two halves make a whole. $)
  2halves $p |- ( A e. CC -> ( ( A / 2 ) + ( A / 2 ) ) = A ) $=
    ( cc wcel c2 cmul co cdiv caddc 2times oveq1d cc0 wne wceq 2cn 2ne0 divcan3
    mp3an23 wa pm3.2i divdir mp3an3 anidms 3eqtr3rd ) ABCZDAEFZDGFZAAHFZDGFZAAD
    GFZUIHFZUDUEUGDGAIJUDDBCZDKLZUFAMNOADPQUDUHUJMZUDUDUKULRUMUKULNOSAADTUAUBUC
    $.
    $( [11-Apr-2005] $)

  $( Closure of half-sum and half-difference.  (Contributed by Paul Chapman,
     12-Oct-2007.) $)
  halfaddsubcl $p |- ( ( A e. CC /\ B e. CC ) -> ( ( ( A + B ) / 2 ) e. CC /\
                         ( ( A - B ) / 2 ) e. CC ) ) $=
    ( cc wcel wa caddc co c2 cdiv cmin addcl halfcl syl subcl jca ) ACDBCDEZABF
    GZHIGCDZABJGZHIGCDZPQCDRABKQLMPSCDTABNSLMO $.
    $( [12-Oct-2007] $)

  $( Sum and difference of half-sum and half-difference.  (Contributed by Paul
     Chapman, 12-Oct-2007.) $)
  halfaddsub $p |- ( ( A e. CC /\ B e. CC ) ->
                      ( ( ( ( A + B ) / 2 ) + ( ( A - B ) / 2 ) ) = A /\
                        ( ( ( A + B ) / 2 ) - ( ( A - B ) / 2 ) ) = B ) ) $=
    ( cc wcel wa caddc cdiv cmin wceq cmul 2times adantr eqtr4d oveq1d 2cn 2ne0
    co c2 mp3an3 syl2anc ppncan 3anidm13 addcl subcl cc0 pm3.2i divcan3 mp3an23
    wne divdir 3eqtr3d pnncan 3anidm23 adantl divsubdir jca ) ACDZBCDZEZABFQZRG
    QZABHQZRGQZFQZAIVAVCHQZBIUSUTVBFQZRGQZRAJQZRGQZVDAUSVFVHRGUSVFAAFQZVHUQURVF
    VJIABAUAUBUQVHVJIURAKLMNUSUTCDZVBCDZVGVDIZABUCZABUDZVKVLRCDZRUEUIZEZVMVPVQO
    PUFZUTVBRUJSTUQVIAIZURUQVPVQVTOPARUGUHLUKUSUTVBHQZRGQZRBJQZRGQZVEBUSWAWCRGU
    SWABBFQZWCUQURWAWEIABBULUMURWCWEIUQBKUNMNUSVKVLWBVEIZVNVOVKVLVRWFVSUTVBRUOS
    TURWDBIZUQURVPVQWGOPBRUGUHUNUKUP $.
    $( [12-Oct-2007] $)

  $( MAY BE REVISED - TODO - use this to reduce:
  Statement "lt2add" is directly referenced in the proofs of 9 statements:
    2climnn 2climnn0 climaddlem3 climmullem5 climcaui ser1f0i
  MM> sh us 2halves
  Statement "2halves" is directly referenced in the proofs of 10 statements:
    2climnn 2climnn0 climmullem5 climcaui caucvgi ser1f0i
  MM>
  $)
  $( A sum is less than the whole if each term is less than half. $)
  lt2halves $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( ( A < ( C / 2 ) /\ B < ( C / 2 ) ) -> ( A + B ) < C ) ) $=
    ( cr wcel w3a c2 co clt wbr wa caddc wi 3simpa rehalfcl jca 3ad2ant3 lt2add
    cdiv syl2anc wb cc wceq recn 2halves syl breq2d sylibd ) ADEZBDEZCDEZFZACGS
    HZIJBUMIJKZABLHZUMUMLHZIJZUOCIJZULUIUJKUMDEZUSKZUNUQMUIUJUKNUKUIUTUJUKUSUSC
    OZVAPQABUMUMRTUKUIUQURUAUJUKUPCUOIUKCUBEUPCUCCUDCUEUFUGQUH $.
    $( [13-Dec-2006] $)

  $( Sum is less than product for numbers greater than 2.  (Contributed by
     Stefan Allan, 24-Sep-2010.) $)
  addltmul $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 2 < A /\ 2 < B ) )
              -> ( A + B ) < ( A x. B ) ) $=
    ( cr wcel wa c2 clt wbr caddc co cmul c1 cmin wb ax-1cn syl2an cc ax-mulrcl
    1re wceq 2re ltsub1 mp3an13 df-2 eqcomi subaddrii breq1i syl6bb bi2anan9 wi
    2cn peano2rem mulgt1 sylbid recn mulsub mpanl2 mpanr2 breq2d mulid1i oveq2i
    ex breq2i readdcl remulcli sylancl ltaddsub2 mp3an2 syl2anc syl5rbbr ltadd1
    mpan2 mp3an3 mulid1 syl oveqan12d breq1d bitr3d 3bitrd sylibd imp ) ACDZBCD
    ZEZFAGHZFBGHZEZABIJZABKJZGHZWDWGLALMJZBLMJZKJZGHZWJWDWGLWKGHZLWLGHZEZWNWBWE
    WOWCWFWPWBWEFLMJZWKGHZWOFCDZWBLCDZWEWSNUASFALUBUCWRLWKGFLLUKOOFLLIJUDUEUFZU
    GUHWCWFWRWLGHZWPWTWCXAWFXCNUASFBLUBUCWRLWLGXBUGUHUIWBWKCDZWLCDZWQWNUJWCAULB
    ULXDXEEWQWNWKWLUMVBPUNWDWNLWILLKJZIJZALKJZBLKJZIJZMJZGHZXJLIJZWILIJZGHZWJWD
    WMXKLGWBAQDZBQDZWMXKTZWCAUOZBUOZXPXQLQDZXROXPYAXQYAEXROALBLUPUQURPUSXOXMXGG
    HZWDXLXGXNXMGXFLWIILOUTVAVCWDXJCDZXGCDZYBXLNZWBXHCDZXICDZYCWCWBXAYFSALRVLWC
    XAYGSBLRVLXHXIVDPZWDWICDZXFCDYDABRZLLSSVEWIXFVDVFYCXAYDYESXJLXGVGVHVIVJWDXJ
    WIGHZXOWJWDYCYIYKXONZYHYJYCYIXAYLSXJWILVKVMVIWDXJWHWIGWBWCXHAXIBIWBXPXHATXS
    AVNVOWCXQXIBTXTBVNVOVPVQVRVSVTWA $.
    $( [24-Sep-2010] $)

  ${
    $d x y $.
    $( There is no smallest positive real number. $)
    nominpos $p |- -. E. x e. RR ( 0 < x /\
                   -. E. y e. RR ( 0 < y /\ y < x ) ) $=
      ( cc0 cv clt wbr wa cr wrex wn wcel wi c2 cdiv co 2re 2pos divgt0 mpanr12
      ex halfpos biimpd jcad rehalfcl jctild wceq breq2 breq1 anbi12d syl6 iman
      rcla4ev sylib nrex ) CADZEFZCBDZEFZUQUOEFZGZBHIZJGZAHUOHKZUPVALVBJVCUPUOM
      NOZHKZCVDEFZVDUOEFZGZGVAVCUPVHVEVCUPVFVGVCUPVFVCUPGMHKCMEFVFPQUOMRSTVCUPV
      GUOUAUBUCUOUDUEUTVHBVDHUQVDUFURVFUSVGUQVDCEUGUQVDUOEUHUIULUJUPVAUKUMUN $.
      $( [28-Oct-2004] $)
  $}
  $( Ordering property for average.  (Contributed by Mario Carneiro,
     28-May-2014.) $)
  avglt1 $p |- ( ( A e. RR /\ B e. RR ) ->
                 ( A < B <-> A < ( ( A + B ) / 2 ) ) ) $=
    ( cr wcel wa clt wbr caddc co c2 cmul cdiv wb simpl simpr ltadd2 syl3anc cc
    wceq recnd times2 syl breq1d bicomd cc0 readdcl 2pos pm3.2i ltmuldiv 3bitrd
    2re a1i ) ACDZBCDZEZABFGZAAHIZABHIZFGZAJKIZURFGZAURJLIFGZUOUMUNUMUPUSMUMUNN
    ZUMUNOVCABAPQUOVAUSUOUTUQURFUOARDUTUQSUOAVCTAUAUBUCUDUOUMURCDJCDZUEJFGZEZVA
    VBMVCABUFVFUOVDVEUKUGUHULAURJUIQUJ $.
    $( [28-May-2014] $)

  $( Ordering property for average.  (Contributed by Mario Carneiro,
     28-May-2014.) $)
  avglt2 $p |- ( ( A e. RR /\ B e. RR ) ->
                 ( A < B <-> ( ( A + B ) / 2 ) < B ) ) $=
    ( cr wcel wa caddc co c2 cmul clt wbr cdiv cc wceq simpr 2times syl syl3anc
    recnd wb breq2d cc0 readdcl 2re 2pos pm3.2i ltdivmul simpl ltadd1 3bitr4rd
    a1i ) ACDZBCDZEZABFGZHBIGZJKZUOBBFGZJKZUOHLGBJKZABJKZUNUPURUOJUNBMDUPURNUNB
    ULUMOZSBPQUAUNUOCDUMHCDZUBHJKZEZUTUQTABUCVBVEUNVCVDUDUEUFUKUOBHUGRUNULUMUMV
    AUSTULUMUHVBVBABBUIRUJ $.
    $( [28-May-2014] $)

  $( Ordering property for average.  (Contributed by Mario Carneiro,
     28-May-2014.) $)
  avgle1 $p |- ( ( A e. RR /\ B e. RR ) ->
                 ( A <_ B <-> A <_ ( ( A + B ) / 2 ) ) ) $=
    ( cr wcel wa clt wbr wn caddc co c2 cdiv cle wb avglt2 ancoms cc wceq lenlt
    recn addcom syl2an oveq1d breq1d bitr4d notbid readdcl rehalfcl syl 3bitr4d
    syldan ) ACDZBCDZEZBAFGZHABIJZKLJZAFGZHZABMGAUQMGZUNUOURUNUOBAIJZKLJZAFGZUR
    UMULUOVCNBAOPUNUQVBAFUNUPVAKLULAQDBQDUPVARUMATBTABUAUBUCUDUEUFABSULUMUQCDZU
    TUSNUNUPCDVDABUGUPUHUIAUQSUKUJ $.
    $( [28-May-2014] $)

  $( Ordering property for average.  (Contributed by Jeff Hankins,
     15-Sep-2013.)  (Revised by Mario Carneiro, 28-May-2014.) $)
  avgle2 $p |- ( ( A e. RR /\ B e. RR ) ->
                 ( A <_ B <-> ( ( A + B ) / 2 ) <_ B ) ) $=
    ( cr wcel wa clt wbr wn caddc co c2 cdiv cle wb avglt1 ancoms cc wceq lenlt
    recn addcom syl2an oveq1d breq2d bitr4d notbid readdcl rehalfcl syl 3bitr4d
    sylancom ) ACDZBCDZEZBAFGZHBABIJZKLJZFGZHZABMGUQBMGZUNUOURUNUOBBAIJZKLJZFGZ
    URUMULUOVCNBAOPUNUQVBBFUNUPVAKLULAQDBQDUPVARUMATBTABUAUBUCUDUEUFABSULUMUQCD
    ZUTUSNUNUPCDVDABUGUPUHUIUQBSUKUJ $.
    $( [28-May-2014] $) $( [15-Sep-2013] $)

  $( The average of two numbers is less than or equal to at least one of
     them. $)
  avgle $p |- ( ( A e. RR /\ B e. RR ) ->
               ( ( ( A + B ) / 2 ) <_ A \/ ( ( A + B ) / 2 ) <_ B ) ) $=
    ( cr wcel wa cle wo caddc co c2 cdiv letric orcomd wb avgle2 ancoms cc wceq
    wbr recn addcom syl2an oveq1d breq1d bitr4d orbi12d mpbid ) ACDZBCDZEZBAFSZ
    ABFSZGABHIZJKIZAFSZUNBFSZGUJULUKABLMUJUKUOULUPUJUKBAHIZJKIZAFSZUOUIUHUKUSNB
    AOPUJUNURAFUJUMUQJKUHAQDBQDUMUQRUIATBTABUAUBUCUDUEABOUFUG $.
    $( [28-May-2014] $) $( [9-Dec-2005] $)


