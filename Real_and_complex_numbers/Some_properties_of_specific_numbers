$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Some properties of specific numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Two plus two equals four.  For more information, see "2+2=4 Trivia" on the
     Metamath Proof Explorer Home Page:
     ~ http://us.metamath.org/mpegif/mmset.html#trivia . $)
  2p2e4 $p |- ( 2 + 2 ) = 4 $=
    ( c2 caddc co c1 c4 df-2 opreq2i c3 df-4 df-3 opreq1i 2cn ax1cn addassi
    3eqtri eqtr4i ) AABCADDBCZBCZEAQABFGEHDBCADBCZDBCRIHSDBJKADDLMMNOP $.
    $( [27-May-1999] $)

  $( 4 is a natural number. $)
  4nn $p |- 4 e. NN $=
    ( c2 caddc co c4 cn 2p2e4 wcel 2nn nnaddcl mp2an eqeltrri ) AABCZDEFAEGZMLE
    GHHAAIJK $.
    $( [8-Jan-2006] $)

  ${
    2times.1 $e |- A e. CC $.
    $( Two times a number. $)
    2timesi $p |- ( 2 x. A ) = ( A + A ) $=
      ( c2 cmul co c1 caddc df-2 opreq1i 1p1timesi eqtri ) CADEFFGEZADEAAGECLAD
      HIABJK $.
      $( [1-Aug-1999] $)
  $}

  $( Two times a number. $)
  2times $p |- ( A e. CC -> ( 2 x. A ) = ( A + A ) ) $=
    ( cc wcel c2 cmul co caddc wceq cc0 cif opreq2 id opreq12d eqeq12d 0cn
    elimel 2timesi dedth ) ABCZDAEFZAAGFZHDSAIJZEFZUBUBGFZHAIAUBHZTUCUAUDAUBDEK
    UEAUBAUBGUELZUFMNUBAIBOPQR $.
    $( [10-Oct-2004] $)

  $( A number times 2. $)
  times2 $p |- ( A e. CC -> ( A x. 2 ) = ( A + A ) ) $=
    ( cc wcel c2 cmul co caddc wceq 2cn mulcom mpan2 2times eqtrd ) ABCZADEFZDA
    EFZAAGFNDBCOPHIADJKALM $.
    $( [16-Oct-2007] $)

  ${
    times2.1 $e |- A e. CC $.
    $( A number times 2. $)
    times2i $p |- ( A x. 2 ) = ( A + A ) $=
      ( cc wcel c2 cmul co caddc wceq times2 ax-mp ) ACDAEFGAAHGIBAJK $.
      $( [11-May-2004] $)
  $}

  $( 3 + 2 = 5. $)
  3p2e5 $p |- ( 3 + 2 ) = 5 $=
    ( c4 c1 caddc co c3 c5 c2 df-4 opreq1i df-5 df-2 opreq2i 3re recni ax1cn
    addassi eqtr4i 3eqtr4ri ) ABCDEBCDZBCDZFEGCDZASBCHIJUAEBBCDZCDTGUBECKLEBBEM
    NOOPQR $.
    $( [11-May-2004] $)

  $( 3 + 3 = 6. $)
  3p3e6 $p |- ( 3 + 3 ) = 6 $=
    ( c3 c2 caddc co c1 c5 c6 3p2e5 opreq1i df-3 opreq2i 3re recni 2cn ax1cn
    addassi eqtr4i df-6 3eqtr4i ) ABCDZECDZFECDAACDZGTFECHIUBABECDZCDUAAUCACJKA
    BEALMNOPQRS $.
    $( [11-May-2004] $)

  $( 4 + 2 = 6. $)
  4p2e6 $p |- ( 4 + 2 ) = 6 $=
    ( c5 c1 caddc co c4 c6 c2 df-5 opreq1i df-6 df-2 opreq2i 4re recni ax1cn
    addassi eqtr4i 3eqtr4ri ) ABCDEBCDZBCDZFEGCDZASBCHIJUAEBBCDZCDTGUBECKLEBBEM
    NOOPQR $.
    $( [11-May-2004] $)

  $( 4 + 3 = 7. $)
  4p3e7 $p |- ( 4 + 3 ) = 7 $=
    ( c4 c2 caddc co c1 c6 c3 c7 4p2e6 opreq1i df-3 opreq2i 4re recni 2cn
    ax1cn addassi eqtr4i df-7 3eqtr4i ) ABCDZECDZFECDAGCDZHUAFECIJUCABECDZCDUBG
    UDACKLABEAMNOPQRST $.
    $( [11-May-2004] $)

  $( 4 + 4 = 8. $)
  4p4e8 $p |- ( 4 + 4 ) = 8 $=
    ( c4 c3 caddc co c1 c7 c8 4p3e7 opreq1i df-4 opreq2i 4re recni 3re ax1cn
    addassi eqtr4i df-8 3eqtr4i ) ABCDZECDZFECDAACDZGTFECHIUBABECDZCDUAAUCACJKA
    BEALMBNMOPQRS $.
    $( [11-May-2004] $)

  $( 5 + 2 = 7. $)
  5p2e7 $p |- ( 5 + 2 ) = 7 $=
    ( c6 c1 caddc co c5 c7 c2 df-6 opreq1i df-7 df-2 opreq2i 5re recni ax1cn
    addassi eqtr4i 3eqtr4ri ) ABCDEBCDZBCDZFEGCDZASBCHIJUAEBBCDZCDTGUBECKLEBBEM
    NOOPQR $.
    $( [11-May-2004] $)

  $( 5 + 3 = 8. $)
  5p3e8 $p |- ( 5 + 3 ) = 8 $=
    ( c5 c2 caddc co c1 c7 c3 c8 5p2e7 opreq1i df-3 opreq2i 5re recni 2cn
    ax1cn addassi eqtr4i df-8 3eqtr4i ) ABCDZECDZFECDAGCDZHUAFECIJUCABECDZCDUBG
    UDACKLABEAMNOPQRST $.
    $( [11-May-2004] $)

  $( 5 + 4 = 9. $)
  5p4e9 $p |- ( 5 + 4 ) = 9 $=
    ( c5 c3 caddc co c1 c8 c4 c9 5p3e8 opreq1i df-4 opreq2i 5re recni 3re
    ax1cn addassi eqtr4i df-9 3eqtr4i ) ABCDZECDZFECDAGCDZHUAFECIJUCABECDZCDUBG
    UDACKLABEAMNBONPQRST $.
    $( [11-May-2004] $)

  $( 5 + 5 = 10. $)
  5p5e10 $p |- ( 5 + 5 ) = 10 $=
    ( c5 c4 caddc co c1 c9 c10 5p4e9 opreq1i df-5 opreq2i 5re recni 4re ax1cn
    addassi eqtr4i df-10 3eqtr4i ) ABCDZECDZFECDAACDZGTFECHIUBABECDZCDUAAUCACJK
    ABEALMBNMOPQRS $.
    $( [5-Feb-2007] $)

  $( 6 + 2 = 8. $)
  6p2e8 $p |- ( 6 + 2 ) = 8 $=
    ( c7 c1 caddc co c6 c8 c2 df-7 opreq1i df-8 df-2 opreq2i 6re recni ax1cn
    addassi eqtr4i 3eqtr4ri ) ABCDEBCDZBCDZFEGCDZASBCHIJUAEBBCDZCDTGUBECKLEBBEM
    NOOPQR $.
    $( [11-May-2004] $)

  $( 6 + 3 = 9. $)
  6p3e9 $p |- ( 6 + 3 ) = 9 $=
    ( c6 c2 caddc co c1 c8 c3 c9 6p2e8 opreq1i df-3 opreq2i 6re recni 2cn
    ax1cn addassi eqtr4i df-9 3eqtr4i ) ABCDZECDZFECDAGCDZHUAFECIJUCABECDZCDUBG
    UDACKLABEAMNOPQRST $.
    $( [11-May-2004] $)

  $( 6 + 4 = 10. $)
  6p4e10 $p |- ( 6 + 4 ) = 10 $=
    ( c6 c3 caddc co c1 c9 c4 c10 6p3e9 opreq1i df-4 opreq2i 6re recni 3re
    ax1cn addassi eqtr4i df-10 3eqtr4i ) ABCDZECDZFECDAGCDZHUAFECIJUCABECDZCDUB
    GUDACKLABEAMNBONPQRST $.
    $( [5-Feb-2007] $)

  $( 7 + 2 = 9. $)
  7p2e9 $p |- ( 7 + 2 ) = 9 $=
    ( c8 c1 caddc co c7 c9 c2 df-8 opreq1i df-9 df-2 opreq2i 7re recni ax1cn
    addassi eqtr4i 3eqtr4ri ) ABCDEBCDZBCDZFEGCDZASBCHIJUAEBBCDZCDTGUBECKLEBBEM
    NOOPQR $.
    $( [11-May-2004] $)

  $( 7 + 3 = 10. $)
  7p3e10 $p |- ( 7 + 3 ) = 10 $=
    ( c7 c2 caddc co c1 c9 c3 c10 7p2e9 opreq1i df-3 opreq2i 7re recni 2cn
    ax1cn addassi eqtr4i df-10 3eqtr4i ) ABCDZECDZFECDAGCDZHUAFECIJUCABECDZCDUB
    GUDACKLABEAMNOPQRST $.
    $( [5-Feb-2007] $)

  $( 8 + 2 = 10. $)
  8p2e10 $p |- ( 8 + 2 ) = 10 $=
    ( c9 c1 caddc co c8 c10 c2 df-9 opreq1i df-10 df-2 opreq2i 8re recni ax1cn
    addassi eqtr4i 3eqtr4ri ) ABCDEBCDZBCDZFEGCDZASBCHIJUAEBBCDZCDTGUBECKLEBBEM
    NOOPQR $.
    $( [5-Feb-2007] $)

  $( 2 times 2 equals 4. $)
  2t2e4 $p |- ( 2 x. 2 ) = 4 $=
    ( c2 cmul co caddc c4 2cn 2timesi 2p2e4 eqtri ) AABCAADCEAFGHI $.
    $( [11-May-2004] $) $( [1-Aug-1999] $)

  $( 3 times 2 equals 6. $)
  3t2e6 $p |- ( 3 x. 2 ) = 6 $=
    ( c3 c2 cmul co caddc c6 3re recni times2i 3p3e6 eqtri ) ABCDAAEDFAAGHIJK
    $.
    $( [2-Aug-2004] $)

  $( 3 times 3 equals 9. $)
  3t3e9 $p |- ( 3 x. 3 ) = 9 $=
    ( c3 cmul co c2 c1 caddc c6 c9 df-3 opreq2i 3re recni 2cn ax1cn adddii
    3t2e6 mulid1i opreq12i eqtri 6p3e9 3eqtri ) AABCADEFCZBCZGAFCZHAUBABIJUCADB
    CZAEBCZFCUDADEAKLZMNOUEGUFAFPAUGQRSTUA $.
    $( [11-May-2004] $)

  $( 4 times 2 equals 8. $)
  4t2e8 $p |- ( 4 x. 2 ) = 8 $=
    ( c4 c2 cmul co caddc c8 4re recni times2i 4p4e8 eqtri ) ABCDAAEDFAAGHIJK
    $.
    $( [2-Aug-2004] $)

  $( 5 times 2 equals 10. $)
  5t2e10 $p |- ( 5 x. 2 ) = 10 $=
    ( c5 c2 cmul co caddc c10 5re recni times2i 5p5e10 eqtri ) ABCDAAEDFAAGHIJK
    $.
    $( [5-Feb-2007] $)

  $( One half of four is two. $)
  4d2e2 $p |- ( 4 / 2 ) = 2 $=
    ( c4 c2 cdiv co wceq cmul 2t2e4 4re recni 2cn 2ne0 divmuli mpbir ) ABCDBEBB
    FDAEGABBAHIJJKLM $.
    $( [3-Sep-1999] $)

  $( 9 is a natural number. $)
  9nn $p |- 9 e. NN $=
    ( c9 c3 cmul co cn 3t3e9 eqcomi wcel 3nn nnmulcl mp2an eqeltri ) ABBCDZEMAF
    GBEHZNMEHIIBBJKL $.
    $( [21-Oct-2012] $)

  $( 10 is a natural number. $)
  10nn $p |- 10 e. NN $=
    ( c10 c9 c1 caddc co cn df-10 wcel 9nn peano2nn ax-mp eqeltri ) ABCDEZFGBFH
    MFHIBJKL $.
    $( [8-Nov-2012] $)

  $( 1 is less than 2. $)
  1lt2 $p |- 1 < 2 $=
    ( c1 caddc co c2 clt 1re ltp1i df-2 breqtrri ) AAABCDEAFGHI $.
    $( [24-Feb-2005] $)

  $( 1 is not equal to 2. $)
  1ne2 $p |- 1 =/= 2 $=
    ( c2 c1 clt wbr wne 1lt2 1re 2re ltnei ax-mp necomi ) ABBACDABEFBAGHIJK $.
    $( [19-Oct-2012] $)

  $( 2 is less than 3. $)
  2lt3 $p |- 2 < 3 $=
    ( c2 c1 caddc co c3 clt 2re ltp1i df-3 breqtrri ) AABCDEFAGHIJ $.
    $( [26-Sep-2010] $)

  $( 1 is less than 3. $)
  1lt3 $p |- 1 < 3 $=
    ( c1 c2 clt wbr c3 1lt2 2lt3 1re 2re 3re lttri mp2an ) ABCDBECDAECDFGABEHIJ
    KL $.
    $( [26-Sep-2010] $)

  $( 1 is less than 6. $)
  1lt6 $p |- 1 < 6 $=
    ( c1 cc0 caddc co c3 c6 clt wbr 1lt3 3pos 1re 0re 3re lt2addi mp2an ax1cn
    addid1i 3p3e6 3brtr3i ) ABCDZEECDZAFGAEGHBEGHTUAGHIJABEEKLMMNOAPQRS $.
    $( [19-Oct-2012] $)

  $( 1 is less than 9. $)
  1lt9 $p |- 1 < 9 $=
    ( cc0 c1 caddc co c8 c9 clt wbr 8pos 0re 8re 1re ltadd1i mpbi ax1cn
    addid2i df-9 eqcomi 3brtr3i ) ABCDZEBCDZBFGAEGHTUAGHIAEBJKLMNBOPFUAQRS $.
    $( [19-Oct-2012] $)

  $( 1 is not equal to 9. $)
  1ne9 $p |- 1 =/= 9 $=
    ( c9 c1 clt wbr wne 1lt9 1re 9re ltnei ax-mp necomi ) ABBACDABEFBAGHIJK $.
    $( [19-Oct-2012] $)

  $( 1 is less than 10. $)
  1lt10 $p |- 1 < 10 $=
    ( cc0 c1 caddc co c9 c10 clt wbr 9pos 0re 9re 1re ltadd1i mpbi ax1cn
    addid2i df-10 eqcomi 3brtr3i ) ABCDZEBCDZBFGAEGHTUAGHIAEBJKLMNBOPFUAQRS $.
    $( [7-Nov-2012] $)

  $( One-half is greater than zero. $)
  halfgt0 $p |- 0 < ( 1 / 2 ) $=
    ( c2 2re 2pos recgt0ii ) ABCD $.
    $( [24-Feb-2005] $)

  $( One-half is less than one. $)
  halflt1 $p |- ( 1 / 2 ) < 1 $=
    ( c1 cdiv co c2 clt wbr ax1cn div1i 1lt2 eqbrtri 1re 2re lt01 2pos
    ltdiv23ii mpbi ) AABCZDEFADBCAEFQADEAGHIJAADKKLMNOP $.
    $( [24-Feb-2005] $)

  $( An eighth of four thirds is a sixth.  (Contributed by Paul Chapman,
     24-Nov-2007.) $)
  8th4div3 $p |- ( ( 1 / 8 ) x. ( 4 / 3 ) ) = ( 1 / 6 ) $=
    ( c1 c8 cdiv co c4 c3 cmul c6 ax1cn 8re recni 4re 3re 8pos gt0ne0ii 3pos
    divmuldivi mulcomi opreq1i c2 2cn mul32i 4t2e8 mulassi 3eqtr3i 3t2e6
    opreq2i eqtri 3eqtri cc wcel cc0 wne wceq 4pos wa 6re 6pos divcan5 mp3an1
    mpanl12 mp2an ) ABCDEFCDGDZEAGDZEHGDZCDZAHCDZVCAEGDZBFGDZCDVDVICDVFABEFIBJK
    ELKZFMKZBJNOFMPOQVHVDVICAEIVJRSVIVEVDCVIEFTGDZGDZVEETGDZFGDEFGDTGDVIVMETFVJ
    UAVKUBVNBFGUCSEFTVJVKUAUDUEVLHEGUFUGUHUGUIEUJUKZEULUMZVFVGUNZVJELUOOHUJUKZH
    ULUMZVOVPUPZVQHUQKHUQUROAUJUKVRVSUPVTVQIAHEUSUTVAVBUH $.
    $( [24-Nov-2007] $)

  $( One half plus or minus one sixth.  (Contributed by Paul Chapman,
     17-Jan-2008.) $)
  halfpm6th $p |- ( ( ( 1 / 2 ) - ( 1 / 6 ) ) = ( 1 / 3 ) /\
                       ( ( 1 / 2 ) + ( 1 / 6 ) ) = ( 2 / 3 ) ) $=
    ( c1 c2 cdiv co c6 cmin c3 wceq caddc cmul 3nn nncni ax1cn 2cn nnne0i 2ne0
    divmuldivi dividi opreq1i 2re rereccli recni mulid2i eqtri mulid1i 3t2e6
    opreq12i 3eqtr3i cc wcel cc0 wne wa 6re 6pos gt0ne0ii pm3.2i divsubdir
    mp3an df-3 pncan mp2an opreq2i reccli 3eqtr2i c4 divdiri df-4 3eqtr4ri
    2t2e4 divcli ) ABCDZAECDZFDZAGCDZHVLVMIDZBGCDZHVNGECDZVMFDZGAFDZECDZVOVLVRV
    MFGGCDZVLJDZGAJDZGBJDZCDVLVRGGABGKLZWFMNGKOZPQWCAVLJDVLWBAVLJGWFWGRSVLVLBTP
    UAUBUCUDWDGWEECGWFUEUFUGUHZSGUIUJAUIUJZEUIUJZEUKULZUMWAVSHWFMWJWKEUNUBZEUNU
    OUPZUQGAEURUSWABECDABJDZWECDZVOVTBECVTBAIDZAFDZBGWPAFUTSBUIUJWIWQBHNMBAVAVB
    UDSWNBWEECBNUCUFUGVOBBCDZJDVOAJDWOVOWRAVOJBNPRZVCAGBBMWFNNWGPQVOGWFWGVDUEUH
    VEVEVPVFECDZBBJDZWECDZVQGAIDZECDVRVMIDWTVPGAEWFMWLWMVGVFXCECVHSVLVRVMIWHSVI
    XAVFWEECVJUFUGVQWRJDVQAJDXBVQWRAVQJWSVCBGBBNWFNNWGPQVQBGNWFWGVKUEUHVEUQ $.
    $( [17-Jan-2008] $)

  $( Closure of half of a number (frequently used special case). $)
  halfcl $p |- ( A e. CC -> ( A / 2 ) e. CC ) $=
    ( cc wcel c2 cc0 wne cdiv co 2cn 2ne0 divcl mp3an23 ) ABCDBCDEFADGHBCIJADKL
    $.
    $( [1-Jan-2006] $)

  $( Real closure of half. $)
  rehalfcl $p |- ( A e. RR -> ( A / 2 ) e. RR ) $=
    ( cr wcel c2 cc0 wne cdiv co 2re 2ne0 redivcl mp3an23 ) ABCDBCDEFADGHBCIJAD
    KL $.
    $( [1-Jan-2006] $)

  $( Half of a number is zero iff the number is zero. $)
  half0 $p |- ( A e. CC -> ( ( A / 2 ) = 0 <-> A = 0 ) ) $=
    ( cc wcel c2 cc0 wne cdiv co wceq wb 2cn 2ne0 diveq0 mp3an23 ) ABCDBCDEFADG
    HEIAEIJKLADMN $.
    $( [20-Apr-2006] $)

  $( A positive number is greater than its half. $)
  halfpos $p |- ( A e. RR -> ( 0 < A <-> ( A / 2 ) < A ) ) $=
    ( cr wcel cc0 clt wbr c1 caddc co cdiv c2 wb cif wceq breq2 opreq1 id
    breq12d bibi12d 0re elimel halfposi dedth df-2 opreq2i breq1i syl6bbr ) ABC
    ZDAEFZAGGHIZJIZAEFZAKJIZAEFUHUIULLDUHADMZEFZUNUJJIZUNEFZLADAUNNZUIUOULUQAUN
    DEOURUKUPAUNEAUNUJJPURQRSUNADBTUAUBUCUMUKAEKUJAJUDUEUFUG $.
    $( [28-Oct-2004] $)

  $( A number is positive iff its half is positive. $)
  halfpos2 $p |- ( A e. RR -> ( 0 < A <-> 0 < ( A / 2 ) ) ) $=
    ( cr wcel c2 cc0 clt wbr cdiv co wb 2re 2pos gt0div mp3an23 ) ABCDBCEDFGEAF
    GEADHIFGJKLADMN $.
    $( [10-Apr-2005] $)

  $( A number is nonnegative iff its half is nonnegative. $)
  halfnneg2 $p |- ( A e. RR -> ( 0 <_ A <-> 0 <_ ( A / 2 ) ) ) $=
    ( cr wcel c2 cc0 clt wbr cle cdiv co wb 2re 2pos ge0div mp3an23 ) ABCDBCEDF
    GEAHGEADIJHGKLMADNO $.
    $( [9-Dec-2005] $)

  $( Two halves make a whole. $)
  2halves $p |- ( A e. CC -> ( ( A / 2 ) + ( A / 2 ) ) = A ) $=
    ( cc wcel c2 cmul co cdiv caddc 2times opreq1d cc0 wne wceq 2cn 2ne0
    divcan3 mp3an23 wa pm3.2i divdir mp3an3 anidms 3eqtr3rd ) ABCZDAEFZDGFZAAHF
    ZDGFZAADGFZUIHFZUDUEUGDGAIJUDDBCZDKLZUFAMNOADPQUDUHUJMZUDUDUKULRUMUKULNOSAA
    DTUAUBUC $.
    $( [11-Apr-2005] $)

  $( Closure of half-sum and half-difference.  (Contributed by Paul Chapman,
     12-Oct-2007.) $)
  halfaddsubcl $p |- ( ( A e. CC /\ B e. CC ) -> ( ( ( A + B ) / 2 ) e. CC /\
                         ( ( A - B ) / 2 ) e. CC ) ) $=
    ( cc wcel wa caddc co c2 cdiv cmin addcl halfcl syl subcl jca ) ACDBCDEZABF
    GZHIGCDZABJGZHIGCDZPQCDRABKQLMPSCDTABNSLMO $.
    $( [12-Oct-2007] $)

  $( Sum and difference of half-sum and half-difference.  (Contributed by Paul
     Chapman, 12-Oct-2007.) $)
  halfaddsub $p |- ( ( A e. CC /\ B e. CC ) ->
                      ( ( ( ( A + B ) / 2 ) + ( ( A - B ) / 2 ) ) = A /\
                        ( ( ( A + B ) / 2 ) - ( ( A - B ) / 2 ) ) = B ) ) $=
    ( cc wcel wa caddc co c2 cdiv cmin wceq cmul ppncan 3anidm13 2times adantr
    eqtr4d opreq1d addcl subcl cc0 wne 2cn 2ne0 pm3.2i divdir mp3an3 syl11anc
    divcan3 mp3an23 3eqtr3d pnncan 3anidm23 adantl divsubdir jca ) ACDZBCDZEZAB
    FGZHIGZABJGZHIGZFGZAKVAVCJGZBKUSUTVBFGZHIGZHALGZHIGZVDAUSVFVHHIUSVFAAFGZVHU
    QURVFVJKABAMNUQVHVJKURAOPQRUSUTCDZVBCDZVGVDKZABSZABTZVKVLHCDZHUAUBZEZVMVPVQ
    UCUDUEZUTVBHUFUGUHUQVIAKZURUQVPVQVTUCUDAHUIUJPUKUSUTVBJGZHIGZHBLGZHIGZVEBUS
    WAWCHIUSWABBFGZWCUQURWAWEKABBULUMURWCWEKUQBOUNQRUSVKVLWBVEKZVNVOVKVLVRWFVSU
    TVBHUOUGUHURWDBKZUQURVPVQWGUCUDBHUIUJUNUKUP $.
    $( [12-Oct-2007] $)

  $( MAY BE REVISED - TODO - use this to reduce:
  Statement "lt2add" is directly referenced in the proofs of 9 statements:
    2climnn 2climnn0 climaddlem3 climmullem5 climcaui ser1f0i
  MM> sh us 2halves
  Statement "2halves" is directly referenced in the proofs of 10 statements:
    2climnn 2climnn0 climmullem5 climcaui caucvgi ser1f0i
  MM>
  $)
  $( A sum is less than the whole if each term is less than half. $)
  lt2halves $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( ( A < ( C / 2 ) /\ B < ( C / 2 ) ) -> ( A + B ) < C ) ) $=
    ( cr wcel w3a c2 cdiv co clt wbr wa caddc wi 3simpa rehalfcl jca 3ad2ant3
    lt2add syl11anc wb cc wceq recn 2halves syl breq2d sylibd ) ADEZBDEZCDEZFZA
    CGHIZJKBUMJKLZABMIZUMUMMIZJKZUOCJKZULUIUJLUMDEZUSLZUNUQNUIUJUKOUKUIUTUJUKUS
    USCPZVAQRABUMUMSTUKUIUQURUAUJUKUPCUOJUKCUBEUPCUCCUDCUEUFUGRUH $.
    $( [13-Dec-2006] $)

  $( Sum is less than product for numbers greater than 2.  (Contributed by
     Stefan Allan, 24-Sep-2010.) $)
  addltmul $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 2 < A /\ 2 < B ) )
              -> ( A + B ) < ( A x. B ) ) $=
    ( cr wcel wa c2 clt wbr caddc co cmul c1 cmin wb 2re 1re ltsub1 mp3an13
    2cn ax1cn df-2 eqcomi subaddrii breq1i syl6bb bi2anan9 wi mulgt1 ex
    peano2rem syl2an sylbid cc wceq mulsub mpanl2 mpanr2 recn breq2d readdcl
    axmulrcl mpan2 remulcli sylancl ltaddsub2 mp3an2 syl11anc mulid1i opreq2i
    breq2i syl5rbbr ltadd1 mp3an3 mulid1 syl opreqan12d breq1d bitr3d 3bitrd
    sylibd imp ) ACDZBCDZEZFAGHZFBGHZEZABIJZABKJZGHZWDWGLALMJZBLMJZKJZGHZWJWDWG
    LWKGHZLWLGHZEZWNWBWEWOWCWFWPWBWEFLMJZWKGHZWOFCDZWBLCDZWEWSNOPFALQRWRLWKGFLL
    STTFLLIJUAUBUCZUDUEWCWFWRWLGHZWPWTWCXAWFXCNOPFBLQRWRLWLGXBUDUEUFWKCDZWLCDZW
    QWNUGWBWCXDXEEWQWNWKWLUHUIAUJBUJUKULWDWNLWILLKJZIJZALKJZBLKJZIJZMJZGHZXJLIJ
    ZWILIJZGHZWJWDWMXKLGAUMDZBUMDZWMXKUNZWBWCXPXQLUMDZXRTXPXSXQXSEXRTALBLUOUPUQ
    AURZBURZUKUSWDXMXGGHZXLXOWDXJCDZXGCDZYBXLNZXHCDZXICDZYCWBWCXHXIUTWBXAYFPALV
    AVBWCXAYGPBLVAVBUKZWICDZXFCDYDWDWIXFUTABVAZLLPPVCVDYCXAYDYEPXJLXGVEVFVGXGXN
    XMGXFLWIILTVHVIVJVKWDXJWIGHZXOWJWDYCYIYKXONZYHYJYCYIXAYLPXJWILVLVMVGWDXJWHW
    IGWBWCXHAXIBIWBXPXHAUNXTAVNVOWCXQXIBUNYABVNVOVPVQVRVSVTWA $.
    $( [24-Sep-2010] $)

  ${
    $d x y $.
    $( There is no smallest positive real number. $)
    nominpos $p |- -. E. x e. RR ( 0 < x /\
                   -. E. y e. RR ( 0 < y /\ y < x ) ) $=
      ( cc0 cv clt wbr wa cr wrex wn wcel wi c2 cdiv co 2re 2pos divgt0
      mpanr12 ex halfpos biimpd jcad rehalfcl jctild wceq breq2 breq1 anbi12d
      rcla4ev syl6 iman sylib nrex ) CADZEFZCBDZEFZUQUOEFZGZBHIZJGZAHUOHKZUPVAL
      VBJVCUPUOMNOZHKZCVDEFZVDUOEFZGZGVAVCUPVHVEVCUPVFVGVCUPVFVCUPGMHKCMEFVFPQU
      OMRSTVCUPVGUOUAUBUCUOUDUEUTVHBVDHUQVDUFURVFUSVGUQVDCEUGUQVDUOEUHUIUJUKUPV
      AULUMUN $.
      $( [28-Oct-2004] $)
  $}

  $( The average of two numbers is less than or equal to at least one of
     them. $)
  avgle $p |- ( ( A e. RR /\ B e. RR ) ->
               ( ( ( A + B ) / 2 ) <_ A \/ ( ( A + B ) / 2 ) <_ B ) ) $=
    ( cr wcel wa caddc co c2 cdiv cle wbr wo cmul wn clt wb 2re remulcl mpan
    adantr readdcl ltnle syl11anc ltadd2 3anidm13 ltadd1 3anidm23 bitr3d wceq
    cc recn 2times syl breq1d adantl breq2d 3bitr4d wi ltle sylbid sylbird
    orrd simpl w3a cc0 2pos ledivmulOLD mpan2 mp3an2 sylancom orbi12d mpbird )
    ACDZBCDZEZABFGZHIGZAJKZVQBJKZLVPHAMGZJKZVPHBMGZJKZLVOWAWCVOWANZVTVPOKZWCVOV
    TCDZVPCDZWEWDPVMWFVNHCDZVMWFQHARSTABUAZVTVPUBUCVOWEVPWBOKZWCVOAAFGZVPOKZVPB
    BFGZOKZWEWJVOABOKZWLWNVMVNWOWLPABAUDUEVMVNWOWNPABBUFUGUHVOVTWKVPOVMVTWKUIZV
    NVMAUJDWPAUKAULUMTUNVOWBWMVPOVNWBWMUIZVMVNBUJDWQBUKBULUMUOUPUQVOWGWBCDZWJWC
    URWIVNWRVMWHVNWRQHBRSUOVPWBUSUCUTVAVBVOVRWAVSWCVOWGVMVRWAPZWIVMVNVCWGWHVMWS
    QWGWHVMVDVEHOKZWSVFVPHAVGVHVIUCVMVNWGVSWCPZWIWGWHVNXAQWGWHVNVDWTXAVFVPHBVGV
    HVIVJVKVL $.
    $( [9-Dec-2005] $)


