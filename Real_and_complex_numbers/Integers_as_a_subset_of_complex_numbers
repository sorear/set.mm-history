$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Integers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of integers.  Definition of integers in [Apostol] p. 22. $)
  df-z $a |- ZZ = { x e. RR | ( x = 0 \/ x e. NN \/ -u x e. NN ) } $.

  ${
    $d x A $.
    $( Membership in the set of integers. $)
    elz $p |- ( A e. ZZ <->
              ( A e. RR /\ ( A = 0 \/ A e. NN \/ -u A e. NN ) ) ) $=
      ( vx cz wcel cv cc0 wceq cn cneg w3o cr crab wa df-z eleq2i cleq1 eleq1
      negeq eleq1d bi3ord elrab bitr ) ACDABEZFGZUCHDZUCIZHDZJZBKLZDAKDAFGZAHDZ
      AIZHDZJZMCUIABNOUHUNBAKUCAGZUDUJUEUKUGUMUCAFPUCAHQUOUFULHUCARSTUAUB $.
      $( [8-Jan-02] $)
  $}

  $( The negative of a natural number is an integer. $)
  nnnegz $p |- ( A e. NN -> -u A e. ZZ ) $=
    ( cn wcel cneg cr cc0 wceq w3o wa cz nnret negrclt syl wo cc nncnt negnegt
    eleq1d biimprd mpcom olc df-3or sylibr jca elz ) ABCZADZECZUGFGZUGBCZUGDZBC
    ZHZIUGJCUFUHUMUFAECUHAKALMUFUIUJNZULNZUMUFULUOAOCZUFULAPUPULUFUPUKABAQRSTUL
    UNUAMUIUJULUBUCUDUGUEUC $.
    $( [12-Jan-02] $)

  $( An integer is a real. $)
  zret $p |- ( A e. ZZ -> A e. RR ) $=
    ( cz wcel cr cc0 wceq cn cneg w3o elz pm3.26bd ) ABCADCAEFAGCAHGCIAJK $.
    $( [8-Jan-02] $)

  $( An integer is a complex number. $)
  zcnt $p |- ( A e. ZZ -> A e. CC ) $=
    ( cz wcel zret recnd ) ABCAADE $.
    $( [6-Jun-04] $) $( [9-May-04] $)

  $( The integers are a subset of the reals. $)
  zssre $p |- ZZ (_ RR $=
    ( vx cz cr cv zret ssriv ) ABCADEF $.
    $( [5-Sep-04] $) $( [2-Aug-04] $)

  $( The integers are a subset of the complex numbers. $)
  zsscn $p |- ZZ (_ CC $=
    ( vx cz cc cv zcnt ssriv ) ABCADEF $.
    $( [10-Sep-04] $) $( [2-Aug-04] $)

  $( Natural number property expressed in terms of integers. $)
  elnnz $p |- ( A e. NN <-> ( A e. ZZ /\ 0 < A ) ) $=
    ( cn wcel cr cneg cc0 wceq wo wa clt wbr cz nnret orc jca nngt0t wi idd wn
    lt0neg2t negrclt ax0re ltnsymt mpan2 syl sylbid imp nsyl ltnet mpan
    cleqcom bineg sylibr ioran pm2.21d jaod exp com23 imp31 impbi w3o elz
    3orrot 3orass bitr bilan biran bitr4 ) ABCZADCZVIAEZBCZAFGZHZHZIZFAJKZIZALC
    ZVQIVIVRVIVPVQVIVJVOAMVIVNNOAPOVJVOVQVIVJVQVOVIVJVQVOVIQVJVQIZVIVIVNVTVIRVT
    VNVIVTVLSZVMSZIVNSVTWAWBVTFVKJKZVLVJVQWCSZVJVQVKFJKZWDATVJVKDCZWEWDQZAUAWFF
    DCZWGUBVKFUCUDUEUFUGVKPUHVTFAGZSZWBVJVQWJWHVJVQWJQUBFAUIUJUGVMWIAFUKULUMOVL
    VMUNUMUOUPUQURUSUTVSVPVQVSVJVMVIVLVAZIVPAVBWKVOVJWKVIVLVMVAVOVMVIVLVCVIVLVM
    VDVEVFVEVGVH $.
    $( [8-Jan-02] $)

  $( Zero is an integer. $)
  0z $p |- 0 e. ZZ $=
    ( cc0 cz wcel cr wceq cn cneg w3o wa ax0re wo cleqid orc ax-mp 3orass
    mpbir pm3.2i elz ) ABCADCZAAEZAFCZAGFCZHZISUCJUCTUAUBKZKZTUEALTUDMNTUAUBOPQ
    ARP $.
    $( [12-Jan-02] $)

  $( Nonnegative integer property expressed in terms of integers. $)
  elnn0z $p |- ( A e. NN0 <-> ( A e. ZZ /\ 0 <_ A ) ) $=
    ( cn0 wcel cn cc0 wceq wo cz clt wbr wa cle elnn0 elnnz cleqcom bi2or id
    0z eleq1 mpbii jaoi orc impbi biran ordir cr wb zret ax0re leloet mpan syl
    pm5.32i 3bitr4 3bitr ) ABCADCZAEFZGAHCZEAIJZKZEAFZGZUREALJZKZAMUPUTUQVAANAE
    OPURVAGZUSVAGZKURVFKVBVDVEURVFVEURURURVAURQVAEHCURREAHSTUAURVAUBUCUDURUSVAU
    EURVCVFURAUFCZVCVFUGZAUHEUFCVGVHUIEAUJUKULUMUNUO $.
    $( [7-Jun-04] $) $( [9-May-04] $)

  $( Integer property expressed in terms nonnegative integers and natural
     numbers. $)
  elznn0nn $p |- ( A e. ZZ <-> ( A e. NN0 \/ ( A e. RR /\ -u A e. NN ) ) ) $=
    ( cz wcel cr cc0 wceq cn cneg w3o wa wo cn0 elz df-3or bilan andi nn0ret
    pm4.71ri elnn0 orcom bitr biror bitr4 3bitr ) ABCADCZAEFZAGCZAHGCZIZJUEUFUG
    KZUHKZJZALCZUEUHJZKZAMUIUKUEUFUGUHNOULUEUJJZUNKUOUEUJUHPUMUPUNUMUEUMJUPUMUE
    AQRUMUJUEUMUGUFKUJASUGUFTUAOUAUBUCUD $.
    $( [4-Jul-04] $) $( [10-May-04] $)

  $( Integer property expressed in terms of nonnegative integers. $)
  elznn0 $p |- ( A e. ZZ <-> ( A e. RR /\ ( A e. NN0 \/ -u A e. NN0 ) ) ) $=
    ( cz wcel cr cc0 wceq cn cneg w3o wa cn0 wo elz wb elnn0 a1i cc recnt 0cn
    negcon1t mpan2 syl neg0 cleq1i cleqcom bitr syl6bb bilord syl5bb bi2ord
    3orass orcom orordir bitr2 syl6rbb pm5.32i ) ABCADCZAEFZAGCZAHZGCZIZJUQAKCZ
    UTKCZLZJAMUQVBVEUQVEUSURLZVAURLZLZVBUQVCVFVDVGVCVFNUQAOPUQVAUTEFZLVGVDUQVIU
    RVAUQVIEHZAFZURUQAQCZVIVKNZARVLEQCVMSAETUAUBVKEAFURVJEAUCUDEAUEUFUGUHUTOUIU
    JVBURUSVALZLZVHURUSVAUKVOVNURLVHURVNULUSVAURUMUFUNUOUPUF $.
    $( [7-Jun-04] $) $( [9-May-04] $)

  $( Natural numbers are a subset of integers. $)
  nnssz $p |- NN (_ ZZ $=
    ( vx cn cz cv wcel cc0 clt wbr elnnz pm3.26bd ssriv ) ABCADZBELCEFLGHLIJK
    $.
    $( [9-Jan-02] $)

  $( Nonnegative integers are a subset of the integers. $)
  nn0ssz $p |- NN0 (_ ZZ $=
    ( cn0 cn cc0 csn cun cz df-n0 nnssz wcel wss 0z elisseti snss mpbi unssi
    eqsstr ) ABCDZEFGBQFHCFIQFJKCFCFKLMNOP $.
    $( [5-Jul-04] $) $( [9-May-04] $)

  $( A natural number is an integer. $)
  nnzt $p |- ( A e. NN -> A e. ZZ ) $=
    ( cn cz nnssz sseli ) BCADE $.
    $( [4-Jun-04] $) $( [9-May-04] $)

  $( A nonnegative integer is an integer. $)
  nn0zt $p |- ( A e. NN0 -> A e. ZZ ) $=
    ( cn0 cz nn0ssz sseli ) BCADE $.
    $( [5-Jul-04] $) $( [9-May-04] $)

  $( Natural number property expressed in terms of integers. $)
  elnnz1 $p |- ( A e. NN <-> ( A e. ZZ /\ 1 <_ A ) ) $=
    ( cn wcel cz c1 cle wbr wa nnzt nnge1t jca cneg cc0 wceq wo wn clt cr lt01
    wb ax1re lt0neg2t ax-mp mpbi wi lenegt mpan negrclt negrcl ax0re lelttrt
    mp3an3 mpan2 ltlet syld syl exp3a sylbid imp mpi leltt adantr mpbid zret
    sylan nngt0t nsyl breq2 lelt syl6bb bicon2d mpbii con2i adantl ioran
    sylibr w3o elz pm3.27 3orrot df-3or bitr3 sylib sylbi ord mpd impbi ) ABCZA
    DCZEAFGZHZWHWIWJAIAJKWKALZBCZAMNZOZPZWHWKWMPZWNPZHWPWKWQWRWKMWLQGZWMARCZWJW
    SPZWIWTWJHZWLMFGZXAXBELZMQGZXCMEQGZXESERCZXFXETUAEUBUCUDWTWJXEXCUEZWTWJWLXD
    FGZXHXGWTWJXITUAEAUFUGWTXIXEXCWTWLRCZXIXEHZXCUEAUHZXJXKWLMQGZXCXJXDRCZXKXMU
    EZEUAUIXJXNMRCZXOUJWLXDMUKULUMXJXPXMXCUEUJWLMUNUMUOUPUQURUSUTWTXCXATZWJWTXJ
    XQXLXJXPXQUJWLMVAUMUPVBVCAVDVEWLVFVGWJWRWIWNWJWNXFWJPSWNWJXFWNWJEMFGXFPAMEF
    VHEMUAUJVIVJVKVLVMVNKWMWNVOVPWIWPWHUEWJWIWOWHWIWTWNWHWMVQZHZWOWHOZAVRXSXRXT
    WTXRVSXRWMWNWHVQXTWMWNWHVTWMWNWHWAWBWCWDWEVBWFWG $.
    $( [9-Jun-04] $) $( [10-May-04] $)

  $( One is an integer. $)
  1z $p |- 1 e. ZZ $=
    ( c1 cn wcel cz 1nn nnzt ax-mp ) ABCADCEAFG $.
    $( [4-Jun-04] $) $( [10-May-04] $)

  $( Two is an integer. $)
  2z $p |- 2 e. ZZ $=
    ( c2 cn wcel cz 2nn nnzt ax-mp ) ABCADCEAFG $.
    $( [5-Jul-04] $) $( [10-May-04] $)

  $( One-half is not an integer. $)
  halfnz $p |- -. ( 1 / 2 ) e. ZZ $=
    ( c1 c2 cdiv co cz wcel cr cc0 wceq cn cneg w3o wa wo wne wn 1cn 2cn
    ax1ne0 2re 2pos gt0ne0i divneq0 df-ne mpbi cle wbr clt caddc df-2 opreq2i
    lt01 ax1re halfpos eqbrtr divrcl lelt bicon2 nnge1t mto pm3.2ni ax0re
    recgt0i ltlei wb le0neg2t ax-mp negrcl nngt0t df-3or mtbir intnan elz ) ABC
    DZEFVNGFZVNHIZVNJFZVNKZJFZLZMVTVOVTVPVQNZVSNWAVSVPVQVNHOVPPABQRSBTUAUBZUCVN
    HUDUEVQAVNUFUGZVNAUHUGZWCPVNAAAUIDZCDZAUHBWEACUJUKHAUHUGWFAUHUGULAUMUNUEUOW
    CWDAVNUMABUMTWBUPZUQURUEVNUSUTVAVSHVRUHUGZVRHUFUGZWHPHVNUFUGZWIHVNVBWGBTUAV
    CVDVOWJWIVEWGVNVFVGUEVRHVNWGVHVBUQUEVRVIUTVAVPVQVSVJVKVLVNVMVK $.
    $( [1-Aug-04] $) $( [31-Jul-04] $)

  $( Subtraction of nonnegative integers. $)
  nn0subt $p |- ( ( A e. NN0 /\ B e. NN0 ) ->
                ( A <_ B <-> ( B - A ) e. NN0 ) ) $=
    ( cn wcel cc0 wceq wo cle wbr cmin co cn0 wb wa clt wi nnsubt exp breq2
    opreq1 eleq1d bi2bid cneg cr nnret lt0neg1t syl cz nnnegz elnnz baib
    df-neg eleq1i syl5rbbr bitrd syl5bir com12 jaod breq1 opreq2 nnzt cc zcnt
    subid1t bitr2d wn ax0re ltnr 0nnn 0cn subid mtbir pm5.21 mp2an mpbiri jaoi
    imp subeq0t cleqcom syl6rbbr ancoms nncnt eleq1 syl2an bi2ord leloet elnn0
    a1i 3bitr4d syl2anb ) ACDZAEFZGZBCDZBEFZGZABHIZBAJKZLDZMALDBLDWMWPNZABOIZAB
    FZGZWRCDZWREFZGZWQWSWTXAXDXBXEWMWPXAXDMZWKWPXGPWLWKWNXGWOWKWNXGABQRWOWKXGWO
    XGAEOIZEAJKZCDZMWKWOXAXHXDXJBEAOSWOWRXICBEAJTUAUBWKXHEAUCZOIZXJWKAUDDZXHXLM
    AUEZAUFUGWKXKCDZXLXJWKXKUHDZXOXLMAUIXOXPXLXKUJUKUGXKXICAULUMUNUOUPUQURWLWNX
    GWOWLXGEBOIZBEJKZCDZMZWNWLXAXQXDXSAEBOUSWLWRXRCAEBJUTUAUBZWNBUHDZXTBVAYBXSW
    NXQYBBVBDZXSWNMBVCYCXRBCBVDUAUGWNYBXQBUJUKVEUGUPWLXGXTWOYAWOXTEEOIZEEJKZCDZ
    MZYDVFYFVFYGEVGVHYFECDVIYEECEVJVKUMVLYDYFVMVNWOXQYDXSYFBEEOSWOXRYECBEEJTUAU
    BVOUPURVPVQAVBDZYCXBXEMZWMWPYCYHYIYCYHNXEBAFXBBAVRABVSVTWAWKYHWLAWBWLYHEVBD
    ZVJAEVBWCVOVPWNYCWOBWBWOYCYJVJBEVBWCVOVPWDWEXMBUDDZWQXCMWMWPABWFWKXMWLXNWLX
    MEUDDZVGAEUDWCVOVPWNYKWOBUEWOYKYLVGBEUDWCVOVPWDWSXFMWTWRWGWHWIAWGBWGWJ $.
    $( [9-Jun-04] $) $( [9-May-04] $)

  $( Closure law for negative integers. $)
  znegclt $p |- ( A e. ZZ -> -u A e. ZZ ) $=
    ( cz wcel cr cc0 wceq cn cneg w3o wa elz 0z negeq neg0 syl6eq eleq1d
    mpbiri nnnegz nnssz sseli 3jaoi adantl sylbi ) ABCADCZAEFZAGCZAHZGCZIZJUGBC
    ZAKUIUJUDUEUJUFUHUEUJEBCLUEUGEBUEUGEHEAEMNOPQARGBUGSTUAUBUC $.
    $( [11-Jun-04] $) $( [9-May-04] $)

  $( The negative of a nonnegative integer is an integer. $)
  nn0negz $p |- ( A e. NN0 -> -u A e. ZZ ) $=
    ( cn0 wcel cz cneg nn0zt znegclt syl ) ABCADCAEDCAFAGH $.
    $( [10-Sep-04] $) $( [9-May-04] $)

  $( Closure of addition of integers. $)
  zaddclt $p |- ( ( A e. ZZ /\ B e. ZZ ) -> ( A + B ) e. ZZ ) $=
    ( cr wcel cn0 cneg wo wa caddc co cz axaddrcl a1d wi nn0addclt orc syl a1i
    jcad cle wbr letrit negrclt sylan adantr cmin nn0subt cc wceq subneg2t
    ancoms axaddcom eqtr4d recnt syl2an eleq1d sylan9bbr wb subnegt negclt
    negdistt bi2ord mpbid exp sylan5 syl5bir olc syl6 ccased elznn0 syl6ibr
    imp an4s syl2anb ) ACDZAEDZAFZEDZGZHBCDZBEDZBFZEDZGZHABIJZKDZAKDBKDVOVTVSWD
    WFVOVTHZVSWDHZWFWGWHWECDZWEEDZWEFZEDZGZHZWFWGVPWAVRWCWNWGVPWAHZWIWMWGWIWOAB
    LZMWOWMNWGWOWJWMABOWJWLPQRSWGVRWAHZWIWMWGWIWQWPMWGWQWMWGWQHZVQBTUAZBVQTUAZG
    ZWMWGXAWQVQCDVTXAVOVQBUBAUCUDUEWRWSWJWTWLWQWSBVQUFJZEDWGWJVQBUGWGXBWEEAUHDZ
    BUHDZXBWEUIVOVTXCXDHZXBBAIJZWEXDXCXBXFUIBAUJUKABULUMAUNZBUNZUOUPUQWQWTVQBUF
    JZEDZWGWLWAVRWTXJURBVQUGUKWGXIWKEXCXDXIWKUIVOVTXEXIVQWBIJZWKVQUHDZXDXIXKUIX
    CVQBUSAUTZUDABVAZUMXGXHUOUPUQVBVCVDSWGVPWCHZWIWMWGWIXOWPMWGXOWMWGXOHZWBATUA
    ZAWBTUAZGZWMWGXSXOVTVOXSWBCDVOXSVTWBAUBBUCUDUKUEXPXQWJXRWLXOXQAWBUFJZEDZWGW
    JWCVPXQYAURWBAUGUKWGXTWEEXCXDXTWEUIVOVTABUJXGXHUOUPUQXOXRWBAUFJZEDWGWLAWBUG
    WGYBWKEXCXDYBWKUIVOVTXEYBXKWKXCWBUHDZYBXKUIXDXCYCHYBWBVQIJZXKYCXCYBYDUIWBAU
    SUKXLYCXKYDUIXCVQWBULXMUDUMBUTVEXNUMXGXHUOUPUQVBVCVDSWGVRWCHZWIWMWGWIYEWPMW
    GYEWLWMWGWLXKEDYEWGWKXKEXCXDWKXKUIVOVTXNXGXHUOUPVQWBOVFWLWJVGVHSVIWEVJVKVLV
    MAVJBVJVN $.
    $( [11-Jun-04] $) $( [9-May-04] $)

  $( Closure of subtraction of integers. $)
  zsubclt $p |- ( ( A e. ZZ /\ B e. ZZ ) -> ( A - B ) e. ZZ ) $=
    ( cz wcel wa cmin co cneg caddc cc wceq subnegt zcnt syl2an zaddclt
    znegclt sylan5 eqeltrd ) ACDZBCDZEABFGZABHZIGZCAJDBJDUAUCKSTABLAMBMNSUBCDUC
    CDTAUBOBPQR $.
    $( [11-Jun-04] $) $( [11-May-04] $)

  $( Reverse closure law for addition of integers. $)
  zrevaddclt $p |- ( B e. ZZ ->
                   ( ( A e. CC /\ ( A + B ) e. ZZ ) <-> A e. ZZ ) ) $=
    ( cz wcel cc caddc co wa wb cmin zsubclt ancoms adantlr wceq pncant zcnt
    sylan5 adantr eleq1d mpbid exp wi zaddclt impbid pm5.32d pm4.71ri syl6bbr
    ) BCDZAEDZABFGZCDZHUIACDZHULUHUIUKULUHUIUKULIUHUIHZUKULUMUKULUMUKHZUJBJGZCD
    ZULUHUKUPUIUKUHUPUJBKLMUNUOACUMUOANZUKUIUHUQUIBEDUQUHABOBPQLRSTUAUHULUKUBUI
    UHULUKULUHUKABUCLUARUDUAUEULUIAPUFUG $.
    $( [14-Jun-04] $) $( [11-May-04] $)

  $( The nonnegative integer property expressed in terms of natural numbers. $)
  elnn0nn $p |- ( A e. NN0 <-> ( A e. CC /\ ( A + 1 ) e. NN ) ) $=
    ( cr wcel cn0 wa c1 caddc co cn cc cc0 wceq wo wi peano2c 1nn opreq1 1cn
    addid2 syl6eq eleq1d mpbiri jaoi a1i cz cle wbr clt recnt wb 1z zrevaddclt
    ax-mp biimp exp syl ax1re ax0re leadd1t mp3an1 mpan2 breq1i syl6bb leloet
    mpan bitr3d biimpd im2and andi elnnz cleqcom 0z eleq1 mpbii pm4.71ri bitr
    bi2or bitr4 syl6ib elnnz1 syl5ib impbid elnn0 syl5bb pm5.32i nn0ret zret
    sylbi nnzt sylan5 pm3.27 jca imran impbi 3bitr4 ) ABCZADCZEWPAFGHZICZEZWQAJ
    CZWSEZWPWQWSWPAICZAKLZMZWSWQWPXEWSXEWSNWPXCWSXDAOXDWSFICPXDWRFIXDWRKFGHZFAK
    FGQFRSZTUAUBUCUDWPWRUECZFWRUFUGZEZXEWSWPXJAUECZKAUHUGZKALZMZEZXEWPXHXKXIXNW
    PXAXHXKNAUIZXAXHXKXAXHEZXKFUECXQXKUJUKAFULUMZUNUOUPWPXIXNWPKAUFUGZXIXNWPXSX
    FWRUFUGZXIWPFBCZXSXTUJZUQKBCZWPYAYBURKAFUSUTVAXFFWRUFXGVBVCYCWPXSXNUJURKAVD
    VEVFVGVHXOXKXLEZXKXMEZMXEXKXLXMVIXCYDXDYEAVJXDXMYEAKVKXMXKXMKUECXKVLKAUEVMV
    NVOVPVQVRVSWRVTWAWBAWCWDWEWQWPAWFVOXBWTXBWPWSXAXHWPWSXQXKWPXRAWGWHWRWIWJXAW
    SWKWLWPXAWSXPWMWNWO $.
    $( [10-Sep-04] $) $( [10-May-04] $)

  $( The natural number property expressed in terms of nonnegative integers. $)
  elnnnn0 $p |- ( A e. NN <-> ( A e. CC /\ ( A - 1 ) e. NN0 ) ) $=
    ( cn wcel cc wa c1 cmin co cn0 nncnt pm4.71ri caddc wceq 1cn npcant mpan2
    eleq1d subclt biantrurd bitr3d elnn0nn syl6bbr pm5.32i bitr ) ABCZADCZUEEUF
    AFGHZICZEUEUFAJKUFUEUHUFUEUGDCZUGFLHZBCZEZUHUFUKUEULUFUJABUFFDCZUJAMNAFOPQU
    FUIUKUFUMUINAFRPSTUGUAUBUCUD $.
    $( [11-Sep-04] $) $( [10-May-04] $)

  $( The positive difference of unequal integers is a natural number.
     (Generalization of ~ nnsubt .) $)
  znnsubt $p |- ( ( A e. ZZ /\ B e. ZZ ) -> ( A < B <-> ( B - A ) e. NN ) ) $=
    ( cz wcel wa clt wbr cneg caddc co cmin cn cr wb w3a pm3.26 pm3.27 negrclt
    adantr 3jca ltadd1t syl zret syl2an cc0 zsubclt ancoms elnnz baib cc wceq
    subidt subnegt anidms eqtr3d breq12d zcnt bitr2d bitrd ) ACDZBCDZEZABFGZAAH
    ZIJZBVDIJZFGZBAKJZLDZAMDZBMDZVCVGNZUTVAVJVKEZVJVKVDMDZOVLVMVJVKVNVJVKPVJVKQ
    VJVNVKARSTABVDUAUBAUCBUCUDVBVIUEVHFGZVGVBVHCDZVIVONVAUTVPBAUFUGVIVPVOVHUHUI
    UBAUJDZBUJDZVOVGNUTVAVQVREUEVEVHVFFVQUEVEUKVRVQAAKJZUEVEAULVQVSVEUKAAUMUNUO
    SVRVQVHVFUKBAUMUGUPAUQBUQUDURUS $.
    $( [11-Sep-04] $) $( [11-May-04] $)

  $( Closure of multiplication of integers. $)
  zmulclt $p |- ( ( A e. ZZ /\ B e. ZZ ) -> ( A x. B ) e. ZZ ) $=
    ( cr wcel cn0 cneg wo wa cmulc co cz axmulrcl a1d wi nn0mulclt orc syl a1i
    jcad cc wceq mulneg1t recnt syl2an eleq1d syl5bi olc syl6 mulneg2t
    mul2negt ccased elznn0 syl6ibr imp an4s syl2anb ) ACDZAEDZAFZEDZGZHBCDZBEDZ
    BFZEDZGZHABIJZKDZAKDBKDUQVBVAVFVHUQVBHZVAVFHZVHVIVJVGCDZVGEDZVGFZEDZGZHZVHV
    IURVCUTVEVPVIURVCHZVKVOVIVKVQABLZMVQVONVIVQVLVOABOVLVNPZQRSVIUTVCHZVKVOVIVK
    VTVRMVIVTVNVOVIUSBIJZEDVNVTVIWAVMEATDZBTDZWAVMUAUQVBABUBAUCZBUCZUDUEUSBOUFV
    NVLUGZUHSVIURVEHZVKVOVIVKWGVRMVIWGVNVOVIAVDIJZEDVNWGVIWHVMEWBWCWHVMUAUQVBAB
    UIWDWEUDUEAVDOUFWFUHSVIUTVEHZVKVOVIVKWIVRMVIWIVLVOVIUSVDIJZEDVLWIVIWJVGEWBW
    CWJVGUAUQVBABUJWDWEUDUEUSVDOUFVSUHSUKVGULUMUNUOAULBULUP $.
    $( [8-Sep-04] $) $( [30-Jul-04] $)

  $( Integer ordering relation. $)
  zltle1 $p |- ( ( A e. ZZ /\ B e. ZZ ) -> ( A < B <-> ( A + 1 ) <_ B ) ) $=
    ( cr wcel cn0 cneg wo wa clt wbr c1 caddc co cle wb cz wi nn0ltle1 a1i cn
    cc0 wceq cc recnt 0cn negcon1t mpan2 neg0 cleq1i cleqcom bitr syl6bb syl
    bilord elnn0 syl5bb bi2and adantr lt0neg1t nngt0t syl5bir imp im2an ax0re
    axlttrn mp3an2 nnret sylan5 adantlr mpd adantl mpbird ltlet ax1re leadd1t
    mpbid 1cn addid2 syl6breq nnge1t letrt axaddrcl syl2an jca exp31 imp3a
    pm5.1 syl6 breq1 opreq1 syl6eq breq1d bi2bid sylan13 breq2 cmin nnnn0t
    0nn0 nn0ltlem1 mpan le0neg1t negdistt subnegt negclt sylan eqtr4d breq2d
    bitrd 3bitr4d com12 exp wn ltnr lt01 lelt bicon2 mpbi pm5.21 mp2an
    sylan9bb mpbiri ccased sylbid nn0ge0t adantrl adantll ad2antrl ancoms
    leltt ltplus1t ad2antll lelttrt 3expb ancli bicon2d ltnegt lenegt an4s
    elznn0 syl2anb ) ACDZAEDZAFZEDZGZHBCDZBEDZBFZEDZGZHABIJZAKLMZBNJZOZAPDBPDUV
    AUVFUVEUVJUVNUVAUVFHZUVEUVJHUVNUVOUVBUVGUVDUVIUVNUVBUVGHUVNQUVOABRSUVOUVDUV
    GHZUVCTDZAUAUBZGZBTDZBUAUBZGZHZUVNUVAUVPUWCOUVFUVAUVDUVSUVGUWBUVAUVQUVCUAUB
    ZGUVSUVDUVAUWDUVRUVQUVAAUCDZUWDUVROAUDZUWEUWDUAFZAUBZUVRUWEUAUCDUWDUWHOUEAU
    AUFUGUWHUAAUBUVRUWGUAAUHUIUAAUJUKULUMUNUVCUOUPUVGUWBOUVABUOSUQURUVOUVQUVTUV
    RUWAUVNUVAUVQUVTHZUVNQUVFUVAUWIUVKUVMHZUVNUVAUVQUVTUWJUVAUVQUVTUWJUVAUVQHZU
    VTHZUVKUVMUWLAUAIJZUABIJZHZUVKUWKUWMUVTUWNUVAUVQUWMUVAUWMUAUVCIJZUVQAUSZUVC
    UTZVAVBBUTZVCUVAUVTUWOUVKQZUVQUVAUVFUWTUVTUVAUACDZUVFUWTVDAUABVEVFBVGZVHVIV
    JUWLUVLKNJZKBNJZHZUVMUWKUXCUVTUXDUWKUVLUAKLMZKNUWKAUANJZUVLUXFNJZUWKUWMUXGU
    WKUWMUWPUVQUWPUVAUWRVKUVAUWMUWPOUVQUWQURZVLUVAUWMUXGQZUVQUVAUXAUXJVDAUAVMUG
    URVJUVAUXGUXHOZUVQUVAKCDZUXKVNUVAUXAUXLUXKVDAUAKVOVFUGURVPKVQVRZVSBVTZVCUVA
    UVTUXEUVMQZUVQUVLCDZUVFUXOUVAUVTUXPUXLUVFUXOVNUVLKBWAVFUVAUXLUXPVNAKWBUGZUX
    BWCVIVJWDWEWFUVKUVMWGWHURUVRUVTHUVNQUVOUVRUVTUVNUVRUVNUWNUXDOZUVTUVRUVKUWNU
    VMUXDAUABIWIUVRUVLKBNUVRUVLUXFKAUAKLWJUXMWKWLWMZUWNUXDUXRUVTUWNUXDWGUWSUXNW
    NVAVBSUVAUVQUWAHUVNQUVFUVAUVQUWAUVNUVAUVQUWAUVNQUWAUWKUVNUWAUVNUWMUVLUANJZO
    UWKUWAUVKUWMUVMUXTBUAAIWOBUAUVLNWOWMUWKUWPUAUVCKWPMZNJZUWMUXTUVQUWPUYBOZUVA
    UVQUVDUYCUVCWQUAEDUVDUYCWRUAUVCWSWTUMVKUXIUVAUXTUYBOUVQUVAUXTUAUVLFZNJZUYBU
    VAUXPUXTUYEOUXQUVLXAUMUVAUYDUYAUANUVAUWEUYDUYAUBZUWFUWEKUCDZUYFVQUWEUYGHUYD
    UVCKFLMZUYAAKXBZUVCUCDZUYGUYAUYHUBZUWEUVCKXCZAXDZXEXFUGUMXGXHURXIVAXJXKWFUR
    UVRUWAHZUVNQUVOUYNUVNUAUAIJZKUANJZOZUYOXLUYPXLZUYQUAVDXMUAKIJZUYRXNUYPUYSKU
    AVNVDXOXPXQUYOUYPXRXSUVRUVNUXRUWAUYQUXSUWAUWNUYOUXDUYPBUAUAIWOBUAKNWOWMXTYA
    SYBYCUVOUVBUVIHZUVKXLZUVMXLZHZUVNUVOUYTVUCUVOUYTHZVUAVUBVUDBANJZVUAVUDBUANJ
    ZUAANJZHZVUEVUDVUFVUGUVFUYTVUFUVAUVFUVIVUFUVBUVFUVIHVUFUAUVHNJZUVIVUIUVFUVH
    YDVKUVFVUFVUIOUVIBXAURVLYEYFUVBVUGUVOUVIAYDYGWDUVOVUHVUEQZUYTUVFUVAVUJUVFUX
    AUVAVUJVDBUAAWAVFYHURVJZUVOVUEVUAOZUYTUVFUVAVULBAYIYHURVPVUDBUVLIJZVUBVUDVU
    EAUVLIJZHZVUMVUDVUEVUNVUKUVAVUNUVFUYTAYJYKWDUVOVUOVUMQZUYTUVFUVAVUPUVFUVAUX
    PHVUPUVAUVFUVAUXPVUPBAUVLYLYMUVAUXPUXQYNVHYHURVJUVOVUMVUBOUYTUVOUVMVUMUXPUV
    FUVMVUMXLOUVAUVLBYIUXQXEYOURVPWDXKUVKUVMXRWHUVOUVDUVIHZUVNUVOVUQHUVHUVCIJZU
    VHUYANJZUVKUVMVUQVURVUSOZUVOUVIUVDVUTUVHUVCWSYHVKUVOUVKVUROVUQABYPURUVOUVMV
    USOVUQUVOUVMUVHUYDNJZVUSUXPUVFUVMVVAOUVAUVLBYQUXQXEUVAVVAVUSOUVFUVAUYDUYAUV
    HNUVAUWEUYFUWFUWEUYDUYHUYAUWEUYGUYDUYHUBVQUYIUGUWEUYJUYKUYMUYJUYGUYKVQUYLUG
    UMXFUMXGURXHURXIXKYBVBYRAYSBYSYT $.
    $( [12-Sep-04] $) $( [10-May-04] $)

  $( Integer ordering relation. $)
  zlelt1 $p |- ( ( A e. ZZ /\ B e. ZZ ) -> ( A <_ B <-> A < ( B + 1 ) ) ) $=
    ( cz wcel wa cle wbr c1 caddc co clt cr wb ax1re leadd1t mp3an3 zret
    syl2an zltle1 1z zaddclt mpan2 sylan5 bitr4d ) ACDZBCDZEABFGZAHIJBHIJZFGZAU
    HKGZALDZBLDZUGUIMZUEUFUKULHLDUMNABHOPAQBQRUEUHCDZUJUIMUFAUHSUFHCDUNTBHUAUBU
    CUD $.
    $( [13-Sep-04] $) $( [10-May-04] $)

  $( The square of a non-zero integer is a natural number. $)
  sqznn $p |- ( ( A e. ZZ /\ A =/= 0 ) -> ( A x. A ) e. NN ) $=
    ( cz wcel cc0 wne wa cmulc co clt wbr cn zmulclt anidms adantr cr wceq wn
    ltsqt df-ne syl5ib imp zret sylan jca elnnz sylibr ) ABCZADEZFZAAGHZBCZDUJI
    JZFUJKCUIUKULUGUKUHUGUKAALMNAOCZUHULUGUMUHULUMADPQULUHARADSTUAAUBUCUDUJUEUF
    $.
    $( [13-Sep-04] $) $( [2-Aug-04] $)

  $( No even integer equals an odd integer (i.e. no integer can be both even
     and odd). $)
  zneo $p |- ( ( A e. ZZ /\ B e. ZZ ) ->
             -. ( 2 x. A ) = ( ( 2 x. B ) + 1 ) ) $=
    ( cz wcel wa c2 cmulc co c1 caddc wceq cdiv wn cmin halfnz cc zcnt 1cn 2cn
    2re 2pos gt0ne0i divcl axaddcom mpan2 opreq1d pncant mpan eqtrd syl eleq1d
    mtbiri zsubclt ancoms exp mtod adantl wi wb cc0 wne divcan3t axmulcl w3a
    divdistrt mp3an3 cleqan12d opreq1 syl5bi syl2an imp exp31 com3l ibib
    syl6ibr com3r ) ACDZBCDZEZFAGHZFBGHZIJHZKZBIFLHZJHZCDZVRWFMVQVRWFWEBNHZCDZV
    RWHWDCDOVRWGWDCVRBPDZWGWDKBQZWIWGWDBJHZBNHZWDWIWEWKBNWIWDPDZWEWKKIFRSFTUAUB
    ZUCZBWDUDUEUFWMWIWLWDKWOWDBUGUHUIUJUKULVRWFWHWFVRWHWEBUMUNUOUPUQVQVRWCWFURV
    RWCVQWFVRWCVQVQWFUSZURVQWFURVQVRWCWPVQVRWCWPVSWCEAWECVSWCAWEKZAPDZWIWCWQURV
    QVRWRWIEVTFLHZWBFLHZKWQWCWRWIWSAWTWEFPDZWRWSAKZSXAWREFUTVAZXBWNFAVBUEUHWIWT
    WAFLHZWDJHZWEWIWAPDZWTXEKZXAWIXFSFBVCUHXFIPDZXGRXFXHXAXGSXFXHXAVDXCXGWNWAIF
    VEUEVFUEUJWIXDBWDJXAWIXDBKZSXAWIEXCXIWNFBVBUEUHUFUIVGVTWBFLVHVIAQWJVJVKUKVL
    VMVQWFVNVOVPVKUP $.
    $( [15-Sep-04] $) $( [31-Jul-04] $)

$(
  @{
    @d x z w A @.  @d x z ps @.  @d x z ch @.  @d x z th @.  @d x z ta @.
    @d y w ph @.  @d x y z w B @.
    zindlem.a @e |- A e. ZZ @.
    zindlem.b @e |- B e. ZZ @.
    zindlem.c @e |- B <_ A @.
    @( Substitutions. @)
    zindlem.1 @e |- ( x = B -> ( ph <-> ps ) ) @.
    zindlem.2 @e |- ( x = y -> ( ph <-> ch ) ) @.
    zindlem.3 @e |- ( x = ( y + 1 ) -> ( ph <-> th ) ) @.
    zindlem.4 @e |- ( x = A -> ( ph <-> ta ) ) @.
    @( Basis. @)
    zindlem.5 @e |- ps @.
    @( Induction hypothesis. @)
    zindlem.6 @e |- ( ( y e. ZZ /\ B <_ y ) -> ( ch -> th ) ) @.
    @( Induction on the upper partition of ` ZZ ` that starts at an integer
       ` B ` .  The first four hypotheses give us the substitution instances we
       need; the last two are the basis and the induction hypothesis. @)
    zindlem @p |- ta @=
            ( vz vw cmin co c1 caddc cn wcel ind ax-mp ) HIUAUBUCUDUBZUEUFE?????ESTUI
      ??????UGUH @.
      @([?]@) @([20-Jun-04]@)
  @}

  @{
    @d y w ph @.  @d x y z w B @.
    zindlem2.b @e |- B e. ZZ @.
    @( Basis. @)
    zindlem2.1 @e |- [ B / x ] ph @.
    @( Induction hypothesis. @)
    zindlem2.6 @e |- ( ( x e. ZZ /\ B <_ x ) -> ( [ ( x + 1 ) / x ] ph -> ph ) ) @.
    @( Induction on the upper partition of ` ZZ ` that starts at an integer
       ` B ` .  The first four hypotheses give us the substitution instances we
       need; the last two are the basis and the induction hypothesis. @)
    zindlem2 @p |- ( ( x e. ZZ /\ B <_ x ) -> ph ) @=
      ? @.
      @([?]@) @([20-Jun-04]@)
  @}
$)

  ${
    $d x z w A $.  $d x z ps $.  $d x z ch $.  $d x z th $.  $d x z ta $.
    $d y w ph $.  $d x y z w B $.
    $( Substitutions. $)
    zind.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    zind.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    zind.3 $e |- ( x = ( y + 1 ) -> ( ph <-> th ) ) $.
    zind.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    zind.5 $e |- ps $.
    $( Induction hypothesis. $)
    zind.6 $e |- ( ( ( y e. ZZ /\ B e. ZZ ) /\ B <_ y ) -> ( ch -> th ) ) $.
    $( Induction on the upper partition of ` ZZ ` that starts at an integer
       ` B ` .  The first four hypotheses give us the substitution instances we
       need; the last two are the basis and the induction hypothesis.

       _Warning:  The HTML proof page is 3/4 megabyte in size.  An attempt
      to shorten it is on my to-do list_. $)
    zind $p |- ( ( ( A e. ZZ /\ B e. ZZ ) /\ B <_ A ) -> ta ) $=
      ( vz vw cz wcel wa cle wbr c1 cmin co caddc cr wb w3a ax1re a1i subrclt
      jctir axaddrcl syl mpan2 adantl 3jca leadd2t cc wceq 1cn npcant subclt
      add12t opreq2d 3eqtrd breq12d recnt syl2an bitr2d zret wi zsubclt 1z
      zaddclt cn elnnz1 cv wsbc eleq1 wex oprex isseti ax-17 hbsbcv hbbi hbim
      sbceq1 adantr cleqtr opreq1 opreq1d cc0 zcnt addid2t subid opreq1i
      syl5eq sylan9eq sylan5 bitr3d exp 19.23ai ax-mp adantld pm5.74d bi2imd
      weq eeanv cleq12 syl5bir syl6 bi2bian9 sylibd sylbir mp2an bilimd
      axaddcl sylan dfsbcq pm5.74i syl5bbr pm3.27 add23t eqtrd addsubt pncant
      3eqtr3d anasss nnzt a1d birand breq2 bi2and jca lesub1t subsubt mp3an3
      anidms subidt addid2 syl6eq sylan13 bitrd biimpar an1rs sylanb biimpd
      syl34d mpi nncnt sylibrd a2d ind pm2.43a com23 mpcom sylbid imp ) HRSZIRS
      ZTZIHUAUBZEUWCUWDUCHIUDUEZUCUFUEZUAUBZEHUGSZIUGSZUWDUWGUHUWAUWBUWHUWITZUW
      GIUCUDUEZUCUFUEZUWKUWFUFUEZUAUBZUWDUWJUCUGSZUWFUGSZUWKUGSZUIUWGUWNUHUWJUW
      OUWPUWQUWOUWJUJUKUWJUWEUGSZUWOTUWPUWJUWRUWOHIULUJUMUWEUCUNUOUWIUWQUWHUWIU
      WOUWQUJIUCULUPZUQURUCUWFUWKUSUOHUTSZIUTSZUWNUWDUHUWHUWIUWTUXATZUWLIUWMHUA
      UXAUWLIVAZUWTUXAUCUTSZUXCVBIUCVCUPZUQUXBUWMUWEUWLUFUEZUWEIUFUEZHUXBUWKUTS
      ZUWEUTSZUXDUIUWMUXFVAUXBUXHUXIUXDUXAUXHUWTUXAUXDUXHVBIUCVDUPUQHIVDZUXDUXB
      VBUKZURUWKUWEUCVEUOUXAUXFUXGVAUWTUXAUWLIUWEUFUXEVFUQHIVCZVGVHHVIIVIZVJVKH
      VLIVLZVJUWFRSZUWCUWGEVMUWCUWERSZUXOHIVNUXPUCRSZUXOVOUWEUCVPUPUOUXOUWGUWCE
      UWGUXOUWCEVMZUXOUWGUXOUXRVMZUXOUWGTUWFVQSUXSUWFVRPVSZRSZUWCAFUXTUCUDUEZIU
      FUEZVTZVMZVMZUXQUWCBVMZVMQVSZRSZUWCCGUYHUCUDUEZIUFUEZVTZVMZVMUYHUCUFUEZRS
      ZUWCDGUYNUCUDUEZIUFUEZUCUDUEZVTZVMZVMZUXSPQUWFUXTUCVAZUYAUXQUYEUYGUXTUCRW
      AVUBUWCUYDBVUBUWBUYDBUHZUWAVUBUWBVUCFVSZUYCVAZFWBZVUBUWBTZVUCVMZFUYCUYBIU
      FWCZWDZVUEVUHFVUGVUCFVUGFWEUYDBFAFUYCVUIWFZBFWEWGWHVUEVUGVUCVUEVUGTZAUYDB
      VUEAUYDUHZVUGAFUYCWIZWJVULVUDIVAZABUHVUEUYCIVAVUOVUGVUDUYCIWKVUBUWBUYCUCU
      CUDUEZIUFUEZIVUBUYBVUPIUFUXTUCUCUDWLWMUWBWNIUFUEZIVUQUWBUXAVURIVAIWOZIWPU
      OVUPWNIUFUCVBWQWRWSWTXAJUOXBXCXDXEXCXFXGXHPQXIZUYAUYIUYEUYMUXTUYHRWAVUTUY
      DUYLUWCVUFGVSZUYKVAZGWBZVUTUYDUYLUHZVMZVUJGUYKUYJIUFWCZWDZVUFVVCTVUEVVBTZ
      GWBZFWBVVEVUEVVBFGXJVVIVVEFVUTVVDFVUTFWEUYDUYLFVUKUYLFWEWGWHVVHVVEGVUTVVD
      GVUTGWEUYDUYLGUYDGWECGUYKVVFWFZWGWHVVHVUTACUHZVVDVVHVUTFGXIZVVKVVHVVLUYCU
      YKVAVUTVUDUYCVVAUYKXKVUTUYBUYJIUFUXTUYHUCUDWLWMXLKXMVUEAUYDVVBCUYLVUNCGUY
      KWIZXNXOXDXDXPXQXRXHUXTUYNVAZUYAUWCAFUYCUCUDUEZUCUFUEZVTZVMZVMVUAUYFVVNUY
      AUYOVVRUYTUXTUYNRWAVVNVVQUYSUWCVUDVVPVAZFWBZVVAUYRVAZGWBZVVNVVQUYSUHZVMZF
      VVPVVOUCUFWCZWDGUYRUYQUCUDWCZWDVVTVWBTVVSVWATZGWBZFWBVWDVVSVWAFGXJVWHVWDF
      VVNVWCFVVNFWEVVQUYSFAFVVPVWEWFUYSFWEWGWHVWGVWDGVVNVWCGVVNGWEVVQUYSGVVQGWE
      DGUYRVWFWFWGWHVWGVVNADUHZVWCVWGVVNVUDVVAUCUFUEZVAZVWIVWGVWKVVPUYRUCUFUEZV
      AZVVNVVSVWJVWLVAVWKVWMUHVWAVUDVVPVWJVWLXKVVAUYRUCUFWLXAVVNVVOUYRUCUFVVNUY
      CUYQUCUDVVNUYBUYPIUFUXTUYNUCUDWLWMWMWMXLLXMVVSAVVQVWADUYSAFVVPWIDGUYRWIXN
      XOXDXDXPXQXRXHUYAVVRUYEUYAUWCVVQUYDUYAUWCVVPUYCVAZVVQUYDUHUYAUWBVWNUWAUYA
      UWBVWNUXTUTSZUXAVWNUYAUWBVWOUXATUYCUTSZVWNUYBUTSZUXAVWPVWOUYBIXSVWOUXDVWQ
      VBUXTUCVDUPXTVWPUXDVWNVBUYCUCVCUPUOUXTWOVUSVJXCXFAFVVPUYCYAXMXGYBYCUXTUWF
      VAZUYAUXOUYEUXRUXTUWFRWAVWRUWCUYDEVWRUWCUYDEUHZVUFVWRUWCTZVWSVMZVUJVUEVXA
      FVWTVWSFVWTFWEUYDEFVUKEFWEWGWHVUEVWTVWSVUEVWTTZAUYDEVUEVUMVWTVUNWJVXBVUDH
      VAZAEUHVUEVWRUWCVXCVUEVWRTUWCVUDUWFUCUDUEZIUFUEZHVUEUYCVXEVAVUDVXEVAVWRVU
      DUYCVXEWKVWRUYBVXDIUFUXTUWFUCUDWLWMXAUWTUXAVXEHVAUWAUWBUXBUWFIUFUEZUCUDUE
      ZHUCUFUEZUCUDUEZVXEHUXBVXFVXHUCUDUXBVXFUXGUCUFUEZVXHUXBUXIUXDUXAUIVXFVXJV
      AUXBUXIUXDUXAUXJUXKUWTUXAYDZURUWEUCIYEUOUXBUXGHUCUFUXLWMYFWMUXBUWFUTSZUXA
      UXDUIVXGVXEVAUXBVXLUXAUXDUXBUXIVXLUXJUXIUXDVXLVBUWEUCXSUPUOVXKUXKURUWFIUC
      YGUOUWTVXIHVAZUXAUWTUXDVXMVBHUCYHUPWJYIHWOVUSVJWTYJMUOXBXCXDXEXCXGXHUYGUX
      QBUWCNUKUKUYHVQSZUYOUYIUYMUYTVXNUYIUYOUYHYKZYLVXNUWCUYLUYSVXNUWBUYLUYSVMZ
      UWAVXNUWBVXPVXNUWBTZUYLDGUYKVTZUYSVVCVXQUYLVXRVMZVMZVVGVVBVXTGVXQVXSGVXQG
      WEUYLVXRGVVJDGUYKVVFWFWHWHVVBVVARSZUWBTZIVVAUAUBZTZCDVMZVMVXTOVVBVXQVYDVY
      EVXSVVBVYDUYKRSZUWBTZIUYKUAUBZTVXQVVBVYBVYGVYCVYHVVBVYAVYFUWBVVAUYKRWAYMV
      VAUYKIUAYNYOVXQVYGVYHVXQVYFUWBUYIUWBVYFVXNUYJRSZUWBVYFUYIUYJIVPUYIUXQVYIV
      OUYHUCVNUPXTVXOXTVXNUWBYDYPUYIUCUYHUAUBZTUWBVYHVXNUYIUWBVYJVYHUYIUWBTVYHV
      YJUYHUGSZUWIVYHVYJUHUYIUWBVYKUWITZVYHIUWKUDUEZUYKUWKUDUEZUAUBZVYJVYLUWIUY
      KUGSZUWQUIVYHVYOUHVYLUWIVYPUWQVYKUWIYDUYJUGSZUWIVYPVYKUYJIUNVYKUWOVYQUJUY
      HUCULUPXTUWIUWQVYKUWSUQURIUYKUWKYQUOUYHUTSZUXAVYOVYJUHVYKUWIVYRUXATZVYMUC
      VYNUYHUAUXAVYMUCVAVYRUXAVYMIIUDUEZUCUFUEZUCUXAVYMWUAVAZUXAUXAUXDWUBVBIIUC
      YRYSYTUXAWUAWNUCUFUEUCUXAVYTWNUCUFIUVAWMUCVBUVBUVCYFUQVYSVYNUYKIUDUEZUCUF
      UEZUYJUCUFUEZUYHUYKUTSZUXAVYNWUDVAZVYSWUFUXAUXDWUGVBUYKIUCYRYSUYJUTSZUXAW
      UFVYRUYJIXSVYRUXDWUHVBUYHUCVDUPZXTVYRUXAYDUVDVYSWUCUYJUCUFWUHUXAWUCUYJVAV
      YRUYJIYHWUIXTWMVYRWUEUYHVAZUXAVYRUXDWUJVBUYHUCVCUPWJVGVHUYHVIUXMVJUVEUYHV
      LUXNVJUVFUVGUYHVRUVHYPXLVVBVYEVXSVVBCUYLDVXRVVMDGUYKWIXHUVIUVJUVKXDXEVXQU
      YRUYKVAZUYSVXRUHVYRUXAWUKVXNUWBVYSUYRUYPUCUDUEZIUFUEZUYKUYPUTSZUXAUYRWUMV
      AZVYRWUNUXAUXDWUOVBUYPIUCYGYSVYRUYNUTSZWUNVYRUXDWUPVBUYHUCXSUPWUPUXDWUNVB
      UYNUCVDUPUOXTVYRWUMUYKVAUXAVYRWULUYJIUFVYRUYPUYHUCUDVYRUXDUYPUYHVAVBUYHUC
      YHUPWMWMWJYFUYHUVLVUSVJDGUYRUYKYAUOUVMXCXFUVNUVJUVOXPXCUVPUVQUVRUVSUVT $.
      $( [18-Jun-04] $) $( [11-May-04] $)
  $}

  ${
    $d x y $.  $d A x $.  $d ps x $.  $d ch x $.  $d th x $.  $d ta x $.
    $d ph y $.
    nn0ind.1 $e |- ( x = 0 -> ( ph <-> ps ) ) $.
    nn0ind.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    nn0ind.3 $e |- ( x = ( y + 1 ) -> ( ph <-> th ) ) $.
    nn0ind.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    nn0ind.5 $e |- ps $.
    nn0ind.6 $e |- ( y e. NN0 -> ( ch -> th ) ) $.
    $( Principle of Mathematical Induction (inference schema) on nonnegative
       integers.  The first four hypotheses give us the substitution instances
       we need; the last two are the basis and the induction hypothesis.
       (Contributed by Raph Levien, 10-Apr-04.) $)
    nn0indNEW $p |- ( A e. NN0 -> ta ) $=
      ( cn0 wcel cz cc0 cle wbr wa elnn0z 0z cv wi sylbir adantlr zind mpan12
      sylbi ) HOPHQPZRHSTZUAEHUBUKRQPZULEUCABCDEFGHRIJKLMGUDZQPZRUNSTZCDUEZUMUO
      UPUAUNOPUQUNUBNUFUGUHUIUJ $.
      $( [?] $) $( [13-May-04] $)
  $}

  ${
    $d x y z $.  $d A x z $.  $d ps x z $.  $d ch x z $.  $d th x z $.
    $d ta x z $.  $d ph y z $.
    nn0ind.1 $e |- ( x = 0 -> ( ph <-> ps ) ) $.
    nn0ind.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    nn0ind.3 $e |- ( x = ( y + 1 ) -> ( ph <-> th ) ) $.
    nn0ind.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    nn0ind.5 $e |- ps $.
    nn0ind.6 $e |- ( y e. NN0 -> ( ch -> th ) ) $.
    $( Principle of Mathematical Induction (inference schema) on nonnegative
       integers.  The first four hypotheses give us the substitution instances
       we need; the last two are the basis and the induction hypothesis.
       (Contributed by Raph Levien, 10-Apr-04.  Raph says: "This seems a bit
       painful.  I wonder if an explicit substitution version would be
       easier.") $)
    nn0ind $p |- ( A e. NN0 -> ta ) $=
      ( vz cn0 wcel cn cc0 wceq wo elnn0 wsb c1 wsbc cv dfsbcq vsbcint caddc
      co cr ax1re elisseti hbsbcv wi 0nn0 wa eleq1a ax-mp mpbiri weq wb cleq2
      syl6bir pm5.74d mpbii com12 vtocle syl13 adantr opreq1 1cn addid2 syl6eq
      cleq2d imp mpbird exp sbceq1 mpbid vtoclef nnnn0t syl ind ax-17 hbim
      cleq1 bicomd sylan9bb sylbird cleqcoms jaoi sylbi ) HPQHRQZHSTZUAEHUBWNEW
      OAFOUCAFUDUEZCDEOGHAFOUFUDUGACFOGUFZJUHADFOWQUDUIUJZKUHAEFOHLUHWPFUDAFUDU
      DUKULUMZUNWSFUFZUDTZAWPXAAUOGSSPUPUMZWQSTZXAAXCXAUQADXCDXAWQPQZCDXCNSPQXC
      XDUOUPSPWQURUSXCCUOFSXBXCWTSTZCXCXEAUOXECUOXEABMIUTXCXEACXCXEFGVAACVBWQSW
      TVCJVDVEVFVGVHVIVJXCXAADVBZXCXAWTWRTXFXCWRUDWTXCWRSUDUIUJUDWQSUDUIVKUDVLV
      MVNVOKVDVPVQVRVHAFUDVSVTWAWQRQXDCDUOWQWBNWCWDESHSHTZEUOFSXGEFXGFWEEFWEWFX
      BXEXGWTHTZEWTSHWGXEXHEXEXHUQBEMXEBAXHEXEABIWHLWIVFVRWJWAWKWLWM $.
      $( [25-May-04] $) $( [25-May-04] $)
  $}


