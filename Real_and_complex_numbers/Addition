$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Addition
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Commutative/associative law that swaps the first two terms in a triple
     sum. $)
  add12 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( A + ( B + C ) ) = ( B + ( A + C ) ) ) $=
    ( cc wcel w3a caddc co wceq wa addcom opreq1d 3adant3 addass 3com12
    3eqtr3d ) ADEZBDEZCDEZFABGHZCGHZBAGHZCGHZABCGHGHBACGHGHZQRUAUCISQRJTUBCGABK
    LMABCNRQSUCUDIBACNOP $.
    $( [11-May-2004] $)

  $( Commutative/associative law that swaps the last two terms in a triple
     sum. $)
  add23 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A + B ) + C ) = ( ( A + C ) + B ) ) $=
    ( cc wcel w3a caddc co wceq wa addcom opreq2d 3adant1 addass 3com23
    3eqtr4d ) ADEZBDEZCDEZFABCGHZGHZACBGHZGHZABGHCGHACGHBGHZRSUAUCIQRSJTUBAGBCK
    LMABCNQSRUDUCIACBNOP $.
    $( [13-Nov-1999] $)

  $( Rearrangement of 4 terms in a sum.  (The proof was shortened by Andrew
     Salmon, 22-Oct-2011.) $)
  add4 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A + B ) + ( C + D ) ) = ( ( A + C ) + ( B + D ) ) ) $=
    ( cc wcel wa caddc co wceq add12 3expb opreq2d adantll addass 3expa addcl
    sylan2 an4s 3eqtr4d ) AEFZBEFZGZCEFZDEFZGZGABCDHIZHIZHIZACBDHIZHIZHIZABHIUG
    HIZACHIUJHIZUBUFUIULJUAUBUFGUHUKAHUBUDUEUHUKJBCDKLMNUCUGEFZUMUIJZUFUAUBUOUP
    ABUGOPCDQRUAUDUBUEUNULJZUAUDGUJEFZUQUBUEGUAUDURUQACUJOPBDQRST $.
    $( [22-Oct-2011] $) $( [13-Nov-1999] $)

  $( Rearrangement of 4 terms in a sum. $)
  add4OLD $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A + B ) + ( C + D ) ) = ( ( A + C ) + ( B + D ) ) ) $=
    ( cc wcel wa caddc co wceq w3a add23 opreq1d 3expa adantrr addass 3expb
    addcl sylan an4s 3eqtr3d ) AEFZBEFZGZCEFZDEFZGZGABHIZCHIZDHIZACHIZBHIZDHIZU
    HCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOUHEFZUGUJUNJZUDUQU
    EUFURUHCDPQABRSUBUEUCUFUMUOJZUKEFZUCUFGUSUBUEGUTUCUFUSUKBDPQACRSTUA $.
    $( [13-Nov-1999] $)

  $( Rearrangement of 4 terms in a sum. $)
  add42 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A + B ) + ( C + D ) ) = ( ( A + C ) + ( D + B ) ) ) $=
    ( cc wcel wa caddc co add4 wceq addcom ad2ant2l opreq2d eqtrd ) AEFZBEFZGCE
    FZDEFZGGZABHICDHIHIACHIZBDHIZHIUADBHIZHIABCDJTUBUCUAHQSUBUCKPRBDLMNO $.
    $( [12-May-2005] $)

  ${
    add.1 $e |- A e. CC $.
    add.2 $e |- B e. CC $.
    add.3 $e |- C e. CC $.
    $( Commutative/associative law that swaps the first two terms in a triple
       sum. $)
    add12i $p |- ( A + ( B + C ) ) = ( B + ( A + C ) ) $=
      ( cc wcel caddc co wceq add12 mp3an ) AGHBGHCGHABCIJIJBACIJIJKDEFABCLM $.
      $( [21-Jan-1997] $)

    $( Commutative/associative law that swaps the last two terms in a triple
       sum. $)
    add23i $p |- ( ( A + B ) + C ) = ( ( A + C ) + B ) $=
      ( cc wcel caddc co wceq add23 mp3an ) AGHBGHCGHABIJCIJACIJBIJKDEFABCLM $.
      $( [21-Jan-1997] $)
  $}

  ${
    add4.1 $e |- A e. CC $.
    add4.2 $e |- B e. CC $.
    add4.3 $e |- C e. CC $.
    add4.4 $e |- D e. CC $.
    $( Rearrangement of 4 terms in a sum. $)
    add4i $p |- ( ( A + B ) + ( C + D ) ) = ( ( A + C ) + ( B + D ) ) $=
      ( cc wcel wa caddc co wceq pm3.2i add4 mp2an ) AIJZBIJZKCIJZDIJZKABLMCDLM
      LMACLMBDLMLMNRSEFOTUAGHOABCDPQ $.
      $( [9-May-1999] $)

    $( Rearrangement of 4 terms in a sum. $)
    add42i $p |- ( ( A + B ) + ( C + D ) ) = ( ( A + C ) + ( D + B ) ) $=
      ( caddc co add4i addcomi opreq2i eqtri ) ABIJCDIJIJACIJZBDIJZIJODBIJZIJAB
      CDEFGHKPQOIBDFHLMN $.
      $( [22-Aug-1999] $)
  $}

  $( A theorem for complex numbers analogous the second Peano postulate
     ~ peano2nn . $)
  peano2cn $p |- ( A e. CC -> ( A + 1 ) e. CC ) $=
    ( cc wcel c1 caddc co ax1cn addcl mpan2 ) ABCDBCADEFBCGADHI $.
    $( [17-Aug-2005] $)

