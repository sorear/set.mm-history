$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Addition
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.  $d x C $.
    addcan.1 $e |- A e. CC $.
    addcan.2 $e |- B e. CC $.
    addcan.3 $e |- C e. CC $.
    $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18. $)
    addcan $p |- ( ( A + B ) = ( A + C ) <-> B = C ) $=
      ( vx caddc co wceq cv cc0 cc wrex wi negex wcel wa wb axaddass mp3an3
      eqeq12d mpan2 opreq2 syl5bir adantr axaddcom mpan eqeq1d opreq1 addid2
      syl6eq syl6bi imp sylibd r19.23aiva ax-mp impbi ) ABHIZACHIZJZBCJZAGKZHIZ
      LJZGMNVAVBOZGADPVEVFGMVCMQZVERVAVCAHIZBHIZVHCHIZJZVBVGVAVKOVEVGVKVCUSHIZV
      CUTHIZJZVAVGAMQZVKVNSDVGVORVIVLVJVMVGVOBMQVIVLJEVCABTUAVGVOCMQVJVMJFVCACT
      UAUBUCUSUTVCHUDUEUFVGVEVKVBSZVGVEVHLJZVPVGVDVHLVOVGVDVHJDAVCUGUHUIVQVIBVJ
      CVQVILBHIBVHLBHUJBEUKULVQVJLCHICVHLCHUJCFUKULUBUMUNUOUPUQBCAHUDUR $.
      $( [27-Oct-1999] $)

    $( Cancellation law for addition. $)
    addcan2 $p |- ( ( A + C ) = ( B + C ) <-> A = B ) $=
      ( caddc co wceq addcom eqeq12i addcan bitr ) ACGHZBCGHZICAGHZCBGHZIABINPO
      QACDFJBCEFJKCABFDELM $.
      $( [14-May-2003] $)
  $}

  $( Cancellation law for addition.  Theorem I.1 of [Apostol] p. 18.  This
     proof illustrates how ~ dedth3h can be used to convert
     the assumptions of ~ addcan into antecedents.  This general
     method can be used to convert deductions into theorems as needed. $)
  addcant $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) = ( A + C ) <-> B = C ) ) $=
    ( cc wcel caddc co wceq wb cc0 cif opreq1 eqeq12d bibi1d opreq2 eqeq1d
    eqeq1 bibi12d eqeq2d eqeq2 0cn elimel addcan dedth3h ) ADEZBDEZCDEZABFGZACF
    GZHZBCHZIUEAJKZBFGZULCFGZHZUKIABCJJJULUFBJKZFGZUNHZUPCHZIUQULUGCJKZFGZHZUPU
    THZIAULHZUJUOUKVDUHUMUIUNAULBFLAULCFLMNBUPHZUOURUKUSVEUMUQUNBUPULFOPBUPCQRC
    UTHZURVBUSVCVFUNVAUQCUTULFOSCUTUPTRULUPUTAJDUAUBBJDUAUBCJDUAUBUCUD $.
    $( [22-Nov-1994] $)

  $( Cancellation law for addition. $)
  addcan2t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + C ) = ( B + C ) <-> A = B ) ) $=
    ( cc wcel caddc co wceq wb w3a axaddcom 3adant3 3adant2 eqeq12d addcant
    bitr3d 3coml ) CDEZADEZBDEZACFGZBCFGZHZABHZIRSTJZCAFGZCBFGZHUCUDUEUFUAUGUBR
    SUFUAHTCAKLRTUGUBHSCBKMNCABOPQ $.
    $( [23-Aug-2004] $) $( [30-Jul-2004] $)

  $( Commutative/associative law that swaps the first two terms in a triple
     sum. $)
  add12t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( A + ( B + C ) ) = ( B + ( A + C ) ) ) $=
    ( cc wcel w3a caddc co wceq wa axaddcom opreq1d 3adant3 axaddass 3com12
    3eqtr3d ) ADEZBDEZCDEZFABGHZCGHZBAGHZCGHZABCGHGHBACGHGHZQRUAUCISQRJTUBCGABK
    LMABCNRQSUCUDIBACNOP $.
    $( [13-May-2004] $) $( [11-May-2004] $)

  $( Commutative/associative law that swaps the last two terms in a triple
     sum. $)
  add23t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A + B ) + C ) = ( ( A + C ) + B ) ) $=
    ( cc wcel w3a caddc co wceq wa axaddcom opreq2d 3adant1 axaddass 3com23
    3eqtr4d ) ADEZBDEZCDEZFABCGHZGHZACBGHZGHZABGHCGHACGHBGHZRSUAUCIQRSJTUBAGBCK
    LMABCNQSRUDUCIACBNOP $.
    $( [13-Nov-1999] $)

  $( Rearrangement of 4 terms in a sum. $)
  add4t $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A + B ) + ( C + D ) ) = ( ( A + C ) + ( B + D ) ) ) $=
    ( cc wcel wa caddc co wceq w3a add23t opreq1d 3expa adantrr axaddass 3expb
    axaddcl sylan an4s 3eqtr3d ) AEFZBEFZGZCEFZDEFZGZGABHIZCHIZDHIZACHIZBHIZDHI
    ZUHCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOUHEFZUGUJUNJZUDU
    QUEUFURUHCDPQABRSUBUEUCUFUMUOJZUKEFZUCUFGUSUBUEGUTUCUFUSUKBDPQACRSTUA $.
    $( [13-Nov-1999] $)

  $( Rearrangement of 4 terms in a sum. $)
  add42t $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A + B ) + ( C + D ) ) = ( ( A + C ) + ( D + B ) ) ) $=
    ( cc wcel wa caddc co add4t wceq axaddcom ad2ant2l opreq2d eqtrd ) AEFZBEFZ
    GCEFZDEFZGGZABHICDHIHIACHIZBDHIZHIUADBHIZHIABCDJTUBUCUAHQSUBUCKPRBDLMNO $.
    $( [15-May-2005] $) $( [12-May-2005] $)

  ${
    add.1 $e |- A e. CC $.
    add.2 $e |- B e. CC $.
    add.3 $e |- C e. CC $.
    $( Commutative/associative law that swaps the first two terms in a triple
       sum. $)
    add12 $p |- ( A + ( B + C ) ) = ( B + ( A + C ) ) $=
      ( cc wcel caddc co wceq add12t mp3an ) AGHBGHCGHABCIJIJBACIJIJKDEFABCLM
      $.
      $( [21-Jan-1997] $)

    $( Commutative/associative law that swaps the last two terms in a triple
       sum. $)
    add23 $p |- ( ( A + B ) + C ) = ( ( A + C ) + B ) $=
      ( cc wcel caddc co wceq add23t mp3an ) AGHBGHCGHABIJCIJACIJBIJKDEFABCLM
      $.
      $( [21-Jan-1997] $)
  $}

  ${
    add4.1 $e |- A e. CC $.
    add4.2 $e |- B e. CC $.
    add4.3 $e |- C e. CC $.
    add4.4 $e |- D e. CC $.
    $( Rearrangement of 4 terms in a sum. $)
    add4 $p |- ( ( A + B ) + ( C + D ) ) = ( ( A + C ) + ( B + D ) ) $=
      ( cc wcel wa caddc co wceq pm3.2i add4t mp2an ) AIJZBIJZKCIJZDIJZKABLMCDL
      MLMACLMBDLMLMNRSEFOTUAGHOABCDPQ $.
      $( [9-May-1999] $)

    $( Rearrangement of 4 terms in a sum. $)
    add42 $p |- ( ( A + B ) + ( C + D ) ) = ( ( A + C ) + ( D + B ) ) $=
      ( caddc co add4 addcom opreq2i eqtr ) ABIJCDIJIJACIJZBDIJZIJODBIJZIJABCDE
      FGHKPQOIBDFHLMN $.
      $( [22-Aug-1999] $)
  $}

  $( Identity law for addition. $)
  addid2t $p |- ( A e. CC -> ( 0 + A ) = A ) $=
    ( cc wcel cc0 caddc co wceq 0cn axaddcom mpan ax0id eqtrd ) ABCZDAEFZADEFZA
    DBCMNOGHDAIJAKL $.
    $( [10-Aug-1999] $)

  $( A theorem for complex numbers analogous the second Peano postulate
     ~ peano2nn . $)
  peano2cn $p |- ( A e. CC -> ( A + 1 ) e. CC ) $=
    ( cc wcel c1 caddc co ax1cn axaddcl mpan2 ) ABCDBCADEFBCGADHI $.
    $( [18-Aug-2005] $) $( [17-Aug-2005] $)

