$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Infinite sums (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d m n x y A $.  $d m n x y M $.  $d m n x y k $.
    $( Series sum with an infinite index set (i.e. an infinite series). $)
    dfisum $p |- ( M e. ZZ -> sum_ k e. ( ZZ>= ` M ) A =
       U. { x |
    ( <. M , + >. seq ( { <. k , y >. | y = A } |` ZZ ) ) ~~> x } ) $=
      ( vm vn cz wcel cuz cfv cv cfz co wceq caddc cop copab cres cseqz wa
      wrex wex cab cli wbr cuni cun csu c0 wn fzneuz eqcom notbii sylib ancoms
      intnanrd nrexdv nexdv abn0 necon1bbii uz11 syl6bb anbi1d rexbidv opeq1
      opreq1d breq1d ceqsrexv bitrd abbidv unieqd uneq12d uncom un0 eqtri
      syl6eq df-sum syl5eq ) EHIZEJKZFLZGLZMNZOZALZWCWBPQZBLCODBRHSZTNZKIZUAZGW
      BJKZUBZFUCZAUDZWAWLOZWIWFUEUFZUAZFHUBZAUDZUGZUHZEPQZWHTNZWFUEUFZAUDZUGZWA
      CDUIVTXBUJXGUHZXGVTWOUJXAXGVTWNAUCZUKWOUJOVTWNAVTWMFVTWKGWLVTWCWLIZUAWEWJ
      XJVTWEUKZXJVTUAWDWAOZUKXKEWBWCULXLWEWDWAUMUNUOUPUQURUSUSXIWOUJWNAUTVAUOVT
      WTXFVTWSXEAVTWSWBEOZWQUAZFHUBXEVTWRXNFHVTWPXMWQVTWPEWBOXMEWBVBEWBUMVCVDVE
      WQXEFEHXMWIXDWFUEXMWGXCWHTWBEPVFVGVHVIVJVKVLVMXHXGUJUHXGUJXGVNXGVOVPVQABW
      ACDFGVRVS $.
      $( [25-Dec-2005] $) $( [11-Dec-2005] $)
  $}

  ${
    $d k x y F $.  $d x y M $.
    isumvalt.1 $e |- F e. _V $.
    $( The value of the sum of a series ` F ` from ` M ` to infinity is the
       unique (by ~ climuni ) value it converges to (if it converges).  The sum
       evaluates to the empty set when it doesn't converge, but this shouldn't
       be depended on to indicate non-convergence (because it would presuppose
       a construction-dependent property of complex numbers, i.e. that no
       complex number equals the empty set, which, although by ~ 0ncn is true
       for our specific construction, is not specified by their axioms and may
       fail for some other construction).  Instead, use
       ` -. E. x e. CC ( <. M , + >. seq F ) ~~> x ` to indicate
       non-convergence. $)
    isumval $p |- ( M e. ZZ ->
            sum_ k e. ( ZZ>= ` M ) ( F ` k ) =
                   U. { x | ( <. M , + >. seq F ) ~~> x } ) $=
      ( vy cz wcel cuz cfv cv csu caddc cop wceq copab cres cseqz co cli wbr
      cab cuni dfisum addex seqzres2 breq1d abbidv unieqd eqtrd ) DGHZDIJBKCJZB
      LDMNZFKULOBFPGQRSZAKZTUAZAUBZUCUMCRSZUOTUAZAUBZUCAFULBDUDUKUQUTUKUPUSAUKU
      NURUOTFMBCDUEEUFUGUHUIUJ $.
      $( [28-Dec-2005] $) $( [11-Dec-2005] $)
  $}

  ${
    $d j x y z F $.  $d x z M $.  $d j k y z $.
    isumvaltf.1 $e |- F e. _V $.
    isumvaltf.2 $e |- ( y e. F -> A. k y e. F ) $.
    $( Version of ~ isumval with a bound-variable hypothesis instead of a
       distinct variable condition. $)
    isumvaltfi $p |- ( M e. ZZ ->
            sum_ k e. ( ZZ>= ` M ) ( F ` k ) =
                   U. { x | ( <. M , + >. seq F ) ~~> x } ) $=
      ( vj vz cz wcel cuz cfv cv csu caddc cop cseqz co cli wbr cab cuni
      isumval ax-17 hblem hbfv fveq2 cbvsumi syl5eq ) EJKELMZHNZDMZHOEPQDRSANTU
      AAUBUCUKCNZDMZCOAHDEFUDIUKUOUMCHINZUOKHUECIULDCBIDGUFUPULKCUEUGUNULDUHUIU
      J $.
      $( [12-May-2006] $) $( [13-Dec-2005] $)
  $}

  ${
    $d y A $.  $d x z F $.  $d k x y z M $.
    isumval2t.1 $e |- A e. _V $.
    isumval2t.2 $e |- F = { <. k , y >. | ( k e. ( ZZ>= ` M ) /\ y = A ) } $.
    $( Sum of ` A ( k ) ` from ` M ` to infinity. $)
    isumval2 $p |- ( M e. ZZ ->
            sum_ k e. ( ZZ>= ` M ) A =
                   U. { x | ( <. M , + >. seq F ) ~~> x } ) $=
      ( vz cz wcel cuz cfv cv csu caddc cop cseqz co cli wbr cab cuni fvex
      fopabex2 wceq wa copab hbopab1 hbxfr isumvaltfi cvv fvopab2 mpan2 fveq1i
      syl5eq sumeq2i syl5eqr ) FJKFLMZDNZEMZDOFPQERSANTUAAUBUCUSCDOAIDEFDBUSCEF
      LUDHUEDIEUTUSKZBNCUFUGZDBUHZHVCDBIUIUJUKUSVACDVBUTVDMZCVAVBCULKVECUFGDBUS
      CULUMUNUTEVDHUOUPUQUR $.
      $( [31-Mar-2008] $) $( [2-Jan-2006] $)
  $}

  ${
    $d x A $.  $d x y F $.  $d x M $.  $d k y $.
    isumclimtf.1 $e |- ( y e. F -> A. k y e. F ) $.
    isumclimtf.2 $e |- F e. _V $.
    isumclimtf.3 $e |- A e. _V $.
    $( Version of ~ isumclim with a bound-variable hypothesis instead of a
       distinct variable condition. $)
    isumclimtfi $p |- ( ( M e. ZZ /\ ( <. M , + >. seq F ) ~~> A ) ->
                   sum_ k e. ( ZZ>= ` M ) ( F ` k ) = A ) $=
      ( vx cz wcel caddc cop cseqz co cli wbr cuz cfv cv csu cvv crab cuni cab
      isumvaltfi rabab unieqi syl6eqr wceq wreu wb breq2 reuuni2 weu climeu wa
      df-reu visset biantrur eubii bitr4i sylibr sylancr ibi sylan9eq ) EJKZELM
      DNOZBPQZERSCTDSCUAZVHITZPQZIUBUCZUDZBVGVJVLIUEZUDVNIACDEGFUFVMVOVLIUGUHUI
      VIVNBUJZBUBKVLIUBUKZVIVPULVIVLVIIUBBVKBVHPUMUNHVIVLIUOZVQIBVHHUPVQVKUBKZV
      LUQZIUOVRVLIUBURVLVTIVSVLIUSUTVAVBVCVDVEVF $.
      $( [12-May-2006] $) $( [10-May-2006] $)
  $}

  ${
    $d x k F $.
    isumclimt.1 $e |- F e. _V $.
    isumclimt.2 $e |- A e. _V $.
    $( An infinite sum equals the value its series converges to. $)
    isumclim $p |- ( ( M e. ZZ /\ ( <. M , + >. seq F ) ~~> A ) ->
                   sum_ k e. ( ZZ>= ` M ) ( F ` k ) = A ) $=
      ( vx cv wcel ax-17 isumclimtfi ) GABCDGHCIBJEFK $.
      $( [10-Jan-2006] $) $( [25-Dec-2005] $)

    $( An infinite sum equals the value its series converges to. $)
    isum1clim $p |- ( ( + seq1 F ) ~~> A -> sum_ k e. NN ( F ` k ) = A ) $=
      ( caddc cseq1 co cli wbr c1 cuz cfv cv csu cn cop cseqz wceq addex
      seq1seqz breq1i cz wcel 1z isumclim mpan sylbi nnuz sumeq1i syl5eq ) FCGH
      ZAIJZKLMZBNCMZBOZAPUOBOUMKFQCRHZAIJZUPASZULUQAIFCTDUAUBKUCUDURUSUEABCKDEU
      FUGUHPUNUOBUIUJUK $.
      $( [5-Aug-2008] $) $( [10-Jul-2008] $)
  $}

  ${
    $d x y F $.  $d x M $.  $d k x y $.
    isumclim2tf.1 $e |- ( y e. F -> A. k y e. F ) $.
    isumclim2tf.2 $e |- F e. _V $.
    $( Version of ~ isumclim2 with a bound-variable hypothesis instead of a
       distinct variable condition. $)
    isumclim2f $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seq F ) ~~> x ) ->
              ( <. M , + >. seq F ) ~~> sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) $=
      ( cz wcel caddc cop cseqz co cv cli wbr wex cuz cfv csu wa pm3.27 visset
      isumclimtfi breqtrrd ex 19.23adv imp ) EHIZEJKDLMZANZOPZAQUJERSCNDSCTZOPZ
      UIULUNAUIULUNUIULUAUJUKUMOUIULUBBUKCDEFGAUCUDUEUFUGUH $.
      $( [17-Feb-2006] $) $( [9-Jan-2006] $)
  $}

  ${
    $d k x y F $.  $d x M $.
    isumclim2t.1 $e |- F e. _V $.
    $( A converging series converges to its infinite sum. $)
    isumclim2 $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seq F ) ~~> x ) ->
              ( <. M , + >. seq F ) ~~> sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) $=
      ( vy cv wcel ax-17 isumclim2f ) AFBCDFGCHBIEJ $.
      $( [10-Jan-2006] $) $( [2-Jan-2006] $)
  $}

  ${
    $d x y z A $.  $d k x y z M $.  $d z F $.
    isumclim3t.1 $e |- A e. _V $.
    isumclim3t.2 $e |- F = { <. k , y >. | ( k e. ( ZZ>= ` M ) /\ y = A ) } $.
    $( A converging series converges to its infinite sum. $)
    isumclim3 $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seq F ) ~~> x ) ->
                     ( <. M , + >. seq F ) ~~> sum_ k e. ( ZZ>= ` M ) A ) $=
      ( vz cz wcel caddc cop cseqz co cv cli wbr wex wa cuz cfv wceq copab csu
      opreq2i a1i eqcomd breq1d exbidv biimpar hbopab1 fvex opabex2 isumclim2f
      syldan cvv fvopab2 mpan2 sumeq2i syl6breq syl5eqbr ) FJKZFLMZENOZAPZQRZAS
      ZTZVDDPZFUAUBZKZBPCUCTZDBUDZNOZVKCDUEZVEQVIVOVKVJVNUBZDUEZVPQVCVHVOVFQRZA
      SZVOVRQRVCVTVHVCVSVGAVCVOVEVFQVCVEVOVEVOUCVCEVNVDNHUFZUGUHUIUJUKAIDVNFVMD
      BIULDBVKCFUAUMUNUOUPVKVQCDVLCUQKVQCUCGDBVKCUQURUSUTVAWAVB $.
      $( [17-Feb-2006] $) $( [9-Jan-2006] $)

    ${
      isumclim4t.3 $e |- B e. _V $.
      $( An infinite sum equals the value its series converges to. $)
      isumclim4 $p |- ( ( M e. ZZ /\ ( <. M , + >. seq F ) ~~> B ) ->
                     sum_ k e. ( ZZ>= ` M ) A = B ) $=
        ( vz cz wcel caddc cop cseqz co cli wbr wa cuz cfv cv csu wceq copab
        hbopab1 hbxfr fvex fopabex2 isumclimtfi wss ssid sumeqfv ax-mp syl5eqr
        ) FKLFMNEOPCQRSFTUAZDUBZEUADUCZCUPBDUCZJCDEFDJEUQUPLAUBBUDSZDAUEHUTDAJU
        FUGDAUPBEFTUHHUIIUJUPUPUKURUSUDUPULABUPUPDEGHUMUNUO $.
        $( [9-Jun-2006] $) $( [10-May-2006] $)
    $}
  $}

  ${
    $d j x y z A $.  $d j k x y z M $.
    isumclim5t.1 $e |- A e. _V $.
    isumclim5t.2 $e |- F = { <. j , y >. | ( j e. ( ZZ>= ` M ) /\ y =
                      sum_ k e. ( M ... j ) A ) } $.
    $( The sequence of partial finite sums of a converging infinite series
       converge to the infinite sum of the series.  Note that ` j ` must not
       occur in ` A ` . $)
    isumclim5 $p |- ( ( M e. ZZ /\ E. x F ~~> x ) ->
                     F ~~> sum_ k e. ( ZZ>= ` M ) A ) $=
      ( vz cz wcel cv cli wbr wex wa caddc cop cuz cfv wceq copab cseqz co csu
      eqid serzfsum adantr breq1d exbidv biimpar isumclim3 syldan eqbrtrrd ) GK
      LZFAMZNOZAPZQGRSEMGTUAZLJMCUBQEJUCZUDUEZFUTCEUFZNUPVBFUBUSJBCEDVAFGHVAUGZ
      IUHZUIUPUSVBUQNOZAPZVBVCNOUPVGUSUPVFURAUPVBFUQNVEUJUKULAJCEVAGHVDUMUNUO
      $.
      $( [17-Feb-2006] $) $( [9-Jan-2006] $)
  $}

  ${
    $d F k x $.  $d M x $.
    isumnul.1 $e |- F e. _V $.
    $( The sum of a non-convergent infinite series evaluates to the empty set.
       (Contributed by Paul Chapman, 04-Nov-2007.) $)
    isumnul $p |- ( ( M e. ZZ /\ -. E. x ( <. M , + >. seq F ) ~~> x ) ->
                  sum_ k e. ( ZZ>= ` M ) ( F ` k ) = (/) ) $=
      ( cz wcel caddc cop cseqz co cv cli wbr wex wn wa cuz cfv csu cab cuni
      c0 wceq isumval adantr wne abn0 biimpi necon1bi unieqd adantl uni0 a1i
      3eqtrd ) DFGZDHICJKALMNZAOZPZQZDRSBLCSBTZUQAUAZUBZUCUBZUCUPVAVCUDUSABCDEU
      EUFUSVCVDUDUPUSVBUCURVBUCVBUCUGURUQAUHUIUJUKULVDUCUDUTUMUNUO $.
      $( [4-Nov-2007] $)
  $}

  ${
    $d F k x $.  $d K k x $.  $d M k x $.  $d N k x $.
    isumshft.1 $e |- F e. _V $.
    isumshft.2 $e |- K e. ZZ $.
    isumshft.3 $e |- M e. ZZ $.
    isumshft.4 $e |- N = ( M + K ) $.
    $( Index shift of an infinite sum.  (Contributed by Paul Chapman,
       31-Oct-2007.) $)
    isumshfti $p |- sum_ k e. ( ZZ>= ` M ) ( F ` k ) =
                      sum_ k e. ( ZZ>= ` N ) ( F ` ( k - K ) ) $=
      ( vx caddc cop cseqz co cv cli wbr wex cuz cfv csu cmin wceq cz wcel
      visset isumclim mpan cshi cvv wb eqid iserzshfti ax-mp zaddcl mp2an
      eqeltri oprex sylbi cc shftval eluzelz zcn syl sylancr sumeq2i syl5eqr
      eqtr4d 19.23aiv wn c0 isumnul exbii notbii syl6eq pm2.61i ) DKLBMNJOZPQZJ
      RZDSTAOZBTAUAZESTZVTCUBNBTZAUAZUCZVRWEJVRWAVQWDDUDUEZVRWAVQUCHVQABDFJUFZU
      GUHVRWBVTBCUINZTZAUAZVQWDVREKLWHMNVQPQZWJVQUCZVQUJUEVRWKUKWGVQUJBWHCDEFHG
      IWHULUMUNZEUDUEZWKWLEDCKNZUDIWFCUDUEZWOUDUEHGDCUOUPUQZVQAWHEBCUIURZWGUGUH
      USWBWIWCAWPVTUTUEZWIWCUCVTWBUEZCVTUDBFVAGWTVTUDUEWSEVTVBVTVCVDVEVFZVGVHVI
      VSVJZWAWJWDXBWAVKWJWFXBWAVKUCHJABDFVLUHXBWKJRZVJZWJVKUCZVSXCVRWKJWMVMVNWN
      XDXEWQJAWHEWRVLUHUSVHXAVOVP $.
      $( [4-Nov-2007] $) $( [31-Oct-2007] $)

    $( Index shift of an infinite sum.  (Contributed by Paul Chapman,
       31-Oct-2007.) $)
    isumshft2i $p |- sum_ k e. ( ZZ>= ` N ) ( F ` k ) =
                       sum_ k e. ( ZZ>= ` M ) ( F ` ( K + k ) ) $=
      ( vx caddc cop cseqz co cv cli wbr wex cuz cfv csu wceq cz wcel zaddcl
      mp2an eqeltri visset isumclim mpan cneg cshi cvv wb znegcl ax-mp cmin cc
      zcn negsubi eqcomi subadd2i mpbir eqtr2i eqid iserzshfti oprex sylbi
      shftval4 eluzelz syl sylancr sumeq2i syl5eqr eqtr4d 19.23aiv wn c0
      isumnul exbii notbii syl6eq pm2.61i ) EKLBMNJOZPQZJRZESTAOZBTAUAZDSTZCWGK
      NBTZAUAZUBZWEWLJWEWHWDWKEUCUDZWEWHWDUBEDCKNZUCIDUCUDZCUCUDZWNUCUDHGDCUEUF
      UGZWDABEFJUHZUIUJWEWIWGBCUKZULNZTZAUAZWDWKWEDKLWTMNWDPQZXBWDUBZWDUMUDWEXC
      UNWRWDUMBWTWSEDFWQWPWSUCUDGCUOUPEWSKNECUQNZDECWMEURUDWQEUSUPZWPCURUDZGCUS
      UPZUTXEDUBWNEUBEWNIVAECDXFXHWODURUDHDUSUPVBVCVDWTVEVFUPZWOXCXDHWDAWTDBWSU
      LVGZWRUIUJVHWIXAWJAXGWGURUDZXAWJUBWGWIUDZCWGBFVIXHXLWGUCUDXKDWGVJWGUSVKVL
      VMZVNVOVPWFVQZWHXBWKXNWHVRXBWMXNWHVRUBWQJABEFVSUJXNXCJRZVQZXBVRUBZWFXOWEX
      CJXIVTWAWOXPXQHJAWTDXJVSUJVHVOXMWBWC $.
      $( [4-Nov-2007] $) $( [31-Oct-2007] $)
  $}

  ${
    $d j k n x F $.  $d j k n x M $.
    isum1p.1 $e |- F e. _V $.
    $( The infinite sum of a converging infinite series equals the first term
       plus the infinite sum of the rest of it. $)
    isum1p $p |- ( ( M e. ZZ /\ A. k e. ( ZZ>= ` M ) ( F ` k ) e. CC /\
  E. x ( <. ( M + 1 ) , + >. seq F ) ~~> x ) -> sum_ k e. ( ZZ>= ` M )
   ( F ` k ) = ( ( F ` M ) + sum_ k e. ( ZZ>= ` ( M + 1 ) ) ( F ` k ) ) ) $=
      ( vj vn cz wcel cv cfv cc cuz wral c1 caddc co cop cseqz cli wbr wex csu
      wceq wa isumclim2 peano2z sylan adantlr oprex isumclim simpll sumex fvex
      climaddc2 pm3.27 fveq2 eleq1d rcla4va uzid adantr ad2antrr cfz serzcl
      peano2uzr ex elfzuz syl5 imim1d ralimdv2 a1dd imp31 sylanc clt serz1p
      3expa eluzp1l jca an1rs imim1i ralimi2 sylanl2 r19.21aiva syl2anc syldan
      3impa cbvralv syl3an2b ) DHIZFJZCKZLIZFDMKZNZDOPQZPRZCSQZAJTUAAUBZWMBJZCK
      ZBUCDCKZWOMKZWTBUCZPQZUDZWTLIZBWMNWIWNWRXEWIWNUEZWRWQXCTUAZXEWIWRXHWNWOHI
      ZWRXHWIABCWOEUFDUGZUHUIWIDPRZCSQZXDTUAZXEXGXHUEZXDBCDEXAXCPUJUKWIWNXHULXH
      XALIZXIGJZWQKZLIZXPXLKXAXQPQUDZUEZGXBNZXMXNXCXAGWQXLWOWPCSUJXKCSUJXBWTBUM
      DCUNUOXGXHUPXGXOXHDWMIWNXOWIWLXOFDWMWJDUDWKXALWJDCUQURUSDUTUHVAWIXIWNXHXJ
      VBXGYAXHXGXTGXBXGXPXBIZUEZXRXSYBWLFWOXPVCQZNZXRYCFCWOXPEVDXGYBUPWIWNYBYEW
      IWNYEYBWIWLWLFWMYDWIWJYDIZWJWMIZWLWIWJXBIZYGYFWIYHYGDWJVEVFWJWOXPVGVHVIVJ
      VKVLVMWIWLFDXPVCQZNZYBXSWNWIYBYJXSXPWMIZDXPVNUAZUEYJXSWIYBUEZYKYLYJXSFCDX
      PEVOVPYMYKYLDXPVEDXPVQVRUHVSWLWLFWMYIWJYIIYGWLWJDXPVGVTWAWBVRWCVAWDVMWEWF
      XFWLBFWMWSWJUDWTWKLWSWJCUQURWGWH $.
      $( [10-Jan-2006] $) $( [2-Jan-2006] $)
  $}

  ${
    $d x k F $.
    isumnn0nn.1 $e |- F e. _V $.
    $( Sum from 0 to infinity in terms of sum from 1 to infinity. $)
    isumnn0nn $p |- ( ( A. k e. NN0 ( F ` k ) e. CC /\
  E. x ( + seq1 F ) ~~> x ) -> sum_ k e. NN0 ( F ` k )
                 = ( ( F ` 0 ) + sum_ k e. NN ( F ` k ) ) ) $=
      ( cv cfv cc wcel cn0 wral caddc cseq1 co cli wbr wex wa cc0 cuz csu c1
      cn cop cseqz wceq cz 0z isum1p mp3an1 wb nn0uz raleq ax-mp addex
      seq1seqz ax1cn addid2i opeq1i opreq1i eqtr4i breq1i exbii syl2anb
      sumeq1i nnuz fveq2i opreq2i 3eqtr4g ) BECFZGHZBIJZKCLMZAEZNOZAPZQRSFZVIBT
      ZRCFZRUAKMZSFZVIBTZKMZIVIBTVRUBVIBTZKMVJBVPJZVSKUCZCUDMZVMNOZAPZVQWBUEZVK
      VORUFHWDWHWIUGABCRDUHUIIVPUEVKWDUJUKVJBIVPULUMVNWGAVLWFVMNVLUAKUCZCUDMWFK
      CUNDUOWEWJCUDVSUAKUAUPUQZURUSUTVAVBVCIVPVIBUKVDWCWAVRKUBVTVIBUBUASFVTVEVS
      UASWKVFUTVDVGVH $.
      $( [10-Jan-2006] $) $( [2-Jan-2006] $)
  $}

  ${
    $d z A $.  $d j x y F $.  $d j k y z $.
    isumnn0nna.1 $e |- A e. _V $.
    isumnn0nna.3 $e |- F e. _V $.
    isumnn0nna.4 $e |- ( y e. F -> A. k y e. F ) $.
    isumnn0nna.5 $e |- ( k e. NN0 -> ( F ` k ) = A ) $.
    $( Sum from 0 to infinity in terms of sum from 1 to infinity of a class
       term ` A ( k ) ` . $)
    isumnn0nnai $p |- ( ( A. k e. NN0 A e. CC /\ E. x ( + seq1 F ) ~~> x ) ->
                     sum_ k e. NN0 A = ( [_ 0 / k ]_ A + sum_ k e. NN A ) ) $=
      ( vj vz cv cfv cc wcel cn0 wral caddc cseq1 co cli wbr wex csu cc0 csb
      cn wceq wa isumnn0nn sumeq2i ax-17 hbfv fveq2 cbvsumi eqtr3i 0nn0
      elisseti hbcsb1 hbeq csbeq1a eqeq12d vtoclgaf ax-mp eqcomi nnnn0 syl
      eqtri opreq12i 3eqtr4g eleq1d ralbiia hbel cbvral bitr3i sylanb ) JLZEMZN
      OZJPQZRESTALUAUBAUCZPCDUDZDUECUFZUGCDUDZRTZUHCNOZDPQZVTWAUIPVRJUDZUEEMZUG
      VRJUDZRTZWBWEAJEGUJPDLZEMZDUDWBWHPWMCDIUKBPWMVRDJBLZWMOJULZDBVQEHWNVQODUL
      UMZWLVQEUNZUOUPWKWEWIWCWJWDRUEPOWIWCUHZUQWMCUHZWRDKUEPKLZUEODULZDBKWIWCDB
      UEEHWNUEODULUMDKUECUEPUQURXAUSUTWLUEUHWMWICWCWLUEEUNDUECVAVBIVCVDWJUGWMDU
      DZWDXBWJBUGWMVRDJWOWPWQUOVEUGWMCDWLUGOWLPOZWSWLVFIVGUKVHVIVEVJWGWMNOZDPQV
      TXDWFDPXCWMCNIVKVLXDVSDJPXDJULDBKVRNWPWTNODULVMWLVQUHWMVRNWQVKVNVOVP $.
      $( [9-Feb-2007] $) $( [2-Nov-2007] $)
  $}

$(
  @{
    @d x A @.  @d k x F @.  @d x N @.
    isumadd.1 @e |- F e. _V @.
    isumadd.2 @e |- G e. _V @.
    @( Infinite sum of the sum of two converging sequences. @)
    isumadd @p |- ( ( N e. ZZ /\ E. x ( <. N , + >. seq F ) ~~> x /\
                    E. y ( <. N , + >. seq G ) ~~> y ) ->
               sum_ k e. ( ZZ>= ` N ) ( ( F ` k ) + ( G ` k ) ) =
   ( sum_ k e. ( ZZ>= ` N ) ( F ` k ) + sum_ k e. ( ZZ>= ` N ) ( G ` k ) ) ) @=
      ? @.
      @( [ ?] @) @( [2-Jan-2006] @)
  @}
$)

  ${
    $d j k x F $.  $d j x M $.
    isumclt.1 $e |- F e. _V $.
    $( The sum of a converging infinite series is a complex number. $)
    isumcl $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seq F ) ~~> x ) ->
                    sum_ k e. ( ZZ>= ` M ) ( F ` k ) e. CC ) $=
      ( cz wcel caddc cop cseqz co cv cli wbr wex cuz cfv csu cc cab cuni
      isumval eleq1d crab wreu hbreu1 visset climreu 19.23ai reucl syl wa cvv
      climcl mpan pm4.71ri abbii df-rab eqtr4i unieqi syl5eqel syl5bir imp ) DF
      GZDHICJKZALZMNZAOZDPQBLCQBRZSGZVDVJVGATZUAZSGVHVDVIVLSABCDEUBUCVHVGASUDZU
      AZSVLVHVGASUEZVNSGVGVOAVGASUFAVFVEAUGZUHUIVGASUJUKVKVMVKVFSGZVGULZATVMVGV
      RAVGVQVFUMGVGVQVPVFUMVEUNUOUPUQVGASURUSUTVAVBVC $.
      $( [1-Feb-2006] $) $( [13-Dec-2005] $)
  $}

  ${
    $d j k x F $.  $d j k x M $.
    isumreclt.1 $e |- F e. _V $.
    $( The sum of a converging real infinite series is a real number. $)
    isumrecl $p |- ( ( M e. ZZ /\ A. k e. ( ZZ>= ` M ) ( F ` k ) e. RR /\
                       E. x ( <. M , + >. seq F ) ~~> x ) ->
                    sum_ k e. ( ZZ>= ` M ) ( F ` k ) e. RR ) $=
      ( vj cz wcel cv cfv cr cuz wral caddc cop cseqz co cli wbr wex csu wa
      wceq visset isumclim adantlr wi cfz serzrecl elfzuz imim1i ralimi2
      sylan2 expcom adantl r19.21aiv climrecl 3expa syldan an1rs eqeltrd ex
      19.23adv 3impia ) DGHZBIZCJZKHZBDLJZMZDNOCPQZAIZRSZATVIVGBUAZKHZVEVJUBZVM
      VOAVPVMVOVPVMUBVNVLKVEVMVNVLUCVJVLBCDEAUDZUEUFVEVMVJVLKHZVEVMUBVJFIZVKJKH
      ZFVIMZVRVEVJWAVMVPVTFVIVJVSVIHZVTUGVEWBVJVTWBVHBDVSUHQZMVTVJBCDVSEUIVHVHB
      VIWCVFWCHVFVIHVHVFDVSUJUKULUMUNUOUPUFVEVMWAVRVLFVKDVQUQURUSUTVAVBVCVD $.
      $( [28-Jun-2006] $) $( [25-Dec-2005] $)
  $}

  ${
    $d F k x $.  $d M k x $.  $d N k x $.
    iserzgt0.1 $e |- F e. _V $.
    $( The infinite sum of positive reals is positive.  (Contributed by Paul
       Chapman, 9-Feb-2008.) $)
    iserzgt0 $p |- ( ( N e. ( ZZ>= ` M ) /\
              A. k e. ( ZZ>= ` M ) ( ( F ` k ) e. RR /\ 0 < ( F ` k ) ) /\
              E. x ( <. M , + >. seq F ) ~~> x ) ->
            0 < sum_ k e. ( ZZ>= ` N ) ( F ` k ) ) $=
      ( cuz cfv wcel cv cr cc0 clt wbr wa wral caddc cop cseqz co cli wex w3a
      c1 csu cle addgtge0 an4s wceq fveq2 eleq1d breq2d anbi12d rcla4va
      3adant3 cz isumrecl eluzelz peano2zdi 3ad2ant1 wi peano2uz uztrn expcom
      syl imim1d pm3.26 syl8 ralimdv2 imp cc iserzex recn adantr ralimi
      syl3an2 syl3anc sumex iserzcmp0 isumclim2 sylanc 0re ltle mpan imdistani
      jca isum1p breqtrrd ) EDGHZIZBJZCHZKIZLWLMNZOZBWIPZDQRCSTAJZUANAUBZUCZLEC
      HZEUDQTZGHZWLBUEZQTZEGHZWLBUEZMWTKIZLWTMNZOZXCKIZLXCUFNZOLXDMNZWSXGXJXHXK
      XLWTXCUGUHWJWPXIWRWOXIBEWIWKEUIZWMXGWNXHXMWLWTKWKECUJZUKXMWLWTLMXNULUMUNU
      OWSXJXKXAUPIZWMBXBPZXAQRCSTZWQUANAUBZXJWSABCXAFUQWJWPXOWRWJEDEURZUSUTZWJW
      PXPWRWJWPXPWJWOWMBWIXBWJWKWIIZWOVAZWKXBIZWOWMWJYCYAWOWJXAWIIZYCYAVADEVBYC
      YDYAXAWKDVCVDVEVFZWMWNVGVHVIVJUOWJWLVKIZBWIPWRXRWPABCDEFVLWOYFBWIWMYFWNWL
      VMVNZVOVPZVQXOXQXCUANZWMLWLUFNZOZBXBPZXKWSXCBCXAXBWLBVRFVSXTXOXRYIWSABCXA
      FVTXTYHWAWJWPYLWRWJWPYLWJWOYKBWIXBWJYBYCWOYKYEWMWNYJLKIWMWNYJVAWBLWLWCWDW
      EVHVIVJUOVQWFWAEUPIZYFBXEPZXRXFXDUIWSABCEFWGWJWPYMWRXSUTWJWPYNWRWJWPYNWJW
      OYFBWIXEWJYBWKXEIZWOYFWJYOYAWOYOWJYAEWKDVCVDVFYGVHVIVJUOYHVQWH $.
      $( [9-Feb-2008] $)
  $}

  ${
    $d j k m n x y C $.  $d j k m n x y F $.  $d j k m n x y M $.
    isummulc1.1 $e |- F e. _V $.
    $( Distribute a constant multiplier into an infinite sum (function value
       version).  See ~ isummulc1ai for class term version. $)
    isummulc1 $p |- ( ( ( M e. ZZ /\ C e. CC ) /\ ( A. k e. ( ZZ>= ` M )
        ( F ` k ) e. CC /\ E. x ( <. M , + >. seq F ) ~~> x ) )
                   -> ( C x. sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) =
                      sum_ k e. ( ZZ>= ` M ) ( C x. ( F ` k ) ) ) $=
      ( vj vy cz wcel cc wa cv cfv cuz wral caddc cop cseqz co cli wbr wex
      cmul csu wceq copab wi w3a isumclim2 ex 3ad2ant1 sumex fvex opabex2
      iserzmulc1 r19.26 fveq2 opreq2d eqid oprex fvopab4 rgen mpbiran2
      syl3an3br syld 3expia imp32 isumclim4 ad2antrr mpd ax-17 cbvsumi
      syl5reqr ) EIJZBKJZLZCMZDNZKJZCEONZPZEQRZDSTZAMZUAUBAUCZLZLZWABGMZDNZUDTZ
      GUEZBWAVSCUEZUDTZWABVSUDTZCUEWHWCWIWAJHMWKUFLGHUGZSTWNUAUBZWLWNUFZVQWBWFW
      QVOVPWBWFWQUHVOVPWBUIWFWDWMUAUBZWQVOVPWFWSUHWBVOWFWSACDEFUJUKULVOVPVTVRWP
      NWOUFZLCWAPZWSWQUHWBWMBCDWPEWAVSCUMFGHWAWKEOUNUOUPXAWBWTCWAPVTWTCWAUQWTCW
      AGHVRWKWOWAWPWIVRUFWJVSBUDWIVRDURUSZWPUTZBVSUDVAVBVCVDVEVFVGVHVOWQWRUHVPW
      GVOWQWRHWKWNGWPEBWJUDVAXCBWMUDVAVIUKVJVKAWAWKWOGCWEWKJCVLWEWOJGVLXBVMVN
      $.
      $( [16-Nov-2007] $) $( [1-Nov-2007] $)
  $}

  ${
    $d k m n x y C $.  $d k m n x y F $.  $d k m n x y M $.
    isummulc1iALT.1 $e |- F e. _V $.
    $( Older ~ isummulc1 proved without using ~ iserzmulc1 . $)
    isummulc1iALT $p |- ( ( ( M e. ZZ /\ C e. CC ) /\ ( A. k e. ( ZZ>= ` M )
        ( F ` k ) e. CC /\ E. x ( <. M , + >. seq F ) ~~> x ) )
                   -> ( C x. sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) =
                      sum_ k e. ( ZZ>= ` M ) ( C x. ( F ` k ) ) ) $=
      ( vy vn vm cz wcel cc cv cfv cuz wral caddc cop cseqz co cli wbr wex csu
      cmul wceq wi w3a wa visset isumclim opreq2d 3ad2antl1 copab oprex eqid
      isumclim4 3simpl1 climmulc2 3simp2 anim1i serzcl2 3adant2 cfz fvex
      opabex2 serzmulc1 fveq2 fvopab4 anim2i expcom a2i elfzuz syl5 ralimi2
      syl3an3 eqcomd jca 3coml 3expa r19.21aiva eleq1d cbvralv sylan2b 3impb
      adantr sylanc eqtr4d ex 19.23adv 3exp imp43 ) EJKZBLKZCMZDNZLKZCEONZPZEQR
      ZDSTZAMZUAUBZAUCZBWRWPCUDZUETZWRBWPUETZCUDZUFZWMWNWSXDXIUGWMWNWSUHZXCXIAX
      JXCXIXJXCUIZXFBXBUETZXHWMWNXCXFXLUFWSWMXCUIXEXBBUEXBCDEFAUJZUKULUMWMWTWOW
      RKGMXGUFUICGUNZSTZXLUAUBZXHXLUFXKGXGXLCXNEBWPUEUOXNUPZBXBUEUOUQWMWNWSXCUR
      WNXCUIWMHMZXANZLKZXRXONZBXSUETZUFZUIZHWRPZUIZXPXKXBBHXAXOEWTDSUOWTXNSUOXM
      USXJWNXCWMWNWSUTVAXJYFXCWMWNWSYFWNWSUIYEWMWNIMZDNZLKZIWRPZYEWSWNYJUIYDHWR
      WNYJXRWRKZYDYKWNYJYDYKWNYJUHZXTYCYKYJXTWNIDEXRFVBVCYLYBYAYKWNYIYGXNNBYHUE
      TZUFZUIZIEXRVDTZPYBYAUFYJBIDXNEXRFCGWRXGEOVEVFVGYIYOIWRYPYGWRKZYIUGYQYOYG
      YPKYQYIYOYIYQYOYQYNYICGYGXGYMWRXNWOYGUFZWPYHBUEWOYGDVHZULXQBYHUEUOVIVJVKV
      LYGEXRVMVNVOVPVQVRVSVTWAWQYICIWRYRWPYHLYSWBWCWDVJWEWFWGWGWHWIWJWKWL $.
      $( [2-Nov-2007] $) $( [1-Nov-2007] $)
  $}

  ${
    $d j k x y C $.  $d j x y F $.  $d j k x y M $.
    isummulc1a.1 $e |- A e. _V $.
    isummulc1a.2 $e |- F e. _V $.
    isummulc1a.3 $e |- ( y e. F -> A. k y e. F ) $.
    isummulc1a.4 $e |- ( k e. ( ZZ>= ` M ) -> ( F ` k ) = A ) $.
    $( Distribute a constant multiplier into an infinite sum of a class term
       ` A ( k ) ` .  See ~ isummulc1 for function value version. $)
    isummulc1ai $p |- ( ( ( M e. ZZ /\ C e. CC ) /\ ( A. k e. ( ZZ>= ` M )
        A e. CC /\ E. x ( <. M , + >. seq F ) ~~> x ) ) ->
     ( C x. sum_ k e. ( ZZ>= ` M ) A ) = sum_ k e. ( ZZ>= ` M ) ( C x. A ) ) $=
      ( vj cz wcel cc wa cv cfv cuz wral caddc cop cseqz co cli wbr wex csu
      cmul wceq isummulc1 sumeq2i ax-17 hbfv fveq2 cbvsumi eqtr3i opreq2i
      opreq2d hbopr 3eqtr4g eleq1d ralbiia hbel cbvral bitr3i anbi1i sylan2b )
      GMNDONPZLQZFRZONZLGSRZTZGUAUBFUCUDAQUEUFAUGZPZDVMCEUHZUIUDZVMDCUIUDZEUHZU
      JCONZEVMTZVOPVIVPPDVMVKLUHZUIUDVMDVKUIUDZLUHZVRVTADLFGIUKVQWCDUIVMEQZFRZE
      UHVQWCVMWGCEKULBVMWGVKELBQZWGNLUMEBVJFJWHVJNEUMUNZWFVJFUOZUPUQURVMDWGUIUD
      ZEUHVTWEVMWKVSEWFVMNZWGCDUIKUSULBVMWKWDELWHWKNLUMEBDVKUIWHDNEUMWHUINEUMWI
      UTWFVJUJZWGVKDUIWJUSUPUQVAWBVNVOWBWGONZEVMTVNWNWAEVMWLWGCOKVBVCWNVLELVMWN
      LUMEBBVKOWIWHONEUMVDWMWGVKOWJVBVEVFVGVH $.
      $( [2-Nov-2007] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d k x F $.  $d k x G $.  $d j k x y M $.
    isumcmpi.1 $e |- A e. _V $.
    isumcmpi.2 $e |- B e. _V $.
    isumcmpi.3 $e |- F = { <. j , y >. | ( j e. ( ZZ>= ` M ) /\ y = A ) } $.
    isumcmpi.4 $e |- G = { <. j , y >. | ( j e. ( ZZ>= ` M ) /\ y = B ) } $.
    isumcmpi.5 $e |- ( j e. ( ZZ>= ` M ) ->
               ( A e. RR /\ B e. RR /\ A <_ B ) ) $.
    $( Comparison of two infinite sums.  (Contributed by Paul Chapman,
       13-Nov-2007.) $)
    isumcmpii $p |- ( ( M e. ZZ /\ ( E. x ( <. M , + >. seq F ) ~~> x /\
                       E. x ( <. M , + >. seq G ) ~~> x ) ) ->
                     sum_ j e. ( ZZ>= ` M ) A <_ sum_ j e. ( ZZ>= ` M ) B ) $=
      ( vk cz wcel caddc cop cseqz co cv cli wbr wex wa cuz cfv csu cle cr w3a
      wral r19.26 wceq 3simp1d copab fvopab2 fveq1i syl5eq mpdan eqeltrd rgen
      ax-17 hbopab1 hbxfr hbfv hbel fveq2 eleq1d cbvral mpbi 3simp2d mpbir2an
      3simp3d 3brtr4d hbbr breq12d df-3an ralbii mpbir sumex fvex fopabex2
      iserzcmp mpanr2 expcom isumclim3 ex syl2and imp ) HOPZHQRZFSTZAUAZUBUCAUD
      ZWLGSTZWNUBUCAUDZUEHUFUGZCEUHZWRDEUHZUIUCZWKWMWSUBUCZWPWTUBUCZXAWOWQXBXCU
      EZWKXAXDWKNUAZFUGZUJPZXEGUGZUJPZXFXHUIUCZUKZNWRULZXAXLXGXIUEZXJUEZNWRULZX
      OXMNWRULZXJNWRULZXMXJNWRUMXPXGNWRULZXINWRULZXGXINWRUMEUAZFUGZUJPZEWRULXRY
      BEWRXTWRPZYACUJYCCUJPZYACUNYCYDDUJPZCDUIUCZMUOZYCYDUEXTYCBUAZCUNUEZEBUPZU
      GCYAEBWRCUJUQXTFYJKURUSUTZYGVAVBYBXGENWRYBNVCEAAXFUJEAXEFEAFYJKYIEBAVDVEW
      NXEPEVCZVFZWNUJPEVCZVGXTXEUNZYAXFUJXTXEFVHZVIVJVKXTGUGZUJPZEWRULXSYREWRYC
      YQDUJYCYEYQDUNYCYDYEYFMVLZYCYEUEXTYCYHDUNUEZEBUPZUGDYQEBWRDUJUQXTGUUALURU
      SUTZYSVAVBYRXIENWRYRNVCEAAXHUJEAXEGEAGUUALYTEBAVDVEYLVFZYNVGYOYQXHUJXTXEG
      VHZVIVJVKVMYAYQUIUCZEWRULXQUUEEWRYCCDYAYQUIYCYDYEYFMVNYKUUBVOVBUUEXJENWRU
      UENVCEAXFXHUIYMWNUIPEVCUUCVPYOYAXFYQXHUIYPUUDVQVJVKVMXKXNNWRXGXIXJVRVSVTW
      SWTNFGHWRCEWAWRDEWAEBWRCFHUFWBZKWCEBWRDGUUFLWCWDWEWFWKWOXBABCEFHIKWGWHWKW
      QXCABDEGHJLWGWHWIWJ $.
      $( [13-Nov-2007] $)
  $}

  ${
    $d F k x $.  $d M k x $.  $d N k x $.
    isumsplit.1 $e |- M e. ZZ $.
    isumsplit.2 $e |- N e. ( ZZ>= ` M ) $.
    isumsplit.3 $e |- F : ( ZZ>= ` M ) --> CC $.
    isumsplit.4 $e |- E. x ( <. M , + >. seq F ) ~~> x $.
    $( Split off the first ` N ` terms of an infinite sum.  (Contributed by
       Paul Chapman, 9-Feb-2007.) $)
    isumspliti $p |- sum_ k e. ( ZZ>= ` M ) ( F ` k ) =
                    ( sum_ k e. ( M ... N ) ( F ` k ) +
                      sum_ k e. ( ZZ>= ` ( N + 1 ) ) ( F ` k ) ) $=
      ( cfz co cv cfv csu c1 caddc cuz cmin wceq cz wcel cop cseqz cli wbr cle
      wa eluz1i mpbi pm3.26i peano2z ax-mp cc wf cvv fvex fex mp2an sumex wex
      isumclim2 clim2serzi fsumserz opreq2i breqtrri oprex isumclim eqcomi
      isumcl wral elfzuz ffvelrni syl rgen fsumcl breq2 cla4ev subaddi ) DEJKZB
      LZCMZBNZEOPKZQMWABNZPKZDQMZWABNZWGWBRKZWDSWEWGSWDWHWCTUAZWCPUBCUCKZWHUDUE
      ZWDWHSETUAZWIWLDEUFUEZEWFUAZWLWMUGGDEFUHUIUJEUKULZWJWGEDPUBCUCKZMZRKZWHUD
      WNWJWRUDUEGWGCDEWFUMCUNWFUOUACUOUAHDQUPWFUMUOCUQURZWFWABUSDTUAZWPALZUDUEA
      UTZWPWGUDUEFIABCDWSVAURHVBULWBWQWGRWNWBWQSGBCDEWSVCULVDVEZWHBCWCWSWGWBRVF
      ZVGURVHWGWBWDWTXBWGUMUAFIABCDWSVIURWNWAUMUAZBVSVJWBUMUAGXEBVSVTVSUAVTWFUA
      XEVTDEVKWFUMVTCHVLVMVNWABDEVOURWIWJXAUDUEZAUTZWDUMUAWOWKXGXCXFWKAWHXDXAWH
      WJUDVPVQULABCWCWSVIURVRUIVH $.
      $( [9-Feb-2008] $)
  $}

  ${
    $d F k x $.  $d N k x $.
    isum0split.1 $e |- N e. NN0 $.
    isum0split.2 $e |- F : NN0 --> CC $.
    isum0split.3 $e |- E. x ( + seq0 F ) ~~> x $.
    $( Split off the first ` N ` terms of a 0-based infinite sum.  (Contributed
       by Paul Chapman, 9-Feb-2007.) $)
    isum0spliti $p |- sum_ k e. NN0 ( F ` k ) =
                     ( sum_ k e. ( 0 ... N ) ( F ` k ) +
                       sum_ k e. ( ZZ>= ` ( N + 1 ) ) ( F ` k ) ) $=
      ( cn0 cv cfv csu cc0 cuz cfz co c1 caddc nn0uz sumeq1i 0z eleqtri cc wf
      feq2i mpbi cseq0 cli wbr wex cop cseqz addex cvv wcel nn0ex fex mp2an
      seq0seqz breq1i exbii isumspliti eqtri ) HBICJZBKLMJZVCBKLDNOVCBKDPQOMJVC
      BKQOHVDVCBRSABCLDTDHVDERUAHUBCUCZVDUBCUCFHVDUBCRUDUEQCUFOZAIZUGUHZAUILQUJ
      CUKOZVGUGUHZAUIGVHVJAVFVIVGUGQCULVEHUMUNCUMUNFUOHUBUMCUPUQURUSUTUEVAVB $.
      $( [9-Feb-2008] $)
  $}

$(
  @{
    fsumisumt.1 @e |- F e. _V @.
    fsumisumt.2 @e |- A e. _V @.
    @( A finite sum of nonnegative numbers is less than its limit. @)
    fsumisumt @p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( ZZ>= ` M )
      ( ( F ` k ) e. RR /\ 0 <_ ( F ` k ) ) /\ ( <. M , + >. seq F ) ~~> A ) ->
                   sum_ k e. ( M ... N ) ( F ` k ) <_ A ) @=
      (  ) ? @.
      @( [ ?] @) @( [25-Dec-2005] @)
  @}
$)

