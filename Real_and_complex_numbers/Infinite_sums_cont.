$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Infinite sums (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d m n x y A $.  $d m n x y M $.  $d m n x y k $.
    $( Series sum with an infinite index set (i.e. an infinite series). $)
    dfisum $p |- ( M e. ZZ -> sum_ k e. ( ZZ>= ` M ) A =
       U. { x |
    ( <. M , + >. seqz ( { <. k , y >. | y = A } |` ZZ ) ) ~~> x } ) $=
      ( vm vn cz wcel cuz cfv csu cv cfz co wceq caddc cop copab cres cseqz wa
      wrex wex cab cli wbr cuni cun df-sum c0 wn fzneuz eqcom notbii sylib
      ancoms intnanrd nrexdv nexdv abn0 necon1bbii uz11 syl6bb anbi1d rexbidv
      opeq1 opreq1d breq1d ceqsrexv bitrd abbidv unieqd uneq12d uncom un0
      eqtri syl6eq syl5eq ) EHIZEJKZCDLWAFMZGMZNOZPZAMZWCWBQRZBMCPDBSHTZUAOZKIZ
      UBZGWBJKZUCZFUDZAUEZWAWLPZWIWFUFUGZUBZFHUCZAUEZUHZUIZEQRZWHUAOZWFUFUGZAUE
      ZUHZABWACDFGUJVTXBUKXGUIZXGVTWOUKXAXGVTWNAUDZULWOUKPVTWNAVTWMFVTWKGWLVTWC
      WLIZUBWEWJXJVTWEULZXJVTUBWDWAPZULXKEWBWCUMXLWEWDWAUNUOUPUQURUSUTUTXIWOUKW
      NAVAVBUPVTWTXFVTWSXEAVTWSWBEPZWQUBZFHUCXEVTWRXNFHVTWPXMWQVTWPEWBPXMEWBVCE
      WBUNVDVEVFWQXEFEHXMWIXDWFUFXMWGXCWHUAWBEQVGVHVIVJVKVLVMVNXHXGUKUIXGUKXGVO
      XGVPVQVRVS $.
      $( [11-Dec-2005] $)
  $}

  ${
    $d k x y F $.  $d x y M $.
    isumvalt.1 $e |- F e. _V $.
    $( The value of the sum of a series ` F ` from ` M ` to infinity is the
       unique (by ~ climuni ) value it converges to (if it converges).  The sum
       evaluates to the empty set when it doesn't converge, but this shouldn't
       be depended on to indicate non-convergence (because it would presuppose
       a construction-dependent property of complex numbers, i.e. that no
       complex number equals the empty set, which, although by ~ 0ncn is true
       for our specific construction, is not specified by their axioms and may
       fail for some other construction).  Instead, use
       ` -. E. x e. CC ( <. M , + >. seqz F ) ~~> x ` to indicate
       non-convergence. $)
    isumval $p |- ( M e. ZZ ->
            sum_ k e. ( ZZ>= ` M ) ( F ` k ) =
                   U. { x | ( <. M , + >. seqz F ) ~~> x } ) $=
      ( vy cz wcel cuz cfv cv csu caddc cop wceq copab cres cseqz co cli wbr
      cab cuni dfisum addex seqzres2 breq1d abbidv unieqd eqtrd ) DGHZDIJBKCJZB
      LDMNZFKULOBFPGQRSZAKZTUAZAUBZUCUMCRSZUOTUAZAUBZUCAFULBDUDUKUQUTUKUPUSAUKU
      NURUOTFMBCDUEEUFUGUHUIUJ $.
      $( [11-Dec-2005] $)
  $}

  ${
    $d j x y z F $.  $d x z M $.  $d j k y z $.
    isumvaltf.1 $e |- F e. _V $.
    isumvaltf.2 $e |- ( y e. F -> A. k y e. F ) $.
    $( Version of ~ isumval with a bound-variable hypothesis instead of a
       distinct variable condition. $)
    isumvaltfi $p |- ( M e. ZZ ->
            sum_ k e. ( ZZ>= ` M ) ( F ` k ) =
                   U. { x | ( <. M , + >. seqz F ) ~~> x } ) $=
      ( vj vz cz wcel cuz cfv cv csu caddc cop cseqz co cli wbr cab cuni ax-17
      hblem hbfv fveq2 cbvsumi isumval syl5eq ) EJKELMZCNZDMZCOUKHNZDMZHOEPQDRS
      ANTUAAUBUCIUKUMUOCHINZUMKHUDCIUNDCBIDGUEUPUNKCUDUFULUNDUGUHAHDEFUIUJ $.
      $( [13-Dec-2005] $)
  $}

  ${
    $d y A $.  $d x z F $.  $d k x y z M $.
    isumval2t.1 $e |- A e. _V $.
    isumval2t.2 $e |- F = { <. k , y >. | ( k e. ( ZZ>= ` M ) /\ y = A ) } $.
    $( Sum of ` A ( k ) ` from ` M ` to infinity. $)
    isumval2 $p |- ( M e. ZZ ->
            sum_ k e. ( ZZ>= ` M ) A =
                   U. { x | ( <. M , + >. seqz F ) ~~> x } ) $=
      ( vz cz wcel cuz cfv csu cv caddc cop cseqz co cli wbr cab cuni wceq wa
      copab fveq1i cvv fvopab2 mpan2 syl5eq sumeq2i fvex fopabex2 hbopab1
      hbxfreq isumvaltfi syl5eqr ) FJKFLMZCDNUSDOZEMZDNFPQERSAOTUAAUBUCUSVACDUT
      USKZVAUTVBBOCUDUEZDBUFZMZCUTEVDHUGVBCUHKVECUDGDBUSCUHUIUJUKULAIDEFDBUSCEF
      LUMHUNDIEVDHVCDBIUOUPUQUR $.
      $( [31-Mar-2008] $) $( [2-Jan-2006] $)
  $}

  ${
    $d x A $.  $d x y F $.  $d x M $.  $d k y $.
    isumclimtf.1 $e |- ( y e. F -> A. k y e. F ) $.
    isumclimtf.2 $e |- F e. _V $.
    isumclimtf.3 $e |- A e. _V $.
    $( Version of ~ isumclim with a bound-variable hypothesis instead of a
       distinct variable condition. $)
    isumclimtfi $p |- ( ( M e. ZZ /\ ( <. M , + >. seqz F ) ~~> A ) ->
                   sum_ k e. ( ZZ>= ` M ) ( F ` k ) = A ) $=
      ( vx cz wcel caddc cop cseqz co cli wbr cuz cfv cv csu cvv crab cuni cab
      isumvaltfi rabab unieqi syl6eqr wceq wreu wb weu climeu wa df-reu visset
      biantrur eubii bitr4i sylibr breq2 reuuni2 sylancr ibi sylan9eq ) EJKZELM
      DNOZBPQZERSCTDSCUAZVHITZPQZIUBUCZUDZBVGVJVLIUEZUDVNIACDEGFUFVMVOVLIUGUHUI
      VIVNBUJZVIBUBKVLIUBUKZVIVPULHVIVLIUMZVQIBVHHUNVQVKUBKZVLUOZIUMVRVLIUBUPVL
      VTIVSVLIUQURUSUTVAVLVIIUBBVKBVHPVBVCVDVEVF $.
      $( [10-May-2006] $)
  $}

  ${
    $d x k F $.
    isumclimt.1 $e |- F e. _V $.
    isumclimt.2 $e |- A e. _V $.
    $( An infinite sum equals the value its series converges to. $)
    isumclim $p |- ( ( M e. ZZ /\ ( <. M , + >. seqz F ) ~~> A ) ->
                   sum_ k e. ( ZZ>= ` M ) ( F ` k ) = A ) $=
      ( vx cv wcel ax-17 isumclimtfi ) GABCDGHCIBJEFK $.
      $( [25-Dec-2005] $)

    $( An infinite sum equals the value its series converges to. $)
    isum1clim $p |- ( ( + seq1 F ) ~~> A -> sum_ k e. NN ( F ` k ) = A ) $=
      ( caddc cseq1 co cli wbr cn cv cfv csu c1 cuz nnuz sumeq1i cop cseqz
      wceq addex seq1seqz breq1i cz wcel 1z isumclim mpan sylbi syl5eq ) FCGHZA
      IJZKBLCMZBNOPMZUNBNZAKUOUNBQRUMOFSCTHZAIJZUPAUAZULUQAIFCUBDUCUDOUEUFURUSU
      GABCODEUHUIUJUK $.
      $( [10-Jul-2008] $)
  $}

  ${
    $d x y F $.  $d x M $.  $d k x y $.
    isumclim2tf.1 $e |- ( y e. F -> A. k y e. F ) $.
    isumclim2tf.2 $e |- F e. _V $.
    $( Version of ~ isumclim2 with a bound-variable hypothesis instead of a
       distinct variable condition. $)
    isumclim2f $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seqz F ) ~~> x ) ->
              ( <. M , + >. seqz F ) ~~> sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) $=
      ( cz wcel caddc cop cseqz co cv cli wbr wex cuz cfv csu wa simpr visset
      isumclimtfi breqtrrd ex 19.23adv imp ) EHIZEJKDLMZANZOPZAQUJERSCNDSCTZOPZ
      UIULUNAUIULUNUIULUAUJUKUMOUIULUBBUKCDEFGAUCUDUEUFUGUH $.
      $( [9-Jan-2006] $)
  $}

  ${
    $d k x y F $.  $d x M $.
    isumclim2t.1 $e |- F e. _V $.
    $( A converging series converges to its infinite sum. $)
    isumclim2 $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seqz F ) ~~> x ) ->
              ( <. M , + >. seqz F ) ~~> sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) $=
      ( vy cv wcel ax-17 isumclim2f ) AFBCDFGCHBIEJ $.
      $( [2-Jan-2006] $)
  $}

  ${
    $d x y z A $.  $d k x y z M $.  $d z F $.
    isumclim3t.1 $e |- A e. _V $.
    isumclim3t.2 $e |- F = { <. k , y >. | ( k e. ( ZZ>= ` M ) /\ y = A ) } $.
    $( A converging series converges to its infinite sum. $)
    isumclim3 $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seqz F ) ~~> x ) ->
                     ( <. M , + >. seqz F ) ~~> sum_ k e. ( ZZ>= ` M ) A ) $=
      ( vz cz wcel caddc cop cseqz co cv cli wbr wex wa cuz cfv wceq copab csu
      opreq2i a1i eqcomd breq1d exbidv biimpar hbopab1 fvex opabex2 isumclim2f
      syldan cvv fvopab2 mpan2 sumeq2i syl6breq syl5eqbr ) FJKZFLMZENOZAPZQRZAS
      ZTZVEVDDPZFUAUBZKZBPCUCTZDBUDZNOZVKCDUEZQEVNVDNHUFZVIVOVKVJVNUBZDUEZVPQVC
      VHVOVFQRZASZVOVSQRVCWAVHVCVTVGAVCVOVEVFQVCVEVOVEVOUCVCVQUGUHUIUJUKAIDVNFV
      MDBIULDBVKCFUAUMUNUOUPVKVRCDVLCUQKVRCUCGDBVKCUQURUSUTVAVB $.
      $( [9-Jan-2006] $)

    ${
      isumclim4t.3 $e |- B e. _V $.
      $( An infinite sum equals the value its series converges to. $)
      isumclim4 $p |- ( ( M e. ZZ /\ ( <. M , + >. seqz F ) ~~> B ) ->
                     sum_ k e. ( ZZ>= ` M ) A = B ) $=
        ( vz cz wcel caddc cop cseqz co cli wbr wa cuz cfv csu cv wss wceq
        ssid sumeqfv ax-mp copab hbopab1 hbxfreq fvex fopabex2 isumclimtfi
        syl5eqr ) FKLFMNEOPCQRSFTUAZBDUBZUPDUCZEUADUBZCUPUPUDUSUQUEUPUFABUPUPDE
        GHUGUHJCDEFDJEURUPLAUCBUESZDAUIHUTDAJUJUKDAUPBEFTULHUMIUNUO $.
        $( [10-May-2006] $)
    $}
  $}

  ${
    $d j x y z A $.  $d j k x y z M $.
    isumclim5t.1 $e |- A e. _V $.
    isumclim5t.2 $e |- F = { <. j , y >. | ( j e. ( ZZ>= ` M ) /\ y =
                      sum_ k e. ( M ... j ) A ) } $.
    $( The sequence of partial finite sums of a converging infinite series
       converge to the infinite sum of the series.  Note that ` j ` must not
       occur in ` A ` . $)
    isumclim5 $p |- ( ( M e. ZZ /\ E. x F ~~> x ) ->
                     F ~~> sum_ k e. ( ZZ>= ` M ) A ) $=
      ( vz cz wcel cv cli wbr wex wa caddc cop cuz cfv wceq copab cseqz co csu
      eqid serzfsum adantr breq1d exbidv biimpar isumclim3 syldan eqbrtrrd ) GK
      LZFAMZNOZAPZQGRSEMGTUAZLJMCUBQEJUCZUDUEZFUTCEUFZNUPVBFUBUSJBCEDVAFGHVAUGZ
      IUHZUIUPUSVBUQNOZAPZVBVCNOUPVGUSUPVFURAUPVBFUQNVEUJUKULAJCEVAGHVDUMUNUO
      $.
      $( [9-Jan-2006] $)
  $}

  ${
    $d F k x $.  $d M x $.
    isumnul.1 $e |- F e. _V $.
    $( The sum of a non-convergent infinite series evaluates to the empty set.
       (Contributed by Paul Chapman, 04-Nov-2007.) $)
    isumnul $p |- ( ( M e. ZZ /\ -. E. x ( <. M , + >. seqz F ) ~~> x ) ->
                  sum_ k e. ( ZZ>= ` M ) ( F ` k ) = (/) ) $=
      ( cz wcel caddc cop cseqz co cv cli wbr wex wn wa cuz cfv csu cab cuni
      c0 wceq isumval adantr wne abn0 biimpi necon1bi unieqd adantl uni0 a1i
      3eqtrd ) DFGZDHICJKALMNZAOZPZQZDRSBLCSBTZUQAUAZUBZUCUBZUCUPVAVCUDUSABCDEU
      EUFUSVCVDUDUPUSVBUCURVBUCVBUCUGURUQAUHUIUJUKULVDUCUDUTUMUNUO $.
      $( [4-Nov-2007] $)
  $}

  ${
    $d F k x $.  $d K k x $.  $d M k x $.  $d N k x $.
    isumshft.1 $e |- F e. _V $.
    isumshft.2 $e |- K e. ZZ $.
    isumshft.3 $e |- M e. ZZ $.
    isumshft.4 $e |- N = ( M + K ) $.
    $( Index shift of an infinite sum.  (Contributed by Paul Chapman,
       31-Oct-2007.) $)
    isumshfti $p |- sum_ k e. ( ZZ>= ` M ) ( F ` k ) =
                      sum_ k e. ( ZZ>= ` N ) ( F ` ( k - K ) ) $=
      ( vx caddc cop cseqz co cv cli wbr wex cuz cfv csu cmin wceq cz wcel
      visset isumclim mpan cshi cc eluzelz zcn syl shftval sylancr sumeq2i cvv
      wb eqid iserzshfti ax-mp zaddcl mp2an eqeltri oprex sylbi syl5eqr eqtr4d
      19.23aiv wn c0 isumnul exbii notbii syl6eq pm2.61i ) DKLBMNJOZPQZJRZDSTAO
      ZBTAUAZESTZVTCUBNBTZAUAZUCZVRWEJVRWAVQWDDUDUEZVRWAVQUCHVQABDFJUFZUGUHVRWD
      WBVTBCUINZTZAUAZVQWBWIWCAVTWBUEZCUDUEZVTUJUEZWIWCUCGWKVTUDUEWMEVTUKVTULUM
      CVTUDBFUNUOUPZVREKLWHMNVQPQZWJVQUCZVQUQUEVRWOURWGVQUQBWHCDEFHGIWHUSUTVAZE
      UDUEZWOWPEDCKNZUDIWFWLWSUDUEHGDCVBVCVDZVQAWHEBCUIVEZWGUGUHVFVGVHVIVSVJZWA
      WJWDXBWAVKWJWFXBWAVKUCHJABDFVLUHXBWOJRZVJZWJVKUCZVSXCVRWOJWQVMVNWRXDXEWTJ
      AWHEXAVLUHVFVHWNVOVP $.
      $( [31-Oct-2007] $)

    $( Index shift of an infinite sum.  (Contributed by Paul Chapman,
       31-Oct-2007.) $)
    isumshft2i $p |- sum_ k e. ( ZZ>= ` N ) ( F ` k ) =
                       sum_ k e. ( ZZ>= ` M ) ( F ` ( K + k ) ) $=
      ( vx caddc cop cseqz co cv cli wbr wex cuz cfv csu wceq cz wcel zaddcl
      mp2an eqeltri visset isumclim mpan cneg cshi cc zcn ax-mp eluzelz syl
      shftval4 sylancr sumeq2i cvv wb znegcl cmin negsubi eqcomi subadd2i
      mpbir eqtr2i eqid iserzshfti oprex sylbi syl5eqr eqtr4d 19.23aiv wn c0
      isumnul exbii notbii syl6eq pm2.61i ) EKLBMNJOZPQZJRZESTAOZBTAUAZDSTZCWGK
      NBTZAUAZUBZWEWLJWEWHWDWKEUCUDZWEWHWDUBEDCKNZUCIDUCUDZCUCUDZWNUCUDHGDCUEUF
      UGZWDABEFJUHZUIUJWEWKWIWGBCUKZULNZTZAUAZWDWIXAWJAWGWIUDZCUMUDZWGUMUDZXAWJ
      UBWPXDGCUNUOZXCWGUCUDXEDWGUPWGUNUQCWGBFURUSUTZWEDKLWTMNWDPQZXBWDUBZWDVAUD
      WEXHVBWRWDVABWTWSEDFWQWPWSUCUDGCVCUOEWSKNECVDNZDECWMEUMUDWQEUNUOZXFVEXJDU
      BWNEUBEWNIVFECDXKXFWODUMUDHDUNUOVGVHVIWTVJVKUOZWOXHXIHWDAWTDBWSULVLZWRUIU
      JVMVNVOVPWFVQZWHXBWKXNWHVRXBWMXNWHVRUBWQJABEFVSUJXNXHJRZVQZXBVRUBZWFXOWEX
      HJXLVTWAWOXPXQHJAWTDXMVSUJVMVOXGWBWC $.
      $( [31-Oct-2007] $)
  $}

  ${
    $d j k n x F $.  $d j k n x M $.
    isum1p.1 $e |- F e. _V $.
    $( The infinite sum of a converging infinite series equals the first term
       plus the infinite sum of the rest of it. $)
    isum1p $p |- ( ( M e. ZZ /\ A. k e. ( ZZ>= ` M ) ( F ` k ) e. CC /\
  E. x ( <. ( M + 1 ) , + >. seqz F ) ~~> x ) -> sum_ k e. ( ZZ>= ` M )
   ( F ` k ) = ( ( F ` M ) + sum_ k e. ( ZZ>= ` ( M + 1 ) ) ( F ` k ) ) ) $=
      ( vj vn cv cfv cc wcel cuz wral cz c1 caddc co cop cseqz cli wbr wex csu
      wceq fveq2 eleq1d cbvralv wa peano2z isumclim2 sylan adantlr simpll
      simpr uzid rcla4va adantr ad2antrr cfz elfzuz peano2uzr ex syl5 imim1d
      ralimdv2 a1dd imp31 serzcl syl11anc imim1i ralimi2 clt eluzp1l jca
      serz1p 3expa an32s sylanl2 r19.21aiva oprex sumex fvex climaddc2
      syl22anc isumclim syldan 3impa syl3an2b ) BHZCIZJKZBDLIZMDNKZFHZCIZJKZFWL
      MZDOPQZPRZCSQZAHTUAAUBZWLWJBUCDCIZWRLIZWJBUCZPQZUDZWKWPBFWLWIWNUDWJWOJWIW
      NCUEUFUGWMWQXAXFWMWQUHZXAWTXDTUAZXFWMXAXHWQWMWRNKZXAXHDUIZABCWREUJUKULXGX
      HUHZWMDPRZCSQZXETUAZXFWMWQXHUMXKXHXBJKZXIGHZWTIZJKZXPXMIXBXQPQUDZUHZGXCMZ
      XNXGXHUNXGXOXHWMDWLKWQXODUOWPXOFDWLWNDUDWOXBJWNDCUEUFUPUKUQWMXIWQXHXJURXG
      YAXHXGXTGXCXGXPXCKZUHZXRXSYCYBWPFWRXPUSQZMZXRXGYBUNWMWQYBYEWMWQYEYBWMWPWP
      FWLYDWMWNYDKZWNWLKZWPYFWNXCKZWMYGWNWRXPUTWMYHYGDWNVAVBVCVDVEVFVGFCWRXPEVH
      VIWQWMWPFDXPUSQZMZYBXSWPWPFWLYIWNYIKYGWPWNDXPUTVJVKWMYBYJXSWMYBUHZXPWLKZD
      XPVLUAZUHYJXSYKYLYMDXPVADXPVMVNYLYMYJXSFCDXPEVOVPUKVQVRVNVSUQXDXBGWTXMWRW
      SCSVTXLCSVTXCWJBWADCWBWCWDXEBCDEXBXDPVTWEVIWFWGWH $.
      $( [2-Jan-2006] $)
  $}

  ${
    $d x k F $.
    isumnn0nn.1 $e |- F e. _V $.
    $( Sum from 0 to infinity in terms of sum from 1 to infinity. $)
    isumnn0nn $p |- ( ( A. k e. NN0 ( F ` k ) e. CC /\
  E. x ( + seq1 F ) ~~> x ) -> sum_ k e. NN0 ( F ` k )
                 = ( ( F ` 0 ) + sum_ k e. NN ( F ` k ) ) ) $=
      ( cv cfv cc wcel cn0 wral caddc cseq1 co cli wbr wex wa cc0 cuz csu c1
      cn cop cseqz wceq nn0uz raleqi addex seq1seqz ax-1cn addid2i opeq1i
      opreq1i eqtr4i breq1i exbii cz 0z isum1p mp3an1 syl2anb sumeq1i nnuz
      fveq2i opreq2i 3eqtr4g ) BECFZGHZBIJZKCLMZAEZNOZAPZQRSFZVGBTZRCFZRUAKMZSF
      ZVGBTZKMZIVGBTVPUBVGBTZKMVIVHBVNJZVQKUCZCUDMZVKNOZAPZVOVTUEZVMVHBIVNUFUGV
      LWEAVJWDVKNVJUAKUCZCUDMWDKCUHDUIWCWHCUDVQUAKUAUJUKZULUMUNUOUPRUQHWBWFWGUR
      ABCRDUSUTVAIVNVGBUFVBWAVSVPKUBVRVGBUBUASFVRVCVQUASWIVDUNVBVEVF $.
      $( [2-Jan-2006] $)
  $}

  ${
    $d z A $.  $d j x y F $.  $d j k y z $.
    isumnn0nna.1 $e |- A e. _V $.
    isumnn0nna.3 $e |- F e. _V $.
    isumnn0nna.4 $e |- ( y e. F -> A. k y e. F ) $.
    isumnn0nna.5 $e |- ( k e. NN0 -> ( F ` k ) = A ) $.
    $( Sum from 0 to infinity in terms of sum from 1 to infinity of a class
       term ` A ( k ) ` . $)
    isumnn0nnai $p |- ( ( A. k e. NN0 A e. CC /\ E. x ( + seq1 F ) ~~> x ) ->
                     sum_ k e. NN0 A = ( [_ 0 / k ]_ A + sum_ k e. NN A ) ) $=
      ( vj vz cc wcel cn0 wral cv cfv caddc cseq1 co cli wbr wex csu cc0 csb
      cn wceq eleq1d ralbiia ax-17 hbfv hbel fveq2 cbvral bitr3i wa isumnn0nn
      sumeq2i cbvsumi eqtr3i 0nn0 elisseti hbcsb1 hbeq csbeq1a eqeq12d
      vtoclgaf ax-mp eqcomi nnnn0 syl eqtri opreq12i 3eqtr4g sylanb ) CLMZDNOZJ
      PZEQZLMZJNOZRESTAPUAUBAUCZNCDUDZDUECUFZUGCDUDZRTZUHVRDPZEQZLMZDNOWBWJVQDN
      WHNMZWICLIUIUJWJWADJNWJJUKDBKVTLDBVSEHBPZVSMDUKULZKPZLMDUKUMWHVSUHWIVTLWH
      VSEUNZUIUOUPWBWCUQNVTJUDZUEEQZUGVTJUDZRTZWDWGAJEGURNWIDUDWDWPNWICDIUSBNWI
      VTDJWLWIMJUKZWMWOUTVAWSWGWQWEWRWFRUENMWQWEUHZVBWICUHZXADKUENWNUEMDUKZDBKW
      QWEDBUEEHWLUEMDUKULDKUECUENVBVCXCVDVEWHUEUHWIWQCWEWHUEEUNDUECVFVGIVHVIWRU
      GWIDUDZWFXDWRBUGWIVTDJWTWMWOUTVJUGWICDWHUGMWKXBWHVKIVLUSVMVNVJVOVP $.
      $( [9-Feb-2007] $) $( [2-Nov-2007] $)
  $}

$(
  @{
    @d x A @.  @d k x F @.  @d x N @.
    isumadd.1 @e |- F e. _V @.
    isumadd.2 @e |- G e. _V @.
    @( Infinite sum of the sum of two converging sequences. @)
    isumadd @p |- ( ( N e. ZZ /\ E. x ( <. N , + >. seqz F ) ~~> x /\
                    E. y ( <. N , + >. seqz G ) ~~> y ) ->
               sum_ k e. ( ZZ>= ` N ) ( ( F ` k ) + ( G ` k ) ) =
   ( sum_ k e. ( ZZ>= ` N ) ( F ` k ) + sum_ k e. ( ZZ>= ` N ) ( G ` k ) ) ) @=
      ? @.
      @( [ ?] @) @( [2-Jan-2006] @)
  @}
$)

  ${
    $d j k x F $.  $d j x M $.
    isumclt.1 $e |- F e. _V $.
    $( The sum of a converging infinite series is a complex number. $)
    isumcl $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seqz F ) ~~> x ) ->
                    sum_ k e. ( ZZ>= ` M ) ( F ` k ) e. CC ) $=
      ( cz wcel caddc cop cseqz co cv cli wbr wex cuz cfv csu cc cab cuni crab
      wa cvv visset climcl mpan pm4.71ri abbii df-rab eqtr4i unieqi wreu
      hbreu1 climreu 19.23ai reucl syl syl5eqel isumval eleq1d syl5ibr imp ) DF
      GZDHICJKZALZMNZAOZDPQBLCQBRZSGZVHVJVDVGATZUAZSGVHVLVGASUBZUAZSVKVMVKVFSGZ
      VGUCZATVMVGVPAVGVOVFUDGVGVOAUEZVFUDVEUFUGUHUIVGASUJUKULVHVGASUMZVNSGVGVRA
      VGASUNAVFVEVQUOUPVGASUQURUSVDVIVLSABCDEUTVAVBVC $.
      $( [13-Dec-2005] $)
  $}

  ${
    $d j k x F $.  $d j k x M $.
    isumreclt.1 $e |- F e. _V $.
    $( The sum of a converging real infinite series is a real number. $)
    isumrecl $p |- ( ( M e. ZZ /\ A. k e. ( ZZ>= ` M ) ( F ` k ) e. RR /\
                       E. x ( <. M , + >. seqz F ) ~~> x ) ->
                    sum_ k e. ( ZZ>= ` M ) ( F ` k ) e. RR ) $=
      ( vj cz wcel cv cfv cr cuz wral caddc cop cseqz co cli wbr wex csu wa
      wceq visset isumclim adantlr wi cfz elfzuz imim1i ralimi2 serzrecl
      sylan2 expcom adantl r19.21aiv climrecl 3expa syldan an32s eqeltrd ex
      19.23adv 3impia ) DGHZBIZCJZKHZBDLJZMZDNOCPQZAIZRSZATVIVGBUAZKHZVEVJUBZVM
      VOAVPVMVOVPVMUBVNVLKVEVMVNVLUCVJVLBCDEAUDZUEUFVEVMVJVLKHZVEVMUBVJFIZVKJKH
      ZFVIMZVRVEVJWAVMVPVTFVIVJVSVIHZVTUGVEWBVJVTVJWBVHBDVSUHQZMVTVHVHBVIWCVFWC
      HVFVIHVHVFDVSUIUJUKBCDVSEULUMUNUOUPUFVEVMWAVRVLFVKDVQUQURUSUTVAVBVCVD $.
      $( [25-Dec-2005] $)
  $}

  ${
    $d F k x $.  $d M k x $.  $d N k x $.
    iserzgt0.1 $e |- F e. _V $.
    $( The infinite sum of positive reals is positive.  (Contributed by Paul
       Chapman, 9-Feb-2008.) $)
    iserzgt0 $p |- ( ( N e. ( ZZ>= ` M ) /\
              A. k e. ( ZZ>= ` M ) ( ( F ` k ) e. RR /\ 0 < ( F ` k ) ) /\
              E. x ( <. M , + >. seqz F ) ~~> x ) ->
            0 < sum_ k e. ( ZZ>= ` N ) ( F ` k ) ) $=
      ( cuz cfv wcel cv cr cc0 clt wbr wa wral caddc cop cseqz co cli wex w3a
      c1 csu cle wceq fveq2 eleq1d breq2d anbi12d rcla4va 3adant3 cz eluzelz
      peano2zdi 3ad2ant1 wi peano2uz uztrn expcom syl imim1d simpl syl8
      ralimdv2 imp cc recn adantr ralimi iserzex syl3an2 isumrecl syl111anc
      isumclim2 syl11anc 0re ltle mpan imdistani sumex iserzcmp0 addgtge0 an4s
      syl12anc isum1p breqtrrd ) EDGHZIZBJZCHZKIZLWLMNZOZBWIPZDQRCSTAJZUANAUBZU
      CZLECHZEUDQTZGHZWLBUEZQTZEGHZWLBUEZMWSWTKIZLWTMNZOZXCKIZLXCUFNZLXDMNZWJWP
      XIWRWOXIBEWIWKEUGZWMXGWNXHXMWLWTKWKECUHZUIXMWLWTLMXNUJUKULUMWSXAUNIZWMBXB
      PZXAQRCSTZWQUANAUBZXJWJWPXOWRWJEDEUOZUPUQZWJWPXPWRWJWPXPWJWOWMBWIXBWJWKWI
      IZWOURZWKXBIZWOWMWJYCYAWOWJXAWIIZYCYAURDEUSYCYDYAXAWKDUTVAVBVCZWMWNVDVEVF
      VGUMWPWJWLVHIZBWIPWRXRWOYFBWIWMYFWNWLVIVJZVKABCDEFVLVMZABCXAFVNVOWSXOXQXC
      UANZWMLWLUFNZOZBXBPZXKXTWSXOXRYIXTYHABCXAFVPVQWJWPYLWRWJWPYLWJWOYKBWIXBWJ
      YBYCWOYKYEWMWNYJLKIWMWNYJURVRLWLVSVTWAVEVFVGUMXCBCXAXBWLBWBFWCVOXGXJXHXKX
      LWTXCWDWEWFWSEUNIZYFBXEPZXRXFXDUGWJWPYMWRXSUQWJWPYNWRWJWPYNWJWOYFBWIXEWJY
      BWKXEIZWOYFWJYOYAWOYOWJYAEWKDUTVAVCYGVEVFVGUMYHABCEFWGVOWH $.
      $( [9-Feb-2008] $)
  $}

  ${
    $d j k m n x y C $.  $d j k m n x y F $.  $d j k m n x y M $.
    isummulc1.1 $e |- F e. _V $.
    $( Distribute a constant multiplier into an infinite sum (function value
       version).  See ~ isummulc1ai for class term version. $)
    isummulc1 $p |- ( ( ( M e. ZZ /\ C e. CC ) /\ ( A. k e. ( ZZ>= ` M )
        ( F ` k ) e. CC /\ E. x ( <. M , + >. seqz F ) ~~> x ) )
                   -> ( C x. sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) =
                      sum_ k e. ( ZZ>= ` M ) ( C x. ( F ` k ) ) ) $=
      ( vj vy cz wcel cc wa cv cfv cuz wral caddc cop cseqz co cli wbr wex
      cmul csu ax-17 wceq fveq2 opreq2d cbvsumi copab wi w3a isumclim2 ex
      3ad2ant1 r19.26 eqid oprex fvopab4 rgen mpbiran2 sumex fvex opabex2
      iserzmulc1 syl3an3br syld 3expia imp32 isumclim4 ad2antrr mpd syl5reqr )
      EIJZBKJZLZCMZDNZKJZCEONZPZEQRZDSTZAMZUAUBAUCZLZLZWABVSUDTZCUEWABGMZDNZUDT
      ZGUEZBWAVSCUEZUDTZAWAWLWIGCWEWLJCUFWEWIJGUFWJVRUGWKVSBUDWJVRDUHUIZUJWHWCW
      JWAJHMWLUGLGHUKZSTWOUAUBZWMWOUGZVQWBWFWRVOVPWBWFWRULVOVPWBUMWFWDWNUAUBZWR
      VOVPWFWTULWBVOWFWTACDEFUNUOUPWBVOVPVTVRWQNWIUGZLCWAPZWTWRULXBWBXACWAPVTXA
      CWAUQXACWAGHVRWLWIWAWQWPWQURZBVSUDUSUTVAVBWNBCDWQEWAVSCVCFGHWAWLEOVDVEVFV
      GVHVIVJVOWRWSULVPWGVOWRWSHWLWOGWQEBWKUDUSXCBWNUDUSVKUOVLVMVN $.
      $( [1-Nov-2007] $)
  $}

  ${
    $d k m n x y C $.  $d k m n x y F $.  $d k m n x y M $.
    isummulc1iALT.1 $e |- F e. _V $.
    $( Older ~ isummulc1 proved without using ~ iserzmulc1 . $)
    isummulc1iALT $p |- ( ( ( M e. ZZ /\ C e. CC ) /\ ( A. k e. ( ZZ>= ` M )
        ( F ` k ) e. CC /\ E. x ( <. M , + >. seqz F ) ~~> x ) )
                   -> ( C x. sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) =
                      sum_ k e. ( ZZ>= ` M ) ( C x. ( F ` k ) ) ) $=
      ( vy vn vm cz wcel cc cv cfv cuz wral caddc cop cseqz co cli wbr wex csu
      cmul wceq wi w3a wa visset isumclim opreq2d 3ad2antl1 copab simpl1 simp2
      anim1i fveq2 eleq1d cbvralv serzcl2 3adant2 cfz elfzuz eqid oprex
      fvopab4 anim2i expcom a2i syl5 ralimi2 fvex opabex2 serzmulc1 syl3an3
      eqcomd jca 3coml 3expa r19.21aiva sylan2b 3impb adantr climmulc2
      syl11anc isumclim4 eqtr4d ex 19.23adv 3exp imp43 ) EJKZBLKZCMZDNZLKZCEONZ
      PZEQRZDSTZAMZUAUBZAUCZBWRWPCUDZUETZWRBWPUETZCUDZUFZWMWNWSXDXIUGWMWNWSUHZX
      CXIAXJXCXIXJXCUIZXFBXBUETZXHWMWNXCXFXLUFWSWMXCUIXEXBBUEXBCDEFAUJZUKULUMXK
      WMWTWOWRKGMXGUFUICGUNZSTZXLUAUBZXHXLUFWMWNWSXCUOXKWNXCUIWMHMZXANZLKZXQXON
      ZBXRUETZUFZUIZHWRPZUIZXPXJWNXCWMWNWSUPUQXJYEXCWMWNWSYEWNWSUIYDWMWSWNIMZDN
      ZLKZIWRPZYDWQYHCIWRWOYFUFZWPYGLWOYFDURZUSUTWNYIUIYCHWRWNYIXQWRKZYCYLWNYIY
      CYLWNYIUHZXSYBYLYIXSWNIDEXQFVAVBYMYAXTYIYLWNYHYFXNNBYGUETZUFZUIZIEXQVCTZP
      YAXTUFYHYPIWRYQYFYQKYFWRKZYRYHUGYPYFEXQVDYRYHYPYHYRYPYRYOYHCGYFXGYNWRXNYJ
      WPYGBUEYKULXNVEZBYGUEVFVGVHVIVJVKVLBIDXNEXQFCGWRXGEOVMVNVOVPVQVRVSVTWAWBV
      HWCWDXBBHXAXOEWTDSVFWTXNSVFXMWEWFGXGXLCXNEBWPUEVFYSBXBUEVFWGWFWHWIWJWKWL
      $.
      $( [1-Nov-2007] $)
  $}

  ${
    $d j k x y C $.  $d j x y F $.  $d j k x y M $.
    isummulc1a.1 $e |- A e. _V $.
    isummulc1a.2 $e |- F e. _V $.
    isummulc1a.3 $e |- ( y e. F -> A. k y e. F ) $.
    isummulc1a.4 $e |- ( k e. ( ZZ>= ` M ) -> ( F ` k ) = A ) $.
    $( Distribute a constant multiplier into an infinite sum of a class term
       ` A ( k ) ` .  See ~ isummulc1 for function value version. $)
    isummulc1ai $p |- ( ( ( M e. ZZ /\ C e. CC ) /\ ( A. k e. ( ZZ>= ` M )
        A e. CC /\ E. x ( <. M , + >. seqz F ) ~~> x ) ) ->
     ( C x. sum_ k e. ( ZZ>= ` M ) A ) = sum_ k e. ( ZZ>= ` M ) ( C x. A ) ) $=
      ( vj cc wcel cuz cfv wral caddc cop cseqz co cv cli wbr wex wa cz csu
      cmul wceq eleq1d ralbiia ax-17 hbfv hbel fveq2 cbvral bitr3i anbi1i
      isummulc1 sumeq2i cbvsumi eqtr3i opreq2i opreq2d hbopr 3eqtr4g sylan2b )
      CMNZEGOPZQZGRSFTUAAUBUCUDAUEZUFGUGNDMNUFZLUBZFPZMNZLVJQZVLUFZDVJCEUHZUIUA
      ZVJDCUIUAZEUHZUJVKVQVLVKEUBZFPZMNZEVJQVQWEVIEVJWCVJNZWDCMKUKULWEVPELVJWEL
      UMEBBVOMEBVNFJBUBZVNNEUMUNZWGMNEUMUOWCVNUJZWDVOMWCVNFUPZUKUQURUSVMVRUFDVJ
      VOLUHZUIUAVJDVOUIUAZLUHZVTWBADLFGIUTVSWKDUIVJWDEUHVSWKVJWDCEKVABVJWDVOELW
      GWDNLUMWHWJVBVCVDVJDWDUIUAZEUHWBWMVJWNWAEWFWDCDUIKVEVABVJWNWLELWGWNNLUMEB
      DVOUIWGDNEUMWGUINEUMWHVFWIWDVODUIWJVEVBVCVGVH $.
      $( [2-Nov-2007] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d k x F $.  $d k x G $.  $d j k x y M $.
    isumcmpi.1 $e |- A e. _V $.
    isumcmpi.2 $e |- B e. _V $.
    isumcmpi.3 $e |- F = { <. j , y >. | ( j e. ( ZZ>= ` M ) /\ y = A ) } $.
    isumcmpi.4 $e |- G = { <. j , y >. | ( j e. ( ZZ>= ` M ) /\ y = B ) } $.
    isumcmpi.5 $e |- ( j e. ( ZZ>= ` M ) ->
               ( A e. RR /\ B e. RR /\ A <_ B ) ) $.
    $( Comparison of two infinite sums.  (Contributed by Paul Chapman,
       13-Nov-2007.) $)
    isumcmpii $p |- ( ( M e. ZZ /\ ( E. x ( <. M , + >. seqz F ) ~~> x /\
                       E. x ( <. M , + >. seqz G ) ~~> x ) ) ->
                     sum_ j e. ( ZZ>= ` M ) A <_ sum_ j e. ( ZZ>= ` M ) B ) $=
      ( vk cz wcel caddc cop cseqz co cv cli wbr wex wa cuz cfv csu cle
      isumclim3 ex cr w3a wral r19.26 wceq simp1d copab fveq1i fvopab2 syl5eq
      mpdan eqeltrd rgen ax-17 hbopab1 hbxfreq hbfv hbel fveq2 eleq1d cbvral
      mpbi simp2d mpbir2an simp3d 3brtr4d hbbr breq12d df-3an ralbii mpbir
      sumex fvex fopabex2 iserzcmp mpanr2 expcom syl2and imp ) HOPZHQRZFSTZAUAZ
      UBUCAUDZWLGSTZWNUBUCAUDZUEHUFUGZCEUHZWRDEUHZUIUCZWKWOWMWSUBUCZWQWPWTUBUCZ
      XAWKWOXBABCEFHIKUJUKWKWQXCABDEGHJLUJUKXBXCUEZWKXAXDWKNUAZFUGZULPZXEGUGZUL
      PZXFXHUIUCZUMZNWRUNZXAXLXGXIUEZXJUEZNWRUNZXOXMNWRUNZXJNWRUNZXMXJNWRUOXPXG
      NWRUNZXINWRUNZXGXINWRUOEUAZFUGZULPZEWRUNXRYBEWRXTWRPZYACULYCCULPZYACUPYCY
      DDULPZCDUIUCZMUQZYCYDUEYAXTYCBUAZCUPUEZEBURZUGCXTFYJKUSEBWRCULUTVAVBZYGVC
      VDYBXGENWRYBNVEEAAXFULEAXEFEAFYJKYIEBAVFVGWNXEPEVEZVHZWNULPEVEZVIXTXEUPZY
      AXFULXTXEFVJZVKVLVMXTGUGZULPZEWRUNXSYREWRYCYQDULYCYEYQDUPYCYDYEYFMVNZYCYE
      UEYQXTYCYHDUPUEZEBURZUGDXTGUUALUSEBWRDULUTVAVBZYSVCVDYRXIENWRYRNVEEAAXHUL
      EAXEGEAGUUALYTEBAVFVGYLVHZYNVIYOYQXHULXTXEGVJZVKVLVMVOYAYQUIUCZEWRUNXQUUE
      EWRYCCDYAYQUIYCYDYEYFMVPYKUUBVQVDUUEXJENWRUUENVEEAXFXHUIYMWNUIPEVEUUCVRYO
      YAXFYQXHUIYPUUDVSVLVMVOXKXNNWRXGXIXJVTWAWBWSWTNFGHWRCEWCWRDEWCEBWRCFHUFWD
      ZKWEEBWRDGUUFLWEWFWGWHWIWJ $.
      $( [13-Nov-2007] $)
  $}

  ${
    $d F k x $.  $d M k x $.  $d N k x $.
    isumsplit.1 $e |- M e. ZZ $.
    isumsplit.2 $e |- N e. ( ZZ>= ` M ) $.
    isumsplit.3 $e |- F : ( ZZ>= ` M ) --> CC $.
    isumsplit.4 $e |- E. x ( <. M , + >. seqz F ) ~~> x $.
    $( Split off the first ` N ` terms of an infinite sum.  (Contributed by
       Paul Chapman, 9-Feb-2007.) $)
    isumspliti $p |- sum_ k e. ( ZZ>= ` M ) ( F ` k ) =
                    ( sum_ k e. ( M ... N ) ( F ` k ) +
                      sum_ k e. ( ZZ>= ` ( N + 1 ) ) ( F ` k ) ) $=
      ( cfz co cv cfv csu c1 caddc cuz cmin wceq cz wcel cop cseqz cli wbr cle
      wa eluz1i mpbi simpli peano2z ax-mp cc wf cvv fvex fex mp2an sumex wex
      isumclim2 clim2serzi fsumserz opreq2i breqtrri oprex isumclim eqcomi
      isumcl wral elfzuz ffvelrni syl rgen fsumcl breq2 cla4ev subaddi ) DEJKZB
      LZCMZBNZEOPKZQMWABNZPKZDQMZWABNZWGWBRKZWDSWEWGSWDWHWCTUAZWCPUBCUCKZWHUDUE
      ZWDWHSETUAZWIWLDEUFUEZEWFUAZWLWMUGGDEFUHUIUJEUKULZWJWGEDPUBCUCKZMZRKZWHUD
      WNWJWRUDUEGWGCDEWFUMCUNWFUOUACUOUAHDQUPWFUMUOCUQURZWFWABUSDTUAZWPALZUDUEA
      UTZWPWGUDUEFIABCDWSVAURHVBULWBWQWGRWNWBWQSGBCDEWSVCULVDVEZWHBCWCWSWGWBRVF
      ZVGURVHWGWBWDWTXBWGUMUAFIABCDWSVIURWNWAUMUAZBVSVJWBUMUAGXEBVSVTVSUAVTWFUA
      XEVTDEVKWFUMVTCHVLVMVNWABDEVOURWIWJXAUDUEZAUTZWDUMUAWOWKXGXCXFWKAWHXDXAWH
      WJUDVPVQULABCWCWSVIURVRUIVH $.
      $( [9-Feb-2008] $)
  $}

  ${
    $d F k x $.  $d N k x $.
    isum0split.1 $e |- N e. NN0 $.
    isum0split.2 $e |- F : NN0 --> CC $.
    isum0split.3 $e |- E. x ( + seq0 F ) ~~> x $.
    $( Split off the first ` N ` terms of a 0-based infinite sum.  (Contributed
       by Paul Chapman, 9-Feb-2007.) $)
    isum0spliti $p |- sum_ k e. NN0 ( F ` k ) =
                     ( sum_ k e. ( 0 ... N ) ( F ` k ) +
                       sum_ k e. ( ZZ>= ` ( N + 1 ) ) ( F ` k ) ) $=
      ( cn0 cv cfv csu cc0 cuz cfz co c1 caddc nn0uz sumeq1i 0z eleqtri cc wf
      feq2i mpbi cseq0 cli wbr wex cop cseqz addex cvv wcel nn0ex fex mp2an
      seq0seqz breq1i exbii isumspliti eqtri ) HBICJZBKLMJZVCBKLDNOVCBKDPQOMJVC
      BKQOHVDVCBRSABCLDTDHVDERUAHUBCUCZVDUBCUCFHVDUBCRUDUEQCUFOZAIZUGUHZAUILQUJ
      CUKOZVGUGUHZAUIGVHVJAVFVIVGUGQCULVEHUMUNCUMUNFUOHUBUMCUPUQURUSUTUEVAVB $.
      $( [9-Feb-2008] $)
  $}

$(
  @{
    fsumisumt.1 @e |- F e. _V @.
    fsumisumt.2 @e |- A e. _V @.
    @( A finite sum of nonnegative numbers is less than its limit. @)
    fsumisumt @p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( ZZ>= ` M )
      ( ( F ` k ) e. RR /\ 0 <_ ( F ` k ) ) /\ ( <. M , + >. seqz F ) ~~> A ) ->
                   sum_ k e. ( M ... N ) ( F ` k ) <_ A ) @=
      (  ) ? @.
      @( [ ?] @) @( [25-Dec-2005] @)
  @}
$)

