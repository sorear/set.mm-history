$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Infinite sums (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d m n x y A $.  $d m n x y M $.  $d m n x y k $.
    $( Series sum with an infinite index set (i.e. an infinite series). $)
    dfisum $p |- ( M e. ZZ -> sum_ k e. ( ZZ>= ` M ) A =
       U. { x |
    ( <. M , + >. seqz ( { <. k , y >. | y = A } |` ZZ ) ) ~~> x } ) $=
      ( vm vn cz wcel cfv cv co wceq caddc cseqz wa wrex cab cli c0 cuz csu cfz
      cop copab cres wex wbr cuni df-sum wn fzneuz eqcom sylnib ancoms intnanrd
      cun nrexdv nexdv abn0 necon1bbii sylib uz11 syl6bb anbi1d rexbidv opreq1d
      opeq1 breq1d ceqsrexv bitrd abbidv unieqd uneq12d uncom un0 syl6eq syl5eq
      eqtri ) EHIZEUAJZCDUBWAFKZGKZUCLZMZAKZWCWBNUDZBKCMDBUEHUFZOLZJIZPZGWBUAJZ
      QZFUGZARZWAWLMZWIWFSUHZPZFHQZARZUIZUQZENUDZWHOLZWFSUHZARZUIZABWACDFGUJVTX
      BTXGUQZXGVTWOTXAXGVTWNAUGZUKWOTMVTWNAVTWMFVTWKGWLVTWCWLIZPWEWJXJVTWEUKXJV
      TPWDWAMWEEWBWCULWDWAUMUNUOUPURUSUSXIWOTWNAUTVAVBVTWTXFVTWSXEAVTWSWBEMZWQP
      ZFHQXEVTWRXLFHVTWPXKWQVTWPEWBMXKEWBVCEWBUMVDVEVFWQXEFEHXKWIXDWFSXKWGXCWHO
      WBENVHVGVIVJVKVLVMVNXHXGTUQXGTXGVOXGVPVSVQVR $.
      $( [11-Dec-2005] $)
  $}

  ${
    $d k x y F $.  $d x y M $.
    isumvalt.1 $e |- F e. _V $.
    $( The value of the sum of a series ` F ` from ` M ` to infinity is the
       unique (by ~ climuni ) value it converges to (if it converges).  The sum
       evaluates to the empty set when it doesn't converge, but this shouldn't
       be depended on to indicate non-convergence (because it would presuppose
       a construction-dependent property of complex numbers, i.e. that no
       complex number equals the empty set, which, although by ~ 0ncn is true
       for our specific construction, is not specified by their axioms and may
       fail for some other construction).  Instead, use
       ` -. E. x e. CC ( <. M , + >. seqz F ) ~~> x ` to indicate
       non-convergence. $)
    isumval $p |- ( M e. ZZ ->
            sum_ k e. ( ZZ>= ` M ) ( F ` k ) =
                   U. { x | ( <. M , + >. seqz F ) ~~> x } ) $=
      ( vy cz wcel cuz cfv cv csu caddc cop cseqz co cli wbr cab cuni wceq cres
      copab dfisum addex seqzres2 breq1d abbidv unieqd eqtrd ) DGHZDIJBKCJZBLDM
      NZFKULUABFUCGUBOPZAKZQRZASZTUMCOPZUOQRZASZTAFULBDUDUKUQUTUKUPUSAUKUNURUOQ
      FMBCDUEEUFUGUHUIUJ $.
      $( [11-Dec-2005] $)
  $}

  ${
    $d j x y z F $.  $d x z M $.  $d j k y z $.
    isumvaltf.1 $e |- F e. _V $.
    isumvaltf.2 $e |- ( y e. F -> A. k y e. F ) $.
    $( Version of ~ isumval with a bound-variable hypothesis instead of a
       distinct variable condition. $)
    isumvaltfi $p |- ( M e. ZZ ->
            sum_ k e. ( ZZ>= ` M ) ( F ` k ) =
                   U. { x | ( <. M , + >. seqz F ) ~~> x } ) $=
      ( vj vz cz wcel cuz cfv cv csu caddc cop cseqz co ax-17 cli wbr cab hblem
      cuni hbfv fveq2 cbvsumi isumval syl5eq ) EJKELMZCNZDMZCOUKHNZDMZHOEPQDRSA
      NUAUBAUCUEIUKUMUOCHINZUMKHTCIUNDCBIDGUDUPUNKCTUFULUNDUGUHAHDEFUIUJ $.
      $( [13-Dec-2005] $)
  $}

  ${
    $d y A $.  $d x z F $.  $d k x y z M $.
    isumval2t.1 $e |- A e. _V $.
    isumval2t.2 $e |- F = { <. k , y >. | ( k e. ( ZZ>= ` M ) /\ y = A ) } $.
    $( Sum of ` A ( k ) ` from ` M ` to infinity. $)
    isumval2 $p |- ( M e. ZZ ->
            sum_ k e. ( ZZ>= ` M ) A =
                   U. { x | ( <. M , + >. seqz F ) ~~> x } ) $=
      ( vz cz wcel cuz cfv csu cv caddc cop cseqz wceq cvv co cli wbr cab copab
      cuni wa fveq1i fvopab2 mpan2 syl5eq sumeq2i fvex fopabex2 hbopab1 hbxfreq
      isumvaltfi syl5eqr ) FJKFLMZCDNUSDOZEMZDNFPQERUAAOUBUCAUDUFUSVACDUTUSKZVA
      UTVBBOCSUGZDBUEZMZCUTEVDHUHVBCTKVECSGDBUSCTUIUJUKULAIDEFDBUSCEFLUMHUNDIEV
      DHVCDBIUOUPUQUR $.
      $( [31-Mar-2008] $) $( [2-Jan-2006] $)
  $}

  ${
    $d x A $.  $d x y F $.  $d x M $.  $d k y $.
    isumclimtf.1 $e |- ( y e. F -> A. k y e. F ) $.
    isumclimtf.2 $e |- F e. _V $.
    isumclimtf.3 $e |- A e. _V $.
    $( Version of ~ isumclim with a bound-variable hypothesis instead of a
       distinct variable condition. $)
    isumclimtfi $p |- ( ( M e. ZZ /\ ( <. M , + >. seqz F ) ~~> A ) ->
                   sum_ k e. ( ZZ>= ` M ) ( F ` k ) = A ) $=
      ( vx cz wcel caddc cop cli wbr cfv cv cvv cuni weu cseqz cuz csu crab cab
      co isumvaltfi rabab unieqi syl6eqr wceq wreu wb climeu wa df-reu biantrur
      visset eubii bitr4i sylibr breq2 reuuni2 sylancr ibi sylan9eq ) EJKZELMDU
      AUFZBNOZEUBPCQDPCUCZVHIQZNOZIRUDZSZBVGVJVLIUEZSVNIACDEGFUGVMVOVLIUHUIUJVI
      VNBUKZVIBRKVLIRULZVIVPUMHVIVLITZVQIBVHHUNVQVKRKZVLUOZITVRVLIRUPVLVTIVSVLI
      URUQUSUTVAVLVIIRBVKBVHNVBVCVDVEVF $.
      $( [10-May-2006] $)
  $}

  ${
    $d x k F $.
    isumclimt.1 $e |- F e. _V $.
    isumclimt.2 $e |- A e. _V $.
    $( An infinite sum equals the value its series converges to. $)
    isumclim $p |- ( ( M e. ZZ /\ ( <. M , + >. seqz F ) ~~> A ) ->
                   sum_ k e. ( ZZ>= ` M ) ( F ` k ) = A ) $=
      ( vx cv wcel ax-17 isumclimtfi ) GABCDGHCIBJEFK $.
      $( [25-Dec-2005] $)

    $( An infinite sum equals the value its series converges to. $)
    isum1clim $p |- ( ( + seq1 F ) ~~> A -> sum_ k e. NN ( F ` k ) = A ) $=
      ( caddc cseq1 co cli wbr cn cv cfv csu c1 cuz nnuz sumeq1i cop cseqz wceq
      addex seq1seqz breq1i cz wcel 1z isumclim mpan sylbi syl5eq ) FCGHZAIJZKB
      LCMZBNOPMZUNBNZAKUOUNBQRUMOFSCTHZAIJZUPAUAZULUQAIFCUBDUCUDOUEUFURUSUGABCO
      DEUHUIUJUK $.
      $( [10-Jul-2008] $)
  $}

  ${
    $d x y F $.  $d x M $.  $d k x y $.
    isumclim2tf.1 $e |- ( y e. F -> A. k y e. F ) $.
    isumclim2tf.2 $e |- F e. _V $.
    $( Version of ~ isumclim2 with a bound-variable hypothesis instead of a
       distinct variable condition. $)
    isumclim2f $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seqz F ) ~~> x ) ->
              ( <. M , + >. seqz F ) ~~> sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) $=
      ( cz wcel caddc cop cseqz co cv cli wbr wex cuz cfv csu simpr isumclimtfi
      wa visset breqtrrd ex exlimdv imp ) EHIZEJKDLMZANZOPZAQUJERSCNDSCTZOPZUIU
      LUNAUIULUNUIULUCUJUKUMOUIULUABUKCDEFGAUDUBUEUFUGUH $.
      $( [9-Jan-2006] $)
  $}

  ${
    $d k x y F $.  $d x M $.
    isumclim2t.1 $e |- F e. _V $.
    $( A converging series converges to its infinite sum. $)
    isumclim2 $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seqz F ) ~~> x ) ->
              ( <. M , + >. seqz F ) ~~> sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) $=
      ( vy cv wcel ax-17 isumclim2f ) AFBCDFGCHBIEJ $.
      $( [2-Jan-2006] $)
  $}

  ${
    $d x y z A $.  $d k x y z M $.  $d z F $.
    isumclim3t.1 $e |- A e. _V $.
    isumclim3t.2 $e |- F = { <. k , y >. | ( k e. ( ZZ>= ` M ) /\ y = A ) } $.
    $( A converging series converges to its infinite sum. $)
    isumclim3 $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seqz F ) ~~> x ) ->
                     ( <. M , + >. seqz F ) ~~> sum_ k e. ( ZZ>= ` M ) A ) $=
      ( vz wcel cseqz co cv cli wbr wex wa cuz cfv wceq caddc cop copab opreq2i
      cz csu a1i eqcomd breq1d exbidv biimpar hbopab1 opabex2 isumclim2f syldan
      fvex cvv fvopab2 mpan2 sumeq2i syl6breq syl5eqbr ) FUEJZFUAUBZEKLZAMZNOZA
      PZQZVEVDDMZFRSZJZBMCTQZDBUCZKLZVKCDUFZNEVNVDKHUDZVIVOVKVJVNSZDUFZVPNVCVHV
      OVFNOZAPZVOVSNOVCWAVHVCVTVGAVCVOVEVFNVCVEVOVEVOTVCVQUGUHUIUJUKAIDVNFVMDBI
      ULDBVKCFRUPUMUNUOVKVRCDVLCUQJVRCTGDBVKCUQURUSUTVAVB $.
      $( [9-Jan-2006] $)

    ${
      isumclim4t.3 $e |- B e. _V $.
      $( An infinite sum equals the value its series converges to. $)
      isumclim4 $p |- ( ( M e. ZZ /\ ( <. M , + >. seqz F ) ~~> B ) ->
                     sum_ k e. ( ZZ>= ` M ) A = B ) $=
        ( vz cz wcel caddc cop wa cuz cfv csu cv wceq cseqz co cli wbr wss ssid
        sumeqfv ax-mp copab hbopab1 hbxfreq fvex fopabex2 isumclimtfi syl5eqr )
        FKLFMNEUAUBCUCUDOFPQZBDRZUPDSZEQDRZCUPUPUEUSUQTUPUFABUPUPDEGHUGUHJCDEFD
        JEURUPLASBTOZDAUIHUTDAJUJUKDAUPBEFPULHUMIUNUO $.
        $( [10-May-2006] $)
    $}
  $}

  ${
    $d j x y z A $.  $d j k x y z M $.
    isumclim5t.1 $e |- A e. _V $.
    isumclim5t.2 $e |- F = { <. j , y >. | ( j e. ( ZZ>= ` M ) /\ y =
                      sum_ k e. ( M ... j ) A ) } $.
    $( The sequence of partial finite sums of a converging infinite series
       converge to the infinite sum of the series.  Note that ` j ` must not
       occur in ` A ` . $)
    isumclim5 $p |- ( ( M e. ZZ /\ E. x F ~~> x ) ->
                     F ~~> sum_ k e. ( ZZ>= ` M ) A ) $=
      ( vz cz wcel cv cli wbr wex wa caddc cop wceq cuz cfv copab cseqz co eqid
      csu serzfsum adantr breq1d exbidv biimpar isumclim3 syldan eqbrtrrd ) GKL
      ZFAMZNOZAPZQGRSEMGUAUBZLJMCTQEJUCZUDUEZFUTCEUGZNUPVBFTUSJBCEDVAFGHVAUFZIU
      HZUIUPUSVBUQNOZAPZVBVCNOUPVGUSUPVFURAUPVBFUQNVEUJUKULAJCEVAGHVDUMUNUO $.
      $( [9-Jan-2006] $)
  $}

  ${
    $d F k x $.  $d M x $.
    isumnul.1 $e |- F e. _V $.
    $( The sum of a non-convergent infinite series evaluates to the empty set.
       (Contributed by Paul Chapman, 04-Nov-2007.) $)
    isumnul $p |- ( ( M e. ZZ /\ -. E. x ( <. M , + >. seqz F ) ~~> x ) ->
                  sum_ k e. ( ZZ>= ` M ) ( F ` k ) = (/) ) $=
      ( cz wcel caddc cop cseqz co cv cli wbr wex wn cfv cuni c0 wceq wa adantr
      cuz csu cab isumval wne abn0 biimpi necon1bi unieqd adantl uni0 3eqtrd
      a1i ) DFGZDHICJKALMNZAOZPZUAZDUCQBLCQBUDZUQAUEZRZSRZSUPVAVCTUSABCDEUFUBUS
      VCVDTUPUSVBSURVBSVBSUGURUQAUHUIUJUKULVDSTUTUMUOUN $.
      $( [4-Nov-2007] $)
  $}

  ${
    $d F k x $.  $d K k x $.  $d M k x $.  $d N k x $.
    isumshft.1 $e |- F e. _V $.
    isumshft.2 $e |- K e. ZZ $.
    isumshft.3 $e |- M e. ZZ $.
    isumshft.4 $e |- N = ( M + K ) $.
    $( Index shift of an infinite sum.  (Contributed by Paul Chapman,
       31-Oct-2007.) $)
    isumshfti $p |- sum_ k e. ( ZZ>= ` M ) ( F ` k ) =
                      sum_ k e. ( ZZ>= ` N ) ( F ` ( k - K ) ) $=
      ( vx cfv co caddc cli wbr cz wcel wceq ax-mp cc cuz cv csu cshi cop cseqz
      cmin cab cuni cseq cvv wb visset seqex climshft mp2an eqeltri addex oprex
      zaddcl seqzfval seqshft breq1i eqcomi zcn subadd2i seqeq1 eqtr4i 3bitr4ri
      eqtri mpbir abbii unieqi isumval 3eqtr4i eluzelz shftval sylancr sumeq2i
      syl ) DUAKAUBZBKAUCZEUAKZWABCUDLZKZAUCZWCWACUGLBKZAUCDMUEBUFLZJUBZNOZJUHZ
      UIZEMUEWDUFLZWINOZJUHZUIZWBWFWKWOWJWNJMBECUGLZUJZCUDLZWINOZWRWINOZWNWJCPQ
      ZWIUKQWTXAULGJUMWIWRCUKMBWQUNUOUPWMWSWINWMMWDEUJZWSEPQZWMXCREDCMLZPIDPQZX
      BXEPQHGDCUTUPUQZMWDEPURBCUDUSZVASXDXBXCWSRXGGMBECFVBUPVJVCWHWRWINWHMBDUJZ
      WRXFWHXIRHMBDPURFVASWQDRZWRXIRXJXEEREXEIVDECDXDETQXGEVESXBCTQZGCVESZXFDTQ
      HDVESVFVKMBWQDVGSVHVCVIVLVMXFWBWLRHJABDFVNSXDWFWPRXGJAWDEXHVNSVOWCWEWGAWA
      WCQZXKWATQZWEWGRXLXMWAPQXNEWAVPWAVEVTCWABFVQVRVSVJ $.
      $( [5-Nov-2013] $) $( [31-Oct-2007] $)

    $( Index shift of an infinite sum.  (Contributed by Paul Chapman,
       31-Oct-2007.) $)
    isumshft2i $p |- sum_ k e. ( ZZ>= ` N ) ( F ` k ) =
                       sum_ k e. ( ZZ>= ` M ) ( F ` ( K + k ) ) $=
      ( vx caddc co cfv wceq cz wcel mpan cc zcn ax-mp cop cseqz cv cli wbr wex
      cuz csu zaddcl eqeltri visset isumclim cneg cshi eluzelz shftval4 sylancr
      mp2an syl sumeq2i cvv wb znegcl cmin negsubi eqcomi subadd2i mpbir eqtr2i
      iserzshfti oprex sylbi syl5eqr eqtr4d exlimiv wn c0 isumnul sylnbi syl6eq
      eqid exbii pm2.61i ) EKUABUBLJUCZUDUEZJUFZEUGMAUCZBMAUHZDUGMZCWGKLBMZAUHZ
      NZWEWLJWEWHWDWKEOPZWEWHWDNEDCKLZOIDOPZCOPZWNOPHGDCUIURUJZWDABEFJUKZULQWEW
      KWIWGBCUMZUNLZMZAUHZWDWIXAWJAWGWIPZCRPZWGRPZXAWJNWPXDGCSTZXCWGOPXEDWGUOWG
      SUSCWGBFUPUQUTZWEDKUAWTUBLWDUDUEZXBWDNZWDVAPWEXHVBWRWDVABWTWSEDFWQWPWSOPG
      CVCTEWSKLECVDLZDECWMERPWQESTZXFVEXJDNWNENEWNIVFECDXKXFWODRPHDSTVGVHVIWTWA
      VJTZWOXHXIHWDAWTDBWSUNVKZWRULQVLVMVNVOWFVPZWHXBWKXNWHVQXBWMXNWHVQNWQJABEF
      VRQWFXHJUFZXBVQNZWEXHJXLWBWOXOVPXPHJAWTDXMVRQVSVNXGVTWC $.
      $( [31-Oct-2007] $)
  $}

  ${
    $d j k n x F $.  $d j k n x M $.
    isum1p.1 $e |- F e. _V $.
    $( The infinite sum of a converging infinite series equals the first term
       plus the infinite sum of the rest of it. $)
    isum1p $p |- ( ( M e. ZZ /\ A. k e. ( ZZ>= ` M ) ( F ` k ) e. CC /\
  E. x ( <. ( M + 1 ) , + >. seqz F ) ~~> x ) -> sum_ k e. ( ZZ>= ` M )
   ( F ` k ) = ( ( F ` M ) + sum_ k e. ( ZZ>= ` ( M + 1 ) ) ( F ` k ) ) ) $=
      ( vj vn cv cfv cc wcel wral caddc co cseqz cli wbr wceq wa sylan cz fveq2
      cuz c1 cop wex eleq1d cbvralv peano2z isumclim2 adantlr simpll simpr uzid
      csu rcla4va adantr ad2antrr elfzuz peano2uzr ex syl5 imim1d ralimdv2 a1dd
      cfz imp31 serzcl syl11anc imim1i ralimi2 clt eluzp1l serz1p 3expa sylanl2
      an32s ralrimiva oprex sumex fvex climaddc2 syl22anc isumclim syldan 3impa
      jca syl3an2b ) BHZCIZJKZBDUCIZLDUAKZFHZCIZJKZFWLLZDUDMNZMUEZCONZAHPQAUFZW
      LWJBUODCIZWRUCIZWJBUOZMNZRZWKWPBFWLWIWNRWJWOJWIWNCUBUGUHWMWQXAXFWMWQSZXAW
      TXDPQZXFWMXAXHWQWMWRUAKZXAXHDUIZABCWREUJTUKXGXHSZWMDMUEZCONZXEPQZXFWMWQXH
      ULXKXHXBJKZXIGHZWTIZJKZXPXMIXBXQMNRZSZGXCLZXNXGXHUMXGXOXHWMDWLKWQXODUNWPX
      OFDWLWNDRWOXBJWNDCUBUGUPTUQWMXIWQXHXJURXGYAXHXGXTGXCXGXPXCKZSZXRXSYCYBWPF
      WRXPVFNZLZXRXGYBUMWMWQYBYEWMWQYEYBWMWPWPFWLYDWMWNYDKZWNWLKZWPYFWNXCKZWMYG
      WNWRXPUSWMYHYGDWNUTVAVBVCVDVEVGFCWRXPEVHVIWQWMWPFDXPVFNZLZYBXSWPWPFWLYIWN
      YIKYGWPWNDXPUSVJVKWMYBYJXSWMYBSZXPWLKZDXPVLQZSYJXSYKYLYMDXPUTDXPVMWGYLYMY
      JXSFCDXPEVNVOTVQVPWGVRUQXDXBGWTXMWRWSCOVSXLCOVSXCWJBVTDCWAWBWCXEBCDEXBXDM
      VSWDVIWEWFWH $.
      $( [2-Jan-2006] $)
  $}

  ${
    $d x k F $.
    isumnn0nn.1 $e |- F e. _V $.
    $( Sum from 0 to infinity in terms of sum from 1 to infinity. $)
    isumnn0nn $p |- ( ( A. k e. NN0 ( F ` k ) e. CC /\
  E. x ( + seq1 F ) ~~> x ) -> sum_ k e. NN0 ( F ` k )
                 = ( ( F ` 0 ) + sum_ k e. NN ( F ` k ) ) ) $=
      ( cv cfv wcel cn0 wral caddc co cli wbr wex cc0 cuz csu c1 cn cseqz cseq1
      cc wa cop wceq nn0uz raleqi seq1seqz ax-1cn addid2i opeq1i opreq1i eqtr4i
      addex breq1i cz isum1p mp3an1 syl2anb sumeq1i nnuz fveq2i opreq2i 3eqtr4g
      exbii 0z ) BECFZUBGZBHIZJCUAKZAEZLMZANZUCOPFZVGBQZOCFZORJKZPFZVGBQZJKZHVG
      BQVPSVGBQZJKVIVHBVNIZVQJUDZCTKZVKLMZANZVOVTUEZVMVHBHVNUFUGVLWEAVJWDVKLVJR
      JUDZCTKWDJCUNDUHWCWHCTVQRJRUIUJZUKULUMUOVEOUPGWBWFWGVFABCODUQURUSHVNVGBUF
      UTWAVSVPJSVRVGBSRPFVRVAVQRPWIVBUMUTVCVD $.
      $( [2-Jan-2006] $)
  $}

  ${
    $d z A $.  $d j x y F $.  $d j k y z $.
    isumnn0nna.1 $e |- A e. _V $.
    isumnn0nna.3 $e |- F e. _V $.
    isumnn0nna.4 $e |- ( y e. F -> A. k y e. F ) $.
    isumnn0nna.5 $e |- ( k e. NN0 -> ( F ` k ) = A ) $.
    $( Sum from 0 to infinity in terms of sum from 1 to infinity of a class
       term ` A ( k ) ` . $)
    isumnn0nnai $p |- ( ( A. k e. NN0 A e. CC /\ E. x ( + seq1 F ) ~~> x ) ->
                     sum_ k e. NN0 A = ( [_ 0 / k ]_ A + sum_ k e. NN A ) ) $=
      ( vj vz cc wcel cn0 cv csu cc0 cn wceq ax-17 wral cfv caddc cseq1 cli wbr
      co wex csb eleq1d ralbiia hbfv hbel fveq2 cbvral bitr3i isumnn0nn sumeq2i
      wa cbvsumi eqtr3i 0nn0 elisseti hbcsb1 hbeq csbeq1a vtoclgaf ax-mp eqcomi
      eqeq12d nnnn0 syl eqtri opreq12i 3eqtr4g sylanb ) CLMZDNUAZJOZEUBZLMZJNUA
      ZUCEUDUGAOUEUFAUHZNCDPZDQCUIZRCDPZUCUGZSVRDOZEUBZLMZDNUAWBWJVQDNWHNMZWICL
      IUJUKWJWADJNWJJTDBKVTLDBVSEHBOZVSMDTULZKOZLMDTUMWHVSSWIVTLWHVSEUNZUJUOUPW
      BWCUSNVTJPZQEUBZRVTJPZUCUGZWDWGAJEGUQNWIDPWDWPNWICDIURBNWIVTDJWLWIMJTZWMW
      OUTVAWSWGWQWEWRWFUCQNMWQWESZVBWICSZXADKQNWNQMDTZDBKWQWEDBQEHWLQMDTULDKQCQ
      NVBVCXCVDVEWHQSWIWQCWEWHQEUNDQCVFVJIVGVHWRRWIDPZWFXDWRBRWIVTDJWTWMWOUTVIR
      WICDWHRMWKXBWHVKIVLURVMVNVIVOVP $.
      $( [9-Feb-2007] $) $( [2-Nov-2007] $)
  $}

$(
  @{
    @d x A @.  @d k x F @.  @d x N @.
    isumadd.1 @e |- F e. _V @.
    isumadd.2 @e |- G e. _V @.
    @( Infinite sum of the sum of two converging sequences. @)
    isumadd @p |- ( ( N e. ZZ /\ E. x ( <. N , + >. seqz F ) ~~> x /\
                    E. y ( <. N , + >. seqz G ) ~~> y ) ->
               sum_ k e. ( ZZ>= ` N ) ( ( F ` k ) + ( G ` k ) ) =
   ( sum_ k e. ( ZZ>= ` N ) ( F ` k ) + sum_ k e. ( ZZ>= ` N ) ( G ` k ) ) ) @=
      ? @.
      @( [ ?] @) @( [2-Jan-2006] @)
  @}
$)

  ${
    $d j k x F $.  $d j x M $.
    isumclt.1 $e |- F e. _V $.
    $( The sum of a converging infinite series is a complex number. $)
    isumcl $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seqz F ) ~~> x ) ->
                    sum_ k e. ( ZZ>= ` M ) ( F ` k ) e. CC ) $=
      ( cz wcel caddc cop cseqz co cv cli wbr wex cfv cc cab cuni cvv wa visset
      cuz csu crab climcl mpan pm4.71ri abbii df-rab eqtr4i unieqi wreu climreu
      hbreu1 exlimi reucl syl syl5eqel isumval eleq1d syl5ibr imp ) DFGZDHICJKZ
      ALZMNZAOZDUCPBLCPBUDZQGZVHVJVDVGARZSZQGVHVLVGAQUEZSZQVKVMVKVFQGZVGUAZARVM
      VGVPAVGVOVFTGVGVOAUBZVFTVEUFUGUHUIVGAQUJUKULVHVGAQUMZVNQGVGVRAVGAQUOAVFVE
      VQUNUPVGAQUQURUSVDVIVLQABCDEUTVAVBVC $.
      $( [13-Dec-2005] $)
  $}

  ${
    $d j k x F $.  $d j k x M $.
    isumreclt.1 $e |- F e. _V $.
    $( The sum of a converging real infinite series is a real number. $)
    isumrecl $p |- ( ( M e. ZZ /\ A. k e. ( ZZ>= ` M ) ( F ` k ) e. RR /\
                       E. x ( <. M , + >. seqz F ) ~~> x ) ->
                    sum_ k e. ( ZZ>= ` M ) ( F ` k ) e. RR ) $=
      ( vj cz wcel cv cfv cr cuz wral caddc cop cseqz co cli wa adantlr wbr wex
      csu wceq visset isumclim cfz elfzuz imim1i ralimi2 serzrecl sylan2 expcom
      wi adantl ralrimiv climrecl 3expa syldan an32s eqeltrd ex exlimdv 3impia
      ) DGHZBIZCJZKHZBDLJZMZDNOCPQZAIZRUAZAUBVIVGBUCZKHZVEVJSZVMVOAVPVMVOVPVMSV
      NVLKVEVMVNVLUDVJVLBCDEAUEZUFTVEVMVJVLKHZVEVMSVJFIZVKJKHZFVIMZVRVEVJWAVMVP
      VTFVIVJVSVIHZVTUNVEWBVJVTVJWBVHBDVSUGQZMVTVHVHBVIWCVFWCHVFVIHVHVFDVSUHUIU
      JBCDVSEUKULUMUOUPTVEVMWAVRVLFVKDVQUQURUSUTVAVBVCVD $.
      $( [25-Dec-2005] $)
  $}

  ${
    $d F k x $.  $d M k x $.  $d N k x $.
    iserzgt0.1 $e |- F e. _V $.
    $( The infinite sum of positive reals is positive.  (Contributed by Paul
       Chapman, 9-Feb-2008.) $)
    iserzgt0 $p |- ( ( N e. ( ZZ>= ` M ) /\
              A. k e. ( ZZ>= ` M ) ( ( F ` k ) e. RR /\ 0 < ( F ` k ) ) /\
              E. x ( <. M , + >. seqz F ) ~~> x ) ->
            0 < sum_ k e. ( ZZ>= ` N ) ( F ` k ) ) $=
      ( cuz cfv wcel cr cc0 clt wbr wa wral caddc co cli 3adant3 wi cop wex w3a
      cv cseqz c1 csu cle wceq fveq2 eleq1d breq2d anbi12d rcla4va cz peano2zdi
      eluzelz 3ad2ant1 peano2uz uztrn expcom syl imim1d simpl syl8 ralimdv2 imp
      cc recn adantr iserzex syl3an2 isumrecl syl111anc isumclim2 syl11anc ltle
      ralimi 0re mpan imdistani sumex iserzcmp0 addgtge0 an4s syl12anc breqtrrd
      isum1p ) EDGHZIZBUDZCHZJIZKWLLMZNZBWIOZDPUACUEQAUDZRMAUBZUCZKECHZEUFPQZGH
      ZWLBUGZPQZEGHZWLBUGZLWSWTJIZKWTLMZNZXCJIZKXCUHMZKXDLMZWJWPXIWRWOXIBEWIWKE
      UIZWMXGWNXHXMWLWTJWKECUJZUKXMWLWTKLXNULUMUNSWSXAUOIZWMBXBOZXAPUACUEQZWQRM
      AUBZXJWJWPXOWRWJEDEUQZUPURZWJWPXPWRWJWPXPWJWOWMBWIXBWJWKWIIZWOTZWKXBIZWOW
      MWJYCYAWOWJXAWIIZYCYATDEUSYCYDYAXAWKDUTVAVBVCZWMWNVDVEVFVGSWPWJWLVHIZBWIO
      WRXRWOYFBWIWMYFWNWLVIVJZVRABCDEFVKVLZABCXAFVMVNWSXOXQXCRMZWMKWLUHMZNZBXBO
      ZXKXTWSXOXRYIXTYHABCXAFVOVPWJWPYLWRWJWPYLWJWOYKBWIXBWJYBYCWOYKYEWMWNYJKJI
      WMWNYJTVSKWLVQVTWAVEVFVGSXCBCXAXBWLBWBFWCVNXGXJXHXKXLWTXCWDWEWFWSEUOIZYFB
      XEOZXRXFXDUIWJWPYMWRXSURWJWPYNWRWJWPYNWJWOYFBWIXEWJYBWKXEIZWOYFWJYOYAWOYO
      WJYAEWKDUTVAVCYGVEVFVGSYHABCEFWHVNWG $.
      $( [9-Feb-2008] $)
  $}

  ${
    $d j k m n x y C $.  $d j k m n x y F $.  $d j k m n x y M $.
    isummulc1.1 $e |- F e. _V $.
    $( Distribute a constant multiplier into an infinite sum (function value
       version).  See ~ isummulc1ai for class term version. $)
    isummulc1 $p |- ( ( ( M e. ZZ /\ C e. CC ) /\ ( A. k e. ( ZZ>= ` M )
        ( F ` k ) e. CC /\ E. x ( <. M , + >. seqz F ) ~~> x ) )
                   -> ( C x. sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) =
                      sum_ k e. ( ZZ>= ` M ) ( C x. ( F ` k ) ) ) $=
      ( vj vy wcel wa cv cfv wral co cli wbr cmul csu wceq wi cz cc caddc cseqz
      cuz cop wex ax-17 fveq2 opreq2d cbvsumi copab w3a isumclim2 3ad2ant1 eqid
      r19.26 oprex fvopab4 rgen mpbiran2 fvex opabex2 iserzmulc1 syl3an3br syld
      ex sumex 3expia imp32 isumclim4 ad2antrr mpd syl5reqr ) EUAIZBUBIZJZCKZDL
      ZUBIZCEUELZMZEUCUFZDUDNZAKZOPAUGZJZJZWABVSQNZCRWABGKZDLZQNZGRZBWAVSCRZQNZ
      AWAWLWIGCWEWLICUHWEWIIGUHWJVRSWKVSBQWJVRDUIUJZUKWHWCWJWAIHKWLSJGHULZUDNWO
      OPZWMWOSZVQWBWFWRVOVPWBWFWRTVOVPWBUMWFWDWNOPZWRVOVPWFWTTWBVOWFWTACDEFUNVG
      UOWBVOVPVTVRWQLWISZJCWAMZWTWRTXBWBXACWAMVTXACWAUQXACWAGHVRWLWIWAWQWPWQUPZ
      BVSQURUSUTVAWNBCDWQEWAVSCVHFGHWAWLEUEVBVCVDVEVFVIVJVOWRWSTVPWGVOWRWSHWLWO
      GWQEBWKQURXCBWNQURVKVGVLVMVN $.
      $( [1-Nov-2007] $)
  $}

  ${
    $d k m n x y C $.  $d k m n x y F $.  $d k m n x y M $.
    isummulc1iALT.1 $e |- F e. _V $.
    $( Older ~ isummulc1 proved without using ~ iserzmulc1 . $)
    isummulc1iALT $p |- ( ( ( M e. ZZ /\ C e. CC ) /\ ( A. k e. ( ZZ>= ` M )
        ( F ` k ) e. CC /\ E. x ( <. M , + >. seqz F ) ~~> x ) )
                   -> ( C x. sum_ k e. ( ZZ>= ` M ) ( F ` k ) ) =
                      sum_ k e. ( ZZ>= ` M ) ( C x. ( F ` k ) ) ) $=
      ( vy vn vm wcel cc cv cfv wral cseqz co cmul wceq wa oprex cz cuz cop cli
      caddc wbr wex csu wi visset isumclim opreq2d 3ad2antl1 copab simpl1 simp2
      w3a anim1i fveq2 eleq1d cbvralv serzcl2 3adant2 cfz elfzuz fvopab4 anim2i
      eqid expcom a2i syl5 ralimi2 opabex2 serzmulc1 syl3an3 eqcomd 3coml 3expa
      jca ralrimiva sylan2b 3impb adantr climmulc2 syl11anc isumclim4 eqtr4d ex
      fvex exlimdv 3exp imp43 ) EUAJZBKJZCLZDMZKJZCEUBMZNZEUEUCZDOPZALZUDUFZAUG
      ZBWRWPCUHZQPZWRBWPQPZCUHZRZWMWNWSXDXIUIWMWNWSUQZXCXIAXJXCXIXJXCSZXFBXBQPZ
      XHWMWNXCXFXLRWSWMXCSXEXBBQXBCDEFAUJZUKULUMXKWMWTWOWRJGLXGRSCGUNZOPZXLUDUF
      ZXHXLRWMWNWSXCUOXKWNXCSWMHLZXAMZKJZXQXOMZBXRQPZRZSZHWRNZSZXPXJWNXCWMWNWSU
      PURXJYEXCWMWNWSYEWNWSSYDWMWSWNILZDMZKJZIWRNZYDWQYHCIWRWOYFRZWPYGKWOYFDUSZ
      UTVAWNYISYCHWRWNYIXQWRJZYCYLWNYIYCYLWNYIUQZXSYBYLYIXSWNIDEXQFVBVCYMYAXTYI
      YLWNYHYFXNMBYGQPZRZSZIEXQVDPZNYAXTRYHYPIWRYQYFYQJYFWRJZYRYHUIYPYFEXQVEYRY
      HYPYHYRYPYRYOYHCGYFXGYNWRXNYJWPYGBQYKULXNVHZBYGQTVFVGVIVJVKVLBIDXNEXQFCGW
      RXGEUBWIVMVNVOVPVSVQVRVTWAVGWBWCXBBHXAXOEWTDOTWTXNOTXMWDWEGXGXLCXNEBWPQTY
      SBXBQTWFWEWGWHWJWKWL $.
      $( [1-Nov-2007] $)
  $}

  ${
    $d j k x y C $.  $d j x y F $.  $d j k x y M $.
    isummulc1a.1 $e |- A e. _V $.
    isummulc1a.2 $e |- F e. _V $.
    isummulc1a.3 $e |- ( y e. F -> A. k y e. F ) $.
    isummulc1a.4 $e |- ( k e. ( ZZ>= ` M ) -> ( F ` k ) = A ) $.
    $( Distribute a constant multiplier into an infinite sum of a class term
       ` A ( k ) ` .  See ~ isummulc1 for function value version. $)
    isummulc1ai $p |- ( ( ( M e. ZZ /\ C e. CC ) /\ ( A. k e. ( ZZ>= ` M )
        A e. CC /\ E. x ( <. M , + >. seqz F ) ~~> x ) ) ->
     ( C x. sum_ k e. ( ZZ>= ` M ) A ) = sum_ k e. ( ZZ>= ` M ) ( C x. A ) ) $=
      ( vj cc wcel co cv wa csu cmul ax-17 cuz cfv wral caddc cop cseqz cli wbr
      wex cz wceq eleq1d ralbiia hbfv hbel fveq2 cbvral bitr3i anbi1i isummulc1
      sumeq2i cbvsumi eqtr3i opreq2i opreq2d hbopr 3eqtr4g sylan2b ) CMNZEGUAUB
      ZUCZGUDUEFUFOAPUGUHAUIZQGUJNDMNQZLPZFUBZMNZLVJUCZVLQZDVJCERZSOZVJDCSOZERZ
      UKVKVQVLVKEPZFUBZMNZEVJUCVQWEVIEVJWCVJNZWDCMKULUMWEVPELVJWELTEBBVOMEBVNFJ
      BPZVNNETUNZWGMNETUOWCVNUKZWDVOMWCVNFUPZULUQURUSVMVRQDVJVOLRZSOVJDVOSOZLRZ
      VTWBADLFGIUTVSWKDSVJWDERVSWKVJWDCEKVABVJWDVOELWGWDNLTWHWJVBVCVDVJDWDSOZER
      WBWMVJWNWAEWFWDCDSKVEVABVJWNWLELWGWNNLTEBDVOSWGDNETWGSNETWHVFWIWDVODSWJVE
      VBVCVGVH $.
      $( [2-Nov-2007] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d k x F $.  $d k x G $.  $d j k x y M $.
    isumcmpi.1 $e |- A e. _V $.
    isumcmpi.2 $e |- B e. _V $.
    isumcmpi.3 $e |- F = { <. j , y >. | ( j e. ( ZZ>= ` M ) /\ y = A ) } $.
    isumcmpi.4 $e |- G = { <. j , y >. | ( j e. ( ZZ>= ` M ) /\ y = B ) } $.
    isumcmpi.5 $e |- ( j e. ( ZZ>= ` M ) ->
               ( A e. RR /\ B e. RR /\ A <_ B ) ) $.
    $( Comparison of two infinite sums.  (Contributed by Paul Chapman,
       13-Nov-2007.) $)
    isumcmpii $p |- ( ( M e. ZZ /\ ( E. x ( <. M , + >. seqz F ) ~~> x /\
                       E. x ( <. M , + >. seqz G ) ~~> x ) ) ->
                     sum_ j e. ( ZZ>= ` M ) A <_ sum_ j e. ( ZZ>= ` M ) B ) $=
      ( vk wcel wbr wa cle cr wral cz caddc cop cseqz co cv cli wex cuz cfv csu
      isumclim3 ex r19.26 wceq simp1d copab fveq1i fvopab2 syl5eq mpdan eqeltrd
      w3a rgen ax-17 hbopab1 hbfv hbel fveq2 eleq1d cbvral mpbi simp2d mpbir2an
      hbxfreq simp3d 3brtr4d hbbr breq12d df-3an ralbii mpbir fopabex2 iserzcmp
      sumex fvex mpanr2 expcom syl2and imp ) HUAOZHUBUCZFUDUEZAUFZUGPAUHZWLGUDU
      EZWNUGPAUHZQHUIUJZCEUKZWRDEUKZRPZWKWOWMWSUGPZWQWPWTUGPZXAWKWOXBABCEFHIKUL
      UMWKWQXCABDEGHJLULUMXBXCQZWKXAXDWKNUFZFUJZSOZXEGUJZSOZXFXHRPZVCZNWRTZXAXL
      XGXIQZXJQZNWRTZXOXMNWRTZXJNWRTZXMXJNWRUNXPXGNWRTZXINWRTZXGXINWRUNEUFZFUJZ
      SOZEWRTXRYBEWRXTWROZYACSYCCSOZYACUOYCYDDSOZCDRPZMUPZYCYDQYAXTYCBUFZCUOQZE
      BUQZUJCXTFYJKUREBWRCSUSUTVAZYGVBVDYBXGENWRYBNVEEAAXFSEAXEFEAFYJKYIEBAVFVO
      WNXEOEVEZVGZWNSOEVEZVHXTXEUOZYAXFSXTXEFVIZVJVKVLXTGUJZSOZEWRTXSYREWRYCYQD
      SYCYEYQDUOYCYDYEYFMVMZYCYEQYQXTYCYHDUOQZEBUQZUJDXTGUUALUREBWRDSUSUTVAZYSV
      BVDYRXIENWRYRNVEEAAXHSEAXEGEAGUUALYTEBAVFVOYLVGZYNVHYOYQXHSXTXEGVIZVJVKVL
      VNYAYQRPZEWRTXQUUEEWRYCCDYAYQRYCYDYEYFMVPYKUUBVQVDUUEXJENWRUUENVEEAXFXHRY
      MWNROEVEUUCVRYOYAXFYQXHRYPUUDVSVKVLVNXKXNNWRXGXIXJVTWAWBWSWTNFGHWRCEWEWRD
      EWEEBWRCFHUIWFZKWCEBWRDGUUFLWCWDWGWHWIWJ $.
      $( [13-Nov-2007] $)
  $}

  ${
    $d F k x $.  $d M k x $.  $d N k x $.
    isumsplit.1 $e |- M e. ZZ $.
    isumsplit.2 $e |- N e. ( ZZ>= ` M ) $.
    isumsplit.3 $e |- F : ( ZZ>= ` M ) --> CC $.
    isumsplit.4 $e |- E. x ( <. M , + >. seqz F ) ~~> x $.
    $( Split off the first ` N ` terms of an infinite sum.  (Contributed by
       Paul Chapman, 9-Feb-2007.) $)
    isumspliti $p |- sum_ k e. ( ZZ>= ` M ) ( F ` k ) =
                    ( sum_ k e. ( M ... N ) ( F ` k ) +
                      sum_ k e. ( ZZ>= ` ( N + 1 ) ) ( F ` k ) ) $=
      ( co cfv caddc cmin wceq wcel cli wbr ax-mp cc mp2an cfz cv csu c1 cuz cz
      cop cseqz cle wa eluz1i mpbi simpli peano2z wf cvv fvex fex wex isumclim2
      sumex clim2serzi fsumserz opreq2i breqtrri oprex isumclim eqcomi ffvelrni
      isumcl wral elfzuz syl rgen fsumcl breq2 cla4ev subaddi ) DEUAJZBUBZCKZBU
      CZEUDLJZUEKWABUCZLJZDUEKZWABUCZWGWBMJZWDNWEWGNWDWHWCUFOZWCLUGCUHJZWHPQZWD
      WHNEUFOZWIWLDEUIQZEWFOZWLWMUJGDEFUKULUMEUNRZWJWGEDLUGCUHJZKZMJZWHPWNWJWRP
      QGWGCDEWFSCUOWFUPOCUPOHDUEUQWFSUPCURTZWFWABVADUFOZWPAUBZPQAUSZWPWGPQFIABC
      DWSUTTHVBRWBWQWGMWNWBWQNGBCDEWSVCRVDVEZWHBCWCWSWGWBMVFZVGTVHWGWBWDWTXBWGS
      OFIABCDWSVJTWNWASOZBVSVKWBSOGXEBVSVTVSOVTWFOXEVTDEVLWFSVTCHVIVMVNWABDEVOT
      WIWJXAPQZAUSZWDSOWOWKXGXCXFWKAWHXDXAWHWJPVPVQRABCWCWSVJTVRULVH $.
      $( [9-Feb-2008] $)
  $}

  ${
    $d F k x $.  $d N k x $.
    isum0split.1 $e |- N e. NN0 $.
    isum0split.2 $e |- F : NN0 --> CC $.
    isum0split.3 $e |- E. x ( + seq0 F ) ~~> x $.
    $( Split off the first ` N ` terms of a 0-based infinite sum.  (Contributed
       by Paul Chapman, 9-Feb-2007.) $)
    isum0spliti $p |- sum_ k e. NN0 ( F ` k ) =
                     ( sum_ k e. ( 0 ... N ) ( F ` k ) +
                       sum_ k e. ( ZZ>= ` ( N + 1 ) ) ( F ` k ) ) $=
      ( cn0 cv cfv csu cc0 cuz co caddc nn0uz cc wf cli cvv cfz sumeq1i eleqtri
      c1 feq2i mpbi cseq0 wbr wex cop cseqz addex wcel nn0ex fex mp2an seq0seqz
      0z breq1i exbii isumspliti eqtri ) HBICJZBKLMJZVCBKLDUANVCBKDUDONMJVCBKON
      HVDVCBPUBABCLDURDHVDEPUCHQCRZVDQCRFHVDQCPUEUFOCUGNZAIZSUHZAUILOUJCUKNZVGS
      UHZAUIGVHVJAVFVIVGSOCULVEHTUMCTUMFUNHQTCUOUPUQUSUTUFVAVB $.
      $( [9-Feb-2008] $)
  $}

$(
  @{
    fsumisumt.1 @e |- F e. _V @.
    fsumisumt.2 @e |- A e. _V @.
    @( A finite sum of nonnegative numbers is less than its limit. @)
    fsumisumt @p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( ZZ>= ` M )
     ( ( F ` k ) e. RR /\ 0 <_ ( F ` k ) ) /\ ( <. M , + >. seqz F ) ~~> A ) ->
                   sum_ k e. ( M ... N ) ( F ` k ) <_ A ) @=
      (  ) ? @.
      @( [ ?] @) @( [25-Dec-2005] @)
  @}
$)

