$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Infinite sums (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d m n x y A $.  $d m n x y M $.  $d m n x y k $.
    $( Series sum with an infinite index set (i.e. an infinite series). $)
    dfisum $p |- ( M e. ZZ -> sum_ k e. ( ZZ> ` M ) A =
       U. { x |
    ( <. M , + >. seq ( { <. k , y >. | y = A } |` ZZ ) ) ~~> x } ) $=
      ( vm vn cz wcel cuz cfv cv cfz co wceq caddc cop copab cres cseqz wa
      wrex wex cab cli wbr cuni cun csu c0 wn fzneuzt eqcom negbii sylib
      ancoms intnanrd nrexdv nexdv abn0 necon1bbii uz11t syl6bb anbi1d rexbidv
      opeq1 opreq1d breq1d ceqsrexv bitrd abbidv unieqd uneq12d uncom un0 eqtr
      syl6eq df-sum syl5eq ) EHIZEJKZFLZGLZMNZOZALZWCWBPQZBLCODBRHSZTNZKIZUAZGW
      BJKZUBZFUCZAUDZWAWLOZWIWFUEUFZUAZFHUBZAUDZUGZUHZEPQZWHTNZWFUEUFZAUDZUGZWA
      CDUIVTXBUJXGUHZXGVTWOUJXAXGVTWNAUCZUKWOUJOVTWNAVTWMFVTWKGWLVTWCWLIZUAWEWJ
      XJVTWEUKZXJVTUAWDWAOZUKXKEWBWCULXLWEWDWAUMUNUOUPUQURUSUSXIWOUJWNAUTVAUOVT
      WTXFVTWSXEAVTWSWBEOZWQUAZFHUBXEVTWRXNFHVTWPXMWQVTWPEWBOXMEWBVBEWBUMVCVDVE
      WQXEFEHXMWIXDWFUEXMWGXCWHTWBEPVFVGVHVIVJVKVLVMXHXGUJUHXGUJXGVNXGVOVPVQABW
      ACDFGVRVS $.
      $( [25-Dec-2005] $) $( [11-Dec-2005] $)
  $}

  ${
    $d k x y F $.  $d x y M $.
    isumvalt.1 $e |- F e. V $.
    $( The value of the sum of a series ` F ` from ` M ` to infinity is the
      unique (by ~ climuni ) value it converges to (if it converges).  The sum
      evaluates to the empty set when it doesn't converge, but this shouldn't
      be depended on to indicate non-convergence (because it would presuppose a
      construction-dependent property of complex numbers, i.e. that no complex
      number equals the empty set, which, although by ~ 0ncn is true for our
      specific construction, is not specified by their axioms and may fail for
      some other construction).  Instead, use
      ` -. E. x e. CC ( <. M , + >. seq F ) ~~> x ` to indicate
      non-convergence.  $)
    isumvalt $p |- ( M e. ZZ ->
            sum_ k e. ( ZZ> ` M ) ( F ` k ) =
                   U. { x | ( <. M , + >. seq F ) ~~> x } ) $=
      ( vy cz wcel cuz cfv cv csu caddc cop wceq copab cres cseqz co cli wbr
      cab cuni dfisum addex seqzres2 breq1d abbidv unieqd eqtrd ) DGHZDIJBKCJZB
      LDMNZFKULOBFPGQRSZAKZTUAZAUBZUCUMCRSZUOTUAZAUBZUCAFULBDUDUKUQUTUKUPUSAUKU
      NURUOTFMBCDUEEUFUGUHUIUJ $.
      $( [28-Dec-2005] $) $( [11-Dec-2005] $)
  $}

  ${
    $d j x y z F $.  $d x z M $.  $d j k y z $.
    isumvaltf.1 $e |- F e. V $.
    isumvaltf.2 $e |- ( y e. F -> A. k y e. F ) $.
    $( Version of ~ isumvalt with a bound-variable hypothesis instead of a
       distinct variable condition. $)
    isumvaltf $p |- ( M e. ZZ ->
            sum_ k e. ( ZZ> ` M ) ( F ` k ) =
                   U. { x | ( <. M , + >. seq F ) ~~> x } ) $=
      ( vj vz cz wcel cuz cfv cv csu caddc cop cseqz co cli wbr cab cuni
      isumvalt ax-17 hblem hbfv fveq2 cbvsum syl5eq ) EJKELMZHNZDMZHOEPQDRSANTU
      AAUBUCUKCNZDMZCOAHDEFUDIUKUOUMCHINZUOKHUECIULDCBIDGUFUPULKCUEUGUNULDUHUIU
      J $.
      $( [12-May-2006] $) $( [13-Dec-2005] $)
  $}

  ${
    $d y A $.  $d x z F $.  $d k x y z M $.
    isumval2t.1 $e |- A e. V $.
    isumval2t.2 $e |- F = { <. k , y >. | ( k e. ( ZZ> ` M ) /\ y = A ) } $.
    $( Sum of ` A ( k ) ` from ` M ` to infinity. $)
    isumval2t $p |- ( M e. ZZ ->
            sum_ k e. ( ZZ> ` M ) A =
                   U. { x | ( <. M , + >. seq F ) ~~> x } ) $=
      ( vz cz wcel cuz cfv cv csu caddc cop cseqz co cli wbr cab cuni wceq wa
      copab cvv fvex funopabex2 eqeltr wal hbopab1 eleq2i albii 3imtr4
      isumvaltf fvopab2 mpan2 fveq1i syl5eq sumeq2i syl5eqr ) FJKFLMZDNZEMZDOFP
      QERSANTUAAUBUCVCCDOAIDEFEVDVCKZBNCUDUEZDBUFZUGHDBVCCFLUHUIUJINZVHKZVJDUKV
      IEKZVKDUKVGDBIULEVHVIHUMZVKVJDVLUNUOUPVCVECDVFVDVHMZCVEVFCUGKVMCUDGDBVCCU
      GUQURVDEVHHUSUTVAVB $.
      $( [?] $) $( [2-Jan-2006] $)
  $}

  ${
    $d x A $.  $d x y F $.  $d x M $.  $d k y $.
    isumclimtf.1 $e |- ( y e. F -> A. k y e. F ) $.
    isumclimtf.2 $e |- F e. V $.
    isumclimtf.3 $e |- A e. V $.
    $( Version of ~ isumclimt with a bound-variable hypothesis instead of a
       distinct variable condition. $)
    isumclimtf $p |- ( ( M e. ZZ /\ ( <. M , + >. seq F ) ~~> A ) ->
                   sum_ k e. ( ZZ> ` M ) ( F ` k ) = A ) $=
      ( vx cz wcel caddc cop cseqz co cli wbr cuz cfv cv csu cvv crab cuni cab
      isumvaltf rabab unieqi syl6eqr wceq wreu wa wb weu climeu df-reu visset
      biantrur eubii bitr4 sylibr jctil breq2 reuuni2 syl ibi sylan9eq ) EJKZEL
      MDNOZBPQZERSCTDSCUAZVIITZPQZIUBUCZUDZBVHVKVMIUEZUDVOIACDEGFUFVNVPVMIUGUHU
      IVJVOBUJZVJBUBKZVMIUBUKZULVJVQUMVJVSVRVJVMIUNZVSIBVIHUOVSVLUBKZVMULZIUNVT
      VMIUBUPVMWBIWAVMIUQURUSUTVAHVBVMVJIUBBVLBVIPVCVDVEVFVG $.
      $( [12-May-2006] $) $( [10-May-2006] $)
  $}

  ${
    $d x k F $.
    isumclimt.1 $e |- F e. V $.
    isumclimt.2 $e |- A e. V $.
    $( An infinite sum equals the value its series converges to. $)
    isumclimt $p |- ( ( M e. ZZ /\ ( <. M , + >. seq F ) ~~> A ) ->
                   sum_ k e. ( ZZ> ` M ) ( F ` k ) = A ) $=
      ( vx cv wcel ax-17 isumclimtf ) GABCDGHCIBJEFK $.
      $( [10-Jan-2006] $) $( [25-Dec-2005] $)
  $}

  ${
    $d x y F $.  $d x M $.  $d k x y $.
    isumclim2tf.1 $e |- ( y e. F -> A. k y e. F ) $.
    isumclim2tf.2 $e |- F e. V $.
    $( Version of ~ isumclim2t with a bound-variable hypothesis instead of a
       distinct variable condition. $)
    isumclim2tf $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seq F ) ~~> x ) ->
              ( <. M , + >. seq F ) ~~> sum_ k e. ( ZZ> ` M ) ( F ` k ) ) $=
      ( cz wcel caddc cop cseqz co cv cli wbr wex cuz cfv csu wa pm3.27 visset
      isumclimtf breqtrrd ex 19.23adv imp ) EHIZEJKDLMZANZOPZAQUJERSCNDSCTZOPZU
      IULUNAUIULUNUIULUAUJUKUMOUIULUBBUKCDEFGAUCUDUEUFUGUH $.
      $( [17-Feb-2006] $) $( [9-Jan-2006] $)
  $}

  ${
    $d k x y F $.  $d x M $.
    isumclim2t.1 $e |- F e. V $.
    $( A converging series converges to its infinite sum. $)
    isumclim2t $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seq F ) ~~> x ) ->
              ( <. M , + >. seq F ) ~~> sum_ k e. ( ZZ> ` M ) ( F ` k ) ) $=
      ( vy cv wcel ax-17 isumclim2tf ) AFBCDFGCHBIEJ $.
      $( [10-Jan-2006] $) $( [2-Jan-2006] $)
  $}

  ${
    $d x y z A $.  $d k x y z M $.  $d z F $.
    isumclim3t.1 $e |- A e. V $.
    isumclim3t.2 $e |- F = { <. k , y >. | ( k e. ( ZZ> ` M ) /\ y = A ) } $.
    $( A converging series converges to its infinite sum. $)
    isumclim3t $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seq F ) ~~> x ) ->
                     ( <. M , + >. seq F ) ~~> sum_ k e. ( ZZ> ` M ) A ) $=
      ( vz cz wcel caddc cop cseqz co cv cli wbr wex wa cuz cfv wceq copab csu
      opreq2i a1i eqcomd breq1d exbidv biimpar hbopab1 fvex funopabex2
      isumclim2tf syldan cvv fvopab2 mpan2 sumeq2i syl6breq syl5eqbr ) FJKZFLMZ
      ENOZAPZQRZASZTZVDDPZFUAUBZKZBPCUCTZDBUDZNOZVKCDUEZVEQVIVOVKVJVNUBZDUEZVPQ
      VCVHVOVFQRZASZVOVRQRVCVTVHVCVSVGAVCVOVEVFQVCVEVOVEVOUCVCEVNVDNHUFZUGUHUIU
      JUKAIDVNFVMDBIULDBVKCFUAUMUNUOUPVKVQCDVLCUQKVQCUCGDBVKCUQURUSUTVAWAVB $.
      $( [17-Feb-2006] $) $( [9-Jan-2006] $)

    ${
      isumclim4t.3 $e |- B e. V $.
      $( An infinite sum equals the value its series converges to. $)
      isumclim4t $p |- ( ( M e. ZZ /\ ( <. M , + >. seq F ) ~~> B ) ->
                     sum_ k e. ( ZZ> ` M ) A = B ) $=
        ( vz cz wcel caddc cop cseqz co cli wbr wa cuz cfv cv csu wceq copab
        wal hbopab1 eleq2i albii 3imtr4 cvv fvex funopabex2 eqeltr isumclimtf
        wss ssid sumeqfv ax-mp syl5eqr ) FKLFMNEOPCQRSFTUAZDUBZEUADUCZCVABDUCZJ
        CDEFJUBZVBVALAUBBUDSZDAUEZLZVHDUFVEELZVIDUFVFDAJUGEVGVEHUHZVIVHDVJUIUJE
        VGUKHDAVABFTULUMUNIUOVAVAUPVCVDUDVAUQABVAVADEGHURUSUT $.
        $( [9-Jun-2006] $) $( [10-May-2006] $)
    $}
  $}

  ${
    $d j x y z A $.  $d j k x y z M $.
    isumclim5t.1 $e |- A e. V $.
    isumclim5t.2 $e |- F = { <. j , y >. | ( j e. ( ZZ> ` M ) /\ y =
                      sum_ k e. ( M ... j ) A ) } $.
    $( The sequence of partial finite sums of a converging infinite series
       converge to the infinite sum of the series.  Note that ` j ` must not
       occur in ` A ` . $)
    isumclim5t $p |- ( ( M e. ZZ /\ E. x F ~~> x ) ->
                     F ~~> sum_ k e. ( ZZ> ` M ) A ) $=
      ( vz cz wcel cv cli wbr wex wa caddc cop cuz cfv wceq copab cseqz co csu
      eqid serzfsum adantr breq1d exbidv biimpar isumclim3t syldan eqbrtrrd )
      GKLZFAMZNOZAPZQGRSEMGTUAZLJMCUBQEJUCZUDUEZFUTCEUFZNUPVBFUBUSJBCEDVAFGHVAU
      GZIUHZUIUPUSVBUQNOZAPZVBVCNOUPVGUSUPVFURAUPVBFUQNVEUJUKULAJCEVAGHVDUMUNUO
      $.
      $( [17-Feb-2006] $) $( [9-Jan-2006] $)
  $}

  ${
    $d j k n x F $.  $d j k n x M $.
    isum1p.1 $e |- F e. V $.
    $( The infinite sum of a converging infinite series equals the first term
       plus the infinite sum of the rest of it. $)
    isum1p $p |- ( ( M e. ZZ /\ A. k e. ( ZZ> ` M ) ( F ` k ) e. CC /\
  E. x ( <. ( M + 1 ) , + >. seq F ) ~~> x ) -> sum_ k e. ( ZZ> ` M ) ( F ` k )
                 = ( ( F ` M ) + sum_ k e. ( ZZ> ` ( M + 1 ) ) ( F ` k ) ) ) $=
      ( vj vn cz wcel cv cfv cc cuz wral c1 caddc co cop cseqz cli wbr wex csu
      wceq wa isumclim2t peano2z sylan adantlr oprex isumclimt simpll sumex
      fvex climaddc2 pm3.27 fveq2 eleq1d rcla4va uzidt adantr jca ad2antrr cfz
      serzclt peano2uzr ex elfzuzt syl5 imim1d r19.20dv2 a1dd imp31 sylanc clt
      serz1p 3expa eluzp1lt an1rs imim1i r19.20i2 sylanl2 r19.21aiva syldan
      3impa cbvralv syl3an2b ) DHIZFJZCKZLIZFDMKZNZDOPQZPRZCSQZAJTUAAUBZWLBJZCK
      ZBUCDCKZWNMKZWSBUCZPQZUDZWSLIZBWLNWHWMWQXDWHWMUEZWQWPXBTUAZXDWHWQXGWMWNHI
      ZWQXGWHABCWNEUFDUGZUHUIWHDPRZCSQZXCTUAZXDXFXGUEZXCBCDEWTXBPUJUKWHWMXGULXG
      WTLIZUEXHGJZWPKZLIZXOXKKWTXPPQUDZUEZGXANZUEXLXMXBWTGWPXKWNWOCSUJXJCSUJXAW
      SBUMDCUNUOXMXGXNXFXGUPXFXNXGDWLIWMXNWHWKXNFDWLWIDUDWJWTLWIDCUQURUSDUTUHVA
      VBXMXHXTWHXHWMXGXIVCXFXTXGXFXSGXAXFXOXAIZUEZXQXRYAWKFWNXOVDQZNZXQYBFCWNXO
      EVEXFYAUPWHWMYAYDWHWMYDYAWHWKWKFWLYCWHWIYCIZWIWLIZWKWHWIXAIZYFYEWHYGYFDWI
      VFVGWIWNXOVHVIVJVKVLVMVNWHWKFDXOVDQZNZYAXRWMWHYAYIXRXOWLIZDXOVOUAZUEYIXRW
      HYAUEZYJYKYIXRFCDXOEVPVQYLYJYKDXOVFDXOVRVBUHVSWKWKFWLYHWIYHIYFWKWIDXOVHVT
      WAWBVBWCVAVBVNVNWDWEXEWKBFWLWRWIUDWSWJLWRWICUQURWFWG $.
      $( [10-Jan-2006] $) $( [2-Jan-2006] $)
  $}

  ${
    $d x k F $.
    isumnn0nn.1 $e |- F e. V $.
    $( Sum from 0 to infinity in terms of sum from 1 to infinity. $)
    isumnn0nn $p |- ( ( A. k e. NN0 ( F ` k ) e. CC /\
  E. x ( + seq1 F ) ~~> x ) -> sum_ k e. NN0 ( F ` k )
                 = ( ( F ` 0 ) + sum_ k e. NN ( F ` k ) ) ) $=
      ( cv cfv cc wcel cn0 wral caddc cseq1 co cli wbr wex wa cc0 cuz csu c1
      cn cop cseqz wceq cz 0z isum1p mp3an1 wb nn0uz raleq1 ax-mp addex
      seq1seqz ax1cn addid2 opeq1i opreq1i eqtr4 breq1i exbii syl2anb sumeq1i
      nnuz fveq2i opreq2i 3eqtr4g ) BECFZGHZBIJZKCLMZAEZNOZAPZQRSFZVIBTZRCFZRUA
      KMZSFZVIBTZKMZIVIBTVRUBVIBTZKMVJBVPJZVSKUCZCUDMZVMNOZAPZVQWBUEZVKVORUFHWD
      WHWIUGABCRDUHUIIVPUEVKWDUJUKVJBIVPULUMVNWGAVLWFVMNVLUAKUCZCUDMWFKCUNDUOWE
      WJCUDVSUAKUAUPUQZURUSUTVAVBVCIVPVIBUKVDWCWAVRKUBVTVIBUBUASFVTVEVSUASWKVFU
      TVDVGVH $.
      $( [10-Jan-2006] $) $( [2-Jan-2006] $)
  $}

$(
  @{
    @d x A @.  @d k x F @.  @d x N @.
    isumadd.1 @e |- F e. V @.
    isumadd.2 @e |- G e. V @.
    @( Infinite sum of the sum of two converging sequences. @)
    isumadd @p |- ( ( N e. ZZ /\ E. x ( <. N , + >. seq F ) ~~> x /\
                    E. y ( <. N , + >. seq G ) ~~> y ) ->
               sum_ k e. ( ZZ> ` N ) ( ( F ` k ) + ( G ` k ) ) =
     ( sum_ k e. ( ZZ> ` N ) ( F ` k ) + sum_ k e. ( ZZ> ` N ) ( G ` k ) ) ) @=
      ? @.
      @( [?] @) @( [2-Jan-2006] @)
  @}
$)

  ${
    $d j k x F $.  $d j x M $.
    isumclt.1 $e |- F e. V $.
    $( The sum of a converging infinite series is a complex number. $)
    isumclt $p |- ( ( M e. ZZ /\ E. x ( <. M , + >. seq F ) ~~> x ) ->
                    sum_ k e. ( ZZ> ` M ) ( F ` k ) e. CC ) $=
      ( cz wcel caddc cop cseqz co cv cli wbr wex cuz cfv csu cc cab cuni
      isumvalt eleq1d crab wreu hbreu1 visset climreu 19.23ai reucl syl wa cvv
      climcl mpan pm4.71ri abbii df-rab eqtr4 unieqi syl5eqel syl5bir imp ) DFG
      ZDHICJKZALZMNZAOZDPQBLCQBRZSGZVDVJVGATZUAZSGVHVDVIVLSABCDEUBUCVHVGASUDZUA
      ZSVLVHVGASUEZVNSGVGVOAVGASUFAVFVEAUGZUHUIVGASUJUKVKVMVKVFSGZVGULZATVMVGVR
      AVGVQVFUMGVGVQVPVFUMVEUNUOUPUQVGASURUSUTVAVBVC $.
      $( [1-Feb-2006] $) $( [13-Dec-2005] $)
  $}

  ${
    $d j k x F $.  $d j k x M $.
    isumreclt.1 $e |- F e. V $.
    $( The sum of a converging real infinite series is a real number. $)
    isumreclt $p |- ( ( M e. ZZ /\ A. k e. ( ZZ> ` M ) ( F ` k ) e. RR /\
                       E. x ( <. M , + >. seq F ) ~~> x ) ->
                    sum_ k e. ( ZZ> ` M ) ( F ` k ) e. RR ) $=
      ( vj cz wcel cv cfv cr cuz wral caddc cop cseqz co cli wbr wex csu wa
      wceq visset isumclimt adantlr wi cfz serzreclt elfzuzt imim1i r19.20i2
      sylan2 expcom adantl r19.21aiv climrecl 3expa syldan an1rs eqeltrd ex
      19.23adv 3impia ) DGHZBIZCJZKHZBDLJZMZDNOCPQZAIZRSZATVIVGBUAZKHZVEVJUBZVM
      VOAVPVMVOVPVMUBVNVLKVEVMVNVLUCVJVLBCDEAUDZUEUFVEVMVJVLKHZVEVMUBVJFIZVKJKH
      ZFVIMZVRVEVJWAVMVPVTFVIVJVSVIHZVTUGVEWBVJVTWBVHBDVSUHQZMVTVJBCDVSEUIVHVHB
      VIWCVFWCHVFVIHVHVFDVSUJUKULUMUNUOUPUFVEVMWAVRVLFVKDVQUQURUSUTVAVBVCVD $.
      $( [28-Jun-2006] $) $( [25-Dec-2005] $)
  $}

$(
  @{
    fsumisumt.1 @e |- F e. V @.
    fsumisumt.2 @e |- A e. V @.
    @( A finite sum of nonnegative numbers is less than its limit. @)
    fsumisumt @p |- ( ( N e. ( ZZ> ` M ) /\ A. k e. ( ZZ> ` M )
      ( ( F ` k ) e. RR /\ 0 <_ ( F ` k ) ) /\ ( <. M , + >. seq F ) ~~> A ) ->
                   sum_ k e. ( M ... N ) ( F ` k ) <_ A ) @=
      (  ) ? @.
      @( [?] @) @( [25-Dec-05] @)
  @}
$)

