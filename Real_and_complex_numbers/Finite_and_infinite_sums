$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Finite and infinite sums
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c sum_ $. $( Summation sign. $)

  $( Extend class notation to include finite summations. (An underscore was
     added to the ASCII token in order to facilitate set.mm text searches,
     since "sum" is is a commonly used word in comments.) $)
  csu $a class sum_ k e. A B $.

  ${
    $d k m n x y $.  $d A m n x y $.  $d B m n x y $.  $d C m n x y $.
    $( Define the sum of a series with an index set of integers ` A ` .
       ` k ` is normally a free variable in ` B ` , i.e. ` B ` can be thought
       of as ` B ( k ) ` .  The definition is meaningful when ` A ` is a finite
       set of sequential integers (representing a finite sum over them) or a
       set of upper integers (representing an infinite sum, when the sum
       converges).  The left-hand side of the union expresses the finite sum
       case, and the right-hand side expresses the infinite sum case.  In
       either case, the other side of the union equals the empty set.
       Examples: ` sum_ k e. ( 2 ... 4 ) ` ` k ` means 2 + 3 + 4 = 9, and
       ` sum_ k e. NN ` ` ( 1 / ( 2 ^ k ) ) ` means 1/2 + 1/4 + 1/8 + ... = 1.
       Note 1:  The restrictions to ` ZZ ` force the class abstractions to be
       sets.  Note 2:  This definition is complex because it is "overloaded"
       with both finite and infinite sums as disjoint sets.  An advantage is
       that equality theorems, for example, need to be proved only once for
       both cases. $)
    df-sum $a |- sum_ k e. A B = (
                     { x | E. m E. n e. ( ZZ>= ` m ) ( A = ( m ... n )
   /\ x e. ( ( <. m , + >. seq ( { <. k , y >. | y = B } |` ZZ ) ) ` n ) ) }
   u. U. { x | E. m e. ZZ ( A = ( ZZ>= ` m ) /\
    ( <. m , + >. seq ( { <. k , y >. | y = B } |` ZZ ) ) ~~> x ) } ) $.

    $( A sum is a set. $)
    sumex $p |- sum_ k e. A B e. V $=
      ( vm vn vx vy csu cv cfz co wceq caddc cop copab cz cres cseqz cfv wcel
      wa cuz wrex wex cab cli wbr cuni cun cvv df-sum cle 2rexuz abbii zex
      fvex anass ancom bitr3i ssab2 eqsstri ssexi abrexex2 eqeltri cc abid2
      axcnex visset climcl mpan adantl ss2abi uniex unex ) ABCHADIZEIZJKLZFIZVP
      VOMNGIBLCGOPQRKZSZTZUAZEVOUBSZUCDUDZFUEZAWCLZVSVRUFUGZUAZDPUCFUEZUHZUIUJF
      GABCDEUKWEWJWEVOVPULUGZWBUAZEPUCZDPUCZFUEUJWDWNFWBDEUMUNWMDFPUOWLEFPUOWLF
      UEZVTVPVSUPWOWAWKVQUAZUAZFUEVTWLWQFWLWPWAUAWQWKVQWAUQWPWAURUSUNWPFVTUTVAV
      BVCVCVDWIWHDFPUOWHFUEVRVETZFUEZWSVEUJFVEVFVGVDWHWRFWGWRWFVRUJTWGWRFVHVRUJ
      VSVIVJVKVLVBVCVMVNVD $.
      $( [23-Dec-2005] $) $( [11-Dec-2005] $)

    $( Equality theorem for a sum. $)
    sumeq1 $p |- ( A = B -> sum_ k e. A C = sum_ k e. B C ) $=
      ( vm vn vx vy wceq cv cfz co caddc cop copab cz cres cseqz cfv wcel wa
      cuz wrex wex cab cli wbr cuni cun csu eqeq1 anbi1d rexbidv exbidv abbidv
      unieqd uneq12d df-sum 3eqtr4g ) ABIZAEJZFJZKLZIZGJZVBVAMNHJCIDHOPQRLZSTZU
      AZFVAUBSZUCZEUDZGUEZAVIIZVFVEUFUGZUAZEPUCZGUEZUHZUIBVCIZVGUAZFVIUCZEUDZGU
      EZBVIIZVNUAZEPUCZGUEZUHZUIACDUJBCDUJUTVLWCVRWHUTVKWBGUTVJWAEUTVHVTFVIUTVD
      VSVGABVCUKULUMUNUOUTVQWGUTVPWFGUTVOWEEPUTVMWDVNABVIUKULUMUOUPUQGHACDEFURG
      HBCDEFURUS $.
      $( [20-Dec-2005] $) $( [11-Dec-2005] $)
  $}

  ${
    $d m n x y z A $.  $d m n x y z B $.  $d m n x y z k $.
    hbsum1.1 $e |- ( x e. A -> A. k x e. A ) $.
    $( Bound-variable hypothesis builder for sum. $)
    hbsum1 $p |- ( x e. sum_ k e. A B -> A. k x e. sum_ k e. A B ) $=
      ( vm vn vy vz csu cv cfz co wceq caddc cop copab cz cres cseqz cfv wcel
      wa cuz wrex wex cab cli wbr cuni cun df-sum ax-17 hbeq hbopab1 hbres
      hbopr hbfv hban hbrex hbex hbab hbbr hbuni hbun hbxfr ) DABCDJBFKZGKZLMZN
      ZHKZVHVGOPZIKCNZDIQZRSZTMZUAUBZUCZGVGUDUAZUEZFUFZHUGZBVSNZVPVKUHUIZUCZFRU
      EZHUGZUJZUKHIBCDFGULDAWBWHWADHAVTDFVRDGVSVHVSUBDUMVJVQDDAABVIEAKZVIUBDUMU
      NDHVHVPDHVLVOTVKVLUBDUMVKTUBDUMDHVNRVMDIHUOVKRUBDUMUPUQVKVHUBDUMURUSUTVAV
      BDAWGWFDHAWEDFRVGRUBDUMWCWDDDAABVSEWIVSUBDUMUNDGVPVKUHDGVLVOTVHVLUBDUMVHT
      UBDUMDGVNRVMDIGUOVHRUBDUMUPUQVHUHUBDUMVHVKUBDUMVCUSUTVBVDVEVF $.
      $( [24-Dec-2005] $) $( [11-Dec-2005] $)
  $}

  ${
    $d m n w y z A $.  $d m n w y z B $.  $d k m n w x y z $.
    hbsum.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbsum.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for sum:  if ` x ` is (effectively)
       not free in ` A ` and ` B ` , it is not free in ` sum_ k e. A B ` . $)
    hbsum $p |- ( y e. sum_ k e. A B -> A. x y e. sum_ k e. A B ) $=
      ( vm vn vz vw csu cv cfz co wceq caddc cop copab cz cres cseqz cfv wcel
      wa cuz wrex wex cab cli wbr cuni cun df-sum ax-17 hbeq hbopab hbres
      hbopr hbfv hban hbrex hbex hbab hbbr hbuni hbun hbxfr ) ABCDELCHMZIMZNOZP
      ZJMZVJVIQRZKMZDPZEKSZTUAZUBOZUCUDZUEZIVIUFUCZUGZHUHZJUIZCWBPZVSVMUJUKZUEZ
      HTUGZJUIZULZUMJKCDEHIUNABWEWKWDAJBWCAHWAAIWBVJWBUDAUOVLVTAABBCVKFBMZVKUDA
      UOUPAJVJVSAJVNVRUBVMVNUDAUOVMUBUDAUOAJVQTVPEKAJABBVODWLVOUDAUOGUPZUQVMTUD
      AUOURUSVMVJUDAUOUTVAVBVCVDABWJWIAJBWHAHTVITUDAUOWFWGAABBCWBFWLWBUDAUOUPAB
      VSVMUJABVNVRUBWLVNUDAUOWLUBUDAUOABVQTVPEKABWMUQWLTUDAUOURUSWLUJUDAUOWLVMU
      DAUOVEVAVBVDVFVGVH $.
      $( [2-Feb-2006] $) $( [11-Dec-2005] $)
  $}

  ${
    $d k m n x y A $.  $d m n x y B $.  $d m n x y C $.
    $( Equality theorem for sum. $)
    sumeq2 $p |- ( A. k e. A B = C -> sum_ k e. A B = sum_ k e. A C ) $=
      ( vm vn vx vy wceq wral cv cfz co caddc cop copab cz cres cseqz cfv wcel
      wa cuz wrex wex cab cli wbr cuni cun csu hbra1 ax-17 ra4 imp eqeq2d
      pm5.32da opabbid resopab 3eqtr4g adantr reseq2 adantl 3eqtr3d opreq2d
      fveq1d adantlr elfzelz fvres syl eqtr4d rgen addex wfun cvv funopabeq
      zex resfunexg mp2an oprex seqzfveq mpan2 ad2antlr 3eqtr4d eleq2d
      rexbidva exbidv abbidv eluzelz fvex seqzeq breq1d unieqd uneq12d df-sum
      ) BCIZDAJZAEKZFKZLMZIZGKZWSWRNOZHKZBIZDHPZQRZSMZTZUAZUBZFWRUCTZUDZEUEZGUF
      ZAXLIZXHXBUGUHZUBZEQUDZGUFZUIZUJXAXBWSXCXDCIZDHPZQRZSMZTZUAZUBZFXLUDZEUEZ
      GUFZXPYEXBUGUHZUBZEQUDZGUFZUIZUJABDUKACDUKWQXOYKYAYPWQXNYJGWQXMYIEWQXKYHF
      XLWQWSXLUAZUBZXAXJYGYRXAUBZXIYFXBYSWSXCXFWTRZSMZTZWSXCYCWTRZSMZTZXIYFWQXA
      UUBUUEIYQWQXAUBZWSUUAUUDUUFYTUUCXCSUUFXFARZYCARZYTUUCWQUUGUUHIZXAWQDKAUAZ
      XEUBZDHPUUJYBUBZDHPUUGUUHWQUUKUULDHWPDAULWQHUMWQUUJXEYBWQUUJUBBCXDWQUUJWP
      WPDAUNUOUPUQURXEDHAUSYBDHAUSUTZVAXAUUGYTIWQAWTXFVBVCXAUUHUUCIWQAWTYCVBVCV
      DVEVFVGYQXIUUBIZWQXAYQXBXGTZXBYTTZIZGWTJUUNUUQGWTXBWTUAZUUOXBXFTZUUPUURXB
      QUAZUUOUUSIZXBWRWSVHZXBQXFVIZVJXBWTXFVIVKVLNGXGYTWRWSVMXFVNZQVOUAZXGVOUAD
      HBVPZVQXFQVOVRVSZUVDWTVOUAZYTVOUAUVFWRWSLVTZXFWTVOVRVSWAWBWCYQYFUUEIZWQXA
      YQXBYDTZXBUUCTZIZGWTJUVJUVMGWTUURUVKXBYCTZUVLUURUUTUVKUVNIZUVBXBQYCVIZVJX
      BWTYCVIVKVLNGYDUUCWRWSVMYCVNZUVEYDVOUADHCVPZVQYCQVOVRVSZUVQUVHUUCVOUAUVRU
      VIYCWTVOVRVSWAWBWCWDWEUQWFWGWHWQXTYOWQXSYNGWQXRYMEQWQWRQUAZUBZXPXQYLUWAXP
      UBZXHYEXBUGUWBXCXFXLRZSMZXCYCXLRZSMZXHYEWQXPUWDUWFIUVTWQXPUBZUWCUWEXCSUWG
      UUGUUHUWCUWEWQUUIXPUUMVAXPUUGUWCIWQAXLXFVBVCXPUUHUWEIWQAXLYCVBVCVDVEVGUVT
      XHUWDIZWQXPUVTUUOXBUWCTZIZGXLJUWHUWJGXLXBXLUAZUUOUUSUWIUWKUUTUVAWRXBWIZUV
      CVJXBXLXFVIVKVLNGXGUWCWRVMUVGUVDXLVOUAZUWCVOUAUVFWRUCWJZXFXLVOVRVSWKWBWCU
      VTYEUWFIZWQXPUVTUVKXBUWETZIZGXLJUWOUWQGXLUWKUVKUVNUWPUWKUUTUVOUWLUVPVJXBX
      LYCVIVKVLNGYDUWEWRVMUVSUVQUWMUWEVOUAUVRUWNYCXLVOVRVSWKWBWCWDWLUQWFWHWMWNG
      HABDEFWOGHACDEFWOUT $.
      $( [20-Dec-2005] $) $( [11-Dec-2005] $)
  $}

  ${
    $d m n x y A $.  $d m n x y B $.  $d m n x y C $.  $d j k m n x y $.
    cbvsum.1 $e |- ( x e. B -> A. k x e. B ) $.
    cbvsum.2 $e |- ( x e. C -> A. j x e. C ) $.
    cbvsum.3 $e |- ( j = k -> B = C ) $.
    $( Change bound variable in a sum. $)
    cbvsumi $p |- sum_ j e. A B = sum_ k e. A C $=
      ( vm vn vy cv cfz co wceq caddc cop copab cz cres cseqz cfv wcel wa cuz
      wrex wex cab cli wbr cuni cun csu hbeleq eqeq2d cbvopab1 reseq1 ax-mp
      opreq2i fveq1i eleq2i anbi2i rexbii exbii abbii breq1i unieqi uneq12i
      df-sum 3eqtr4i ) BJMZKMZNOPZLMZVMVLQRZAMZCPZEASZTUAZUBOZUCZUDZUEZKVLUFUCZ
      UGZJUHZLUIZBWEPZWAVOUJUKZUEZJTUGZLUIZULZUMVNVOVMVPVQDPZFASZTUAZUBOZUCZUDZ
      UEZKWEUGZJUHZLUIZWIWRVOUJUKZUEZJTUGZLUIZULZUMBCEUNBDFUNWHXDWNXIWGXCLWFXBJ
      WDXAKWEWCWTVNWBWSVOVMWAWRVTWQVPUBVSWPPVTWQPVRWOEAFFACGUOEADHUOEMFMPCDVQIU
      PUQVSWPTURUSUTZVAVBVCVDVEVFWMXHWLXGLWKXFJTWJXEWIWAWRVOUJXJVGVCVDVFVHVILAB
      CEJKVJLABDFJKVJVK $.
      $( [20-Dec-2005] $) $( [11-Dec-2005] $)
  $}

  ${
    sumeq1i.1 $e |- A = B $.
    $( Equality inference for sum. $)
    sumeq1i $p |- sum_ k e. A C = sum_ k e. B C $=
      ( wceq csu sumeq1 ax-mp ) ABFACDGBCDGFEABCDHI $.
      $( [10-Jan-2006] $) $( [2-Jan-2006] $)
  $}

  ${
    $d k A $.
    sumeq2i.1 $e |- ( k e. A -> B = C ) $.
    $( Equality inference for sum. $)
    sumeq2i $p |- sum_ k e. A B = sum_ k e. A C $=
      ( wceq csu sumeq2 mprg ) BCFABDGACDGFDAABCDHEI $.
      $( [19-Dec-2005] $) $( [3-Dec-2005] $)
  $}

  ${
    $d k A $.
    sumeq12i.1 $e |- A = B $.
    sumeq12i.2 $e |- ( k e. A -> C = D ) $.
    $( Equality inference for sum.  (Contributed by FL, 10-Dec-2006.) $)
    sumeq12i $p |- sum_ k e. A C = sum_ k e. B D $=
      ( csu sumeq2i sumeq1i eqtri ) ACEHADEHBDEHACDEGIABDEFJK $.
      $( [30-Jan-2007] $) $( [9-Dec-2006] $)
  $}

  ${
    sumeq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for sum. $)
    sumeq1d $p |- ( ph -> sum_ k e. A C = sum_ k e. B C ) $=
      ( wceq csu sumeq1 syl ) ABCGBDEHCDEHGFBCDEIJ $.
      $( [18-Nov-2005] $) $( [1-Nov-2005] $)
  $}

  ${
    $d k A $.
    sumeq2d.1 $e |- ( ph -> A. k e. A B = C ) $.
    $( Equality deduction for sum.  Note that unlike ~ sumeq2dv , ` k ` may
       occur in ` ph ` . $)
    sumeq2d $p |- ( ph -> sum_ k e. A B = sum_ k e. A C ) $=
      ( wceq wral csu sumeq2 syl ) ACDGEBHBCEIBDEIGFBCDEJK $.
      $( [27-Nov-2005] $) $( [1-Nov-2005] $)
  $}

  ${
    $d k A $.  $d k ph $.
    sumeq2dv.1 $e |- ( ( ph /\ k e. A ) -> B = C ) $.
    $( Equality deduction for sum. $)
    sumeq2dv $p |- ( ph -> sum_ k e. A B = sum_ k e. A C ) $=
      ( wceq wral csu r19.21aiva sumeq2 syl ) ACDGZEBHBCEIBDEIGAMEBFJBCDEKL $.
      $( [11-Jan-2006] $) $( [3-Jan-2006] $)
  $}

  ${
    $d k A $.  $d k ph $.
    sumeq2sdv.1 $e |- ( ph -> B = C ) $.
    $( Equality deduction for sum. $)
    sumeq2sdv $p |- ( ph -> sum_ k e. A B = sum_ k e. A C ) $=
      ( wceq cv wcel adantr sumeq2dv ) ABCDEACDGEHBIFJK $.
      $( [11-Jan-2006] $) $( [3-Jan-2006] $)
  $}

  ${
    $d j k A $.  $d k B $.  $d j k ph $.
    2sumeq2dv.1 $e |- ( ( ph /\ j e. A /\ k e. B ) -> C = D ) $.
    $( Equality deduction for double sum. $)
    2sumeq2dv $p |- ( ph ->
           sum_ j e. A sum_ k e. B C = sum_ j e. A sum_ k e. B D ) $=
      ( csu wceq wral cv wcel wa 3expa r19.21aiva sumeq2 r19.20si syl ) ACDGIZC
      EGIZJZFBKZBTFIBUAFIJADEJZGCKZFBKUCAUEFBAFLBMZNUDGCAUFGLCMUDHOPPUEUBFBCDEG
      QRSBTUAFQS $.
      $( [19-Apr-2006] $) $( [3-Jan-2006] $)
  $}

  ${
    $d k A $.  $d k ph $.
    sumeq12dv.1 $e |- ( ph -> A = B ) $.
    sumeq12dv.2 $e |- ( ( ph /\ k e. A ) -> C = D ) $.
    $( Equality deduction for sum. $)
    sumeq12dv $p |- ( ph -> sum_ k e. A C = sum_ k e. B D ) $=
      ( csu sumeq2dv sumeq1d eqtrd ) ABDFIBEFICEFIABDEFHJABCEFGKL $.
      $( [3-Feb-2007] $) $( [1-Dec-2005] $)
  $}

  ${
    $d k B $.  $d k ph $.
    sumeq12rdv.1 $e |- ( ph -> A = B ) $.
    sumeq12rdv.2 $e |- ( ( ph /\ k e. B ) -> C = D ) $.
    $( Equality deduction for sum. $)
    sumeq12rdv $p |- ( ph -> sum_ k e. A C = sum_ k e. B D ) $=
      ( csu sumeq1d sumeq2dv eqtrd ) ABDFICDFICEFIABCDFGJACDEFHKL $.
      $( [1-Feb-2007] $) $( [1-Dec-2005] $)
  $}

  ${
    $d y A $.  $d k y B $.  $d k C $.
    sumeqfv.1 $e |- A e. V $.
    sumeqfv.2 $e |- F = { <. k , y >. | ( k e. B /\ y = A ) } $.
    $( Convert a sum of function values to a sum of classes ` A ( k ) ` . $)
    sumeqfv $p |- ( C (_ B -> sum_ k e. C ( F ` k ) = sum_ k e. C A ) $=
      ( wss cv cfv wcel wa wceq ssel2 copab cvv fvopab2 mpan2 fveq1i syl5eq
      syl sumeq2dv ) DCIZDEJZFKZBEUDUEDLMUECLZUFBNDCUEOUGUEUGAJBNMEAPZKZBUFUGBQ
      LUIBNGEACBQRSUEFUHHTUAUBUC $.
      $( [11-May-2006] $) $( [10-May-2006] $)
  $}

