$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Finite intervals of integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ... $. $( Dots. $)

  $( Extend class notation to include the notation for a contiguous finite set
     of integers.  Read " ` M ... N ` " as "the set of integers from ` M ` to
     ` N ` inclusive." $)
  cfz $a class ... $.

  ${
    $d m n k z $.
    $( Define an operation that produces a finite set of sequential integers.
       Read " ` M ... N ` " as "the set of integers from ` M ` to ` N `
       inclusive."  See ~ fzval for its value and additional comments. $)
    df-fz $a |- ... = { <. <. m , n >. , z >. | ( ( m e. ZZ /\ n e. ZZ ) /\
      z = { k e. ZZ | ( m <_ k /\ k <_ n ) } ) } $.
  $}

  ${
    $d k m n z M $.  $d k m n z N $.
    $( The value of a finite set of sequential integers.  E.g., ` 2 ... 5 `
       means the set ` { 2 , 3 , 4 , 5 } ` .  A special case of this definition
       (starting at 1) appears as Definition 11-2.1 of [Gleason] p. 141, where
       ` NN ` _k means our ` 1 ... k ` ; he calls these sets _segments_ of the
       integers. $)
    fzval $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M ... N ) =
                 { k e. ZZ | ( M <_ k /\ k <_ N ) } ) $=
      ( vm vn vz cz cv cle wbr wa crab cfz zex rabex wceq breq1 anbi1d rabbidv
      breq2 anbi2d df-fz oprabval2 ) DEFBCGGDHZAHZIJZUEEHZIJZKZAGLBUEIJZUECIJZK
      ZAGLMUJUHKZAGLULAGNOUDBPZUIUMAGUNUFUJUHUDBUEIQRSUGCPZUMULAGUOUHUKUJUGCUEI
      TUASFADEUBUC $.
      $( [6-Sep-2005] $)
  $}

  ${
    $d j K $.  $d j M $.  $d j N $.
    $( Membership in a finite set of sequential integers. $)
    elfz1 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( K e. ( M ... N ) <->
                 ( K e. ZZ /\ M <_ K /\ K <_ N ) ) ) $=
      ( vj cz wcel wa cfz co cv cle wbr crab w3a fzval eleq2d wceq breq2 breq1
      anbi12d elrab 3anass bitr4i syl6bb ) BEFCEFGZABCHIZFABDJZKLZUGCKLZGZDEMZF
      ZAEFZBAKLZACKLZNZUEUFUKADBCOPULUMUNUOGZGUPUJUQDAEUGAQUHUNUIUOUGABKRUGACKS
      TUAUMUNUOUBUCUD $.
      $( [21-Jul-2005] $)
  $}

  $( Membership in a finite set of sequential integers. $)
  elfz $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) -> ( K e. ( M ... N ) <->
                 ( M <_ K /\ K <_ N ) ) ) $=
    ( cz wcel cfz co cle wbr wa wb w3a elfz1 3anass baib sylan9bb 3impa 3comr
    ) BDEZCDEZADEZABCFGEZBAHIZACHIZJZKZSTUAUFSTJUBUAUCUDLZUAUEABCMUGUAUEUAUCUDN
    OPQR $.
    $( [29-Sep-2005] $)

  ${
    $d k n m z M $.  $d k n m z N $.
    $( Membership in a finite set of sequential integers.  We use the fact that
       an operation's value is empty outside of its domain to show ` M e. ZZ `
       and ` N e. ZZ ` . $)
    elfz2 $p |- ( N e. A -> ( K e. ( M ... N ) <->
           ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) /\ ( M <_ K /\ K <_ N ) ) ) ) $=
      ( vm vn vz vk wcel cfz co cz wa cle wbr w3a wb elfz1 ibar 3anass syl5bb
      bitrd adantl wn c0 cdm cxp wceq cv crab zex rabex df-fz dmoprab2 ndmoprg
      mp3an1 eleq2d noel pm2.21i simpl pm5.21ni pm2.61dan df-3an anbi1i anass
      bitr2i syl6bb ) DAIZBCDJKZIZCLIZDLIZMZBLIZCBNOZBDNOZMZMZMZVKVLVNPZVQMZVHV
      MVJVSQZVMWBVHVMVJVNVOVPPZVSBCDRVMVRVSWCVMVRSVNVOVPTUAUBUCVHVMUDZMZVJBUEIZ
      VSWEVIUEBJUFLLUGUHVHWDVIUEUHEFGLLEUIHUIZNOWGFUINOMZHLUJJWHHLUKULGHEFUMUNC
      DALLJUOUPUQWDWFVSQVHWFVMVSWFVMBURUSVMVRUTVAUCUBVBWAVMVNMZVQMVSVTWIVQVKVLV
      NVCVDVMVNVQVEVFVG $.
      $( [6-Sep-2005] $)

    $( Lemma for ~ elfzel1 and others. $)
    elfzlem $p |- ( K e. ( M ... N ) ->
                    ( ( M e. ZZ /\ K e. ZZ ) /\ ( M <_ K /\ K <_ N ) ) ) $=
      ( vm vn vz vk cvv wcel cfz co cz wa cle wbr wi w3a elfz2 3simpb anim1i
      syl6bi wn oprprc2 eleq2d brprc anbi2d simpl anim12i cr zre leid syl
      3ad2ant3 adantr jcad anass syl6ib c0 wceq cdm wrel cxp relxp cv crab zex
      rabex df-fz dmoprab2 releqi mpbir oprprc1 pm2.24d n0i syl5 pm2.61i
      syl5bir sylbid ) CHIZABCJKZIZBLIZALIZMZBANOZACNOZMZMZPVSWAWBCLIZWCQZWGMWH
      HABCRWJWDWGWBWIWCSTUAVSUBZWAABBJKZIZWHWKVTWLABCJUCUDWKWHWDWEAANOZMZMZWMWK
      WGWOWDWKWFWNWEACNUEUFUFBHIZWMWPPWQWMWDWEMZWNMWPWQWMWRWNWQWMWBWBWCQZWEABNO
      ZMZMZWRHABBRZWSWDXAWEWBWBWCSWEWTUGUHUAWQWMXBWNXCWSWNXAWCWBWNWBWCAUIIWNAUJ
      AUKULUMUNUAUOWDWEWNUPUQWQUBZWLURUSZUBWPWMXDXEWPBBJJUTZVALLVBZVALLVCXFXGDE
      FLLDVDGVDZNOXHEVDNOMZGLVEJXIGLVFVGFGDEVHVIVJVKVLVMWLAVNVOVPVQVRVP $.
      $( [6-Sep-2005] $)
  $}

  $( Membership in a finite set of sequential integers. $)
  elfz5 $p |- ( ( K e. ( ZZ>= ` M ) /\ N e. ZZ ) ->
                 ( K e. ( M ... N ) <-> K <_ N ) ) $=
    ( cuz cfv wcel cz wa cfz co cle wbr wb elfz 3expa eluzelz eluzel2 jca
    sylan eluzle biantrurd adantr bitr4d ) ABDEFZCGFZHABCIJFZBAKLZACKLZHZUHAGFZ
    BGFZHUEUFUIMZUDUJUKUEULABCNOUDUJUKBAPBAQRSUDUHUIMUEUDUGUHBATUAUBUC $.
    $( [26-Dec-2005] $)

  $( Membership in a finite set of sequential integers. $)
  elfz4 $p |- ( ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) /\ ( M <_ K /\ K <_ N ) )
               -> K e. ( M ... N ) ) $=
    ( cz wcel w3a cle wbr cfz co wi wa elfz1 biimprcd 3exp com4r 3impia imp32
    ) BDEZCDEZADEZFBAGHZACGHZABCIJEZSTUAUBUCUDKKUAUBUCSTLZUDUAUBUCUEUDKUEUDUAUB
    UCFABCMNOPQR $.
    $( [21-Jul-2005] $)

  $( Membership in a finite set of sequential integers in terms of sets of
     upper integers. $)
  elfzuzb $p |- ( N e. A -> ( K e. ( M ... N ) <->
         ( K e. ( ZZ>= ` M ) /\ N e. ( ZZ>= ` K ) ) ) ) $=
    ( wcel cfz co cz w3a cle wbr wa cuz cfv elfz2 an6 eluz2 anbi12i df-3an
    anandir ancom anbi2i 3bitri anbi1i bitr4i 3bitr4ri syl6bb ) DAEBCDFGECHEZDH
    EZBHEZIZCBJKZBDJKZLZLZBCMNEZDBMNEZLZABCDOUHUJULIZUJUIUMIZLUHUJLZUJUILZUNIZU
    RUOUHUJULUJUIUMPUPUSUQUTCBQBDQRUOVAVBLZUNLVCUKVDUNUKUHUILUJLVAUIUJLZLVDUHUI
    UJSUHUIUJTVEVBVAUIUJUAUBUCUDVAVBUNSUEUFUG $.
    $( [18-Sep-2005] $)

  $( Membership in a finite set of sequential integers. $)
  eluzfz $p |- ( ( K e. ( ZZ>= ` M ) /\ N e. ( ZZ>= ` K ) )
               -> K e. ( M ... N ) ) $=
    ( cz wcel cuz cfv wa cfz co eluzelz adantl elfzuzb biimpar mpancom ) CDEZAB
    FGEZCAFGEZHZABCIJEZRPQACKLPTSDABCMNO $.
    $( [4-Oct-2005] $)

  $( Membership in a finite set of sequential integers implies membership in a
     set of upper integers. $)
  elfzuz3 $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> N e. ( ZZ>= ` K ) ) $=
    ( wcel cfz co cuz cfv elfzuzb simplbda ) DAEBCDFGEBCHIEDBHIEABCDJK $.
    $( [28-Sep-2005] $)

  ${
    elfzel2.1 $e |- N e. _V $.
    $( Membership in a finite set of sequential integer implies the upper bound
       is an integer. $)
    elfzel2i $p |- ( K e. ( M ... N ) -> N e. ZZ ) $=
      ( cfz co wcel cz w3a cle wbr wa cvv wb elfz2 ax-mp simpl2 sylbi ) ABCEFGZ
      BHGZCHGZAHGZIBAJKACJKLZLZUACMGSUDNDMABCOPTUAUBUCQR $.
      $( [6-Sep-2005] $)
  $}

  $( Membership in a finite set of sequential integers implies the upper bound
     is an integer. $)
  elfzel2g $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> N e. ZZ ) $=
    ( wcel cfz co cz w3a cle wbr wa elfz2 simpl2 syl6bi imp ) DAEZBCDFGEZDHEZQR
    CHEZSBHEZICBJKBDJKLZLSABCDMTSUAUBNOP $.
    $( [6-Sep-2005] $)

  $( Membership in a finite set of sequential integer implies the lower bound
     is an integer. $)
  elfzel1 $p |- ( K e. ( M ... N ) -> M e. ZZ ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simpll syl ) ABCDEFBGFZAGFZHBAIJACIJHZH
    NABCKNOPLM $.
    $( [6-Sep-2005] $)

  $( A member of a finite set of sequential integer is an integer. $)
  elfzelz $p |- ( K e. ( M ... N ) -> K e. ZZ ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simplr syl ) ABCDEFBGFZAGFZHBAIJACIJHZH
    OABCKNOPLM $.
    $( [6-Sep-2005] $)

  $( A member of a finite set of sequential integer is greater than or equal to
     the lower bound. $)
  elfzle1 $p |- ( K e. ( M ... N ) -> M <_ K ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simprl syl ) ABCDEFBGFAGFHZBAIJZACIJZHH
    OABCKNOPLM $.
    $( [6-Sep-2005] $)

  $( A member of a finite set of sequential integer is less than or equal to
     the upper bound. $)
  elfzle2 $p |- ( K e. ( M ... N ) -> K <_ N ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simprr syl ) ABCDEFBGFAGFHZBAIJZACIJZHH
    PABCKNOPLM $.
    $( [6-Sep-2005] $)

  $( Membership in a finite set of sequential integer implies the bounds are
     comparable. $)
  elfzle3 $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> M <_ N ) $=
    ( wcel cfz co wa cle wbr elfzle1 adantl elfzle2 cz wi elfzel1 elfzelz
    elfzel2g cr letr zre syl3an syl111anc mp2and ) DAEZBCDFGEZHZCBIJZBDIJZCDIJZ
    UFUHUEBCDKLUFUIUEBCDMLUGCNEZBNEZDNEZUHUIHUJOZUFUKUEBCDPLUFULUEBCDQLABCDRCSE
    BSEDSEUNUKULUMCBDTCUABUADUAUBUCUD $.
    $( [18-Sep-2005] $)

  $( Implication of membership in a finite set of sequential integers. $)
  elfzuz2 $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> N e. ( ZZ>= ` M ) ) $=
    ( wcel cfz co wa cuz cfv elfzuzb ancom syl6bb biimpa uztrn syl ) DAEZBCDFGE
    ZHDBIJEZBCIJZEZHZDTEQRUBQRUASHUBABCDKUASLMNBDCOP $.
    $( [20-Sep-2005] $)

  $( Membership in a finite set of sequential integers - special case. $)
  eluzfz1 $p |- ( N e. ( ZZ>= ` M ) -> M e. ( M ... N ) ) $=
    ( cuz cfv wcel cz cle wbr w3a cfz co eluz2 simp1 simp2 cr zre leid syl
    3ad2ant1 simp3 elfz4 syl32anc sylbi ) BACDEAFEZBFEZABGHZIZAABJKEZABLUGUDUEU
    DAAGHZUFUHUDUEUFMZUDUEUFNUJUDUEUIUFUDAOEUIAPAQRSUDUEUFTAABUAUBUC $.
    $( [21-Jul-2005] $)

  $( A member of a finite set of sequential integers belongs to a set of upper
     integers. $)
  elfzuz $p |- ( K e. ( M ... N ) -> K e. ( ZZ>= ` M ) ) $=
    ( cfz co wcel cz cle wbr w3a cuz cfv elfzel1 elfzelz elfzle1 3jca eluz2
    sylibr ) ABCDEFZBGFZAGFZBAHIZJABKLFSTUAUBABCMABCNABCOPBAQR $.
    $( [17-Sep-2005] $)

  $( Membership in a finite set of sequential integers - special case. $)
  eluzfz2 $p |- ( N e. ( ZZ>= ` M ) -> N e. ( M ... N ) ) $=
    ( cuz cfv wcel cz cle wbr cfz co eluzel2 eluzelz eluzle cr zre leid 3syl
    elfz4 syl32anc ) BACDEZAFEBFEZUAABGHBBGHZBABIJEABKABLZUCABMTUABNEUBUCBOBPQB
    ABRS $.
    $( [13-Sep-2005] $)

  $( Membership in a finite set of sequential integers - special case. $)
  eluzfz2b $p |- ( N e. ( ZZ>= ` M ) <-> N e. ( M ... N ) ) $=
    ( cuz cfv wcel cfz co eluzfz2 elfzuz impbii ) BACDEBABFGEABHBABIJ $.
    $( [14-Sep-2005] $)

  $( Membership in a finite set of sequential integers containing one
     integer. $)
  elfz3 $p |- ( N e. ZZ -> N e. ( N ... N ) ) $=
    ( cz wcel cuz cfv cfz co uzid eluzfz1 syl ) ABCAADECAAAFGCAHAAIJ $.
    $( [21-Jul-2005] $)

  $( Membership in a finite set of sequential integers containing one
     integer. $)
  elfz1eq $p |- ( K e. ( N ... N ) -> K = N ) $=
    ( cfz co wcel cz wa cle wbr wceq elfzlem cr wb letri3 zre syl2an biimpar
    eqcomd syl ) ABBCDEBFEZAFEZGZBAHIABHIGZGZABJABBKUDBAUBBAJZUCBLEALEUEUCMTUAB
    ANBOAOPQRS $.
    $( [19-Sep-2005] $)

  ${
    $d K k m $.  $d M k m $.  $d N k m $.
    $( A finite set of sequential integers is empty if the bounds are
       reversed. $)
    fzn $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( N < M <-> ( M ... N ) = (/) ) ) $=
      ( vk cz wcel wa clt wbr cfz co c0 wceq cle wn cr wb lenlt zre syl2an w3a
      cuz cfv eluz2 eluzfz1 sylbir n0i syl 3expia wi cv wex elfzle3 ex
      19.23adv neq0 syl5ib adantl impbid bitr3d con4bid ) ADEZBDEZFZBAGHZABIJZK
      LZVCABMHZVDNZVFNZAOEBOEVGVHPVAVBABQARBRSVCVGVIVAVBVGVIVAVBVGTZAVEEZVIVJBA
      UAUBEVKABUCABUDUEVEAUFUGUHVBVIVGUIVAVBCUJZVEEZCUKVGVIVBVMVGCVBVMVGDVLABUL
      UMUNCVEUOUPUQURUSUT $.
      $( [18-Apr-2006] $) $( [22-Aug-2005] $)

    $( A shifted finite set of sequential integers is equinumerous to the
       original set.  (Contributed by Paul Chapman, 11-Apr-2009.) $)
    fzen $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) ->
                 ( M ... N ) ~~ ( ( M + K ) ... ( N + K ) ) ) $=
      ( vk vm cz wcel w3a cfz co caddc cv cmin cvv oprex a1i cle wbr wi wa
      elfz1 biimpd 3adant3 zaddcl expcom 3ad2ant3 adantrd cr wb leadd1 zre
      syl3an 3com23 3expia imp3a 3adant2 adantld 3coml 3adant1 3jcad syl11anc
      biimprd syld com12 3impb zsubcl leaddsub lesubadd ancoms wceq imp simp1d
      ex anim12d cc subadd eqcom 3bitr3g addcom eqeq2d bitrd zcn 3expib en3d )
      BFGZCFGZAFGZHZDEBCIJZBAKJZCAKJZIJZDLZAKJZELZAMJZWINGWHBCIOPWHWMWIGZWMFGZB
      WMQRZWMCQRZHZWNWLGZWEWFWQXASWGWEWFTZWQXAWMBCUAUBUCZXAWHXBWRWSWTWHXBSWHWRW
      SWTTZTZXBWHXFWNFGZWJWNQRZWNWKQRZHZXBWHXFXGXHXIWHWRXGXEWGWEWRXGSWFWRWGXGWM
      AUDUEUFUGWEWGXFXHSWFWEWGTZWRXEXHWEWGWRXEXHSZWEWRWGXLWEWRWGHZWSXHWTXMWSXHB
      UHGZWMUHGZAUHGZWSXHUIWEWRWGBWMAUJBUKZWMUKZAUKZULUBUGUMUNUOUPWFWGXFXISWEWF
      WGTWRXEXIWFWGWRXEXISZWRWFWGXTWRWFWGHZWTXIWSYAWTXIXOCUHGZXPWTXIUIWRWFWGWMC
      AUJXRCUKZXSULUBUQURUNUOUSUTWHXBXJWHWJFGZWKFGZXBXJUIWEWGYDWFBAUDUPZWFWGYEW
      ECAUDUSZWNWJWKUAVAVBVCVDVEVDVCWHWOWLGZWOFGZWJWOQRZWOWKQRZHZWPWIGZWHYHYLWH
      YDYEYHYLUIYFYGWOWJWKUAVAUBZYLWHYMYIYJYKWHYMSWHYIYJYKTZTZYMWHYPWPFGZBWPQRZ
      WPCQRZHZYMWHYPYQYRYSWHYIYQYOWGWEYIYQSWFYIWGYQWOAVFUEUFUGWEWGYPYRSWFXKYIYO
      YRWEWGYIYOYRSWEWGYIHZYJYRYKUUAYJYRXNXPWOUHGZYJYRUIWEWGYIBAWOVGXQXSWOUKZUL
      UBUGUNUOUPWFWGYPYSSZWEWGWFUUDWGWFTYIYOYSWGWFYIYOYSSZYIWGWFUUEYIWGWFHZYKYS
      YJUUFYSYKUUBXPYBYSYKUIYIWGWFWOACVHUUCXSYCULVBUQURUNUOVIUSUTWEWFYTYMSWGXCY
      MYTWPBCUAVBUCVCVDVEVDVCWHWQYHTWRYITZWMWPVJZWOWNVJZUIZWHWQWRYHYIWHWQWRWHWQ
      TWRWSWTWHWQXAXDVKVLVMWHYHYIWHYHTYIYJYKWHYHYLYNVKVLVMVNWGWEUUGUUJSWFWGWRYI
      UUJYIWGWRUUJWOVOGZAVOGZWMVOGZUUJYIWGWRUUKUULUUMHZUUHWOAWMKJZVJZUUIUUNWPWM
      VJUUOWOVJUUHUUPWOAWMVPWPWMVQUUOWOVQVRUUNUUOWNWOUULUUMUUOWNVJUUKAWMVSUSVTW
      AWOWBAWBWMWBULURWCUFVCWD $.
      $( [11-Apr-2009] $)
  $}

  $( A 1-based finite set of sequential integers is empty iff it ends at index
     ` 0 ` .  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  fz1n $p |- ( N e. NN0 -> ( ( 1 ... N ) = (/) <-> N = 0 ) ) $=
    ( cn0 wcel c1 clt wbr cfz co c0 wceq cc0 cz wb nn0z 1z fzn mpan syl
    nn0lt10b bitr3d ) ABCZADEFZDAGHIJZAKJUAALCZUBUCMZANDLCUDUEODAPQRAST $.
    $( [22-Jun-2011] $)

  $( Two ways to say a finite 1-based sequence is empty.  (Contributed by Paul
     Chapman, 26-Oct-2012.) $)
  0fz1 $p |- ( ( N e. NN0 /\ F Fn ( 1 ... N ) ) -> ( F = (/) <-> N = 0 ) ) $=
    ( c1 cfz co wfn c0 wceq cn0 wcel cc0 fndmu fn0 sylan2br ex fneq2 syl6bb
    biimpcd impbid fz1n sylan9bbr ) ACBDEZFZAGHZUBGHZBIJBKHUCUDUEUCUDUEUCAGFZUE
    UDUBGALAMZNOUEUCUDUEUCUFUDUBGAPUGQRSBTUA $.
    $( [26-Oct-2012] $)

  $( 0-based and 1-based finite sets of sequential integers are equinumerous.
     (Contributed by Paul Chapman, 11-Apr-2009.) $)
  fz01en $p |- ( N e. ZZ -> ( 0 ... ( N - 1 ) ) ~~ ( 1 ... N ) ) $=
    ( cz wcel cc0 c1 cmin co cfz caddc cen wbr peano2zm 0z 1z fzen mp3an13 syl
    wceq ax1cn addid2i a1i cc npcan zcn sylancl opreq12d breq2d mpbid ) ABCZDAE
    FGZHGZDEIGZUJEIGZHGZJKZUKEAHGZJKUIUJBCZUOALDBCUQEBCUOMNEDUJOPQUIUNUPUKJUIUL
    EUMAHULERUIESTUAAUBCEUBCUMARUIAEUCAUDSUEUFUGUH $.
    $( [11-Apr-2009] $)

  $( A member of a finite set of sequential integers starting at 1 is a natural
     number. $)
  elfznn $p |- ( K e. ( 1 ... N ) -> K e. NN ) $=
    ( cn wcel cz c1 cle wbr cfz co elnnz1 elfzelz elfzle1 sylanbrc ) ACDAEDFAGH
    AFBIJDAKAFBLAFBMN $.
    $( [24-Aug-2005] $)

  $( Membership in a finite set of sequential integers starting at 0. $)
  elfz2nn0 $p |- ( N e. A ->
           ( K e. ( 0 ... N ) <-> ( K e. NN0 /\ N e. NN0 /\ K <_ N ) ) ) $=
    ( wcel cc0 cfz co cz w3a cle wbr wa cn0 elfz2 simpr simpl anim12i simpll
    cr wi 0re letr mp3an1 ancoms zre syl2an imp jca simprr 3jca 3adantl1 0z
    a1i 3anim123i 3com13 id adantll 3adant2 impbii elnn0z biid 3anbi123i
    bitr4i syl6bb ) CADBECFGDEHDZCHDZBHDZIZEBJKZBCJKZLZLZBMDZCMDZVJIZABECNVLVGV
    ILZVFECJKZLZVJIZVOVLVSVFVGVKVSVEVFVGLZVKLZVPVRVJVTVGVKVIVFVGOVIVJPQWAVFVQVF
    VGVKRVTVKVQCSDZBSDZVKVQTZVFVGWCWBWDESDWCWBWDUAEBCUBUCUDCUEBUEUFUGUHVTVIVJUI
    UJUKVSVHVKVJVRVPVHVJVEVRVFVPVGVEVJULUMVFVQPVGVIPUNUOVPVJVKVRVIVJVKVGVKUPUQU
    RUHUSVMVPVNVRVJVJBUTCUTVJVAVBVCVD $.
    $( [16-Sep-2005] $)

  $( A member of a finite set of sequential integers starting at 0 is a
     nonnegative integer. $)
  elfznn0 $p |- ( K e. ( 0 ... N ) -> K e. NN0 ) $=
    ( cn0 wcel cz cc0 cle wbr cfz co elnn0z elfzelz elfzle1 sylanbrc ) ACDAEDFA
    GHAFBIJDAKAFBLAFBMN $.
    $( [5-Aug-2005] $)

  $( The upper bound of a nonempty finite set of sequential integers starting
     at 0 is a nonnegative integer. $)
  elfz3nn0 $p |- ( ( N e. A /\ K e. ( 0 ... N ) ) -> N e. NN0 ) $=
    ( wcel cc0 cfz co cn0 cle wbr w3a elfz2nn0 simp2 syl6bi imp ) CADZBECFGDZCH
    DZPQBHDZRBCIJZKRABCLSRTMNO $.
    $( [16-Sep-2005] $)

  $( Subtraction closure for a member of a finite set of sequential
     integers. $)
  fznn0sub $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> ( N - K ) e. NN0 ) $=
    ( wcel cfz co cmin cn0 cz w3a cle wbr wa elfz2 znn0sub2 3com12 3expa
    adantrl 3adantl1 syl6bi imp ) DAEZBCDFGEZDBHGIEZUCUDCJEZDJEZBJEZKCBLMZBDLMZ
    NZNUEABCDOUGUHUKUEUFUGUHNUJUEUIUGUHUJUEUHUGUJUEBDPQRSTUAUB $.
    $( [16-Sep-2005] $)

  $( Subtraction closure for a member of a finite set of sequential
     integers. $)
  fznn0sub2 $p |- ( ( N e. A /\ K e. ( 0 ... N ) ) ->
                   ( N - K ) e. ( 0 ... N ) ) $=
    ( wcel cc0 cfz co cmin cn0 cle wbr w3a wa fznn0sub elfz3nn0 elfzle1 adantl
    cz wb elfzel2g elfzelz cr subge02 zre syl2an syl11anc mpbid 3jca elfz2nn0
    biimpar syldan ) CADZBECFGZDZCBHGZIDZCIDZUOCJKZLZUOUMDZULUNMZUPUQURABECNABC
    OVAEBJKZURUNVBULBECPQVACRDZBRDZVBURSZABECTUNVDULBECUAQCUBDBUBDVEVCVDCBUCCUD
    BUDUEUFUGUHULUTUSAUOCUIUJUK $.
    $( [26-Sep-2005] $)

  $( Membership of a sum in a finite set of sequential integers. $)
  fzaddel $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( J e. ( M ... N ) <-> ( J + K ) e. ( ( M + K ) ... ( N + K ) ) ) ) $=
    ( cz wcel wa cle wbr w3a caddc co cfz wb zaddcl a1d simpl impbid adantl cr
    leadd1 zre syl3an 3expb adantlr 3com12 adantll 3anbi123d elfz1 adantr
    syl2an anandirs adantrl 3bitr4d ) CEFZDEFZGZAEFZBEFZGZGZURCAHIZADHIZJZABKLZ
    EFZCBKLZVEHIZVEDBKLZHIZJZACDMLFZVEVGVIMLFZVAURVFVBVHVCVJUTURVFNUQUTURVFUTVF
    URABOPUTURVFURUSQPRSUOUTVBVHNZUPUOURUSVNCTFATFZBTFZVNUOURUSCABUACUBAUBZBUBZ
    UCUDUEUPUTVCVJNZUOUPURUSVSURUPUSVSVODTFVPVSURUPUSADBUAVQDUBVRUCUFUDUGUHUQVL
    VDNUTACDUIUJUQUSVMVKNZURUOUPUSVTVGEFVIEFVTUOUSGUPUSGVEVGVIUICBODBOUKULUMUN
    $.
    $( [30-Jul-2005] $)

  $( Membership of a difference in a finite set of sequential integers. $)
  fzsubel $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( J e. ( M ... N ) <-> ( J - K ) e. ( ( M - K ) ... ( N - K ) ) ) ) $=
    ( cz wcel wa cfz co cneg caddc cmin wb fzaddel znegcl sylanr2 cc wceq
    negsub adantl opreqan12d anandirs adantrl eleq12d zcn anim12i syl2an bitrd
    ) CEFZDEFZGZAEFZBEFZGZGACDHIFZABJZKIZCUPKIZDUPKIZHIZFZABLIZCBLIZDBLIZHIZFZU
    KULUPEFUOVAMUMAUPCDNBOPCQFZDQFZGZAQFZBQFZGZVAVFMUKUNVIVLGUQVBUTVEVLUQVBRVIA
    BSTVIVKUTVERZVJVGVHVKVMVGVKGVHVKGURVCUSVDHCBSDBSUAUBUCUDUIVGUJVHCUEDUEUFULV
    JUMVKAUEBUEUFUGUH $.
    $( [1-Aug-2006] $) $( [30-Jul-2005] $)

  $( A finite set of sequential integers can represent an ordered pair. $)
  fzopth $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. A ) ->
                ( ( M ... N ) = ( J ... K ) <-> ( M = J /\ N = K ) ) ) $=
    ( cuz cfv wcel wa cfz co wceq eleq2 eluzfz2 syl5bi anim1d elfzuz2 ancoms
    syl6com wi cle wbr eluzfz1 syl5bir elfzle1 adantl adantr jcad cr wb letri3
    cz eluzel2 zre syl syl2an sylibrd elfzle2 anim12ii eluzelz ex com23 mpdd
    opreq12 impbid1 ) EDFGHZCAHZIZDEJKZBCJKZLZDBLZECLZIZVHVKCBFGHZVNVKVHEVJHZVG
    IVOVKVFVPVGVKEVIHVPVFVIVJEMDENOZPVGVPVOAEBCQRSVFVKVOVNTTVGVFVOVKVNVFVOVKVNT
    VFVOIZVKVLVMVRVKDBUAUBZBDUAUBZIZVLVRVKVSVTVOVKVSTVFVKVOBVIHZVSVKWBBVJHVOVIV
    JBMBCUCUDBDEUESUFVFVKVTTVOVKVFDVJHZVTVKDVIHWCVFVIVJDMDEUCODBCUESUGUHDUIHZBU
    IHZVLWAUJVFVODBUKVFDULHWDDEUMDUNUOVOBULHWEBCUMBUNUOUPUQVRVKECUAUBZCEUAUBZIZ
    VMVFVKWFVOWGVKVFVPWFVQEBCURSVKVOCVIHZWGVKWICVJHVOVIVJCMBCNUDCDEURSUSEUIHZCU
    IHZVMWHUJVFVOECUKVFEULHWJDEUTEUNUOVOCULHWKBCUTCUNUOUPUQUHVAVBUGVCDBECJVDVE
    $.
    $( [31-Oct-2005] $)

$(
  @{
    @d j K @.  @d j M @.  @d j N @.
    @( Union of two adjacent finite sets of sequential integers. @)
    fzunt @p |- ( ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) /\ ( K <_ M /\ M <_ N ) )
                 -> ( ( K ... M ) u. ( M ... N ) ) = ( K ... N ) ) @=
      ( vj cz wcel w3a cle wbr wa cfz co cun cv wo cr wb wi letr exp3a com23
      3exp com3l imp42 adantlll adantrl anim2d 3expa imp adantlrl adantrr
      anim1d jaod impbid exp42 com34 ex 3imp zre syl3an syl5 pm5.32d andi
      bicomi syl5bb elfz1 3anass syl6bb 3adant3 adantr 3adant1 orbi12d
      3adant2 3bitr4d elun eqrdv ) AEFZBEFZCEFZGZABHIZBCHIZJZJZDABKLZBCKLZMZACK
      LZWDDNZWEFZWIWFFZOZWIWHFZWIWGFWDWIEFZAWIHIZWIBHIZJZJZWNBWIHIZWICHIZJZJZOZ
      WNWOWTJZJZWLWMWDWNWQXAOZJZXEXCWDWNXFXDWDWIPFZXFXDQZWNVTWCXHXIRZAPFZBPFZCP
      FZWCXJRZVQVRVSXKXLXMXNXKXLXMXNRXKXLJZXMXHWCXIXOXMXHWCXIXOXMXHJZJZWCJZXFXD
      XRWQXDXAXRWPWTWOXQWBWPWTRZWAXLXPWBXSXKXLXMXHWBXSXHXLXMWBXSRZXHXLXMXTXHXLX
      MGZWPWBWTYAWPWBWTWIBCSTUAUBUCUDUEUFUGXRWSWOWTXQWAWSWORZWBXOXHWAYBXMXOXHJZ
      WAYBYCWAWSWOXKXLXHWAWSJWORABWISUHTUIUJUKULUM?UNUOUPUQURAUSBUSCUSUTUIWIUSV
      AVBXGXCWNWQXAVCVDVEWDWJWRWKXBVTWJWRQZWCVQVRYDVSVQVRJWJWNWOWPGWRWIABVFWNWO
      WPVGVHVIVJVTWKXBQZWCVRVSYEVQVRVSJWKWNWSWTGXBWIBCVFWNWSWTVGVHVKVJVLVTWMXEQ
      ZWCVQVSYFVRVQVSJWMWNWOWTGXEWIACVFWNWOWTVGVHVMVJVNWIWEWFVOVEVP @.
      @( [ ?] @) @( [21-Jul-2005] @)
  @}
$)

  ${
    $d k M $.  $d k N $.  $d k K $.
    $( Subset relationship for finite sets of sequential integers. $)
    fzss1 $p |- ( ( K e. ( ZZ>= ` M ) /\ N e. ZZ ) ->
                  ( K ... N ) C_ ( M ... N ) ) $=
      ( vk cuz cfv wcel cz wa cv cle wbr crab cfz co wss eluzle adantr wi cr
      letr zre syl3an 3expa eluzel2 eluzelz jca sylan mpand anim1d ss2rabdv
      wceq fzval 3sstr4d ) ABEFGZCHGZIADJZKLZUQCKLZIZDHMZBUQKLZUSIZDHMZACNOZBCN
      OZUOVAVDPUPUOUTVCDHUOUQHGZIZURVBUSVHBAKLZURVBUOVIVGBAQRBHGZAHGZIVGVIURIVB
      SZUOVJVKVGVLBTGATGUQTGVLVJVKVGBAUQUABUBAUBUQUBUCUDUOVJVKBAUEZBAUFZUGUHUIU
      JUKRVKUPVEVAULUODACUMVNUHVJUPVFVDULUODBCUMVMUHUN $.
      $( [28-Sep-2005] $)

    $( Subset relationship for finite sets of sequential integers. $)
    fzss2 $p |- ( ( N e. ( ZZ>= ` K ) /\ M e. ZZ ) ->
                  ( M ... K ) C_ ( M ... N ) ) $=
      ( vk cz wcel cuz cfv cfz co wss wa cv cle wbr crab wi eluzle adantl cr
      letr zre syl3an 3expb eluzel2 eluzelz jca sylan2 mpan2d ancoms anim2d
      ss2rabdv wceq fzval 3sstr4d ) BEFZCAGHFZBAIJZBCIJZKUPUQLBDMZNOZUTANOZLZDE
      PZVAUTCNOZLZDEPZURUSUQVDVGKUPUQVCVFDEUQUTEFZLVBVEVAVHUQVBVEQVHUQLVBACNOZV
      EUQVIVHACRSVHAEFZCEFZLVBVILVEQZUQVHVJVKVLUTTFATFCTFVLVHVJVKUTACUAUTUBAUBC
      UBUCUDUQVJVKACUEZACUFZUGUHUIUJUKULSUPVJURVDUMUQDBAUNVMUHUPVKUSVGUMUQDBCUN
      VNUHUOUJ $.
      $( [4-Oct-2005] $)

    $( A finite set of sequential integers is a subset of a set of upper
       integers. $)
    fzssuz $p |- ( M ... N ) C_ ( ZZ>= ` M ) $=
      ( vk cfz co cuz cfv cv elfzuz ssriv ) CABDEAFGCHABIJ $.
      $( [28-Oct-2005] $)
  $}

  ${
    $d M k $.  $d N k $.
    $( Join a successor to the end of a finite set of sequential integers. $)
    fzsuc $p |- ( ( M e. ZZ /\ N e. ZZ /\ M <_ ( N + 1 ) )
                -> ( M ... ( N + 1 ) ) = ( ( M ... N ) u. { ( N + 1 ) } ) ) $=
      ( vk cz wcel c1 caddc co cle wbr w3a cfz csn cun cv wo wa wceq wb elfz1
      3anass syl6bb 3adant3 orbi1d wi eleq1 breq2 anbi12d biimprcd peano2z
      sylan pm4.71rd anass elsn syl5bb 3adant1 orbi2d clt zleltp1 cr leloe zre
      peano2re syl syl2an bitr4d ancoms adantll anbi2d pm5.32da andi anbi2i
      bitr2i 3bitrd sylan2 elun syl5rbb eqrdv ) ADEZBDEZABFGHZIJZKZCAWALHZABLHZ
      WAMZNZWCCOZWEEZWHWFEZPZWHWDEZWHWGEWCWKWHDEZAWHIJZWHWAIJZQZQZWLWCWKWMWNWHB
      IJZQZQZWJPWTWMWNWHWARZQZQZPZWQWCWIWTWJVSVTWIWTSWBVSVTQZWIWMWNWRKWTWHABTWM
      WNWRUAUBUCUDWCWJXCWTVTWBWJXCSVSVTWBQZXAXCWJXFXAWMWNQZXAQXCXFXAXGWADEZWBXA
      XGUEVTXAXGXHWBQXAWMXHWNWBWHWADUFWHWAAIUGUHUIBUJZUKULWMWNXAUMUBCWAUNUOUPUQ
      VSVTXDWQSWBXEWMWNWRXAPZQZQZWQXDXEWMXKWPXEWMQXJWOWNVTWMXJWOSZVSWMVTXMWMVTQ
      ZXJWHWAURJZXAPZWOXNWRXOXAWHBUSUDWHUTEWAUTEZWOXPSWMVTWHWAVAWHVBVTBUTEXQBVB
      BVCVDVEVFVGVHVIVJXLWMWSXBPZQXDXKXRWMWNWRXAVKVLWMWSXBVKVMUOUCVNVSVTWLWQSZW
      BVSXHXSVTVSXHQWLWMWNWOKWQWHAWATWMWNWOUAUBXIVOUCVFWHWEWFVPVQVR $.
      $( [20-Feb-2009] $) $( [19-Jul-2008] $)
  $}

  $( Subset relationship for finite sets of sequential integers. $)
  fzssp1 $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                ( M ... N ) C_ ( M ... ( N + 1 ) ) ) $=
    ( cz wcel cfz co c1 caddc wss cuz cfv fzss2 uzid peano2uz syl sylan ancoms
    ) BCDZACDZABEFABGHFZEFIZTBJKZDZSUARBATLRBUBDUCBMBBNOPQ $.
    $( [21-Jul-2005] $)

  $( Subset relationship for finite sets of sequential integers. $)
  fzp1ss $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                ( ( M + 1 ) ... N ) C_ ( M ... N ) ) $=
    ( c1 caddc co cuz cfv wcel cz cfz wss fzss1 uzid peano2uz syl sylan ) ACDEZ
    AFGZHZBIHQBJEABJEKAIHZQABLTARHSAMAANOP $.
    $( [26-Jul-2005] $)

  $( Membership in a set of sequential integers with an appended element. $)
  fzelp1 $p |- ( ( N e. A /\ K e. ( M ... N ) ) ->
                K e. ( M ... ( N + 1 ) ) ) $=
    ( wcel cfz co wa c1 caddc cz wss elfzel1 adantl elfzel2g fzssp1 syl11anc
    simpr sseldd ) DAEZBCDFGZEZHZUACDIJGFGZBUCCKEZDKEUAUDLUBUETBCDMNABCDOCDPQTU
    BRS $.
    $( [7-Dec-2005] $)

  ${
    fzelp1.1 $e |- N e. _V $.
    $( Membership in a set of sequential integers with an appended element. $)
    fzelp1i $p |- ( K e. ( M ... N ) -> K e. ( M ... ( N + 1 ) ) ) $=
      ( cvv wcel cfz co c1 caddc fzelp1 mpan ) CEFABCGHFABCIJHGHFDEABCKL $.
      $( [3-Dec-2005] $)
  $}

  $( MAY BE REVISED - generalize antecedent per ~ fzsuc ; use it or v.versa $)
  $( Append an element to a finite set of sequential integers. $)
  elfzp1 $p |- ( N e. ( ZZ>= ` M ) ->
    ( K e. ( M ... ( N + 1 ) ) <-> ( K e. ( M ... N ) \/ K = ( N + 1 ) ) ) ) $=
    ( cuz cfv wcel c1 caddc co cfz wceq wo wa wne wi cz w3a cle wbr eluzel2
    ad2antrr eluzelz elfzelz ad2antlr 3jca elfzle1 clt elfzle2 adantl anim1i
    cr wb zre syl peano2re 3syl ltlen syl11anc mpbird zleltp1 jca32 ex elfz2
    adantr sylibrd eqcom orbi1i neor orcom 3bitr3i sylib wss fzssp1 sseld
    eleq1 peano2uz eluzfz2 syl5cbir jaod impbid ) CBDEZFZABCGHIZJIZFZABCJIZFZAW
    CKZLZWBWEWIWBWEMZWCANZWGOZWIWJWKBPFZCPFZAPFZQZBARSZACRSZMMZWGWJWKWSWJWKMZWP
    WQWRWTWMWNWOWBWMWEWKBCTZUAWBWNWEWKBCUBZUAZWEWOWBWKABWCUCZUDZUEWEWQWBWKABWCU
    FUDWTWRAWCUGSZWTXFAWCRSZWKMZWJXGWKWEXGWBABWCUHUIUJWTAUKFZWCUKFZXFXHULWEXIWB
    WKWEWOXIXDAUMUNUDWBXJWEWKWBWNCUKFXJXBCUMCUOUPUAAWCUQURUSWTWOWNWRXFULXEXCACU
    TURUSVAVBWBWGWSULWEWAABCVCVDVEWCAKZWGLWHWGLWLWIXKWHWGWCAVFVGWGWCAVHWHWGVIVJ
    VKVBWBWGWEWHWBWFWDAWBWMWNWFWDVLXAXBBCVMURVNWHWEWCWDFZWBAWCWDVOWBWCWAFXLBCVP
    BWCVQUNVRVSVT $.
    $( [19-Sep-2005] $)

  ${
    $d M m $.
    $( A finite interval of integers with one element.  (Contributed by Jeff
       Madsen, 2-Sep-2009 ) $)
    fzsn $p |- ( M e. ZZ -> ( M ... M ) = { M } ) $=
      ( vm cz wcel cfz co csn cv wceq elfz1eq eleq1 elfz3 syl5cbir impbid2
      elsn syl6bbr eqrdv ) ACDZBAAEFZAGZRBHZSDZUAAIZUATDRUBUCUAAJUCUBASDRUAASKA
      LMNBAOPQ $.
      $( [1-Dec-2010] $)  $( [2-Sep-2009] $)

    $( A finite interval of integers with two elements.  (Contributed by Jeff
       Madsen, 2-Sep-2009 ) $)
    fzpr $p |- ( M e. ZZ -> ( M ... ( M + 1 ) ) = { M , ( M + 1 ) } ) $=
      ( vm cz wcel c1 caddc co cfz cpr cv wceq wo cuz cfv wb uzid elfzp1 syl
      csn fzsn eleq2d elsn syl6bb orbi1d bitrd visset elpr syl6bbr eqrdv ) ACDZ
      BAAEFGZHGZAUKIZUJBJZULDZUNAKZUNUKKZLZUNUMDUJUOUNAAHGZDZUQLZURUJAAMNDUOVAO
      APUNAAQRUJUTUPUQUJUTUNASZDUPUJUSVBUNATUABAUBUCUDUEUNAUKBUFUGUHUI $.
      $( [1-Dec-2010] $)  $( [2-Sep-2009] $)

    $( A finite interval of integers with three elements. $)
    fztp $p |- ( M e. ZZ -> ( M ... ( M + 2 ) )
                 = { M , ( M + 1 ) , ( M + 2 ) } ) $=
      ( cz wcel c1 caddc co cfz csn cun c2 ctp cle wbr wceq peano2z cr zre id
      peano2re syl lep1 letrd fzsuc mpd3an23 cc zcn ax1cn axaddass mp3an23
      df-2 opreq2i syl6eqr opreq2d cpr fzpr sneqd uneq12d df-tp 3eqtr3d ) ABCZA
      ADEFZDEFZGFZAVAGFZVBHZIZAAJEFZGFAVAVGKZUTVABCAVBLMZVCVFNAOUTAPCZVIAQVJAVA
      VBVJRASZVJVAPCZVBPCVKVASTAUAVJVLVAVBLMVKVAUATUBTAVAUCUDUTVBVGAGUTVBADDEFZ
      EFZVGUTAUECZVBVNNZAUFVODUECZVQVPUGUGADDUHUITJVMAEUJUKULZUMUTVFAVAUNZVGHZI
      VHUTVDVSVEVTAUOUTVBVGVRUPUQAVAVGURULUS $.
      $( [13-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x F $.
    $( Two ways of defining the first two values of a sequence on ` NN ` . $)
    fzprval $p |- ( A. x e. ( 1 ... 2 ) ( F ` x ) = if ( x = 1 , A , B )
                  <-> ( ( F ` 1 ) = A /\ ( F ` 2 ) = B ) ) $=
      ( cv cfv c1 wceq cif c2 cfz co wral cpr wsbc wa caddc cz wcel 1z fzpr
      ax-mp df-2 opreq2i preq2i 3eqtr4i raleqi cr 1re elisseti 2re ralpr fveq2
      iftrue eqeq12d sbcie wn wne clt wbr 1lt2 ltnei pm13.181 mpan2 df-ne
      sylib iffalse syl anbi12i 3bitri ) AEZDFZVKGHZBCIZHZAGJKLZMVOAGJNZMVOAGOZ
      VOAJOZPGDFZBHZJDFZCHZPVOAVPVQGGGQLZKLZGWDNZVPVQGRSWEWFHTGUAUBJWDGKUCUDJWD
      GUCUEUFUGVOAGJGUHUIUJZJUHUKUJZULVRWAVSWCVOWAAGWGVMVLVTVNBVKGDUMVMBCUNUOUP
      VOWCAJWHVKJHZVLWBVNCVKJDUMWIVMUQZVNCHWIVKGURZWJWIJGURZWKGJUSUTWLVAGJUIUKV
      BUBVKJGVCVDVKGVEVFVMBCVGVHUOUPVIVJ $.
      $( [5-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x F $.
    $( Two ways of defining the first three values of a sequence on ` NN ` . $)
    fztpval $p |- ( A. x e. ( 1 ... 3 ) ( F ` x )
                      = if ( x = 1 , A , if ( x = 2 , B , C ) )
                  <-> ( ( F ` 1 ) = A /\ ( F ` 2 ) = B /\ ( F ` 3 ) = C ) ) $=
      ( cv cfv c1 wceq c2 cif c3 cfz co wral ctp wsbc w3a caddc cz wcel 1z
      fztp ax-mp df-3 2cn ax1cn addcomi eqtri opreq2i tpeq3 df-2 tpeq2 3eqtr4i
      raleqi cc elisseti cr 3re raltp fveq2 iftrue eqeq12d sbcie wn wne clt
      wbr 1lt2 1re 2re ltnei pm13.181 mpan2 df-ne sylib iffalse syl eqtrd 1lt3
      2lt3 3anbi123i 3bitri ) AFZEGZWDHIZBWDJIZCDKZKZIZAHLMNZOWJAHJLPZOWJAHQZWJ
      AJQZWJALQZRHEGZBIZJEGZCIZLEGZDIZRWJAWKWLHHJSNZMNZHHHSNZXBPZWKWLHTUAXCXEIU
      BHUCUDLXBHMLJHSNXBUEJHUFUGUHUIZUJWLHJXBPZXELXBIWLXGIXFLXBHJUKUDJXDIXGXEIU
      LJXDHXBUMUDUIUNUOWJAHJLHUPUGUQZJUPUFUQZLURUSUQZUTWMWQWNWSWOXAWJWQAHXHWFWE
      WPWIBWDHEVAWFBWHVBVCVDWJWSAJXIWGWEWRWICWDJEVAWGWIWHCWGWFVEZWIWHIZWGWDHVFZ
      XKWGJHVFZXMHJVGVHXNVIHJVJVKVLUDWDJHVMVNWDHVOZVPWFBWHVQZVRWGCDVBVSVCVDWJXA
      ALXJWDLIZWEWTWIDWDLEVAXQWIWHDXQXKXLXQXMXKXQLHVFZXMHLVGVHXRVTHLVJUSVLUDWDL
      HVMVNXOVPXPVRXQWGVEZWHDIXQWDJVFZXSXQLJVFZXTJLVGVHYAWAJLVKUSVLUDWDLJVMVNWD
      JVOVPWGCDVQVRVSVCVDWBWC $.
      $( [13-Sep-2011] $)
  $}

  $( Reversal of start and end of a finite set of sequential integers. $)
  fzrev $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( K e. ( ( J - N ) ... ( J - M ) ) <-> ( J - K ) e. ( M ... N ) ) ) $=
    ( cz wcel wa cmin co cle wbr cfz wb cr suble zre syl3an 3comr 3expb
    adantll lesub adantlr anbi12d ancom syl5rbbr simprr zsubcl ancoms
    ad2ant2lr ad2ant2r elfz syl111anc adantl simpll simplr 3bitr4d ) CEFZDEFZGZ
    AEFZBEFZGZGZADHIZBJKZBACHIZJKZGZCABHIZJKZVIDJKZGZBVDVFLIFZVICDLIFZVCVKVJGVH
    VLVCVKVEVJVGURVBVKVEMZUQURUTVAVOUTVAURVOANFZBNFZDNFVOUTVAURABDOAPZBPZDPQRST
    UQVBVJVGMZURUQUTVAVTCNFVPVQVTUQUTVACABUACPVRVSQSUBUCVKVJUDUEVCVAVDEFZVFEFZV
    MVHMUSUTVAUFURUTWAUQVAUTURWAADUGUHUIUQUTWBURVAUTUQWBACUGUHUJBVDVFUKULVCVIEF
    ZUQURVNVLMVBWCUSABUGUMUQURVBUNUQURVBUOVICDUKULUP $.
    $( [25-Nov-2005] $)

  $( Reversal of start and end of a finite set of sequential integers. $)
  fzrev2 $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( K e. ( M ... N ) <-> ( J - K ) e. ( ( J - N ) ... ( J - M ) ) ) ) $=
    ( cz wcel wa cmin co cfz wb fzrev simpl zsubcl jca sylan2 wceq cc nncan
    zcn syl2an adantl eleq1d bitr2d ) CEFDEFGZAEFZBEFZGZGZABHIZADHIACHIJIFZAUJH
    IZCDJIZFZBUMFUEUFUJEFZGUKUNKUHAUJCDLUHUFUOUFUGMABNOPUIULBUMUHULBQZUEARFBRFU
    PUFUGABSATBTUAUBUCUD $.
    $( [25-Nov-2005] $)

  $( Reversal of start and end of a finite set of sequential integers. $)
  fzrev2i $p |- ( ( N e. A /\ J e. ZZ /\ K e. ( M ... N ) )
                 -> ( J - K ) e. ( ( J - N ) ... ( J - M ) ) ) $=
    ( wcel cz cfz co w3a cmin simp3 wa wb elfzel1 adantl elfzel2g jca 3adant2
    elfzelz anim2i 3adant1 fzrev2 syl11anc mpbid ) EAFZBGFZCDEHIFZJZUHBCKIBEKIB
    DKIHIFZUFUGUHLUIDGFZEGFZMZUGCGFZMZUHUJNUFUHUMUGUFUHMUKULUHUKUFCDEOPACDEQRSU
    GUHUOUFUHUNUGCDETUAUBBCDEUCUDUE $.
    $( [25-Nov-2005] $)

  $( The "complement" of a member of a finite set of sequential integers. $)
  fzrev3 $p |- ( ( N e. A /\ K e. ZZ ) -> ( K e. ( M ... N )
                <-> ( ( M + N ) - K ) e. ( M ... N ) ) ) $=
    ( wcel cz wa cfz co caddc cmin w3a simplr elfzel1 adantl elfzel2g adantlr
    3jca wb cc wceq pncan pncan2 opreq12d zcn syl2an eleq2d 3adant1 3simpc
    zaddcl simp1 fzrev syl12anc bitr3d pm5.21nd ) DAEZBFEZGZBCDHIZEZCDJIZBKIZUS
    EZUQCFEZDFEZLZURUTGUQVDVEUPUQUTMUTVDURBCDNOUPUTVEUQABCDPQRURVCGUQVDVEUPUQVC
    MVCVDURVBCDNOUPVCVEUQAVBCDPQRVFBVADKIZVACKIZHIZEZUTVCVDVEVJUTSUQVDVEGZVIUSB
    CTEZDTEZVIUSUAVDVEVLVMGVGCVHDHCDUBCDUCUDCUEDUEUFUGUHVFVKVAFEZUQVJVCSUQVDVEU
    IVDVEVNUQCDUJUHUQVDVEUKVABCDULUMUNUO $.
    $( [20-Nov-2005] $)

  $( The "complement" of a member of a finite set of sequential integers. $)
  fzrev3i $p |- ( ( N e. A /\ K e. ( M ... N ) )
                -> ( ( M + N ) - K ) e. ( M ... N ) ) $=
    ( wcel cfz co wa caddc cmin simpr cz wb fzrev3 elfzelz sylan2 mpbid ) DAEZB
    CDFGZEZHTCDIGBJGSEZRTKRBLETUAMTABCDNBCDOPQ $.
    $( [20-Nov-2005] $)

  $( Finite set of sequential integers starting at 0. $)
  fznn0 $p |- ( N e. NN0
              -> ( K e. ( 0 ... N ) <-> ( K e. NN0 /\ K <_ N ) ) ) $=
    ( cn0 wcel cc0 cfz co cz cle wbr w3a wa wb elfz1 0z nn0z sylancr df-3an
    elnn0z anbi1i bitr4i syl6bb ) BCDZAEBFGDZAHDZEAIJZABIJZKZACDZUGLZEHDBHDUDUH
    MUCAEBNOBPQUHUEUFLZUGLUJUEUFUGRUIUKUGASTUAUB $.
    $( [1-Aug-2005] $)

  $( Finite set of sequential integers starting at 1. $)
  fznn $p |- ( N e. NN
              -> ( K e. ( 1 ... N ) <-> ( K e. NN /\ K <_ N ) ) ) $=
    ( cn wcel c1 cfz co cz cle wbr w3a wa wb elfz1 1z nnz sylancr df-3an
    elnnz1 anbi1i bitr4i syl6bb ) BCDZAEBFGDZAHDZEAIJZABIJZKZACDZUGLZEHDBHDUDUH
    MUCAEBNOBPQUHUEUFLZUGLUJUEUFUGRUIUKUGASTUAUB $.
    $( [31-Aug-2011] $)

  ${
    $d k N $.
    $( Quantification over a one-member finite set of sequential integers in
       terms of substitution. $)
    fz1sbc $p |- ( N e. ZZ -> ( A. k e. ( N ... N ) ph <-> [ N / k ] ph ) ) $=
      ( cz wcel wsbc cv wceq wi wal cfz co wral sbc6g elfz1eq eleq1 elfz3
      syl5cbir impbid2 imbi1d albidv df-ral syl5rbb bitr2d ) CDEZABCFBGZCHZAIZB
      JZABCCKLZMZABCDNUEUFUJEZAIZBJUIUKUEUMUHBUEULUGAUEULUGUFCOUGULCUJEUEUFCUJP
      CQRSTUAABUJUBUCUD $.
      $( [28-Nov-2005] $)
  $}

  $( No finite set of sequential integers equals a set of upper integers. $)
  fzneuz $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. ZZ )
               -> -. ( M ... N ) = ( ZZ>= ` K ) ) $=
    ( cuz cfv wcel cz wa cfz co wceq wn c1 caddc peano2uz adantl cle wbr cr
    eluzelz zre clt ltp1 wb peano2re ltnle mpdan mpbid 3syl elfzle2 nsyl
    ad2antrr nelneq2 syl11anc eqcom notbii sylib eluzfz2 sylancom pm2.61dan )
    CBDEFZAGFZHZCADEZFZBCIJZVDKZLZVCVEHZVDVFKZLZVHVICMNJZVDFZVLVFFZLZVKVEVMVCAC
    OPVAVOVBVEVAVLCQRZVNVACGFCSFZVPLZBCTCUAVQCVLUBRZVRCUCVQVLSFVSVRUDCUECVLUFUG
    UHUIVLBCUJUKULVLVDVFUMUNVJVGVDVFUOUPUQVCVELZCVFFZVHVAWAVBVTBCURULCVFVDUMUSU
    T $.
    $( [11-Dec-2005] $)

  ${
    $d j k x K $.  $d j k x M $.  $d j k x N $.  $d k x ph $.
    $( Reversal of scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzrevral $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) -> ( A. j e. ( M ... N
       ) ph <-> A. k e. ( ( K - N ) ... ( K - M ) ) [ ( K - k ) / j ] ph ) ) $=
      ( cz wcel w3a cfz co wral cv cmin wsbc wi wa simpr wb fzrev anassrs
      elfzelz sylan2 mpbid ra4sbc syl ex 3impa com23 r19.21adv ax-17 oprex
      hbsbc1v hbral fzrev2i 3expa wceq opreq2 dfsbcq rcla4v cc nncan zcn
      syl2an eqcomd sbceq1a adantll sylibrd r19.21ad 3adant1 impbid ) EGHZFGHZD
      GHZIZABEFJKZLZABDCMZNKZOZCDFNKZDENKZJKZLZVOVQVTCWCVOVRWCHZVQVTVLVMVNWEVQV
      TPZPVLVMQZVNQZWEWFWHWEQZVSVPHZWFWIWEWJWHWERWHVRGHZWEWJSZWEWGVNWKWLDVREFTU
      AVRWAWBUBUCUDABVSVPUEUFUGUHUIUJVMVNWDVQPVLVMVNQZWDABVPWMBUKVTBCWCWEBUKABV
      SDVRNULUMUNWMBMZVPHZWDAWMWOWDAPWMWOQZWDABDDWNNKZNKZOZAWPWQWCHZWDWSPVMVNWO
      WTGDWNEFUOUPVTWSCWQWCVRWQUQVSWRUQVTWSSVRWQDNURABVSWRUSUFUTUFVNWOAWSSZVMVN
      WOQZWNWRUQXAXBWRWNDVAHWNVAHZWRWNUQVNWODWNVBDVCWOWNGHXCWNEFUBWNVCUFVDVEABW
      RVFUFVGVHUGUIVIVJVK $.
      $( [25-Nov-2005] $)

    $( Reversal of scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzrevral2 $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) -> ( A. j e. ( ( K - N
       ) ... ( K - M ) ) ph <-> A. k e. ( M ... N ) [ ( K - k ) / j ] ph ) ) $=
      ( cz wcel cmin co cfz wral cv wsbc wb w3a zsubcl 3adant2 3adant3 simp1
      fzrevral syl111anc cc wceq nncan opreq12d zcn syl3an raleqdv bitrd 3coml
      ) DGHZEGHZFGHZABDFIJZDEIJZKJLZABDCMIJNZCEFKJZLZOULUMUNPZUQURCDUPIJZDUOIJZ
      KJZLZUTVAUOGHZUPGHZULUQVEOULUNVFUMDFQRULUMVGUNDEQSULUMUNTABCDUOUPUAUBVAUR
      CVDUSDUCHZEUCHZFUCHZVDUSUDULUMUNVHVIVJPVBEVCFKVHVIVBEUDVJDEUESVHVJVCFUDVI
      DFUERUFDUGEUGFUGUHUIUJUK $.
      $( [25-Nov-2005] $)

    $( Reversal of scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzrevral3 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( A. j e. ( M ... N ) ph
                   <-> A. k e. ( M ... N ) [ ( ( M + N ) - k ) / j ] ph ) ) $=
      ( cz wcel wa cfz co wral caddc cv cmin wsbc wb zaddcl fzrevral mpd3an3
      cc wceq pncan pncan2 opreq12d zcn syl2an raleqdv bitrd ) DFGZEFGZHZABDEIJ
      ZKZABDELJZCMNJOZCUNENJZUNDNJZIJZKZUOCULKUIUJUNFGUMUSPDEQABCUNDERSUKUOCURU
      LDTGZETGZURULUAUIUJUTVAHUPDUQEIDEUBDEUCUDDUEEUEUFUGUH $.
      $( [11-Jul-2006] $) $( [20-Nov-2005] $)

    $( Shift the scanning order inside of a quantification over a finite set of
       sequential integers. $)
    fzshftral $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) -> ( A. j e. ( M ... N
       ) ph <-> A. k e. ( ( M + K ) ... ( N + K ) ) [ ( k - K ) / j ] ph ) ) $=
      ( vx cz wcel w3a cfz co wral cc0 cv cmin wsbc caddc wb 0z fzrevral
      mp3an3 3adant3 zsubcl mpan id syl3an 3com12 cc wceq wa cneg subneg
      addcom eqtrd df-neg opreq2i syl5eqr 3adant2 opreq12d 3coml zcn raleqdv
      negsubdi2 elfzelz syl syl2an dfsbcq ralbidva 3ad2ant3 bitrd cvv wal
      oprex ax-gen opreq2 sbcco3g mp2an ralbii syl5bb 3bitrd ) EHIZFHIZDHIZJZAB
      EFKLMZABNGOZPLZQZGNFPLZNEPLZKLMZWIGDCOZPLZQZCDWKPLZDWJPLZKLZMZABWMDPLZQZC
      EDRLZFDRLZKLZMZWBWCWFWLSZWDWBWCNHIZXFTABGNEFUAUBUCWCWBWDWLWSSZWJHIZWKHIZW
      DXHWCWBWDWIGCDWJWKUAXGWCXITNFUDUEXGWBXJTNEUDUEWDUFUGUHWEABNWNPLZQZCWRMZXE
      WSWEXMXLCXDMZXEWEXLCWRXDEUIIZFUIIZDUIIZWRXDUJZWBWCWDXQXOXPXRXQXOXPJWPXBWQ
      XCKXQXOWPXBUJXPXQXOUKZDEULZPLZXBWPXSYADERLXBDEUMDEUNUOXTWKDPEUPUQURUCXQXP
      WQXCUJXOXQXPUKZDFULZPLZXCWQYBYDDFRLXCDFUMDFUNUOYCWJDPFUPUQURUSUTVAEVBFVBD
      VBZUGVCWDWBXNXESWCWDXLXACXDWDWMXDIZUKXKWTUJZXLXASXQWMUIIZYGWDYFXQYHUKWNUL
      WTXKDWMVDWNUPURYEYFWMHIYHWMXBXCVEWMVBVFVGABXKWTVHVFVIVJVKWOXLCWRWNVLIWHVL
      IZGVMWOXLSDWMPVNYIGNWGPVNVOAGBWNWHXKVLVLWGWNNPVPVQVRVSVTWA $.
      $( [19-Jun-2007] $) $( [27-Nov-2005] $)
  $}

  ${
    $d j k m n x y F $.  $d j k m n x y M $.  $d j k n x y N $.
    $( The values of a finite real sequence have an upper bound. _Warning:  The
       HTML proof page is 1/2 megabyte in size_. $)
    fsequb $p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... N ) ( F ` k ) e. RR )
                 -> E. x e. RR A. k e. ( M ... N ) ( F ` k ) < x ) $=
      ( vn vj vm vy cuz cfv wcel cv cr cfz co wral clt wbr wrex wi c1 caddc
      wceq opreq2 raleqdv rexbidv imbi12d cz elfz3 fveq2 eleq1d rcla4v syl wa
      peano2re breq1d ltp1 syl5cbir elfz1eq syl5 r19.21aiv jca a1i breq2
      ralbidv rcla4ev syl6 syld wss eluzel2 eluzelz fzssp1 syl11anc sseld
      imim1d ralimdv2 imp32 rcla4va peano2uz eluzfz2 sylan adantrl cle cif wo
      wb elfzp1 ad3antrrr max2 ancoms ad2antlr fzelp1 an1rs adantlr simplrl
      ifcl sylan2 ltletr syl111anc mpan2d ex a2d imp simpr adantl max1ALT
      ltletrd jaod sylbid jctild exp32 com34 r19.23adv imp3a cbvrexv syl6ib
      mp2and uzind4 cbvralv sylan2b ) EDJKZLZFMZCKZNLZFDEOPZQZBMZCKZAMZRSZBYGQZ
      ANTZYJNLZBYGQYCYHYNYFFDGMZOPZQZYLBYQQZANTZUAYFFDDOPZQZYLBUUAQZANTZUAYFFDH
      MZOPZQZYLBUUFQZANTZUAZYFFDUUEUBUCPZOPZQZYLBUULQZANTZUAYHYNUAGHDEYPDUDZYRU
      UBYTUUDUUPYFFYQUUAYPDDOUEZUFUUPYSUUCANUUPYLBYQUUAUUQUFUGUHYPUUEUDZYRUUGYT
      UUIUURYFFYQUUFYPUUEDOUEZUFUURYSUUHANUURYLBYQUUFUUSUFUGUHYPUUKUDZYRUUMYTUU
      OUUTYFFYQUULYPUUKDOUEZUFUUTYSUUNANUUTYLBYQUULUVAUFUGUHYPEUDZYRYHYTYNUVBYF
      FYQYGYPEDOUEZUFUVBYSYMANUVBYLBYQYGUVCUFUGUHDUILZUUBDCKZNLZUUDUVDDUUALUUBU
      VFUADUJYFUVFFDUUAYDDUDYEUVENYDDCUKULUMUNUVDUVFUVEUBUCPZNLZYJUVGRSZBUUAQZU
      OZUUDUVFUVKUAUVDUVFUVHUVJUVEUPUVFUVIBUUAUVFYIDUDZUVIYIUUALUVLUVIUVEUVGRSU
      VFUVLYJUVEUVGRYIDCUKUQUVEURUSYIDUTVAVBVCVDUUCUVJAUVGNYKUVGUDYLUVIBUUAYKUV
      GYJRVEVFVGVHVIUUEYBLZUUJUUMUUOUVMUUJUUMUOUOUUIUUKCKZNLZUUOUVMUUJUUMUUIUVM
      UUMUUGUUIUVMYFYFFUULUUFUVMYDUUFLYDUULLYFUVMUUFUULYDUVMUVDUUEUILUUFUULVJDU
      UEVKDUUEVLDUUEVMVNVOVPVQVPVRUVMUUMUVOUUJUUKUULLZUUMUVOUVMYFUVOFUUKUULYDUU
      KUDYEUVNNYDUUKCUKULVSUVMUUKYBLUVPDUUEVTDUUKWAUNWBWCUVMUUMUUIUVOUOZUUOUAUU
      JUVMUUMUOZUVQYJIMZRSZBUULQZINTZUUOUVRUUIUVOUWBUVRUUHUVOUWBUAANUVRYKNLZUVO
      UUHUWBUVRUWCUVOUUHUWBUAUVRUWCUVOUOZUOZUUHUVNUBUCPZYKWDSZYKUWFWEZNLZYJUWHR
      SZBUULQZUOUWBUWEUUHUWKUWIUWEYLUWJBUUFUULUWEYIUUFLZYLUAZYIUULLZUWJUAUWEUWM
      UOZUWNUWLYIUUKUDZWFZUWJUVMUWNUWQWGUUMUWDUWMYIDUUEWHWIUWOUWLUWJUWPUWEUWMUW
      LUWJUAUWEUWLYLUWJUWEUWLYLUWJUAUWEUWLUOZYLYKUWHWDSZUWJUWDUWSUVRUWLUVOUWCUW
      SUWFNLZUWCUWSUVOUWFYKWJUVNUPZWBWKWLUWRYOUWCUWIYLUWSUOUWJUAUVRUWLYOUWDUVMU
      WLUUMYOUWNUUMYOUVMUWLUOYFYOFYIUULYDYIUDYEYJNYDYICUKULVSYBYIDUUEWMWBWNWOUV
      RUWCUVOUWLWPUWDUWIUVRUWLUWCUWTUWIUVOUWGYKUWFNWQUXAWRZWLYJYKUWHWSWTXAXBXCX
      DUWDUWPUWJUAUVRUWMUWPUWJUVNUWHRSUWDUWPYJUVNUWHRYIUUKCUKUQUWDUVNUWFUWHUWCU
      VOXEUVOUWTUWCUXAXFUXBUVOUVNUWFRSUWCUVNURXFUVOUWFUWHWDSZUWCUVOUWTUXCUXAUWF
      YKXGUNXFXHUSWLXIXJXBVQUWDUWIUVRUXBXFXKUWAUWKIUWHNUVSUWHUDUVTUWJBUULUVSUWH
      YJRVEVFVGVHXLXMXNXOUWAUUNIANUVSYKUDUVTYLBUULUVSYKYJRVEVFXPXQWCXRXLXSXDYOY
      FBFYGYIYDUDYJYENYIYDCUKULXTYA $.
      $( [19-Sep-2005] $)

    $( The values of a finite real sequence have an upper bound. $)
    fsequb2 $p |- ( ( N e. ( ZZ>= ` M ) /\ F : ( M ... N ) --> RR ) ->
                  E. x e. RR A. y e. ran F y <_ x ) $=
      ( vk cuz cfv wcel cfz co cr wf wa cv clt wbr wral wrex cle crn fsequb
      ffvelrn r19.21aiva sylan2 wi wceq wfn wb ffn fvelrnb syl biimpa breq1
      biimpd reximi r19.36av adantll adantlr frn sseld imp simplr ltle
      syl11anc adantlll syld r19.21adva reximdva mpd ) EDGHIZDEJKZLCMZNZFOZCHZA
      OZPQZFVLRZALSZBOZVQTQZBCUAZRZALSVKVPLIZFVLRVTVMAFCDEUBVMWEFVLVLLVOCUCUDUE
      VNVSWDALVNVQLIZNZVSWBBWCWGWAWCIZNVSWAVQPQZWBVNWHVSWIUFZWFVMWHWJVKVMWHNZVR
      WIUFZFVLSZWJWKVPWAUGZFVLSZWMVMWHWOVMCVLUHWHWOUIVLLCUJFVLWACUKULUMWNWLFVLW
      NVRWIVPWAVQPUNUOUPULVRWIFVLUQULURUSVMWFWHWIWBUFZVKVMWFNWHNWALIZWFWPVMWHWQ
      WFVMWHWQVMWCLWAVLLCUTVAVBUSVMWFWHVCWAVQVDVEVFVGVHVIVJ $.
      $( [15-Oct-2006] $) $( [20-Sep-2005] $)

    $( The values of a finite real sequence have a supremum. $)
    fseqsupcl $p |- ( ( N e. ( ZZ>= ` M ) /\ F : ( M ... N ) --> RR )
                 -> sup ( ran F , RR , < ) e. RR ) $=
      ( vy vx cuz cfv wcel cfz co cr wf wa crn wss c0 wne cv cle wbr wral wrex
      clt csup frn adantl cdm wceq eqeq1 biimpd dm0rn0 syl5ibr necon3d ne0i
      syl5com imp eluzfz1 fdm syl2an fsequb2 suprcl syl111anc ) CBFGHZBCIJZKALZ
      MANZKOZVFPQZDRERSTDVFUAEKUBVFKUCUDKHVEVGVCVDKAUEUFBVDHZAUGZVDUHZVHVCVEVIV
      KVHVKVDPQVHVIVKVFPVDPVKVJPUHZVDPUHZVFPUHVKVLVMVJVDPUIUJAUKULUMVDBUNUOUPBC
      UQVDKAURUSEDABCUTEDVFVAVB $.
      $( [9-Jul-2007] $) $( [20-Sep-2005] $)

    fseqsupub.1 $e |- N e. _V $.
    $( The values of a finite real sequence are bounded by their supremum. $)
    fseqsupubi $p |- ( ( K e. ( M ... N ) /\ F : ( M ... N ) --> RR ) ->
               ( F ` K ) <_ sup ( ran F , RR , < ) ) $=
      ( vy vx cfz co wcel cr wf wa crn wss c0 wne cv cle wbr wral wrex cfv clt
      csup frn adantl cdm wceq eqeq1 biimpd dm0rn0 syl5ibr necon3d ne0i
      syl5com imp fdm sylan2 cuz fsequb2 cvv elfzuz2 mpan sylan wfn fnfvelrn
      ancoms ffn suprub syl31anc ) BCDHIZJZVLKALZMANZKOZVOPQZFRGRSTFVOUAGKUBZBA
      UCZVOJZVSVOKUDUESTVNVPVMVLKAUFUGVMAUHZVLUIZVQVNVMWBVQWBVLPQVQVMWBVOPVLPWB
      WAPUIZVLPUIZVOPUIWBWCWDWAVLPUJUKAULUMUNVLBUOUPUQVLKAURUSDCUTUCJZVNVRVMGFA
      CDVADVBJVMWEEVBBCDVCVDVEVMAVLVFZVTVNWFVMVTVLBAVGVHVLKAVIUSGFVOVSVJVK $.
      $( [29-Jul-2007] $) $( [20-Sep-2005] $)
  $}

