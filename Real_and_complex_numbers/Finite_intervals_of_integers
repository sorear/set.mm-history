$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Finite intervals of integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ... $. $( Dots. $)

  $( Extend class notation to include the notation for a contiguous finite set
     of integers.  Read " ` M ... N ` " as "the set of integers from ` M ` to
     ` N ` inclusive." $)
  cfz $a class ... $.

  ${
    $d m n k z $.
    $( Define an operation that produces a finite set of sequential integers.
       Read " ` M ... N ` " as "the set of integers from ` M ` to ` N `
       inclusive."  See ~ fzval for its value and additional comments. $)
    df-fz $a |- ... = ( m e. ZZ , n e. ZZ |->
                  { k e. ZZ | ( m <_ k /\ k <_ n ) } ) $.
  $}

  ${
    $d k m n z M $.  $d k m n z N $.
    $( The value of a finite set of sequential integers.  E.g., ` 2 ... 5 `
       means the set ` { 2 , 3 , 4 , 5 } ` .  A special case of this definition
       (starting at 1) appears as Definition 11-2.1 of [Gleason] p. 141, where
       ` NN ` _k means our ` 1 ... k ` ; he calls these sets _segments_ of the
       integers.  (Revised by Mario Carneiro, 3-Nov-2013.) $)
    fzval $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M ... N ) =
                 { k e. ZZ | ( M <_ k /\ k <_ N ) } ) $=
      ( vm vn cz cv cle wbr wa crab cfz breq1 anbi1d rabbidv breq2 anbi2d df-fz
      wceq zex rabex ovmpt2 ) DEBCFFDGZAGZHIZUDEGZHIZJZAFKBUDHIZUDCHIZJZAFKLUIU
      GJZAFKUCBSZUHULAFUMUEUIUGUCBUDHMNOUFCSZULUKAFUNUGUJUIUFCUDHPQOADERUKAFTUA
      UB $.
      $( [3-Nov-2013] $) $( [6-Sep-2005] $)

    $( An alternative way of expressing a finite set of sequential integers.
       (Contributed by Mario Carneiro, 3-Nov-2013.) $)
    fzval2 $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
        ( M ... N ) = ( ( M [,] N ) i^i ZZ ) ) $=
      ( vk cz wcel wa cfz co cv cle wbr crab cicc cin fzval wceq cr zssre sseli
      cxr ressxr sstri iccval syl2an ineq1d inrab2 wss sseqin2 mpbi rabeq ax-mp
      eqtri syl6req eqtrd ) ADEZBDEZFZABGHACIZJKURBJKFZCDLZABMHZDNZCABOUQVBUSCT
      LZDNZUTUQVAVCDUOATEBTEVAVCPUPDTADQTRUAUBZSDTBVESCABUCUDUEVDUSCTDNZLZUTUSC
      TDUFVFDPZVGUTPDTUGVHVEDTUHUIUSCVFDUJUKULUMUN $.
      $( [3-Nov-2013] $)

    $( Establish the domain and codomain of the finite integer sequence
       function.  (Contributed by Scott Fenton, 8-Aug-2013.)  (Revised by Mario
       Carneiro, 3-Nov-2013.) $)
    fzf $p |- ... : ( ZZ X. ZZ ) --> ~P ZZ $=
      ( vm vk vn cv cle wbr wa cz crab cpw wcel cxp cfz wf wss ssrab2 zex rabex
      wral elpw mpbir a1i rgen2a df-fz fmpt2 mpbi ) ADZBDZEFUHCDZEFGZBHIZHJZKZC
      HSAHSHHLULMNUMACHUMUGHKUIHKGUMUKHOUJBHPUKHUJBHQRTUAUBUCACHHUKULMBACUDUEUF
      $.
      $( [16-Nov-2013] $) $( [8-Aug-2013] $)
  $}

  ${
    $d j K $.  $d j M $.  $d j N $.
    $( Membership in a finite set of sequential integers. $)
    elfz1 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( K e. ( M ... N ) <->
                 ( K e. ZZ /\ M <_ K /\ K <_ N ) ) ) $=
      ( vj cz wcel wa cfz co cv cle wbr crab w3a fzval wceq breq2 breq1 anbi12d
      eleq2d elrab 3anass bitr4i syl6bb ) BEFCEFGZABCHIZFABDJZKLZUGCKLZGZDEMZFZ
      AEFZBAKLZACKLZNZUEUFUKADBCOTULUMUNUOGZGUPUJUQDAEUGAPUHUNUIUOUGABKQUGACKRS
      UAUMUNUOUBUCUD $.
      $( [21-Jul-2005] $)
  $}

  $( Membership in a finite set of sequential integers. $)
  elfz $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) -> ( K e. ( M ... N ) <->
                 ( M <_ K /\ K <_ N ) ) ) $=
    ( cz wcel cfz co cle wbr wa wb w3a elfz1 3anass baib sylan9bb 3impa 3comr )
    BDEZCDEZADEZABCFGEZBAHIZACHIZJZKZSTUAUFSTJUBUAUCUDLZUAUEABCMUGUAUEUAUCUDNOP
    QR $.
    $( [29-Sep-2005] $)

  ${
    $d k n m z M $.  $d k n m z N $.
    $( Membership in a finite set of sequential integers.  We use the fact that
       an operation's value is empty outside of its domain to show ` M e. ZZ `
       and ` N e. ZZ ` . $)
    elfz2 $p |- ( N e. A -> ( K e. ( M ... N ) <->
           ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) /\ ( M <_ K /\ K <_ N ) ) ) ) $=
      ( wcel cfz co cz wa cle wbr w3a wb elfz1 3anass ibar bitrd adantl c0 wceq
      syl5bb wn cdm cxp cpw fzf fdmi ndmoprg mp3an1 noel pm2.21i simpl pm5.21ni
      eleq2d pm2.61dan df-3an anbi1i anass bitr2i syl6bb ) DAEZBCDFGZEZCHEZDHEZ
      IZBHEZCBJKZBDJKZIZIZIZVDVEVGLZVJIZVAVFVCVLMZVFVOVAVFVCVGVHVILZVLBCDNVPVKV
      FVLVGVHVIOVFVKPUAQRVAVFUBZIZVCBSEZVLVRVBSBFUCHHUDZTVAVQVBSTVTHUEFUFUGCDAH
      HFUHUIUNVQVSVLMVAVSVFVLVSVFBUJUKVFVKULUMRQUOVNVFVGIZVJIVLVMWAVJVDVEVGUPUQ
      VFVGVJURUSUT $.
      $( [3-Nov-2013] $) $( [6-Sep-2005] $)

    $( Lemma for ~ elfzel1 and others. $)
    elfzlem $p |- ( K e. ( M ... N ) ->
                    ( ( M e. ZZ /\ K e. ZZ ) /\ ( M <_ K /\ K <_ N ) ) ) $=
      ( cvv wcel cfz co cz wa cle wbr wi w3a elfz2 3simpb syl6bi wn wrel anbi2d
      pm2.61i anim1i oprprc2 eleq2d simpl anim12i cr zre leid syl 3ad2ant3 jcad
      adantr anass syl6ib c0 wceq n0i cdm cxp relxp cpw fzf fdmi releqi oprprc1
      mpbir pm2.24d syl5 brprc syl5ibr sylbid ) CDEZABCFGZEZBHEZAHEZIZBAJKZACJK
      ZIZIZLVLVNVOCHEZVPMZVTIWADABCNWCVQVTVOWBVPOUAPVLQZVNABBFGZEZWAWDVMWEABCFU
      BUCWFWAWDVQVRAAJKZIZIZBDEZWFWILWJWFVQVRIZWGIWIWJWFWKWGWJWFVOVOVPMZVRABJKZ
      IZIZWKDABBNZWLVQWNVRVOVOVPOVRWMUDUEPWJWFWOWGWPWLWGWNVPVOWGVOVPAUFEWGAUGAU
      HUIUJULPUKVQVRWGUMUNWFWEUOUPZQWJQZWIWEAUQWRWQWIBBFFURZRHHUSZRHHUTWSWTWTHV
      AFVBVCVDVFVEVGVHTWDVTWHVQWDVSWGVRACJVISSVJVKT $.
      $( [3-Nov-2013] $) $( [6-Sep-2005] $)
  $}

  $( Membership in a finite set of sequential integers. $)
  elfz5 $p |- ( ( K e. ( ZZ>= ` M ) /\ N e. ZZ ) ->
                 ( K e. ( M ... N ) <-> K <_ N ) ) $=
    ( cuz cfv wcel cz wa cfz co cle wbr wb eluzelz eluzel2 jca elfz 3expa sylan
    eluzle biantrurd adantr bitr4d ) ABDEFZCGFZHABCIJFZBAKLZACKLZHZUHUDAGFZBGFZ
    HUEUFUIMZUDUJUKBANBAOPUJUKUEULABCQRSUDUHUIMUEUDUGUHBATUAUBUC $.
    $( [26-Dec-2005] $)

  $( Membership in a finite set of sequential integers. $)
  elfz4 $p |- ( ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) /\ ( M <_ K /\ K <_ N ) )
               -> K e. ( M ... N ) ) $=
    ( cz wcel w3a cle wbr cfz co wi wa elfz1 biimprcd 3exp com4r 3impia imp32 )
    BDEZCDEZADEZFBAGHZACGHZABCIJEZSTUAUBUCUDKKUAUBUCSTLZUDUAUBUCUEUDKUEUDUAUBUC
    FABCMNOPQR $.
    $( [21-Jul-2005] $)

  $( Membership in a finite set of sequential integers in terms of sets of
     upper integers. $)
  elfzuzb $p |- ( N e. A -> ( K e. ( M ... N ) <->
         ( K e. ( ZZ>= ` M ) /\ N e. ( ZZ>= ` K ) ) ) ) $=
    ( wcel cfz co cz w3a cle wbr cuz cfv elfz2 an6 eluz2 anbi12i df-3an anandir
    wa ancom anbi2i 3bitri anbi1i bitr4i 3bitr4ri syl6bb ) DAEBCDFGECHEZDHEZBHE
    ZIZCBJKZBDJKZTZTZBCLMEZDBLMEZTZABCDNUHUJULIZUJUIUMIZTUHUJTZUJUITZUNIZURUOUH
    UJULUJUIUMOUPUSUQUTCBPBDPQUOVAVBTZUNTVCUKVDUNUKUHUITUJTVAUIUJTZTVDUHUIUJRUH
    UIUJSVEVBVAUIUJUAUBUCUDVAVBUNRUEUFUG $.
    $( [18-Sep-2005] $)

  $( Membership in a finite set of sequential integers. $)
  eluzfz $p |- ( ( K e. ( ZZ>= ` M ) /\ N e. ( ZZ>= ` K ) )
               -> K e. ( M ... N ) ) $=
    ( cz wcel cuz cfv wa cfz co eluzelz adantl elfzuzb biimpar mpancom ) CDEZAB
    FGEZCAFGEZHZABCIJEZRPQACKLPTSDABCMNO $.
    $( [4-Oct-2005] $)

  $( Membership in a finite set of sequential integers implies membership in a
     set of upper integers. $)
  elfzuz3 $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> N e. ( ZZ>= ` K ) ) $=
    ( wcel cfz co cuz cfv elfzuzb simplbda ) DAEBCDFGEBCHIEDBHIEABCDJK $.
    $( [28-Sep-2005] $)

  ${
    elfzel2.1 $e |- N e. _V $.
    $( Membership in a finite set of sequential integer implies the upper bound
       is an integer. $)
    elfzel2i $p |- ( K e. ( M ... N ) -> N e. ZZ ) $=
      ( cfz co wcel cz w3a cle wbr wa cvv wb elfz2 ax-mp simpl2 sylbi ) ABCEFGZ
      BHGZCHGZAHGZIBAJKACJKLZLZUACMGSUDNDMABCOPTUAUBUCQR $.
      $( [6-Sep-2005] $)
  $}

  $( Membership in a finite set of sequential integers implies the upper bound
     is an integer. $)
  elfzel2g $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> N e. ZZ ) $=
    ( wcel cfz co cz w3a cle wbr wa elfz2 simpl2 syl6bi imp ) DAEZBCDFGEZDHEZQR
    CHEZSBHEZICBJKBDJKLZLSABCDMTSUAUBNOP $.
    $( [6-Sep-2005] $)

  $( Membership in a finite set of sequential integer implies the lower bound
     is an integer. $)
  elfzel1 $p |- ( K e. ( M ... N ) -> M e. ZZ ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simpll syl ) ABCDEFBGFZAGFZHBAIJACIJHZH
    NABCKNOPLM $.
    $( [6-Sep-2005] $)

  $( A member of a finite set of sequential integer is an integer. $)
  elfzelz $p |- ( K e. ( M ... N ) -> K e. ZZ ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simplr syl ) ABCDEFBGFZAGFZHBAIJACIJHZH
    OABCKNOPLM $.
    $( [6-Sep-2005] $)

  $( A member of a finite set of sequential integer is greater than or equal to
     the lower bound. $)
  elfzle1 $p |- ( K e. ( M ... N ) -> M <_ K ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simprl syl ) ABCDEFBGFAGFHZBAIJZACIJZHH
    OABCKNOPLM $.
    $( [6-Sep-2005] $)

  $( A member of a finite set of sequential integer is less than or equal to
     the upper bound. $)
  elfzle2 $p |- ( K e. ( M ... N ) -> K <_ N ) $=
    ( cfz co wcel cz wa cle wbr elfzlem simprr syl ) ABCDEFBGFAGFHZBAIJZACIJZHH
    PABCKNOPLM $.
    $( [6-Sep-2005] $)

  $( Membership in a finite set of sequential integer implies the bounds are
     comparable. $)
  elfzle3 $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> M <_ N ) $=
    ( wcel cfz co wa cle wbr elfzle1 adantl elfzle2 cz elfzel1 elfzelz elfzel2g
    wi cr zre letr syl3an syl3anc mp2and ) DAEZBCDFGEZHZCBIJZBDIJZCDIJZUFUHUEBC
    DKLUFUIUEBCDMLUGCNEZBNEZDNEZUHUIHUJRZUFUKUEBCDOLUFULUEBCDPLABCDQUKCSEULBSEU
    MDSEUNCTBTDTCBDUAUBUCUD $.
    $( [18-Sep-2005] $)

  $( Implication of membership in a finite set of sequential integers. $)
  elfzuz2 $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> N e. ( ZZ>= ` M ) ) $=
    ( wcel cfz co wa cuz cfv elfzuzb ancom syl6bb biimpa uztrn syl ) DAEZBCDFGE
    ZHDBIJEZBCIJZEZHZDTEQRUBQRUASHUBABCDKUASLMNBDCOP $.
    $( [20-Sep-2005] $)

  $( Membership in a finite set of sequential integers - special case. $)
  eluzfz1 $p |- ( N e. ( ZZ>= ` M ) -> M e. ( M ... N ) ) $=
    ( cuz cfv wcel cz cle wbr w3a cfz co eluz2 simp1 simp2 cr zre leid 3ad2ant1
    syl simp3 elfz4 syl32anc sylbi ) BACDEAFEZBFEZABGHZIZAABJKEZABLUGUDUEUDAAGH
    ZUFUHUDUEUFMZUDUEUFNUJUDUEUIUFUDAOEUIAPAQSRUDUEUFTAABUAUBUC $.
    $( [21-Jul-2005] $)

  $( A member of a finite set of sequential integers belongs to a set of upper
     integers. $)
  elfzuz $p |- ( K e. ( M ... N ) -> K e. ( ZZ>= ` M ) ) $=
    ( cfz co wcel cle wbr w3a cuz cfv elfzel1 elfzelz elfzle1 3jca eluz2 sylibr
    cz ) ABCDEFZBRFZARFZBAGHZIABJKFSTUAUBABCLABCMABCNOBAPQ $.
    $( [17-Sep-2005] $)

  $( Membership in a finite set of sequential integers - special case. $)
  eluzfz2 $p |- ( N e. ( ZZ>= ` M ) -> N e. ( M ... N ) ) $=
    ( cuz cfv wcel cz cle wbr cfz co eluzel2 eluzelz eluzle zre leid 3syl elfz4
    cr syl32anc ) BACDEZAFEBFEZUAABGHBBGHZBABIJEABKABLZUCABMTUABREUBUCBNBOPBABQ
    S $.
    $( [13-Sep-2005] $)

  $( Membership in a finite set of sequential integers - special case. $)
  eluzfz2b $p |- ( N e. ( ZZ>= ` M ) <-> N e. ( M ... N ) ) $=
    ( cuz cfv wcel cfz co eluzfz2 elfzuz impbii ) BACDEBABFGEABHBABIJ $.
    $( [14-Sep-2005] $)

  $( Membership in a finite set of sequential integers containing one
     integer. $)
  elfz3 $p |- ( N e. ZZ -> N e. ( N ... N ) ) $=
    ( cz wcel cuz cfv cfz co uzid eluzfz1 syl ) ABCAADECAAAFGCAHAAIJ $.
    $( [21-Jul-2005] $)

  $( Membership in a finite set of sequential integers containing one
     integer. $)
  elfz1eq $p |- ( K e. ( N ... N ) -> K = N ) $=
    ( cfz co wcel cz wa cle wbr wceq elfzlem cr wb letri3 syl2an biimpar eqcomd
    zre syl ) ABBCDEBFEZAFEZGZBAHIABHIGZGZABJABBKUDBAUBBAJZUCTBLEALEUEUCMUABRAR
    BANOPQS $.
    $( [19-Sep-2005] $)

  ${
    $d K k m $.  $d M k m $.  $d N k m $.
    $( A finite set of sequential integers is empty if the bounds are
       reversed. $)
    fzn $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( N < M <-> ( M ... N ) = (/) ) ) $=
      ( vk cz wcel wa clt wbr cfz co c0 wceq cle wn cr wb zre lenlt syl2an w3a
      cuz cfv eluz2 eluzfz1 sylbir n0i syl 3expia wi cv neq0 elfzle3 ex exlimdv
      wex syl5bi adantl impbid bitr3d con4bid ) ADEZBDEZFZBAGHZABIJZKLZVCABMHZV
      DNZVFNZVAAOEBOEVGVHPVBAQBQABRSVCVGVIVAVBVGVIVAVBVGTZAVEEZVIVJBAUAUBEVKABU
      CABUDUEVEAUFUGUHVBVIVGUIVAVICUJZVEEZCUOVBVGCVEUKVBVMVGCVBVMVGDVLABULUMUNU
      PUQURUSUT $.
      $( [18-Apr-2006] $) $( [22-Aug-2005] $)

    $( A shifted finite set of sequential integers is equinumerous to the
       original set.  (Contributed by Paul Chapman, 11-Apr-2009.) $)
    fzen $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) ->
                 ( M ... N ) ~~ ( ( M + K ) ... ( N + K ) ) ) $=
      ( cz wcel w3a co caddc cle wbr wi wa elfz1 biimpd cr zre syl3an syld wceq
      wb vk vm cfz cv cmin cvv oprex a1i 3adant3 zaddcl expcom 3ad2ant3 adantrd
      leadd1 3com23 3expia imp3a 3adant2 adantld 3adant1 syl2anc biimprd zsubcl
      3coml 3jcad com12 3impb leaddsub lesubadd ancoms imp simp1d ex anim12d cc
      zcn subadd eqcom 3bitr3g addcom eqeq2d bitrd 3expib en3d ) BDEZCDEZADEZFZ
      UAUBBCUCGZBAHGZCAHGZUCGZUAUDZAHGZUBUDZAUEGZWIUFEWHBCUCUGUHWHWMWIEZWMDEZBW
      MIJZWMCIJZFZWNWLEZWEWFWQXAKWGWEWFLZWQXAWMBCMNUIZXAWHXBWRWSWTWHXBKWHWRWSWT
      LZLZXBWHXFWNDEZWJWNIJZWNWKIJZFZXBWHXFXGXHXIWHWRXGXEWGWEWRXGKWFWRWGXGWMAUJ
      UKULUMWEWGXFXHKWFWEWGLZWRXEXHWEWGWRXEXHKZWEWRWGXLWEWRWGFZWSXHWTXMWSXHWEBO
      EZWRWMOEZWGAOEZWSXHTBPZWMPZAPZBWMAUNQNUMUOUPUQURWFWGXFXIKWEWFWGLWRXEXIWFW
      GWRXEXIKZWRWFWGXTWRWFWGFZWTXIWSYAWTXIWRXOWFCOEZWGXPWTXITXRCPZXSWMCAUNQNUS
      VDUPUQUTVEWHXBXJWHWJDEZWKDEZXBXJTWEWGYDWFBAUJURZWFWGYEWECAUJUTZWNWJWKMVAV
      BRVFVGVFRWHWOWLEZWODEZWJWOIJZWOWKIJZFZWPWIEZWHYHYLWHYDYEYHYLTYFYGWOWJWKMV
      ANZYLWHYMYIYJYKWHYMKWHYIYJYKLZLZYMWHYPWPDEZBWPIJZWPCIJZFZYMWHYPYQYRYSWHYI
      YQYOWGWEYIYQKWFYIWGYQWOAVCUKULUMWEWGYPYRKWFXKYIYOYRWEWGYIYOYRKWEWGYIFZYJY
      RYKUUAYJYRWEXNWGXPYIWOOEZYJYRTXQXSWOPZBAWOVHQNUMUPUQURWFWGYPYSKZWEWGWFUUD
      WGWFLYIYOYSWGWFYIYOYSKZYIWGWFUUEYIWGWFFZYKYSYJUUFYSYKYIUUBWGXPWFYBYSYKTUU
      CXSYCWOACVIQVBUSVDUPUQVJUTVEWEWFYTYMKWGXCYMYTWPBCMVBUIRVFVGVFRWHWQYHLWRYI
      LZWMWPSZWOWNSZTZWHWQWRYHYIWHWQWRWHWQLWRWSWTWHWQXAXDVKVLVMWHYHYIWHYHLYIYJY
      KWHYHYLYNVKVLVMVNWGWEUUGUUJKWFWGWRYIUUJYIWGWRUUJYIWOVOEZWGAVOEZWRWMVOEZUU
      JWOVPAVPWMVPUUKUULUUMFZUUHWOAWMHGZSZUUIUUNWPWMSUUOWOSUUHUUPWOAWMVQWPWMVRU
      UOWOVRVSUUNUUOWNWOUULUUMUUOWNSUUKAWMVTUTWAWBQVDWCULRWD $.
      $( [11-Apr-2009] $)
  $}

  $( A 1-based finite set of sequential integers is empty iff it ends at index
     ` 0 ` .  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  fz1n $p |- ( N e. NN0 -> ( ( 1 ... N ) = (/) <-> N = 0 ) ) $=
    ( cn0 wcel c1 clt wbr cfz co c0 wceq cc0 cz wb nn0z 1z mpan nn0lt10b bitr3d
    fzn syl ) ABCZADEFZDAGHIJZAKJUAALCZUBUCMZANDLCUDUEODASPTAQR $.
    $( [22-Jun-2011] $)

  $( Two ways to say a finite 1-based sequence is empty.  (Contributed by Paul
     Chapman, 26-Oct-2012.) $)
  0fz1 $p |- ( ( N e. NN0 /\ F Fn ( 1 ... N ) ) -> ( F = (/) <-> N = 0 ) ) $=
    ( c1 cfz co wfn c0 wceq cn0 wcel cc0 fndmu sylan2br ex fneq2 syl6bb biimpcd
    fn0 impbid fz1n sylan9bbr ) ACBDEZFZAGHZUBGHZBIJBKHUCUDUEUCUDUEUDUCAGFZUEAR
    ZUBGALMNUEUCUDUEUCUFUDUBGAOUGPQSBTUA $.
    $( [26-Oct-2012] $)

  $( There are no integers between 1 and 0.  (Contributed by Jeff Madsen,
     16-Jun-2010.) $)
  fz10 $p |- ( 1 ... 0 ) = (/) $=
    ( vx c1 cc0 cfz co c0 wceq cv wcel eq0 cle wbr clt lt01 0re 1re ltnlei mpbi
    wn cr elfzle3 mpan mto mpgbir ) BCDEZFGAHZUEIZSAAUEJUGBCKLZCBMLUHSNCBOPQRCT
    IUGUHOTUFBCUAUBUCUD $.
    $( [16-Jun-2010] $)

  ${
    $d M x $.  $d N x $.  $d A x $.
    $( Properties of a finite interval of integers which is non-empty.
       (Contributed by Jeff Madsen, 17-Jun-2010.) $)
    fzn0 $p |- ( ( N e. A /\ ( M ... N ) =/= (/) ) ->
                                      ( M e. ZZ /\ N e. ZZ /\ M <_ N ) ) $=
      ( vx cfz co c0 wne wcel cv wex cz cle wbr w3a n0 elfzel1 adantl elfzel2g
      wa elfzle3 3jca ex exlimdv imp sylan2b ) BCEFZGHCAIZDJZUGIZDKZBLIZCLIZBCM
      NZOZDUGPUHUKUOUHUJUODUHUJUOUHUJTULUMUNUJULUHUIBCQRAUIBCSAUIBCUAUBUCUDUEUF
      $.
      $( [17-Jun-2010] $)
  $}

  ${
    $d M x $.  $d N x $.  $d K x $.
    $( Split a finite interval of integers into two parts.  (Contributed by
       Jeff Madsen, 17-Jun-2010.) $)
    fzsplit $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> ( M ... N )
                              = ( ( M ... K ) u. ( ( K + 1 ) ... N ) ) ) $=
      ( wcel cfz co cz cle wbr w3a sylan2 wa wi wo cr zre 3anass wb elfz1 vx c1
      caddc cun wceq c0 wne ne0i fzn0 elfzelz elfzle1 elfzle2 3jca an6 lelttric
      cv clt ancoms syl2an zltp1le orbi2d mpbid 3ad2antl2 adantlr biantrud andi
      letr syl3an 3expb ancomsd expdimp anasss ancom2s 3adantl1 anassrs adantld
      adantlrl pm4.71rd ancom anbi1i anass bitri syl6rbbr letrp1 3expa peano2re
      anim12i exp3a 3expia com23 imp syldan syl5 impr 3adantl3 adantlrr adantrd
      sylan anbi2i orbi12d syl5bb bitrd pm5.32da orbi12i bitr4i 3bitr4g 3adant2
      adantr elun 3adant3 peano2z 3adant1 3bitr4d eqrdv exp32 imp42 3impa sylbi
      3adant3l sylancom ) DAEZBCDFGZEZCHEZDHEZCDIJZKZYBCBFGZBUBUCGZDFGZUDZUEZYC
      YAYBUFUGYGYBBUHACDUILYCYGBHEZCBIJZBDIJZKZYLYCYMYNYOBCDUJBCDUKBCDULUMYGYPM
      YDYMMZYEYNMZYFYOMKYLYDYEYFYMYNYOUNYQYRYOYLYFYQYRYOYLYQYEYNYOYLYDYMYEYNYOY
      LNNYDYMYEKZYNYOYLYSYNYOMZMZUAYBYKUUAUAUPZHEZCUUBIJZUUBDIJZKZUUCUUDUUBBIJZ
      KZUUCYIUUBIJZUUEKZOZUUBYBEZUUBYKEZUUAUUCUUDUUEMZMUUCUUDUUGMZUUIUUEMZOZMZU
      UFUUKUUAUUCUUNUUQUUAUUCMZUUNUUNUUGUUIOZMZUUQUUSUUTUUNYSUUCUUTYTYMYDUUCUUT
      YEYMUUCMZUUGBUUBUQJZOZUUTYMBPEZUUBPEZUVDUUCBQZUUBQZUVFUVEUVDUUBBUOURUSUVB
      UVCUUIUUGBUUBUTVAVBVCVDVEUVAUUNUUGMZUUNUUIMZOUUSUUQUUNUUGUUIVFUUSUVIUUOUV
      JUUPUUSUUOUUEUUOMZUVIUUSUUOUUEUUSUUGUUEUUDYSYOUUCUUGUUENZYNYSYOUUCUVLYMYE
      YOUUCMUVLYDYMYEMZUUCYOUVLUVMUUCYOUVLUVMUUCMZYOUUGUUEUVNUUGYOUUEUUCUVMUUGY
      OMUUENZUUCYMYEUVOUUCUVFYMUVEYEDPEUVOUVHUVGDQUUBBDVGVHVIURVJVKVLVMVNVOVQVP
      VRUVIUUEUUDMZUUGMUVKUUNUVPUUGUUDUUEVSVTUUEUUDUUGWAWBWCUUSUUPUUDUUPMZUVJUU
      SUUPUUDUUSUUIUUDUUEYSYNUUCUUIUUDNZYOYSYNUUCUVRYDYMYNUUCMUVRYEYQYNUUCUVRUU
      CUVFYQYNMUVRUVHYQCPEZUVEMZYNUVFUVRNZYDUVSYMUVECQUVGWGUVTYNCYIIJZUWAUVSUVE
      YNUWBCBWDWEUVTUWBUWAUVEUVSYIPEZUWBUWANBWFUVSUWCMUVFUWBUVRUVSUWCUVFUWBUVRN
      UVSUWCUVFKUWBUUIUUDCYIUUBVGWHWIWJLWKWLWRWMWNWOVOWPWQVRUVJUUDUUEUUIMZMUVQU
      UDUUEUUIWAUWDUUPUUDUUEUUIVSWSWBWCWTXAXBXCUUCUUDUUERUUKUUCUUOMZUUCUUPMZOUU
      RUUHUWEUUJUWFUUCUUDUUGRUUCUUIUUERXDUUCUUOUUPVFXEXFYSUULUUFSZYTYDYEUWGYMUU
      BCDTXGXHYSUUMUUKSYTUUMUUBYHEZUUBYJEZOYSUUKUUBYHYJXIYSUWHUUHUWIUUJYDYMUWHU
      UHSYEUUBCBTXJYMYEUWIUUJSZYDYMYIHEYEUWJBXKUUBYIDTWRXLWTXAXHXMXNXOWIXPXQXSX
      RLXT $.
      $( [17-Jun-2010] $)

    $d J x $.  $d A x $.
    $( Condition for two finite intervals of integers to be disjoint.
       (Contributed by Jeff Madsen, 17-Jun-2010.) $)
    fzdisj $p |- ( ( K e. A /\ K < M ) ->
                                ( ( J ... K ) i^i ( M ... N ) ) = (/) ) $=
      ( vx wcel wbr wa cfz co wn cr cle cz zre syl wi ancom2s imp clt cv cin c0
      wal wceq elin elfzel2g elfzle2 adantl elfzel1 elfzle1 elfzelz jca31 3expb
      jca letr an12s anasss lenlt ancoms ad2antrl mpbid expcom an4s impr syl2an
      wb expl syl5bi con2d alrimiv eq0 sylibr ) CAGZCDUAHZIZFUBZBCJKZDEJKZUCZGZ
      LZFUEWAUDUFVQWCFVOVPWCVOWBVPWBVRVSGZVRVTGZIVOVPLZVRVSVTUGVOWDWEWFVOWDIZCM
      GZVRCNHZIZDMGZDVRNHZIZVRMGZIWFWEWGWHWIWGCOGWHAVRBCUHCPQWDWIVOVRBCUIUJUPWE
      WKWLWNWEDOGWKVRDEUKDPQVRDEULWEVROGWNVRDEUMVRPQUNWJWMWNWFWHWKWIWLWNWFRWNWH
      WKIZWIWLIZIZWFWNWQIDCNHZWFWNWOWPWRWNWOIZWLWIWRWSWLWIIZWRWNWKWHWTWRRZWKWNW
      HXAWKWNWHXADVRCUQUOURSTSUSWOWRWFVHZWNWPWKWHXBDCUTVAVBVCVDVEVFVGVIVJVKTVLF
      WAVMVN $.
      $( [17-Jun-2010] $)
  $}

  $( 0-based and 1-based finite sets of sequential integers are equinumerous.
     (Contributed by Paul Chapman, 11-Apr-2009.) $)
  fz01en $p |- ( N e. ZZ -> ( 0 ... ( N - 1 ) ) ~~ ( 1 ... N ) ) $=
    ( cz wcel cc0 c1 cmin co cfz caddc cen wbr peano2zm 0z 1z fzen mp3an13 wceq
    syl ax-1cn cc addid2i a1i zcn npcan sylancl opreq12d breq2d mpbid ) ABCZDAE
    FGZHGZDEIGZUJEIGZHGZJKZUKEAHGZJKUIUJBCZUOALDBCUQEBCUOMNEDUJOPRUIUNUPUKJUIUL
    EUMAHULEQUIESUAUBUIATCETCUMAQAUCSAEUDUEUFUGUH $.
    $( [11-Apr-2009] $)

  $( A member of a finite set of sequential integers starting at 1 is a natural
     number. $)
  elfznn $p |- ( K e. ( 1 ... N ) -> K e. NN ) $=
    ( c1 cfz co wcel cz cle wbr cn elfzelz elfzle1 elnnz1 sylanbrc ) ACBDEFAGFC
    AHIAJFACBKACBLAMN $.
    $( [24-Aug-2005] $)

  $( Membership in a finite set of sequential integers starting at 0. $)
  elfz2nn0 $p |- ( N e. A ->
           ( K e. ( 0 ... N ) <-> ( K e. NN0 /\ N e. NN0 /\ K <_ N ) ) ) $=
    ( wcel cc0 cfz co cz w3a cle wbr wa cn0 elfz2 simpr simpl cr zre jca elnn0z
    anim12i simpll wi 0re letr mp3an1 ancoms syl2an imp simprr 3jca 3adantl1 0z
    a1i 3anim123i 3com13 id adantll 3adant2 impbii biid 3anbi123i bitr4i syl6bb
    ) CADBECFGDEHDZCHDZBHDZIZEBJKZBCJKZLZLZBMDZCMDZVJIZABECNVLVGVILZVFECJKZLZVJ
    IZVOVLVSVFVGVKVSVEVFVGLZVKLZVPVRVJVTVGVKVIVFVGOVIVJPUAWAVFVQVFVGVKUBVTVKVQV
    FCQDZBQDZVKVQUCZVGCRBRWCWBWDEQDWCWBWDUDEBCUEUFUGUHUISVTVIVJUJUKULVSVHVKVJVR
    VPVHVJVEVRVFVPVGVEVJUMUNVFVQPVGVIPUOUPVPVJVKVRVIVJVKVGVKUQURUSSUTVMVPVNVRVJ
    VJBTCTVJVAVBVCVD $.
    $( [16-Sep-2005] $)

  $( A member of a finite set of sequential integers starting at 0 is a
     nonnegative integer. $)
  elfznn0 $p |- ( K e. ( 0 ... N ) -> K e. NN0 ) $=
    ( cc0 cfz co wcel cz cle wbr cn0 elfzelz elfzle1 elnn0z sylanbrc ) ACBDEFAG
    FCAHIAJFACBKACBLAMN $.
    $( [5-Aug-2005] $)

  $( The upper bound of a nonempty finite set of sequential integers starting
     at 0 is a nonnegative integer. $)
  elfz3nn0 $p |- ( ( N e. A /\ K e. ( 0 ... N ) ) -> N e. NN0 ) $=
    ( wcel cc0 cfz co cn0 cle wbr w3a elfz2nn0 simp2 syl6bi imp ) CADZBECFGDZCH
    DZPQBHDZRBCIJZKRABCLSRTMNO $.
    $( [16-Sep-2005] $)

  $( Subtraction closure for a member of a finite set of sequential
     integers. $)
  fznn0sub $p |- ( ( N e. A /\ K e. ( M ... N ) ) -> ( N - K ) e. NN0 ) $=
    ( wcel cfz co cmin cn0 cz w3a cle wa elfz2 znn0sub2 3com12 adantrl 3adantl1
    wbr 3expa syl6bi imp ) DAEZBCDFGEZDBHGIEZUCUDCJEZDJEZBJEZKCBLSZBDLSZMZMUEAB
    CDNUGUHUKUEUFUGUHMUJUEUIUGUHUJUEUHUGUJUEBDOPTQRUAUB $.
    $( [16-Sep-2005] $)

  $( Subtraction closure for a member of a finite set of sequential
     integers. $)
  fznn0sub2 $p |- ( ( N e. A /\ K e. ( 0 ... N ) ) ->
                   ( N - K ) e. ( 0 ... N ) ) $=
    ( wcel cc0 cfz co cn0 cle wbr w3a wa fznn0sub elfz3nn0 elfzle1 adantl cz cr
    cmin zre elfzel2g elfzelz subge02 syl2an syl2anc mpbid 3jca elfz2nn0 syldan
    wb biimpar ) CADZBECFGZDZCBSGZHDZCHDZUOCIJZKZUOUMDZULUNLZUPUQURABECMABCNVAE
    BIJZURUNVBULBECOPVACQDZBQDZVBURUJZABECUAUNVDULBECUBPVCCRDBRDVEVDCTBTCBUCUDU
    EUFUGULUTUSAUOCUHUKUI $.
    $( [26-Sep-2005] $)

  $( Membership of a sum in a finite set of sequential integers. $)
  fzaddel $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( J e. ( M ... N ) <-> ( J + K ) e. ( ( M + K ) ... ( N + K ) ) ) ) $=
    ( cz wcel wa cle wbr w3a caddc co cfz wb zaddcl a1d cr zre leadd1 syl3an
    simpl impbid adantl adantlr 3com12 adantll 3anbi123d adantr syl2an anandirs
    3expb elfz1 adantrl 3bitr4d ) CEFZDEFZGZAEFZBEFZGZGZURCAHIZADHIZJZABKLZEFZC
    BKLZVEHIZVEDBKLZHIZJZACDMLFZVEVGVIMLFZVAURVFVBVHVCVJUTURVFNUQUTURVFUTVFURAB
    OPUTURVFURUSUAPUBUCUOUTVBVHNZUPUOURUSVNUOCQFURAQFZUSBQFZVNCRARZBRZCABSTUKUD
    UPUTVCVJNZUOUPURUSVSURUPUSVSURVOUPDQFUSVPVSVQDRVRADBSTUEUKUFUGUQVLVDNUTACDU
    LUHUQUSVMVKNZURUOUPUSVTUOUSGVGEFVIEFVTUPUSGCBODBOVEVGVIULUIUJUMUN $.
    $( [30-Jul-2005] $)

  $( Membership of a difference in a finite set of sequential integers. $)
  fzsubel $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( J e. ( M ... N ) <-> ( J - K ) e. ( ( M - K ) ... ( N - K ) ) ) ) $=
    ( cz wcel wa cfz co cneg caddc cmin wb znegcl fzaddel cc zcn anim12i negsub
    wceq sylanr2 adantl opreqan12d anandirs adantrl eleq12d syl2an bitrd ) CEFZ
    DEFZGZAEFZBEFZGZGACDHIFZABJZKIZCUPKIZDUPKIZHIZFZABLIZCBLIZDBLIZHIZFZUMUKULU
    PEFUOVAMBNAUPCDOUAUKCPFZDPFZGZAPFZBPFZGZVAVFMUNUIVGUJVHCQDQRULVJUMVKAQBQRVI
    VLGUQVBUTVEVLUQVBTVIABSUBVIVKUTVETZVJVGVHVKVMVGVKGVHVKGURVCUSVDHCBSDBSUCUDU
    EUFUGUH $.
    $( [1-Aug-2006] $) $( [30-Jul-2005] $)

  $( A finite set of sequential integers can represent an ordered pair. $)
  fzopth $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. A ) ->
                ( ( M ... N ) = ( J ... K ) <-> ( M = J /\ N = K ) ) ) $=
    ( cuz cfv wcel wa cfz co wceq eluzfz2 eleq2 syl5ib syl6com wi cle wbr cr wb
    anim1d elfzuz2 ancoms eluzfz1 syl5ibr elfzle1 adantl adantr jcad cz eluzel2
    zre letri3 syl2an sylibrd elfzle2 anim12ii eluzelre ex mpdd opreq12 impbid1
    syl com23 ) EDFGHZCAHZIZDEJKZBCJKZLZDBLZECLZIZVHVKCBFGHZVNVKVHEVJHZVGIVOVKV
    FVPVGVFEVIHVKVPDEMVIVJENOZUBVGVPVOAEBCUCUDPVFVKVOVNQQVGVFVOVKVNVFVOVKVNQVFV
    OIZVKVLVMVRVKDBRSZBDRSZIZVLVRVKVSVTVOVKVSQVFVKVOBVIHZVSVOWBVKBVJHBCUEVIVJBN
    UFBDEUGPUHVFVKVTQVOVKVFDVJHZVTVFDVIHVKWCDEUEVIVJDNODBCUGPUIUJVFDTHZBTHZVLWA
    UAVOVFDUKHWDDEULDUMVDVOBUKHWEBCULBUMVDDBUNUOUPVRVKECRSZCERSZIZVMVFVKWFVOWGV
    KVFVPWFVQEBCUQPVKVOCVIHZWGVOWIVKCVJHBCMVIVJCNUFCDEUQPURVFETHCTHVMWHUAVODEUS
    BCUSECUNUOUPUJUTVEUIVADBECJVBVC $.
    $( [31-Oct-2005] $)

$(
  @{
    @d j K @.  @d j M @.  @d j N @.
    @( Union of two adjacent finite sets of sequential integers. @)
    fzunt @p |- ( ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) /\ ( K <_ M /\ M <_ N ) )
                 -> ( ( K ... M ) u. ( M ... N ) ) = ( K ... N ) ) @=
      ( vj cz wcel w3a cle wbr wa cfz co cun cv wo cr wb wi letr exp3a com23
      3exp com3l imp42 adantlll adantrl anim2d 3expa imp adantlrl adantrr
      anim1d jaod impbid exp42 com34 ex 3imp zre syl3anOLD syl5OLD pm5.32d andi
      bicomi syl5bbOLD elfz1 3anass syl6bb 3adant3 adantr 3adant1 orbi12d
      3adant2 3bitr4d elun eqrdv ) AEFZBEFZCEFZGZABHIZBCHIZJZJZDABKLZBCKLZMZACK
      LZWDDNZWEFZWIWFFZOZWIWHFZWIWGFWDWIEFZAWIHIZWIBHIZJZJZWNBWIHIZWICHIZJZJZOZ
      WNWOWTJZJZWLWMWDWNWQXAOZJZXEXCWDWNXFXDWDWIPFZXFXDQZWNVTWCXHXIRZAPFZBPFZCP
      FZWCXJRZVQVRVSXKXLXMXNXKXLXMXNRXKXLJZXMXHWCXIXOXMXHWCXIXOXMXHJZJZWCJZXFXD
      XRWQXDXAXRWPWTWOXQWBWPWTRZWAXLXPWBXSXKXLXMXHWBXSXHXLXMWBXSRZXHXLXMXTXHXLX
      MGZWPWBWTYAWPWBWTWIBCSTUAUBUCUDUEUFUGXRWSWOWTXQWAWSWORZWBXOXHWAYBXMXOXHJZ
      WAYBYCWAWSWOXKXLXHWAWSJWORABWISUHTUIUJUKULUM?UNUOUPUQURAUSBUSCUSUTUIWIUSV
      AVBXGXCWNWQXAVCVDVEWDWJWRWKXBVTWJWRQZWCVQVRYDVSVQVRJWJWNWOWPGWRWIABVFWNWO
      WPVGVHVIVJVTWKXBQZWCVRVSYEVQVRVSJWKWNWSWTGXBWIBCVFWNWSWTVGVHVKVJVLVTWMXEQ
      ZWCVQVSYFVRVQVSJWMWNWOWTGXEWIACVFWNWOWTVGVHVMVJVNWIWEWFVOVEVP @.
      @( [ ?] @) @( [21-Jul-2005] @)
  @}
$)

  ${
    $d k M $.  $d k N $.  $d k K $.  $d k V $.
    $( Subset relationship for finite sets of sequential integers.  (Revised by
       Mario Carneiro, 30-Jun-2013.) $)
    fzss1 $p |- ( ( K e. ( ZZ>= ` M ) /\ N e. V ) ->
                  ( K ... N ) C_ ( M ... N ) ) $=
      ( vk wcel wa cfz co cz cle wbr crab adantr cr zre sylan wceq fzval com12
      cuz cfv elfzel2g expcom anim2d wss eluzle eluzel2 eluzelz jca letr syl3an
      cv wi 3expa mpand anim1d ss2rabdv 3sstr4d sseld syld ssrdv ) ABUAUBFZCDFZ
      GZEACHIZBCHIZEUMZVFFZVEVHVGFZVIVEVCCJFZGZVJVIVDVKVCVDVIVKDVHACUCUDUEVLVIV
      JVLVFVGVHVLAVHKLZVHCKLZGZEJMZBVHKLZVNGZEJMZVFVGVCVPVSUFVKVCVOVREJVCVHJFZG
      ZVMVQVNWABAKLZVMVQVCWBVTBAUGNVCBJFZAJFZGVTWBVMGVQUNZVCWCWDBAUHZBAUIZUJWCW
      DVTWEWCBOFWDAOFVTVHOFWEBPAPVHPBAVHUKULUOQUPUQURNVCWDVKVFVPRWGEACSQVCWCVKV
      GVSRWFEBCSQUSUTTVATVB $.
      $( [30-Jun-2013] $) $( [28-Sep-2005] $)

    $( Subset relationship for finite sets of sequential integers.  (Revised by
       Mario Carneiro, 30-Jun-2013.) $)
    fzss2 $p |- ( N e. ( ZZ>= ` K ) -> ( M ... K ) C_ ( M ... N ) ) $=
      ( vk cuz wcel cfz co cz wi wa cle wbr crab adantl zre sylan2 wceq fzval
      cr cfv cv elfzel1 wss eluzle eluzel2 eluzelz jca letr syl3an 3expb mpan2d
      ancoms anim2d ss2rabdv 3sstr4d sseld expcom com23 mpdi ssrdv ) CAEUAFZDBA
      GHZBCGHZVBDUBZVCFZBIFZVEVDFZVEBAUCVBVGVFVHVGVBVFVHJVGVBKZVCVDVEVIBVELMZVE
      ALMZKZDINZVJVECLMZKZDINZVCVDVBVMVPUDVGVBVLVODIVBVEIFZKVKVNVJVQVBVKVNJVQVB
      KVKACLMZVNVBVRVQACUEOVBVQAIFZCIFZKVKVRKVNJZVBVSVTACUFZACUGZUHVQVSVTWAVQVE
      TFVSATFVTCTFWAVEPAPCPVEACUIUJUKQULUMUNUOOVBVGVSVCVMRWBDBASQVBVGVTVDVPRWCD
      BCSQUPUQURUSUTVA $.
      $( [30-Jun-2013] $) $( [4-Oct-2005] $)

    $( A finite set of sequential integers is a subset of a set of upper
       integers. $)
    fzssuz $p |- ( M ... N ) C_ ( ZZ>= ` M ) $=
      ( vk cfz co cuz cfv cv elfzuz ssriv ) CABDEAFGCHABIJ $.
      $( [28-Oct-2005] $)
  $}

  ${
    $d k M $.  $d k N $.  $d k V $.
    $( Subset relationship for finite sets of sequential integers. $)
    fzssp1 $p |- ( N e. V -> ( M ... N ) C_ ( M ... ( N + 1 ) ) ) $=
      ( vk wcel cfz co c1 caddc cv wa simpr cuz cfv elfzel2g uzid peano2uz 3syl
      wss cz fzss2 syl sseld mpd ex ssrdv ) BCEZDABFGZABHIGZFGZUGDJZUHEZUKUJEZU
      GULKZULUMUGULLUNUHUJUKUNUIBMNZEZUHUJSUNBTEBUOEUPCUKABOBPBBQRBAUIUAUBUCUDU
      EUF $.
      $( [21-Jul-2005] $)
  $}
  $( Append an element to a finite set of sequential integers. $)
  elfzp1 $p |- ( N e. ( ZZ>= ` M ) ->
    ( K e. ( M ... ( N + 1 ) ) <-> ( K e. ( M ... N ) \/ K = ( N + 1 ) ) ) ) $=
    ( wcel co cfz wceq wo wa cz cle wbr ad2antrr ad2antlr cr wb zre syl syl2anc
    mpbird cuz cfv c1 caddc wne wi w3a eluzel2 eluzelz elfzelz 3jca elfzle1 clt
    elfzle2 adantl anim1i peano2re 3syl ltlen zleltp1 jca32 elfz2 sylibrd eqcom
    ex adantr orbi1i neor orcom 3bitr3i sylib sseld peano2uz eluzfz2 syl5ibrcom
    fzssp1 eleq1 jaod impbid ) CBUAUBZDZABCUCUDEZFEZDZABCFEZDZAWBGZHZWAWDWHWAWD
    IZWBAUEZWFUFZWHWIWJBJDZCJDZAJDZUGZBAKLZACKLZIIZWFWIWJWRWIWJIZWOWPWQWSWLWMWN
    WAWLWDWJBCUHMWAWMWDWJBCUIZMZWDWNWAWJABWBUJZNZUKWDWPWAWJABWBULNWSWQAWBUMLZWS
    XDAWBKLZWJIZWIXEWJWDXEWAABWBUNUOUPWSAODZWBODZXDXFPWDXGWAWJWDWNXGXBAQRNWAXHW
    DWJWAWMCODXHWTCQCUQURMAWBUSSTWSWNWMWQXDPXCXAACUTSTVAVEWAWFWRPWDVTABCVBVFVCW
    BAGZWFHWGWFHWKWHXIWGWFWBAVDVGWFWBAVHWGWFVIVJVKVEWAWFWDWGWAWEWCABCVTVPVLWAWD
    WGWBWCDZWAWBVTDXJBCVMBWBVNRAWBWCVQVOVRVS $.
    $( [2-Sep-2013] $) $( [19-Sep-2005] $)

  ${
    $d M k $.  $d N k $.
    $( Join a successor to the end of a finite set of sequential integers.
       (Revised by Mario Carneiro, 7-Mar-2014.) $)
    fzsuc $p |- ( N e. ( ZZ>= ` M ) ->
                  ( M ... ( N + 1 ) ) = ( ( M ... N ) u. { ( N + 1 ) } ) ) $=
      ( vk cuz cfv wcel c1 caddc co cfz csn cun cv wceq elfzp1 elun elsn orbi2i
      wo bitri syl6bbr eqrdv ) BADEFZCABGHIZJIZABJIZUDKZLZUCCMZUEFUIUFFZUIUDNZS
      ZUIUHFZUIABOUMUJUIUGFZSULUIUFUGPUNUKUJCUDQRTUAUB $.
      $( [7-Mar-2014] $) $( [19-Jul-2008] $)
  $}

  $( Subset relationship for finite sets of sequential integers.  (Revised by
     Mario Carneiro, 30-Jun-2013.) $)
  fzp1ss $p |- ( ( M e. ZZ /\ N e. V ) ->
                ( ( M + 1 ) ... N ) C_ ( M ... N ) ) $=
    ( cz wcel c1 caddc co cuz cfv cfz wss uzid peano2uz syl fzss1 sylan ) ADEZA
    FGHZAIJZEZBCESBKHABKHLRATEUAAMAANOSABCPQ $.
    $( [30-Jun-2013] $) $( [26-Jul-2005] $)

  $( Membership in a set of sequential integers with an appended element. $)
  fzelp1 $p |- ( ( N e. A /\ K e. ( M ... N ) ) ->
                K e. ( M ... ( N + 1 ) ) ) $=
    ( wcel cfz co c1 caddc fzssp1 sseld imp ) DAEZBCDFGZEBCDHIGFGZEMNOBCDAJKL
    $.
    $( [2-Sep-2013] $) $( [7-Dec-2005] $)
  $( Add one to an element of a finite set of integers.  (Contributed by Jeff
     Madsen, 6-Jun-2010.) $)
  fzp1elp1 $p |- ( ( N e. A /\ K e. ( M ... N ) ) ->
                                  ( K + 1 ) e. ( M ... ( N + 1 ) ) ) $=
    ( wcel cfz co wa c1 caddc cvv wss elfzel1 adantl oprex fzp1ss sylancl simpr
    cz wb elfzel2g elfzelz 1z a1i fzaddel syl22anc mpbid sseldd ) DAEZBCDFGEZHZ
    CIJGDIJGZFGZCULFGZBIJGZUKCSEZULKEUMUNLUJUPUIBCDMNZDIJOCULKPQUKUJUOUMEZUIUJR
    UKUPDSEBSEZISEZUJURTUQABCDUAUJUSUIBCDUBNUTUKUCUDBICDUEUFUGUH $.
    $( [6-Jun-2010] $)

  ${
    fzelp1.1 $e |- N e. _V $.
    $( Membership in a set of sequential integers with an appended element. $)
    fzelp1i $p |- ( K e. ( M ... N ) -> K e. ( M ... ( N + 1 ) ) ) $=
      ( cvv wcel cfz co c1 caddc fzelp1 mpan ) CEFABCGHFABCIJHGHFDEABCKL $.
      $( [3-Dec-2005] $)
  $}


  ${
    $d M m $.
    $( A finite interval of integers with one element.  (Contributed by Jeff
       Madsen, 2-Sep-2009 ) $)
    fzsn $p |- ( M e. ZZ -> ( M ... M ) = { M } ) $=
      ( vm cz wcel cfz co csn cv elfz1eq elfz3 eleq1 syl5ibrcom impbid2 syl6bbr
      wceq elsn eqrdv ) ACDZBAAEFZAGZRBHZSDZUAAOZUATDRUBUCUAAIRUBUCASDAJUAASKLM
      BAPNQ $.
      $( [1-Dec-2010] $)  $( [2-Sep-2009] $)

    $( A finite interval of integers with two elements.  (Contributed by Jeff
       Madsen, 2-Sep-2009 ) $)
    fzpr $p |- ( M e. ZZ -> ( M ... ( M + 1 ) ) = { M , ( M + 1 ) } ) $=
      ( vm cz wcel c1 caddc co cfz cpr cv wceq cuz cfv uzid elfzp1 syl csn fzsn
      wo wb eleq2d elsn syl6bb orbi1d bitrd vex elpr syl6bbr eqrdv ) ACDZBAAEFG
      ZHGZAUKIZUJBJZULDZUNAKZUNUKKZSZUNUMDUJUOUNAAHGZDZUQSZURUJAALMDUOVATANUNAA
      OPUJUTUPUQUJUTUNAQZDUPUJUSVBUNARUABAUBUCUDUEUNAUKBUFUGUHUI $.
      $( [1-Dec-2010] $)  $( [2-Sep-2009] $)

    $( A finite interval of integers with three elements. $)
    fztp $p |- ( M e. ZZ -> ( M ... ( M + 2 ) )
                 = { M , ( M + 1 ) , ( M + 2 ) } ) $=
      ( cz wcel c1 caddc co cfz csn cun c2 ctp cuz cfv wceq peano2uz syl ax-1cn
      uzid cc syl6eqr fzsuc zcn ax-addass mp3an23 df-2 opreq2i opreq2d cpr fzpr
      sneqd uneq12d df-tp 3eqtr3d ) ABCZAADEFZDEFZGFZAUOGFZUPHZIZAAJEFZGFAUOVAK
      ZUNUOALMZCZUQUTNUNAVCCVDARAAOPAUOUAPUNUPVAAGUNUPADDEFZEFZVAUNASCZUPVFNZAU
      BVGDSCZVIVHQQADDUCUDPJVEAEUEUFTZUGUNUTAUOUHZVAHZIVBUNURVKUSVLAUIUNUPVAVJU
      JUKAUOVAULTUM $.
      $( [7-Mar-2014] $) $( [13-Sep-2011] $)
  $}

  ${
    $d M k $.  $d N k $.
    $( Join a successor to the end of a finite set of sequential integers.
       (Contributed by Mario Carneiro, 7-Mar-2014.) $)
    fzsuc2 $p |- ( ( M e. ZZ /\ N e. ( ZZ>= ` ( M - 1 ) ) ) ->
                   ( M ... ( N + 1 ) ) = ( ( M ... N ) u. { ( N + 1 ) } ) ) $=
      ( c1 cmin co cuz cfv wcel cz wceq caddc cfz csn cun c0 cc opreq2d uneq12d
      syl sneqd wo uzp1 fzsn uncom un0 eqtri syl6eqr zcn ax-1cn sylancl clt wbr
      npcan cr zre wb peano2zm fzn mpdan mpbid 3eqtr4d opreq1 opreq2 syl5ibrcom
      ltm1 eqeq12d imp wa fveq2d eleq2d biimpa fzsuc jaodan sylan2 ) BACDEZFGHA
      IHZBVOJZBVOCKEZFGZHZUAABCKEZLEZABLEZWAMZNZJZVOBUBVPVQWFVTVPVQWFVPWFVQAVRL
      EZAVOLEZVRMZNZJVPAALEZOAMZNZWGWJVPWKWLWMAUCWMWLONWLOWLUDWLUEUFUGVPVRAALVP
      APHCPHVRAJAUHUIACUMUJZQVPWHOWIWLVPVOAUKULZWHOJZVPAUNHWOAUOAVESVPVOIHWOWPU
      PAUQAVOURUSUTVPVRAWNTRVAVQWBWGWEWJVQWAVRALBVOCKVBZQVQWCWHWDWIBVOALVCVQWAV
      RWQTRVFVDVGVPVTVHBAFGZHZWFVPVTWSVPVSWRBVPVRAFWNVIVJVKABVLSVMVN $.
      $( [7-Mar-2014] $)
  $}

  $( ` ( M ... ( N + 1 ) ) ` is the disjoint union of ` ( M ... N ) ` with
     ` { ( N + 1 ) } ` .  (Contributed by Mario Carneiro, 7-Mar-2014.) $)
  fzp1disj $p |- ( N e. V -> ( ( M ... N ) i^i { ( N + 1 ) } ) = (/) ) $=
    ( wcel c1 caddc co cfz wn csn cin c0 wceq wi wa cle wbr elfzle2 adantl cr
    cz elfzel2g zre ltp1 wb peano2re ltnle mpdan mpbid 3syl pm2.65i imnan mpbir
    clt disjsn sylibr ) BCDZBEFGZABHGZDZIZUSURJKLMUQVANUQUTOZIVBURBPQZUTVCUQURA
    BRSVBBUADBTDZVCIZCURABUBBUCVDBURUNQZVEBUDVDURTDVFVEUEBUFBURUGUHUIUJUKUQUTUL
    UMUSURUOUP $.
    $( [7-Mar-2014] $)

  ${
    $d x A $.  $d x B $.  $d x F $.
    $( Two ways of defining the first two values of a sequence on ` NN ` . $)
    fzprval $p |- ( A. x e. ( 1 ... 2 ) ( F ` x ) = if ( x = 1 , A , B )
                  <-> ( ( F ` 1 ) = A /\ ( F ` 2 ) = B ) ) $=
      ( cfv c1 wceq c2 cfz co wral cpr wsbc wa df-2 cr 1re elexi 2re fveq2 wcel
      cv cif caddc cz 1z fzpr ax-mp opreq2i preq2i 3eqtr4i raleqi ralpr eqeq12d
      iftrue sbcie wn wne ltneii pm13.181 mpan2 df-ne sylib iffalse syl anbi12i
      1lt2 3bitri ) AUBZDEZVIFGZBCUCZGZAFHIJZKVMAFHLZKVMAFMZVMAHMZNFDEZBGZHDEZC
      GZNVMAVNVOFFFUDJZIJZFWBLZVNVOFUEUAWCWDGUFFUGUHHWBFIOUIHWBFOUJUKULVMAFHFPQ
      RZHPSRZUMVPVSVQWAVMVSAFWEVKVJVRVLBVIFDTVKBCUOUNUPVMWAAHWFVIHGZVJVTVLCVIHD
      TWGVKUQZVLCGWGVIFURZWHWGHFURWIFHQSVGUSVIHFUTVAVIFVBVCVKBCVDVEUNUPVFVH $.
      $( [5-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x F $.
    $( Two ways of defining the first three values of a sequence on ` NN ` . $)
    fztpval $p |- ( A. x e. ( 1 ... 3 ) ( F ` x )
                      = if ( x = 1 , A , if ( x = 2 , B , C ) )
                  <-> ( ( F ` 1 ) = A /\ ( F ` 2 ) = B /\ ( F ` 3 ) = C ) ) $=
      ( cfv c1 wceq c2 c3 cfz co ctp wsbc caddc ax-mp elexi 3re fveq2 wne cv cz
      cif wral w3a wcel 1z fztp df-3 2cn addcomi eqtri opreq2i tpeq3 df-2 tpeq2
      ax-1cn 3eqtr4i raleqi cc cr raltp iftrue eqeq12d sbcie wn 1re 1lt2 ltneii
      2re pm13.181 mpan2 df-ne sylib iffalse eqtrd 1lt3 2lt3 3anbi123i 3bitri
      syl ) AUAZEFZWBGHZBWBIHZCDUCZUCZHZAGJKLZUDWHAGIJMZUDWHAGNZWHAINZWHAJNZUEG
      EFZBHZIEFZCHZJEFZDHZUEWHAWIWJGGIOLZKLZGGGOLZWTMZWIWJGUBUFXAXCHUGGUHPJWTGK
      JIGOLWTUIIGUJUQUKULZUMWJGIWTMZXCJWTHWJXEHXDJWTGIUNPIXBHXEXCHUOIXBGWTUPPUL
      URUSWHAGIJGUTUQQZIUTUJQZJVARQZVBWKWOWLWQWMWSWHWOAGXFWDWCWNWGBWBGESWDBWFVC
      VDVEWHWQAIXGWEWCWPWGCWBIESWEWGWFCWEWDVFZWGWFHZWEWBGTZXIWEIGTXKGIVGVJVHVIW
      BIGVKVLWBGVMZVNWDBWFVOZWAWECDVCVPVDVEWHWSAJXHWBJHZWCWRWGDWBJESXNWGWFDXNXI
      XJXNXKXIXNJGTXKGJVGRVQVIWBJGVKVLXLVNXMWAXNWEVFZWFDHXNWBITZXOXNJITXPIJVJRV
      RVIWBJIVKVLWBIVMVNWECDVOWAVPVDVEVSVT $.
      $( [13-Sep-2011] $)
  $}

  $( Reversal of start and end of a finite set of sequential integers. $)
  fzrev $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( K e. ( ( J - N ) ... ( J - M ) ) <-> ( J - K ) e. ( M ... N ) ) ) $=
    ( cz wcel wa cmin co cle wbr cfz wb cr zre syl3an 3expb zsubcl ancoms elfz
    ancom suble 3comr adantll lesub adantlr anbi12d syl5rbbr ad2ant2lr ad2ant2r
    simprr syl3anc adantl simpll simplr 3bitr4d ) CEFZDEFZGZAEFZBEFZGZGZADHIZBJ
    KZBACHIZJKZGZCABHIZJKZVIDJKZGZBVDVFLIFZVICDLIFZVLVKVJGVCVHVKVJUAVCVKVEVJVGU
    RVBVKVEMZUQURUTVAVOUTVAURVOUTANFZVABNFZURDNFVOAOZBOZDOABDUBPUCQUDUQVBVJVGMZ
    URUQUTVAVTUQCNFUTVPVAVQVTCOVRVSCABUEPQUFUGUHVCVAVDEFZVFEFZVMVHMUSUTVAUKURUT
    WAUQVAUTURWAADRSUIUQUTWBURVAUTUQWBACRSUJBVDVFTULVCVIEFZUQURVNVLMVBWCUSABRUM
    UQURVBUNUQURVBUOVICDTULUP $.
    $( [25-Nov-2005] $)

  $( Reversal of start and end of a finite set of sequential integers. $)
  fzrev2 $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ ( J e. ZZ /\ K e. ZZ ) ) ->
         ( K e. ( M ... N ) <-> ( J - K ) e. ( ( J - N ) ... ( J - M ) ) ) ) $=
    ( cz wcel wa cmin co cfz wb simpl zsubcl jca fzrev sylan2 wceq cc zcn nncan
    syl2an adantl eleq1d bitr2d ) CEFDEFGZAEFZBEFZGZGZABHIZADHIACHIJIFZAUJHIZCD
    JIZFZBUMFUHUEUFUJEFZGUKUNKUHUFUOUFUGLABMNAUJCDOPUIULBUMUHULBQZUEUFARFBRFUPU
    GASBSABTUAUBUCUD $.
    $( [25-Nov-2005] $)

  $( Reversal of start and end of a finite set of sequential integers. $)
  fzrev2i $p |- ( ( N e. A /\ J e. ZZ /\ K e. ( M ... N ) )
                 -> ( J - K ) e. ( ( J - N ) ... ( J - M ) ) ) $=
    ( wcel cz cfz co w3a cmin simp3 elfzel1 adantl elfzel2g jca 3adant2 elfzelz
    wa wb anim2i 3adant1 fzrev2 syl2anc mpbid ) EAFZBGFZCDEHIFZJZUHBCKIBEKIBDKI
    HIFZUFUGUHLUIDGFZEGFZSZUGCGFZSZUHUJTUFUHUMUGUFUHSUKULUHUKUFCDEMNACDEOPQUGUH
    UOUFUHUNUGCDERUAUBBCDEUCUDUE $.
    $( [25-Nov-2005] $)

  $( The "complement" of a member of a finite set of sequential integers. $)
  fzrev3 $p |- ( ( N e. A /\ K e. ZZ ) -> ( K e. ( M ... N )
                <-> ( ( M + N ) - K ) e. ( M ... N ) ) ) $=
    ( wcel cz wa cfz co cmin simplr elfzel1 adantl elfzel2g adantlr 3jca wb zcn
    cc 3adant1 caddc w3a wceq pncan pncan2 opreq12d syl2an eleq2d 3simpc zaddcl
    simp1 fzrev syl12anc bitr3d pm5.21nd ) DAEZBFEZGZBCDHIZEZCDUAIZBJIZUSEZUQCF
    EZDFEZUBZURUTGUQVDVEUPUQUTKUTVDURBCDLMUPUTVEUQABCDNOPURVCGUQVDVEUPUQVCKVCVD
    URVBCDLMUPVCVEUQAVBCDNOPVFBVADJIZVACJIZHIZEZUTVCVDVEVJUTQUQVDVEGZVIUSBVDCSE
    ZDSEZVIUSUCVECRDRVLVMGVGCVHDHCDUDCDUEUFUGUHTVFVKVAFEZUQVJVCQUQVDVEUIVDVEVNU
    QCDUJTUQVDVEUKVABCDULUMUNUO $.
    $( [20-Nov-2005] $)

  $( The "complement" of a member of a finite set of sequential integers. $)
  fzrev3i $p |- ( ( N e. A /\ K e. ( M ... N ) )
                -> ( ( M + N ) - K ) e. ( M ... N ) ) $=
    ( wcel cfz co wa caddc cmin simpr cz wb elfzelz fzrev3 sylan2 mpbid ) DAEZB
    CDFGZEZHTCDIGBJGSEZRTKTRBLETUAMBCDNABCDOPQ $.
    $( [20-Nov-2005] $)

  $( Finite set of sequential integers starting at 0. $)
  fznn0 $p |- ( N e. NN0
              -> ( K e. ( 0 ... N ) <-> ( K e. NN0 /\ K <_ N ) ) ) $=
    ( cn0 wcel cc0 cfz co cz cle wbr w3a wa wb nn0z elfz1 sylancr df-3an elnn0z
    0z anbi1i bitr4i syl6bb ) BCDZAEBFGDZAHDZEAIJZABIJZKZACDZUGLZUCEHDBHDUDUHMS
    BNAEBOPUHUEUFLZUGLUJUEUFUGQUIUKUGARTUAUB $.
    $( [1-Aug-2005] $)

  $( Finite set of sequential integers starting at 1. $)
  fznn $p |- ( N e. NN
              -> ( K e. ( 1 ... N ) <-> ( K e. NN /\ K <_ N ) ) ) $=
    ( cn wcel c1 cfz co cz cle wbr w3a wa wb 1z nnz elfz1 sylancr df-3an elnnz1
    anbi1i bitr4i syl6bb ) BCDZAEBFGDZAHDZEAIJZABIJZKZACDZUGLZUCEHDBHDUDUHMNBOA
    EBPQUHUEUFLZUGLUJUEUFUGRUIUKUGASTUAUB $.
    $( [31-Aug-2011] $)

  $( Membership in a finite set of sequential integers.  (Contributed by Paul
     Chapman, 21-Mar-2011.) $)
  elfzm11 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( K e. ( M ... ( N - 1 ) ) <->
                                         ( K e. ZZ /\ M <_ K /\ K < N ) ) ) $=
    ( cz wcel wa c1 cmin co cfz cle wbr w3a clt wb peano2zm elfz1 sylan2 3anass
    zltlem1 anbi2d expcom pm5.32d syl5bb syl6bbr adantl bitr4d ) BDEZCDEZFABCGH
    IZJIEZADEZBAKLZAUJKLZMZULUMACNLZMZUIUHUJDEUKUOOCPABUJQRUIUQUOOUHUIUQULUMUNF
    ZFZUOUQULUMUPFZFUIUSULUMUPSUIULUTURULUIUTUROULUIFUPUNUMACTUAUBUCUDULUMUNSUE
    UFUG $.
    $( [21-Mar-2011] $)

  $( Closure law for the modulo operation restricted to integers.  (Contributed
     by Mario Carneiro, 24-Feb-2014.) $)
  zmodcl2 $p |- ( ( A e. ZZ /\ B e. NN ) ->
                  ( A mod B ) e. ( 0 ... ( B - 1 ) ) ) $=
    ( cz wcel cn wa cmo co cc0 c1 cmin cfz cle wbr clt w3a cn0 zmodcl nn0z syl
    nn0ge0 cr crp zre nnrp modlt syl2an wb 0z nnz adantl elfzm11 sylancr mpbird
    3jca ) ACDZBEDZFZABGHZIBJKHLHDZUSCDZIUSMNZUSBONZPZURVAVBVCURUSQDZVAABRZUSST
    URVEVBVFUSUATUPAUBDBUCDVCUQAUDBUEABUFUGUOURICDBCDZUTVDUHUIUQVGUPBUJUKUSIBUL
    UMUN $.
    $( [24-Feb-2014] $)

  ${
    $d k M $.  $d k N $.
    $( Express an upper integer set as the disjoint (see ~ uzdisj ) union of
       the first ` N ` values and the rest.  (Contributed by Mario Carneiro,
       24-Apr-2014.) $)
    uzsplit $p |- ( N e. ( ZZ>= ` M ) -> ( ZZ>= ` M ) =
                  ( ( M ... ( N - 1 ) ) u. ( ZZ>= ` N ) ) ) $=
      ( vk cuz cfv wcel c1 cmin co cfz wo wa cle wbr cr eluzelre syl2an eluzelz
      cz wb cun clt lelttric eluz eluzel2 w3a elfzm11 df-3an syl6bb syl2anr jca
      cv eluzle adantl biantrurd bitr4d orbi12d mpbird orcomd elfzuz a1i expcom
      ex wi uztrn jaod impbid elun syl6bbr eqrdv ) BADEZFZCVKABGHIZJIZBDEZUAZVL
      CULZVKFZVQVNFZVQVOFZKZVQVPFVLVRWAVLVRWAVLVRLZVTVSWBVTVSKBVQMNZVQBUBNZKZVL
      BOFVQOFWEVRABPAVQPBVQUCQWBVTWCVSWDVLBSFZVQSFZVTWCTVRABRZAVQRZBVQUDQWBVSWG
      AVQMNZLZWDLZWDVRASFZWFVSWLTVLAVQUEWHWMWFLVSWGWJWDUFWLVQABUGWGWJWDUHUIUJWB
      WKWDVRWKVLVRWGWJWIAVQUMUKUNUOUPUQURUSVCVLVSVRVTVSVRVDVLVQAVMUTVAVTVLVRBVQ
      AVEVBVFVGVQVNVOVHVIVJ $.
      $( [24-Apr-2014] $)

    $( The first ` N ` elements of an upper integer set are distinct from any
       later members.  (Contributed by Mario Carneiro, 24-Apr-2014.) $)
    uzdisj $p |- ( ( M ... ( N - 1 ) ) i^i ( ZZ>= ` N ) ) = (/) $=
      ( vk c1 cmin co cfz cuz cfv c0 wcel wbr cle syl cz wb syl2anc mpbid zre
      cr cin wss wceq cv elin simprbi eluzle eluzel2 eluzelz zlem1lt wn simplbi
      clt elfzle2 peano2zm lenlt pm2.65i pm2.21i ssriv ss0 ax-mp ) ABDEFZGFZBHI
      ZUAZJUBVEJUCCVEJCUDZVEKZVFJKVGVBVFUMLZVGBVFMLZVHVGVFVDKZVIVGVFVCKZVJVFVCV
      DUEZUFZBVFUGNVGBOKZVFOKZVIVHPVGVJVNVMBVFUHNZVGVJVOVMBVFUINZBVFUJQRVGVFVBM
      LZVHUKZVGVKVRVGVKVJVLULVFAVBUNNVGVFTKZVBTKZVRVSPVGVOVTVQVFSNVGVBOKZWAVGVN
      WBVPBUONVBSNVFVBUPQRUQURUSVEUTVA $.
      $( [24-Apr-2014] $)
  $}

  ${
    fseq1p1m1.1 $e |- H = { <. ( N + 1 ) , B >. } $.
    $( Add/remove an item to/from the end of a finite sequence.  (Contributed
       by Paul Chapman, 17-Nov-2012.)  (Revised by Mario Carneiro,
       7-Mar-2014.) $)
    fseq1p1m1 $p |- ( N e. NN0 ->
        ( ( F : ( 1 ... N ) --> A /\ B e. A /\ G = ( F u. H ) ) <->
          ( G : ( 1 ... ( N + 1 ) ) --> A /\ ( G ` ( N + 1 ) ) = B /\
            F = ( G |` ( 1 ... N ) ) ) ) ) $=
      ( cn0 wcel c1 co wf cun wceq cfv cres csn c0 syl2anc syl cfz w3a caddc wa
      cin simpr1 wss cn nn0p1nn adantr simpr2 cop mpbiri snssi fss fzp1disj fun
      fsng 3impa syl3anc cz cmin cuz 1z simpl nn0uz ax-1cn subidi fveq2i eqtr4i
      cc0 syl6eleq fzsuc2 sylancr eqcomd unidm feq23d mpbid simpr3 feq1d mpbird
      a1i oprex snid fvres ax-mp reseq1 wfn ffn fnresdisj uneq1d resundir uncom
      un0 eqtr2i 3eqtr4g fnresdm 3syl 3eqtrd fveq1d fveq1i fvsng syl5eq syl5eqr
      wb eqtrd incom uneq2d eqcomi 3eqtrrd 3jca fzssp1 fssres feq1 nnuz eluzfz2
      ffvelrn eqeltrrd fnressn syl6eqr uneq12d reseq2d resundi syl6req impbida
      opeq2 sneqd ) FHIZJFUAKZACLZBAIZDCEMZNZUBZJFJUCKZUAKZADLZYODOZBNZCDYIPZNZ
      UBZYHYNUDZYQYSUUAUUCYQYPAYLLZUUCYIYOQZMZAAMZYLLZUUDUUCYJUUEAELZYIUUEUEZRN
      ZUUHYHYJYKYMUFZUUCUUEBQZELZUUMAUGZUUIUUCYOUHIZYKUUNYHUUPYNFUIZUJZYHYJYKYM
      UKZUUPYKUDZUUNEYOBULZQZNGYOBUHAEURUMSZUUCYKUUOUUSBAUNTUUEUUMAEUOSZYHUUKYN
      JFHUPUJZYJUUIUUKUUHYIUUEAACEUQUSUTUUCUUFUUGYPAYLUUCYPUUFUUCJVAIZFJJVBKZVC
      OZIZYPUUFNZVDUUCFHUVHYHYNVEHVKVCOUVHVFUVGVKVCJVGVHVIVJZVLJFVMZVNVOUUGANUU
      CAVPWBVQVRUUCYPADYLYHYJYKYMVSZVTWAUUCYRYODUUEPZOZBYOUUEIUVOYRNYOFJUCWCWDY
      OUUEDWEWFUUCUVOYOEOZBUUCYOUVNEUUCUVNYLUUEPZEUUEPZEUUCYMUVNUVQNUVMDYLUUEWG
      TUUCCUUEPZUVRMRUVRMZUVQUVRUUCUVSRUVRUUCUUKUVSRNZUVEUUCCYIWHZUUKUWAXEUUCYJ
      UWBUULYIACWIZTYIUUECWJTVRWKCEUUEWLUVTUVRRMUVRRUVRWMUVRWNWOWPUUCUUIEUUEWHZ
      UVRENUVDUUEAEWIUUEEWQWRWSWTUUCUUPYKUVPBNUURUUSUUTUVPYOUVBOBYOEUVBGXAYOBUH
      AXBXCSXFXDUUCYTYLYIPZCYIPZCUUCYMYTUWENUVMDYLYIWGTUUCUWFEYIPZMUWFRMZUWEUWF
      UUCUWGRUWFUUCUUEYIUEZRNZUWGRNZUUCUWIUUJRUUEYIXGUVEXCUUCUWDUWJUWKXEUUCUUNU
      WDUVCUUEUUMEWITUUEYIEWJTVRXHCEYIWLUWHUWFUWFWNXIWPUUCYJUWBUWFCNUULUWCYICWQ
      WRXJXKYHUUBUDZYJYKYMUWLYJYIAYTLZUWLYQYIYPUGZUWMYHYQYSUUAUFZYHUWNUUBJFHXLU
      JYPAYIDXMSUWLUUAYJUWMXEYHYQYSUUAVSZYIACYTXNTWAUWLYRBAYHYQYSUUAUKZUWLYQYOY
      PIZYRAIUWOUWLYOJVCOZIUWRUWLYOUHUWSYHUUPUUBUUQUJXOVLJYOXPTZYPAYODXQSXRUWLY
      LYTUVNMZDYPPZDUWLCYTEUVNUWPUWLUVNEUWLUVNUVBEUWLUVNYOYRULZQZUVBUWLDYPWHZUW
      RUVNUXDNUWLYQUXEUWOYPADWITZUWTYPYODXSSUWLYSUXDUVBNUWQYSUXCUVAYRBYOYFYGTXF
      GXTVOYAUWLUXBDUUFPUXAUWLYPUUFDUWLUVFUVIUVJVDUWLFHUVHYHUUBVEUVKVLUVLVNYBDY
      IUUEYCYDUWLUXEUXBDNUXFYPDWQTXJXKYE $.
      $( [17-Nov-2012] $)
  $}

  ${
    fseq1m1p1.1 $e |- H = { <. N , B >. } $.
    $( Add/remove an item to/from the end of a finite sequence.  (Contributed
       by Paul Chapman, 17-Nov-2012.) $)
    fseq1m1p1 $p |- ( N e. NN ->
        ( ( F : ( 1 ... ( N - 1 ) ) --> A /\ B e. A /\ G = ( F u. H ) ) <->
          ( G : ( 1 ... N ) --> A /\ ( G ` N ) = B /\
            F = ( G |` ( 1 ... ( N - 1 ) ) ) ) ) ) $=
      ( wcel c1 co cfz wf cop csn cun wceq w3a cfv syl cc cn cmin caddc cres wb
      cn0 nnm1nn0 eqid fseq1p1m1 ax-1cn npcan mpan2 opeq1d sneqd syl6eqr uneq2d
      nncn eqeq2d 3anbi3d opreq2d feq2d fveq2d eqeq1d 3anbi12d 3bitr3d ) FUAHZI
      FIUBJZKJZACLZBAHZDCVGIUCJZBMZNZOZPZQZIVKKJZADLZVKDRZBPZCDVHUDPZQZVIVJDCEO
      ZPZQIFKJZADLZFDRZBPZWAQVFVGUFHVPWBUEFUGABCDVMVGVMUHUISVFVOWDVIVJVFVNWCDVF
      VMECVFVMFBMZNEVFVLWIVFVKFBVFFTHZVKFPZFUQWJITHWKUJFIUKULSZUMUNGUOUPURUSVFV
      RWFVTWHWAVFVQWEADVFVKFIKWLUTVAVFVSWGBVFVKFDWLVBVCVDVE $.
      $( [17-Nov-2012] $)
  $}

  ${
    $d k N $.
    $( Quantification over a one-member finite set of sequential integers in
       terms of substitution. $)
    fz1sbc $p |- ( N e. ZZ -> ( A. k e. ( N ... N ) ph <-> [ N / k ] ph ) ) $=
      ( cz wcel wsbc cv wceq wi wal cfz co wral sbc6g df-ral elfz1eq syl5ibrcom
      elfz3 eleq1 impbid2 imbi1d albidv syl5rbb bitr2d ) CDEZABCFBGZCHZAIZBJZAB
      CCKLZMZABCDNUKUFUJEZAIZBJUEUIABUJOUEUMUHBUEULUGAUEULUGUFCPUEULUGCUJECRUFC
      UJSQTUAUBUCUD $.
      $( [28-Nov-2005] $)
  $}

  $( An integer is a member of a 1-based finite set of sequential integers iff
     its predecessor is a member of the corresponding 0-based set.
     (Contributed by Paul Chapman, 22-Jun-2011.) $)
  elfzm1b $p |- ( ( K e. ZZ /\ N e. ZZ ) ->
                ( K e. ( 1 ... N ) <-> ( K - 1 ) e. ( 0 ... ( N - 1 ) ) ) ) $=
    ( cz wcel c1 cfz co cmin cc0 wb fzsubel mpanl1 mpanr2 ax-1cn subidi opreq1i
    wa 1z eleq2i syl6bb ancoms ) BCDZACDZAEBFGDZAEHGZIBEHGZFGZDZJUBUCQUDUEEEHGZ
    UFFGZDZUHUBUCECDZUDUKJZRULUBUCULQUMRAEEBKLMUJUGUEUIIUFFENOPSTUA $.
    $( [22-Jun-2011] $)

  $( Options for membership in a finite interval of integers.  (Contributed by
     Jeff Madsen, 18-Jun-2010.) $)
  elfzp12 $p |- ( N e. ( ZZ>= ` M ) -> ( K e. ( M ... N ) <->
                                ( K = M \/ K e. ( ( M + 1 ) ... N ) ) ) ) $=
    ( cuz cfv wcel cfz co wceq c1 caddc wo cvv wa elex anim2i eluzel2 syl cun
    cz eleq1 syl5ibrcom imdistani jaodan csn eluzfz1 fzsplit mpdan uneq1d eqtrd
    fzsn eleq2d elun syl6bb elsncg orbi1d sylan9bb pm5.21nd ) CBDEZFZABCGHZFZAB
    IZABJKHCGHZFZLZUTAMFZNZVBVGUTAVAOPUTVCVHVEUTVCVGUTVGVCBMFZUTBTFZVIBCQZBTORA
    BMUAUBUCVEVGUTAVDOPUDUTVBABUEZFZVELZVGVFUTVBAVLVDSZFVNUTVAVOAUTVABBGHZVDSZV
    OUTBVAFVAVQIBCUFUSBBCUGUHUTVPVLVDUTVJVPVLIVKBUKRUIUJULAVLVDUMUNVGVMVCVEABMU
    OUPUQUR $.
    $( [18-Jun-2010] $)

  $( Choices for an element of a finite interval of integers.  (Contributed by
     Jeff Madsen, 2-Sep-2009.) $)
  fzm1 $p |- ( N e. ( ZZ>= ` M ) -> ( K e. ( M ... N ) <->
                              ( K e. ( M ... ( N - 1 ) ) \/ K = N ) ) ) $=
    ( wcel cfz co c1 cmin wceq wa wi cz cle wbr w3a wb elfz1 syl2anc a1i ex cuz
    cfv wo wn eluzel2 eluzelz simp1 simp2 clt wne cr zre eluzelre syl2an ancoms
    ltlen necom df-ne bitri anbi2i ancom syl6bb biimpar zltlem1 sylan2 ad2ant2r
    an4s biimpd mpd 3adantr2 3jcad 1z zsubcl sylancl biimprd syl6d com23 sylbid
    imp orcomd caddc cvv wss oprex fzssp1 ax-mp cc zcn syl ax-1cn npcan opreq2d
    orrd syl5sseq sseld eluzfz2 eleq1 syl5ibrcom jaod impbid ) CBUAUBDZABCEFZDZ
    ABCGHFZEFZDZACIZUCZXAXCXHXAXCJZXGXFXIXGXFXAXCXGUDZXFKZXAXCALDZBAMNZACMNZOZX
    KXABLDZCLDZXCXOPBCUEZBCUFZABCQRXAXJXOXFXAXJXOXLXMAXDMNZOZXFXAXJXOYAKXAXJJZX
    OXLXMXTXOXLKYBXLXMXNUGSXOXMKYBXLXMXNUHSYBXOXTYBXLXNXTXMYBXLXNJJACUINZXTXAXL
    XJXNYCXAXLJZYCXJXNJZYDYCXNCAUJZJZYEXLXAYCYGPZXLAUKDCUKDYHXAAULBCUMACUPUNUOY
    GXNXJJYEYFXJXNYFACUJXJCAUQACURUSUTXNXJVAUSVBVCVGXAXLYCXTKZXJXNXLXAYIXLXAJYC
    XTXAXLXQYCXTPXSACVDVEVHUOVFVIVJTVKTXAXFYAXAXPXDLDZXFYAPXRXAXQGLDYJXSVLCGVMV
    NABXDQRVOVPVQVRVSWMVTTXAXFXCXGXAXEXBAXABXDGWAFZEFZXEXBXDWBDXEYLWCCGHWDBXDWB
    WEWFXAYKCBEXACWGDZGWGDYKCIXAXQYMXSCWHWIWJCGWKVNWLWNWOXAXCXGCXBDBCWPACXBWQWR
    WSWT $.
    $( [2-Sep-2009] $)

  $( No finite set of sequential integers equals a set of upper integers. $)
  fzneuz $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. ZZ )
               -> -. ( M ... N ) = ( ZZ>= ` K ) ) $=
    ( cuz cfv wcel cz wa cfz co wceq wn c1 caddc peano2uz adantl wbr cr nelneq2
    ad2antrr cle eluzelz zre clt ltp1 wb peano2re ltnle mpdan 3syl elfzle2 nsyl
    mpbid syl2anc eqcom sylnib eluzfz2 sylancom pm2.61dan ) CBDEFZAGFZHZCADEZFZ
    BCIJZVCKZLZVBVDHZVCVEKZVFVHCMNJZVCFZVJVEFZLZVILVDVKVBACOPUTVMVAVDUTVJCUAQZV
    LUTCGFCRFZVNLZBCUBCUCVOCVJUDQZVPCUEVOVJRFVQVPUFCUGCVJUHUIUMUJVJBCUKULTVJVCV
    ESUNVCVEUOUPVBVDLZCVEFZVGUTVSVAVRBCUQTCVEVCSURUS $.
    $( [11-Dec-2005] $)

  ${
    $d M k n $.  $d N k n $.
    $( A convenience lemma for the sequential integer function showing
       existence of the second argument with no constraints to start with.
       (Contributed by Mario Carneiro, 13-Aug-2013.) $)
    fz2ex $p |- E. n e. ZZ ( M ... N ) = ( M ... n ) $=
      ( vk cz wcel cfz co cv wceq cvv wex wn opreq2 eqeq2d rcla4ev c0 sylbi cc0
      wa wrex wi oprprc2 sylan2 expcom n0 necon1bbii c1 peano2zm a1i clt wbr cr
      cmin zre ltm1 syl wb fzn mpdan mpbid biimprcd jcad syl6 elfzel2g mpan2 ex
      eqid exlimdv imp a1d ecase 0z wne elfzel1 exlimiv necon1bi eqtr4d sylancr
      pm2.61i ) BEFZBCGHZBAIZGHZJZAEUAZCKFZDIZWBFZDLZWAWFUBZWAWGMZWFWLWAWBBBGHZ
      JZWFBCGUCWEWNABEWCBJWDWMWBWCBBGNOPUDUEWJMWBQJZWKWJWBQDWBUFZUGWOWABUHUNHZE
      FZWBBWQGHZJZTWFWOWAWRWTWAWRUBWOBUIZUJWAWTWOWAWSQWBWAWQBUKULZWSQJZWABUMFXB
      BUOBUPUQWAWRXBXCURXABWQUSUTVAOVBVCWEWTAWQEWCWQJWDWSWBWCWQBGNOPVDRWGWJTWFW
      AWGWJWFWGWIWFDWGWIWFWGWITCEFZWFKWHBCVEXDWBWBJZWFWBVHWEXEACEWCCJWDWBWBWCCB
      GNOPVFUQVGVIVJVKVLWAMZSEFWBBSGHZJZWFVMXFWBQXGWAWBQWBQVNWJWAWPWIWADWHBCVOV
      PRVQWAXGQXGQVNWHXGFZDLWADXGUFXIWADWHBSVOVPRVQVRWEXHASEWCSJWDXGWBWCSBGNOPV
      SVT $.
      $( [13-Aug-2013] $)
  $}

  $( Disjointness of the upper integers and a finite sequence.  (Revised by
     Mario Carneiro, 30-Jun-2013.) $)
  fznuz $p |- ( ( N e. V /\ K e. ( M ... N ) ) ->
      -. K e. ( ZZ>= ` ( N + 1 ) ) ) $=
    ( wcel cfz co wa c1 caddc cuz cfv cle wbr elfzle2 adantl clt cz cr zre mt2d
    wn wi elfzel2g eluzp1l ex syl wb elfzelz ltnle syl2an syl2anc sylibd ) CDEZ
    ABCFGEZHZACIJGKLEZACMNZUOURUNABCOPUPUQCAQNZURUBZUPCREZUQUSUCDABCUDZVAUQUSCA
    UEUFUGUPVAAREZUSUTUHZVBUOVCUNABCUIPVACSEASEVDVCCTATCAUJUKULUMUA $.
    $( [24-Aug-2013] $)

  $( Disjointness of the upper integers and a finite sequence.  (Contributed by
     Mario Carneiro, 24-Aug-2013.) $)
  uznfz $p |- ( K e. ( ZZ>= ` N ) -> -. K e. ( M ... ( N - 1 ) ) ) $=
    ( cuz cfv wcel c1 cmin co cfz cle wbr eluzle clt wn cz eluzel2 wi cr zre wa
    elfzel1 w3a elfzm11 simp3 syl6bi impancom mpancom syl5com wb eluzelz syl2an
    ltnle syl2anc sylibd mt2d ) ACDEFZABCGHIZJIFZCAKLZCAMUQUSACNLZUTOZUQCPFZUSV
    ACAQZBPFZUSVCVARABURUBVEVCUSVAVEVCUAUSAPFZBAKLZVAUCVAABCUDVFVGVAUEUFUGUHUIU
    QVFVCVAVBUJZCAUKVDVFASFCSFVHVCATCTACUMULUNUOUP $.
    $( [24-Aug-2013] $)

  ${
    $d j k x K $.  $d j k x M $.  $d j k x N $.  $d k x ph $.
    $( Reversal of scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzrevral $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) -> ( A. j e. ( M ... N
       ) ph <-> A. k e. ( ( K - N ) ... ( K - M ) ) [ ( K - k ) / j ] ph ) ) $=
      ( cz wcel cfz co wral cv cmin wsbc wi wa wb elfzelz syl wceq simpr sylan2
      fzrev anassrs mpbid ra4sbc 3impa com23 ralrimdv ax-17 oprex hbsbc1v hbral
      w3a ex fzrev2i 3expa opreq2 dfsbcq rcla4v zcn nncan syl2an eqcomd sbceq1a
      cc adantll sylibrd ralrimd 3adant1 impbid ) EGHZFGHZDGHZUNZABEFIJZKZABDCL
      ZMJZNZCDFMJZDEMJZIJZKZVOVQVTCWCVOVRWCHZVQVTVLVMVNWEVQVTOZOVLVMPZVNPZWEWFW
      HWEPZVSVPHZWFWIWEWJWHWEUAWEWHVRGHZWEWJQZVRWAWBRWGVNWKWLDVREFUCUDUBUEABVSV
      PUFSUOUGUHUIVMVNWDVQOVLVMVNPZWDABVPWMBUJVTBCWCWEBUJABVSDVRMUKULUMWMBLZVPH
      ZWDAWMWOWDAOWMWOPZWDABDDWNMJZMJZNZAWPWQWCHZWDWSOVMVNWOWTGDWNEFUPUQVTWSCWQ
      WCVRWQTVSWRTVTWSQVRWQDMURABVSWRUSSUTSVNWOAWSQZVMVNWOPZWNWRTXAXBWRWNVNDVFH
      WNVFHZWRWNTWODVAWOWNGHXCWNEFRWNVASDWNVBVCVDABWRVESVGVHUOUHVIVJVK $.
      $( [25-Nov-2005] $)

    $( Reversal of scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzrevral2 $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) -> ( A. j e. ( ( K - N
       ) ... ( K - M ) ) ph <-> A. k e. ( M ... N ) [ ( K - k ) / j ] ph ) ) $=
      ( cz wcel cmin co cfz wral wb w3a zsubcl 3adant2 3adant3 cc wceq zcn wsbc
      cv simp1 fzrevral syl3anc nncan opreq12d syl3an raleqdv bitrd 3coml ) DGH
      ZEGHZFGHZABDFIJZDEIJZKJLZABDCUBIJUAZCEFKJZLZMULUMUNNZUQURCDUPIJZDUOIJZKJZ
      LZUTVAUOGHZUPGHZULUQVEMULUNVFUMDFOPULUMVGUNDEOQULUMUNUCABCDUOUPUDUEVAURCV
      DUSULDRHZUMERHZUNFRHZVDUSSDTETFTVHVIVJNVBEVCFKVHVIVBESVJDEUFQVHVJVCFSVIDF
      UFPUGUHUIUJUK $.
      $( [25-Nov-2005] $)

    $( Reversal of scanning order inside of a quantification over a finite set
       of sequential integers. $)
    fzrevral3 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( A. j e. ( M ... N ) ph
                   <-> A. k e. ( M ... N ) [ ( ( M + N ) - k ) / j ] ph ) ) $=
      ( cz wcel wa cfz co wral caddc cv cmin wsbc wb zaddcl fzrevral cc zcn
      mpd3an3 wceq pncan pncan2 opreq12d syl2an raleqdv bitrd ) DFGZEFGZHZABDEI
      JZKZABDELJZCMNJOZCUNENJZUNDNJZIJZKZUOCULKUIUJUNFGUMUSPDEQABCUNDERUAUKUOCU
      RULUIDSGZESGZURULUBUJDTETUTVAHUPDUQEIDEUCDEUDUEUFUGUH $.
      $( [11-Jul-2006] $) $( [20-Nov-2005] $)

    $( Shift the scanning order inside of a quantification over a finite set of
       sequential integers. $)
    fzshftral $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) -> ( A. j e. ( M ... N
       ) ph <-> A. k e. ( ( M + K ) ... ( N + K ) ) [ ( k - K ) / j ] ph ) ) $=
      ( vx cz wcel cfz co wral cc0 cmin wsbc caddc wb cvv cc wceq w3a cv mp3an3
      0z fzrevral 3adant3 zsubcl mpan id syl3an 3com12 wal oprex ax-gen sbcco3g
      opreq2 mp2an ralbii zcn cneg df-neg opreq2i subneg addcom syl5eqr 3adant2
      eqtrd opreq12d 3coml raleqdv elfzelz syl negsubdi2 syl2an dfsbcq ralbidva
      wa 3ad2ant3 bitrd syl5bb 3bitrd ) EHIZFHIZDHIZUAZABEFJKLZABMGUBZNKZOZGMFN
      KZMENKZJKLZWIGDCUBZNKZOZCDWKNKZDWJNKZJKZLZABWMDNKZOZCEDPKZFDPKZJKZLZWBWCW
      FWLQZWDWBWCMHIZXFUDABGMEFUEUCUFWCWBWDWLWSQZWCWJHIZWBWKHIZWDWDXHXGWCXIUDMF
      UGUHXGWBXJUDMEUGUHWDUIWIGCDWJWKUEUJUKWSABMWNNKZOZCWRLZWEXEWOXLCWRWNRIWHRI
      ZGULWOXLQDWMNUMXNGMWGNUMUNAGBWNWHXKRRWGWNMNUPUOUQURWEXMXLCXDLZXEWEXLCWRXD
      WBESIZWCFSIZWDDSIZWRXDTZEUSFUSDUSZXRXPXQXSXRXPXQUAWPXBWQXCJXRXPWPXBTXQXRX
      PVQZWPDEUTZNKZXBYBWKDNEVAVBYAYCDEPKXBDEVCDEVDVGVEUFXRXQWQXCTXPXRXQVQZWQDF
      UTZNKZXCYEWJDNFVAVBYDYFDFPKXCDFVCDFVDVGVEVFVHVIUJVJWDWBXOXEQWCWDXLXACXDWD
      WMXDIZVQXKWTTZXLXAQWDXRWMSIZYHYGXTYGWMHIYIWMXBXCVKWMUSVLXRYIVQXKWNUTWTWNV
      ADWMVMVEVNABXKWTVOVLVPVRVSVTWA $.
      $( [19-Jun-2007] $) $( [27-Nov-2005] $)
  $}

  ${
    $d j k m n x y F $.  $d j k m n x y M $.  $d j k n x y N $.
    $( The values of a finite real sequence have an upper bound. $)
    fsequb $p |- ( ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... N ) ( F ` k ) e. RR )
                 -> E. x e. RR A. k e. ( M ... N ) ( F ` k ) < x ) $=
      ( vn cv cr wcel cfz co wral clt wbr wrex wceq fveq2 wi raleqdv wa cfv cuz
      vj vm vy eleq1d cbvralv c1 caddc opreq2 rexbidv imbi12d cz elfz3 peano2re
      rcla4v syl elfz1eq ltp1 breq1d syl5ibrcom syl5 ralrimiv jca breq2 ralbidv
      a1i rcla4ev syl6 syld fzssp1 sseld imim1d ralimdv2 imp32 peano2uz eluzfz2
      rcla4va sylan adantrl cle cif wo wb elfzp1 ad3antrrr max2 ancoms ad2antlr
      fzelp1 an32s adantlr simplrl ifcl sylan2 ltletr syl3anc mpan2d ex a2d imp
      simpr adantl max1ALT ltletrd jaod sylbid jctild exp32 com34 imp3a cbvrexv
      rexlimdv syl6ib mp2and uzind4 sylan2b ) BGZCUAZHIZBDEJKZLEDUBUAZIZFGZCUAZ
      HIZFYALZXSAGZMNZBYALZAHOZXTYFBFYAXRYDPXSYEHXRYDCQUFUGYCYGYKYFFDUCGZJKZLZY
      IBYMLZAHOZRYFFDDJKZLZYIBYQLZAHOZRYFFDUDGZJKZLZYIBUUBLZAHOZRZYFFDUUAUHUIKZ
      JKZLZYIBUUHLZAHOZRYGYKRUCUDDEYLDPZYNYRYPYTUULYFFYMYQYLDDJUJZSUULYOYSAHUUL
      YIBYMYQUUMSUKULYLUUAPZYNUUCYPUUEUUNYFFYMUUBYLUUADJUJZSUUNYOUUDAHUUNYIBYMU
      UBUUOSUKULYLUUGPZYNUUIYPUUKUUPYFFYMUUHYLUUGDJUJZSUUPYOUUJAHUUPYIBYMUUHUUQ
      SUKULYLEPZYNYGYPYKUURYFFYMYAYLEDJUJZSUURYOYJAHUURYIBYMYAUUSSUKULDUMIZYRDC
      UAZHIZYTUUTDYQIYRUVBRDUNYFUVBFDYQYDDPYEUVAHYDDCQUFUPUQUUTUVBUVAUHUIKZHIZX
      SUVCMNZBYQLZTZYTUVBUVGRUUTUVBUVDUVFUVAUOUVBUVEBYQXRYQIXRDPZUVBUVEXRDURUVB
      UVEUVHUVAUVCMNUVAUSUVHXSUVAUVCMXRDCQUTVAVBVCVDVGYSUVFAUVCHYHUVCPYIUVEBYQY
      HUVCXSMVEVFVHVIVJUUAYBIZUUFUUIUUKUVIUUFUUITTUUEUUGCUAZHIZUUKUVIUUFUUIUUEU
      VIUUIUUCUUEUVIYFYFFUUHUUBUVIYDUUBIYDUUHIYFUVIUUBUUHYDDUUAYBVKVLVMVNVMVOUV
      IUUIUVKUUFUVIUUGUUHIZUUIUVKUVIUUGYBIUVLDUUAVPDUUGVQUQYFUVKFUUGUUHYDUUGPYE
      UVJHYDUUGCQUFVRVSVTUVIUUIUUEUVKTZUUKRUUFUVIUUITZUVMXSUEGZMNZBUUHLZUEHOZUU
      KUVNUUEUVKUVRUVNUUDUVKUVRRAHUVNYHHIZUVKUUDUVRUVNUVSUVKUUDUVRRUVNUVSUVKTZT
      ZUUDUVJUHUIKZYHWANZYHUWBWBZHIZXSUWDMNZBUUHLZTUVRUWAUUDUWGUWEUWAYIUWFBUUBU
      UHUWAXRUUBIZYIRZXRUUHIZUWFRUWAUWITZUWJUWHXRUUGPZWCZUWFUVIUWJUWMWDUUIUVTUW
      IXRDUUAWEWFUWKUWHUWFUWLUWAUWIUWHUWFRUWAUWHYIUWFUWAUWHYIUWFRUWAUWHTZYIYHUW
      DWANZUWFUVTUWOUVNUWHUVKUVSUWOUVKUWBHIZUVSUWOUVJUOZUWBYHWGVSWHWIUWNXTUVSUW
      EYIUWOTUWFRUVNUWHXTUVTUVIUWHUUIXTUVIUWHTUWJUUIXTYBXRDUUAWJYFXTFXRUUHYDXRP
      YEXSHYDXRCQUFVRVSWKWLUVNUVSUVKUWHWMUVTUWEUVNUWHUVKUVSUWPUWEUWQUWCYHUWBHWN
      WOZWIXSYHUWDWPWQWRWSWTXAUVTUWLUWFRUVNUWIUVTUWFUWLUVJUWDMNUVTUVJUWBUWDUVSU
      VKXBUVKUWPUVSUWQXCUWRUVKUVJUWBMNUVSUVJUSXCUVKUWBUWDWANZUVSUVKUWPUWSUWQUWB
      YHXDUQXCXEUWLXSUVJUWDMXRUUGCQUTVAWIXFXGWSVNUVTUWEUVNUWRXCXHUVQUWGUEUWDHUV
      OUWDPUVPUWFBUUHUVOUWDXSMVEVFVHVIXIXJXMXKUVQUUJUEAHUVOYHPUVPYIBUUHUVOYHXSM
      VEVFXLXNVTXOXIXPXAXQ $.
      $( [19-Sep-2005] $)

    $( The values of a finite real sequence have an upper bound. $)
    fsequb2 $p |- ( ( N e. ( ZZ>= ` M ) /\ F : ( M ... N ) --> RR ) ->
                  E. x e. RR A. y e. ran F y <_ x ) $=
      ( vk cuz cfv wcel cfz cr wa cv clt wbr wral wrex wi syl adantlr co wf cle
      crn ffvelrn ralrimiva fsequb sylan2 wceq wfn wb ffn fvelrnb biimpa biimpd
      breq1 reximi r19.36av adantll frn sseld simplr ltle syl2anc adantlll syld
      imp ralrimdva reximdva mpd ) EDGHIZDEJUAZKCUBZLZFMZCHZAMZNOZFVLPZAKQZBMZV
      QUCOZBCUDZPZAKQVMVKVPKIZFVLPVTVMWEFVLVLKVOCUEUFAFCDEUGUHVNVSWDAKVNVQKIZLZ
      VSWBBWCWGWAWCIZLVSWAVQNOZWBVNWHVSWIRZWFVMWHWJVKVMWHLZVRWIRZFVLQZWJWKVPWAU
      IZFVLQZWMVMWHWOVMCVLUJWHWOUKVLKCULFVLWACUMSUNWNWLFVLWNVRWIVPWAVQNUPUOUQSV
      RWIFVLURSUSTVMWFWHWIWBRZVKVMWFLWHLWAKIZWFWPVMWHWQWFVMWHWQVMWCKWAVLKCUTVAV
      GTVMWFWHVBWAVQVCVDVEVFVHVIVJ $.
      $( [15-Oct-2006] $) $( [20-Sep-2005] $)

    $( The values of a finite real sequence have a supremum. $)
    fseqsupcl $p |- ( ( N e. ( ZZ>= ` M ) /\ F : ( M ... N ) --> RR )
                 -> sup ( ran F , RR , < ) e. RR ) $=
      ( vy vx cuz cfv wcel cfz co cr wf wa crn wss c0 wne cv cle wceq wral wrex
      wbr clt csup frn adantl cdm eluzfz1 fdm ne0i dm0rn0 eqeq1 syl5bir necon3d
      biimpd syl5com imp syl2an fsequb2 suprcl syl3anc ) CBFGHZBCIJZKALZMANZKOZ
      VFPQZDRERSUCDVFUAEKUBVFKUDUEKHVEVGVCVDKAUFUGVCBVDHZAUHZVDTZVHVEBCUIVDKAUJ
      VIVKVHVIVDPQVKVHVDBUKVKVFPVDPVFPTVJPTZVKVDPTZAULVKVLVMVJVDPUMUPUNUOUQURUS
      EDABCUTEDVFVAVB $.
      $( [9-Jul-2007] $) $( [20-Sep-2005] $)

    fseqsupub.1 $e |- N e. _V $.
    $( The values of a finite real sequence are bounded by their supremum. $)
    fseqsupubi $p |- ( ( K e. ( M ... N ) /\ F : ( M ... N ) --> RR ) ->
               ( F ` K ) <_ sup ( ran F , RR , < ) ) $=
      ( vy vx cfz co wcel cr c0 wne cv cle wbr cfv wceq sylan2 cvv crn wss wral
      wf wa wrex clt csup frn adantl cdm fdm ne0i dm0rn0 biimpd syl5bir necon3d
      eqeq1 syl5com imp cuz elfzuz2 mpan fsequb2 wfn ffn fnfvelrn ancoms suprub
      sylan syl31anc ) BCDHIZJZVLKAUDZUEAUAZKUBZVOLMZFNGNOPFVOUCGKUFZBAQZVOJZVS
      VOKUGUHOPVNVPVMVLKAUIUJVNVMAUKZVLRZVQVLKAULVMWBVQVMVLLMWBVQVLBUMWBVOLVLLV
      OLRWALRZWBVLLRZAUNWBWCWDWAVLLURUOUPUQUSUTSVMDCVAQJZVNVRDTJVMWEETBCDVBVCGF
      ACDVDVJVNVMAVLVEZVTVLKAVFWFVMVTVLBAVGVHSGFVOVSVIVK $.
      $( [29-Jul-2007] $) $( [20-Sep-2005] $)
  $}

