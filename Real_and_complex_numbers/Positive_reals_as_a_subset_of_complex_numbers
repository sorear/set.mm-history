$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Positive reals (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of positive reals.  Definition of positive numbers in
     [Apostol] p. 20. $)
  df-rp $a |- RR+ = { x e. RR | 0 < x } $.

  ${
    $d x A $.
    $( Membership in the set of positive reals. $)
    elrp $p |- ( A e. RR+ <-> ( A e. RR /\ 0 < A ) ) $=
      ( vx cc0 cv clt wbr cr crp breq2 df-rp elrab2 ) CBDZEFCAEFBAGHLACEIBJK $.
      $( [27-Oct-2007] $)
  $}

  ${
    elrpi.1 $e |- A e. RR $.
    elrpi.2 $e |- 0 < A $.
    $( Membership in the set of positive reals. $)
    elrpii $p |- A e. RR+ $=
      ( crp wcel cr cc0 clt wbr elrp mpbir2an ) ADEAFEGAHIAJBCK $.
      $( [23-Feb-2008] $)
  $}

  $( 1 is a positive real.  (Contributed by Jeffrey Hankins, 23-Nov-2008.) $)
  1rp $p |- 1 e. RR+ $=
    ( c1 1re lt01 elrpii ) ABCD $.
    $( [23-Nov-2008] $)

  $( A positive real is a real. $)
  rpre $p |- ( A e. RR+ -> A e. RR ) $=
    ( vx crp cr cc0 cv clt wbr crab df-rp ssrab2 eqsstri sseli ) CDACEBFGHZBDID
    BJNBDKLM $.
    $( [27-Oct-2007] $)

  $( A positive real is a complex number. $)
  rpcn $p |- ( A e. RR+ -> A e. CC ) $=
    ( crp wcel rpre recnd ) ABCAADE $.
    $( [11-Nov-2008] $)

  $( A natural number is a positive real. $)
  nnrp $p |- ( A e. NN -> A e. RR+ ) $=
    ( crp wcel cr cc0 clt wbr cn elrp nnre nngt0 sylanbrc ) ABCADCEAFGAHCAIAJAK
    L $.
    $( [28-Nov-2008] $)

  $( The positive reals are a subset of the reals. $)
  rpssre $p |- RR+ C_ RR $=
    ( vx crp cr cv rpre ssriv ) ABCADEF $.
    $( [24-Feb-2008] $)

  $( A positive real is greater than zero.  (Contributed by FL,
     27-Dec-2007.) $)
  rpgt0 $p |- ( A e. RR+ -> 0 < A ) $=
    ( crp wcel cr cc0 clt wbr elrp simprbi ) ABCADCEAFGAHI $.
    $( [27-Dec-2007] $)

  $( A positive real is greater than or equal to zero. $)
  rpge0 $p |- ( A e. RR+ -> 0 <_ A ) $=
    ( crp wcel cr cc0 clt wbr cle rpre rpgt0 wi 0re ltle mpan sylc ) ABCADCZEAF
    GZEAHGZAIAJEDCPQRKLEAMNO $.
    $( [22-Feb-2008] $)

  $( A positive real is a positive real number. $)
  rpregt0 $p |- ( A e. RR+ -> ( A e. RR /\ 0 < A ) ) $=
    ( crp wcel cr cc0 clt wbr rpre rpgt0 jca ) ABCADCEAFGAHAIJ $.
    $( [11-Nov-2008] $)

  $( A positive real is nonzero. $)
  rpne0 $p |- ( A e. RR+ -> A =/= 0 ) $=
    ( crp wcel cr cc0 clt wbr wa wne rpregt0 gt0ne0 syl ) ABCADCEAFGHAEIAJAKL
    $.
    $( [18-Jul-2008] $)

  $( A positive real is a nonzero real number. $)
  rprene0 $p |- ( A e. RR+ -> ( A e. RR /\ A =/= 0 ) ) $=
    ( crp wcel cr cc0 wne rpre rpne0 jca ) ABCADCAEFAGAHI $.
    $( [11-Nov-2008] $)

  $( A positive real is a nonzero complex number. $)
  rpcnne0 $p |- ( A e. RR+ -> ( A e. CC /\ A =/= 0 ) ) $=
    ( crp wcel cc cc0 wne rpcn rpne0 jca ) ABCADCAEFAGAHI $.
    $( [11-Nov-2008] $)

  $( Quantification over positive reals. $)
  ralrp $p |- ( A. x e. RR+ ph <-> A. x e. RR ( 0 < x -> ph ) ) $=
    ( cc0 cv clt wbr wi crp cr wcel wa elrp imbi1i impexp bitri ralbii2 ) ACBDZ
    EFZAGZBHIQHJZAGQIJZRKZAGUASGTUBAQLMUARANOP $.
    $( [12-Feb-2008] $)

  $( Closure law for addition of positive reals.  Part of Axiom 7 of [Apostol]
     p. 20. $)
  rpaddcl $p |- ( ( A e. RR+ /\ B e. RR+ ) -> ( A + B ) e. RR+ ) $=
    ( caddc co crp wcel cr cc0 clt wbr wa elrp readdcl rpre syl2an addgt0 an4s
    syl2anb sylanbrc ) ABCDZEFTGFZHTIJZAEFZBEFZKTLAGFZBGFZUAUCUDABMANBNOUEHAIJZ
    KUFHBIJZKUBUCUDUEUFUGUHUBABPQALBLRS $.
    $( [27-Oct-2007] $)

  $( Closure law for multiplication of positive reals.  Part of Axiom 7 of
     [Apostol] p. 20. $)
  rpmulcl $p |- ( ( A e. RR+ /\ B e. RR+ ) -> ( A x. B ) e. RR+ ) $=
    ( cmul co crp wcel cr cc0 clt wbr wa elrp remulcl rpre syl2an axmulgt0 imp
    an4s syl2anb sylanbrc ) ABCDZEFUAGFZHUAIJZAEFZBEFZKUALAGFZBGFZUBUDUEABMANBN
    OUFHAIJZKUGHBIJZKUCUDUEUFUGUHUIUCUFUGKUHUIKUCABPQRALBLST $.
    $( [27-Oct-2007] $)

  $( Closure law for division of positive reals.  (Contributed by FL,
     27-Dec-2007.) $)
  rpdivcl $p |- ( ( A e. RR+ /\ B e. RR+ ) -> ( A / B ) e. RR+ ) $=
    ( cdiv co crp wcel cr cc0 clt wbr wa elrp wne redivcl 3expb rpre rprene0
    syl2an divgt0 syl2anb sylanbrc ) ABCDZEFUBGFZHUBIJZAEFZBEFZKUBLAGFZBGFZBHMZ
    KUCUEUFUGUHUIUCABNOAPBQRUGHAIJKUHHBIJKUDUEUFABSALBLTUA $.
    $( [27-Dec-2007] $)

  $( Closure law for reciprocation of positive reals.  (Contributed by Jeffrey
     Hankins, 23-Nov-2008.) $)
  rpreccl $p |- ( A e. RR+ -> ( 1 / A ) e. RR+ ) $=
    ( c1 crp wcel cdiv co 1rp rpdivcl mpan ) BCDACDBAEFCDGBAHI $.
    $( [23-Nov-2008] $)

  $( Closure law for division of a real by a positive real. $)
  rerpdivcl $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A / B ) e. RR ) $=
    ( cr wcel cc0 wne wa cdiv co crp redivcl 3expb rprene0 sylan2 ) ACDZBCDZBEF
    ZGABHICDZBJDOPQRABKLBMN $.
    $( [10-Nov-2008] $)

  $( Either a nonzero real or its negation is a positive real, but not both.
     Axiom 8 of [Apostol] p. 20. $)
  rpneg $p |- ( ( A e. RR /\ A =/= 0 ) -> ( A e. RR+ <-> -. -u A e. RR+ ) ) $=
    ( cr wcel cc0 wne wa clt wbr cneg wn crp wo cle wi 0re ltle mpan imp olcd
    renegcl pm2.24d adantr wb ltlen biimprd exp3a com23 jaod simpl jctild
    impbid2 lenlt lt0neg1 notbid bitrd orbi2d ianor syl6bbr elrp notbii
    3bitr4g ) ABCZADEZFZVBDAGHZFZAIZBCZDVGGHZFZJZAKCVGKCZJVDVFVHJZVIJZLZVKVDVFV
    MDAMHZLZVOVDVFVQVFVPVMVBVEVPDBCZVBVEVPNODAPQRSVDVQVEVBVDVMVEVPVBVMVENVCVBVH
    VEATUAUBVBVCVPVENVBVPVCVEVBVPVCVEVBVEVPVCFZVRVBVEVSUCODAUDQUEUFUGRUHVBVCUIU
    JUKVDVPVNVMVBVPVNUCVCVBVPADGHZJZVNVRVBVPWAUCODAULQVBVTVIAUMUNUOUBUPUOVHVIUQ
    URAUSVLVJVGUSUTVA $.
    $( [7-Nov-2008] $)

  $( Zero is not a positive real.  Axiom 9 of [Apostol] p. 20. $)
  0nrp $p |- -. 0 e. RR+ $=
    ( cc0 crp wcel clt wbr 0re ltnri cr elrp simprbi mto ) ABCZAADEZAFGLAHCMAIJ
    K $.
    $( [27-Oct-2007] $)


