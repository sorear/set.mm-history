$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Positive reals (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of positive reals.  Definition of positive numbers in
     [Apostol] p. 20. $)
  df-rp $a |- RR+ = { x e. RR | 0 < x } $.

  ${
    $d x A $.
    $( Membership in the set of positive reals. $)
    elrp $p |- ( A e. RR+ <-> ( A e. RR /\ 0 < A ) ) $=
      ( vx cc0 cv clt wbr cr crp breq2 df-rp elrab2 ) CBDZEFCAEFBAGHLACEIBJK $.
      $( [27-Oct-2007] $) $( [27-Oct-2007] $)
  $}

  ${
    elrpi.1 $e |- A e. RR $.
    elrpi.2 $e |- 0 < A $.
    $( Membership in the set of positive reals. $)
    elrpi $p |- A e. RR+ $=
      ( crp wcel cr cc0 clt wbr elrp mpbir2an ) ADEAFEGAHIAJBCK $.
      $( [24-Feb-2008] $) $( [23-Feb-2008] $)
  $}

  $( A positive real is a real. $)
  rpret $p |- ( A e. RR+ -> A e. RR ) $=
    ( vx crp cr cc0 cv clt wbr crab df-rp ssrab2 eqsstr sseli ) CDACEBFGHZBDIDB
    JNBDKLM $.
    $( [27-Oct-2007] $) $( [27-Oct-2007] $)

  $( The positive reals are a subset of the reals. $)
  rpssre $p |- RR+ (_ RR $=
    ( vx crp cr cv rpret ssriv ) ABCADEF $.
    $( [19-Mar-2008] $) $( [24-Feb-2008] $)

  $( A positive real is greater than zero.  (Contributed by FL,
     27-Dec-2007.) $)
  rpgt0t $p |- ( A e. RR+ -> 0 < A ) $=
    ( crp wcel cr cc0 clt wbr elrp pm3.27bi ) ABCADCEAFGAHI $.
    $( [22-Jan-2008] $) $( [27-Dec-2007] $)

  $( A positive real is greater than or equal to zero. $)
  rpge0t $p |- ( A e. RR+ -> 0 <_ A ) $=
    ( cr wcel cc0 clt wbr cle crp wi 0re ltlet mpan rpret rpgt0t sylc ) ABCZDAE
    FZDAGFZAHCDBCPQRIJDAKLAMANO $.
    $( [6-Mar-2008] $) $( [22-Feb-2008] $)

  $( Quantification over positive reals. $)
  ralrp $p |- ( A. x e. RR+ ph <-> A. x e. RR ( 0 < x -> ph ) ) $=
    ( cc0 cv clt wbr wi crp cr wcel wa elrp imbi1i impexp bitr ralbii2 ) ACBDZE
    FZAGZBHIQHJZAGQIJZRKZAGUASGTUBAQLMUARANOP $.
    $( [13-Feb-2008] $) $( [12-Feb-2008] $)

  $( Closure law for addition of positive reals.  Part of Axiom 7 of [Apostol]
     p. 20. $)
  rpaddclt $p |- ( ( A e. RR+ /\ B e. RR+ ) -> ( A + B ) e. RR+ ) $=
    ( crp wcel wa caddc co cr cc0 clt wbr axaddrcl rpret syl2an addgt0t an4s
    elrp syl2anb jca sylibr ) ACDZBCDZEZABFGZHDZIUDJKZEUDCDUCUEUFAHDZBHDZUEUAUB
    ABLAMBMNUGIAJKZEUHIBJKZEUFUAUBUGUHUIUJUFABOPAQBQRSUDQT $.
    $( [27-Oct-2007] $) $( [27-Oct-2007] $)

  $( Closure law for multiplication of positive reals.  Part of Axiom 7 of
     [Apostol] p. 20. $)
  rpmulclt $p |- ( ( A e. RR+ /\ B e. RR+ ) -> ( A x. B ) e. RR+ ) $=
    ( crp wcel wa cmul co cr cc0 clt wbr axmulrcl rpret syl2an axmulgt0 imp
    an4s elrp syl2anb jca sylibr ) ACDZBCDZEZABFGZHDZIUEJKZEUECDUDUFUGAHDZBHDZU
    FUBUCABLAMBMNUHIAJKZEUIIBJKZEUGUBUCUHUIUJUKUGUHUIEUJUKEUGABOPQARBRSTUERUA
    $.
    $( [27-Oct-2007] $) $( [27-Oct-2007] $)

  $( Closure law for multiplication of positive reals.  (Contributed by FL,
     27-Dec-2007.) $)
  rpdivclt $p |- ( ( A e. RR+ /\ B e. RR+ ) -> ( A / B ) e. RR+ ) $=
    ( crp wcel wa cdiv co cr cc0 clt wbr wne redivclt 3expb rpret gt0ne0t
    rpgt0t sylanc jca syl2an divgt0t elrp syl2anb sylibr ) ACDZBCDZEZABFGZHDZIU
    HJKZEUHCDUGUIUJAHDZBHDZBILZEUIUEUFUKULUMUIABMNAOUFULUMBOZULIBJKZUMUFBPUNBQR
    STUKIAJKEULUOEUJUEUFABUAAUBBUBUCSUHUBUD $.
    $( [22-Jan-2008] $) $( [27-Dec-2007] $)

  $( Either a nonzero real or its negation is a positive real, but not both.
     Axiom 8 of [Apostol] p. 20. $)
$(
  rpnegt $p |- ( A e. RR+ -> ( 0 < A <-> -. 0 < -u A ) ) $=
? $.
$)

  $( Zero is not a positive real.  Axiom 9 of [Apostol] p. 20. $)
  0nrp $p |- -. 0 e. RR+ $=
    ( cc0 crp wcel clt wbr 0re ltnr cr elrp pm3.27bi mto ) ABCZAADEZAFGLAHCMAIJ
    K $.
    $( [27-Oct-2007] $) $( [27-Oct-2007] $)


