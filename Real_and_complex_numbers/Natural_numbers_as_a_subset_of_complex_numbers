$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Natural numbers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The natural numbers of analysis start at one (unlike the ordinal natural
       numbers, i.e. the members of the set ` om ` , ~ df-om , which start at
       zero).  This is the convention used by most analysis books, and it is
       often convenient in proofs because we don't have to worry about
       division by zero.  See ~ nnind for the principle of mathematical
       induction.  See ~ dfnn2 for a slight variant.  See ~ df-n0 for the set
       of nonnegative integers ` NN0 ` starting at zero.  See ~ dfn2 for ` NN `
       defined in terms of ` NN0 ` . $)
    df-n $a |- NN = |^| { x | ( 1 e. x /\ A. y e. x ( y + 1 ) e. x ) } $.
  $}

  ${
    $d x y A $.
    peano5nn.1 $e |- A e. V $.
    $( Peano's inductive postulate.  Theorem I.36 (principle of mathematical
       induction) of [Apostol] p. 34. $)
    peano5nni $p |- ( ( 1 e. A /\ A. x e. A ( x + 1 ) e. A ) -> NN (_ A ) $=
      ( vy c1 wcel cv caddc co wral wa cab cint cn wss wceq eleq2 raleqd
      anbi12d elab intss1 sylbir df-n syl5ss ) EBFZAGEHIZBFZABJZKZEDGZFZUFUJFZA
      UJJZKZDLZMZBNUIBUOFUPBOUNUIDBCUJBPUKUEUMUHUJBEQULUGAUJBUJBUFQRSTBUOUAUBDA
      UCUD $.
      $( [5-Jul-2005] $) $( [10-Jan-1997] $)
  $}


  $( The natural numbers are a subset of the reals. $)
  nnssre $p |- NN (_ RR $=
    ( vx c1 cr wcel cv caddc co wral cn wss 1re peano2re rgen reex peano5nni
    mp2an ) BCDAEZBFGCDZACHICJKRACQLMACNOP $.
    $( [10-Jan-1997] $)

  $( The natural numbers are a subset of the complex numbers. $)
  nnsscn $p |- NN (_ CC $=
    ( cn cr cc nnssre axresscn sstri ) ABCDEF $.
    $( [17-Aug-2004] $) $( [2-Aug-2004] $)

  $( A natural number is a real number. $)
  nnre $p |- ( A e. NN -> A e. RR ) $=
    ( cn cr nnssre sseli ) BCADE $.
    $( [18-Aug-1999] $)

  $( A natural number is a complex number. $)
  nncn $p |- ( A e. NN -> A e. CC ) $=
    ( cn cc nnsscn sseli ) BCADE $.
    $( [18-Aug-1999] $)

  ${
    nnre.1 $e |- A e. NN $.
    $( A natural number is a real number. $)
    nnrei $p |- A e. RR $=
      ( cn wcel cr nnre ax-mp ) ACDAEDBAFG $.
      $( [18-Aug-1999] $)

    $( A natural number is a complex number. $)
    nncni $p |- A e. CC $=
      ( nnrei recni ) AABCD $.
      $( [18-Aug-1999] $)
  $}

  $( The set of natural numbers exists. $)
  nnex $p |- NN e. V $=
    ( cn cr reex nnssre ssexi ) ABCDE $.
    $( [3-Oct-1999] $)

  ${
    $d x y z A $.
    $( Peano postulate: 1 is a natural number. $)
    1nn $p |- 1 e. NN $=
      ( vx vy c1 cv wcel caddc co wral wa cab cint cn wi cc ax1cn elisseti
      elintab pm3.26 mpgbir df-n eleqtrri ) CCADZEZBDCFGUBEBUBHZIZAJKZLCUFEUEUC
      MAUEACCNOPQUCUDRSABTUA $.
      $( [11-Jan-1997] $)

    $( Peano postulate: a successor of a natural number is a natural number. $)
    peano2nn $p |- ( A e. NN -> ( A + 1 ) e. NN ) $=
      ( vz vx vy cv c1 caddc co cn wcel wceq opreq1 eleq1d wral wa cab cint wi
      wal rcla4cv adantl a2i 19.20i visset elintab oprex 3imtr4i df-n eleq2i
      vtoclga ) BEZFGHZIJZAFGHZIJBAIUKAKULUNIUKAFGLMUKFCEZJZDEZFGHZUOJZDUONZOZC
      PQZJZULVBJZUKIJUMVAUKUOJZRZCSVAULUOJZRZCSVCVDVFVHCVAVEVGUTVEVGRUPUSVGDUKU
      OUQUKKURULUOUQUKFGLMTUAUBUCVACUKBUDUEVACULUKFGUFUEUGIVBUKCDUHZUIIVBULVIUI
      UGUJ $.
      $( [11-Jan-1997] $)

    $( Alternate definition of the set of natural numbers.  Definition of
       positive integers in [Apostol] p. 22. $)
    dfnn2 $p |- NN =
               |^| { x | ( x (_ RR /\ 1 e. x /\ A. y e. x ( y + 1 ) e. x ) } $=
      ( vz cv cr wss c1 wcel caddc co wral wa cab cint w3a cn wceq eleq2
      raleqd anbi12d wb df-n eqeq2i sylbir nnssre eqsstr3i 1nn peano2nn rgen
      pm3.2i intabs 3anass abbii inteqi 3eqtr4ri ) ADZEFZGUPHZBDZGIJZUPHZBUPKZL
      ZLZAMZNVCAMNUQURVBOZAMZNPVCGCDZHZUTVHHZBVHKZLZGPHZUTPHZBPKZLZACEUPVHQURVI
      VBVKUPVHGRVAVJBUPVHUPVHUTRSTUPVLCMNZQUPPQZVCVPUAPVQUPCBUBZUCVRURVMVBVOUPP
      GRVAVNBUPPUPPUTRSTUDVQEFVPVQPEVSUEUFVMVOUGVNBPUSUHUIUJUJUKVGVEVFVDAUQURVB
      ULUMUNABUBUO $.
      $( [10-Jul-2005] $) $( [3-Jul-2005] $)
  $}

