$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Natural numbers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The natural numbers of analysis start at one (unlike the ordinal natural
       numbers, i.e. the members of the set ` om ` , ~ df-om , which start at
       zero).  This is the convention used by most analysis books, and it is
       often convenient in proofs because we don't have to worry about division
       by zero.  See ~ nnind for the principle of mathematical induction.  See
       ~ dfnn2 for a slight variant.  See ~ df-n0 for the set of nonnegative
       integers ` NN0 ` starting at zero.  See ~ dfn2 for ` NN ` defined in
       terms of ` NN0 ` . $)
    df-n $a |- NN = |^| { x | ( 1 e. x /\ A. y e. x ( y + 1 ) e. x ) } $.
  $}

  ${
    $d x y z $.
    $( The set of natural numbers exists.  (Revised by Mario Carneiro,
       16-Jun-2013.) $)
    nnex $p |- NN e. _V $=
      ( vx vy vz c1 cv wcel caddc co wral wa cvv com c0 cfv wceq ax-mp fnfvelrn
      cc mp2an eleq2 cn cab cint df-n wex cmpt crdg cres crn ax-1cn fr0g frfnom
      wfn peano1 eqeltrri wrex wb fvelrnb csuc frsuc fveq2 sylan9eq visset eqid
      opreq1 oprex fvmpt syl6eq peano2 sylancr adantr rexlimiva sylbi rgen omex
      eqeltrrd fnex rnex raleqbi1dv anbi12d cla4ev intexab mpbi eqeltri ) UADAE
      ZFZBEZDGHZWEFZBWEIZJZAUBUCZKABUDWKAUEZWLKFDCKCEZDGHZUFZDUGLUHZUIZFZWHWRFZ
      BWRIZWMMWQNZDWRDRFXBDOUJDRWPUKPWQLUMZMLFXBWRFDWPULZUNLMWQQSUOWTBWRWGWRFZW
      EWQNZWGOZALUPZWTXCXEXHUQXDALWGWQURPXGWTALWELFZXGJZWEUSZWQNZWHWRXJXLWGWPNZ
      WHXIXGXLXFWPNXMDWEWPUTXFWGWPVAVBWGKFXMWHOBVCCWGWOWHKWPWNWGDGVEWPVDWGDGVFV
      GPVHXIXLWRFZXGXIXCXKLFXNXDWEVILXKWQQVJVKVPVLVMVNWKWSXAJAWRWQXCLKFWQKFXDVO
      LKWQVQSVRWEWROWFWSWJXAWEWRDTWIWTBWEWRWEWRWHTVSVTWASWKAWBWCWD $.
      $( [16-Jun-2013] $) $( [3-Oct-1999] $)
  $}

  ${
    $d x y z A $.
    $( Peano's inductive postulate.  Theorem I.36 (principle of mathematical
       induction) of [Apostol] p. 34.  (Revised by Mario Carneiro,
       16-Jun-2013.) $)
    peano5nni $p |- ( ( 1 e. A /\ A. x e. A ( x + 1 ) e. A ) -> NN C_ A ) $=
      ( vz vy c1 wcel cv wral wa cn cab cint df-n wss wi cvv elin ax-mp anim12i
      ssralv caddc co wex eqeltrri intexab mpbir cin biimpri inss1 inss2 ralbii
      nnex r19.26 bitr2i sylib an4s visset inex1 wceq raleqbi1dv anbi12d sylibr
      eleq2 elab intss1 sstr mpan2 3syl ex exlimiv syl5eqss ) EBFZAGEUAUBZBFZAB
      HZIZJECGZFZVMVQFZAVQHZIZCKZLZBCAMEDGZFZVMWDFZAWDHZIZDUCZVPWCBNZOZWIWHDKLZ
      PFJWLPDAMULUDWHDUEUFWHWKDWHVPWJWHVPIZWDBUGZWBFZWCWNNZWJWMEWNFZVMWNFZAWNHZ
      IZWOWEVLWGVOWTWEVLIZWQWGVOIZWSWQXAEWDBQUHXBWFAWNHZVNAWNHZIZWSWGXCVOXDWNWD
      NWGXCOWDBUIWFAWNWDTRWNBNZVOXDOWDBUJZVNAWNBTRSWSWFVNIZAWNHXEWRXHAWNVMWDBQU
      KWFVNAWNUMUNUOSUPWAWTCWNWDBDUQURVQWNUSVRWQVTWSVQWNEVCVSWRAVQWNVQWNVMVCUTV
      AVDVBWNWBVEWPXFWJXGWCWNBVFVGVHVIVJRVK $.
      $( [16-Jun-2013] $) $( [10-Jan-1997] $)
  $}

  $( The natural numbers are a subset of the reals. $)
  nnssre $p |- NN C_ RR $=
    ( vx c1 cr wcel cv caddc co wral cn wss 1re peano2re rgen peano5nni mp2an )
    BCDAEZBFGCDZACHICJKQACPLMACNO $.
    $( [16-Jun-2013] $) $( [10-Jan-1997] $)

  $( The natural numbers are a subset of the complex numbers. $)
  nnsscn $p |- NN C_ CC $=
    ( cn cr cc nnssre ax-resscn sstri ) ABCDEF $.
    $( [2-Aug-2004] $)

  $( A natural number is a real number. $)
  nnre $p |- ( A e. NN -> A e. RR ) $=
    ( cn cr nnssre sseli ) BCADE $.
    $( [18-Aug-1999] $)

  $( A natural number is a complex number. $)
  nncn $p |- ( A e. NN -> A e. CC ) $=
    ( cn cc nnsscn sseli ) BCADE $.
    $( [18-Aug-1999] $)

  ${
    nnre.1 $e |- A e. NN $.
    $( A natural number is a real number. $)
    nnrei $p |- A e. RR $=
      ( cn wcel cr nnre ax-mp ) ACDAEDBAFG $.
      $( [18-Aug-1999] $)

    $( A natural number is a complex number. $)
    nncni $p |- A e. CC $=
      ( nnrei recni ) AABCD $.
      $( [18-Aug-1999] $)
  $}

  ${
    $d x y z A $.
    $( Peano postulate: 1 is a natural number. $)
    1nn $p |- 1 e. NN $=
      ( vx vy c1 cv wcel caddc co wral wa cint cn wi cc ax-1cn elisseti elintab
      cab simpl mpgbir df-n eleqtrri ) CCADZEZBDCFGUBEBUBHZIZAQJZKCUFEUEUCLAUEA
      CCMNOPUCUDRSABTUA $.
      $( [11-Jan-1997] $)

    $( Peano postulate: a successor of a natural number is a natural number. $)
    peano2nn $p |- ( A e. NN -> ( A + 1 ) e. NN ) $=
      ( vz vx vy cv c1 caddc co cn wcel wceq opreq1 eleq1d wral elintab 3imtr4i
      wa wi wal eleq2i cab cint rcla4cv adantl alimi visset oprex df-n vtoclga
      a2i ) BEZFGHZIJZAFGHZIJBAIUKAKULUNIUKAFGLMUKFCEZJZDEZFGHZUOJZDUONZQZCUAUB
      ZJZULVBJZUKIJUMVAUKUOJZRZCSVAULUOJZRZCSVCVDVFVHCVAVEVGUTVEVGRUPUSVGDUKUOU
      QUKKURULUOUQUKFGLMUCUDUJUEVACUKBUFOVACULUKFGUGOPIVBUKCDUHZTIVBULVITPUI $.
      $( [11-Jan-1997] $)

    $( Alternate definition of the set of natural numbers.  Definition of
       positive integers in [Apostol] p. 22. $)
    dfnn2 $p |- NN =
               |^| { x | ( x C_ RR /\ 1 e. x /\ A. y e. x ( y + 1 ) e. x ) } $=
      ( vz cv cr wss c1 wcel caddc wral wa cab cint cn eleq2 raleqbi1dv anbi12d
      wceq df-n pm3.2i co w3a eqeq2i sylbir nnssre eqsstr3i 1nn peano2nn intabs
      wb rgen 3anass abbii inteqi 3eqtr4ri ) ADZEFZGUPHZBDZGIUAZUPHZBUPJZKZKZAL
      ZMVCALMUQURVBUBZALZMNVCGCDZHZUTVHHZBVHJZKZGNHZUTNHZBNJZKZACEUPVHRURVIVBVK
      UPVHGOVAVJBUPVHUPVHUTOPQUPVLCLMZRUPNRZVCVPUJNVQUPCBSZUCVRURVMVBVOUPNGOVAV
      NBUPNUPNUTOPQUDVQEFVPVQNEVSUEUFVMVOUGVNBNUSUHUKTTUIVGVEVFVDAUQURVBULUMUNA
      BSUO $.
      $( [3-Jul-2005] $)
  $}

