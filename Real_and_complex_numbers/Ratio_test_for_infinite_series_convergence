$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ratio test for infinite series convergence
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y z $.  $d x y z A $.  $d x y z B $.  $d y C $.  $d x y z F $.
    cvgratlem1ALT.1 $e |- F : NN --> RR $.
    cvgratlem1ALT.2 $e |- A e. RR $.
    cvgratlem1ALT.3 $e |- B e. NN $.
    cvgratlem1ALT.4 $e |- C e. NN $.
    $( Lemma for ~ cvgrati .  Establish, by induction, an exponential upper
       bound for the terms of a real series, given that the ratio of successive
       terms is less than some positive constant ` A ` beyond a starting index
       ` B ` . $)
    cvgratlem1ALT $p |- ( ( 0 < A /\
       A. x e. NN ( B <_ x -> ( F ` ( x + 1 ) ) < ( A x. ( F ` x ) ) ) ) ->
                     ( F ` ( B + C ) ) <_ ( ( A ^ C ) x. ( F ` B ) ) ) $=
      ( vy vz cc0 clt wbr cv cle c1 caddc co cfv cmul wi cn wral wa cexp wcel
      wceq opreq2 fveq2d opreq1d breq12d imbi2d nnrei leidi breq2 opreq1 fveq2
      opreq2d imbi12d rcla4v ax-mp mpi cc recni exp1 opreq1i syl6breqr adantl
      cr cn0 nnnn0 nn0addge1 sylancr nnaddcl mpan syl mpid ffvelrni reexpcl
      remulcl sylancl a1i w3a ltmul2OLD biimpd ex syl111anc imp3a peano2nn
      axlttrn syl2and exp4d com23 imp32 wb nncn nncni ax1cn addass mp3an13
      expp1 expcl mulcom eqtrd mulass eqtr2d adantr sylibd a2d nnind mp2an
      sylan2 remulcli ltlei ) LBMNZCAOZPNZXQQRSZETZBXQETZUASZMNZUBZAUCUDZUEZCDR
      SZETZBDUFSZCETZUASZMNZYHYKPNDUCUGZYFYLUBZIYFCJOZRSZETZBYOUFSZYJUASZMNZUBY
      FCQRSZETZBQUFSZYJUASZMNZUBYFCKOZRSZETZBUUFUFSZYJUASZMNZUBYFCUUFQRSZRSZETZ
      BUULUFSZYJUASZMNZUBYNJKDYOQUHZYTUUEYFUURYQUUBYSUUDMUURYPUUAEYOQCRUIUJUURY
      RUUCYJUAYOQBUFUIUKULUMYOUUFUHZYTUUKYFUUSYQUUHYSUUJMUUSYPUUGEYOUUFCRUIUJUU
      SYRUUIYJUAYOUUFBUFUIUKULUMYOUULUHZYTUUQYFUUTYQUUNYSUUPMUUTYPUUMEYOUULCRUI
      UJUUTYRUUOYJUAYOUULBUFUIUKULUMYODUHZYTYLYFUVAYQYHYSYKMUVAYPYGEYODCRUIUJUV
      AYRYIYJUAYODBUFUIUKULUMYEUUEXPYEUUBBYJUASZUUDMYECCPNZUUBUVBMNZCCHUNZUOCUC
      UGZYEUVCUVDUBZUBHYDUVGACUCXQCUHZXRUVCYCUVDXQCCPUPUVHXTUUBYBUVBMUVHXSUUAEX
      QCQRUQUJUVHYAYJBUAXQCEURUSULUTVAVBVCUUCBYJUABVDUGZUUCBUHBGVEZBVFVBVGVHVIU
      UFUCUGZYFUUKUUQUVKYFUUKUUQUBUVKYFUEUUKUUGQRSZETZBUUJUASZMNZUUQUVKXPYEUUKU
      VOUBZUVKYEXPUVPUVKYEXPUUKUVOUVKYEUVMBUUHUASZMNZXPUUKUEUVQUVNMNZUVOUVKYECU
      UGPNZUVRUVKCVJUGUUFVKUGZUVTUVEUUFVLZCUUFVMVNUVKUUGUCUGZYEUVTUVRUBZUBUVFUV
      KUWCHCUUFVOVPZYDUWDAUUGUCXQUUGUHZXRUVTYCUVRXQUUGCPUPUWFXTUVMYBUVQMUWFXSUV
      LEXQUUGQRUQUJUWFYAUUHBUAXQUUGEURUSULUTVAVQVRUVKXPUUKUVSUVKUUHVJUGZUUJVJUG
      ZBVJUGZXPUUKUVSUBZUBUVKUWCUWGUWEUCVJUUGEFVSVQZUVKUUIVJUGZYJVJUGZUWHUVKUWI
      UWAUWLGUWBBUUFVTVNUVFUWMHUCVJCEFVSVBZUUIYJWAWBZUWIUVKGWCUWGUWHUWIWDZXPUWJ
      UWPXPUEUUKUVSUUHUUJBWEWFWGWHWIUVKUVMVJUGZUVQVJUGZUVNVJUGZUVRUVSUEUVOUBUVK
      UVLUCUGZUWQUVKUWCUWTUWEUUGWJVQUCVJUVLEFVSVQUVKUWIUWGUWRGUWKBUUHWAVNUVKUWI
      UWHUWSGUWOBUUJWAVNUVMUVQUVNWKWHWLWMWNWOUVKUVOUUQWPYFUVKUVMUUNUVNUUPMUVKUV
      LUUMEUVKUUFVDUGZUVLUUMUHZUUFWQCVDUGUXAQVDUGUXBCHWRWSCUUFQWTXAVQUJUVKUUPBU
      UIUASZYJUASZUVNUVKUUOUXCYJUAUVKUUOUUIBUASZUXCUVKUVIUWAUUOUXEUHUVJUWBBUUFX
      BVNUVKUUIVDUGZUVIUXEUXCUHUVKUVIUWAUXFUVJUWBBUUFXCVNZUVJUUIBXDWBXEUKUVKUXF
      UXDUVNUHZUXGUVIUXFYJVDUGUXHUVJYJUWNVEBUUIYJXFXAVQXGULXHXIWGXJXKVBYHYKYGUC
      UGZYHVJUGUVFYMUXIHICDVOXLUCVJYGEFVSVBYIYJUWIYMYIVJUGZGIYMUWIDVKUGUXJDVLBD
      VTXMXLUWNXNXOVQ $.
      $( [28-May-2005] $)

    $( Lemma for ~ cvgrati .  Using ~ expsub , restate ~ cvgratlem1ALT with an
       absolute index ` C ` instead of just an offset from the starting index
       ` B ` . $)
    cvgratlem2ALT $p |- ( ( 0 < A /\
         A. x e. NN ( B <_ x -> ( F ` ( x + 1 ) ) < ( A x. ( F ` x ) ) ) ) ->
                     ( B < C ->
                 ( F ` C ) <_ ( ( ( F ` B ) / ( A ^ B ) ) x. ( A ^ C ) ) ) ) $=
      ( cc0 clt wbr cv cle c1 caddc co cfv cmul wi cn wral wa cexp cdiv cmin
      wcel nnsubi cif wceq opreq2 fveq2d opreq1d breq12d imbi2d 1nn elimel
      cvgratlem1ALT dedth nncni pncan3i fveq2i breq1i syl6ib sylbi impcom wne
      gt0ne0i cc w3a recni 3pm3.2i cn0 simpr jctil nnnn0 3ad2ant2 adantr
      3ad2ant3 3jca adantrr cr nnre ltle syl2an ancoms imp 3adantl1 adantrl
      expsub syl11anc mpan sylan reexpcl sylan2 mp2an expgt0 syl3an2 mp3an12
      syl ffvelrni ax-mp expcl div13 mp3an13 sylancr eqtr4d adantlr breqtrd ex
      ) JBKLZCAMZNLXLOPQERBXLERSQKLTAUAUBZUCZCDKLZDERZCERZBCUDQZUEQBDUDQZSQZNLX
      NXOUCXPBDCUFQZUDQZXQSQZXTNXOXNXPYCNLZXOYAUAUGZXNYDTCDHIUHYEXNCYAPQZERZYCN
      LZYDYEXNYHTXNCYEYAOUIZPQZERZBYIUDQZXQSQZNLZTYAOYAYIUJZYHYNXNYOYGYKYCYMNYO
      YFYJEYAYICPUKULYOYBYLXQSYAYIBUDUKUMUNUOABCYIEFGHYAOUAUPUQURUSYGXPYCNYFDEC
      DCHUTDIUTVAVBVCVDVEVFXKXOYCXTUJXMXKXOUCZYCXSXRUEQZXQSQZXTYPYBYQXQSXKBJVGZ
      XOYBYQUJZBGVHBVIUGZDUAUGZCUAUGZVJZYSXOUCZYTUUAUUBUUCBGVKZIHVLUUDUUEUCUUAY
      SUCZDVMUGZCVMUGZVJZCDNLZYTUUDYSUUJXOUUDYSUCZUUGUUHUUIUULYSUUAUUDYSVNUUFVO
      UUDUUHYSUUBUUAUUHUUCDVPZVQVRUUDUUIYSUUCUUAUUIUUBCVPZVSVRVTWAUUDXOUUKYSUUB
      UUCXOUUKUUAUUBUUCUCXOUUKUUCUUBXOUUKTZUUCCWBUGDWBUGUUOUUBCWCDWCCDWDWEWFWGW
      HWIBDCWJWKWLWMUMXKXTYRUJZXOXKXRVIUGZXRJVGZUUPXRBWBUGZUUCXRWBUGZGHUUCUUSUU
      IUUTUUNBCWNWOWPZVKXKJXRKLZUURUUSUUCXKUVBGHUUCUUSUUIXKUVBUUNBCWQWRWSXRUVAV
      HWTXQVIUGUUQUURUCXSVIUGZUUPXQUUCXQWBUGHUAWBCEFXAXBVKUUAUUBUVCUUFIUUBUUAUU
      HUVCUUMBDXCWOWPXQXRXSXDXEXFVRXGXHXIXJ $.
      $( [28-May-2005] $)
  $}

  ${
    $d x A $.  $d x y z B $.  $d x y z F $.  $d y z C $.
    cvgratlem3ALT.1 $e |- F : NN --> CC $.
    $d G x $.
    cvgratlem3ALT.2 $e |- G =
                    { <. y , z >. | ( y e. NN /\ z = ( abs ` ( F ` y ) ) ) } $.
    cvgratlem3ALT.3 $e |- A e. RR $.
    cvgratlem3ALT.4 $e |- B e. NN $.
    cvgratlem3ALT.5 $e |- C e. NN $.
    $( Lemma for ~ cvgrati .  Restate ~ cvgratlem2ALT (which was for a real
       function) in terms of the absolute values of the terms of a complex
       function ` F ` , with the help of an auxiliary function ` G ` . $)
    cvgratlem3ALT $p |- ( ( 0 < A /\ A. x e. NN ( B <_ x ->
  ( abs ` ( F ` ( x + 1 ) ) ) < ( A x. ( abs ` ( F ` x ) ) ) ) ) ->
  ( B < C -> ( abs ` ( F ` C ) ) <_ ( ( ( abs ` ( F ` B ) )
                       / ( A ^ B ) ) x. ( A ^ C ) ) ) ) $=
      ( cv cle wbr c1 caddc co cfv cabs cmul clt wi cn wral cc0 cexp cdiv wcel
      wceq peano2nn fveq2 fveq2d fvex fvopab4 syl opreq2d breq12d imbi2d
      ralbiia wa cr cc ffvelrni abscl fopab cvgratlem2ALT imp ax-mp opreq1i
      3brtr3g ex sylan2br ) EANZOPZVOQRSZGTZUATZDVOGTZUATZUBSZUCPZUDZAUEUFUGDUC
      PZVPVQHTZDVOHTZUBSZUCPZUDZAUEUFZEFUCPZFGTZUATZEGTZUATZDEUHSZUISZDFUHSZUBS
      ZOPZUDWJWDAUEVOUEUJZWIWCVPXBWFVSWHWBUCXBVQUEUJWFVSUKVOULBCVQBNZGTZUATZVSU
      EHXCVQUKXDVRUAXCVQGUMUNJVRUAUOUPUQXBWGWADUBBCVOXEWAUEHXCVOUKXDVTUAXCVOGUM
      UNJVTUAUOUPURUSUTVAWEWKVBZWLXAXFWLVBFHTZEHTZWQUISZWSUBSZWNWTOXFWLXGXJOPAD
      EFHBCUEVCXEHJXCUEUJXDVDUJXEVCUJUEVDXCGIVEXDVFUQVGKLMVHVIFUEUJXGWNUKMBCFXE
      WNUEHXCFUKXDWMUAXCFGUMUNJWMUAUOUPVJXIWRWSUBXHWPWQUIEUEUJXHWPUKLBCEXEWPUEH
      XCEUKXDWOUAXCEGUMUNJWOUAUOUPVJVKVKVLVMVN $.
      $( [28-May-2005] $)
  $}

  ${
    $d x y z $.  $d x y z A $.  $d x y z B $.  $d y C $.  $d x y z F $.
    cvgratlem1.1 $e |- F : NN --> RR $.
    $( Lemma for ~ cvgrati .  Establish, by induction, an exponential upper
       bound for the terms of a real series, given that the ratio of successive
       terms is less than some positive constant ` A ` beyond a starting index
       ` B ` . $)
    cvgratlem1 $p |- ( ( C e. NN /\ ( ( A e. RR /\ 0 < A ) /\ ( B e. NN /\
       A. x e. NN ( B <_ x -> ( F ` ( x + 1 ) ) < ( A x. ( F ` x ) ) ) ) ) ) ->
                     ( F ` ( B + C ) ) <_ ( ( A ^ C ) x. ( F ` B ) ) ) $=
      ( vy vz cn wcel cr cc0 clt wbr wa cv cle c1 caddc co cfv cmul wi wral
      cexp wceq opreq2 fveq2d opreq1d breq12d imbi2d nnre leid syl breq2
      opreq1 fveq2 opreq2d imbi12d rcla4v mpid imp adantl cc recn exp1 adantr
      breqtrrd cn0 nnnn0 nn0addge1 syl2an nnaddcl ancoms adantll ffvelrni
      reexpcl sylan2 remulcl simpll w3a ltmul2OLD biimpd ex syl111anc imp3a
      peano2nn anassrs syl11anc axlttrn syl2and exp4d exp31 imp4a com12 com34
      imp44 wb nncn ax1cn addass mp3an3 expp1 expcl simpl mulcom eqtrd adantlr
      jca recnd mulass 3expa an32s eqtr2d sylanl1 adantrlr adantrrr sylibd a2d
      nnind adantrl anasss ltle mpd ) DIJZBKJZLBMNZOZCIJZCAPZQNZYJRSTZEUAZBYJEU
      AZUBTZMNZUCZAIUDZOZOZOCDSTZEUAZBDUETZCEUAZUBTZMNZUUBUUEQNZYEYTUUFYTCGPZST
      ZEUAZBUUHUETZUUDUBTZMNZUCYTCRSTZEUAZBRUETZUUDUBTZMNZUCYTCHPZSTZEUAZBUUSUE
      TZUUDUBTZMNZUCYTCUUSRSTZSTZEUAZBUVEUETZUUDUBTZMNZUCYTUUFUCGHDUUHRUFZUUMUU
      RYTUVKUUJUUOUULUUQMUVKUUIUUNEUUHRCSUGUHUVKUUKUUPUUDUBUUHRBUEUGUIUJUKUUHUU
      SUFZUUMUVDYTUVLUUJUVAUULUVCMUVLUUIUUTEUUHUUSCSUGUHUVLUUKUVBUUDUBUUHUUSBUE
      UGUIUJUKUUHUVEUFZUUMUVJYTUVMUUJUVGUULUVIMUVMUUIUVFEUUHUVECSUGUHUVMUUKUVHU
      UDUBUUHUVEBUEUGUIUJUKUUHDUFZUUMUUFYTUVNUUJUUBUULUUEMUVNUUIUUAEUUHDCSUGUHU
      VNUUKUUCUUDUBUUHDBUEUGUIUJUKYTUUOBUUDUBTZUUQMYSUUOUVOMNZYHYIYRUVPYIYRCCQN
      ZUVPYICKJZUVQCULZCUMUNYQUVQUVPUCACIYJCUFZYKUVQYPUVPYJCCQUOUVTYMUUOYOUVOMU
      VTYLUUNEYJCRSUPUHUVTYNUUDBUBYJCEUQURUJUSUTVAVBVCYHUUQUVOUFYSYHUUPBUUDUBYF
      UUPBUFZYGYFBVDJZUWABVEZBVFUNVGUIVGVHUUSIJZYTUVDUVJUWDYTUVDUVJUCUWDYTOUVDU
      UTRSTZEUAZBUVCUBTZMNZUVJUWDYFYGYSUVDUWHUCZUWDYFYSYGUWIYFUWDYSYGUWIUCZUCYF
      UWDYIYRUWJYFUWDYIYRUWJUCYFUWDOZYIOZYRYGUVDUWHUWLYRUWFBUVAUBTZMNZYGUVDOUWM
      UWGMNZUWHUWDYIYRUWNUCZYFYIUWDUWPYIUWDOZYRCUUTQNZUWNYIUVRUUSVIJZUWRUWDUVSU
      USVJZCUUSVKVLUWQUUTIJZYRUWRUWNUCZUCCUUSVMZYQUXBAUUTIYJUUTUFZYKUWRYPUWNYJU
      UTCQUOUXDYMUWFYOUWMMUXDYLUWEEYJUUTRSUPUHUXDYNUVABUBYJUUTEUQURUJUSUTUNVAVN
      VOUWLYGUVDUWOUWLUVAKJZUVCKJZYFYGUVDUWOUCZUCUWLUXAUXEUWDYIUXAYFYIUWDUXAUXC
      VNZVOIKUUTEFVPZUNUWKUVBKJZUUDKJZUXFYIUWDYFUWSUXJUWTBUUSVQVRIKCEFVPZUVBUUD
      VSVLZYFUWDYIVTZUXEUXFYFWAZYGUXGUXOYGOUVDUWOUVAUVCBWBWCWDWEWFUWLUWFKJZUWMK
      JZUWGKJZUWNUWOOUWHUCUWDYIUXPYFUWDYIOZUWEIJZUXPUXSUXAUXTUXHUUTWGUNIKUWEEFV
      PUNVOYFUWDYIUXQUXSYFUXEUXQUXSUXAUXEUXHUXIUNBUVAVSVRWHUWLYFUXFUXRUXNUXMBUV
      CVSWIUWFUWMUWGWJWEWKWLWMWNWOWPWQUWDYHYIUWHUVJWRZYRUWDYFYIUYAYGYFYIOZUWDUY
      AYFUWBYIUWDUYAUWCUWBYIOUWDOZUWFUVGUWGUVIMYIUWDUWFUVGUFUWBUWQUWEUVFEYICVDJ
      ZUUSVDJZUWEUVFUFZUWDCWSUUSWSUYDUYERVDJUYFWTCUUSRXAXBVLUHVOUYCUVIBUVBUBTZU
      UDUBTZUWGUYCUVHUYGUUDUBUWBUWDUVHUYGUFYIUWBUWDOZUVHUVBBUBTZUYGUWDUWBUWSUVH
      UYJUFUWTBUUSXCVRUYIUVBVDJZUWBUYJUYGUFUWDUWBUWSUYKUWTBUUSXDVRZUWBUWDXEZUVB
      BXFWIXGXHUIUWBUWDYIUYHUWGUFZUYIUWBUYKOUUDVDJZUYNYIUYIUWBUYKUYMUYLXIYIUUDU
      XLXJUWBUYKUYOUYNBUVBUUDXKXLVLXMXNUJXOVNXPXQXRWDXSXTVBYEYHYIUUFUUGUCZYRYEY
      FYIUYPYGYEUYBOUUBKJZUUEKJZUYPYEYIUYQYFYEYIOUUAIJZUYQYIYEUYSCDVMVNIKUUAEFV
      PUNYAYEYFYIUYRYEYFOUUCKJZUXKUYRYIYFYEUYTYEYFDVIJUYTDVJBDVQVRVNUXLUUCUUDVS
      VLYBUUBUUEYCWIXPXQYD $.
      $( [28-Jul-2006] $) $( [26-Apr-2005] $)

    $( Lemma for ~ cvgrati .  Using ~ expsubOLD , restate ~ cvgratlem1 with an
       absolute index ` C ` instead of just an offset from the starting index
       ` B ` . $)
    cvgratlem2 $p |- ( ( ( A e. RR /\ 0 < A ) /\ ( B e. NN /\
         A. x e. NN ( B <_ x -> ( F ` ( x + 1 ) ) < ( A x. ( F ` x ) ) ) ) ) ->
                     ( ( C e. NN /\ B < C ) ->
                 ( F ` C ) <_ ( ( ( F ` B ) / ( A ^ B ) ) x. ( A ^ C ) ) ) ) $=
      ( cr wcel cc0 clt wbr wa cn cv cle c1 caddc co cfv cmul wi wral cexp
      cdiv cmin nnsub cvgratlem1 exp45 com3r imp4d adantr cc wceq nncn pncan3
      syl2an fveq2d breq1d sylibd exp3a sylbid expimpd exp4a com12 com4l imp42
      w3a wne recn id 3anim123i 3expa an32s adantllr adantrr gt0ne0 simpr
      anim12i cn0 3ad2antl1 nnnn0 3ad2ant2 3ad2ant3 3jca nnre ltle ancoms imp
      3adantl1 adantrl expsub syl11anc opreq1d ffvelrni recnd ad2antlr reexpcl
      sylan2 adantlr expgt0 syl3an2 div13 syl121anc eqtr4d adantlrr breqtrd ex
      ) BGHZIBJKZLZCMHZCANZOKXLPQRESBXLESTRJKUAAMUBZLLZDMHZCDJKZLZDESZCESZBCUCR
      ZUDRBDUCRZTRZOKXNXQLXRBDCUERZUCRZXSTRZYBOXJXKXMXQXRYEOKZXKXJXMXQYFUAUAXQX
      KXJXMYFXKXQXJXMYFUAUAXKXQXJXMYFXKXOXPXJXMLZYFUAZXKXOLZXPYCMHZYHCDUFYIYJYG
      YFYIYJYGLZCYCQRZESZYEOKZYFXKYKYNUAXOXKYJXJXMYNYJXJXKXMYNUAYJXJXKXMYNABCYC
      EFUGUHUIUJUKYIYMXRYEOYIYLDEXKCULHDULHYLDUMXOCUNDUNCDUOUPUQURUSUTVAVBVCVDV
      EVDVFXJXKXQYEYBUMXMXJXKLZXQLZYEYAXTUDRZXSTRZYBYPYDYQXSTYPBULHZXOXKVGZBIVH
      ZXPLZYDYQUMZYOXOYTXPXHXKXOYTXIXHXOXKYTXHXOXKYTXHYSXOXOXKXKBVIXOVJXKVJVKVL
      VMVNVOYOUUAXQXPXJUUAXKBVPUKXOXPVQVRYTUUBLYSUUALZDVSHZCVSHZVGZCDOKZUUCYTUU
      AUUGXPYTUUALUUDUUEUUFYSXOUUAUUDXKUUDVJVTYTUUEUUAXOYSUUEXKDWAZWBUKYTUUFUUA
      XKYSUUFXOCWAZWCUKWDVOYTXPUUHUUAXOXKXPUUHYSXOXKLXPUUHXODGHZCGHZXPUUHUAZXKD
      WECWEUULUUKUUMCDWFWGUPWHWIWJBDCWKWLWLWMYOXOYBYRUMZXPYOXOLXSULHZXTULHZXTIV
      HZYAULHZUUNXKUUOXJXOXKXSMGCEFWNWOWPYOUUPXOXHXKUUPXIXHXKLXTXKXHUUFXTGHZUUJ
      BCWQWRZWOWSUKYOUUQXOYOUUSIXTJKZUUQXHXKUUSXIUUTWSXHXKXIUVAXHXKXIUVAXKXHUUF
      XIUVAUUJBCWTXAVLVMXTVPWLUKXJXOUURXKXHXOUURXIXHXOLYAXOXHUUEYAGHUUIBDWQWRWO
      WSWSXSXTYAXBXCVOXDXEXFXG $.
      $( [28-Jul-2006] $) $( [22-Apr-2005] $)
  $}

  ${
    $d x A $.  $d x y z B $.  $d x y z F $.  $d y z C $.
    cvgratlem3.1 $e |- F : NN --> CC $.
    $d G x $.
    cvgratlem3.2 $e |- G =
                    { <. y , z >. | ( y e. NN /\ z = ( abs ` ( F ` y ) ) ) } $.
    $( Lemma for ~ cvgrati .  Restate ~ cvgratlem2 (which was for a real
       function) in terms of the absolute values of the terms of a complex
       function ` F ` , with the help of an auxiliary function ` G ` . $)
    cvgratlem3 $p |- ( ( ( A e. RR /\ 0 < A ) /\ ( B e. NN /\
       A. x e. NN ( B <_ x ->
  ( abs ` ( F ` ( x + 1 ) ) ) < ( A x. ( abs ` ( F ` x ) ) ) ) ) ) ->
  ( ( C e. NN /\ B < C ) -> ( abs ` ( F ` C ) ) <_ ( ( ( abs ` ( F ` B ) )
                       / ( A ^ B ) ) x. ( A ^ C ) ) ) ) $=
      ( cn wcel cv cle wbr c1 caddc co cfv cabs cmul clt wi wral wa cr cc0
      cexp cdiv wceq peano2nn fveq2 fveq2d fvex fvopab4 syl opreq2d breq12d
      imbi2d ralbiia anbi2i cc ffvelrni abscl fopab cvgratlem2 imp ad2antrl
      opreq1d adantr ad2antlr 3brtr3d ex sylan2br ) EKLZEAMZNOZVPPQRZGSZTSZDVPG
      SZTSZUARZUBOZUCZAKUDZUEDUFLUGDUBOUEZVOVQVRHSZDVPHSZUARZUBOZUCZAKUDZUEZFKL
      ZEFUBOZUEZFGSZTSZEGSZTSZDEUHRZUIRZDFUHRZUARZNOZUCWMWFVOWLWEAKVPKLZWKWDVQX
      GWHVTWJWCUBXGVRKLWHVTUJVPUKBCVRBMZGSZTSZVTKHXHVRUJXIVSTXHVRGULUMJVSTUNUOU
      PXGWIWBDUABCVPXJWBKHXHVPUJXIWATXHVPGULUMJWATUNUOUQURUSUTVAWGWNUEZWQXFXKWQ
      UEFHSZEHSZXBUIRZXDUARZWSXENXKWQXLXONOADEFHBCKUFXJHJXHKLXIVBLXJUFLKVBXHGIV
      CXIVDUPVEVFVGWOXLWSUJXKWPBCFXJWSKHXHFUJXIWRTXHFGULUMJWRTUNUOVHWNXOXEUJZWG
      WQVOXPWMVOXNXCXDUAVOXMXAXBUIBCEXJXAKHXHEUJXIWTTXHEGULUMJWTTUNUOVIVIVJVKVL
      VMVN $.
      $( [25-Apr-2005] $)
  $}

  ${
    $d x y z w v A $.  $d x y v u B $.  $d x y v u F $.  $d y v G $.
    cvgrat.1 $e |- F : NN --> CC $.
    $( Lemma for ~ cvgrati .  The ratio of successive terms meeting the ratio
       test criterion is positive. $)
    cvgratlem4 $p |- ( ( A e. RR /\ ( B e. NN /\ A. x e. NN ( B <_ x ->
 ( abs ` ( F ` ( x + 1 ) ) ) < ( A x. ( abs ` ( F ` x ) ) ) ) ) ) -> 0 < A ) $=
      ( cr wcel cn cv cle wbr c1 caddc co cfv cabs cmul clt wi wral cc0 wa
      nnre leid syl wceq breq2 opreq1 fveq2d fveq2 opreq2d breq12d imbi12d
      rcla4v mpid adantl cc peano2nn ffvelrni absge0 3syl abscl remulcl sylan2
      0re lelttr mp3an1 syl11anc mpand jctild prodgt02 ex 3syld impr ) BFGZCHGZ
      CAIZJKZVQLMNZDOZPOZBVQDOZPOZQNZRKZSZAHTZUABRKZVOVPUBZWGCLMNZDOZPOZBCDOZPO
      ZQNZRKZUAWNJKZUAWORKZUBZWHVPWGWPSVOVPWGCCJKZWPVPCFGWTCUCCUDUEWFWTWPSACHVQ
      CUFZVRWTWEWPVQCCJUGXAWAWLWDWORXAVTWKPXAVSWJDVQCLMUHUIUIXAWCWNBQXAWBWMPVQC
      DUJUIUKULUMUNUOUPWIWPWRWQWIUAWLJKZWPWRVPXBVOVPWJHGZWKUQGZXBCURZHUQWJDEUSZ
      WKUTVAUPWIWLFGZWOFGZXBWPUBWRSZVPXGVOVPXCXDXGXEXFWKVBVAUPVPVOWNFGZXHVPWMUQ
      GZXJHUQCDEUSZWMVBUEZBWNVCVDUAFGXGXHXIVEUAWLWOVFVGVHVIVPWQVOVPXKWQXLWMUTUE
      UPVJVPVOXJWSWHSXMVOXJUBWSWHBWNVKVLVDVMVN $.
      $( [25-Apr-2005] $)

    ${
      cvgratlem5.2 $e |- G = { <. z , w >. | ( z e. NN /\ w = ( A ^ z ) ) } $.
      $( Lemma for ~ cvgrati .  A complex infinite series ` F ` meeting the
         ratio test criterion converges.  We show that the partial sums of
         ` F ` are smaller than the partial sums of a geometric series (which
         converges by ~ geolimi ), so by the comparison test ~ cvgcmp3ce ,
         ` F ` also converges. $)
      cvgratlem5 $p |- ( ( ( A e. RR /\ A < 1 ) /\ ( B e. NN /\
           A. x e. NN ( B <_ x ->
  ( abs ` ( F ` ( x + 1 ) ) ) < ( A x. ( abs ` ( F ` x ) ) ) ) ) ) ->
                    E. y ( + seq1 F ) ~~> y ) $=
        ( vv vu cr wcel c1 clt wbr wa cn cv cle caddc co cfv cabs cmul wi wral
        wf cc0 cseq1 cmin cdiv cli cexp cc wex simprl cvgratlem4 adantlr 0re
        ltle mpan imp cn0 nnnn0 reexpcl sylan2 r19.21aiva fopab2 sylib adantr
        expge0 syl3an2 3expa an32s wceq opreq2 oprex fvopab4 adantl breqtrrd
        jca recn ad2antrr absid simplr eqbrtrd geolim1 syl11anc syldan w3a wne
        ffvelrni abscl syl 3ad2ant2 3adant3 gt0ne0 3adant2 wb expne0 sylan
        mpbird redivcl syl111anc absge0 expgt0 divge0 syl22anc adantrr copab
        eqid cvgratlem3 opreq2d ad2antrl exp32 r19.21aiv jctil adantllr exp31
        com23 mpd cvgcmp3ce ) EMNZEOPQZRZFSNZFATZUAQYIOUBUCGUDUEUDEYIGUDUEUDUFU
        CPQUGASUHZRZRZYHSMHUIZUJKTZHUDZUAQZKSUHZRZUBHUKUCEOEULUCZUMUCZUNQZRZFGU
        DZUEUDZEFUOUCZUMUCZMNZUJUUFUAQZRZSUPGUIZFYNPQZYNGUDUEUDZUUFYOUFUCZUAQZU
        GZKSUHZRZRZRZUBGUKUCBTZUNQBUQYGYHYJURYLUJEPQZUUSYEYKUVAYFAEFGIUSUTYGYKU
        VAUUSUGYGUVAYKUUSYGUVAYKUUSYGUVARZYKRUUBUURUVBUUBYKYGUVAUJEUAQZUUBYEUVA
        UVCYFYEUVAUVCUJMNYEUVAUVCUGVAUJEVBVCVDUTYGUVCRZYRUUAYEUVCYRYFYEUVCRZYMY
        QYEYMUVCYEECTZUOUCZMNZCSUHYMYEUVHCSUVFSNYEUVFVENUVHUVFVFEUVFVGVHVICDSMU
        VGHJVJVKVLUVEYPKSUVEYNSNZRUJEYNUOUCZYOUAYEUVIUVCUJUVJUAQZYEUVIUVCUVKUVI
        YEYNVENUVCUVKYNVFEYNVMVNVOVPUVIYOUVJVQUVECDYNUVGUVJSHUVFYNEUOVRJEYNUOVS
        VTZWAWBVIWCUTUVDEUPNZEUEUDZOPQUUAYEUVMYFUVCEWDZWEUVDUVNEOPYEUVCUVNEVQYF
        EWFUTYEYFUVCWGWHDECHJWIWJWCWKVLYEUVAYKUURYFYEUVARZYKRZUUIUUQUVPYHUUIYJY
        EYHUVAUUIYEYHUVAUUIYEYHUVAWLZUUGUUHUVRUUDMNZUUEMNZUUEUJWMZUUGYHYEUVSUVA
        YHUUCUPNZUVSSUPFGIWNZUUCWOWPWQZYEYHUVTUVAYHYEFVENZUVTFVFZEFVGVHWRZUVRUW
        AEUJWMZYEUVAUWHYHEWSWTYEYHUWAUWHXAZUVAYEUVMYHUWIUVOEFXBXCWRXDUUDUUEXEXF
        UVRUVSUJUUDUAQZUVTUJUUEPQZUUHUWDYHYEUWJUVAYHUWBUWJUWCUUCXGWPWQUWGYHYEUW
        EUVAUWKUWFEFXHVNUUDUUEXIXJWCVOVPXKUVQUUPUUJUVQUUOKSUVQUVIUUKUUNUVQUVIUU
        KRZRUULUUFUVJUFUCZUUMUAUVQUWLUULUWMUAQABLEFYNGUUTSNLTUUTGUDUEUDVQRBLXLZ
        IUWNXMXNVDUVIUUMUWMVQUVQUUKUVIYOUVJUUFUFUVLXOXPWBXQXRIXSWCXTWCYAYBVDYCK
        BYTFUUFHGEYSUMVSYDWJ $.
        $( [25-Jun-2006] $) $( [26-Apr-2005] $)
    $}

    $( Ratio test for convergence of a complex infinite series.  If the ratio
       ` A ` of the absolute values of of successive terms in an infinite
       sequence ` F ` is less than 1 for all terms beyond some index ` B ` ,
       then the infinite sum of the terms of ` F ` converges to a complex
       number.  Equivalent to first part of Exercise 4 of [Gleason] p. 182. $)
    cvgrati $p |- ( ( ( A e. RR /\ A < 1 ) /\ ( B e. NN /\ A. x e. NN (
  B <_ x -> ( abs ` ( F ` ( x + 1 ) ) ) < ( A x. ( abs ` ( F ` x ) ) ) ) ) ) ->
                 E. y ( + seq1 F ) ~~> y ) $=
      ( vz vw cv cn wcel cexp co wceq wa copab eqid cvgratlem5 ) ABGHCDEGIZJKHI
      CSLMNOGHPZFTQR $.
      $( [25-Jun-2006] $) $( [26-Apr-2005] $)
  $}


