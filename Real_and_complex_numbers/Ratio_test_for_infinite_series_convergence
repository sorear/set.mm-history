$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ratio test for infinite series convergence
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y z $.  $d x y z A $.  $d x y z B $.  $d y C $.  $d x y z F $.
    cvgratlem1ALT.1 $e |- F : NN --> RR $.
    cvgratlem1ALT.2 $e |- A e. RR $.
    cvgratlem1ALT.3 $e |- B e. NN $.
    cvgratlem1ALT.4 $e |- C e. NN $.
    $( Lemma for ~ cvgrati .  Establish, by induction, an exponential upper
       bound for the terms of a real series, given that the ratio of successive
       terms is less than some positive constant ` A ` beyond a starting index
       ` B ` . $)
    cvgratlem1ALT $p |- ( ( 0 < A /\
       A. x e. NN ( B <_ x -> ( F ` ( x + 1 ) ) < ( A x. ( F ` x ) ) ) ) ->
                     ( F ` ( B + C ) ) <_ ( ( A ^ C ) x. ( F ` B ) ) ) $=
      ( clt wbr c1 caddc co cmul wi cn wcel wceq cr vy vz cc0 cle cfv wral cexp
      cv wa opreq2 fveq2d opreq1d breq12d imbi2d nnrei leidi breq2 opreq1 fveq2
      weq opreq2d imbi12d rcla4v ax-mp mpi cc exp1 opreq1i syl6breqr adantl cn0
      recni nn0addge1 sylancr nnaddcl mpan syl ffvelrni reexpcl remulcl sylancl
      nnnn0 mpid w3a ltmul2 biimpd 3expia syl11anc mpani imp3a peano2nn axlttrn
      3syl syl111anc syl2and exp4d com23 imp32 nncn nncni ax-1cn addass mp3an13
      wb expp1 expcl mulcom eqtrd mulass eqtr2d adantr sylibd ex nnind remulcli
      a2d mp2an sylan2 ltlei ) UCBJKZCAUHZUDKZYALMNZEUEZBYAEUEZONZJKZPZAQUFZUIZ
      CDMNZEUEZBDUGNZCEUEZONZJKZYLYOUDKDQRZYJYPPZIYJCUAUHZMNZEUEZBYSUGNZYNONZJK
      ZPYJCLMNZEUEZBLUGNZYNONZJKZPYJCUBUHZMNZEUEZBUUJUGNZYNONZJKZPYJCUUJLMNZMNZ
      EUEZBUUPUGNZYNONZJKZPYRUAUBDYSLSZUUDUUIYJUVBUUAUUFUUCUUHJUVBYTUUEEYSLCMUJ
      UKUVBUUBUUGYNOYSLBUGUJULUMUNUAUBUTZUUDUUOYJUVCUUAUULUUCUUNJUVCYTUUKEYSUUJ
      CMUJUKUVCUUBUUMYNOYSUUJBUGUJULUMUNYSUUPSZUUDUVAYJUVDUUAUURUUCUUTJUVDYTUUQ
      EYSUUPCMUJUKUVDUUBUUSYNOYSUUPBUGUJULUMUNYSDSZUUDYPYJUVEUUAYLUUCYOJUVEYTYK
      EYSDCMUJUKUVEUUBYMYNOYSDBUGUJULUMUNYIUUIXTYIUUFBYNONZUUHJYICCUDKZUUFUVFJK
      ZCCHUOZUPCQRZYIUVGUVHPZPHYHUVKACQYACSZYBUVGYGUVHYACCUDUQUVLYDUUFYFUVFJUVL
      YCUUEEYACLMURUKUVLYEYNBOYACEUSVAUMVBVCVDVEUUGBYNOBVFRZUUGBSBGVLZBVGVDVHVI
      VJUUJQRZYJUUOUVAUVOYJUUOUVAPUVOYJUIUUOUUKLMNZEUEZBUUNONZJKZUVAUVOXTYIUUOU
      VSPZUVOYIXTUVTUVOYIXTUUOUVSUVOYIUVQBUULONZJKZXTUUOUIUWAUVRJKZUVSUVOYICUUK
      UDKZUWBUVOCTRUUJVKRZUWDUVIUUJWBZCUUJVMVNUVOUUKQRZYIUWDUWBPZPUVJUVOUWGHCUU
      JVOVPZYHUWHAUUKQYAUUKSZYBUWDYGUWBYAUUKCUDUQUWJYDUVQYFUWAJUWJYCUVPEYAUUKLM
      URUKUWJYEUULBOYAUUKEUSVAUMVBVCVQWCUVOXTUUOUWCUVOBTRZXTUUOUWCPZGUVOUULTRZU
      UNTRZUWKXTUIZUWLPUVOUWGUWMUWIQTUUKEFVRVQZUVOUUMTRZYNTRZUWNUVOUWKUWEUWQGUW
      FBUUJVSVNUVJUWRHQTCEFVRVDZUUMYNVTWAZUWMUWNUWOUWLUWMUWNUWOWDUUOUWCUULUUNBW
      EWFWGWHWIWJUVOUVQTRZUWATRZUVRTRZUWBUWCUIUVSPUVOUWGUVPQRUXAUWIUUKWKQTUVPEF
      VRWMUVOUWKUWMUXBGUWPBUULVTVNUVOUWKUWNUXCGUWTBUUNVTVNUVQUWAUVRWLWNWOWPWQWR
      UVOUVSUVAXDYJUVOUVQUURUVRUUTJUVOUVPUUQEUVOUUJVFRZUVPUUQSZUUJWSCVFRUXDLVFR
      UXECHWTXACUUJLXBXCVQUKUVOUUTBUUMONZYNONZUVRUVOUUSUXFYNOUVOUUSUUMBONZUXFUV
      OUVMUWEUUSUXHSUVNUWFBUUJXEVNUVOUUMVFRZUVMUXHUXFSUVOUVMUWEUXIUVNUWFBUUJXFV
      NZUVNUUMBXGWAXHULUVOUXIUXGUVRSZUXJUVMUXIYNVFRUXKUVNYNUWSVLBUUMYNXIXCVQXJU
      MXKXLXMXPXNVDYLYOYKQRZYLTRUVJYQUXLHICDVOXQQTYKEFVRVDYMYNUWKYQYMTRZGIYQUWK
      DVKRUXMDWBBDVSXRXQUWSXOXSVQ $.
      $( [28-May-2005] $)

    $( Lemma for ~ cvgrati .  Using ~ expsub , restate ~ cvgratlem1ALT with an
       absolute index ` C ` instead of just an offset from the starting index
       ` B ` . $)
    cvgratlem2ALT $p |- ( ( 0 < A /\
         A. x e. NN ( B <_ x -> ( F ` ( x + 1 ) ) < ( A x. ( F ` x ) ) ) ) ->
                     ( B < C ->
                 ( F ` C ) <_ ( ( ( F ` B ) / ( A ^ B ) ) x. ( A ^ C ) ) ) ) $=
      ( wbr cle co cfv cmul wi cn wa cexp wcel cr cc0 clt cv c1 caddc wral cdiv
      cmin nnsubi cif opreq2 fveq2d opreq1d breq12d imbi2d elimel cvgratlem1ALT
      wceq 1nn dedth nncni pncan3i fveq2i breq1i syl6ib sylbi impcom gt0ne0i cc
      wne w3a recni 3pm3.2i cn0 simpr jctil nnnn0 3ad2ant2 adantr 3ad2ant3 3jca
      adantrr nnre ltle syl2an ancoms imp 3adantl1 adantrl expsub syl11anc mpan
      sylan reexpcl sylan2 mp2an syl3an2 mp3an12 syl ffvelrni ax-mp expcl div13
      expgt0 mp3an13 sylancr eqtr4d adantlr breqtrd ex ) UABUBJZCAUCZKJXLUDUELE
      MBXLEMNLUBJOAPUFZQZCDUBJZDEMZCEMZBCRLZUGLBDRLZNLZKJXNXOQXPBDCUHLZRLZXQNLZ
      XTKXOXNXPYCKJZXOYAPSZXNYDOCDHIUIYEXNCYAUELZEMZYCKJZYDYEXNYHOXNCYEYAUDUJZU
      ELZEMZBYIRLZXQNLZKJZOYAUDYAYIURZYHYNXNYOYGYKYCYMKYOYFYJEYAYICUEUKULYOYBYL
      XQNYAYIBRUKUMUNUOABCYIEFGHYAUDPUSUPUQUTYGXPYCKYFDECDCHVADIVAVBVCVDVEVFVGX
      KXOYCXTURXMXKXOQZYCXSXRUGLZXQNLZXTYPYBYQXQNXKBUAVJZXOYBYQURZBGVHBVISZDPSZ
      CPSZVKZYSXOQZYTUUAUUBUUCBGVLZIHVMUUDUUEQUUAYSQZDVNSZCVNSZVKZCDKJZYTUUDYSU
      UJXOUUDYSQZUUGUUHUUIUULYSUUAUUDYSVOUUFVPUUDUUHYSUUBUUAUUHUUCDVQZVRVSUUDUU
      IYSUUCUUAUUIUUBCVQZVTVSWAWBUUDXOUUKYSUUBUUCXOUUKUUAUUBUUCQXOUUKUUCUUBXOUU
      KOZUUCCTSDTSUUOUUBCWCDWCCDWDWEWFWGWHWIBDCWJWKWLWMUMXKXTYRURZXOXKXRVISZXRU
      AVJZUUPXRBTSZUUCXRTSZGHUUCUUSUUIUUTUUNBCWNWOWPZVLXKUAXRUBJZUURUUSUUCXKUVB
      GHUUCUUSUUIXKUVBUUNBCXDWQWRXRUVAVHWSXQVISUUQUURQXSVISZUUPXQUUCXQTSHPTCEFW
      TXAVLUUAUUBUVCUUFIUUBUUAUUHUVCUUMBDXBWOWPXQXRXSXCXEXFVSXGXHXIXJ $.
      $( [28-May-2005] $)
  $}

  ${
    $d x A $.  $d x y z B $.  $d x y z F $.  $d y z C $.
    cvgratlem3ALT.1 $e |- F : NN --> CC $.
    $d G x $.
    cvgratlem3ALT.2 $e |- G =
                    { <. y , z >. | ( y e. NN /\ z = ( abs ` ( F ` y ) ) ) } $.
    cvgratlem3ALT.3 $e |- A e. RR $.
    cvgratlem3ALT.4 $e |- B e. NN $.
    cvgratlem3ALT.5 $e |- C e. NN $.
    $( Lemma for ~ cvgrati .  Restate ~ cvgratlem2ALT (which was for a real
       function) in terms of the absolute values of the terms of a complex
       function ` F ` , with the help of an auxiliary function ` G ` . $)
    cvgratlem3ALT $p |- ( ( 0 < A /\ A. x e. NN ( B <_ x ->
  ( abs ` ( F ` ( x + 1 ) ) ) < ( A x. ( abs ` ( F ` x ) ) ) ) ) ->
  ( B < C -> ( abs ` ( F ` C ) ) <_ ( ( ( abs ` ( F ` B ) )
                       / ( A ^ B ) ) x. ( A ^ C ) ) ) ) $=
      ( wbr co cfv cabs cn wcel wceq cv cle c1 caddc cmul clt wi wral cexp cdiv
      cc0 peano2nn fveq2 fveq2d fvex fvopab4 syl opreq2d breq12d imbi2d ralbiia
      wa cr cc ffvelrni abscl fopab cvgratlem2ALT imp ax-mp opreq1i ex sylan2br
      3brtr3g ) EAUAZUBNZVOUCUDOZGPZQPZDVOGPZQPZUEOZUFNZUGZARUHUKDUFNZVPVQHPZDV
      OHPZUEOZUFNZUGZARUHZEFUFNZFGPZQPZEGPZQPZDEUIOZUJOZDFUIOZUEOZUBNZUGWJWDARV
      ORSZWIWCVPXBWFVSWHWBUFXBVQRSWFVSTVOULBCVQBUAZGPZQPZVSRHXCVQTXDVRQXCVQGUMU
      NJVRQUOUPUQXBWGWADUEBCVOXEWARHXCVOTXDVTQXCVOGUMUNJVTQUOUPURUSUTVAWEWKVBZW
      LXAXFWLVBFHPZEHPZWQUJOZWSUEOZWNWTUBXFWLXGXJUBNADEFHBCRVCXEHJXCRSXDVDSXEVC
      SRVDXCGIVEXDVFUQVGKLMVHVIFRSXGWNTMBCFXEWNRHXCFTXDWMQXCFGUMUNJWMQUOUPVJXIW
      RWSUEXHWPWQUJERSXHWPTLBCEXEWPRHXCETXDWOQXCEGUMUNJWOQUOUPVJVKVKVNVLVM $.
      $( [28-May-2005] $)
  $}

  ${
    $d x y z $.  $d x y z A $.  $d x y z B $.  $d y C $.  $d x y z F $.
    cvgratlem1.1 $e |- F : NN --> RR $.
    $( Lemma for ~ cvgrati .  Establish, by induction, an exponential upper
       bound for the terms of a real series, given that the ratio of successive
       terms is less than some positive constant ` A ` beyond a starting index
       ` B ` . $)
    cvgratlem1 $p |- ( ( C e. NN /\ ( ( A e. RR /\ 0 < A ) /\ ( B e. NN /\
       A. x e. NN ( B <_ x -> ( F ` ( x + 1 ) ) < ( A x. ( F ` x ) ) ) ) ) ) ->
                     ( F ` ( B + C ) ) <_ ( ( A ^ C ) x. ( F ` B ) ) ) $=
      ( cn wcel cr clt wbr wa c1 caddc co cfv cmul wi cexp wceq vy cc0 cle wral
      vz cv opreq2 fveq2d opreq1d breq12d imbi2d weq nnre leid syl breq2 opreq1
      fveq2 opreq2d imbi12d rcla4v mpid imp adantl cc recn exp1 adantr breqtrrd
      cn0 nnnn0 nn0addge1 syl2an nnaddcl ancoms adantll ffvelrni reexpcl sylan2
      remulcl simplll w3a ltmul2 biimpd syl112anc expimpd peano2nn 3syl anassrs
      simpr simpll syl11anc axlttrn syl2and exp4d exp31 imp4a com12 com34 imp44
      syl111anc wb nncn ax-1cn addass mp3an3 expp1 expcl simpl mulcom eqtrd jca
      adantlr recnd mulass 3expa eqtr2d sylanl1 adantrlr adantrrr sylibd ex a2d
      an32s nnind adantrl anasss ltle mpd ) DGHZBIHZUBBJKZLZCGHZCAUFZUCKZYOMNOZ
      EPZBYOEPZQOZJKZRZAGUDZLZLZLCDNOZEPZBDSOZCEPZQOZJKZUUGUUJUCKZYJUUEUUKUUECU
      AUFZNOZEPZBUUMSOZUUIQOZJKZRUUECMNOZEPZBMSOZUUIQOZJKZRUUECUEUFZNOZEPZBUVDS
      OZUUIQOZJKZRUUECUVDMNOZNOZEPZBUVJSOZUUIQOZJKZRUUEUUKRUAUEDUUMMTZUURUVCUUE
      UVPUUOUUTUUQUVBJUVPUUNUUSEUUMMCNUGUHUVPUUPUVAUUIQUUMMBSUGUIUJUKUAUEULZUUR
      UVIUUEUVQUUOUVFUUQUVHJUVQUUNUVEEUUMUVDCNUGUHUVQUUPUVGUUIQUUMUVDBSUGUIUJUK
      UUMUVJTZUURUVOUUEUVRUUOUVLUUQUVNJUVRUUNUVKEUUMUVJCNUGUHUVRUUPUVMUUIQUUMUV
      JBSUGUIUJUKUUMDTZUURUUKUUEUVSUUOUUGUUQUUJJUVSUUNUUFEUUMDCNUGUHUVSUUPUUHUU
      IQUUMDBSUGUIUJUKUUEUUTBUUIQOZUVBJUUDUUTUVTJKZYMYNUUCUWAYNUUCCCUCKZUWAYNCI
      HZUWBCUMZCUNUOUUBUWBUWARACGYOCTZYPUWBUUAUWAYOCCUCUPUWEYRUUTYTUVTJUWEYQUUS
      EYOCMNUQUHUWEYSUUIBQYOCEURUSUJUTVAVBVCVDYMUVBUVTTUUDYMUVABUUIQYKUVABTZYLY
      KBVEHZUWFBVFZBVGUOVHUIVHVIUVDGHZUUEUVIUVOUWIUUEUVIUVORUWIUUELUVIUVEMNOZEP
      ZBUVHQOZJKZUVOUWIYKYLUUDUVIUWMRZUWIYKUUDYLUWNYKUWIUUDYLUWNRZRYKUWIYNUUCUW
      OYKUWIYNUUCUWORYKUWILZYNLZUUCYLUVIUWMUWQUUCUWKBUVFQOZJKZYLUVILUWRUWLJKZUW
      MUWIYNUUCUWSRZYKYNUWIUXAYNUWILZUUCCUVEUCKZUWSYNUWCUVDVJHZUXCUWIUWDUVDVKZC
      UVDVLVMUXBUVEGHZUUCUXCUWSRZRCUVDVNZUUBUXGAUVEGYOUVETZYPUXCUUAUWSYOUVECUCU
      PUXIYRUWKYTUWRJUXIYQUWJEYOUVEMNUQUHUXIYSUVFBQYOUVEEURUSUJUTVAUOVBVOVPUWQY
      LUVIUWTUWQYLLUVFIHZUVHIHZYKYLUVIUWTRUWQUXJYLUWIYNUXJYKYNUWIUXJUXBUXFUXJUX
      HGIUVEEFVQZUOVOVPVHUWQUXKYLUWPUVGIHZUUIIHZUXKYNUWIYKUXDUXMUXEBUVDVRVSGICE
      FVQZUVGUUIVTVMZVHYKUWIYNYLWAUWQYLWJUXJUXKYMWBUVIUWTUVFUVHBWCWDWEWFUWQUWKI
      HZUWRIHZUWLIHZUWSUWTLUWMRUWIYNUXQYKUWIYNLZUXFUWJGHUXQYNUWIUXFUXHVOZUVEWGG
      IUWJEFVQWHVPYKUWIYNUXRUXTYKUXJUXRUXTUXFUXJUYAUXLUOBUVFVTVSWIUWQYKUXKUXSYK
      UWIYNWKUXPBUVHVTWLUWKUWRUWLWMXAWNWOWPWQWRWSWTUWIYMYNUWMUVOXBZUUCUWIYKYNUY
      BYLYKYNLZUWIUYBYKUWGYNUWIUYBUWHUWGYNLUWILZUWKUVLUWLUVNJYNUWIUWKUVLTUWGUXB
      UWJUVKEYNCVEHZUVDVEHZUWJUVKTZUWICXCUVDXCUYEUYFMVEHUYGXDCUVDMXEXFVMUHVPUYD
      UVNBUVGQOZUUIQOZUWLUYDUVMUYHUUIQUWGUWIUVMUYHTYNUWGUWILZUVMUVGBQOZUYHUWIUW
      GUXDUVMUYKTUXEBUVDXGVSUYJUVGVEHZUWGUYKUYHTUWIUWGUXDUYLUXEBUVDXHVSZUWGUWIX
      IZUVGBXJWLXKXMUIUWGUWIYNUYIUWLTZUYJUWGUYLLUUIVEHZUYOYNUYJUWGUYLUYNUYMXLYN
      UUIUXOXNUWGUYLUYPUYOBUVGUUIXOXPVMYDXQUJXRVOXSXTYAYBYCYEVCYJYMYNUUKUULRZUU
      CYJYKYNUYQYLYJUYCLUUGIHZUUJIHZUYQYJYNUYRYKYJYNLUUFGHZUYRYNYJUYTCDVNVOGIUU
      FEFVQUOYFYJYKYNUYSYJYKLUUHIHZUXNUYSYNYKYJVUAYJYKDVJHVUADVKBDVRVSVOUXOUUHU
      UIVTVMYGUUGUUJYHWLXSXTYI $.
      $( [28-Jul-2006] $) $( [26-Apr-2005] $)

    $( Lemma for ~ cvgrati .  Using ~ expsubOLD , restate ~ cvgratlem1 with an
       absolute index ` C ` instead of just an offset from the starting index
       ` B ` . $)
    cvgratlem2 $p |- ( ( ( A e. RR /\ 0 < A ) /\ ( B e. NN /\
         A. x e. NN ( B <_ x -> ( F ` ( x + 1 ) ) < ( A x. ( F ` x ) ) ) ) ) ->
                     ( ( C e. NN /\ B < C ) ->
                 ( F ` C ) <_ ( ( ( F ` B ) / ( A ^ B ) ) x. ( A ^ C ) ) ) ) $=
      ( cr wcel cc0 clt wbr wa cn cle co cfv cmul wi adantr cc cv c1 caddc wral
      cexp cdiv cmin nnsub cvgratlem1 exp45 com3r imp4d wceq nncn pncan3 syl2an
      fveq2d breq1d sylibd exp3a sylbid expimpd exp4a com12 com4l imp42 w3a wne
      recn id 3anim123i 3expa an32s adantllr adantrr gt0ne0 simpr cn0 3ad2antl1
      anim12i nnnn0 3ad2ant2 3ad2ant3 3jca nnre ltle ancoms imp 3adantl1 expsub
      adantrl syl11anc opreq1d ffvelrni ad2antlr reexpcl sylan2 adantlr syl3an2
      recnd expgt0 div13 syl121anc eqtr4d adantlrr breqtrd ex ) BGHZIBJKZLZCMHZ
      CAUAZNKXLUBUCOEPBXLEPQOJKRAMUDZLLZDMHZCDJKZLZDEPZCEPZBCUEOZUFOBDUEOZQOZNK
      XNXQLXRBDCUGOZUEOZXSQOZYBNXJXKXMXQXRYENKZXKXJXMXQYFRRXQXKXJXMYFXKXQXJXMYF
      RRXKXQXJXMYFXKXOXPXJXMLZYFRZXKXOLZXPYCMHZYHCDUHYIYJYGYFYIYJYGLZCYCUCOZEPZ
      YENKZYFXKYKYNRXOXKYJXJXMYNYJXJXKXMYNRYJXJXKXMYNABCYCEFUIUJUKULSYIYMXRYENY
      IYLDEXKCTHDTHYLDUMXOCUNDUNCDUOUPUQURUSUTVAVBVCVDVEVDVFXJXKXQYEYBUMXMXJXKL
      ZXQLZYEYAXTUFOZXSQOZYBYPYDYQXSQYPBTHZXOXKVGZBIVHZXPLZYDYQUMZYOXOYTXPXHXKX
      OYTXIXHXOXKYTXHXOXKYTXHYSXOXOXKXKBVIXOVJXKVJVKVLVMVNVOYOUUAXQXPXJUUAXKBVP
      SXOXPVQVTYTUUBLYSUUALZDVRHZCVRHZVGZCDNKZUUCYTUUAUUGXPYTUUALUUDUUEUUFYSXOU
      UAUUDXKUUDVJVSYTUUEUUAXOYSUUEXKDWAZWBSYTUUFUUAXKYSUUFXOCWAZWCSWDVOYTXPUUH
      UUAXOXKXPUUHYSXOXKLXPUUHXODGHZCGHZXPUUHRZXKDWECWEUULUUKUUMCDWFWGUPWHWIWKB
      DCWJWLWLWMYOXOYBYRUMZXPYOXOLXSTHZXTTHZXTIVHZYATHZUUNXKUUOXJXOXKXSMGCEFWNW
      TWOYOUUPXOXHXKUUPXIXHXKLXTXKXHUUFXTGHZUUJBCWPWQZWTWRSYOUUQXOYOUUSIXTJKZUU
      QXHXKUUSXIUUTWRXHXKXIUVAXHXKXIUVAXKXHUUFXIUVAUUJBCXAWSVLVMXTVPWLSXJXOUURX
      KXHXOUURXIXHXOLYAXOXHUUEYAGHUUIBDWPWQWTWRWRXSXTYAXBXCVOXDXEXFXG $.
      $( [28-Jul-2006] $) $( [22-Apr-2005] $)
  $}

  ${
    $d x A $.  $d x y z B $.  $d x y z F $.  $d y z C $.
    cvgratlem3.1 $e |- F : NN --> CC $.
    $d G x $.
    cvgratlem3.2 $e |- G =
                    { <. y , z >. | ( y e. NN /\ z = ( abs ` ( F ` y ) ) ) } $.
    $( Lemma for ~ cvgrati .  Restate ~ cvgratlem2 (which was for a real
       function) in terms of the absolute values of the terms of a complex
       function ` F ` , with the help of an auxiliary function ` G ` . $)
    cvgratlem3 $p |- ( ( ( A e. RR /\ 0 < A ) /\ ( B e. NN /\
       A. x e. NN ( B <_ x ->
  ( abs ` ( F ` ( x + 1 ) ) ) < ( A x. ( abs ` ( F ` x ) ) ) ) ) ) ->
  ( ( C e. NN /\ B < C ) -> ( abs ` ( F ` C ) ) <_ ( ( ( abs ` ( F ` B ) )
                       / ( A ^ B ) ) x. ( A ^ C ) ) ) ) $=
      ( cn wcel wbr co cfv cabs cmul clt wa wceq cv cle c1 caddc wi wral cr cc0
      cexp cdiv peano2nn fveq2d fvex fvopab4 syl opreq2d breq12d imbi2d ralbiia
      fveq2 anbi2i cc ffvelrni abscl fopab cvgratlem2 ad2antrl opreq1d ad2antlr
      imp adantr 3brtr3d ex sylan2br ) EKLZEAUAZUBMZVPUCUDNZGOZPOZDVPGOZPOZQNZR
      MZUEZAKUFZSDUGLUHDRMSZVOVQVRHOZDVPHOZQNZRMZUEZAKUFZSZFKLZEFRMZSZFGOZPOZEG
      OZPOZDEUINZUJNZDFUINZQNZUBMZUEWMWFVOWLWEAKVPKLZWKWDVQXGWHVTWJWCRXGVRKLWHV
      TTVPUKBCVRBUAZGOZPOZVTKHXHVRTXIVSPXHVRGUTULJVSPUMUNUOXGWIWBDQBCVPXJWBKHXH
      VPTXIWAPXHVPGUTULJWAPUMUNUPUQURUSVAWGWNSZWQXFXKWQSFHOZEHOZXBUJNZXDQNZWSXE
      UBXKWQXLXOUBMADEFHBCKUGXJHJXHKLXIVBLXJUGLKVBXHGIVCXIVDUOVEVFVJWOXLWSTXKWP
      BCFXJWSKHXHFTXIWRPXHFGUTULJWRPUMUNVGWNXOXETZWGWQVOXPWMVOXNXCXDQVOXMXAXBUJ
      BCEXJXAKHXHETXIWTPXHEGUTULJWTPUMUNVHVHVKVIVLVMVN $.
      $( [25-Apr-2005] $)
  $}

  ${
    $d x y z w v A $.  $d x y v u B $.  $d x y v u F $.  $d y v G $.
    cvgrat.1 $e |- F : NN --> CC $.
    $( Lemma for ~ cvgrati .  The ratio of successive terms meeting the ratio
       test criterion is positive. $)
    cvgratlem4 $p |- ( ( A e. RR /\ ( B e. NN /\ A. x e. NN ( B <_ x ->
 ( abs ` ( F ` ( x + 1 ) ) ) < ( A x. ( abs ` ( F ` x ) ) ) ) ) ) -> 0 < A ) $=
      ( cr wcel cn cle wbr c1 co cfv cabs clt wi cc0 wa adantl cc cv caddc cmul
      wral nnre leid syl wceq breq2 opreq1 fveq2d fveq2 opreq2d breq12d imbi12d
      rcla4v mpid peano2nn ffvelrni absge0 3syl abscl remulcl sylan2 0re lelttr
      mp3an1 syl11anc mpand jctild prodgt02 ex 3syld impr ) BFGZCHGZCAUAZIJZVQK
      UBLZDMZNMZBVQDMZNMZUCLZOJZPZAHUDZQBOJZVOVPRZWGCKUBLZDMZNMZBCDMZNMZUCLZOJZ
      QWNIJZQWOOJZRZWHVPWGWPPVOVPWGCCIJZWPVPCFGWTCUECUFUGWFWTWPPACHVQCUHZVRWTWE
      WPVQCCIUIXAWAWLWDWOOXAVTWKNXAVSWJDVQCKUBUJUKUKXAWCWNBUCXAWBWMNVQCDULUKUMU
      NUOUPUQSWIWPWRWQWIQWLIJZWPWRVPXBVOVPWJHGZWKTGZXBCURZHTWJDEUSZWKUTVASWIWLF
      GZWOFGZXBWPRWRPZVPXGVOVPXCXDXGXEXFWKVBVASVPVOWNFGZXHVPWMTGZXJHTCDEUSZWMVB
      UGZBWNVCVDQFGXGXHXIVEQWLWOVFVGVHVIVPWQVOVPXKWQXLWMUTUGSVJVPVOXJWSWHPXMVOX
      JRWSWHBWNVKVLVDVMVN $.
      $( [25-Apr-2005] $)

    ${
      cvgratlem5.2 $e |- G = { <. z , w >. | ( z e. NN /\ w = ( A ^ z ) ) } $.
      $( Lemma for ~ cvgrati .  A complex infinite series ` F ` meeting the
         ratio test criterion converges.  We show that the partial sums of
         ` F ` are smaller than the partial sums of a geometric series (which
         converges by ~ geolimi ), so by the comparison test ~ cvgcmp3ce ,
         ` F ` also converges. $)
      cvgratlem5 $p |- ( ( ( A e. RR /\ A < 1 ) /\ ( B e. NN /\
           A. x e. NN ( B <_ x ->
  ( abs ` ( F ` ( x + 1 ) ) ) < ( A x. ( abs ` ( F ` x ) ) ) ) ) ) ->
                    E. y ( + seq1 F ) ~~> y ) $=
        ( cr wcel clt wbr wa cn cle co cfv cc0 vv vu c1 cv caddc cabs cmul wral
        wi cseq1 cmin cdiv cli cexp wex simprl cvgratlem4 adantlr 0re ltle mpan
        wf cc imp cn0 nnnn0 reexpcl sylan2 ralrimiva fopab2 sylib adantr expge0
        syl3an2 3expa an32s wceq opreq2 oprex adantl breqtrrd jca recn ad2antrr
        fvopab4 absid simplr eqbrtrd geolim1 syl11anc syldan w3a ffvelrni abscl
        wne syl 3ad2ant2 3adant3 gt0ne0 3adant2 expne0 mpbird redivcl syl111anc
        wb sylan absge0 expgt0 divge0 syl22anc adantrr copab cvgratlem3 opreq2d
        eqid ad2antrl exp32 ralrimiv jctil adantllr exp31 com23 mpd cvgcmp3ce )
        EKLZEUCMNZOZFPLZFAUDZQNYIUCUERGSUFSEYIGSUFSUGRMNUIAPUHZOZOZYHPKHVBZTUAU
        DZHSZQNZUAPUHZOZUEHUJREUCEUKRZULRZUMNZOZFGSZUFSZEFUNRZULRZKLZTUUFQNZOZP
        VCGVBZFYNMNZYNGSUFSZUUFYOUGRZQNZUIZUAPUHZOZOZOZUEGUJRBUDZUMNBUOYGYHYJUP
        YLTEMNZUUSYEYKUVAYFAEFGIUQURYGYKUVAUUSUIYGUVAYKUUSYGUVAYKUUSYGUVAOZYKOU
        UBUURUVBUUBYKYGUVATEQNZUUBYEUVAUVCYFYEUVAUVCTKLYEUVAUVCUIUSTEUTVAVDURYG
        UVCOZYRUUAYEUVCYRYFYEUVCOZYMYQYEYMUVCYEECUDZUNRZKLZCPUHYMYEUVHCPUVFPLYE
        UVFVELUVHUVFVFEUVFVGVHVICDPKUVGHJVJVKVLUVEYPUAPUVEYNPLZOTEYNUNRZYOQYEUV
        IUVCTUVJQNZYEUVIUVCUVKUVIYEYNVELUVCUVKYNVFEYNVMVNVOVPUVIYOUVJVQUVECDYNU
        VGUVJPHUVFYNEUNVRJEYNUNVSWEZVTWAVIWBURUVDEVCLZEUFSZUCMNUUAYEUVMYFUVCEWC
        ZWDUVDUVNEUCMYEUVCUVNEVQYFEWFURYEYFUVCWGWHDECHJWIWJWBWKVLYEUVAYKUURYFYE
        UVAOZYKOZUUIUUQUVPYHUUIYJYEYHUVAUUIYEYHUVAUUIYEYHUVAWLZUUGUUHUVRUUDKLZU
        UEKLZUUETWOZUUGYHYEUVSUVAYHUUCVCLZUVSPVCFGIWMZUUCWNWPWQZYEYHUVTUVAYHYEF
        VELZUVTFVFZEFVGVHWRZUVRUWAETWOZYEUVAUWHYHEWSWTYEYHUWAUWHXEZUVAYEUVMYHUW
        IUVOEFXAXFWRXBUUDUUEXCXDUVRUVSTUUDQNZUVTTUUEMNZUUHUWDYHYEUWJUVAYHUWBUWJ
        UWCUUCXGWPWQUWGYHYEUWEUVAUWKUWFEFXHVNUUDUUEXIXJWBVOVPXKUVQUUPUUJUVQUUOU
        APUVQUVIUUKUUNUVQUVIUUKOZOUULUUFUVJUGRZUUMQUVQUWLUULUWMQNABUBEFYNGUUTPL
        UBUDUUTGSUFSVQOBUBXLZIUWNXOXMVDUVIUUMUWMVQUVQUUKUVIYOUVJUUFUGUVLXNXPWAX
        QXRIXSWBXTWBYAYBVDYCUABYTFUUFHGEYSULVSYDWJ $.
        $( [25-Jun-2006] $) $( [26-Apr-2005] $)
    $}

    $( Ratio test for convergence of a complex infinite series.  If the ratio
       ` A ` of the absolute values of of successive terms in an infinite
       sequence ` F ` is less than 1 for all terms beyond some index ` B ` ,
       then the infinite sum of the terms of ` F ` converges to a complex
       number.  Equivalent to first part of Exercise 4 of [Gleason] p. 182. $)
    cvgrati $p |- ( ( ( A e. RR /\ A < 1 ) /\ ( B e. NN /\ A. x e. NN (
  B <_ x -> ( abs ` ( F ` ( x + 1 ) ) ) < ( A x. ( abs ` ( F ` x ) ) ) ) ) ) ->
                 E. y ( + seq1 F ) ~~> y ) $=
      ( vz vw cv cn wcel cexp co wceq wa copab eqid cvgratlem5 ) ABGHCDEGIZJKHI
      CSLMNOGHPZFTQR $.
      $( [25-Jun-2006] $) $( [26-Apr-2005] $)
  $}


