$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordering on reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( 'Less than or equal to' expressed in terms of 'less than'. $)
  lenltt $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B <-> -. B < A ) ) $=
    ( cxr wcel cle wbr clt wn wb cr xrlenltt rexrt syl2an ) ACDBCDABEFBAGFHIAJD
    BJDABKALBLM $.
    $( [15-Oct-2005] $) $( [13-May-1999] $)

  $( 'Less than' expressed in terms of 'less than or equal to'. $)
  ltnlet $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> -. B <_ A ) ) $=
    ( cr wcel wa cle wbr clt wn wb lenltt ancoms con2bid ) ACDZBCDZEBAFGZABHGZO
    NPQIJBAKLM $.
    $( [12-Jul-2005] $) $( [11-Jul-2005] $)

  ${
    $d x y z $.
    $( 'Less than' is a strict ordering.  Note: do not shorten this with
       ~ ltsor , and do not use ~ ltsor in complex number proofs, in order
       to maintain a portable derivation of all complex number proofs directly
       from postulates. $)
    ltso $p |- < Or RR $=
      ( vx vy vz cr clt cv wcel w3a wbr wceq wo wn wb wa wi axlttri 3adant3
      axlttrn jca so ) ABCDEAFZDGZBFZDGZCFZDGZHUAUCEIZUAUCJUCUAEIKLMZUGUCUEEINU
      AUEEIOUBUDUHUFUAUCPQUAUCUERST $.
      $( [19-Jan-1997] $)
  $}

  $( Consequence of trichotomy. $)
  lttri2t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( -. A = B <-> ( A < B \/ B < A ) ) ) $=
    ( cr wcel wa clt wbr wo wceq wn wor wb ltso sotrieq mpan bicomd con1bid )
    ACDBCDEZABFGBAFGHZABIZRTSJZCFKRTUALMCABFNOPQ $.
    $( [9-Oct-1999] $)

  $( Trichotomy law for 'less than'. $)
  lttri3t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A = B <-> ( -. A < B /\ -. B < A ) ) ) $=
    ( cr clt wor wcel wa wceq wbr wn wb ltso sotrieq2 mpan ) CDEACFBCFGABHABDIJ
    BADIJGKLCABDMN $.
    $( [5-May-1999] $)

  $( 'Less than' implies not equal. $)
  ltnet $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B -> -. A = B ) ) $=
    ( cr wcel wa wceq wn clt wbr wo lttri2t orc syl5bir ) ACDBCDEABFGABHIZBAHIZ
    JNABKNOLM $.
    $( [9-Oct-1999] $)

  $( Trichotomy law. $)
  letri3t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A = B <-> ( A <_ B /\ B <_ A ) ) ) $=
    ( cr wcel wa wceq clt wbr wn cle lttri3t ancom syl6bbr lenltt wb ancoms
    anbi12d bitr4d ) ACDZBCDZEZABFZBAGHIZABGHIZEZABJHZBAJHZEUAUBUDUCEUEABKUCUDL
    MUAUFUCUGUDABNTSUGUDOBANPQR $.
    $( [14-May-1999] $)

  $( 'Less than or equal to' expressed in terms of 'less than' or 'equals'. $)
  leloet $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A <_ B <-> ( A < B \/ A = B ) ) ) $=
    ( cr wcel wa cle wbr clt wn wceq wo lenltt wb axlttri ancoms con2bid eqcom
    orbi1i orcom bitr syl5rbbr bitrd ) ACDZBCDZEZABFGBAHGZIZABHGZABJZKZABLUEBAJ
    ZUHKZUGUJUEUFULUDUCUFULIMBANOPULUIUHKUJUKUIUHBAQRUIUHSTUAUB $.
    $( [13-May-1999] $)

  $( Equality in terms of 'less than or equal to', 'less than'. $)
  eqleltt $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A = B <-> ( A <_ B /\ -. A < B ) ) ) $=
    ( cr wcel wa wceq cle wbr clt wn letri3t wb lenltt ancoms anbi2d bitrd ) AC
    DZBCDZEZABFABGHZBAGHZETABIHJZEABKSUAUBTRQUAUBLBAMNOP $.
    $( [7-Apr-2001] $)

  $( 'Less than' implies 'less than or equal to'. $)
  ltlet $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A < B -> A <_ B ) ) $=
    ( cr wcel wa cle wbr clt wceq wo leloet orc syl5bir ) ACDBCDEABFGABHGZABIZJ
    NABKNOLM $.
    $( [25-Aug-1999] $)

  $( 'Less than or equal to' implies 'less than' is not 'equals'. $)
  leltnet $p |- ( ( A e. RR /\ B e. RR /\ A <_ B ) ->
                ( A < B <-> -. A = B ) ) $=
    ( cr wcel cle wbr clt wceq wn wb wa wi lttri3t pm3.26 syl6bi adantr wo
    leloet biimpa ord impbid con2bid 3impa ) ACDZBCDZABEFZABGFZABHZIJUDUEKZUFKZ
    UHUGUJUHUGIZUIUHUKLUFUIUHUKBAGFIZKUKABMUKULNOPUJUGUHUIUFUGUHQABRSTUAUBUC $.
    $( [13-Sep-2005] $) $( [27-Jul-1999] $)

  $( 'Less than' expressed in terms of 'less than or equal to'. $)
  ltlent $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A < B <-> ( A <_ B /\ -. A = B ) ) ) $=
    ( cr wcel wa clt wbr cle wceq wn ltlet ltnet jcad wo wi leloet ax-1 pm2.24
    jaoi syl6bi imp3a impbid ) ACDBCDEZABFGZABHGZABIZJZEUCUDUEUGABKABLMUCUEUGUD
    UCUEUDUFNUGUDOZABPUDUHUFUDUGQUFUDRSTUAUB $.
    $( [27-Oct-1999] $)

  $( Transitive law. $)
  lelttrt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A <_ B /\ B < C ) -> A < C ) ) $=
    ( cr wcel w3a cle wbr clt wceq wo wi wb leloet 3adant3 axlttrn exp3a breq1
    biimprd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACIHZUGUHABIHZABJZK
    ZUIUJLZUDUEUHUMMUFABNOUGUKUNULUGUKUIUJABCPQULUNLUGULUJUIABCIRSTUAUBUC $.
    $( [23-May-1999] $)

  $( Transitive law. $)
  ltletrt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A < B /\ B <_ C ) -> A < C ) ) $=
    ( cr wcel w3a clt wbr cle wceq wo wi wb leloet 3adant1 axlttrn exp3a com23
    breq2 biimpd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACGHZUHUJUIUKU
    HUJBCGHZBCJZKZUIUKLZUFUGUJUNMUEBCNOUHULUOUMUHUIULUKUHUIULUKABCPQRUMUOLUHUMU
    IUKBCAGSTUAUBUCRUD $.
    $( [25-Aug-1999] $)

  $( Transitive law. $)
  letrt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A <_ B /\ B <_ C ) -> A <_ C ) ) $=
    ( cr wcel w3a cle wbr wi wa clt wceq wo wb leloet 3adant1 adantr lelttrt
    ltlet 3adant2 syld exp3a imp breq2 biimpcd adantl jaod sylbid ex imp3a ) AD
    EZBDEZCDEZFZABGHZBCGHZACGHZUNUOUPUQIUNUOJZUPBCKHZBCLZMZUQUNUPVANZUOULUMVBUK
    BCOPQURUSUQUTUNUOUSUQIUNUOUSUQUNUOUSJACKHZUQABCRUKUMVCUQIULACSTUAUBUCUOUTUQ
    IUNUTUOUQBCAGUDUEUFUGUHUIUJ $.
    $( [12-Nov-1999] $)

  ${
    letrd.1 $e |- ( ph -> A e. RR ) $.
    letrd.2 $e |- ( ph -> B e. RR ) $.
    letrd.3 $e |- ( ph -> C e. RR ) $.
    ${
      letrd.4 $e |- ( ph -> A <_ B ) $.
      letrd.5 $e |- ( ph -> B <_ C ) $.
      $( Transitive law deduction for 'less than or equal to'. $)
      letrd $p |- ( ph -> A <_ C ) $=
        ( cle wbr cr wcel wa wi letrt syl3anc mp2and ) ABCJKZCDJKZBDJKZHIBLMCLM
        DLMSTNUAOABCDPEFGQR $.
        $( [22-May-2005] $) $( [20-May-2005] $)
    $}

    ${
      lelttrd.4 $e |- ( ph -> A <_ B ) $.
      lelttrd.5 $e |- ( ph -> B < C ) $.
      $( Transitive law deduction for 'less than or equal to', 'less than'. $)
      lelttrd $p |- ( ph -> A < C ) $=
        ( cle wbr clt cr wcel wa wi lelttrt syl3anc mp2and ) ABCJKZCDLKZBDLKZHI
        BMNCMNDMNTUAOUBPABCDQEFGRS $.
        $( [23-Jan-2006] $) $( [8-Jan-2006] $)
    $}

    ${
      ltletrd.4 $e |- ( ph -> A < B ) $.
      ltletrd.5 $e |- ( ph -> B <_ C ) $.
      $( Transitive law deduction for 'less than', 'less than or equal to'. $)
      ltletrd $p |- ( ph -> A < C ) $=
        ( clt wbr cle cr wcel wa wi ltletrt syl3anc mp2and ) ABCJKZCDLKZBDJKZHI
        BMNCMNDMNTUAOUBPABCDQEFGRS $.
        $( [11-Jan-2006] $) $( [9-Jan-2006] $)
    $}

    ${
      lttrd.4 $e |- ( ph -> A < B ) $.
      lttrd.5 $e |- ( ph -> B < C ) $.
      $( Transitive law deduction for 'less than'. $)
      lttrd $p |- ( ph -> A < C ) $=
        ( clt wbr cr wcel wa wi axlttrn syl3anc mp2and ) ABCJKZCDJKZBDJKZHIBLMC
        LMDLMSTNUAOABCDPEFGQR $.
        $( [1-Feb-2006] $) $( [9-Jan-2006] $)
    $}
  $}

  $( 'Less than' is irreflexive. $)
  ltnrt $p |- ( A e. RR -> -. A < A ) $=
    ( cr clt wor wcel wbr wn ltso sonr mpan ) BCDABEAACFGHBACIJ $.
    $( [18-Aug-1999] $)

  $( 'Less than or equal to' is reflexive. $)
  leidt $p |- ( A e. RR -> A <_ A ) $=
    ( cr wcel cle wbr wa clt wceq wo wn eqid a1i orri leloet mpbiri anidms ) AB
    CZAADEZQQFRAAGEZAAHZISTTSJAKLMAANOP $.
    $( [18-Aug-1999] $)

  $( 'Less than' is not symmetric. $)
  ltnsymt $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B -> -. B < A ) ) $=
    ( cr wcel wa clt wbr wceq wo wn axlttri pm2.46 syl6bi ) ACDBCDEABFGABHZBAFG
    ZIJOJABKNOLM $.
    $( [8-Jan-2002] $)

  $( 'Less than' is antisymmetric and irreflexive. $)
  ltnsym2t $p |- ( ( A e. RR /\ B e. RR ) -> -. ( A < B /\ B < A ) ) $=
    ( cr wcel wa clt wbr wn wi ltnsymt imnan sylib ) ACDBCDEABFGZBAFGZHIMNEHABJ
    MNKL $.
    $( [15-Aug-2005] $) $( [13-Aug-2005] $)

