$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordering on reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Alias for ~ axlttrn , for naming consistency with ~ lttri . $)
  lttr $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A < B /\ B < C ) -> A < C ) ) $=
    ( axlttrn ) ABCD $.
    $( [10-Mar-2008] $) $( [10-Mar-2008] $)

  $( The product of two positive numbers is positive. $)
  mulgt0 $p |- ( ( ( A e. RR /\ 0 < A ) /\
                ( B e. RR /\ 0 < B ) ) -> 0 < ( A x. B ) ) $=
    ( cr wcel cc0 clt wbr cmul co wa axmulgt0 imp an4s ) ACDZBCDZEAFGZEBFGZEABH
    IFGZNOJPQJRABKLM $.
    $( [12-Jun-2009] $) $( [10-Mar-2008] $)

  $( 'Less than or equal to' expressed in terms of 'less than'. $)
  lenlt $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B <-> -. B < A ) ) $=
    ( cxr wcel cle wbr clt wn wb cr xrlenlt rexr syl2an ) ACDBCDABEFBAGFHIAJDBJ
    DABKALBLM $.
    $( [15-Oct-2005] $) $( [13-May-1999] $)

  $( 'Less than' expressed in terms of 'less than or equal to'. $)
  ltnle $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> -. B <_ A ) ) $=
    ( cr wcel wa cle wbr clt wn wb lenlt ancoms con2bid ) ACDZBCDZEBAFGZABHGZON
    PQIJBAKLM $.
    $( [12-Jul-2005] $) $( [11-Jul-2005] $)

  ${
    $d x y z $.
    $( 'Less than' is a strict ordering.  Note: do not shorten this with
       ~ ltsor , and do not use ~ ltsor in complex number proofs, in order to
       maintain a portable derivation of all complex number proofs directly
       from postulates. $)
    ltso $p |- < Or RR $=
      ( vx vy vz cr clt cv wcel w3a wbr wceq wo wn wb wa wi axlttri 3adant3
      axlttrn jca so ) ABCDEAFZDGZBFZDGZCFZDGZHUAUCEIZUAUCJUCUAEIKLMZUGUCUEEINU
      AUEEIOUBUDUHUFUAUCPQUAUCUERST $.
      $( [19-Jan-1997] $)
  $}

  $( Consequence of trichotomy. $)
  lttri2 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A =/= B <-> ( A < B \/ B < A ) ) ) $=
    ( cr wcel wa clt wbr wo wceq wn wor wb ltso sotrieq mpan bicomd necon1abid
    ) ACDBCDEZABFGBAFGHZABRABIZSJZCFKRTUALMCABFNOPQ $.
    $( [22-Aug-2007] $) $( [9-Oct-1999] $)

  $( Trichotomy law for 'less than'. $)
  lttri3 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A = B <-> ( -. A < B /\ -. B < A ) ) ) $=
    ( cr clt wor wcel wa wceq wbr wn wb ltso sotrieq2 mpan ) CDEACFBCFGABHABDIJ
    BADIJGKLCABDMN $.
    $( [5-May-1999] $)

  $( Trichotomy law for 'less than'. $)
  lttri4 $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B \/ A = B \/ B < A ) ) $=
    ( cr wcel wa wceq clt wbr wo w3o wn axlttri biimprd orrd 3orrot df-3or
    bitr2i sylib ) ACDBCDEZABFZBAGHZIZABGHZIZUCTUAJZSUBUCSUCUBKABLMNUETUAUCJUDU
    CTUAOTUAUCPQR $.
    $( [21-Jan-2008] $) $( [20-Sep-2007] $)

  $( 'Less than' implies not equal. $)
  ltne $p |- ( ( A e. RR /\ B e. RR /\ A < B ) -> B =/= A ) $=
    ( cr wcel clt wbr w3a wne wa wo lttri2 orc syl5bir 3impia necomd ) ACDZBCDZ
    ABEFZGABPQRABHZPQISRBAEFZJRABKRTLMNO $.
    $( [6-Aug-2007] $) $( [9-Oct-1999] $)

  $( Trichotomy law. $)
  letri3 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A = B <-> ( A <_ B /\ B <_ A ) ) ) $=
    ( cr wcel wa wceq clt wbr wn cle lttri3 ancom syl6bbr lenlt wb ancoms
    anbi12d bitr4d ) ACDZBCDZEZABFZBAGHIZABGHIZEZABJHZBAJHZEUAUBUDUCEUEABKUCUDL
    MUAUFUCUGUDABNTSUGUDOBANPQR $.
    $( [14-May-1999] $)

  $( 'Less than or equal to' expressed in terms of 'less than' or 'equals'. $)
  leloe $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A <_ B <-> ( A < B \/ A = B ) ) ) $=
    ( cr wcel wa cle wbr clt wn wceq wo lenlt wb axlttri ancoms con2bid eqcom
    orbi1i orcom bitri syl5rbbr bitrd ) ACDZBCDZEZABFGBAHGZIZABHGZABJZKZABLUEBA
    JZUHKZUGUJUEUFULUDUCUFULIMBANOPULUIUHKUJUKUIUHBAQRUIUHSTUAUB $.
    $( [13-May-1999] $)

  $( Equality in terms of 'less than or equal to', 'less than'. $)
  eqlelt $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A = B <-> ( A <_ B /\ -. A < B ) ) ) $=
    ( cr wcel wa wceq cle wbr clt wn letri3 wb lenlt ancoms anbi2d bitrd ) ACDZ
    BCDZEZABFABGHZBAGHZETABIHJZEABKSUAUBTRQUAUBLBAMNOP $.
    $( [7-Apr-2001] $)

  $( 'Less than' implies 'less than or equal to'. $)
  ltle $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A < B -> A <_ B ) ) $=
    ( cr wcel wa cle wbr clt wceq wo leloe orc syl5bir ) ACDBCDEABFGABHGZABIZJN
    ABKNOLM $.
    $( [25-Aug-1999] $)

  $( 'Less than or equal to' implies 'less than' is not 'equals'. $)
  leltne $p |- ( ( A e. RR /\ B e. RR /\ A <_ B ) ->
                ( A < B <-> B =/= A ) ) $=
    ( cr wcel cle wbr clt wne wb wa wceq wn wi lttri3 pm3.26 syl6bi adantr wo
    leloe biimpa ord impbid necon2abid necom syl6bbr 3impa ) ACDZBCDZABEFZABGFZ
    BAHZIUGUHJZUIJZUJABHUKUMUJABUMABKZUJLZULUNUOMUIULUNUOBAGFLZJUOABNUOUPOPQUMU
    JUNULUIUJUNRABSTUAUBUCBAUDUEUF $.
    $( [18-Jul-2007] $) $( [27-Jul-1999] $)

  $( 'Less than' expressed in terms of 'less than or equal to'. $)
  ltlen $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A < B <-> ( A <_ B /\ B =/= A ) ) ) $=
    ( cr wcel wa clt wbr cle wne ltle ltne 3expia jcad wceq wo wi leloe ax-1
    wn pm2.24 eqcoms df-ne syl5ib jaoi syl6bi imp3a impbid ) ACDZBCDZEZABFGZABH
    GZBAIZEUJUKULUMABJUHUIUKUMABKLMUJULUMUKUJULUKABNZOUMUKPZABQUKUOUNUKUMRUNBAN
    ZSZUKUMUQUKPBAUPUKTUABAUBUCUDUEUFUG $.
    $( [7-Aug-2007] $) $( [27-Oct-1999] $)

  $( Transitive law. $)
  lelttr $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A <_ B /\ B < C ) -> A < C ) ) $=
    ( cr wcel w3a cle wbr clt wceq wo wi wb leloe 3adant3 axlttrn exp3a breq1
    biimprd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACIHZUGUHABIHZABJZK
    ZUIUJLZUDUEUHUMMUFABNOUGUKUNULUGUKUIUJABCPQULUNLUGULUJUIABCIRSTUAUBUC $.
    $( [23-May-1999] $)

  $( Transitive law. $)
  ltletr $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A < B /\ B <_ C ) -> A < C ) ) $=
    ( cr wcel w3a clt wbr cle wceq wo wi wb leloe 3adant1 axlttrn exp3a com23
    breq2 biimpd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACGHZUHUJUIUKU
    HUJBCGHZBCJZKZUIUKLZUFUGUJUNMUEBCNOUHULUOUMUHUIULUKUHUIULUKABCPQRUMUOLUHUMU
    IUKBCAGSTUAUBUCRUD $.
    $( [25-Aug-1999] $)

  $( Transitive law. $)
  letr $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A <_ B /\ B <_ C ) -> A <_ C ) ) $=
    ( cr wcel w3a cle wbr wa clt wceq wo wb leloe 3adant1 adantr lelttr wi
    ltle 3adant2 syld expdimp breq2 biimpcd adantl jaod sylbid expimpd ) ADEZBD
    EZCDEZFZABGHZBCGHZACGHZULUMIZUNBCJHZBCKZLZUOULUNUSMZUMUJUKUTUIBCNOPUPUQUOUR
    ULUMUQUOULUMUQIACJHZUOABCQUIUKVAUORUJACSTUAUBUMURUORULURUMUOBCAGUCUDUEUFUGU
    H $.
    $( [12-Nov-1999] $)

  ${
    letrd.1 $e |- ( ph -> A e. RR ) $.
    letrd.2 $e |- ( ph -> B e. RR ) $.
    letrd.3 $e |- ( ph -> C e. RR ) $.
    ${
      letrd.4 $e |- ( ph -> A <_ B ) $.
      letrd.5 $e |- ( ph -> B <_ C ) $.
      $( Transitive law deduction for 'less than or equal to'. $)
      letrd $p |- ( ph -> A <_ C ) $=
        ( cle wbr cr wcel wa wi letr syl3anc mp2and ) ABCJKZCDJKZBDJKZHIBLMCLMD
        LMSTNUAOABCDPEFGQR $.
        $( [22-May-2005] $) $( [20-May-2005] $)
    $}

    ${
      lelttrd.4 $e |- ( ph -> A <_ B ) $.
      lelttrd.5 $e |- ( ph -> B < C ) $.
      $( Transitive law deduction for 'less than or equal to', 'less than'. $)
      lelttrd $p |- ( ph -> A < C ) $=
        ( cle wbr clt cr wcel wa wi lelttr syl3anc mp2and ) ABCJKZCDLKZBDLKZHIB
        MNCMNDMNTUAOUBPABCDQEFGRS $.
        $( [23-Jan-2006] $) $( [8-Jan-2006] $)
    $}

    ${
      ltletrd.4 $e |- ( ph -> A < B ) $.
      ltletrd.5 $e |- ( ph -> B <_ C ) $.
      $( Transitive law deduction for 'less than', 'less than or equal to'. $)
      ltletrd $p |- ( ph -> A < C ) $=
        ( clt wbr cle cr wcel wa wi ltletr syl3anc mp2and ) ABCJKZCDLKZBDJKZHIB
        MNCMNDMNTUAOUBPABCDQEFGRS $.
        $( [11-Jan-2006] $) $( [9-Jan-2006] $)
    $}

    ${
      lttrd.4 $e |- ( ph -> A < B ) $.
      lttrd.5 $e |- ( ph -> B < C ) $.
      $( Transitive law deduction for 'less than'. $)
      lttrd $p |- ( ph -> A < C ) $=
        ( clt wbr cr wcel wa wi axlttrn syl3anc mp2and ) ABCJKZCDJKZBDJKZHIBLMC
        LMDLMSTNUAOABCDPEFGQR $.
        $( [1-Feb-2006] $) $( [9-Jan-2006] $)
    $}
  $}

  $( 'Less than' is irreflexive. $)
  ltnr $p |- ( A e. RR -> -. A < A ) $=
    ( cr clt wor wcel wbr wn ltso sonr mpan ) BCDABEAACFGHBACIJ $.
    $( [18-Aug-1999] $)

  $( 'Less than or equal to' is reflexive. $)
  leid $p |- ( A e. RR -> A <_ A ) $=
    ( cr wcel cle wbr wa clt wceq wo eqid olci leloe mpbiri anidms ) ABCZAADEZO
    OFPAAGEZAAHZIRQAJKAALMN $.
    $( [18-Aug-1999] $)

  $( 'Less than' is not symmetric. $)
  ltnsym $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B -> -. B < A ) ) $=
    ( cr wcel wa clt wbr wceq wo wn axlttri pm2.46 syl6bi ) ACDBCDEABFGABHZBAFG
    ZIJOJABKNOLM $.
    $( [8-Jan-2002] $)

  $( 'Less than' is antisymmetric and irreflexive. $)
  ltnsym2 $p |- ( ( A e. RR /\ B e. RR ) -> -. ( A < B /\ B < A ) ) $=
    ( cr wcel wa clt wbr wn wi ltnsym imnan sylib ) ACDBCDEABFGZBAFGZHIMNEHABJM
    NKL $.
    $( [15-Aug-2005] $) $( [13-Aug-2005] $)

  ${
    pm2.61ltle.1 $e |- ( ( ph /\ A < B ) -> ps ) $.
    pm2.61ltle.2 $e |- ( ( ph /\ B <_ A ) -> ps ) $.
    pm2.61ltle.3 $e |- ( ph -> A e. RR ) $.
    pm2.61ltle.4 $e |- ( ph -> B e. RR ) $.
    $( Ordering elimination by cases. $)
    pm2.61ltlei $p |- ( ph -> ps ) $=
      ( clt wbr wn cle cr wcel wb lenlt sylanc biimpar syldan pm2.61dan ) ACDIJ
      ZBEAUAKZDCLJZBAUCUBDMNCMNUCUBOADCPHGQRFST $.
      $( [4-Jul-2007] $) $( [1-Jul-2007] $)
  $}


