$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordering on reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define 'less than or equal to' on real subset of compex numbers.  Theorem
     ~ leloet relates it to 'less than'. $)
  df-le $a |- <_ = ( ( RR X. RR ) \ `' < ) $.

  $( 'Less than or equal to' expressed in terms of 'less than'. $)
  leltt $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B <-> -. B < A ) ) $=
    ( cr wcel wa cle wbr cop clt ccnv wn cxp wb opelxpi cdif df-le eleq2i
    eldif bitr baib syl df-br syl5bb opelcnvg syl6rbbr binegd bitr4d ) ACDBCDEZ
    ABFGZABHZIJZDZKZBAIGZKUHUJFDZUMUIUHUJCCLZDZUOUMMABCCNUOUQUMUOUJUPUKOZDUQUME
    FURUJPQUJUPUKRSTUAABFUBUCUHUNULUHULBAHIDUNABCCIUDBAIUBUEUFUG $.
    $( [13-May-99] $)

  ${
    $d x y z $. $( ` x y z ` are dummy. $)
    $( 'Less than' is a strict ordering.  Note: do not shorten this with
       ~ ltsor , and do not use ~ ltsor in complex number proofs, in order
       to maintain a "clean" derivation of all complex number proofs directly
       from postulates. $)
    ltso $p |- < Or RR $=
      ( vx vy vz cr clt cv wcel w3a wbr weq wo wn wb wa wi axlttri 3adant3
      axlttrn jca so ) ABCDEAFZDGZBFZDGZCFZDGZHUAUCEIZABJUCUAEIKLMZUGUCUEEINUAU
      EEIOUBUDUHUFUAUCPQUAUCUERST $.
      $( [19-Jan-97] $)
  $}

  $( Consequence of trichotomy. $)
  lttri2t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( -. A = B <-> ( A < B \/ B < A ) ) ) $=
    ( cr wcel wa clt wbr wo wceq wn wor wb ltso sotrieq mpan bicomd bicon1d )
    ACDBCDEZABFGBAFGHZABIZRTSJZCFKRTUALMCABFNOPQ $.
    $( [9-Oct-99] $)

  $( Trichotomy law for 'less than'. $)
  lttri3t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A = B <-> ( -. A < B /\ -. B < A ) ) ) $=
    ( cr clt wor wcel wa wceq wbr wn wb ltso sotrieq2 mpan ) CDEACFBCFGABHABDIJ
    BADIJGKLCABDMN $.
    $( [5-May-99] $)

  $( 'Less than' implies not equal. $)
  ltnet $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B -> -. A = B ) ) $=
    ( cr wcel wa wceq wn clt wbr wo lttri2t orc syl5bir ) ACDBCDEABFGABHIZBAHIZ
    JNABKNOLM $.
    $( [9-Oct-99] $)

  $( Trichotomy law. $)
  letri3t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A = B <-> ( A <_ B /\ B <_ A ) ) ) $=
    ( cr wcel wa wceq clt wbr wn cle lttri3t ancom syl6bbr leltt wb ancoms
    bi2and bitr4d ) ACDZBCDZEZABFZBAGHIZABGHIZEZABJHZBAJHZEUAUBUDUCEUEABKUCUDLM
    UAUFUCUGUDABNTSUGUDOBANPQR $.
    $( [14-May-99] $)

  $( 'Less than or equal to' expressed in terms of 'less than' or 'equals'. $)
  leloet $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A <_ B <-> ( A < B \/ A = B ) ) ) $=
    ( cr wcel wa cle wbr clt wn wceq wo leltt wb axlttri ancoms bicon2d
    cleqcom biror orcom bitr syl5rbbr bitrd ) ACDZBCDZEZABFGBAHGZIZABHGZABJZKZA
    BLUEBAJZUHKZUGUJUEUFULUDUCUFULIMBANOPULUIUHKUJUKUIUHBAQRUIUHSTUAUB $.
    $( [13-May-99] $)
  $( Equality in terms of 'less than or equal to', 'less than'. $)
  lenltt $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A = B <-> ( A <_ B /\ -. A < B ) ) ) $=
    ( cr wcel wa wceq cle wbr clt wn letri3t wb leltt ancoms biland bitrd ) ACD
    ZBCDZEZABFABGHZBAGHZETABIHJZEABKSUAUBTRQUAUBLBAMNOP $.
    $( [7-Apr-01] $)

  $( 'Less than' implies 'less than or equal to'. $)
  ltlet $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A < B -> A <_ B ) ) $=
    ( cr wcel wa cle wbr clt wceq wo leloet orc syl5bir ) ACDBCDEABFGABHGZABIZJ
    NABKNOLM $.
    $( [25-Aug-99] $)

  $( 'Less than or equal to' implies 'less than' is not 'equals'. $)
  leltnet $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A <_ B -> ( A < B <-> -. A = B ) ) ) $=
    ( cr wcel wa cle wbr clt wceq wn wb wi lttri3t pm3.26 syl6bi adantr wo
    leloet biimpa ord impbid bicon2d exp ) ACDBCDEZABFGZABHGZABIZJKUDUEEZUGUFUH
    UGUFJZUDUGUILUEUDUGUIBAHGJZEUIABMUIUJNOPUHUFUGUDUEUFUGQABRSTUAUBUC $.
    $( [27-Jul-99] $)

  $( 'Less than' expressed in terms of 'less than or equal to'. $)
  ltlent $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A < B <-> ( A <_ B /\ -. A = B ) ) ) $=
    ( cr wcel wa clt wbr cle wceq wn ltlet ltnet jcad wo wi leloet ax-1 pm2.21
    com12 jaoi syl6bi imp3a impbid ) ACDBCDEZABFGZABHGZABIZJZEUDUEUFUHABKABLMUD
    UFUHUEUDUFUEUGNUHUEOZABPUEUIUGUEUHQUHUGUEUGUERSTUAUBUC $.
    $( [27-Oct-99] $)

  $( Transitive law. $)
  lelttrt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A <_ B /\ B < C ) -> A < C ) ) $=
    ( cr wcel w3a cle wbr clt wceq wo wi wb leloet 3adant3 axlttrn exp3a breq1
    biimprd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACIHZUGUHABIHZABJZK
    ZUIUJLZUDUEUHUMMUFABNOUGUKUNULUGUKUIUJABCPQULUNLUGULUJUIABCIRSTUAUBUC $.
    $( [23-May-99] $)

  $( Transitive law. $)
  ltletrt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A < B /\ B <_ C ) -> A < C ) ) $=
    ( cr wcel w3a clt wbr cle wceq wo wi wb leloet 3adant1 axlttrn exp3a com23
    breq2 biimpd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACGHZUHUJUIUKU
    HUJBCGHZBCJZKZUIUKLZUFUGUJUNMUEBCNOUHULUOUMUHUIULUKUHUIULUKABCPQRUMUOLUHUMU
    IUKBCAGSTUAUBUCRUD $.
    $( [25-Aug-99] $)

  $( Transitive law. $)
  letrt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A <_ B /\ B <_ C ) -> A <_ C ) ) $=
    ( cr wcel w3a cle wbr wi wa clt wceq wo wb leloet 3adant1 adantr lelttrt
    ltlet 3adant2 syld exp3a imp breq2 biimpcd adantl jaod sylbid exp imp3a )
    ADEZBDEZCDEZFZABGHZBCGHZACGHZUNUOUPUQIUNUOJZUPBCKHZBCLZMZUQUNUPVANZUOULUMVB
    UKBCOPQURUSUQUTUNUOUSUQIUNUOUSUQUNUOUSJACKHZUQABCRUKUMVCUQIULACSTUAUBUCUOUT
    UQIUNUTUOUQBCAGUDUEUFUGUHUIUJ $.
    $( [12-Nov-99] $)

  $( 'Less than' is irreflexive. $)
  ltnrt $p |- ( A e. RR -> -. A < A ) $=
    ( cr clt wor wcel wbr wn ltso sonr mpan ) BCDABEAACFGHBACIJ $.
    $( [18-Aug-99] $)

  $( 'Less than or equal to' is reflexive. $)
  leidt $p |- ( A e. RR -> A <_ A ) $=
    ( cr wcel cle wbr wa clt wceq wo wn cleqid a1i orri leloet mpbiri anidms )
    ABCZAADEZQQFRAAGEZAAHZISTTSJAKLMAANOP $.
    $( [18-Aug-99] $)

  $( 'Less than' is not symmetric. $)
  ltnsymt $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B -> -. B < A ) ) $=
    ( cr wcel wa clt wbr wceq wo wn axlttri olc con3i syl6bi ) ACDBCDEABFGABHZB
    AFGZIZJPJABKPQPOLMN $.
    $( [8-Jan-02] $)

  ${
    lt.1 $e |- A e. RR $.

    ${
      lt.2 $e |- B e. RR $.

      $( Consequence of trichotomy. $)
      lttri2 $p |- ( -. A = B <-> ( A < B \/ B < A ) ) $=
        ( cr wcel wceq wn clt wbr wo wb lttri2t mp2an ) AEFBEFABGHABIJBAIJKLCDA
        BMN $.
        $( [19-Jan-97] $)

      $( Consequence of trichotomy. $)
      lttri3 $p |- ( A = B <-> ( -. A < B /\ -. B < A ) ) $=
        ( cr wcel wceq clt wbr wn wa wb lttri3t mp2an ) AEFBEFABGABHIJBAHIJKLCD
        ABMN $.
        $( [14-May-99] $)

      $( Consequence of trichotomy. $)
      letri3 $p |- ( A = B <-> ( A <_ B /\ B <_ A ) ) $=
        ( cr wcel wceq cle wbr wa wb letri3t mp2an ) AEFBEFABGABHIBAHIJKCDABLM
        $.
        $( [14-May-99] $)

      $( 'Less than or equal to' in terms of 'less than'. $)
      leloe $p |- ( A <_ B <-> ( A < B \/ A = B ) ) $=
        ( cr wcel cle wbr clt wceq wo wb leloet mp2an ) AEFBEFABGHABIHABJKLCDAB
        MN $.
        $( [14-May-99] $)

      $( 'Less than' expressed in terms of 'less than or equal to'. $)
      ltlen $p |- ( A < B <-> ( A <_ B /\ -. A = B ) ) $=
        ( cr wcel clt wbr cle wceq wn wa wb ltlent mp2an ) AEFBEFABGHABIHABJKLM
        CDABNO $.
        $( [27-Oct-99] $)

      $( 'Less than' is not symmetric. $)
      ltnsym $p |- ( A < B -> -. B < A ) $=
        ( cr wcel clt wbr wn wi ltnsymt mp2an ) AEFBEFABGHBAGHIJCDABKL $.
        $( [6-May-99] $)

      $( 'Less than or equal to'  in terms of 'less than'. $)
      lelt $p |- ( A <_ B <-> -. B < A ) $=
        ( cr wcel cle wbr clt wn wb leltt mp2an ) AEFBEFABGHBAIHJKCDABLM $.
        $( [24-May-99] $)

      $( 'Less than' implies 'less than or equal to'. $)
      ltle $p |- ( A < B -> A <_ B ) $=
        ( cr wcel clt wbr cle wi ltlet mp2an ) AEFBEFABGHABIHJCDABKL $.
        $( [14-May-99] $)

      ${
        ltlei.1 $e |- A < B $.
        $( 'Less than' implies 'less than or equal to' (inference). $)
        ltlei $p |- A <_ B $=
          ( clt wbr cle ltle ax-mp ) ABFGABHGEABCDIJ $.
          $( [22-Aug-99] $)
      $}

      $( Equality implies 'less than or equal to'. $)
      eqle $p |- ( A = B -> A <_ B ) $=
        ( wceq cle wbr letri3 pm3.26bd ) ABEABFGBAFGABCDHI $.
        $( [23-May-99] $)

      $( 'Less than' implies not equal. $)
      ltne $p |- ( A < B -> -. A = B ) $=
        ( cr wcel clt wbr wceq wn wi ltnet mp2an ) AEFBEFABGHABIJKCDABLM $.
        $( [28-Jul-99] $)

      $( Trichotomy law for 'less than or equal to'. $)
      letri $p |- ( A <_ B \/ B <_ A ) $=
        ( cle wbr wn clt lelt bicon2 ltle sylbir orri ) ABEFZBAEFZNGBAHFZONPABC
        DIJBADCKLM $.
        $( [2-Aug-99] $)

      ${
        lt.3 $e |- C e. RR $.
        $( 'Less than' is transitive. $)
        lttr $p |- ( ( A < B /\ B < C ) -> A < C ) $=
          ( cr wcel clt wbr wa wi axlttrn mp3an ) AGHBGHCGHABIJBCIJKACIJLDEFABC
          MN $.
          $( [14-May-99] $)

        $( 'Less than or equal to', 'less than' transitive law. $)
        lelttr $p |- ( ( A <_ B /\ B < C ) -> A < C ) $=
          ( cr wcel cle wbr clt wa wi lelttrt mp3an ) AGHBGHCGHABIJBCKJLACKJMDE
          FABCNO $.
          $( [14-May-99] $)

        $( 'Less than', 'less than or equal to' transitive law. $)
        ltletr $p |- ( ( A < B /\ B <_ C ) -> A < C ) $=
          ( cr wcel clt wbr cle wa wi ltletrt mp3an ) AGHBGHCGHABIJBCKJLACIJMDE
          FABCNO $.
          $( [14-May-99] $)

        $( 'Less than or equal to' is transitive. $)
        letr $p |- ( ( A <_ B /\ B <_ C ) -> A <_ C ) $=
          ( cr wcel cle wbr wa wi letrt mp3an ) AGHBGHCGHABIJBCIJKACIJLDEFABCMN
          $.
          $( [14-May-99] $)

        $( Exended trichotomy law for 'less than or equal to'. $)
        le2tri3 $p |- ( ( A <_ B /\ B <_ C /\ C <_ A ) <->
                        ( A = B /\ B = C /\ C = A ) ) $=
          ( cle wbr w3a wceq wa letri3 biimpr letr sylan5 3impb 3comr cleqcomd
          sylan 3impa 3jca eqle im3an impbi ) ABGHZBCGHZCAGHZIZABJZBCJZCAJZIUHU
          IUJUKUEUFUGUIUEBAGHZUIUFUGKUIUEULKABDELMBCAEFDNOPUFUGUEUJUFUGUEUJUFCB
          GHZUJUGUEKUJUFUMKBCEFLMCABFDENOPQUEUFUGUKACGHZUGUKUEUFKUNUGKZACACJUOA
          CDFLMRABCDEFNSTUAUIUEUJUFUKUGABDEUBBCEFUBCAFDUBUCUD $.
          $( [14-Aug-00] $)

        $( Addition to both sides of an inequality. $)
        ltadd2 $p |- ( A < B <-> ( C + A ) < ( C + B ) ) $=
          ( clt wbr caddc co cr wcel wi axltadd mp3an cneg addrcl negrcl cc0
          recn addcom negid eqtr opreq1i addass addid2 3eqtr3 3brtr3g impbi )
          ABGHZCAIJZCBIJZGHZAKLBKLCKLUJUMMDEFABCNOUMCPZUKIJZUNULIJZABGUKKLULKLU
          NKLUMUOUPGHMCAFDQCBFEQCFRZUKULUNNOUNCIJZAIJSAIJUOAURSAIURCUNIJSUNCUNU
          QTZCFTZUACUTUBUCZUDUNCAUSUTADTZUEAVBUFUGURBIJSBIJUPBURSBIVAUDUNCBUSUT
          BETZUEBVCUFUGUHUI $.
          $( [21-Jan-97] $)

        $( Addition to both sides of an inequality.  Theorem I.18 of [Apostol]
           p. 20. $)
        ltadd1 $p |- ( A < B <-> ( A + C ) < ( B + C ) ) $=
          ( clt wbr caddc co ltadd2 recn addcom breq12i bitr ) ABGHCAIJZCBIJZGH
          ACIJZBCIJZGHABCDEFKPRQSGCACFLZADLMCBTBELMNO $.
          $( [21-Jan-97] $)

        $( Addition to both sides of an inequality. $)
        leadd1 $p |- ( A <_ B <-> ( A + C ) <_ ( B + C ) ) $=
          ( clt wbr wn caddc co cle ltadd1 bineg lelt addrcl 3bitr4 ) BAGHZIBCJ
          KZACJKZGHZIABLHTSLHRUABACEDFMNABDEOTSACDFPBCEFPOQ $.
          $( [11-Aug-99] $)

        $( Addition to both sides of an inequality. $)
        leadd2 $p |- ( A <_ B <-> ( C + A ) <_ ( C + B ) ) $=
          ( cle wbr caddc co leadd1 recn addcom breq12i bitr ) ABGHACIJZBCIJZGH
          CAIJZCBIJZGHABCDEFKPRQSGACADLCFLZMBCBELTMNO $.
          $( [11-Aug-99] $)

        $( Inequality relation between subtraction and addition. $)
        ltsubadd $p |- ( ( A - B ) < C <-> A < ( C + B ) ) $=
          ( cmin co clt wbr cneg caddc recn subneg breq1i negrcl addrcl ltadd1
          cc0 addass addcom negid eqtr opreq2i addid1 3eqtr 3bitr ) ABGHZCIJABK
          ZLHZCIJUJBLHZCBLHZIJAULIJUHUJCIABADMZBEMZNOUJCBAUIDBEPZQFERUKAULIUKAU
          IBLHZLHASLHAAUIBUMUIUOMZUNTUPSALUPBUILHSUIBUQUNUABUNUBUCUDAUMUEUFOUG
          $.
          $( [21-Jan-97] $)

        $( Inequality relation between subtraction and addition. $)
        ltsubadd2 $p |- ( ( A - B ) < C <-> A < ( B + C ) ) $=
          ( cmin co clt wbr caddc ltsubadd recn addcom breq2i bitr ) ABGHCIJACB
          KHZIJABCKHZIJABCDEFLQRAICBCFMBEMNOP $.
          $( [21-Jan-97] $)

        $( Inequality relation between subtraction and addition. $)
        lesubadd2 $p |- ( ( A - B ) <_ C <-> A <_ ( B + C ) ) $=
          ( cmin co clt wbr wceq wo caddc cle ltsubadd2 recn subadd cleqcom
          bitr bi2or subrcl leloe addrcl 3bitr4 ) ABGHZCIJZUECKZLABCMHZIJZAUHKZ
          LUECNJAUHNJUFUIUGUJABCDEFOUGUHAKUJABCADPBEPCFPQUHARSTUECABDEUAFUBAUHD
          BCEFUCUBUD $.
          $( [3-Aug-99] $)

        $( Inequality relation between subtraction and addition. $)
        lesubadd $p |- ( ( A - B ) <_ C <-> A <_ ( C + B ) ) $=
          ( cmin co cle wbr caddc lesubadd2 recn addcom breq2i bitr ) ABGHCIJAB
          CKHZIJACBKHZIJABCDEFLQRAIBCBEMCFMNOP $.
          $( [30-Sep-99] $)

        $( Inequality relation between subtraction and addition. $)
        ltaddsub $p |- ( ( A + B ) < C <-> A < ( C - B ) ) $=
          ( cneg caddc co clt wbr cmin cc0 recn negid opreq2i addid1 eqtr2
          negrcl addass eqtr4 breq1i subneg breq2i addrcl ltadd1 3bitr4r ) ACBG
          ZHIZJKABHIZUHHIZUIJKACBLIZJKUJCJKAUKUIJAABUHHIZHIZUKUNAMHIAUMMAHBBENZ
          OPAADNZQRABUHUPUOUHBESZNTUAUBULUIAJCBCFNUOUCUDUJCUHABDEUEFUQUFUG $.
          $( [14-May-99] $)

        ${
          lt.4 $e |- D e. RR $.
          $( Adding both side of two inequalities.  Theorem I.25 of [Apostol]
             p. 20. $)
          lt2add $p |- ( ( A < C /\ B < D ) -> ( A + B ) < ( C + D ) ) $=
            ( caddc co clt wbr addrcl lttr ltadd1 ltadd2 syl2anb ) ABIJZCBIJZKL
            SCDIJZKLRTKLACKLBDKLRSTABEFMCBGFMCDGHMNACBEGFOBDCFHGPQ $.
            $( [14-May-99] $)

          $( Adding both side of two inequalities. $)
          le2add $p |- ( ( A <_ C /\ B <_ D ) -> ( A + B ) <_ ( C + D ) ) $=
            ( caddc co cle wbr addrcl letr leadd1 leadd2 syl2anb ) ABIJZCBIJZKL
            SCDIJZKLRTKLACKLBDKLRSTABEFMCBGFMCDGHMNACBEGFOBDCFHGPQ $.
            $( [16-Sep-99] $)
        $}
      $}

      $( Addition of 2 positive numbers is positive. $)
      addgt0 $p |- ( ( 0 < A /\ 0 < B ) -> 0 < ( A + B ) ) $=
        ( cc0 clt wbr caddc co ax0re addrcl lttr ltadd2 recn addid1 breq1i
        bitr sylan5b ) EAFGAABHIZFGZESFGEBFGZEASJCABCDKLUAAEHIZSFGTEBAJDCMUBASF
        AACNOPQR $.
        $( [16-May-99] $)

      $( Addition of 2 nonnegative numbers is nonnegative. $)
      addge0 $p |- ( ( 0 <_ A /\ 0 <_ B ) -> 0 <_ ( A + B ) ) $=
        ( cc0 clt wbr wceq wo caddc co cle wa addgt0 ax0re addrcl ltle syl
        opreq1 recn addid2 syl5eqr breq2d biimpa opreq2 addid1 biimpac opreq12
        0cn eqle ccase leloe syl2anb ) EAFGZEAHZIEBFGZEBHZIEABJKZLGZEALGEBLGUNU
        PUOUQUSUNUPMEURFGZUSABCDNEUROABCDPZQZRUOUPMUTUSUOUPUTUOBUREFUOEBJKURBEA
        BJSBBDTUAUBUCUDVBRUNUQMUTUSUQUNUTUQAUREFUQAEJKURAEBAJUEAACTUFUBUCUGVBRU
        OUQMZEURHUSVCEEJKUREEAEBJUHEUIUFUBEUROVAUJRUKEAOCULEBODULUM $.
        $( [28-May-99] $)

      $( Addition of nonnegative and positive numbers is positive. $)
      addgegt0 $p |- ( ( 0 <_ A /\ 0 < B ) -> 0 < ( A + B ) ) $=
        ( cc0 cle wbr clt caddc co wceq wo wi ax0re leloe addgt0 exp opreq1
        recn addid2 syl5eqr breq2d biimpd jaoi sylbi imp ) EAFGZEBHGZEABIJZHGZU
        GEAHGZEAKZLUHUJMZEANCOUKUMULUKUHUJABCDPQULUHUJULBUIEHULEBIJUIBEABIRBBDS
        TUAUBUCUDUEUF $.
        $( [25-Sep-99] $)

      ${
        addgt0i.3 $e |- 0 < A $.
        addgt0i.4 $e |- 0 < B $.
        $( Addition of 2 positive numbers is positive. $)
        addgt0i $p |- 0 < ( A + B ) $=
          ( cc0 clt wbr caddc co addgt0 mp2an ) GAHIGBHIGABJKHIEFABCDLM $.
          $( [18-May-99] $)
      $}

      $( Adding a positive number to another number increases it. $)
      ltaddpos $p |- ( 0 < A <-> B < ( B + A ) ) $=
        ( cc0 clt wbr caddc co ax0re ltadd2 recn addid1 breq1i bitr ) EAFGBEHIZ
        BAHIZFGBQFGEABJCDKPBQFBBDLMNO $.
        $( [25-Aug-99] $)

      $( Comparison of two numbers whose difference is positive. $)
      posdif $p |- ( 0 < ( A - B ) <-> B < A ) $=
        ( cc0 cmin co clt wbr caddc subrcl ltaddpos recn subaddeq breq2i bitr
        ) EABFGZHIBBQJGZHIBAHIQBABCDKDLRABHBABDMACMNOP $.
        $( [19-Aug-01] $)

      $( Two nonnegative numbers are zero iff their sum is zero. $)
      add20 $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
        ( ( A + B ) = 0 <-> ( A = 0 /\ B = 0 ) ) ) $=
        ( cc0 cle wbr wa caddc co wceq clt wo wi wn addrcl ax0re lttri3
        pm3.27bd addgt0 nsyl3 pm2.21d cleqcom biimp a1d opreq1 recn addid2
        syl5eqr cleq1d biimprd jcad opreq2 addid1 ccase2 leloe syl2anb 0cn
        syl6eq sylan9eq a1i impbid ) EAFGZEBFGZHZABIJZEKZAEKZBEKZHZEALGZEAKZMEB
        LGZEBKZMVGVJNZVCVDVKVMVLVNVOVKVMHZVGVJVGEVFLGZVPVGVFELGOVQOVFEABCDPQRSA
        BCDTUAUBVLVGVHVIVLVHVGVLVHEAUCUDUEVLVIVGVLBVFEVLEBIJZVFBEABIUFBBDUGUHUI
        UJUKULVNVGVHVIVNVHVGVNAVFEVNAEIJVFAEBAIUMAACUGUNUIUJUKVNVIVGVNVIEBUCUDU
        EULUOEAQCUPEBQDUPUQVJVGNVEVHVIVFVREAEBIUFVIVREEIJEBEEIUMEURUNUSUTVAVB
        $.
        $( [28-Jul-99] $)

      $( Negative of both sides of an inequality.  Theorem I.23 of [Apostol]
         p. 20. $)
      ltneg $p |- ( A < B <-> -u B < -u A ) $=
        ( clt wbr cneg caddc co negrcl addrcl ltadd1 cc0 recn negid opreq1i
        addass addid2 3eqtr3 add12 opreq2i addid1 3eqtr breq12i bitr ) ABEFAAGZ
        BGZHIZHIZBUHHIZEFUGUFEFABUHCDUFUGACJZBDJZKLUIUGUJUFEAUFHIZUGHIMUGHIUIUG
        UMMUGHAACNZOPAUFUGUNUFUKNZUGULNZQUGUPRSUJUFBUGHIZHIUFMHIUFBUFUGBDNZUOUP
        TUQMUFHBUROUAUFUOUBUCUDUE $.
        $( [21-Jan-97] $)

      $( Negative of both sides of an inequality. $)
      leneg $p |- ( A <_ B <-> -u B <_ -u A ) $=
        ( clt wbr wceq wo cneg cle ltneg recn neg11 cleqcom bitr2 bi2or leloe
        negrcl 3bitr4 ) ABEFZABGZHBIZAIZEFZUBUCGZHABJFUBUCJFTUDUAUEABCDKUEBAGUA
        BABDLACLMBANOPABCDQUBUCBDRACRQS $.
        $( [1-Aug-99] $)

      $( Contraposition of negative in an inequality. $)
      ltnegcon1 $p |- ( -u A < B <-> -u B < A ) $=
        ( cneg clt wbr negrcl ltneg recn negneg breq2i bitr ) AEZBFGBEZNEZFGOAF
        GNBACHDIPAOFAACJKLM $.
        $( [14-May-99] $)

      $( Contraposition of negative in an inequality. $)
      ltnegcon2 $p |- ( A < -u B <-> B < -u A ) $=
        ( cneg clt wbr negrcl ltneg recn negneg breq1i bitr ) ABEZFGNEZAEZFGBPF
        GANCBDHIOBPFBBDJKLM $.
        $( [14-May-99] $)

      $( The product of two positive numbers is positive. $)
      mulgt0 $p |- ( ( 0 < A /\ 0 < B ) -> 0 < ( A x. B ) ) $=
        ( cr wcel cc0 clt wbr wa cmulc co wi axmulgt0 mp2an ) AEFBEFGAHIGBHIJGA
        BKLHIMCDABNO $.
        $( [16-May-99] $)

      $( The product of two nonnegative numbers is nonnegative. $)
      mulge0 $p |- ( ( 0 <_ A /\ 0 <_ B ) -> 0 <_ ( A x. B ) ) $=
        ( cc0 clt wbr wceq wo cmulc co cle wa mulgt0 ax0re mulrcl ltle syl
        opreq1 recn mulzer2 syl5eqr eqle opreq2 mulzer1 ccase2 leloe syl2anb )
        EAFGZEAHZIEBFGZEBHZIEABJKZLGZEALGEBLGUIUKUJULUNUIUKMEUMFGUNABCDNEUMOABC
        DPZQRUJEUMHZUNUJEBJKUMEEABJSBBDTUAUBEUMOUOUCZRULUPUNULAEJKUMEEBAJUDAACT
        UEUBUQRUFEAOCUGEBODUGUH $.
        $( [30-Jul-99] $)

      ${
        mulgt0i.3 $e |- 0 < A $.
        mulgt0i.4 $e |- 0 < B $.
        $( The product of two positive numbers is positive. $)
        mulgt0i $p |- 0 < ( A x. B ) $=
          ( cc0 clt wbr cmulc co mulgt0 mp2an ) GAHIGBHIGABJKHIEFABCDLM $.
          $( [18-May-99] $)
      $}

      ${
        ltmullem.1 $e |- C e. RR $.
        $( Multiplication of both sides of an inequality by a positive
           number.  Theorem I.19 of [Apostol] p. 20. $)
        ltmullem $p |- ( 0 < C -> ( A < B -> ( A x. C ) < ( B x. C ) ) ) $=
          ( cc0 clt wbr cmin co cmulc subrcl mulgt0 exp com12 caddc recn
          addid2 breq1i ax0re ltaddsub bitr3 mulrcl subdistr breq2i bitr4
          3imtr4g ) GCHIZGBAJKZHIZGUJCLKZHIZABHIZACLKZBCLKZHIZUKUIUMUKUIUMUJCBA
          EDMFNOPUNGAQKZBHIUKURABHAADRZSTGABUADEUBUCUQGUOQKZUPHIZUMUTUOUPHUOUOA
          CDFUDZRSTVAGUPUOJKZHIUMGUOUPUAVBBCEFUDUBULVCGHBACBERUSCFRUEUFUGUCUH
          $.
          $( [15-May-99] $)
      $}
    $}

    $( 'Less than' is irreflexive. $)
    ltnr $p |- -. A < A $=
      ( cr wcel clt wbr wn ltnrt ax-mp ) ACDAAEFGBAHI $.
      $( [18-Aug-99] $)

    $( 'Less than or equal to' is reflexive. $)
    leid $p |- A <_ A $=
      ( cr wcel cle wbr leidt ax-mp ) ACDAAEFBAGH $.
      $( [18-Aug-99] $)

    $( Positive means non-zero (useful for ordering theorems involving
       division). $)
    gt0ne0 $p |- ( 0 < A -> A =/= 0 ) $=
      ( cc0 clt wbr wne wceq wn ax0re ltne df-ne sylibr necom ) CADEZCAFZACFNCA
      GHOCAIBJCAKLACML $.
      $( [16-Sep-99] $)

    ${
      lesub0.1 $e |- B e. RR $.
      $( Lemma to show a nonnegative number is zero. $)
      lesub0 $p |- ( ( 0 <_ A /\ B <_ ( B - A ) ) <-> A = 0 ) $=
        ( cmin co cle wbr cc0 wa wceq caddc cneg recn addid1 cleqcomi subneg
        breq12i ax0re negrcl leadd2 leneg neg0 breq1i bitr2 bitr3 bitr biran
        ancom letri3 3bitr4 ) BBAEFZGHZIAGHZJAIGHZUNJUNUMJAIKUMUOUNUMBILFZBAMZL
        FZGHZUOBUPULURGUPBBBDNZOPBAUTACNQRUSIUQGHZUOIUQBSACTDUAUOIMZUQGHVAAICSU
        BVBIUQGUCUDUEUFUGUHUNUMUIAICSUJUK $.
        $( [8-Oct-99] $)

      $( Nonnegative subtraction. $)
      subge0 $p |- ( 0 <_ ( A - B ) <-> B <_ A ) $=
        ( cle wbr cneg caddc co cc0 cmin negrcl leadd1 recn negid subneg
        cleqcomi breq12i bitr2 ) BAEFBBGZHIZATHIZEFJABKIZEFBATDCBDLMUAJUBUCEBBD
        NZOUCUBABACNUDPQRS $.
        $( [13-Aug-00] $)
    $}


    $( A non-zero square is positive.  Theorem I.20 of [Apostol] p. 20. $)
    sqgt0 $p |- ( -. A = 0 -> 0 < ( A x. A ) ) $=
      ( cc0 wceq wn clt wbr wo cmulc co ax0re lttri2 cneg ltneg neg0 breq1i
      bitr negrcl mulgt0 anidms recn mul2neg syl6breq sylbi jaoi ) ACDEACFGZCAF
      GZHCAAIJZFGZACBKLUFUIUGUFCAMZFGZUIUFCMZUJFGUKACBKNULCUJFOPQUKCUJUJIJZUHFU
      KCUMFGUJUJABRZUNSTAAABUAZUOUBUCUDUGUIAABBSTUEUD $.
      $( [17-Jan-97] $)

    $( A square is nonnegative. $)
    sqge0 $p |- 0 <_ ( A x. A ) $=
      ( cc0 wceq cmulc co cle wbr clt wo opreq2 recn mulzer1 syl6req olc syl
      ax0re mulrcl leloe sylibr wn sqgt0 ltle pm2.61i ) ACDZCAAEFZGHZUECUFIHZCU
      FDZJZUGUEUIUJUEUFACEFCACAEKAABLMNUIUHOPCUFQAABBRZSTUEUAUHUGABUBCUFQUKUCPU
      D $.
      $( [14-May-99] $)
  $}

  ${
    gt0ne0i.1 $e |- A e. RR $.
    gt0ne0i.2 $e |- 0 < A $.
    $( Positive implies nonzero. $)
    gt0ne0i $p |- A =/= 0 $=
      ( cc0 clt wbr wne gt0ne0 ax-mp ) DAEFADGCABHI $.
      $( [15-May-99] $)
  $}

  $( Positive implies nonzero. $)
  gt0ne0t $p |- ( A e. RR -> ( 0 < A -> A =/= 0 ) ) $=
    ( cr wcel cc0 clt wbr wne wi cded wceq breq2 neeq1 bi2imd ax0re elimel
    gt0ne0 dedth ) ABCZDAEFZADGZHDRADIZEFZUADGZHADAUAJSUBTUCAUADEKAUADLMUAADBNO
    PQ $.
    $( [3-Oct-99] $)

  $( Trichotomy law. $)
  letrit $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B \/ B <_ A ) ) $=
    ( cr wcel cle wbr wo cc0 cded wceq breq1 breq2 bi2ord ax0re elimel letri
    dedth2h ) ACDZBCDZABEFZBAEFZGRAHIZBEFZBUBEFZGUBSBHIZEFZUEUBEFZGABHHAUBJTUCU
    AUDAUBBEKAUBBELMBUEJUCUFUDUGBUEUBELBUEUBEKMUBUEAHCNOBHCNOPQ $.
    $( [18-Aug-99] $)

  $( Addition to both sides of an inequality. $)
  ltadd1t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( A + C ) < ( B + C ) ) ) $=
    ( cr wcel clt wbr caddc co wb cc0 cded wceq breq1 opreq1 breq1d bi2bid
    breq2 breq2d opreq2 breq12d bilbid ax0re elimel ltadd1 dedth3h ) ADEZBDEZCD
    EZABFGZACHIZBCHIZFGZJUGAKLZBFGZUNCHIZULFGZJABCKKKUNUHBKLZFGZUPURCHIZFGZJUSU
    NUICKLZHIZURVBHIZFGZJAUNMZUJUOUMUQAUNBFNVFUKUPULFAUNCHOPQBURMZUOUSUQVABURUN
    FRVGULUTUPFBURCHOSQCVBMZVAVEUSVHUPVCUTVDFCVBUNHTCVBURHTUAUBUNURVBAKDUCUDBKD
    UCUDCKDUCUDUEUF $.
    $( [12-Nov-99] $)

  $( Addition to both sides of an inequality. $)
  ltadd2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B <-> ( C + A ) < ( C + B ) ) ) $=
    ( cr wcel w3a clt wbr caddc co ltadd1t wceq cc axaddcom recnt syl2an
    3adant2 3adant1 breq12d bitrd ) ADEZBDEZCDEZFZABGHACIJZBCIJZGHCAIJZCBIJZGHA
    BCKUDUEUGUFUHGUAUCUEUGLZUBAMECMEZUIUAUCACNAOCOZPQUBUCUFUHLZUABMEUJULUBUCBCN
    BOUKPRST $.
    $( [12-Nov-99] $)

  $( Addition to both sides of an inequality. $)
  leadd1t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( A + C ) <_ ( B + C ) ) ) $=
    ( cr wcel cle wbr caddc co wb cc0 cded wceq breq1 opreq1 breq1d bi2bid
    breq2 breq2d opreq2 breq12d bilbid ax0re elimel leadd1 dedth3h ) ADEZBDEZCD
    EZABFGZACHIZBCHIZFGZJUGAKLZBFGZUNCHIZULFGZJABCKKKUNUHBKLZFGZUPURCHIZFGZJUSU
    NUICKLZHIZURVBHIZFGZJAUNMZUJUOUMUQAUNBFNVFUKUPULFAUNCHOPQBURMZUOUSUQVABURUN
    FRVGULUTUPFBURCHOSQCVBMZVAVEUSVHUPVCUTVDFCVBUNHTCVBURHTUAUBUNURVBAKDUCUDBKD
    UCUDCKDUCUDUEUF $.
    $( [18-Oct-99] $)

  $( Addition to both sides of an inequality. $)
  leadd2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( C + A ) <_ ( C + B ) ) ) $=
    ( cr wcel w3a cle wbr caddc co leadd1t wceq cc axaddcom recnt syl2an
    3adant2 3adant1 breq12d bitrd ) ADEZBDEZCDEZFZABGHACIJZBCIJZGHCAIJZCBIJZGHA
    BCKUDUEUGUFUHGUAUCUEUGLZUBAMECMEZUIUAUCACNAOCOZPQUBUCUFUHLZUABMEUJULUBUCBCN
    BOUKPRST $.
    $( [26-Oct-99] $)

  $( Subtraction from both sides of an inequality. $)
  lesub1t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A <_ B <-> ( A - C ) <_ ( B - C ) ) ) $=
    ( cr wcel w3a cle wbr cneg caddc co cmin wb leadd1t negrclt syl3an3 cc
    wceq subnegt 3adant2 3adant1 breq12d recnt syl3an bitr4d ) ADEZBDEZCDEZFABG
    HZACIZJKZBUJJKZGHZACLKZBCLKZGHZUFUGUJDEUIUMMUHABUJNCOPAQEZBQEZCQEZUPUMMUFUQ
    URUSFUNUKUOULGUQUSUNUKRURACSTURUSUOULRUQBCSUAUBUGUHAUCBUCCUCUDUE $.
    $( [6-Jun-04] $) $( [13-May-04] $)

  $( Inequality relation between subtraction and addition. $)
  ltsubaddt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( ( A - B ) < C <-> A < ( C + B ) ) ) $=
    ( cr wcel cmin co clt wbr caddc wb cc0 cded wceq opreq1 breq1d breq1
    bi2bid opreq2 breq2d breq2 ax0re elimel ltsubadd dedth3h ) ADEZBDEZCDEZABFG
    ZCHIZACBJGZHIZKUFALMZBFGZCHIZUMUKHIZKABCLLLUMUGBLMZFGZCHIZUMCUQJGZHIZKURUHC
    LMZHIZUMVBUQJGZHIZKAUMNZUJUOULUPVFUIUNCHAUMBFOPAUMUKHQRBUQNZUOUSUPVAVGUNURC
    HBUQUMFSPVGUKUTUMHBUQCJSTRCVBNZUSVCVAVECVBURHUAVHUTVDUMHCVBUQJOTRUMUQVBALDU
    BUCBLDUBUCCLDUBUCUDUE $.
    $( [21-Jan-97] $)

  $( Inequality relation between subtraction and addition. $)
  ltsubadd2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) < C <-> A < ( B + C ) ) ) $=
    ( cr wcel w3a cmin co clt wbr caddc ltsubaddt wb wa wceq cc axaddcom recnt
    syl2an ancoms breq2d 3adant1 bitrd ) ADEZBDEZCDEZFABGHCIJACBKHZIJZABCKHZIJZ
    ABCLUEUFUHUJMUDUEUFNUGUIAIUFUEUGUIOZCPEBPEUKUFUECBQCRBRSTUAUBUC $.
    $( [21-Jan-97] $)

  $( Inequality relation between subtraction and addition. $)
  lesubadd2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) <_ C <-> A <_ ( B + C ) ) ) $=
    ( cr wcel cmin co cle wbr caddc wb cc0 cded wceq opreq1 breq1d breq1
    bi2bid opreq2 breq2d breq2 ax0re elimel lesubadd2 dedth3h ) ADEZBDEZCDEZABF
    GZCHIZABCJGZHIZKUFALMZBFGZCHIZUMUKHIZKABCLLLUMUGBLMZFGZCHIZUMUQCJGZHIZKURUH
    CLMZHIZUMUQVBJGZHIZKAUMNZUJUOULUPVFUIUNCHAUMBFOPAUMUKHQRBUQNZUOUSUPVAVGUNUR
    CHBUQUMFSPVGUKUTUMHBUQCJOTRCVBNZUSVCVAVECVBURHUAVHUTVDUMHCVBUQJSTRUMUQVBALD
    UBUCBLDUBUCCLDUBUCUDUE $.
    $( [10-Aug-99] $)

  $( Inequality relation between subtraction and addition. $)
  ltsub23t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) < C <-> ( A - C ) < B ) ) $=
    ( cr wcel w3a cmin co clt wbr caddc ltsubaddt wb ltsubadd2t 3com23 bitr4d
    ) ADEZBDEZCDEZFABGHCIJACBKHIJZACGHBIJZABCLQSRUATMACBNOP $.
    $( [4-Oct-99] $)

  $( Adding both side of two inequalities.  Theorem I.25 of [Apostol] p. 20. $)
  lt2addt $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A < C /\ B < D ) -> ( A + B ) < ( C + D ) ) ) $=
    ( cr wcel clt wbr wa caddc co wi cc0 cded wceq breq1 birand opreq1 breq1d
    bi2imd biland opreq2 breq2 breq2d ax0re elimel lt2add dedth4h ) AEFZBEFZCEF
    ZDEFZACGHZBDGHZIZABJKZCDJKZGHZLABCDMMUIAMNZCGHZUNIZUSBJKZUQGHZLUTUJBMNZDGHZ
    IZUSVDJKZUQGHZLUSUKCMNZGHZVEIZVGVIDJKZGHZLVJVDULDMNZGHZIZVGVIVNJKZGHZLMMAUS
    OZUOVAURVCVSUMUTUNAUSCGPQVSUPVBUQGAUSBJRSTBVDOZVAVFVCVHVTUNVEUTBVDDGPUAVTVB
    VGUQGBVDUSJUBSTCVIOZVFVKVHVMWAUTVJVECVIUSGUCQWAUQVLVGGCVIDJRUDTDVNOZVKVPVMV
    RWBVEVOVJDVNVDGUCUAWBVLVQVGGDVNVIJUBUDTUSVDVIVNAMEUEUFBMEUEUFCMEUEUFDMEUEUF
    UGUH $.
    $( [15-Aug-99] $)

  $( Addition of 2 nonnegative numbers is nonnegative. $)
  addge0t $p |- ( ( A e. RR /\ B e. RR ) ->
               ( ( 0 <_ A /\ 0 <_ B ) -> 0 <_ ( A + B ) ) ) $=
    ( cr wcel cc0 cle wbr wa caddc co wi cded wceq breq2 birand opreq1 breq2d
    bi2imd biland opreq2 ax0re elimel addge0 dedth2h ) ACDZBCDZEAFGZEBFGZHZEABI
    JZFGZKEUEAELZFGZUHHZEULBIJZFGZKUMEUFBELZFGZHZEULUQIJZFGZKABEEAULMZUIUNUKUPV
    BUGUMUHAULEFNOVBUJUOEFAULBIPQRBUQMZUNUSUPVAVCUHURUMBUQEFNSVCUOUTEFBUQULITQR
    ULUQAECUAUBBECUAUBUCUD $.
    $( [27-Sep-99] $)

  $( Negative of both sides of an inequality.  Theorem I.23 of [Apostol]
     p. 20. $)
  ltnegt $p |- ( ( A e. RR /\ B e. RR ) -> ( A < B <-> -u B < -u A ) ) $=
    ( cr wcel clt wbr cneg wb cc0 cded wceq breq1 negeq breq2d bi2bid breq2
    breq1d ax0re elimel ltneg dedth2h ) ACDZBCDZABEFZBGZAGZEFZHUBAIJZBEFZUEUHGZ
    EFZHUHUCBIJZEFZULGZUJEFZHABIIAUHKZUDUIUGUKAUHBELUPUFUJUEEAUHMNOBULKZUIUMUKU
    OBULUHEPUQUEUNUJEBULMQOUHULAICRSBICRSTUA $.
    $( [27-Aug-99] $)

  $( Negative of both sides of an inequality. $)
  lenegt $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B <-> -u B <_ -u A ) ) $=
    ( cr wcel cle wbr cneg wb cc0 cded wceq breq1 negeq breq2d bi2bid breq2
    breq1d ax0re elimel leneg dedth2h ) ACDZBCDZABEFZBGZAGZEFZHUBAIJZBEFZUEUHGZ
    EFZHUHUCBIJZEFZULGZUJEFZHABIIAUHKZUDUIUGUKAUHBELUPUFUJUEEAUHMNOBULKZUIUMUKU
    OBULUHEPUQUEUNUJEBULMQOUHULAICRSBICRSTUA $.
    $( [12-Sep-99] $)

  $( Contraposition of negative in an inequality. $)
  lenegcon1t $p |- ( ( A e. RR /\ B e. RR ) -> ( -u A <_ B <-> -u B <_ A ) ) $=
    ( cr wcel wa cneg cle wbr wb lenegt negrclt sylan cc wceq recnt negnegt
    syl breq2d adantr bitrd ) ACDZBCDZEAFZBGHZBFZUCFZGHZUEAGHZUCCDUBUDUGIUAUCBJ
    AKLUAUGUHIUBUAUFAUEGUAAMDUFANAOAPQRST $.
    $( [14-Jun-04] $) $( [10-May-04] $)

  $( Comparison of a number and its negative to zero.  Theorem I.23 of
     [Apostol] p. 20. $)
  lt0neg1t $p |- ( A e. RR -> ( A < 0 <-> 0 < -u A ) ) $=
    ( cr wcel cc0 clt wbr cneg wb ax0re ltnegt mpan2 neg0 breq1i syl6bb ) ABCZA
    DEFZDGZAGZEFZDREFODBCPSHIADJKQDRELMN $.
    $( [14-May-99] $)

  $( Comparison of a number and its negative to zero. $)
  lt0neg2t $p |- ( A e. RR -> ( 0 < A <-> -u A < 0 ) ) $=
    ( cr wcel cc0 clt wbr cneg wb ax0re ltnegt mpan neg0 breq2i syl6bb ) ABCZDA
    EFZAGZDGZEFZQDEFDBCOPSHIDAJKRDQELMN $.
    $( [10-May-04] $) $( [10-May-04] $)

  $( Comparison of a number and its negative to zero. $)
  le0neg1t $p |- ( A e. RR -> ( A <_ 0 <-> 0 <_ -u A ) ) $=
    ( cr wcel cc0 cle wbr cneg wb ax0re lenegt mpan2 neg0 breq1i syl6bb ) ABCZA
    DEFZDGZAGZEFZDREFODBCPSHIADJKQDRELMN $.
    $( [15-Jun-04] $) $( [10-May-04] $)

  $( Comparison of a number and its negative to zero. $)
  le0neg2t $p |- ( A e. RR -> ( 0 <_ A <-> -u A <_ 0 ) ) $=
    ( cr wcel cc0 cle wbr cneg wb ax0re lenegt mpan neg0 breq2i syl6bb ) ABCZDA
    EFZAGZDGZEFZQDEFDBCOPSHIDAJKRDQELMN $.
    $( [24-Aug-99] $)

  $( Lemma to show a nonnegative number is zero. $)
  lesub0t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( ( 0 <_ A /\ B <_ ( B - A ) ) <-> A = 0 ) ) $=
    ( cr wcel cc0 cle wbr cmin co wa wceq wb cded breq2 opreq2 breq2d bi2and
    cleq1 bi2bid id opreq1 breq12d biland birbid ax0re elimel lesub0 dedth2h )
    ACDZBCDZEAFGZBBAHIZFGZJZAEKZLEUIAEMZFGZBBUPHIZFGZJZUPEKZLUQUJBEMZVBUPHIZFGZ
    JZVALABEEAUPKZUNUTUOVAVFUKUQUMUSAUPEFNVFULURBFAUPBHOPQAUPERSBVBKZUTVEVAVGUS
    VDUQVGBVBURVCFVGTBVBUPHUAUBUCUDUPVBAECUEUFBECUEUFUGUH $.
    $( [8-Oct-99] $)

  $( The product of two nonnegative numbers is nonnegative. $)
  mulge0t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( ( 0 <_ A /\ 0 <_ B ) -> 0 <_ ( A x. B ) ) ) $=
    ( cr wcel cc0 cle wbr wa cmulc co wi cded wceq breq2 birand opreq1 breq2d
    bi2imd biland opreq2 ax0re elimel mulge0 dedth2h ) ACDZBCDZEAFGZEBFGZHZEABI
    JZFGZKEUEAELZFGZUHHZEULBIJZFGZKUMEUFBELZFGZHZEULUQIJZFGZKABEEAULMZUIUNUKUPV
    BUGUMUHAULEFNOVBUJUOEFAULBIPQRBUQMZUNUSUPVAVCUHURUMBUQEFNSVCUOUTEFBUQULITQR
    ULUQAECUAUBBECUAUBUCUD $.
    $( [8-Oct-99] $)

  $( A non-zero square is positive.  Theorem I.20 of [Apostol] p. 20. $)
  ltsqt $p |- ( A e. RR -> ( -. A = 0 -> 0 < ( A x. A ) ) ) $=
    ( cr wcel cc0 wceq wn cmulc co clt wbr wi cded cleq1 binegd id opreq12d
    breq2d bi2imd ax0re elimel sqgt0 dedth ) ABCZADEZFZDAAGHZIJZKUCADLZDEZFZDUH
    UHGHZIJZKADAUHEZUEUJUGULUMUDUIAUHDMNUMUFUKDIUMAUHAUHGUMOZUNPQRUHADBSTUAUB
    $.
    $( [6-May-99] $)

  $( 0 is less than 1.  Theorem I.21 of [Apostol] p. 20. $)
  lt01 $p |- 0 < 1 $=
    ( cc0 c1 cmulc co clt wceq wn wbr wne ax1ne0 df-ne mpbi ax1re sqgt0 ax-mp
    1cn mulid1 breqtr ) ABBCDZBEBAFGZASEHBAITJBAKLBMNOBPQR $.
    $( [17-Jan-97] $)

  ${
    eqneg.1 $e |- A e. CC $.
    $( A number equal to its negative is zero. $)
    eqneg $p |- ( A = -u A <-> A = 0 ) $=
      ( cneg wceq cc0 c1 caddc co wn wne ax1re addrcl lt01 addgt0i gt0ne0i
      df-ne mpbi cmulc wo opreq2 1p1times negid cleqcomi 3eqtr4g recn mul0or
      sylib ord mpi cmin df-neg 0cn subid eqtr2 id negeq cleq12d mpbiri impbi
      ) AACZDZAEDZVAFFGHZEDZIZVBVCEJVEVCFFKKLZFFKKMMNOVCEPQVAVDVBVAVCARHZEDVDVB
      SVAAAGHAUTGHZVGEAUTAGTABUAVHEABUBUCUDVCAVCVFUEBUFUGUHUIVBVAEECZDVIEEUJHEE
      UKEULUMUNVBAEUTVIVBUOAEUPUQURUS $.
      $( [29-May-99] $)

    $( A number is non-zero iff its negative is non-zero. $)
    negne0 $p |- ( A =/= 0 <-> -u A =/= 0 ) $=
      ( cc0 wceq wn cneg wne negneg cleq2i cleqcom bitr2 eqneg negcl 3bitr3
      bineg df-ne 3bitr4 ) ACDZEAFZCDZEACGSCGRTASDZSSFZDZRTUCSADUAUBASABHISAJKA
      BLSABMLNOACPSCPQ $.
      $( [10-Aug-99] $)
  $}

  ${
    negn0.1 $e |- A e. CC $.
    negn0.2 $e |- A =/= 0 $.
    $( The negative of a non-zero number is non-zero. $)
    negn0 $p |- -u A =/= 0 $=
      ( cc0 wne cneg negne0 mpbi ) ADEAFDECABGH $.
      $( [3-Sep-04] $) $( [30-Jul-04] $)
  $}

  $( Hypothesis for weak deduction theorem to eliminate ` 0 < A ` . $)
  elimgt0 $p |- 0 < ded ( 0 < A , A , 1 ) $=
    ( cc0 clt wbr c1 cded breq2 lt01 elimhyp ) BACDZBJAEFZCDBECDAEAKBCGEKBCGHI
    $.
    $( [15-May-99] $)

  $( Hypothesis for weak deduction theorem to eliminate ` 0 <_ A ` . $)
  elimge0 $p |- 0 <_ ded ( 0 <_ A , A , 0 ) $=
    ( cc0 cle wbr cded breq2 ax0re leid elimhyp ) BACDZBJABEZCDBBCDABAKBCFBKBCF
    BGHI $.
    $( [30-Jul-99] $)

  $( A number is less than itself plus 1. $)
  ltplus1t $p |- ( A e. RR -> A < ( A + 1 ) ) $=
    ( cr wcel cc0 caddc co c1 clt cc wceq recnt ax0id syl wbr lt01 wb ax1re
    ax0re ltadd2t mp3an1 mpan mpbii eqbrtrrd ) ABCZADEFZAAGEFZHUDAICUEAJAKALMUD
    DGHNZUEUFHNZOGBCZUDUGUHPZQDBCUIUDUJRDGASTUAUBUC $.
    $( [20-Aug-01] $)

  ${
    ltplus1.1 $e |- A e. RR $.
    $( A number is less than itself plus 1. $)
    ltplus1 $p |- A < ( A + 1 ) $=
      ( cr wcel c1 caddc co clt wbr ltplus1t ax-mp ) ACDAAEFGHIBAJK $.
      $( [20-Aug-01] $)

    ${
      recgt0i.2 $e |- 0 < A $.
      $( The reciprocal of a positive number is positive.  Exercise 4 of
         [Apostol] p. 21. $)
      recgt0i $p |- 0 < ( 1 / A ) $=
        ( cc0 c1 cdiv co clt wbr wceq wo wn wne 1cn recn ax1ne0 gt0ne0i
        divneq0 necom mpbi df-ne lt01 ax0re ax1re ltnsym ax-mp cneg cmulc
        divrcl negrcl mulgt0 mpan2 mulneg1 mulcom recid eqtr negeqi syl6breq
        cr wcel wb lt0neg1t 3imtr4 mto pm3.2ni axlttri mp2an mpbir ) DEAFGZHIZD
        VIJZVIDHIZKLZVKVLDVIMZVKLVIDMVNEANABOZPABCQZRVIDSTDVIUATVLEDHIZDEHIVQLU
        BDEUCUDUEUFDVIUGZHIZDEUGZHIZVLVQVSDVRAUHGZVTHVSDAHIDWBHICVRAVIEAUDBVPUI
        ZUJBUKULWBVIAUHGZUGVTVIAVIWCOZVOUMWDEWDAVIUHGEVIAWEVOUNAVOVPUOUPUQUPURV
        IUSUTZVLVSVAWCVIVBUFEUSUTVQWAVAUDEVBUFVCVDVEDUSUTWFVJVMVAUCWCDVIVFVGVH
        $.
        $( [15-May-99] $)
    $}

    $( The reciprocal of a positive number is positive.  Exercise 4 of [Apostol]
       p. 21. $)
    recgt0 $p |- ( 0 < A -> 0 < ( 1 / A ) ) $=
      ( cc0 clt wbr c1 cdiv co cded wceq opreq2 breq2d cr ax1re keepel elimgt0
      recgt0i dedth ) CADEZCFAGHZDECFSAFIZGHZDEAFAUAJTUBCDAUAFGKLUASAFMBNOAPQR
      $.
      $( [15-May-99] $)

    ${
      prodgt0.2 $e |- B e. RR $.
      ${
        prodgt0i.3 $e |- 0 < A $.
        $( If a number and its product are positive, so is the multiplier. $)
        prodgt0i $p |- ( 0 < ( A x. B ) -> 0 < B ) $=
          ( cc0 cmulc co clt wbr c1 cdiv wi recgt0i ax0re mulrcl ax1re gt0ne0i
          divrcl ltmullem ax-mp recn mulzer2 mul23 recid opreq1i mulid2 3eqtr
          3brtr3g ) FABGHZIJZFKALHZGHZUJULGHZFBIFULIJUKUMUNIJMACENFUJULOABCDPKA
          QCACERZSZTUAULULUPUBZUCUNAULGHZBGHKBGHBABULACUBZBDUBZUQUDURKBGAUSUOUE
          UFBUTUGUHUI $.
          $( [15-May-99] $)
      $}

      $( If a number and its product are positive, so is the multiplier. $)
      prodgt0 $p |- ( ( 0 < A /\ 0 < ( A x. B ) ) -> 0 < B ) $=
        ( cc0 clt wbr cmulc co wi c1 cded wceq opreq1 breq2d birimd cr ax1re
        keepel elimgt0 prodgt0i dedth imp ) EAFGZEABHIZFGZEBFGZUDUFUGJEUDAKLZBH
        IZFGZUGJAKAUHMZUFUJUGUKUEUIEFAUHBHNOPUHBUDAKQCRSDATUAUBUC $.
        $( [15-May-99] $)

      ${
        divgt0lem.3 $e |- 0 < B $.
        $( The ratio of two positive numbers is positive. $)
        divgt0lem $p |- ( 0 < A -> 0 < ( A / B ) ) $=
          ( cc0 clt wbr c1 cdiv co cmulc recgt0i ax1re gt0ne0i divrcl mulgt0
          mpan2 recn divrec breq2i sylibr ) FAGHZFAIBJKZLKZGHZFABJKZGHUCFUDGHUF
          BDEMAUDCIBNDBDEOZPQRUGUEFGABACSBDSUHTUAUB $.
          $( [16-May-99] $)
      $}

      $( The ratio of two positive numbers is positive. $)
      divgt0 $p |- ( ( 0 < A /\ 0 < B ) -> 0 < ( A / B ) ) $=
        ( cc0 clt wbr cdiv co wi c1 cded wceq opreq2 breq2d bilimd cr ax1re
        keepel elimgt0 divgt0lem dedth com12 imp ) EAFGZEBFGZEABHIZFGZUFUEUHUFU
        EUHJUEEAUFBKLZHIZFGZJBKBUIMZUHUKUEULUGUJEFBUIAHNOPAUICUFBKQDRSBTUAUBUCU
        D $.
        $( [16-May-99] $)

      ${
        divgt0i.3 $e |- 0 < A $.
        divgt0i.4 $e |- 0 < B $.
        $( The ratio of two positive numbers is positive. $)
        divgt0i $p |- 0 < ( A / B ) $=
          ( cc0 clt wbr cdiv co divgt0lem ax-mp ) GAHIGABJKHIEABCDFLM $.
          $( [18-May-99] $)
      $}

      $( The ratio of nonnegative and positive numbers is nonnegative. $)
      divge0 $p |- ( ( 0 <_ A /\ 0 < B ) -> 0 <_ ( A / B ) ) $=
        ( cc0 cle wbr clt cdiv co wceq wo divgt0 exp com12 opreq1 cleq1d wne
        gt0ne0 c1 cded opreq2 cc recn 1cn keepel elimne0 divzer dedth syl
        syl5bi cleqcom syl6ib im2ord ax0re leloe syl5ib cr wcel wa wb divrclz
        jctil leloet sylibrd imp ) EAFGZEBHGZEABIJZFGZVHVGVJVHVGEVIHGZEVIKZLZVJ
        VHEAHGZEAKZLVMVGVHVNVKVOVLVNVHVKVNVHVKABCDMNOVHVOVIEKZVLVOVHVPVOEBIJZEK
        ZVPVHVOVQVIEEABIPQVHBERZVRBDSZVSVREVSBTUAZIJZEKBTBWAKVQWBEBWAEIUBQWAVSB
        TUCBDUDUEUFBUGUHUIUJUKOVIEULUMUNEAUOCUPUQVHEURUSZVIURUSZUTVJVMVAVHWDWCV
        HVSWDVTABCDVBUJUOVCEVIVDUJVEOVF $.
        $( [12-Aug-99] $)
    $}

    ${
      ltmul1.2 $e |- B e. RR $.
      ltmul1.3 $e |- C e. RR $.
      ${
        ltmul1i.4 $e |- 0 < C $.
        $( Multiplication of both sides of an inequality by a positive
           number.  Theorem I.19 of [Apostol] p. 20. $)
        ltmul1i $p |- ( A < B <-> ( A x. C ) < ( B x. C ) ) $=
          ( clt wbr cmulc co cc0 wi ltmullem ax-mp c1 cdiv recgt0i mulrcl
          ax1re gt0ne0i divrcl recn 1cn divcl mulass recid opreq2i mulid1
          3eqtr 3brtr3g impbi ) ABHIZACJKZBCJKZHIZLCHIUMUPMGABCDEFNOUPUNPCQKZJK
          ZUOUQJKZABHLUQHIUPURUSHIMCFGRUNUOUQACDFSBCEFSPCTFCFGUAZUBNOURACUQJKZJ
          KAPJKAACUQADUCZCFUCZPCUDVCUTUEZUFVAPAJCVCUTUGZUHAVBUIUJUSBVAJKBPJKBBC
          UQBEUCZVCVDUFVAPBJVEUHBVFUIUJUKUL $.
          $( [16-May-99] $)
      $}

      $( Multiplication of both sides of an inequality by a positive
         number.  Theorem I.19 of [Apostol] p. 20. $)
      ltmul1 $p |- ( 0 < C -> ( A < B <-> ( A x. C ) < ( B x. C ) ) ) $=
        ( cc0 clt wbr cmulc co wb c1 cded wceq opreq2 breq12d bilbid cr ax1re
        keepel elimgt0 ltmul1i dedth ) GCHIZABHIZACJKZBCJKZHIZLUFAUECMNZJKZBUJJ
        KZHIZLCMCUJOZUIUMUFUNUGUKUHULHCUJAJPCUJBJPQRABUJDEUECMSFTUACUBUCUD $.
        $( [16-May-99] $)

      $( Multiplication of both sides of an inequality by a positive
         number.  Theorem I.19 of [Apostol] p. 20. $)
      ltmul2 $p |- ( 0 < C -> ( A < B <-> ( C x. A ) < ( C x. B ) ) ) $=
        ( cc0 clt wbr cmulc co ltmul1 recn mulcom breq12i syl6bb ) GCHIABHIACJK
        ZBCJKZHICAJKZCBJKZHIABCDEFLQSRTHACADMCFMZNBCBEMUANOP $.
        $( [16-May-99] $)

      $( Multiplication of both sides of an inequality by a positive
         number. $)
      lemul2 $p |- ( 0 < C -> ( A <_ B <-> ( C x. A ) <_ ( C x. B ) ) ) $=
        ( cc0 clt wbr wceq wo cmulc co cle ltmul2 wne wb gt0ne0 cc wcel w3a
        recn 3pm3.2i mulcant2 mpan bicomd syl bi2ord leloe mulrcl 3bitr4g ) GCH
        IZABHIZABJZKCALMZCBLMZHIZUOUPJZKABNIUOUPNIULUMUQUNURABCDEFOULCGPZUNURQC
        FRUSURUNCSTZASTZBSTZUAUSURUNQUTVAVBCFUBADUBBEUBUCCABUDUEUFUGUHABDEUIUOU
        PCAFDUJCBFEUJUIUK $.
        $( [1-Aug-99] $)

      $( Multiplication of both sides of an inequality by a positive
         number. $)
      lemul1 $p |- ( 0 < C -> ( A <_ B <-> ( A x. C ) <_ ( B x. C ) ) ) $=
        ( cc0 clt wbr cle cmulc co lemul2 recn mulcom breq12i syl6bb ) GCHIABJI
        CAKLZCBKLZJIACKLZBCKLZJIABCDEFMRTSUAJCACFNZADNOCBUBBENOPQ $.
        $( [2-Aug-99] $)

      ${
        ltdivi.4 $e |- 0 < C $.
        $( Division of both sides of an inequality by a positive number. $)
        ltdivi $p |- ( A < B <-> ( A / C ) < ( B / C ) ) $=
          ( clt wbr c1 cdiv co cmulc ax1re gt0ne0i divrcl recgt0i ltmul1i recn
          divrec breq12i bitr4 ) ABHIAJCKLZMLZBUCMLZHIACKLZBCKLZHIABUCDEJCNFCFG
          OZPCFGQRUFUDUGUEHACADSCFSZUHTBCBESUIUHTUAUB $.
          $( [16-May-99] $)
      $}

      $( Division of both sides of an inequality by a positive number. $)
      ltdiv $p |- ( 0 < C -> ( A < B <-> ( A / C ) < ( B / C ) ) ) $=
        ( cc0 clt wbr cdiv co wb c1 cded wceq opreq2 breq12d bilbid cr ax1re
        keepel elimgt0 ltdivi dedth ) GCHIZABHIZACJKZBCJKZHIZLUFAUECMNZJKZBUJJK
        ZHIZLCMCUJOZUIUMUFUNUGUKUHULHCUJAJPCUJBJPQRABUJDEUECMSFTUACUBUCUD $.
        $( [16-May-99] $)

      $( Inequality relation between division and multiplication. $)
      ltdivmul $p |- ( 0 < C -> ( A < ( B / C ) <-> ( A x. C ) < B ) ) $=
        ( cc0 clt wbr cmulc co cdiv mulrcl ltdiv wne wceq gt0ne0 recn divcan4z
        syl breq1d bitr2d ) GCHIZACJKZBHIUDCLKZBCLKZHIAUFHIUDBCACDFMEFNUCUEAUFH
        UCCGOUEAPCFQCACFRADRSTUAUB $.
        $( [12-Oct-99] $)
    $}
  $}

  $( The reciprocal of a positive number is positive.  Exercise 4 of [Apostol]
     p. 21. $)
  recgt0t $p |- ( A e. RR -> ( 0 < A -> 0 < ( 1 / A ) ) ) $=
    ( cr wcel cc0 clt wbr c1 cdiv co wi cded wceq breq2 opreq2 breq2d bi2imd
    ax0re elimel recgt0 dedth ) ABCZDAEFZDGAHIZEFZJDUAADKZEFZDGUEHIZEFZJADAUELZ
    UBUFUDUHAUEDEMUIUCUGDEAUEGHNOPUEADBQRST $.
    $( [25-Aug-99] $)

  $( The ratio of two positive numbers is positive. $)
  divgt0t $p |- ( ( A e. RR /\ B e. RR ) ->
             ( ( 0 < A /\ 0 < B ) -> 0 < ( A / B ) ) ) $=
    ( cr wcel cc0 clt wbr wa cdiv co wi cded wceq breq2 birand opreq1 breq2d
    bi2imd biland opreq2 ax0re elimel divgt0 dedth2h ) ACDZBCDZEAFGZEBFGZHZEABI
    JZFGZKEUEAELZFGZUHHZEULBIJZFGZKUMEUFBELZFGZHZEULUQIJZFGZKABEEAULMZUIUNUKUPV
    BUGUMUHAULEFNOVBUJUOEFAULBIPQRBUQMZUNUSUPVAVCUHURUMBUQEFNSVCUOUTEFBUQULITQR
    ULUQAECUAUBBECUAUBUCUD $.
    $( [12-Oct-99] $)

  $( The ratio of nonnegative and positive numbers is nonnegative. $)
  divge0t $p |- ( ( A e. RR /\ B e. RR ) ->
             ( ( 0 <_ A /\ 0 < B ) -> 0 <_ ( A / B ) ) ) $=
    ( cr wcel cc0 cle wbr clt wa cdiv co wi cded wceq breq2 birand opreq1
    breq2d bi2imd biland opreq2 ax0re elimel divge0 dedth2h ) ACDZBCDZEAFGZEBHG
    ZIZEABJKZFGZLEUFAEMZFGZUIIZEUMBJKZFGZLUNEUGBEMZHGZIZEUMURJKZFGZLABEEAUMNZUJ
    UOULUQVCUHUNUIAUMEFOPVCUKUPEFAUMBJQRSBURNZUOUTUQVBVDUIUSUNBUREHOTVDUPVAEFBU
    RUMJUARSUMURAECUBUCBECUBUCUDUE $.
    $( [27-Sep-99] $)

  $( Inequality relation between division and multiplication. $)
  ltdivmult $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
               ( 0 < C -> ( A < ( B / C ) <-> ( A x. C ) < B ) ) ) $=
    ( cr wcel cc0 clt wbr cdiv co cmulc wb wi cded wceq breq1 opreq1 breq1d
    bi2bid bilimd breq2d breq2 opreq2 bi2imd ax0re elimel ltdivmul dedth3h ) AD
    EZBDEZCDEZFCGHZABCIJZGHZACKJZBGHZLZMULUIAFNZUMGHZURCKJZBGHZLZMABCFFFULURUJB
    FNZCIJZGHZUTVCGHZLZMFUKCFNZGHZURVCVHIJZGHZURVHKJZVCGHZLZMAUROZUQVBULVOUNUSU
    PVAAURUMGPVOUOUTBGAURCKQRSTBVCOZVBVGULVPUSVEVAVFVPUMVDURGBVCCIQUABVCUTGUBST
    CVHOZULVIVGVNCVHFGUBVQVEVKVFVMVQVDVJURGCVHVCIUCUAVQUTVLVCGCVHURKUCRSUDURVCV
    HAFDUEUFBFDUEUFCFDUEUFUGUH $.
    $( [12-Oct-99] $)

  ${
    ltrec.1 $e |- A e. RR $.
    ltrec.2 $e |- B e. RR $.
    ${
      ltreci.3 $e |- 0 < A $.
      ltreci.4 $e |- 0 < B $.
      $( The reciprocal of both sides of an inequality. $)
      ltreci $p |- ( A < B <-> ( 1 / B ) < ( 1 / A ) ) $=
        ( cdiv co clt wbr c1 cmulc recn gt0ne0i divid recid eqtr4 divrec
        breq12i ltdivi cc0 wb ax1re divrcl ltmul2 ax-mp 3bitr4 ) AAGHZBAGHZIJBK
        BGHZLHZBKAGHZLHZIJZABIJUJULIJZUHUKUIUMIUHKUKAACMZACENZOBBDMZBDFNZPQBAUR
        UPUQRSABACDCETUABIJUOUNUBFUJULBKBUCDUSUDKAUCCUQUDDUEUFUG $.
        $( [15-Sep-99] $)
    $}

    $( The reciprocal of both sides of an inequality. $)
    ltrec $p |- ( ( 0 < A /\ 0 < B ) ->
                ( A < B <-> ( 1 / B ) < ( 1 / A ) ) ) $=
      ( cc0 clt wbr c1 cdiv co wb cded wceq breq1 opreq2 breq2d bi2bid breq2
      breq1d cr ax1re keepel elimgt0 ltreci dedth2h ) EAFGZEBFGZABFGZHBIJZHAIJZ
      FGZKUFAHLZBFGZUIHULIJZFGZKULUGBHLZFGZHUPIJZUNFGZKABHHAULMZUHUMUKUOAULBFNU
      TUJUNUIFAULHIOPQBUPMZUMUQUOUSBUPULFRVAUIURUNFBUPHIOSQULUPUFAHTCUAUBUGBHTD
      UAUBAUCBUCUDUE $.
      $( [15-Sep-99] $)

    $( The reciprocal of both sides of an inequality. $)
    lerec $p |- ( ( 0 < A /\ 0 < B ) ->
                ( A <_ B <-> ( 1 / B ) <_ ( 1 / A ) ) ) $=
      ( cc0 clt wbr wa wceq wo c1 cdiv co cle ltrec wne wb recn rec11 gt0ne0
      syl2an cleqcom syl5rbbr bi2ord cr wcel ax1re divrclz syl im2an ancoms
      leloet bitr4d leloe syl5bb ) EAFGZEBFGZHZABFGZABIZJZKBLMZKALMZNGZABNGURVA
      VBVCFGZVBVCIZJZVDURUSVEUTVFABCDOURVCVBIZUTVFAEPZBEPZVHUTQUPUQABACRBDRSACT
      ZBDTZUAVCVBUBUCUDURVBUEUFZVCUEUFZHZVDVGQUQUPVOUQVMUPVNUQVJVMVLKBUGDUHUIUP
      VIVNVKKAUGCUHUIUJUKVBVCULUIUMABCDUNUO $.
      $( [16-Sep-99] $)

    ${
      ltdiv23.3 $e |- C e. RR $.
      ${
        ltdiv23i.4 $e |- 0 < B $.
        ltdiv23i.5 $e |- 0 < C $.
        $( Swap denominator with other side of inequality. $)
        ltdiv23i $p |- ( ( A / B ) < C <-> ( A / C ) < B ) $=
          ( cdiv co clt wbr cmulc gt0ne0i divrcl ltmul1i recn divcan1 breq1i
          mulrcl ltdivi divcan3 breq2i bitr 3bitr ) ABIJZCKLUFBMJZCBMJZKLAUHKLZ
          ACIJZBKLZUFCBABDEBEGNZOFEGPUGAUHKBABEQZADQULRSUIUJUHCIJZKLUKAUHCDCBFE
          TFHUAUNBUJKCBCFQUMCFHNUBUCUDUE $.
          $( [26-Sep-99] $)
      $}

      $( Swap denominator with other side of inequality. $)
      ltdiv23 $p |- ( ( 0 < B /\ 0 < C ) ->
                  ( ( A / B ) < C <-> ( A / C ) < B ) ) $=
        ( cc0 clt wbr cdiv co wb c1 cded wceq opreq2 breq1d breq2 bi2bid cr
        ax1re keepel elimgt0 ltdiv23i dedth2h ) GBHIZGCHIZABJKZCHIZACJKZBHIZLAU
        FBMNZJKZCHIZUJULHIZLUMUGCMNZHIZAUPJKZULHIZLBCMMBULOZUIUNUKUOUTUHUMCHBUL
        AJPQBULUJHRSCUPOZUNUQUOUSCUPUMHRVAUJURULHCUPAJPQSAULUPDUFBMTEUAUBUGCMTF
        UAUBBUCCUCUDUE $.
        $( [26-Sep-99] $)
    $}

    $( Two nonnegative numbers compare the same as their squares. $)
    lt2sq $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( A < B <-> ( A x. A ) < ( B x. B ) ) ) $=
      ( cc0 clt wbr wceq wo cmulc co wb cle wa ltmul2 ltmul1 bi2anan9 anidm
      syl5bbr mulrcl lttr syl6bi wn lemul2 lemul1 bi2anan9r letr lelt 3imtr3g
      a3d impbid breq1 adantr ax0re opreq2 breq1d recn mulzer1 eqtr4 breq1i
      syl5bb sylan9bbr bitr3d adantl sylan9bb 3bitr3g bicon4d pm5.21 ltnr
      bicomd mtbii sylan13 ccase leloe syl2anb ) EAFGZEAHZIEBFGZEBHZIABFGZAAJKZ
      BBJKZFGZLZEAMGZEBMGVPVRVQVSWDVPVRNZVTWCWFVTWAABJKZFGZWGWBFGZNZWCWFVTVTNWJ
      VTVPVTWHVRVTWIABACDCOABBCDDPQVTRSWAWGWBAACCTZABCDTBBDDTZUAUBWFVTWCWFBAMGZ
      WBWAMGZVTUCZWCUCZWFWMWBBAJKZMGZWQWAMGZNZWNWFWMWMNWTWMVRWMWRVPWMWSBABDCDUD
      BAADCCUEUFWMRSWBWQWAWLBADCTWKUGUBBADCUHZWBWAWLWKUHZUIUJUKVQVRNVRVTWCVQVRV
      TLVREABFULZUMVRVRBEJKZWBFGZVQWCEBBUNDDOVQAEJKZWBFGWCXEVQXFWAWBFEAAJUOUPXD
      XFWBFXDEXFBBDUQURZAACUQURZUSUTVAZVBVCVPVSNZVTWCXJWMWNWOWPXJWEWMWNVSWEWMLV
      PEBAMULVDVPWEXFWAMGZVSWNEAAUNCCUDVSXDWAMGWNXKVSXDWBWAMEBBJUOZUPXFXDWAMXFE
      XDXHXGUSUTVAVEVCXAXBVFVGWOWPWDVQVSNZVTWCVHXMBBFGZVTBDVIVSXNVRVQVTVSVRXNEB
      BFULVJXCVBVKXMWBWBFGZWCWBWLVIVSXOXEVQWCVSXEXOVSXDWBWBFXLUPVJXIVBVKVLVMEAU
      NCVNEBUNDVNVO $.
      $( [3-Aug-99] $)

    $( Two nonnegative numbers compare the same as their squares. $)
    le2sq $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( A <_ B <-> ( A x. A ) <_ ( B x. B ) ) ) $=
      ( cc0 cle wbr cmulc co wb wa clt wn lt2sq binegd lelt mulrcl 3bitr4g
      ancoms ) EBFGZEAFGZABFGZAAHIZBBHIZFGZJTUAKZBALGZMUDUCLGZMUBUEUFUGUHBADCNO
      ABCDPUCUDAACCQBBDDQPRS $.
      $( [2-Aug-99] $)

    $( The square of a nonnegative number is a one-to-one function. $)
    sq11 $p |- ( ( 0 <_ A /\ 0 <_ B ) ->
                ( ( A x. A ) = ( B x. B ) <-> A = B ) ) $=
      ( cc0 cle wbr wa wceq cmulc co clt wo wn lt2sq wb ancoms bi2ord lttri2
      mulrcl 3bitr4g bicon4d bicomd ) EAFGZEBFGZHZABIZAAJKZBBJKZIZUFUGUJUFABLGZ
      BALGZMUHUILGZUIUHLGZMUGNUJNUFUKUMULUNABCDOUEUDULUNPBADCOQRABCDSUHUIAACCTB
      BDDTSUAUBUC $.
      $( [29-Jul-99] $)
  $}

  $( The reciprocal of both sides of an inequality. $)
  ltrect $p |- ( ( A e. RR /\ B e. RR ) -> ( ( 0 < A /\ 0 < B ) ->
              ( A < B <-> ( 1 / B ) < ( 1 / A ) ) ) ) $=
    ( cr wcel cc0 clt wbr wa c1 cdiv co wb wi cded wceq breq2 birand breq1
    opreq2 breq2d bi2bid bi2imd biland breq1d ax0re elimel ltrec dedth2h ) ACDZ
    BCDZEAFGZEBFGZHZABFGZIBJKZIAJKZFGZLZMEUIAENZFGZULHZUSBFGZUOIUSJKZFGZLZMUTEU
    JBENZFGZHZUSVFFGZIVFJKZVCFGZLZMABEEAUSOZUMVAURVEVMUKUTULAUSEFPQVMUNVBUQVDAU
    SBFRVMUPVCUOFAUSIJSTUAUBBVFOZVAVHVEVLVNULVGUTBVFEFPUCVNVBVIVDVKBVFUSFPVNUOV
    JVCFBVFIJSUDUAUBUSVFAECUEUFBECUEUFUGUH $.
    $( [26-Sep-99] $)

  $( The reciprocal of both sides of an inequality. $)
  lerect $p |- ( ( A e. RR /\ B e. RR ) -> ( ( 0 < A /\ 0 < B ) ->
              ( A <_ B <-> ( 1 / B ) <_ ( 1 / A ) ) ) ) $=
    ( cr wcel cc0 clt wbr wa cle c1 cdiv co wb wi cded wceq breq2 birand breq1
    opreq2 breq2d bi2bid bi2imd biland breq1d ax0re elimel lerec dedth2h ) ACDZ
    BCDZEAFGZEBFGZHZABIGZJBKLZJAKLZIGZMZNEUJAEOZFGZUMHZUTBIGZUPJUTKLZIGZMZNVAEU
    KBEOZFGZHZUTVGIGZJVGKLZVDIGZMZNABEEAUTPZUNVBUSVFVNULVAUMAUTEFQRVNUOVCURVEAU
    TBISVNUQVDUPIAUTJKTUAUBUCBVGPZVBVIVFVMVOUMVHVABVGEFQUDVOVCVJVEVLBVGUTIQVOUP
    VKVDIBVGJKTUEUBUCUTVGAECUFUGBECUFUGUHUI $.
    $( [3-Oct-99] $)

  $( Swap denominator with other side of inequality. $)
  ltdiv23t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( ( 0 < B /\ 0 < C ) ->
                  ( ( A / B ) < C <-> ( A / C ) < B ) ) ) $=
    ( cr wcel cc0 clt wbr wa cdiv co wb wi cded wceq opreq1 breq1d bi2bid
    bilimd breq2 birand opreq2 bi2imd biland ax0re elimel ltdiv23 dedth3h ) ADE
    ZBDEZCDEZFBGHZFCGHZIZABJKZCGHZACJKZBGHZLZMUNUIAFNZBJKZCGHZUTCJKZBGHZLZMABCF
    FFFUJBFNZGHZUMIZUTVFJKZCGHZVCVFGHZLZMVGFUKCFNZGHZIZVIVMGHZUTVMJKZVFGHZLZMAU
    TOZUSVEUNVTUPVBURVDVTUOVACGAUTBJPQVTUQVCBGAUTCJPQRSBVFOZUNVHVEVLWAULVGUMBVF
    FGTUAWAVBVJVDVKWAVAVICGBVFUTJUBQBVFVCGTRUCCVMOZVHVOVLVSWBUMVNVGCVMFGTUDWBVJ
    VPVKVRCVMVIGTWBVCVQVFGCVMUTJUBQRUCUTVFVMAFDUEUFBFDUEUFCFDUEUFUGUH $.
    $( [3-Oct-99] $)

  $( Two nonnegative numbers compare the same as their squares. $)
  le2sqt $p |- ( ( A e. RR /\ B e. RR ) -> ( ( 0 <_ A /\ 0 <_ B ) ->
              ( A <_ B <-> ( A x. A ) <_ ( B x. B ) ) ) ) $=
    ( cr wcel cc0 cle wbr wa cmulc co wb wi cded wceq breq2 birand breq1
    opreq12 anidms breq1d bi2bid bi2imd biland breq2d ax0re elimel le2sq
    dedth2h ) ACDZBCDZEAFGZEBFGZHZABFGZAAIJZBBIJZFGZKZLEUIAEMZFGZULHZUSBFGZUSUS
    IJZUPFGZKZLUTEUJBEMZFGZHZUSVFFGZVCVFVFIJZFGZKZLABEEAUSNZUMVAURVEVMUKUTULAUS
    EFOPVMUNVBUQVDAUSBFQVMUOVCUPFVMUOVCNAUSAUSIRSTUAUBBVFNZVAVHVEVLVNULVGUTBVFE
    FOUCVNVBVIVDVKBVFUSFOVNUPVJVCFVNUPVJNBVFBVFIRSUDUAUBUSVFAECUEUFBECUEUFUGUH
    $.
    $( [3-Aug-99] $)

  ${
    halfpos.1 $e |- A e. RR $.
    $( A positive number is greater than its half. $)
    halfpos $p |- ( 0 < A <-> ( A / ( 1 + 1 ) ) < A ) $=
      ( cc0 clt wbr caddc co c1 cdiv cmulc ax0re ltadd1 recn addid2 1p1times
      cleqcomi breq12i ax1re addrcl mulrcl lt01 addgt0i ltdivi bitr 1cn addcl
      gt0ne0i divcan3 breq2i 3bitr ) CADECAFGZAAFGZDEZAHHFGZIGZUNAJGZUNIGZDEZUO
      ADECAAKBBLUMAUPDEURUKAULUPDAABMZNUPULAUSOPQAUPUNBUNAHHRRSZBTUTHHRRUAUAUBZ
      UCUDUQAUODUNAHHUEUEUFUSUNUTVAUGUHUIUJ $.
      $( [18-May-99] $)
  $}

  ${
    $d x A $.  $d x B $.
    posex.1 $e |- A e. RR $.
    posex.2 $e |- B e. RR $.
    posex.3 $e |- 0 < A $.
    posex.4 $e |- 0 < B $.
    $( There exists a positive number less than two others. $)
    posex $p |- E. x e. RR ( 0 < x /\ ( x < A /\ x < B ) ) $=
      ( clt wbr wceq wo cc0 cv wa cr wrex wn lttri2 biimp orri or12 mpbi c1
      caddc co cdiv wcel halfpos ax1re addrcl lt01 addgt0i gt0ne0i divrcl lttr
      mpan jctil divgt0i breq2 breq1 bi2and rcla4ev syl mpbiri jaoi jctir
      ax-mp ) BCHIZBCJZCBHIZKZKZLAMZHIZVMBHIZVMCHIZNZNZAOPZVIVHVJKZKVLVIVTVIQVT
      BCDERSTVIVHVJUAUBVHVSVKVHBUCUCUDUEZUFUEZOUGZLWBHIZWBBHIZWBCHIZNZNZNVSVHWH
      WCVHWGWDVHWFWEWEVHWFLBHIWEFBDUHUBZWBBCBWADUCUCUIUIUJZWAWJUCUCUIUIUKUKULZU
      MZUNZDEUOUPWIUQBWADWJFWKURUQWMUQVRWHAWBOVMWBJZVNWDVQWGVMWBLHUSWNVOWEVPWFV
      MWBBHUTVMWBCHUTVAVAVBVCVKCWAUFUEZOUGZLWOHIZWOBHIZWOCHIZNZNZNVSVKXAWPVKWTW
      QVKWRWSVIWRVJVIWRWSLCHIWSGCEUHUBZBCWOHUSVDWSVJWRXBWOCBCWAEWJWLUNZEDUOUPVE
      XBVFCWAEWJGWKURUQXCUQVRXAAWOOVMWOJZVNWQVQWTVMWOLHUSXDVOWRVPWSVMWOBHUTVMWO
      CHUTVAVAVBVCVEVG $.
      $( [18-May-99] $)
  $}

