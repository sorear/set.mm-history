$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Decimal representation of numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c 2 $. $( The decimal number 2 $)
  $c 3 $. $( The decimal number 3 $)
  $c 4 $. $( The decimal number 4 $)
  $c 5 $. $( The decimal number 5 $)
  $c 6 $. $( The decimal number 6 $)
  $c 7 $. $( The decimal number 7 $)
  $c 8 $. $( The decimal number 8 $)
  $c 9 $. $( The decimal number 9 $)
  $c 10 $. $( The decimal number 10 $)

  $( Extend class notation to include the number 2. $)
  c2 $a class 2 $.
  $( Extend class notation to include the number 3. $)
  c3 $a class 3 $.
  $( Extend class notation to include the number 4. $)
  c4 $a class 4 $.
  $( Extend class notation to include the number 5. $)
  c5 $a class 5 $.
  $( Extend class notation to include the number 6. $)
  c6 $a class 6 $.
  $( Extend class notation to include the number 7. $)
  c7 $a class 7 $.
  $( Extend class notation to include the number 8. $)
  c8 $a class 8 $.
  $( Extend class notation to include the number 9. $)
  c9 $a class 9 $.
  $( Extend class notation to include the number 10. $)
  c10 $a class 10 $.

  $( Define the number 2.

     Note that the numbers 0 and 1 are constants defined as primitives of the
     complex number axiom system (see ~ df-0 and ~ df-1 ).

     _Note_:  Only the digits 0 through 9 ( ~ df-0 through ~ df-9 ) and the
     number 10 ( ~ df-10 ) are explicitly defined.  Integers can be exhibited
     as sums of powers of 10 or as some other expression built from operations
     on the numbers 0 through 10.  For example, the prime number 823541 can be
     expressed as ` ( 7 ^ 7 ) - 2 ` .  Decimals can be expressed as ratios of
     integers, as in ~ cos2bnd .  (Fortunately, most abstract math rarely
     requires numbers larger than 4.  Even in Wiles' proof of Fermat's Last
     Theorem, the largest number used appears to be 12.)

     A decimal representation of numbers may be added at some point in the
     future if it is deemed useful.  Ideas for a clean, eliminable definition
     are welcome.  (An awkward earlier definition was deleted from the database
     on 18-Sep-1999.) $)
  df-2 $a |- 2 = ( 1 + 1 ) $.

  $( Define the number 3. $)
  df-3 $a |- 3 = ( 2 + 1 ) $.
  $( Define the number 4. $)
  df-4 $a |- 4 = ( 3 + 1 ) $.
  $( Define the number 5. $)
  df-5 $a |- 5 = ( 4 + 1 ) $.
  $( Define the number 6. $)
  df-6 $a |- 6 = ( 5 + 1 ) $.
  $( Define the number 7. $)
  df-7 $a |- 7 = ( 6 + 1 ) $.
  $( Define the number 8. $)
  df-8 $a |- 8 = ( 7 + 1 ) $.
  $( Define the number 9. $)
  df-9 $a |- 9 = ( 8 + 1 ) $.
  $( Define the number 10.  See remarks under ~ df-2 . $)
  df-10 $a |- 10 = ( 9 + 1 ) $.

  $( The number 2 is real. $)
  2re $p |- 2 e. RR $=
    ( c2 c1 caddc co cr df-2 1re readdcli eqeltri ) ABBCDEFBBGGHI $.
    $( [27-May-1999] $)

  $( The number 2 is a complex number. $)
  2cn $p |- 2 e. CC $=
    ( c2 2re recni ) ABC $.
    $( [31-Jul-2004] $) $( [30-Jul-2004] $)

  $( The number 3 is real. $)
  3re $p |- 3 e. RR $=
    ( c3 c2 c1 caddc co cr df-3 2re 1re readdcli eqeltri ) ABCDEFGBCHIJK $.
    $( [27-May-1999] $)

  $( The number 4 is real. $)
  4re $p |- 4 e. RR $=
    ( c4 c3 c1 caddc co cr df-4 3re 1re readdcli eqeltri ) ABCDEFGBCHIJK $.
    $( [27-May-1999] $)

  $( The number 5 is real. $)
  5re $p |- 5 e. RR $=
    ( c5 c4 c1 caddc co cr df-5 4re 1re readdcli eqeltri ) ABCDEFGBCHIJK $.
    $( [27-May-1999] $)

  $( The number 6 is real. $)
  6re $p |- 6 e. RR $=
    ( c6 c5 c1 caddc co cr df-6 5re 1re readdcli eqeltri ) ABCDEFGBCHIJK $.
    $( [27-May-1999] $)

  $( The number 7 is real. $)
  7re $p |- 7 e. RR $=
    ( c7 c6 c1 caddc co cr df-7 6re 1re readdcli eqeltri ) ABCDEFGBCHIJK $.
    $( [27-May-1999] $)

  $( The number 8 is real. $)
  8re $p |- 8 e. RR $=
    ( c8 c7 c1 caddc co cr df-8 7re 1re readdcli eqeltri ) ABCDEFGBCHIJK $.
    $( [27-May-1999] $)

  $( The number 9 is real. $)
  9re $p |- 9 e. RR $=
    ( c9 c8 c1 caddc co cr df-9 8re 1re readdcli eqeltri ) ABCDEFGBCHIJK $.
    $( [27-May-1999] $)

  $( The number 10 is real. $)
  10re $p |- 10 e. RR $=
    ( c10 c9 c1 caddc co cr df-10 9re 1re readdcli eqeltri ) ABCDEFGBCHIJK $.
    $( [6-Feb-2007] $) $( [5-Feb-2007] $)

  $( The number 2 is positive. $)
  2pos $p |- 0 < 2 $=
    ( cc0 c1 caddc co c2 clt 1re lt01 addgt0ii df-2 breqtrri ) ABBCDEFBBGGHHIJK
    $.
    $( [27-May-1999] $)

  $( The number 2 is nonzero. $)
  2ne0 $p |- 2 =/= 0 $=
    ( c2 2re 2pos gt0ne0ii ) ABCD $.
    $( [9-Nov-2007] $) $( [9-Nov-2007] $)

  $( The number 3 is positive. $)
  3pos $p |- 0 < 3 $=
    ( cc0 c2 c1 caddc co c3 clt 2re 1re 2pos lt01 addgt0ii df-3 breqtrri ) ABCD
    EFGBCHIJKLMN $.
    $( [27-May-1999] $)

  $( The number 4 is positive. $)
  4pos $p |- 0 < 4 $=
    ( cc0 c3 c1 caddc co c4 clt 3re 1re 3pos lt01 addgt0ii df-4 breqtrri ) ABCD
    EFGBCHIJKLMN $.
    $( [27-May-1999] $)

  $( The number 5 is positive. $)
  5pos $p |- 0 < 5 $=
    ( cc0 c4 c1 caddc co c5 clt 4re 1re 4pos lt01 addgt0ii df-5 breqtrri ) ABCD
    EFGBCHIJKLMN $.
    $( [27-May-1999] $)

  $( The number 6 is positive. $)
  6pos $p |- 0 < 6 $=
    ( cc0 c5 c1 caddc co c6 clt 5re 1re 5pos lt01 addgt0ii df-6 breqtrri ) ABCD
    EFGBCHIJKLMN $.
    $( [27-May-1999] $)

  $( The number 7 is positive. $)
  7pos $p |- 0 < 7 $=
    ( cc0 c6 c1 caddc co c7 clt 6re 1re 6pos lt01 addgt0ii df-7 breqtrri ) ABCD
    EFGBCHIJKLMN $.
    $( [27-May-1999] $)

  $( The number 8 is positive. $)
  8pos $p |- 0 < 8 $=
    ( cc0 c7 c1 caddc co c8 clt 7re 1re 7pos lt01 addgt0ii df-8 breqtrri ) ABCD
    EFGBCHIJKLMN $.
    $( [27-May-1999] $)

  $( The number 9 is positive. $)
  9pos $p |- 0 < 9 $=
    ( cc0 c8 c1 caddc co c9 clt 8re 1re 8pos lt01 addgt0ii df-9 breqtrri ) ABCD
    EFGBCHIJKLMN $.
    $( [27-May-1999] $)

  $( The number 10 is positive. $)
  10pos $p |- 0 < 10 $=
    ( cc0 c9 c1 caddc co c10 clt 9re 1re 9pos lt01 addgt0ii df-10 breqtrri ) AB
    CDEFGBCHIJKLMN $.
    $( [6-Feb-2007] $) $( [5-Feb-2007] $)

  $( 2 is a natural number. $)
  2nn $p |- 2 e. NN $=
    ( c2 c1 caddc co cn df-2 wcel 1nn nnaddcl mp2an eqeltri ) ABBCDZEFBEGZMLEGH
    HBBIJK $.
    $( [20-Aug-2001] $)

  $( 3 is a natural number. $)
  3nn $p |- 3 e. NN $=
    ( c3 c2 c1 caddc co cn df-3 wcel 2nn 1nn nnaddcl mp2an eqeltri ) ABCDEZFGBF
    HCFHNFHIJBCKLM $.
    $( [1-Feb-2006] $) $( [8-Jan-2006] $)

