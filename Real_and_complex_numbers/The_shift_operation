$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The "shift" operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c shift $.  $( Symbol for function shifter. $)

  $( Extend class notation with function shifter. $)
  cshi $a class shift $.

  ${
    $d x y z f g $.
    $( Define a function shifter.  This operation offsets the value argument of
       a function (ordinarily on a subset of ` CC ` ) and produces a new
       function on ` CC ` .  See ~ shftval for its value. $)
    df-shft $a |- shift = ( f e. _V , x e. CC |->
                  { <. y , z >. | ( y e. CC /\ ( y - x ) f z ) } ) $.
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Two ways to write a shifted set ` ( B + A ) ` . $)
    shftlem $p |- ( ( A e. CC /\ B C_ CC ) ->
          { x e. CC | ( x - A ) e. B } = { x | E. y e. B x = ( y + A ) } ) $=
      ( cc wcel wss wa cv cmin co crab cab caddc wceq wrex ancoms opreq1 sylan
      wi df-rab npcan eqcomd eqeq2d rcla4ev expcom expimpd ssel2 ax-addcl pncan
      syl adantr simplr eqeltrd jca anassrs eleq1d anbi12d syl5ibrcom rexlimdva
      eleq1 impbid abbidv syl5eq ) CEFZDEGZHZAIZCJKZDFZAELVHEFZVJHZAMVHBIZCNKZO
      ZBDPZAMVJAEUAVGVLVPAVGVLVPVEVLVPTVFVEVKVJVPVEVKHZVHVICNKZOZVJVPTVQVRVHVKV
      EVRVHOVHCUBQUCVJVSVPVOVSBVIDVMVIOVNVRVHVMVICNRUDUEUFUKUGULVGVOVLBDVGVMDFZ
      HVLVOVNEFZVNCJKZDFZHZVEVFVTWDVFVTHZVEWDWEVEHZWAWCWEVMEFZVEWADEVMUHZVMCUIS
      WFWBVMDWEWGVEWBVMOWHVMCUJSVFVTVEUMUNUOQUPVOVKWAVJWCVHVNEVAVOVIWBDVHVNCJRU
      QURUSUTVBVCVD $.
      $( [3-Nov-2013] $)

    $( A shift of the upper integers. $)
    shftuz $p |- ( ( A e. ZZ /\ B e. ZZ ) ->
          { x e. CC | ( x - A ) e. ( ZZ>= ` B ) } = ( ZZ>= ` ( B + A ) ) ) $=
      ( cz wcel wa cv cmin co cuz cfv cc crab cab caddc df-rab w3a zcn ancoms
      wi wceq simp2 3ad2ant1 syl2anc eluzadd 3adant2 eqeltrrd 3expib adantr syl
      npcan eluzelz a1i eluzsub 3expia jcad impbid abbi1dv syl5eq ) BDEZCDEZFZA
      GZBHIZCJKEZALMVCLEZVEFZANCBOIZJKZVEALPVBVGAVIVBVGVCVIEZUTVGVJTVAUTVFVEVJU
      TVFVEQZVDBOIZVCVIVKVFBLEZVLVCUAUTVFVEUBUTVFVMVEBRUCVCBUKUDUTVEVLVIEZVFVEU
      TVNBCVDUESUFUGUHUIVBVJVFVEVJVFTVBVJVCDEVFVHVCULVCRUJUMVAUTVJVETVAUTVJVEBC
      VCUNUOSUPUQURUS $.
      $( [5-Nov-2013] $)
  $}

  ${
    $d w x y z A $.  $d w x y z F $.  $d w x y z B $.
    shftfval.1 $e |- F e. _V $.
    $( The value of the sequence shifter operation is a function on ` CC ` .
       ` A ` is ordinarily an integer. $)
    shftfval $p |- ( A e. CC -> ( F shift A ) =
                    { <. x , y >. | ( x e. CC /\ ( x - A ) F y ) } ) $=
      ( vz vw cc wcel cv cmin co wbr wa copab cvv cshi wceq caddc wrex cdm elab
      cab crn cxp oprex breldm npcan eqcomd ancoms opreq1 rcla4ev syl2anr eqeq1
      wss eqeq2d vex rexbidv sylibr brelrn adantl jca ssopab2dv df-xp syl6sseqr
      expl dmex abrexex rnex xpex sylancl anbi2d opabbidv opreq2 breq1d df-shft
      ssexg breq ovmpt2g mp3an1 mpdan ) CHIZAJZHIZWCCKLZBJZDMZNZABOZPIZDCQLWIRZ
      WBWIFJZGJZCSLZRZGDUAZTZFUCZDUDZUEZUOWTPIWJWBWIWCWRIZWFWSIZNZABOWTWBWHXCAB
      WBWDWGXCWBWDNZWGNZXAXBXEWCWNRZGWPTZXAWGWEWPIWCWECSLZRZXGXDWEWFDWCCKUFZUGW
      DWBXIWDWBNXHWCWCCUHUIUJXFXIGWEWPWMWERWNXHWCWMWECSUKUPULUMWQXGFWCAUQWLWCRW
      OXFGWPWLWCWNUNURUBUSWGXBXDWEWFDXJBUQUTVAVBVFVCABWRWSVDVEWRWSGFWPWNDEVGVHD
      EVIVJWIWTPVQVKDPIWBWJWKEFGDCPHWDWCWMKLZWFWLMZNZABOWIQWDXKWFDMZNZABOPWLDRZ
      XMXOABXPXLXNWDXKWFWLDVRVLVMWMCRZXOWHABXQXNWGWDXQXKWEWFDWMCWCKVNVOVLVMGABF
      VPVSVTWA $.
      $( [3-Nov-2013] $) $( [20-Jul-2005] $)

    $( Domain of a relation shifted by ` A ` .  The set on the right is more
       commonly notated as ` ( dom F + A ) ` (meaning add ` A ` to every
       element of ` dom F ` ). $)
    shftdm $p |- ( A e. CC -> dom ( F shift A ) =
                  { x e. CC | ( x - A ) e. dom F } ) $=
      ( vy cc wcel cshi co cdm cv cmin wbr wa copab crab shftfval dmeqd wex cab
      19.42v oprex eldm anbi2i bitr4i abbii dmopab df-rab 3eqtr4i syl6eq ) BFGZ
      CBHIZJAKZFGZUMBLIZEKCMZNZAEOZJZUOCJGZAFPZUKULURAEBCDQRUQESZATUNUTNZATUSVA
      VBVCAVBUNUPESZNVCUNUPEUAUTVDUNEUOCUMBLUBUCUDUEUFUQAEUGUTAFUHUIUJ $.
      $( [3-Nov-2013] $)

    $( Value of a fiber of the relation ` F ` . $)
    shftfib $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( F shift A ) " { B } ) = ( F " { ( B - A ) } ) ) $=
      ( vz vx vy cc wcel wa cv co wbr cab cmin csn cima cvv wb wceq copab breqd
      cshi shftfval vex eleq1 opreq1 breq1d anbi12d breq2 anbi2d brabg sylan9bb
      eqid mpan2 ibar adantl bitr4d abbidv imasng oprex ax-mp a1i 3eqtr4d ) AHI
      ZBHIZJZBEKZCAUCLZMZENZBAOLZVHCMZENZVIBPQZCVLPQZVGVJVMEVGVJVFVMJZVMVEVJBVH
      FKZHIZVRAOLZGKZCMZJZFGUAZMZVFVQVEVIWDBVHFGACDUDUBVFVHRIWEVQSEUEWCVFVLWACM
      ZJVQFGBVHHRWDVRBTZVSVFWBWFVRBHUFWGVTVLWACVRBAOUGUHUIWAVHTWFVMVFWAVHVLCUJU
      KWDUNULUOUMVFVMVQSVEVFVMUPUQURUSVFVOVKTVEEBHVIUTUQVPVNTZVGVLRIWHBAOVAEVLR
      CUTVBVCVD $.
      $( [4-Nov-2013] $)

    $( Functionality and domain of a sequence shifted by ` A ` . $)
    shftfn $p |- ( ( F Fn B /\ A e. CC ) ->
                  ( F shift A ) Fn { x e. CC | ( x - A ) e. B } ) $=
      ( vy vz vw wfn cc wcel wa co wfun cv cmin wceq wbr wmo wi cshi crab copab
      cdm wrel wal relopab a1i fnfun adantr oprex ax-17 breq1 mobid funmo vtocl
      imbi2d immo vex eleq1 opreq1 breq1d anbi12d breq2 anbi2d eqid simprbi mpg
      brab syl dffun6 sylanbrc shftfval adantl funeqd mpbird shftdm fndm eleq2d
      alrimiv rabbidv sylan9eqr df-fn ) DCIZBJKZLZDBUAMZNZWGUDZAOZBPMZCKZAJUBZQ
      WGWMIWFWHWJJKZWKFOZDRZLZAFUCZNZWFWRUEZGOZHOZWRRZHSZGUFZWSWTWFWQAFUGUHWFDN
      ZXEWDXFWECDUIUJXFXDGXFXABPMZXBDRZHSZXDXFWJXBDRZHSZTXFXITAXGXABPUKWJXGQZXK
      XIXFXLXJXHHXLHULWJXGXBDUMUNUQAHDUOUPXCXHTXIXDTHXCXHHURXCXAJKZXHWQXMXGWODR
      ZLXMXHLAFXAXBWRGUSHUSWJXAQZWNXMWPXNWJXAJUTXOWKXGWODWJXABPVAVBVCWOXBQXNXHX
      MWOXBXGDVDVEWRVFVIVGVHVJVTVJGHWRVKVLWFWGWRWEWGWRQWDAFBDEVMVNVOVPWEWDWIWKD
      UDZKZAJUBWMABDEVQWDXQWLAJWDXPCWKCDVRVSWAWBWGWMWCVL $.
      $( [3-Nov-2013] $) $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A ` . $)
    shftval $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( F shift A ) ` B ) = ( F ` ( B - A ) ) ) $=
      ( vx cc wcel wa cshi co csn cima cv wceq cab cuni cmin cfv shftfib df-fv
      eqeq1d abbidv unieqd 3eqtr4g ) AFGBFGHZCAIJZBKLZEMKZNZEOZPCBAQJZKLZUHNZEO
      ZPBUFRUKCRUEUJUNUEUIUMEUEUGULUHABCDSUAUBUCEBUFTEUKCTUD $.
      $( [4-Nov-2013] $) $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A - B ` . $)
    shftval2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( F shift ( A - B ) ) ` ( A + C ) ) = ( F ` ( B + C ) ) ) $=
      ( cc wcel w3a caddc cmin cshi wceq subcl 3adant3 ax-addcl 3adant2 shftval
      co cfv syl2anc pnncan 3com23 addcom 3adant1 eqtr4d fveq2d eqtrd ) AFGZBFG
      ZCFGZHZACIRZDABJRZKRSZULUMJRZDSZBCIRZDSUKUMFGZULFGZUNUPLUHUIURUJABMNUHUJU
      SUIACOPUMULDEQTUKUOUQDUKUOCBIRZUQUHUJUIUOUTLACBUAUBUIUJUQUTLUHBCUCUDUEUFU
      G $.
      $( [5-Nov-2013] $) $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A - B ` . $)
    shftval3 $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( F shift ( A - B ) ) ` A ) = ( F ` B ) ) $=
      ( cc wcel wa cc0 caddc co cmin cshi cfv 0cn shftval2 mp3an3 addid1 adantr
      wceq fveq2d adantl 3eqtr3d ) AEFZBEFZGZAHIJZCABKJLJZMZBHIJZCMZAUGMBCMUCUD
      HEFUHUJSNABHCDOPUEUFAUGUCUFASUDAQRTUEUIBCUDUIBSUCBQUATUB $.
      $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` -u A ` . $)
    shftval4 $p |- ( ( A e. CC /\ B e. CC ) ->
                ( ( F shift -u A ) ` B ) = ( F ` ( A + B ) ) ) $=
      ( cc wcel wa cneg cshi co cfv cmin caddc wceq negcl shftval subneg ancoms
      sylan addcom eqtr4d fveq2d eqtrd ) AEFZBEFZGZBCAHZIJKZBUGLJZCKZABMJZCKUDU
      GEFUEUHUJNAOUGBCDPSUFUIUKCUFUIBAMJZUKUEUDUIULNBAQRABTUAUBUC $.
      $( [5-Nov-2013] $) $( [18-Aug-2005] $)

    $( Value of a shifted sequence. $)
    shftval5 $p |- ( ( A e. CC /\ B e. CC ) ->
                ( ( F shift A ) ` ( B + A ) ) = ( F ` B ) ) $=
      ( cc wcel caddc co cshi wceq wa cmin simpr ax-addcl shftval syl2anc pncan
      cfv fveq2d eqtrd ancoms ) BEFZAEFZBAGHZCAIHRZBCRZJUBUCKZUEUDALHZCRZUFUGUC
      UDEFUEUIJUBUCMBANAUDCDOPUGUHBCBAQSTUA $.
      $( [5-Nov-2013] $) $( [19-Aug-2005] $)

    $d y C $.
    $( Functionality of a shifted sequence. $)
    shftf $p |- ( ( F : B --> C /\ A e. CC ) ->
          ( F shift A ) : { x e. CC | ( x - A ) e. B } --> C ) $=
      ( vy wf cc wcel wa co cv cmin wfn cfv wceq simpr simpl syl2an cshi shftfn
      crab wral ffn sylan opreq1 eleq1d elrab shftval ffvelrn eqeltrd ralrimiva
      sylan2b ffnfv sylanbrc ) CDEHZBIJZKZEBUALZAMZBNLZCJZAIUCZOZGMZUTPZDJZGVDU
      DVDDUTHUQECOURVECDEUEABCEFUBUFUSVHGVDVFVDJUSVFIJZVFBNLZCJZKZVHVCVKAVFIVAV
      FQVBVJCVAVFBNUGUHUIUSVLKVGVJEPZDUSURVIVGVMQVLUQURRVIVKSBVFEFUJTUSUQVKVMDJ
      VLUQURSVIVKRCDVJEUKTULUNUMGVDDUTUOUP $.
      $( [5-Nov-2013] $) $( [19-Aug-2005] $)

    $( Composite shift operations. $)
    2shfti $p |- ( ( A e. CC /\ B e. CC ) -> ( ( F shift A ) shift B ) =
                ( F shift ( A + B ) ) ) $=
      ( vx vy vz vw cc wcel wa cv cmin co cshi wbr copab wb shftfval wceq caddc
      breqd oprex vex eleq1 opreq1 breq1d anbi12d breq2 anbi2d eqid brab syl6bb
      ad2antrr subcl biantrurd ancoms adantll w3a sub32 subsub4 eqtr3d pm5.32da
      3expb 3bitr2d opabbidv adantl ax-addcl syl 3eqtr4d ) AIJZBIJZKZELZIJZVNBM
      NZFLZCAONZPZKZEFQZVOVNABUANZMNZVQCPZKZEFQZVRBONZCWBONZVMVTWEEFVMVOVSWDVMV
      OKZVSVPIJZVPAMNZVQCPZKZWLWDVKVSWMRVLVOVKVSVPVQGLZIJZWNAMNZHLZCPZKZGHQZPWM
      VKVRWTVPVQGHACDSUBWSWJWKWQCPZKWMGHVPVQWTVNBMUCFUDWNVPTZWOWJWRXAWNVPIUEXBW
      PWKWQCWNVPAMUFUGUHWQVQTXAWLWJWQVQWKCUIUJWTUKULUMUNVLVOWLWMRZVKVOVLXCVOVLK
      WJWLVNBUOUPUQURWIWKWCVQCVOVMWKWCTZVOVKVLXDVOVKVLUSVNAMNBMNWKWCVNABUTVNABV
      AVBVDUQUGVEVCVFVLWGWATVKEFBVRCAOUCSVGVMWBIJWHWFTABVHEFWBCDSVIVJ $.
      $( [5-Nov-2013] $) $( [19-Aug-2005] $)

    $( Identity law for the shift operation. $)
    shftidt2 $p |- ( F shift 0 ) = ( F |` CC ) $=
      ( vx vy cv cc wcel cc0 cmin co wbr copab cshi cres subid1 pm5.32i opabbii
      wa breq1d wceq 0cn shftfval ax-mp dfres2 3eqtr4i ) CEZFGZUFHIJZDEZAKZRZCD
      LZUGUFUIAKZRZCDLAHMJZAFNUKUNCDUGUJUMUGUHUFUIAUFOSPQHFGUOULTUACDHABUBUCCDF
      AUDUE $.
      $( [5-Nov-2013] $)

    $( Identity law for the shift operation. $)
    shftidt $p |- ( A e. CC -> ( ( F shift 0 ) ` A ) = ( F ` A ) ) $=
      ( cc wcel cc0 cshi co cfv cres shftidt2 fveq1i fvres syl5eq ) ADEABFGHZIA
      BDJZIABIAOPBCKLADBMN $.
      $( [5-Nov-2013] $) $( [19-Aug-2005] $)

    $( Cancellation law for the shift operation. $)
    shftcan1 $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( ( F shift A ) shift -u A ) ` B ) = ( F ` B ) ) $=
      ( cc wcel cshi co cneg cfv cc0 caddc wceq negcl mpdan negid opreq2d eqtrd
      2shfti fveq1d shftidt sylan9eq ) AEFZBEFBCAGHAIZGHZJBCKGHZJBCJUCBUEUFUCUE
      CAUDLHZGHZUFUCUDEFUEUHMANAUDCDSOUCUGKCGAPQRTBCDUAUB $.
      $( [5-Nov-2013] $) $( [4-Aug-2005] $)

    $( Cancellation law for the shift operation. $)
    shftcan2 $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( ( F shift -u A ) shift A ) ` B ) = ( F ` B ) ) $=
      ( cc wcel wa cneg cshi co cfv negneg adantr opreq2d fveq1d negcl shftcan1
      wceq sylan eqtr3d ) AEFZBEFZGZBCAHZIJZUDHZIJZKZBUEAIJZKBCKZUCBUGUIUCUFAUE
      IUAUFARUBALMNOUAUDEFUBUHUJRAPUDBCDQST $.
      $( [5-Nov-2013] $) $( [4-Aug-2005] $)
  $}

  ${
    $d j k n S $.  $d j k n F $.
    seq1shftid.1 $e |- S e. _V $.
    seq1shftid.2 $e |- F e. _V $.
    $( Identity law for the shift operation in a 1-based sequence builder. $)
    seq1shftid $p |- ( S seq1 ( F shift 0 ) ) = ( S seq1 F ) $=
      ( vn vj vk cc0 cshi co cseq1 wceq cv cfv cn c1 fveq2 eqeq12d cc wcel wral
      caddc ax-1cn shftidt ax-mp oprex seq11 3eqtr4i wa opreq1 peano2cn opreq2d
      nncn syl sylan9eqr seq1p1 adantr 3eqtr4d ex nnind rgen wfn seq1fn eqfnfv2
      wb mp2an mpbir ) ABHIJZKJZABKJZLZEMZVINZVLVJNZLZEOUAZVOEOFMZVINZVQVJNZLPV
      INZPVJNZLGMZVINZWBVJNZLZWBPUBJZVINZWFVJNZLZVOFGVLVQPLVRVTVSWAVQPVIQVQPVJQ
      RVQWBLVRWCVSWDVQWBVIQVQWBVJQRVQWFLVRWGVSWHVQWFVIQVQWFVJQRVQVLLVRVMVSVNVQV
      LVIQVQVLVJQRPVHNZPBNZVTWAPSTWJWKLUCPBDUDUEAVHCBHIUFZUGABCDUGUHWBOTZWEWIWM
      WEUIWCWFVHNZAJZWDWFBNZAJZWGWHWEWMWOWDWNAJWQWCWDWNAUJWMWNWPWDAWMWFSTZWNWPL
      WMWBSTWRWBUMWBUKUNWFBDUDUNULUOWMWGWOLWEWBAVHCWLUPUQWMWHWQLWEWBABCDUPUQURU
      SUTVAVIOVBVJOVBVKVPVEAVHCWLVCABCDVCEOVIVJVDVFVG $.
      $( [1-Sep-2005] $)
  $}

  ${
    $d F x y z $.  $d M x y z $.  $d N x y z $.  $d P x y z $.
    seqshft.1 $e |- F e. _V $.
    $( Shifting the index set of a sequence.  (Contributed by Mario Carneiro,
       2-Jul-2013.) $)
    seqshft $p |- ( ( M e. ZZ /\ N e. ZZ ) -> seq M ( P , ( F shift N ) ) =
        ( seq ( M - N ) ( P , F ) shift N ) ) $=
      ( vz vy vx cz wcel co cmin wceq cfv cuz caddc cc zcn syl2an wfn cshi cseq
      wa cv wral cfz simpr znegcl ad2antlr elfzelz shftval negsub ancoms fveq2d
      cneg syl eqtr4d ralrimiva seqshft2 syl3anc adantr seqeq1d eluzelz syl2anr
      adantl fveq12d eqtrd uzssz zsscn sstri sseli seqex wb seqfn zsubcl shftfn
      crab syl2anc shftuz npcan fneq2d mpbid eqfnfv2 mpbird ) CIJZDIJZUCZABDUAK
      ZCUBZABCDLKZUBZDUAKZMZFUDZWINZWNWLNZMZFCONZUEZWGWQFWRWGWNWRJZUCZWOWNDLKZW
      KNZWPXAWOWNDUOZPKZABCXDPKZUBZNZXCXAWTXDIJZGUDZWHNZXJXDPKZBNZMZGCWNUFKZUEZ
      WOXHMWGWTUGWFXIWEWTDUHUIWFXPWEWTWFXNGXOWFDQJZXJQJZXNXJXOJZDRZXSXJIJXRXJCW
      NUJXJRUPXQXRUCZXKXJDLKZBNXMDXJBEUKYAXLYBBXRXQXLYBMXJDULUMUNUQSURUIAGWHBXD
      CWNUSUTXAXEXBXGWKXAXFWJABWGXFWJMZWTWECQJZXQYCWFCRZXTCDULSVAVBWTWNQJZXQXEX
      BMWGWTWNIJYFCWNVCWNRUPWFXQWEXTVEZWNDULVDVFVGWGXQYFWPXCMWTYGWRQWNWRIQCVHVI
      VJVKDWNWKABWJVLZUKSUQURWGWIWRTZWLWRTZWMWSVMWEYIWFAWHCVNVAWGWLHUDDLKWJONZJ
      HQVQZTZYJWGWKYKTZXQYMWGWJIJZYNCDVOZABWJVNUPYGHDYKWKYHVPVRWGYLWRWLWGYLWJDP
      KZONZWRWGWFYOYLYRMWEWFUGYPHDWJVSVRWGYQCOWEYDXQYQCMWFYEXTCDVTSUNVGWAWBFWRW
      IWLWCVRWD $.
      $( [27-Feb-2014] $) $( [17-Mar-2005] $)
  $}

