$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The "shift" operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c shift $.  $( Symbol for function shifter. $)

  $( Extend class notation with function shifter. $)
  cshi $a class shift $.

  ${
    $d x y z f g $.
    $( Define a function shifter.  This operation offsets the value argument of
       a function (ordinarily on a subset of ` CC ` ) and produces a new
       function on ` CC ` .  See ~ shftval for its value. $)
    df-shft $a |- shift = ( f e. _V , x e. CC |->
                  { <. y , z >. | ( y e. CC /\ ( y - x ) f z ) } ) $.
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Two ways to write a shifted set ` ( B + A ) ` . $)
    shftlem $p |- ( ( A e. CC /\ B C_ CC ) ->
          { x e. CC | ( x - A ) e. B } = { x | E. y e. B x = ( y + A ) } ) $=
      ( cc wcel wss wa cv cmin co crab cab caddc wceq wrex df-rab wi npcan
      ancoms eqcomd opreq1 eqeq2d rcla4ev expcom syl expimpd adantr ssel2
      ax-addcl sylan pncan simplr eqeltrd jca anassrs eleq1 eleq1d anbi12d
      syl5ibrcom r19.23adva impbid abbidv syl5eq ) CEFZDEGZHZAIZCJKZDFZAELVHEFZ
      VJHZAMVHBIZCNKZOZBDPZAMVJAEQVGVLVPAVGVLVPVEVLVPRVFVEVKVJVPVEVKHZVHVICNKZO
      ZVJVPRVQVRVHVKVEVRVHOVHCSTUAVJVSVPVOVSBVIDVMVIOVNVRVHVMVICNUBUCUDUEUFUGUH
      VGVOVLBDVGVMDFZHVLVOVNEFZVNCJKZDFZHZVEVFVTWDVFVTHZVEWDWEVEHZWAWCWEVMEFZVE
      WADEVMUIZVMCUJUKWFWBVMDWEWGVEWBVMOWHVMCULUKVFVTVEUMUNUOTUPVOVKWAVJWCVHVNE
      UQVOVIWBDVHVNCJUBURUSUTVAVBVCVD $.
      $( [3-Nov-2013] $)

    $( A shift of the upper integers. $)
    shftuz $p |- ( ( A e. ZZ /\ B e. ZZ ) ->
          { x e. CC | ( x - A ) e. ( ZZ>= ` B ) } = ( ZZ>= ` ( B + A ) ) ) $=
      ( cz wcel wa cv cmin co cuz cfv cc crab cab caddc df-rab wi w3a wceq
      simp2 zcn 3ad2ant1 npcan syl11anc eluzadd ancoms 3adant2 eqeltrrd 3expib
      adantr eluzelz syl a1i eluzsub 3expia jcad impbid abbi1dv syl5eq ) BDEZCD
      EZFZAGZBHIZCJKEZALMVCLEZVEFZANCBOIZJKZVEALPVBVGAVIVBVGVCVIEZUTVGVJQVAUTVF
      VEVJUTVFVERZVDBOIZVCVIVKVFBLEZVLVCSUTVFVETUTVFVMVEBUAUBVCBUCUDUTVEVLVIEZV
      FVEUTVNBCVDUEUFUGUHUIUJVBVJVFVEVJVFQVBVJVCDEVFVHVCUKVCUAULUMVAUTVJVEQVAUT
      VJVEBCVCUNUOUFUPUQURUS $.
      $( [5-Nov-2013] $)
  $}

  ${
    $d w x y z A $.  $d w x y z F $.  $d w x y z B $.
    shftfval.1 $e |- F e. _V $.
    $( The value of the sequence shifter operation is a function on ` CC ` .
       ` A ` is ordinarily an integer. $)
    shftfval $p |- ( A e. CC -> ( F shift A ) =
                    { <. x , y >. | ( x e. CC /\ ( x - A ) F y ) } ) $=
      ( vz vw cc wcel cv cmin co wbr wa copab cvv cshi wceq caddc cdm wrex cab
      crn cxp wss wi wal oprex breldm npcan eqcomd ancoms opreq1 eqeq2d
      rcla4ev syl2anc visset eqeq1 rexbidv elab sylibr brelrn adantl jca expl
      19.21aivv ssopab2 syl df-xp syl6sseqr dmex abrexex rnex xpex ssexg
      sylancl breq anbi2d opabbidv opreq2 breq1d df-shft ovmpt2g mp3an1 mpdan
      ) CHIZAJZHIZWGCKLZBJZDMZNZABOZPIZDCQLWMRZWFWMFJZGJZCSLZRZGDTZUAZFUBZDUCZU
      DZUEXDPIWNWFWMWGXBIZWJXCIZNZABOZXDWFWLXGUFZBUGAUGWMXHUEWFXIABWFWHWKXGWFWH
      NZWKNZXEXFXKWGWRRZGWTUAZXEWKWIWTIWGWICSLZRZXMXJWIWJDWGCKUHZUIWHWFXOWHWFNX
      NWGWGCUJUKULXLXOGWIWTWQWIRWRXNWGWQWICSUMUNUOUPXAXMFWGAUQWPWGRWSXLGWTWPWGW
      RURUSUTVAWKXFXJWIWJDXPBUQVBVCVDVEVFWLXGABVGVHABXBXCVIVJXBXCGFWTWRDEVKVLDE
      VMVNWMXDPVOVPDPIWFWNWOEFGDCPHWHWGWQKLZWJWPMZNZABOWMQWHXQWJDMZNZABOPWPDRZX
      SYAABYBXRXTWHXQWJWPDVQVRVSWQCRZYAWLABYCXTWKWHYCXQWIWJDWQCWGKVTWAVRVSGABFW
      BWCWDWE $.
      $( [3-Nov-2013] $) $( [20-Jul-2005] $)

    $( Domain of a relation shifted by ` A ` .  The set on the right is more
       commonly notated as ` ( dom F + A ) ` (meaning add ` A ` to every
       element of ` dom F ` ). $)
    shftdm $p |- ( A e. CC -> dom ( F shift A ) =
                  { x e. CC | ( x - A ) e. dom F } ) $=
      ( vy cc wcel cshi co cdm cv cmin wbr wa copab crab shftfval dmeqd wex
      cab 19.42v oprex eldm anbi2i bitr4i abbii dmopab df-rab 3eqtr4i syl6eq )
      BFGZCBHIZJAKZFGZUMBLIZEKCMZNZAEOZJZUOCJGZAFPZUKULURAEBCDQRUQESZATUNUTNZAT
      USVAVBVCAVBUNUPESZNVCUNUPEUAUTVDUNEUOCUMBLUBUCUDUEUFUQAEUGUTAFUHUIUJ $.
      $( [3-Nov-2013] $)

    $( Value of a fiber of the relation ` F ` . $)
    shftfib $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( F shift A ) " { B } ) = ( F " { ( B - A ) } ) ) $=
      ( vz vx vy cc wcel wa cv cshi co wbr cab cmin csn cima copab shftfval
      breqd cvv wb visset wceq eleq1 opreq1 breq1d anbi12d breq2 anbi2d eqid
      brabg mpan2 sylan9bb ibar adantl bitr4d abbidv imasng oprex ax-mp a1i
      3eqtr4d ) AHIZBHIZJZBEKZCALMZNZEOZBAPMZVHCNZEOZVIBQRZCVLQRZVGVJVMEVGVJVFV
      MJZVMVEVJBVHFKZHIZVRAPMZGKZCNZJZFGSZNZVFVQVEVIWDBVHFGACDTUAVFVHUBIWEVQUCE
      UDWCVFVLWACNZJVQFGBVHHUBWDVRBUEZVSVFWBWFVRBHUFWGVTVLWACVRBAPUGUHUIWAVHUEW
      FVMVFWAVHVLCUJUKWDULUMUNUOVFVMVQUCVEVFVMUPUQURUSVFVOVKUEVEEBHVIUTUQVPVNUE
      ZVGVLUBIWHBAPVAEVLUBCUTVBVCVD $.
      $( [4-Nov-2013] $)

    $( Functionality and domain of a sequence shifted by ` A ` . $)
    shftfn $p |- ( ( F Fn B /\ A e. CC ) ->
                  ( F shift A ) Fn { x e. CC | ( x - A ) e. B } ) $=
      ( vy vz vw wfn cc wcel wa cshi co wfun cdm cv cmin crab wceq wbr copab
      wrel wmo wal relopab a1i fnfun adantr wi oprex ax-17 breq1 mobid imbi2d
      funmo vtocl immo visset eleq1 opreq1 breq1d anbi12d breq2 anbi2d eqid
      brab simprbi mpg syl 19.21aiv dffun6 sylanbrc shftfval adantl funeqd
      mpbird shftdm fndm eleq2d rabbidv sylan9eqr df-fn ) DCIZBJKZLZDBMNZOZWGPZ
      AQZBRNZCKZAJSZTWGWMIWFWHWJJKZWKFQZDUAZLZAFUBZOZWFWRUCZGQZHQZWRUAZHUDZGUEZ
      WSWTWFWQAFUFUGWFDOZXEWDXFWECDUHUIXFXDGXFXABRNZXBDUAZHUDZXDXFWJXBDUAZHUDZU
      JXFXIUJAXGXABRUKWJXGTZXKXIXFXLXJXHHXLHULWJXGXBDUMUNUOAHDUPUQXCXHUJXIXDUJH
      XCXHHURXCXAJKZXHWQXMXGWODUAZLXMXHLAFXAXBWRGUSHUSWJXATZWNXMWPXNWJXAJUTXOWK
      XGWODWJXABRVAVBVCWOXBTXNXHXMWOXBXGDVDVEWRVFVGVHVIVJVKVJGHWRVLVMWFWGWRWEWG
      WRTWDAFBDEVNVOVPVQWEWDWIWKDPZKZAJSWMABDEVRWDXQWLAJWDXPCWKCDVSVTWAWBWGWMWC
      VM $.
      $( [3-Nov-2013] $) $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A ` . $)
    shftval $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( F shift A ) ` B ) = ( F ` ( B - A ) ) ) $=
      ( vx cc wcel wa cshi co csn cima cv wceq cab cuni cmin cfv shftfib
      eqeq1d abbidv unieqd df-fv 3eqtr4g ) AFGBFGHZCAIJZBKLZEMKZNZEOZPCBAQJZKLZ
      UHNZEOZPBUFRUKCRUEUJUNUEUIUMEUEUGULUHABCDSTUAUBEBUFUCEUKCUCUD $.
      $( [4-Nov-2013] $) $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A - B ` . $)
    shftval2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( F shift ( A - B ) ) ` ( A + C ) ) = ( F ` ( B + C ) ) ) $=
      ( cc wcel w3a caddc co cmin cshi cfv wceq subcl 3adant3 ax-addcl 3adant2
      shftval syl11anc pnncan 3com23 addcom 3adant1 eqtr4d fveq2d eqtrd ) AFGZB
      FGZCFGZHZACIJZDABKJZLJMZULUMKJZDMZBCIJZDMUKUMFGZULFGZUNUPNUHUIURUJABOPUHU
      JUSUIACQRUMULDESTUKUOUQDUKUOCBIJZUQUHUJUIUOUTNACBUAUBUIUJUQUTNUHBCUCUDUEU
      FUG $.
      $( [5-Nov-2013] $) $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A - B ` . $)
    shftval3 $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( F shift ( A - B ) ) ` A ) = ( F ` B ) ) $=
      ( cc wcel wa cc0 caddc co cmin cshi cfv wceq 0cn shftval2 mp3an3 addid1
      adantr fveq2d adantl 3eqtr3d ) AEFZBEFZGZAHIJZCABKJLJZMZBHIJZCMZAUGMBCMUC
      UDHEFUHUJNOABHCDPQUEUFAUGUCUFANUDARSTUEUIBCUDUIBNUCBRUATUB $.
      $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` -u A ` . $)
    shftval4 $p |- ( ( A e. CC /\ B e. CC ) ->
                ( ( F shift -u A ) ` B ) = ( F ` ( A + B ) ) ) $=
      ( cc wcel wa cneg cshi co cfv cmin caddc wceq negcl shftval sylan subneg
      ancoms addcom eqtr4d fveq2d eqtrd ) AEFZBEFZGZBCAHZIJKZBUGLJZCKZABMJZCKUD
      UGEFUEUHUJNAOUGBCDPQUFUIUKCUFUIBAMJZUKUEUDUIULNBARSABTUAUBUC $.
      $( [5-Nov-2013] $) $( [18-Aug-2005] $)

    $( Value of a shifted sequence. $)
    shftval5 $p |- ( ( A e. CC /\ B e. CC ) ->
                ( ( F shift A ) ` ( B + A ) ) = ( F ` B ) ) $=
      ( cc wcel caddc co cshi cfv wceq wa cmin simpr ax-addcl shftval syl11anc
      pncan fveq2d eqtrd ancoms ) BEFZAEFZBAGHZCAIHJZBCJZKUBUCLZUEUDAMHZCJZUFUG
      UCUDEFUEUIKUBUCNBAOAUDCDPQUGUHBCBARSTUA $.
      $( [5-Nov-2013] $) $( [19-Aug-2005] $)

    $d y C $.
    $( Functionality of a shifted sequence. $)
    shftf $p |- ( ( F : B --> C /\ A e. CC ) ->
          ( F shift A ) : { x e. CC | ( x - A ) e. B } --> C ) $=
      ( vy wf cc wcel wa cshi co cv cmin crab wfn cfv wral ffn shftfn sylan
      wceq opreq1 eleq1d elrab simpr simpl shftval syl2an ffvelrn eqeltrd
      sylan2b r19.21aiva ffnfv sylanbrc ) CDEHZBIJZKZEBLMZANZBOMZCJZAIPZQZGNZUT
      RZDJZGVDSVDDUTHUQECQURVECDETABCEFUAUBUSVHGVDVFVDJUSVFIJZVFBOMZCJZKZVHVCVK
      AVFIVAVFUCVBVJCVAVFBOUDUEUFUSVLKVGVJERZDUSURVIVGVMUCVLUQURUGVIVKUHBVFEFUI
      UJUSUQVKVMDJVLUQURUHVIVKUGCDVJEUKUJULUMUNGVDDUTUOUP $.
      $( [5-Nov-2013] $) $( [19-Aug-2005] $)

    $( Composite shift operations. $)
    2shfti $p |- ( ( A e. CC /\ B e. CC ) -> ( ( F shift A ) shift B ) =
                ( F shift ( A + B ) ) ) $=
      ( vx vy vz vw cc wcel wa cv cmin co cshi wbr copab caddc wb shftfval
      breqd oprex visset wceq eleq1 opreq1 breq1d anbi12d breq2 anbi2d eqid
      brab syl6bb ad2antrr subcl biantrurd ancoms adantll w3a sub23 subsub4
      eqtr3d 3expb 3bitr2d pm5.32da opabbidv adantl ax-addcl syl 3eqtr4d ) AIJZ
      BIJZKZELZIJZVNBMNZFLZCAONZPZKZEFQZVOVNABRNZMNZVQCPZKZEFQZVRBONZCWBONZVMVT
      WEEFVMVOVSWDVMVOKZVSVPIJZVPAMNZVQCPZKZWLWDVKVSWMSVLVOVKVSVPVQGLZIJZWNAMNZ
      HLZCPZKZGHQZPWMVKVRWTVPVQGHACDTUAWSWJWKWQCPZKWMGHVPVQWTVNBMUBFUCWNVPUDZWO
      WJWRXAWNVPIUEXBWPWKWQCWNVPAMUFUGUHWQVQUDXAWLWJWQVQWKCUIUJWTUKULUMUNVLVOWL
      WMSZVKVOVLXCVOVLKWJWLVNBUOUPUQURWIWKWCVQCVOVMWKWCUDZVOVKVLXDVOVKVLUSVNAMN
      BMNWKWCVNABUTVNABVAVBVCUQUGVDVEVFVLWGWAUDVKEFBVRCAOUBTVGVMWBIJWHWFUDABVHE
      FWBCDTVIVJ $.
      $( [5-Nov-2013] $) $( [19-Aug-2005] $)

    $( Identity law for the shift operation. $)
    shftidt2 $p |- ( F shift 0 ) = ( F |` CC ) $=
      ( vx vy cv cc wcel cc0 cmin co wbr wa copab cshi cres subid1 breq1d
      pm5.32i opabbii wceq 0cn shftfval ax-mp dfres2 3eqtr4i ) CEZFGZUFHIJZDEZA
      KZLZCDMZUGUFUIAKZLZCDMAHNJZAFOUKUNCDUGUJUMUGUHUFUIAUFPQRSHFGUOULTUACDHABU
      BUCCDFAUDUE $.
      $( [5-Nov-2013] $)

    $( Identity law for the shift operation. $)
    shftidt $p |- ( A e. CC -> ( ( F shift 0 ) ` A ) = ( F ` A ) ) $=
      ( cc wcel cc0 cshi co cfv cres shftidt2 fveq1i fvres syl5eq ) ADEABFGHZIA
      BDJZIABIAOPBCKLADBMN $.
      $( [5-Nov-2013] $) $( [19-Aug-2005] $)

    $( Cancellation law for the shift operation. $)
    shftcan1 $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( ( F shift A ) shift -u A ) ` B ) = ( F ` B ) ) $=
      ( cc wcel cshi co cneg cfv cc0 caddc wceq negcl 2shfti mpdan negid
      opreq2d eqtrd fveq1d shftidt sylan9eq ) AEFZBEFBCAGHAIZGHZJBCKGHZJBCJUCBU
      EUFUCUECAUDLHZGHZUFUCUDEFUEUHMANAUDCDOPUCUGKCGAQRSTBCDUAUB $.
      $( [5-Nov-2013] $) $( [4-Aug-2005] $)

    $( Cancellation law for the shift operation. $)
    shftcan2 $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( ( F shift -u A ) shift A ) ` B ) = ( F ` B ) ) $=
      ( cc wcel wa cneg cshi co cfv wceq negneg adantr opreq2d fveq1d negcl
      shftcan1 sylan eqtr3d ) AEFZBEFZGZBCAHZIJZUDHZIJZKZBUEAIJZKBCKZUCBUGUIUCU
      FAUEIUAUFALUBAMNOPUAUDEFUBUHUJLAQUDBCDRST $.
      $( [5-Nov-2013] $) $( [4-Aug-2005] $)
  $}

  ${
    $d j k n S $.  $d j k n F $.
    seq1shftid.1 $e |- S e. _V $.
    seq1shftid.2 $e |- F e. _V $.
    $( Identity law for the shift operation in a 1-based sequence builder. $)
    seq1shftid $p |- ( S seq1 ( F shift 0 ) ) = ( S seq1 F ) $=
      ( vn vj vk cc0 cshi co cseq1 wceq cv cfv cn wral c1 caddc fveq2 eqeq12d
      cc wcel ax-1cn shftidt ax-mp oprex seq11 3eqtr4i wa opreq1 nncn peano2cn
      syl opreq2d sylan9eqr seq1p1 adantr 3eqtr4d ex nnind rgen wfn wb seq1fn
      eqfnfv2 mp2an mpbir ) ABHIJZKJZABKJZLZEMZVINZVLVJNZLZEOPZVOEOFMZVINZVQVJN
      ZLQVINZQVJNZLGMZVINZWBVJNZLZWBQRJZVINZWFVJNZLZVOFGVLVQQLVRVTVSWAVQQVISVQQ
      VJSTVQWBLVRWCVSWDVQWBVISVQWBVJSTVQWFLVRWGVSWHVQWFVISVQWFVJSTVQVLLVRVMVSVN
      VQVLVISVQVLVJSTQVHNZQBNZVTWAQUAUBWJWKLUCQBDUDUEAVHCBHIUFZUGABCDUGUHWBOUBZ
      WEWIWMWEUIWCWFVHNZAJZWDWFBNZAJZWGWHWEWMWOWDWNAJWQWCWDWNAUJWMWNWPWDAWMWFUA
      UBZWNWPLWMWBUAUBWRWBUKWBULUMWFBDUDUMUNUOWMWGWOLWEWBAVHCWLUPUQWMWHWQLWEWBA
      BCDUPUQURUSUTVAVIOVBVJOVBVKVPVCAVHCWLVDABCDVDEOVIVJVEVFVG $.
      $( [1-Sep-2005] $)
  $}

  ${
    $d F x y z $.  $d M x y z $.  $d N x y z $.  $d P x y z $.
    seqshft.1 $e |- F e. _V $.
    $( Shifting the index set of a sequence.  (Contributed by Mario Carneiro,
       2-Jul-2013.) $)
    seqshft $p |- ( ( M e. ZZ /\ N e. ZZ ) -> seq M ( P , ( F shift N ) ) =
        ( seq ( M - N ) ( P , F ) shift N ) ) $=
      ( vz vx vy cz wcel wa cshi co cseq cmin wceq cv cfv cuz wral cc zcn
      adantl uzssz zsscn sstri sseli seqex shftval syl2an wi c1 caddc opreq1
      fveq2d fveq2 eqeq12d imbi2d syl2anc seq1NEW adantr zsubcl syl 3eqtr4rd
      a1i simplr npcan eleq2d biimpar eluzsub syl111anc seqp1 eluzelz ax-1cn
      ad2antlr addsub peano2uz 3syl eqtrd opreq2d 3eqtr4d syl5ibr expcom a2d
      uzind4 impcom eqtr2d r19.21aiva wfn wb seqfn crab shftfn syl11anc simpr
      shftuz fneq2d mpbid eqfnfv2 mpbird ) CIJZDIJZKZABDLMZCNZABCDOMZNZDLMZPZFQ
      ZXERZXJXHRZPZFCSRZTZXCXMFXNXCXJXNJZKXLXJDOMZXGRZXKXCDUAJZXJUAJXLXRPXPXBXS
      XADUBZUCZXNUAXJXNIUACUDUEUFUGDXJXGABXFUHZUIUJXPXCXRXKPZXCGQZDOMZXGRZYDXER
      ZPZUKXCXFXGRZCXERZPZUKZXCHQZDOMZXGRZYMXERZPZUKXCYMULUMMZDOMZXGRZYRXERZPZU
      KXCYCUKGHCXJYDCPZYHYKXCUUCYFYIYGYJUUCYEXFXGYDCDOUNUOYDCXEUPUQURYDYMPZYHYQ
      XCUUDYFYOYGYPUUDYEYNXGYDYMDOUNUOYDYMXEUPUQURYDYRPZYHUUBXCUUEYFYTYGUUAUUEY
      EYSXGYDYRDOUNUOYDYRXEUPUQURYDXJPZYHYCXCUUFYFXRYGXKUUFYEXQXGYDXJDOUNUOYDXJ
      XEUPUQURYLXAXCCXDRZXFBRZYJYIXBXSCUAJZUUGUUHPXAXTCUBZDCBEUIUSXAYJUUGPXBAXD
      CUTVAXCXFIJZYIUUHPCDVBZABXFUTVCVDVEYMXNJZXCYQUUBXCUUMYQUUBUKYQUUBXCUUMKZY
      OYRXDRZAMZYPUUOAMZPYOYPUUOAUNUUNYTUUPUUAUUQUUNYNULUMMZXGRZYOUURBRZAMZYTUU
      PUUNYNXFSRZJZUUSUVAPUUNUUKXBYMXFDUMMZSRZJZUVCXCUUKUUMUULVAXAXBUUMVFXCUVFU
      UMXCUVEXNYMXCUVDCSXAUUIXSUVDCPXBUUJXTCDVGUJUOZVHVIDXFYMVJVKABXFYNVLVCUUNY
      SUURXGUUNYMUAJZULUAJZXSYSUURPUUMUVHXCUUMYMIJUVHCYMVMYMUBVCUCUVIUUNVNVEXBX
      SXAUUMXTVOYMULDVPVKZUOUUNUUOUUTYOAUUNUUOYSBRZUUTXCXSYRUAJZUUOUVKPUUMYAUUM
      YRXNJYRIJUVLCYMVQCYRVMYRUBVRDYRBEUIUJUUNYSUURBUVJUOVSVTWAUUMUUAUUQPXCAXDC
      YMVLUCUQWBWCWDWEWFWGWHXCXEXNWIZXHXNWIZXIXOWJXAUVMXBAXDCWKVAXCXHYEUVBJGUAW
      LZWIZUVNXCXGUVBWIZXSUVPXCUUKUVQUULABXFWKVCYAGDUVBXGYBWMWNXCUVOXNXHXCUVOUV
      EXNXCXBUUKUVOUVEPXAXBWOUULGDXFWPWNUVGVSWQWRFXNXEXHWSWNWT $.
      $( [5-Nov-2013] $) $( [17-Mar-2005] $)
  $}

