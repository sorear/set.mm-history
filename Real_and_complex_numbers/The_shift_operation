$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The "shift" operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c shift $.  $( Symbol for function shifter. $)

  $( Extend class notation with function shifter. $)
  cshi $a class shift $.

  ${
    $d x y z f g $.
    $( Define a function shifter.  This operation offsets the value argument of
       a function (ordinarily on a subset of ` CC ` ) and produces a new
       function on ` CC ` .  See ~ shftval for its value. $)
    df-shft $a |- shift = { <. <. f , x >. , g >. | g =
                  { <. y , z >. | ( y e. CC /\ z = ( f ` ( y - x ) ) ) } } $.
  $}

  ${
    $d x y w f g A $.  $d x y w f g F $.  $d x y B $.  $d x y C $.
    shftfval.1 $e |- F e. _V $.
    $( The value of the sequence shifter operation is a function on ` CC ` .
       ` A ` is ordinarily an integer. $)
    shftfval $p |- ( A e. B -> ( F shift A ) = { <. x , y >. | ( x e. CC /\
                   y = ( F ` ( x - A ) ) ) } ) $=
      ( vf vw vg cvv wcel cshi co cv cc cmin cfv wceq wa copab axcnex opabex2
      fveq1 eqeq2d anbi2d opabbidv opreq2 fveq2d df-shft oprabval5 mpan ) EJKCD
      KECLMANZOKZBNZULCPMZEQZRZSZABTZRFGHIECJDUMUNULHNZPMZGNZQZRZSZABTUSLUMUNVA
      EQZRZSZABTABOUPUAUBVBERZVEVHABVIVDVGUMVIVCVFUNVAVBEUCUDUEUFUTCRZVHURABVJV
      GUQUMVJVFUPUNVJVAUOEUTCULPUGUHUDUEUFHABGIUIUJUK $.
      $( [20-Jul-2005] $)

    $( Functionality and domain of a sequence shifted by ` A ` . $)
    shftfn $p |- ( A e. B -> ( F shift A ) Fn CC ) $=
      ( vx vy wcel cshi co cc wfn cv cmin cfv wceq wa copab fvex eqid fnopab2
      shftfval fneq1d mpbiri ) ABGZCAHIZJKELZJGFLUFAMIZCNZOPEFQZJKEFJUHUIUGCRUI
      STUDJUEUIEFABCDUAUBUC $.
      $( [20-Jul-2005] $)

    $( Restriction of a shifted sequence. $)
    shftres $p |- ( ( A e. C /\ B C_ CC ) ->
                  ( ( F shift A ) |` B ) Fn B ) $=
      ( wcel cshi co cc wfn wss cres shftfn fnssres sylan ) ACFDAGHZIJBIKPBLBJA
      CDEMIBPNO $.
      $( [19-Aug-2005] $)

    $( Value of a restricted shifted sequence. $)
    shftresval $p |- ( B e. C ->
       ( ( ( F shift A ) |` C ) ` B ) = ( ( F shift A ) ` B ) ) $=
      ( cshi co fvres ) BCDAFGH $.
      $( [19-Aug-2005] $)

    $( Value of a sequence shifted by ` A ` . $)
    shftval $p |- ( ( A e. C /\ B e. CC ) ->
                    ( ( F shift A ) ` B ) = ( F ` ( B - A ) ) ) $=
      ( vx vy wcel cc cshi co cfv cv cmin wceq wa copab shftfval fveq1d opreq1
      fveq2d eqid fvex fvopab4 sylan9eq ) ACHZBIHBDAJKZLBFMZIHGMUHANKZDLZOPFGQZ
      LBANKZDLZUFBUGUKFGACDERSFGBUJUMIUKUHBOUIULDUHBANTUAUKUBULDUCUDUE $.
      $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A - B ` . $)
    shftval2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( F shift ( A - B ) ) ` ( A + C ) ) = ( F ` ( B + C ) ) ) $=
      ( cc wcel w3a caddc co cmin cshi cfv wceq subcl 3adant3 addcl 3adant2
      shftval syl11anc pnncan 3com23 addcom 3adant1 eqtr4d fveq2d eqtrd ) AFGZB
      FGZCFGZHZACIJZDABKJZLJMZULUMKJZDMZBCIJZDMUKUMFGZULFGZUNUPNUHUIURUJABOPUHU
      JUSUIACQRUMULFDESTUKUOUQDUKUOCBIJZUQUHUJUIUOUTNACBUAUBUIUJUQUTNUHBCUCUDUE
      UFUG $.
      $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A - B ` . $)
    shftval3 $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( F shift ( A - B ) ) ` A ) = ( F ` B ) ) $=
      ( cc wcel wa cc0 caddc co cmin cshi cfv wceq 0cn shftval2 mp3an3 addid1
      adantr fveq2d adantl 3eqtr3d ) AEFZBEFZGZAHIJZCABKJLJZMZBHIJZCMZAUGMBCMUC
      UDHEFUHUJNOABHCDPQUEUFAUGUCUFANUDARSTUEUIBCUDUIBNUCBRUATUB $.
      $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` -u A ` . $)
    shftval4 $p |- ( ( A e. CC /\ B e. CC ) ->
                ( ( F shift -u A ) ` B ) = ( F ` ( A + B ) ) ) $=
      ( cc wcel wa cneg cshi co cfv cmin caddc wceq negcl shftval sylan subneg
      ancoms addcom eqtr4d fveq2d eqtrd ) AEFZBEFZGZBCAHZIJKZBUGLJZCKZABMJZCKUD
      UGEFUEUHUJNAOUGBECDPQUFUIUKCUFUIBAMJZUKUEUDUIULNBARSABTUAUBUC $.
      $( [18-Aug-2005] $)

    $( Value of a shifted sequence. $)
    shftval5 $p |- ( ( A e. CC /\ B e. CC ) ->
                ( ( F shift A ) ` ( B + A ) ) = ( F ` B ) ) $=
      ( cc wcel caddc co cshi cfv wceq wa cmin simpr addcl shftval syl11anc
      pncan fveq2d eqtrd ancoms ) BEFZAEFZBAGHZCAIHJZBCJZKUBUCLZUEUDAMHZCJZUFUG
      UCUDEFUEUIKUBUCNBAOAUDECDPQUGUHBCBARSTUA $.
      $( [19-Aug-2005] $)

    $( Functionality of a restricted shifted sequence. $)
    shftf $p |- ( ( A e. D /\ B C_ CC /\ A. x e. B ( F ` ( x - A ) ) e. C )
                  -> ( ( F shift A ) |` B ) : B --> C ) $=
      ( wcel cvv cc wss cv cmin co cfv wral cshi cres wf elisset w3a wfn
      shftres 3adant3 wa wceq shftresval adantl ssel2 anim2i anassrs shftval
      syl eqtr2d eleq1d ralbidva biimp3a ffnfv sylanbrc syl3an1 ) BEHBIHZCJKZAL
      ZBMNFOZDHZACPZCDFBQNZCRZSZBETVAVBVFUAVHCUBZVCVHOZDHZACPZVIVAVBVJVFBCIFGUC
      UDVAVBVFVMVAVBUEZVEVLACVNVCCHZUEZVDVKDVPVKVCVGOZVDVOVKVQUFVNBVCCFGUGUHVPV
      AVCJHZUEZVQVDUFVAVBVOVSVBVOUEVRVACJVCUIUJUKBVCIFGULUMUNUOUPUQACDVHURUSUT
      $.
      $( [19-Aug-2005] $)

    $( Composite shift operations. $)
    2shfti $p |- ( ( A e. CC /\ B e. CC ) -> ( ( F shift A ) shift B ) =
                ( F shift ( A + B ) ) ) $=
      ( vx vy cc wcel wa cv cmin co cshi cfv wceq copab caddc simprl subcl
      adantrl shftval syl11anc w3a sub23 subsub4 eqtr3d 3expb fveq2d eqtrd
      ancoms eqeq2d pm5.32da opabbidv oprex shftfval adantl addcl syl 3eqtr4d
      ) AGHZBGHZIZEJZGHZFJZVCBKLZCAMLZNZOZIZEFPZVDVEVCABQLZKLZCNZOZIZEFPZVGBMLZ
      CVLMLZVBVJVPEFVBVDVIVOVBVDIVHVNVEVDVBVHVNOVDVBIZVHVFAKLZCNZVNVTUTVFGHZVHW
      BOVDUTVARVDVAWCUTVCBSTAVFGCDUAUBVTWAVMCVDUTVAWAVMOVDUTVAUCVCAKLBKLWAVMVCA
      BUDVCABUEUFUGUHUIUJUKULUMVAVRVKOUTEFBGVGCAMUNUOUPVBVLGHVSVQOABUQEFVLGCDUO
      URUS $.
      $( [19-Aug-2005] $)

    $( Cancellation law for the shift operation. $)
    shftcan2 $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( ( F shift -u A ) shift A ) ` B ) = ( F ` B ) ) $=
      ( cc wcel wa cneg cshi co cfv cmin oprex shftval wceq negcl adantl subcl
      syl11anc caddc subneg sylancom npcan eqtrd fveq2d ancoms ) AEFZBEFZGBCAHZ
      IJZAIJKBALJZUJKZBCKZABEUJCUIIMNUHUGULUMOUHUGGZULUKUILJZCKZUMUNUIEFZUKEFZU
      LUPOUGUQUHAPQBARZUIUKECDNSUNUOBCUNUOUKATJZBUHUGURUOUTOUSUKAUAUBBAUCUDUEUD
      UFUD $.
      $( [4-Aug-2005] $)

    $( Cancellation law for the shift operation. $)
    shftcan1 $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( ( F shift A ) shift -u A ) ` B ) = ( F ` B ) ) $=
      ( cc wcel wa cneg cshi co cfv wceq negneg opreq2d opreq1d fveq1d adantr
      negcl shftcan2 sylan eqtr3d ) AEFZBEFZGBCAHZHZIJZUDIJZKZBCAIJZUDIJZKZBCKZ
      UBUHUKLUCUBBUGUJUBUFUIUDIUBUEACIAMNOPQUBUDEFUCUHULLARUDBCDSTUA $.
      $( [4-Aug-2005] $)

    $( Identity law for the shift operation. $)
    shftidt $p |- ( A e. CC ->
                    ( ( F shift 0 ) ` A ) = ( F ` A ) ) $=
      ( cc wcel cc0 cshi co cfv cneg caddc wceq 0cn negcli 2shfti mp2an negidi
      opreq2i eqtri fveq1i shftcan1 mpan syl5eqr ) ADEZABFGHZIAUEFJZGHZIZABIZAU
      GUEUGBFUFKHZGHZUEFDEZUFDEUGUKLMFMNFUFBCOPUJFBGFMQRSTULUDUHUILMFABCUAUBUC
      $.
      $( [19-Aug-2005] $)
  $}

  ${
    $d j k n S $.  $d j k n F $.
    seq1shftid.1 $e |- S e. _V $.
    seq1shftid.2 $e |- F e. _V $.
    $( Identity law for the shift operation in a 1-based sequence builder. $)
    seq1shftid $p |- ( S seq1 ( F shift 0 ) ) = ( S seq1 F ) $=
      ( vn vj vk cc0 cshi co cseq1 wceq cv cfv cn wral c1 caddc fveq2 eqeq12d
      cc wcel ax1cn shftidt ax-mp oprex seq11 3eqtr4i wa opreq1 nncn peano2cn
      syl opreq2d sylan9eqr seq1p1 adantr 3eqtr4d ex nnind rgen wfn wb seq1fn
      eqfnfv2 mp2an mpbir ) ABHIJZKJZABKJZLZEMZVINZVLVJNZLZEOPZVOEOFMZVINZVQVJN
      ZLQVINZQVJNZLGMZVINZWBVJNZLZWBQRJZVINZWFVJNZLZVOFGVLVQQLVRVTVSWAVQQVISVQQ
      VJSTVQWBLVRWCVSWDVQWBVISVQWBVJSTVQWFLVRWGVSWHVQWFVISVQWFVJSTVQVLLVRVMVSVN
      VQVLVISVQVLVJSTQVHNZQBNZVTWAQUAUBWJWKLUCQBDUDUEAVHCBHIUFZUGABCDUGUHWBOUBZ
      WEWIWMWEUIWCWFVHNZAJZWDWFBNZAJZWGWHWEWMWOWDWNAJWQWCWDWNAUJWMWNWPWDAWMWFUA
      UBZWNWPLWMWBUAUBWRWBUKWBULUMWFBDUDUMUNUOWMWGWOLWEWBAVHCWLUPUQWMWHWQLWEWBA
      BCDUPUQURUSUTVAVIOVBVJOVBVKVPVCAVHCWLVDABCDVDEOVIVJVEVFVG $.
      $( [1-Sep-2005] $)
  $}

