$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The "shift" operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c shift $.  $( Symbol for function shifter. $)

  $( Extend class notation with function shifter. $)
  cshi $a class shift $.

  ${
    $d x y z f g $.
    $( Define a function shifter.  This operation offsets the value argument of
       a function (ordinarily on a subset of ` CC ` ) and produces a new
       function on ` CC ` .  See ~ shftvalt for its value. $)
    df-shft $a |- shift = { <. <. f , x >. , g >. | g =
                  { <. y , z >. | ( y e. CC /\ z = ( f ` ( y - x ) ) ) } } $.
  $}

  ${
    $d x y w f g A $.  $d x y w f g F $.  $d x y B $.  $d x y C $.
    shftfval.1 $e |- F e. V $.
    $( The value of the sequence shifter operation is a function on ` CC ` .
       ` A ` is ordinarily an integer. $)
    shftfval $p |- ( A e. B -> ( F shift A ) = { <. x , y >. | ( x e. CC /\
                   y = ( F ` ( x - A ) ) ) } ) $=
      ( vf vw vg cvv wcel cshi co cv cc cmin cfv wceq wa copab axcnex
      funopabex2 fveq1 eqeq2d anbi2d opabbidv opreq2 fveq2d df-shft oprabval5
      mpan ) EJKCDKECLMANZOKZBNZULCPMZEQZRZSZABTZRFGHIECJDUMUNULHNZPMZGNZQZRZSZ
      ABTUSLUMUNVAEQZRZSZABTABOUPUAUBVBERZVEVHABVIVDVGUMVIVCVFUNVAVBEUCUDUEUFUT
      CRZVHURABVJVGUQUMVJVFUPUNVJVAUOEUTCULPUGUHUDUEUFHABGIUIUJUK $.
      $( [20-Aug-2005] $) $( [20-Jul-2005] $)

    $( Functionality and domain of a sequence shifted by ` A ` . $)
    shftfn $p |- ( A e. B -> ( F shift A ) Fn CC ) $=
      ( vx vy wcel cshi co cc wfn cv cmin cfv wceq wa copab fvex eqid fnopab2
      wb shftfval fneq1 syl mpbiri ) ABGZCAHIZJKZELZJGFLUIAMIZCNZOPEFQZJKZEFJUK
      ULUJCRULSTUFUGULOUHUMUAEFABCDUBJUGULUCUDUE $.
      $( [3-Sep-2005] $) $( [20-Jul-2005] $)

    $( Restriction of a shifted sequence. $)
    shftres $p |- ( ( A e. C /\ B (_ CC ) ->
                  ( ( F shift A ) |` B ) Fn B ) $=
      ( cshi co cc wfn wss cres wcel fnssres shftfn sylan ) DAFGZHIBHJPBKBIACLH
      BPMACDENO $.
      $( [4-Sep-2005] $) $( [19-Aug-2005] $)

    $( Value of a restricted shifted sequence. $)
    shftresvalt $p |- ( B e. C ->
       ( ( ( F shift A ) |` C ) ` B ) = ( ( F shift A ) ` B ) ) $=
      ( cshi co fvres ) BCDAFGH $.
      $( [9-Sep-2005] $) $( [19-Aug-2005] $)

    $( Value of a sequence shifted by ` A ` . $)
    shftvalt $p |- ( ( A e. C /\ B e. CC ) ->
                    ( ( F shift A ) ` B ) = ( F ` ( B - A ) ) ) $=
      ( vx vy wcel cc cshi co cfv cv cmin wceq wa copab shftfval fveq1d opreq1
      fveq2d eqid fvex fvopab4 sylan9eq ) ACHZBIHBDAJKZLBFMZIHGMUHANKZDLZOPFGQZ
      LBANKZDLZUFBUGUKFGACDERSFGBUJUMIUKUHBOUIULDUHBANTUAUKUBULDUCUDUE $.
      $( [21-Aug-2005] $) $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A - B ` . $)
    shftval2t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( F shift ( A - B ) ) ` ( A + C ) ) = ( F ` ( B + C ) ) ) $=
      ( cc wcel w3a caddc co cmin cshi cfv wceq shftvalt subclt 3adant3
      axaddcl 3adant2 sylanc pnncant 3com23 axaddcom 3adant1 eqtr4d fveq2d
      eqtrd ) AFGZBFGZCFGZHZACIJZDABKJZLJMZULUMKJZDMZBCIJZDMUMFGZULFGZUNUPNUKUM
      ULFDEOUHUIURUJABPQUHUJUSUIACRSTUKUOUQDUKUOCBIJZUQUHUJUIUOUTNACBUAUBUIUJUQ
      UTNUHBCUCUDUEUFUG $.
      $( [21-Aug-2005] $) $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` A - B ` . $)
    shftval3t $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( F shift ( A - B ) ) ` A ) = ( F ` B ) ) $=
      ( cc wcel wa cc0 caddc co cmin cshi cfv wceq 0cn shftval2t mp3an3 ax0id
      adantr fveq2d adantl 3eqtr3d ) AEFZBEFZGZAHIJZCABKJLJZMZBHIJZCMZAUGMBCMUC
      UDHEFUHUJNOABHCDPQUEUFAUGUCUFANUDARSTUEUIBCUDUIBNUCBRUATUB $.
      $( [21-Aug-2005] $) $( [20-Jul-2005] $)

    $( Value of a sequence shifted by ` -u A ` . $)
    shftval4t $p |- ( ( A e. CC /\ B e. CC ) ->
                ( ( F shift -u A ) ` B ) = ( F ` ( A + B ) ) ) $=
      ( cc wcel wa cneg cshi co cfv cmin caddc wceq shftvalt negclt sylan
      subnegt ancoms axaddcom eqtr4d fveq2d eqtrd ) AEFZBEFZGZBCAHZIJKZBUGLJZCK
      ZABMJZCKUGEFUEUHUJNUDUGBECDOAPQUFUIUKCUFUIBAMJZUKUEUDUIULNBARSABTUAUBUC
      $.
      $( [26-Aug-2005] $) $( [18-Aug-2005] $)

    $( Value of a shifted sequence. $)
    shftval5t $p |- ( ( A e. CC /\ B e. CC ) ->
                ( ( F shift A ) ` ( B + A ) ) = ( F ` B ) ) $=
      ( cc wcel caddc co cshi cfv wceq wa cmin shftvalt pm3.27 axaddcl sylanc
      pncant fveq2d eqtrd ancoms ) BEFZAEFZBAGHZCAIHJZBCJZKUBUCLZUEUDAMHZCJZUFU
      CUDEFUEUIKUGAUDECDNUBUCOBAPQUGUHBCBARSTUA $.
      $( [27-Sep-2005] $) $( [19-Aug-2005] $)

    $( Functionality of a restricted shifted sequence. $)
    shftf $p |- ( ( A e. D /\ B (_ CC /\ A. x e. B ( F ` ( x - A ) ) e. C )
                  -> ( ( F shift A ) |` B ) : B --> C ) $=
      ( cvv wcel cc wss cv cmin co cfv wral cshi cres wf w3a wfn wa shftres
      3adant3 wceq shftresvalt adantl ssel2 anim2i anassrs shftvalt syl eqtr2d
      eleq1d ralbidva biimp3a jca ffnfv sylibr elisset syl3an1 ) BHIZCJKZALZBMN
      FOZDIZACPZCDFBQNZCRZSZBEIVBVCVGTZVICUAZVDVIOZDIZACPZUBVJVKVLVOVBVCVLVGBCH
      FGUCUDVBVCVGVOVBVCUBZVFVNACVPVDCIZUBZVEVMDVRVMVDVHOZVEVQVMVSUEVPBVDCFGUFU
      GVRVBVDJIZUBZVSVEUEVBVCVQWAVCVQUBVTVBCJVDUHUIUJBVDHFGUKULUMUNUOUPUQACDVIU
      RUSBEUTVA $.
      $( [28-Sep-2005] $) $( [19-Aug-2005] $)

    $( Composite shift operations. $)
    2shft $p |- ( ( A e. CC /\ B e. CC ) -> ( ( F shift A ) shift B ) =
                ( F shift ( A + B ) ) ) $=
      ( vx vy cc wcel wa cv cmin co cshi cfv wceq copab caddc shftvalt simprl
      subclt adantrl sylanc w3a sub23t subsub4t eqtr3d 3expb fveq2d eqtrd
      ancoms eqeq2d pm5.32da opabbidv oprex shftfval adantl axaddcl syl
      3eqtr4d ) AGHZBGHZIZEJZGHZFJZVCBKLZCAMLZNZOZIZEFPZVDVEVCABQLZKLZCNZOZIZEF
      PZVGBMLZCVLMLZVBVJVPEFVBVDVIVOVBVDIVHVNVEVDVBVHVNOVDVBIZVHVFAKLZCNZVNUTVF
      GHZVHWBOVTAVFGCDRVDUTVASVDVAWCUTVCBTUAUBVTWAVMCVDUTVAWAVMOVDUTVAUCVCAKLBK
      LWAVMVCABUDVCABUEUFUGUHUIUJUKULUMVAVRVKOUTEFBGVGCAMUNUOUPVBVLGHVSVQOABUQE
      FVLGCDUOURUS $.
      $( [5-Sep-2005] $) $( [19-Aug-2005] $)

    $( Cancellation law for the shift operation. $)
    shftcan2t $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( ( F shift -u A ) shift A ) ` B ) = ( F ` B ) ) $=
      ( cc wcel wa cneg cshi co cfv cmin oprex shftvalt wceq negclt adantl
      subclt sylanc caddc subnegt pm3.27 npcant eqtrd fveq2d ancoms ) AEFZBEFZG
      BCAHZIJZAIJKBALJZUJKZBCKZABEUJCUIIMNUHUGULUMOUHUGGZULUKUILJZCKZUMUIEFZUKE
      FZULUPOUNUIUKECDNUGUQUHAPQBARZSUNUOBCUNUOUKATJZBURUGUOUTOUNUKAUAUSUHUGUBS
      BAUCUDUEUDUFUD $.
      $( [5-Sep-2005] $) $( [4-Aug-2005] $)

    $( Cancellation law for the shift operation. $)
    shftcan1t $p |- ( ( A e. CC /\ B e. CC ) ->
                    ( ( ( F shift A ) shift -u A ) ` B ) = ( F ` B ) ) $=
      ( cc wcel wa cneg cshi co cfv wceq negnegt opreq2d opreq1d fveq1d adantr
      shftcan2t negclt sylan eqtr3d ) AEFZBEFZGBCAHZHZIJZUDIJZKZBCAIJZUDIJZKZBC
      KZUBUHUKLUCUBBUGUJUBUFUIUDIUBUEACIAMNOPQUDEFUCUHULLUBUDBCDRASTUA $.
      $( [5-Sep-2005] $) $( [4-Aug-2005] $)

    $( Identity law for the shift operation. $)
    shftidt $p |- ( A e. CC ->
                    ( ( F shift 0 ) ` A ) = ( F ` A ) ) $=
      ( cc wcel cc0 cshi co cneg cfv wceq 0cn shftcan1t mpan caddc negcl 2shft
      mp2an negid opreq2i eqtr fveq1i syl5eqr ) ADEZABFGHZFIZGHZJZABJZAUEJFDEZU
      DUHUIKLFABCMNAUGUEUGBFUFOHZGHZUEUJUFDEUGULKLFLPFUFBCQRUKFBGFLSTUAUBUC $.
      $( [5-Sep-2005] $) $( [19-Aug-2005] $)
  $}

  ${
    $d j k n S $.  $d j k n F $.
    seq1shftid.1 $e |- S e. V $.
    seq1shftid.2 $e |- F e. V $.
    $( Identity law for the shift operation in a 1-based sequence builder. $)
    seq1shftid $p |- ( S seq1 ( F shift 0 ) ) = ( S seq1 F ) $=
      ( vn vj vk cc0 cshi co cseq1 wceq cn cv cfv wral wfn wa wb oprex seq1fn
      eqfnfv mp2an eqid c1 caddc fveq2 eqeq12d cc wcel ax1cn shftidt ax-mp
      seq11 3eqtr4 opreq1 nncnt peano2cn syl opreq2d sylan9eqr seq1p1 adantr
      3eqtr4d ex nnind rgen mpbir2an ) ABHIJZKJZABKJZLZMMLZENZVJOZVNVKOZLZEMPZV
      JMQVKMQVLVMVRRSAVICBHITZUAABCDUAEMMVJVKUBUCMUDVQEMFNZVJOZVTVKOZLUEVJOZUEV
      KOZLGNZVJOZWEVKOZLZWEUEUFJZVJOZWIVKOZLZVQFGVNVTUELWAWCWBWDVTUEVJUGVTUEVKU
      GUHVTWELWAWFWBWGVTWEVJUGVTWEVKUGUHVTWILWAWJWBWKVTWIVJUGVTWIVKUGUHVTVNLWAV
      OWBVPVTVNVJUGVTVNVKUGUHUEVIOZUEBOZWCWDUEUIUJWMWNLUKUEBDULUMAVICVSUNABCDUN
      UOWEMUJZWHWLWOWHRWFWIVIOZAJZWGWIBOZAJZWJWKWHWOWQWGWPAJWSWFWGWPAUPWOWPWRWG
      AWOWIUIUJZWPWRLWOWEUIUJWTWEUQWEURUSWIBDULUSUTVAWOWJWQLWHWEAVICVSVBVCWOWKW
      SLWHWEABCDVBVCVDVEVFVGVH $.
      $( [6-Sep-2005] $) $( [1-Sep-2005] $)
  $}

