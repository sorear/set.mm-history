$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Some deductions from the field axioms for complex numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( A real number is a complex number. $)
  recnt $p |- ( A e. RR -> A e. CC ) $=
    ( cr cc axresscn sseli ) BCADE $.
    $( [10-Aug-99] $)

  ${
    recn.1 $e |- A e. RR $.
    $( A real number is a complex number. $)
    recn $p |- A e. CC $=
      ( cr cc axresscn ssii ) CDAEBF $.
      $( [1-Mar-95] $)
  $}

  ${
    recnd.1 $e |- ( ph -> A e. RR ) $.
    $( Deduction from real number to complex number. $)
    recnd $p |- ( ph -> A e. CC ) $=
      ( cr wcel cc recnt syl ) ABDEBFECBGH $.
      $( [26-Oct-99] $)
  $}

  $( 0 is a complex number. $)
  0cn $p |- 0 e. CC $=
    ( cc0 ax0re recn ) ABC $.
    $( [1-Mar-95] $)

  $( 1 is a complex number. $)
  1cn $p |- 1 e. CC $=
    ( c1 ax1re recn ) ABC $.
    $( [1-Mar-95] $)

  $( Hypothesis for weak deduction theorem to eliminate ` A =/= 0 ` . $)
    elimne0 $p |- ded ( A =/= 0 , A , 1 ) =/= 0 $=
      ( cc0 wne c1 cded neeq1 ax1ne0 elimhyp ) ABCZIADEZBCDBCADAJBFDJBFGH $.
      $( [15-May-99] $)

  ${
    axi.1 $e |- A e. CC $.
    ${
      axi.2 $e |- B e. CC $.
      $( Closure law for addition. $)
      addcl $p |- ( A + B ) e. CC $=
        ( cc wcel caddc co axaddcl mp2an ) AEFBEFABGHEFCDABIJ $.
        $( [23-Nov-94] $)

      $( Closure law for multiplication. $)
      mulcl $p |- ( A x. B ) e. CC $=
        ( cc wcel cmulc co axmulcl mp2an ) AEFBEFABGHEFCDABIJ $.
        $( [23-Nov-94] $)

      $( Commutative law for addition. $)
      addcom $p |- ( A + B ) = ( B + A ) $=
        ( cc wcel caddc co wceq axaddcom mp2an ) AEFBEFABGHBAGHICDABJK $.
        $( [23-Nov-94] $)

      $( Commutative law for multiplication. $)
      mulcom $p |- ( A x. B ) = ( B x. A ) $=
        ( cc wcel cmulc co wceq axmulcom mp2an ) AEFBEFABGHBAGHICDABJK $.
        $( [23-Nov-94] $)

      ${
        axi.3 $e |- C e. CC $.
        $( Associative law for addition. $)
        addass $p |- ( ( A + B ) + C ) = ( A + ( B + C ) ) $=
          ( cc wcel caddc co wceq axaddass mp3an ) AGHBGHCGHABIJCIJABCIJIJKDEFA
          BCLM $.
          $( [23-Nov-94] $)

        $( Associative law for multiplication. $)
        mulass $p |- ( ( A x. B ) x. C ) = ( A x. ( B x. C ) ) $=
          ( cc wcel cmulc co wceq axmulass mp3an ) AGHBGHCGHABIJCIJABCIJIJKDEFA
          BCLM $.
          $( [23-Nov-94] $)

        $( Distributive law. $)
        distr1 $p |- ( A x. ( B + C ) ) = ( ( A x. B ) + ( A x. C ) ) $=
          ( cc wcel caddc co cmulc wceq axdistr mp3an ) AGHBGHCGHABCIJKJABKJACK
          JIJLDEFABCMN $.
          $( [23-Nov-94] $)

        $( Distributive law. $)
        distr2 $p |- ( ( A + B ) x. C ) = ( ( A x. C ) + ( B x. C ) ) $=
          ( caddc co cmulc distr1 addcl mulcom opreq12i 3eqtr4 ) CABGHZIHCAIHZC
          BIHZGHOCIHACIHZBCIHZGHCABFDEJOCABDEKFLRPSQGACDFLBCEFLMN $.
          $( [16-Feb-95] $)

      $}
    $}

    $( Identity law for addition. $)
    addid1 $p |- ( A + 0 ) = A $=
      ( cc wcel cc0 caddc co wceq ax0id ax-mp ) ACDAEFGAHBAIJ $.
      $( [23-Nov-94] $)

    $( Identity law for addition. $)
    addid2 $p |- ( 0 + A ) = A $=
      ( cc0 caddc co 0cn addcom addid1 eqtr ) CADEACDEACAFBGABHI $.
      $( [23-Nov-94] $)

    $( Identity law for multiplication. $)
    mulid1 $p |- ( A x. 1 ) = A $=
      ( cc wcel c1 cmulc co wceq ax1id ax-mp ) ACDAEFGAHBAIJ $.
      $( [14-Feb-95] $)

    $( Identity law for multiplication. $)
    mulid2 $p |- ( 1 x. A ) = A $=
      ( c1 cmulc co 1cn mulcom mulid1 eqtr ) CADEACDEACAFBGABHI $.
      $( [14-Feb-95] $)

    ${
      $d x A $.
      $( Existence of negatives. $)
      negex $p |- E. x e. CC ( A + x ) = 0 $=
        ( cc wcel cv caddc co cc0 wceq wrex axnegex ax-mp ) BDEBAFGHIJADKCABLM
        $.
        $( [23-Nov-94] $)

      ${
        recex.2 $e |- A =/= 0 $.
        $( Existence of reciprocals. $)
        recex $p |- E. x e. CC ( A x. x ) = 1 $=
          ( cc wcel cc0 wne cv cmulc co c1 wceq wrex axrecex mp2an ) BEFBGHBAIJ
          KLMAENCDABOP $.
          $( [26-Jan-95] $)
      $}
    $}
  $}

  ${
    axri.1 $e |- A e. RR $.
    axri.2 $e |- B e. RR $.

    $( Closure law for addition of reals. $)
    addrcl $p |- ( A + B ) e. RR $=
      ( cr wcel caddc co axaddrcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [17-Jan-97] $)

    $( Closure law for multiplication of reals. $)
    mulrcl $p |- ( A x. B ) e. RR $=
      ( cr wcel cmulc co axmulrcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [17-Jan-97] $)
  $}

