$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Some deductions from the field axioms for complex numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The set of real numbers exists. $)
  reex $p |- RR e. V $=
    ( cr cc axcnex axresscn ssexi ) ABCDE $.
    $( [25-Sep-2004] $) $( [30-Jul-2004] $)

  $( A real number is a complex number. $)
  recnt $p |- ( A e. RR -> A e. CC ) $=
    ( cr cc axresscn sseli ) BCADE $.
    $( [10-Aug-1999] $)

  ${
    recn.1 $e |- A e. RR $.
    $( A real number is a complex number. $)
    recn $p |- A e. CC $=
      ( cr cc axresscn sselii ) CDAEBF $.
      $( [1-Mar-1995] $)
  $}

  ${
    recnd.1 $e |- ( ph -> A e. RR ) $.
    $( Deduction from real number to complex number. $)
    recnd $p |- ( ph -> A e. CC ) $=
      ( cr wcel cc recnt syl ) ABDEBFECBGH $.
      $( [26-Oct-1999] $)
  $}

  $( Hypothesis for weak deduction theorem to eliminate ` A =/= 0 ` . $)
    elimne0 $p |- if ( A =/= 0 , A , 1 ) =/= 0 $=
      ( cc0 wne c1 cif neeq1 ax1ne0 elimhyp ) ABCZIADEZBCDBCADAJBFDJBFGH $.
      $( [15-May-1999] $)

  $( The addition operation is a set. $)
  addex $p |- + e. V $=
    ( cc cxp caddc wf cvv wcel axaddopr axcnex xpex fex mp2an ) AABZACDLEFCEFGA
    AHHILAECJK $.
    $( [29-Nov-2004] $) $( [19-Oct-2004] $)

  $( The multiplication operation is a set. $)
  mulex $p |- x. e. V $=
    ( cc cxp cmul wf cvv wcel axmulopr axcnex xpex fex mp2an ) AABZACDLEFCEFGAA
    HHILAECJK $.
    $( [22-Oct-2004] $) $( [19-Oct-2004] $)

  $( Distributive law for complex numbers. $)
  adddirt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
              ( ( A + B ) x. C ) = ( ( A x. C ) + ( B x. C ) ) ) $=
    ( cc wcel w3a caddc co cmul wceq axdistr 3coml wa axmulcom axaddcl sylan
    3impa 3adant2 3adant1 opreq12d 3eqtr4d ) ADEZBDEZCDEZFZCABGHZIHZCAIHZCBIHZG
    HZUFCIHZACIHZBCIHZGHUDUBUCUGUJJCABKLUBUCUDUKUGJZUFDEUDUNUBUCMUFCNABOPQUEULU
    HUMUIGUBUDULUHJUCACNRUCUDUMUIJUBBCNSTUA $.
    $( [11-Oct-2004] $) $( [10-Oct-2004] $)

  ${
    axi.1 $e |- A e. CC $.
    axi.2 $e |- B e. CC $.
    $( Closure law for addition. $)
    addcl $p |- ( A + B ) e. CC $=
      ( cc wcel caddc co axaddcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [23-Nov-1994] $)

    $( Closure law for multiplication. $)
    mulcl $p |- ( A x. B ) e. CC $=
      ( cc wcel cmul co axmulcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [23-Nov-1994] $)

    $( Commutative law for addition. $)
    addcom $p |- ( A + B ) = ( B + A ) $=
      ( cc wcel caddc co wceq axaddcom mp2an ) AEFBEFABGHBAGHICDABJK $.
      $( [23-Nov-1994] $)

    $( Commutative law for multiplication. $)
    mulcom $p |- ( A x. B ) = ( B x. A ) $=
      ( cc wcel cmul co wceq axmulcom mp2an ) AEFBEFABGHBAGHICDABJK $.
      $( [23-Nov-1994] $)

    ${
      axi.3 $e |- C e. CC $.
      $( Associative law for addition. $)
      addass $p |- ( ( A + B ) + C ) = ( A + ( B + C ) ) $=
        ( cc wcel caddc co wceq axaddass mp3an ) AGHBGHCGHABIJCIJABCIJIJKDEFABC
        LM $.
        $( [23-Nov-1994] $)

      $( Associative law for multiplication. $)
      mulass $p |- ( ( A x. B ) x. C ) = ( A x. ( B x. C ) ) $=
        ( cc wcel cmul co wceq axmulass mp3an ) AGHBGHCGHABIJCIJABCIJIJKDEFABCL
        M $.
        $( [23-Nov-1994] $)

      $( Distributive law. $)
      adddi $p |- ( A x. ( B + C ) ) = ( ( A x. B ) + ( A x. C ) ) $=
        ( cc wcel caddc co cmul wceq axdistr mp3an ) AGHBGHCGHABCIJKJABKJACKJIJ
        LDEFABCMN $.
        $( [23-Nov-1994] $)

      $( Distributive law. $)
      adddir $p |- ( ( A + B ) x. C ) = ( ( A x. C ) + ( B x. C ) ) $=
        ( cc wcel caddc co cmul wceq adddirt mp3an ) AGHBGHCGHABIJCKJACKJBCKJIJ
        LDEFABCMN $.
        $( [16-Feb-1995] $)

    $}
  $}

  $( 0 is a complex number.  (Proved without referencing ~ ax1cn by Eric
     Schmidt, 11-Apr-2007.  Compare previous version, ~ 0cnALT .) $)
  0cn $p |- 0 e. CC $=
    ( vx ci cv caddc co cc0 wceq cc wrex wcel axicn axnegex ax-mp wa axaddcl
    mpan adantr wb eleq1 adantl mpbid r19.23aiva ) BACZDEZFGZAHIZFHJZBHJZUFKABL
    MUEUGAHUCHJZUENUDHJZUGUIUJUEUHUIUJKBUCOPQUEUJUGRUIUDFHSTUAUBM $.
    $( [12-Apr-2007] $) $( [19-Feb-2005] $)

  $( 0 is a complex number. $)
  0cnALT $p |- 0 e. CC $=
    ( ci cmul co c1 caddc cc0 cc axi2m1 axicn mulcl ax1cn addcl eqeltrr ) AABCZ
    DECFGHNDAAIIJKLM $.
    $( [20-Feb-2005] $) $( [19-Feb-2005] $)

  ${
    addid1.1 $e |- A e. CC $.
    $( Identity law for addition. $)
    addid1 $p |- ( A + 0 ) = A $=
      ( cc wcel cc0 caddc co wceq ax0id ax-mp ) ACDAEFGAHBAIJ $.
      $( [23-Nov-1994] $)

    $( Identity law for addition. $)
    addid2 $p |- ( 0 + A ) = A $=
      ( cc0 caddc co 0cn addcom addid1 eqtr ) CADEACDEACAFBGABHI $.
      $( [23-Nov-1994] $)

    $( Identity law for multiplication. $)
    mulid1 $p |- ( A x. 1 ) = A $=
      ( cc wcel c1 cmul co wceq ax1id ax-mp ) ACDAEFGAHBAIJ $.
      $( [14-Feb-1995] $)

    $( Identity law for multiplication. $)
    mulid2 $p |- ( 1 x. A ) = A $=
      ( c1 cmul co ax1cn mulcom mulid1 eqtr ) CADEACDEACAFBGABHI $.
      $( [14-Feb-1995] $)

    ${
      $d x A $.
      $( Existence of negatives. $)
      negex $p |- E. x e. CC ( A + x ) = 0 $=
        ( cc wcel cv caddc co cc0 wceq wrex axnegex ax-mp ) BDEBAFGHIJADKCABLM
        $.
        $( [23-Nov-1994] $)

      ${
        recex.2 $e |- A =/= 0 $.
        $( Existence of reciprocals. $)
        recex $p |- E. x e. CC ( A x. x ) = 1 $=
          ( cc wcel cc0 wne cv cmul co c1 wceq wrex axrecex mp2an ) BEFBGHBAIJK
          LMAENCDABOP $.
          $( [26-Jan-1995] $)
      $}
    $}
  $}

  ${
    axri.1 $e |- A e. RR $.
    axri.2 $e |- B e. RR $.

    $( Closure law for addition of reals. $)
    readdcl $p |- ( A + B ) e. RR $=
      ( cr wcel caddc co axaddrcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [17-Jan-1997] $)

    $( Closure law for multiplication of reals. $)
    remulcl $p |- ( A x. B ) e. RR $=
      ( cr wcel cmul co axmulrcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [17-Jan-1997] $)
  $}

