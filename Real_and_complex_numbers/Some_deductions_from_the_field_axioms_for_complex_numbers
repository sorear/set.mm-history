$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Some deductions from the field axioms for complex numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Alias for ~ axaddcl , for naming consistency with ~ addcl . $)
  addclt $p |- ( ( A e. CC /\ B e. CC ) -> ( A + B ) e. CC ) $=
    ( axaddcl ) ABC $.
    $( [10-Mar-2008] $) $( [10-Mar-2008] $)

  $( Alias for ~ axaddrcl , for naming consistency with ~ readdcl . $)
  readdclt $p |- ( ( A e. RR /\ B e. RR ) -> ( A + B ) e. RR ) $=
    ( axaddrcl ) ABC $.
    $( [10-Mar-2008] $) $( [10-Mar-2008] $)

  $( Alias for ~ axmulcl , for naming consistency with ~ mulcl . $)
  mulclt $p |- ( ( A e. CC /\ B e. CC ) -> ( A x. B ) e. CC ) $=
    ( axmulcl ) ABC $.
    $( [10-Mar-2008] $) $( [10-Mar-2008] $)

  $( Alias for ~ axmulrcl , for naming consistency with ~ remulcl . $)
  remulclt $p |- ( ( A e. RR /\ B e. RR ) -> ( A x. B ) e. RR ) $=
    ( axmulrcl ) ABC $.
    $( [10-Mar-2008] $) $( [10-Mar-2008] $)

  $( Alias for ~ axaddcom , for naming consistency with ~ addcom . $)
  addcomt $p |- ( ( A e. CC /\ B e. CC ) -> ( A + B ) = ( B + A ) ) $=
    ( axaddcom ) ABC $.
    $( [10-Mar-2008] $) $( [10-Mar-2008] $)

  $( Alias for ~ axmulcom , for naming consistency with ~ mulcom . $)
  mulcomt $p |- ( ( A e. CC /\ B e. CC ) -> ( A x. B ) = ( B x. A ) ) $=
    ( axmulcom ) ABC $.
    $( [10-Mar-2008] $) $( [10-Mar-2008] $)

  $( Alias for ~ axaddass , for naming consistency with ~ addass . $)
  addasst $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( ( A + B ) + C ) = ( A + ( B + C ) ) ) $=
    ( axaddass ) ABCD $.
    $( [10-Mar-2008] $) $( [10-Mar-2008] $)

  $( Alias for ~ axmulass , for naming consistency with ~ mulass . $)
  mulasst $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( ( A x. B ) x. C ) = ( A x. ( B x. C ) ) ) $=
    ( axmulass ) ABCD $.
    $( [10-Mar-2008] $) $( [10-Mar-2008] $)

  $( Alias for ~ axdistr , for naming consistency with ~ adddi . $)
  adddit $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( A x. ( B + C ) ) = ( ( A x. B ) + ( A x. C ) ) ) $=
    ( axdistr ) ABCD $.
    $( [10-Mar-2008] $) $( [10-Mar-2008] $)

  $( Alias for ~ ax0id , for naming consistency with ~ addid1 . $)
  addid1t $p |- ( A e. CC -> ( A + 0 ) = A ) $=
    ( ax0id ) AB $.
    $( [10-Mar-2008] $) $( [10-Mar-2008] $)

  $( Alias for ~ ax1id , for naming consistency with ~ mulid1 . $)
  mulid1t $p |- ( A e. CC -> ( A x. 1 ) = A ) $=
    ( ax1id ) AB $.
    $( [10-Mar-2008] $) $( [10-Mar-2008] $)

  $( The set of real numbers exists. $)
  reex $p |- RR e. V $=
    ( cr cc axcnex axresscn ssexi ) ABCDE $.
    $( [25-Sep-2004] $) $( [30-Jul-2004] $)

  $( A real number is a complex number. $)
  recnt $p |- ( A e. RR -> A e. CC ) $=
    ( cr cc axresscn sseli ) BCADE $.
    $( [10-Aug-1999] $)

  ${
    recn.1 $e |- A e. RR $.
    $( A real number is a complex number. $)
    recn $p |- A e. CC $=
      ( cr cc axresscn sselii ) CDAEBF $.
      $( [1-Mar-1995] $)
  $}

  ${
    recnd.1 $e |- ( ph -> A e. RR ) $.
    $( Deduction from real number to complex number. $)
    recnd $p |- ( ph -> A e. CC ) $=
      ( cr wcel cc recnt syl ) ABDEBFECBGH $.
      $( [26-Oct-1999] $)
  $}

  $( Hypothesis for weak deduction theorem to eliminate ` A =/= 0 ` . $)
    elimne0 $p |- if ( A =/= 0 , A , 1 ) =/= 0 $=
      ( cc0 wne c1 cif neeq1 ax1ne0 elimhyp ) ABCZIADEZBCDBCADAJBFDJBFGH $.
      $( [15-May-1999] $)

  $( The addition operation is a set. $)
  addex $p |- + e. V $=
    ( cc cxp caddc wf cvv wcel axaddopr axcnex xpex fex mp2an ) AABZACDLEFCEFGA
    AHHILAECJK $.
    $( [29-Nov-2004] $) $( [19-Oct-2004] $)

  $( The multiplication operation is a set. $)
  mulex $p |- x. e. V $=
    ( cc cxp cmul wf cvv wcel axmulopr axcnex xpex fex mp2an ) AABZACDLEFCEFGAA
    HHILAECJK $.
    $( [22-Oct-2004] $) $( [19-Oct-2004] $)

  $( Distributive law for complex numbers. $)
  adddirt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
              ( ( A + B ) x. C ) = ( ( A x. C ) + ( B x. C ) ) ) $=
    ( cc wcel w3a caddc co cmul wceq axdistr 3coml wa axmulcom axaddcl sylan
    3impa 3adant2 3adant1 opreq12d 3eqtr4d ) ADEZBDEZCDEZFZCABGHZIHZCAIHZCBIHZG
    HZUFCIHZACIHZBCIHZGHUDUBUCUGUJJCABKLUBUCUDUKUGJZUFDEUDUNUBUCMUFCNABOPQUEULU
    HUMUIGUBUDULUHJUCACNRUCUDUMUIJUBBCNSTUA $.
    $( [11-Oct-2004] $) $( [10-Oct-2004] $)

  ${
    axi.1 $e |- A e. CC $.
    axi.2 $e |- B e. CC $.
    $( Closure law for addition. $)
    addcl $p |- ( A + B ) e. CC $=
      ( cc wcel caddc co axaddcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [23-Nov-1994] $)

    $( Closure law for multiplication. $)
    mulcl $p |- ( A x. B ) e. CC $=
      ( cc wcel cmul co axmulcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [23-Nov-1994] $)

    $( Commutative law for addition. $)
    addcom $p |- ( A + B ) = ( B + A ) $=
      ( cc wcel caddc co wceq axaddcom mp2an ) AEFBEFABGHBAGHICDABJK $.
      $( [23-Nov-1994] $)

    $( Commutative law for multiplication. $)
    mulcom $p |- ( A x. B ) = ( B x. A ) $=
      ( cc wcel cmul co wceq axmulcom mp2an ) AEFBEFABGHBAGHICDABJK $.
      $( [23-Nov-1994] $)

    ${
      axi.3 $e |- C e. CC $.
      $( Associative law for addition. $)
      addass $p |- ( ( A + B ) + C ) = ( A + ( B + C ) ) $=
        ( cc wcel caddc co wceq axaddass mp3an ) AGHBGHCGHABIJCIJABCIJIJKDEFABC
        LM $.
        $( [23-Nov-1994] $)

      $( Associative law for multiplication. $)
      mulass $p |- ( ( A x. B ) x. C ) = ( A x. ( B x. C ) ) $=
        ( cc wcel cmul co wceq axmulass mp3an ) AGHBGHCGHABIJCIJABCIJIJKDEFABCL
        M $.
        $( [23-Nov-1994] $)

      $( Distributive law. $)
      adddi $p |- ( A x. ( B + C ) ) = ( ( A x. B ) + ( A x. C ) ) $=
        ( cc wcel caddc co cmul wceq axdistr mp3an ) AGHBGHCGHABCIJKJABKJACKJIJ
        LDEFABCMN $.
        $( [23-Nov-1994] $)

      $( Distributive law. $)
      adddir $p |- ( ( A + B ) x. C ) = ( ( A x. C ) + ( B x. C ) ) $=
        ( cc wcel caddc co cmul wceq adddirt mp3an ) AGHBGHCGHABIJCKJACKJBCKJIJ
        LDEFABCMN $.
        $( [16-Feb-1995] $)

    $}
  $}

  $( 0 is a complex number. $)
  0cn $p |- 0 e. CC $=
    ( ci cmul co c1 caddc cc0 cc axi2m1 axicn mulcl ax1cn addcl eqeltrr ) AABCZ
    DECFGHNDAAIIJKLM $.
    $( [20-Feb-2005] $) $( [19-Feb-2005] $)

  $( Identity law for addition. $)
  addid2t $p |- ( A e. CC -> ( 0 + A ) = A ) $=
    ( cc wcel cc0 caddc co wceq 0cn axaddcom mpan ax0id eqtrd ) ABCZDAEFZADEFZA
    DBCMNOGHDAIJAKL $.
    $( [10-Aug-1999] $)

  ${
    addid1.1 $e |- A e. CC $.
    $( Identity law for addition. $)
    addid1 $p |- ( A + 0 ) = A $=
      ( cc wcel cc0 caddc co wceq ax0id ax-mp ) ACDAEFGAHBAIJ $.
      $( [23-Nov-1994] $)

    $( Identity law for addition. $)
    addid2 $p |- ( 0 + A ) = A $=
      ( cc wcel cc0 caddc co wceq addid2t ax-mp ) ACDEAFGAHBAIJ $.
      $( [23-Nov-1994] $)

    $( Identity law for multiplication. $)
    mulid1 $p |- ( A x. 1 ) = A $=
      ( cc wcel c1 cmul co wceq ax1id ax-mp ) ACDAEFGAHBAIJ $.
      $( [14-Feb-1995] $)

    $( Identity law for multiplication. $)
    mulid2 $p |- ( 1 x. A ) = A $=
      ( c1 cmul co ax1cn mulcom mulid1 eqtr ) CADEACDEACAFBGABHI $.
      $( [14-Feb-1995] $)
  $}

  ${
    axri.1 $e |- A e. RR $.
    axri.2 $e |- B e. RR $.

    $( Closure law for addition of reals. $)
    readdcl $p |- ( A + B ) e. RR $=
      ( cr wcel caddc co axaddrcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [17-Jan-1997] $)

    $( Closure law for multiplication of reals. $)
    remulcl $p |- ( A x. B ) e. RR $=
      ( cr wcel cmul co axmulrcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [17-Jan-1997] $)
  $}

