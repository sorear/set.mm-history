$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Some deductions from the field axioms for complex numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Alias for ~ ax-addcl , for naming consistency with ~ addcli . $)
  addcl $p |- ( ( A e. CC /\ B e. CC ) -> ( A + B ) e. CC ) $=
    ( ax-addcl ) ABC $.
    $( [10-Mar-2008] $)

  $( Alias for ~ ax-addrcl , for naming consistency with ~ readdcli . $)
  readdcl $p |- ( ( A e. RR /\ B e. RR ) -> ( A + B ) e. RR ) $=
    ( ax-addrcl ) ABC $.
    $( [10-Mar-2008] $)

  $( Alias for ~ ax-mulcl , for naming consistency with ~ mulcli . $)
  mulcl $p |- ( ( A e. CC /\ B e. CC ) -> ( A x. B ) e. CC ) $=
    ( ax-mulcl ) ABC $.
    $( [10-Mar-2008] $)

  $( Alias for ~ ax-mulrcl , for naming consistency with ~ remulcli . $)
  remulcl $p |- ( ( A e. RR /\ B e. RR ) -> ( A x. B ) e. RR ) $=
    ( ax-mulrcl ) ABC $.
    $( [10-Mar-2008] $)

  $( Alias for ~ ax-mulcom , for naming consistency with ~ mulcomi . $)
  mulcom $p |- ( ( A e. CC /\ B e. CC ) -> ( A x. B ) = ( B x. A ) ) $=
    ( ax-mulcom ) ABC $.
    $( [10-Mar-2008] $)

  $( Alias for ~ ax-addass , for naming consistency with ~ addassi . $)
  addass $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( ( A + B ) + C ) = ( A + ( B + C ) ) ) $=
    ( ax-addass ) ABCD $.
    $( [10-Mar-2008] $)

  $( Alias for ~ ax-mulass , for naming consistency with ~ mulassi . $)
  mulass $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( ( A x. B ) x. C ) = ( A x. ( B x. C ) ) ) $=
    ( ax-mulass ) ABCD $.
    $( [10-Mar-2008] $)

  $( Alias for ~ ax-distr , for naming consistency with ~ adddii . $)
  adddi $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( A x. ( B + C ) ) = ( ( A x. B ) + ( A x. C ) ) ) $=
    ( ax-distr ) ABCD $.
    $( [10-Mar-2008] $)

  $( The set of real numbers exists. $)
  reex $p |- RR e. _V $=
    ( cr cc ax-cnex ax-resscn ssexi ) ABCDE $.
    $( [30-Jul-2004] $)

  $( A real number is a complex number. $)
  recn $p |- ( A e. RR -> A e. CC ) $=
    ( cr cc ax-resscn sseli ) BCADE $.
    $( [10-Aug-1999] $)

  ${
    recni.1 $e |- A e. RR $.
    $( A real number is a complex number. $)
    recni $p |- A e. CC $=
      ( cr cc ax-resscn sselii ) CDAEBF $.
      $( [1-Mar-1995] $)
  $}

  ${
    recnd.1 $e |- ( ph -> A e. RR ) $.
    $( Deduction from real number to complex number. $)
    recnd $p |- ( ph -> A e. CC ) $=
      ( cr wcel cc recn syl ) ABDEBFECBGH $.
      $( [26-Oct-1999] $)
  $}

  $( Hypothesis for weak deduction theorem to eliminate ` A =/= 0 ` . $)
  elimne0 $p |- if ( A =/= 0 , A , 1 ) =/= 0 $=
    ( cc0 wne c1 cif neeq1 ax-1ne0 elimhyp ) ABCZIADEZBCDBCADAJBFDJBFGH $.
    $( [15-May-1999] $)

  $( The addition operation is a set. $)
  addex $p |- + e. _V $=
    ( cc cxp caddc wf cvv wcel ax-addopr ax-cnex xpex fex mp2an ) AABZACDLEFCEF
    GAAHHILAECJK $.
    $( [19-Oct-2004] $)

  $( The multiplication operation is a set. $)
  mulex $p |- x. e. _V $=
    ( cc cxp cmul wf cvv wcel ax-mulopr ax-cnex xpex fex mp2an ) AABZACDLEFCEFG
    AAHHILAECJK $.
    $( [19-Oct-2004] $)

  $( Distributive law for complex numbers. $)
  adddir $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
              ( ( A + B ) x. C ) = ( ( A x. C ) + ( B x. C ) ) ) $=
    ( cc wcel w3a caddc co cmul adddi 3coml wa addcl mulcom sylan 3impa 3adant2
    wceq 3adant1 opreq12d 3eqtr4d ) ADEZBDEZCDEZFZCABGHZIHZCAIHZCBIHZGHZUFCIHZA
    CIHZBCIHZGHUDUBUCUGUJRCABJKUBUCUDUKUGRZUBUCLUFDEUDUNABMUFCNOPUEULUHUMUIGUBU
    DULUHRUCACNQUCUDUMUIRUBBCNSTUA $.
    $( [10-Oct-2004] $)

  ${
    axi.1 $e |- A e. CC $.
    axi.2 $e |- B e. CC $.
    $( Closure law for addition. $)
    addcli $p |- ( A + B ) e. CC $=
      ( cc wcel caddc co addcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [23-Nov-1994] $)

    $( Closure law for multiplication. $)
    mulcli $p |- ( A x. B ) e. CC $=
      ( cc wcel cmul co mulcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [23-Nov-1994] $)

    $( Commutative law for multiplication. $)
    mulcomi $p |- ( A x. B ) = ( B x. A ) $=
      ( cc wcel cmul co wceq mulcom mp2an ) AEFBEFABGHBAGHICDABJK $.
      $( [23-Nov-1994] $)

    ${
      axi.3 $e |- C e. CC $.
      $( Associative law for addition. $)
      addassi $p |- ( ( A + B ) + C ) = ( A + ( B + C ) ) $=
        ( cc wcel caddc co wceq addass mp3an ) AGHBGHCGHABIJCIJABCIJIJKDEFABCLM
        $.
        $( [23-Nov-1994] $)

      $( Associative law for multiplication. $)
      mulassi $p |- ( ( A x. B ) x. C ) = ( A x. ( B x. C ) ) $=
        ( cc wcel cmul co wceq mulass mp3an ) AGHBGHCGHABIJCIJABCIJIJKDEFABCLM
        $.
        $( [23-Nov-1994] $)

      $( Distributive law. $)
      adddii $p |- ( A x. ( B + C ) ) = ( ( A x. B ) + ( A x. C ) ) $=
        ( cc wcel caddc co cmul wceq adddi mp3an ) AGHBGHCGHABCIJKJABKJACKJIJLD
        EFABCMN $.
        $( [23-Nov-1994] $)

      $( Distributive law. $)
      adddiri $p |- ( ( A + B ) x. C ) = ( ( A x. C ) + ( B x. C ) ) $=
        ( cc wcel caddc co cmul wceq adddir mp3an ) AGHBGHCGHABIJCKJACKJBCKJIJL
        DEFABCMN $.
        $( [16-Feb-1995] $)

    $}
  $}

  $( 0 is a complex number. $)
  0cn $p |- 0 e. CC $=
    ( ci cmul co c1 caddc cc0 cc ax-i2m1 ax-icn mulcli ax-1cn addcli eqeltrri )
    AABCZDECFGHNDAAIIJKLM $.
    $( [19-Feb-2005] $)

  ${
    $d A x y $.
    $( ` 1 ` is an identity element for multiplication.  Based on ideas by Eric
       Schmidt.  (Contributed by Scott Fenton, 3-Jan-2013.) $)
    mulid1 $p |- ( A e. CC -> ( A x. 1 ) = A ) $=
      ( vx vy cc wcel cv ci cmul co caddc wceq cr wrex c1 ax-icn ax-1cn ax-1rid
      recn syl eqtrd ax-cnre mulcl sylancr adddir mp3an3 syl2an mp3an13 opreq2d
      wa mulass opreqan12d opreq1 id eqeq12d syl5ibrcom rexlimivv ) ADEABFZGCFZ
      HIZJIZKZCLMBLMANHIZAKZBCAUAVAVCBCLLUQLEZURLEZUIZVCVAUTNHIZUTKVFVGUQNHIZUS
      NHIZJIZUTVDUQDEZUSDEZVGVJKZVEUQRVEGDEZURDEZVLOURRZGURUBUCVKVLNDEZVMPUQUSN
      UDUEUFVDVEVHUQVIUSJUQQVEVIGURNHIZHIZUSVEVOVIVSKZVPVNVOVQVTOPGURNUJUGSVEVR
      URGHURQUHTUKTVAVBVGAUTAUTNHULVAUMUNUOUPS $.
      $( [3-Jan-2013] $)
  $}


  ${
    mulid1.1 $e |- A e. CC $.
    $( Identity law for multiplication. $)
    mulid1i $p |- ( A x. 1 ) = A $=
      ( cc wcel c1 cmul co wceq mulid1 ax-mp ) ACDAEFGAHBAIJ $.
      $( [14-Feb-1995] $)

    $( Identity law for multiplication. $)
    mulid2i $p |- ( 1 x. A ) = A $=
      ( c1 cmul co ax-1cn mulcomi mulid1i eqtri ) CADEACDEACAFBGABHI $.
      $( [14-Feb-1995] $)
  $}

  ${
    axri.1 $e |- A e. RR $.
    axri.2 $e |- B e. RR $.
    $( Closure law for addition of reals. $)
    readdcli $p |- ( A + B ) e. RR $=
      ( cr wcel caddc co readdcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [17-Jan-1997] $)

    $( Closure law for multiplication of reals. $)
    remulcli $p |- ( A x. B ) e. RR $=
      ( cr wcel cmul co remulcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [17-Jan-1997] $)
  $}

  ${
    $d A x y $.
    $( Alias for ~ ax-cnre , for naming consistency. $)
    cnre $p |- ( A e. CC -> E. x e. RR E. y e. RR A = ( x + ( _i x. y ) ) ) $=
      ( ax-cnre ) ABCD $.
      $( [3-Jan-2013] $)
  $}

  ${
    $d A x y $.
    cnrei.1 $e |- A e. CC $.
    $( A complex number can be expressed in terms of two reals.  Inference form
       of ~ cnre . $)
    cnrei $p |- E. x e. RR E. y e. RR A = ( x + ( _i x. y ) ) $=
      ( cc wcel cv ci cmul co caddc wceq cr wrex cnre ax-mp ) CEFCAGHBGIJKJLBMN
      AMNDABCOP $.
      $( [3-Jan-2013] $)
  $}

