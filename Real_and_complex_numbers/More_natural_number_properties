$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        More natural number properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    nnsqcl.1 $e |- N e. NN $.
    $( The square of a natural number is a natural number. $)
    nnsqcli $p |- ( N ^ 2 ) e. NN $=
      ( cn wcel c2 cn0 cexp co 2nn0 nnexpcl mp2an ) ACDEFDAEGHCDBIAEJK $.
      $( [20-Aug-2001] $)

    $( A natural number is less than or equal to its square. $)
    nnlesqi $p |- N <_ ( N ^ 2 ) $=
      ( c1 cmul co c2 cexp cle nncni mulid1i wbr cn wcel nnge1 ax-mp cc0 clt
      wb nngt0i 1re nnrei lemul2i mpbi sqvali breqtrri eqbrtrri ) ACDEZAAFGEZHA
      ABIZJUGAADEZUHHCAHKZUGUJHKZALMUKBANOPAQKUKULRABSCAATABUAZUMUBOUCAUIUDUEUF
      $.
      $( [15-Sep-1999] $)

    $( A natural number is even iff its square is even. $)
    nnesqi $p |- ( ( N / 2 ) e. NN <-> ( ( N ^ 2 ) / 2 ) e. NN ) $=
      ( c2 cdiv co cn wcel cexp cmul nnmulcl anidms 2nn mpan 2cn nncni 2ne0
      divcli mulassi divassi sqvali opreq1i divcan2i 3eqtr4ri eqtr3i syl5eqelr
      syl c1 caddc wn ax1cn addcli binom2i mulid1i opreq2i sq1 opreq12i
      nnsqcli mulcli add23i 3eqtri nnrei 1re readdcli recni divdiri divcan3i
      3eqtr3i eleq1i cmin clt wbr cc0 addid2i 2re nngt0i lt01 addgt0ii 2pos
      divgt0ii 0re redivcli ltadd1i mpbi eqbrtrri wb nnsub mpbii addsubassi
      subidi addid1i sylbi 3syl con3i nneoi 3imtr4i impbii ) ACDEZFGZACHEZCDEZF
      GZWRWQWQIEZFGZXAWRXCWQWQJKXCCXBIEZFWTCFGZXCXDFGLCXBJMCWQIEZWQIEZXDWTCWQWQ
      NACABOZNPQZXIRAAIEZCDEAWQIEWTXGAACXHXHNPSWSXJCDAXHTUAXFAWQIACXHNPUBUAUCUD
      UEUFWSUGUHEZCDEZFGZUIAUGUHEZCDEZFGZUIXAWRXPXMXPXOXOIEZFGZCXQIEZFGZXMXPXRX
      OXOJKXEXRXTLCXQJMXTXLAUHEZFGZXMXSYAFCXOIEZXOIEXNXOIEZXSYAYCXNXOIXNCAUGXHU
      JUKZNPUBUACXOXONXNCYENPQZYFRXNXNIEZCDEZYDYAXNXNCYEYENPSYHXKCAIEZUHEZCDEXL
      YICDEZUHEYAYGYJCDXNCHEZYGYJXNYETYLWSCAUGIEZIEZUHEZUGCHEZUHEWSYIUHEZUGUHEY
      JAUGXHUJULYOYQYPUGUHYNYIWSUHYMACIAXHUMUNUNUOUPWSYIUGWSABUQZOCANXHURZUJUSU
      TUDUAXKYICXKWSUGWSYRVAZVBVCZVDYSNPVEYKAXLUHACXHNPVFUNUTUDVGVHYBYAAVIEZFXL
      YBAYAVJVKZUUBFGZVLAUHEZAYAVJAXHVMVLXLVJVKUUEYAVJVKXKCUUAVNWSUGYTVBWSYRVOV
      PVQVRVSVLXLAVTXKCUUAVNPWAZABVAWBWCWDAFGYBUUCUUDWEBAYAWFMWGUUBXLAAVIEZUHEX
      LVLUHEXLXLAAXLUUFVDZXHXHWHUUGVLXLUHAXHWIUNXLUUHWJUTUEWKWLWMWSYRWNABWNWOWP
      $.
      $( [20-Aug-2001] $)
  $}

