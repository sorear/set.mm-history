$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        More natural number properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    nnsqcl.1 $e |- N e. NN $.
    $( The square of a natural number is a natural number. $)
    nnsqcl $p |- ( N ^ 2 ) e. NN $=
      ( cn wcel c2 cn0 cexp co 2nn0 nnexpclt mp2an ) ACDEFDAEGHCDBIAEJK $.
      $( [20-Aug-2001] $)

    $( A natural number is less than or equal to its square. $)
    nnlesq $p |- N <_ ( N ^ 2 ) $=
      ( c1 cmul co c2 cexp cle nncn mulid1 wbr cn wcel nnge1t ax-mp cc0 clt wb
      nngt0 1re nnre lemul2 mpbi sqval breqtrr eqbrtrr ) ACDEZAAFGEZHAABIZJUGAA
      DEZUHHCAHKZUGUJHKZALMUKBANOPAQKUKULRABSCAATABUAZUMUBOUCAUIUDUEUF $.
      $( [15-Sep-1999] $)

    $( A natural number is even iff its square is even. $)
    nnesq $p |- ( ( N / 2 ) e. NN <-> ( ( N ^ 2 ) / 2 ) e. NN ) $=
      ( c2 cdiv co cn wcel cexp cmul nnmulclt anidms 2nn mpan 2cn nncn 2ne0
      divcl mulass divass sqval opreq1i divcan2 3eqtr4r eqtr3 syl5eqelr syl c1
      caddc wn ax1cn addcl binom2 mulid1 opreq2i sq1 opreq12i nnsqcl mulcl
      add23 3eqtr nnre 1re readdcl recn divdir divcan3 3eqtr3 eleq1i cmin clt
      wbr cc0 addid2 2re nngt0 lt01 addgt0i 2pos divgt0i 0re redivcl ltadd1
      mpbi eqbrtrr wb nnsubt mpbii addsubass subid addid1 sylbi 3syl con3i
      nneo 3imtr4 impbi ) ACDEZFGZACHEZCDEZFGZWRWQWQIEZFGZXAWRXCWQWQJKXCCXBIEZF
      WTCFGZXCXDFGLCXBJMCWQIEZWQIEZXDWTCWQWQNACABOZNPQZXIRAAIEZCDEAWQIEWTXGAACX
      HXHNPSWSXJCDAXHTUAXFAWQICANXHPUBUAUCUDUEUFWSUGUHEZCDEZFGZUIAUGUHEZCDEZFGZ
      UIXAWRXPXMXPXOXOIEZFGZCXQIEZFGZXMXPXRXOXOJKXEXRXTLCXQJMXTXLAUHEZFGZXMXSYA
      FCXOIEZXOIEXNXOIEZXSYAYCXNXOICXNNAUGXHUJUKZPUBUACXOXONXNCYENPQZYFRXNXNIEZ
      CDEZYDYAXNXNCYEYENPSYHXKCAIEZUHEZCDEXLYICDEZUHEYAYGYJCDXNCHEZYGYJXNYETYLW
      SCAUGIEZIEZUHEZUGCHEZUHEWSYIUHEZUGUHEYJAUGXHUJULYOYQYPUGUHYNYIWSUHYMACIAX
      HUMUNUNUOUPWSYIUGWSABUQZOCANXHURZUJUSUTUDUAXKYICXKWSUGWSYRVAZVBVCZVDYSNPV
      EYKAXLUHCANXHPVFUNUTUDVGVHYBYAAVIEZFXLYBAYAVJVKZUUBFGZVLAUHEZAYAVJAXHVMVL
      XLVJVKUUEYAVJVKXKCUUAVNWSUGYTVBWSYRVOVPVQVRVSVLXLAVTXKCUUAVNPWAZABVAWBWCW
      DAFGYBUUCUUDWEBAYAWFMWGUUBXLAAVIEZUHEXLVLUHEXLXLAAXLUUFVDZXHXHWHUUGVLXLUH
      AXHWIUNXLUUHWJUTUEWKWLWMWSYRWNABWNWOWP $.
      $( [20-Aug-2001] $)
  $}

