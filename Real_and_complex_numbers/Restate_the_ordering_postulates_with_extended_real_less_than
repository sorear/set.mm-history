$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      Restate the ordering postulates with extended real "less than"
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Ordering on reals satisfies strict trichotomy.  Axiom 22 of 27 for real
     and complex numbers, derived from ZF set theory.  (This restates
     ~ pre-axlttri with ordering on the extended reals.) $)
  axlttri $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A < B <-> -. ( A = B \/ B < A ) ) ) $=
    ( cr wcel wa cltrr wbr wceq wo wn clt pre-axlttri ltxrlt wb ancoms orbi2d
    notbid 3bitr4d ) ACDZBCDZEZABFGABHZBAFGZIZJABKGUBBAKGZIZJABLABMUAUFUDUAUEUC
    UBTSUEUCNBAMOPQR $.
    $( [13-Oct-2005] $)

  $( Ordering on reals is transitive.  Axiom 23 of 27 for real and complex
     numbers, derived from ZF set theory.  (This restates ~ pre-axlttrn with
     ordering on the extended reals.) $)
  axlttrn $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A < B /\ B < C ) -> A < C ) ) $=
    ( cr wcel w3a cltrr wbr wa clt pre-axlttrn wb ltxrlt 3adant3 3adant1
    anbi12d 3adant2 3imtr4d ) ADEZBDEZCDEZFZABGHZBCGHZIACGHZABJHZBCJHZIACJHZABC
    KUBUFUCUGUDSTUFUCLUAABMNTUAUGUDLSBCMOPSUAUHUELTACMQR $.
    $( [13-Oct-2005] $)

  $( Ordering property of addition on reals.  Axiom 24 of 27 for real and
     complex numbers, derived from ZF set theory.  (This restates ~ pre-axltadd
     with ordering on the extended reals.) $)
  axltadd $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B -> ( C + A ) < ( C + B ) ) ) $=
    ( cr wcel w3a cltrr wbr caddc co clt pre-axltadd wb ltxrlt 3adant3 wa
    readdcl syl2an 3impdi 3coml 3imtr4d ) ADEZBDEZCDEZFABGHZCAIJZCBIJZGHZABKHZU
    FUGKHZABCLUBUCUIUEMUDABNOUDUBUCUJUHMZUDUBUCUKUFDEUGDEUKUDUBPUDUCPUFUGNCAQCB
    QRSTUA $.
    $( [13-Oct-2005] $)

  $( The product of two positive reals is positive.  Axiom 25 of 27 for real
     and complex numbers, derived from ZF set theory.  (This restates
     ~ pre-axmulgt0 with ordering on the extended reals.) $)
  axmulgt0 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( ( 0 < A /\ 0 < B ) -> 0 < ( A x. B ) ) ) $=
    ( cr wcel wa cc0 cltrr wbr cmul co clt pre-axmulgt0 wb 0re ltxrlt mpan
    bi2anan9 remulcl sylancr 3imtr4d ) ACDZBCDZEZFAGHZFBGHZEFABIJZGHZFAKHZFBKHZ
    EFUFKHZABLUAUHUDUBUIUEFCDZUAUHUDMNFAOPUKUBUIUEMNFBOPQUKUFCDUJUGMUCFUFONABRS
    T $.
    $( [13-Oct-2005] $)

  ${
    $d x y z A $.
    $( A non-empty, bounded-above set of reals has a supremum.  Axiom 27 of 27
       for real and complex numbers, derived from ZF set theory.  (This
       restates ~ pre-axsup with ordering on the extended reals.) $)
    axsup $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y < x ) ->
                E. x e. RR ( A. y e. A -. x < y /\
                A. y e. RR ( y < x -> E. z e. A y < z ) ) ) $=
      ( cr wss c0 wne cv clt wbr wral wrex wn wi wa cltrr pre-axsup 3expia wb
      wcel ltxrlt ssel2 sylan an1rs ralbidva rexbidva adantr ancoms notbid
      adantll adantlr imbi12d anbi12d 3imtr4d 3impia ) DEFZDGHZBIZAIZJKZBDLZAEM
      ZUTUSJKZNZBDLZVAUSCIZJKZCDMZOZBELZPZAEMZUQURPUSUTQKZBDLZAEMZUTUSQKZNZBDLZ
      VNUSVGQKZCDMZOZBELZPZAEMZVCVMUQURVPWEABCDRSUQVCVPTURUQVBVOAEUQUTEUAZPZVAV
      NBDUQUSDUAZWFVAVNTZUSEUAZWFWIUQWHPZUSUTUBZDEUSUCZUDUEUFUGUHUQVMWETURUQVLW
      DAEWGVFVSVKWCWGVEVRBDWGWHPVDVQUQWHWFVDVQTZWJWFWNWKWFWJWNUTUSUBUIWMUDUEUJU
      FWGVJWBBEWGWJPVAVNVIWAWFWJWIUQWJWFWIWLUIUKUQWJVIWATWFUQWJPVHVTCDUQVGDUAZW
      JVHVTTZVGEUAZWJWPUQWOPWJWQWPUSVGUBUIDEVGUCUDUEUGULUMUFUNUGUHUOUP $.
      $( [13-Oct-2005] $)
  $}

