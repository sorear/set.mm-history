$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      Restate the ordering postulates with extended real "less than"
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Ordering on reals satisfies strict trichotomy.  Axiom 22 of 27 for real
     and complex numbers, derived from ZF set theory.  (This restates
     ~ ax-pre-lttri with ordering on the extended reals.) $)
  axlttri $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A < B <-> -. ( A = B \/ B < A ) ) ) $=
    ( cr wcel wa cltrr wbr wceq wo clt ax-pre-lttri ltxrlt ancoms orbi2d notbid
    wn wb 3bitr4d ) ACDZBCDZEZABFGABHZBAFGZIZPABJGUBBAJGZIZPABKABLUAUFUDUAUEUCU
    BTSUEUCQBALMNOR $.
    $( [13-Oct-2005] $)

  $( Ordering on reals is transitive.  Axiom 23 of 27 for real and complex
     numbers, derived from ZF set theory.  (This restates ~ ax-pre-lttrn with
     ordering on the extended reals.) $)
  axlttrn $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A < B /\ B < C ) -> A < C ) ) $=
    ( cr wcel w3a cltrr wbr wa clt ax-pre-lttrn 3adant3 3adant1 anbi12d 3adant2
    wb ltxrlt 3imtr4d ) ADEZBDEZCDEZFZABGHZBCGHZIACGHZABJHZBCJHZIACJHZABCKUBUFU
    CUGUDSTUFUCPUAABQLTUAUGUDPSBCQMNSUAUHUEPTACQOR $.
    $( [13-Oct-2005] $)

  $( Ordering property of addition on reals.  Axiom 24 of 27 for real and
     complex numbers, derived from ZF set theory.  (This restates
     ~ ax-pre-ltadd with ordering on the extended reals.) $)
  axltadd $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                ( A < B -> ( C + A ) < ( C + B ) ) ) $=
    ( cr wcel w3a cltrr wbr caddc co clt ax-pre-ltadd wb ltxrlt 3adant3 readdcl
    wa syl2an 3impdi 3coml 3imtr4d ) ADEZBDEZCDEZFABGHZCAIJZCBIJZGHZABKHZUFUGKH
    ZABCLUBUCUIUEMUDABNOUDUBUCUJUHMZUDUBUCUKUDUBQUFDEUGDEUKUDUCQCAPCBPUFUGNRSTU
    A $.
    $( [13-Oct-2005] $)

  $( The product of two positive reals is positive.  Axiom 25 of 27 for real
     and complex numbers, derived from ZF set theory.  (This restates
     ~ ax-pre-mulgt0 with ordering on the extended reals.) $)
  axmulgt0 $p |- ( ( A e. RR /\ B e. RR ) ->
                ( ( 0 < A /\ 0 < B ) -> 0 < ( A x. B ) ) ) $=
    ( cr wcel wa cc0 cltrr wbr cmul co clt ax-pre-mulgt0 wb 0re ltxrlt bi2anan9
    mpan remulcl sylancr 3imtr4d ) ACDZBCDZEZFAGHZFBGHZEFABIJZGHZFAKHZFBKHZEFUF
    KHZABLUAUHUDUBUIUEFCDZUAUHUDMNFAOQUKUBUIUEMNFBOQPUCUKUFCDUJUGMNABRFUFOST $.
    $( [13-Oct-2005] $)

  ${
    $d x y z A $.
    $( A non-empty, bounded-above set of reals has a supremum.  Axiom 27 of 27
       for real and complex numbers, derived from ZF set theory.  (This
       restates ~ ax-pre-sup with ordering on the extended reals.) $)
    axsup $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y < x ) ->
                E. x e. RR ( A. y e. A -. x < y /\
                A. y e. RR ( y < x -> E. z e. A y < z ) ) ) $=
      ( cr cv clt wbr wral wrex wa cltrr wb wcel ltxrlt sylan ralbidva rexbidva
      an32s ancoms wss c0 wne wn ax-pre-sup 3expia ssel2 adantr adantll adantlr
      wi notbid imbi12d anbi12d 3imtr4d 3impia ) DEUAZDUBUCZBFZAFZGHZBDIZAEJZUT
      USGHZUDZBDIZVAUSCFZGHZCDJZUKZBEIZKZAEJZUQURKUSUTLHZBDIZAEJZUTUSLHZUDZBDIZ
      VNUSVGLHZCDJZUKZBEIZKZAEJZVCVMUQURVPWEABCDUEUFUQVCVPMURUQVBVOAEUQUTENZKZV
      AVNBDUQUSDNZWFVAVNMZUQWHKZUSENZWFWIDEUSUGZUSUTOZPSQRUHUQVMWEMURUQVLWDAEWG
      VFVSVKWCWGVEVRBDWGWHKVDVQUQWHWFVDVQMZWJWKWFWNWLWFWKWNUTUSOTPSULQWGVJWBBEW
      GWKKVAVNVIWAWFWKWIUQWKWFWIWMTUIUQWKVIWAMWFUQWKKVHVTCDUQVGDNZWKVHVTMZUQWOK
      VGENZWKWPDEVGUGWKWQWPUSVGOTPSRUJUMQUNRUHUOUP $.
      $( [13-Oct-2005] $)
  $}

