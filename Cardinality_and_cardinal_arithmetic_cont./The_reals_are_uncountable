$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The reals are uncountable
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    ruclem1.a $e |- A e. RR $.
    ruclem1.b $e |- B e. RR $.
    $( Lemma for ~ ruc (the reals are uncountable).  This is an arithmetic fact
       that will be used to compute ordering relations. $)
    ruclem1 $p |- ( A < B <-> A < ( ( ( 2 x. A ) + B ) / 3 ) ) $=
      ( clt wbr c3 cmul co c2 caddc cdiv 2re remulcli ltadd2i c1 df-3 opreq1i
      2cn ax1cn recni adddiri mulid2i opreq2i 3eqtrri breq1i bitri 3re
      readdcli 3pos ltdiv1ii gt0ne0ii divcan3i 3bitri ) ABEFZGAHIZJAHIZBKIZEFZU
      PGLIZURGLIZEFAVAEFUOUQAKIZUREFUSABUQCDJAMCNZOVBUPUREUPJPKIZAHIUQPAHIZKIVB
      GVDAHQRJPASTACUAZUBVEAUQKAVFUCUDUEUFUGUPURGGAUHCNUQBVCDUIUHUJUKUTAVAEAGVF
      GUHUAGUHUJULUMUFUN $.
      $( [18-Oct-2004] $) $( [14-Oct-2004] $)

    $( Lemma for ~ ruc .  Arithmetic fact that will be used to compute ordering
       relations. $)
    ruclem2 $p |- ( A < B <->
                  ( ( ( 2 x. A ) + B ) / 3 ) < ( ( A + ( 2 x. B ) ) / 3 ) ) $=
      ( clt wbr caddc co c2 cmul c3 cdiv readdcli ltadd1i recni 2timesi
      opreq1i addassi eqtri opreq2i add12i breq12i 2re remulcli 3re 3pos
      ltdiv1ii 3bitr2i ) ABEFAABGHZGHZBUIGHZEFIAJHZBGHZAIBJHZGHZEFUMKLHUOKLHEFA
      BUICDABCDMNUMUJUOUKEUMAAGHZBGHUJULUPBGAACOZPQAABUQUQBDOZRSUOABBGHZGHUKUNU
      SAGBURPTABBUQURURUASUBUMUOKULBIAUCCUDDMAUNCIBUCDUDMUEUFUGUH $.
      $( [18-Oct-2004] $) $( [14-Oct-2004] $)

    $( Lemma for ~ ruc .  Arithmetic fact that will be used to compute ordering
       relations. $)
    ruclem3 $p |- ( A < B <-> ( ( A + ( 2 x. B ) ) / 3 ) < B ) $=
      ( clt wbr c2 cmul co caddc c3 cdiv 2re remulcli ltadd1i c1 df-3 2cn
      ax1cn addcomi eqtri opreq1i recni adddiri mulid2i 3eqtrri breq2i bitri
      readdcli 3re 3pos ltdiv1ii gt0ne0ii divcan3i 3bitri ) ABEFZAGBHIZJIZKBHIZ
      EFZURKLIZUSKLIZEFVABEFUPURBUQJIZEFUTABUQCDGBMDNZOVCUSUREUSPGJIZBHIPBHIZUQ
      JIVCKVEBHKGPJIVEQGPRSTUAUBPGBSRBDUCZUDVFBUQJBVGUEUBUFUGUHURUSKAUQCVDUIKBU
      JDNUJUKULVBBVAEBKVGKUJUCKUJUKUMUNUGUO $.
      $( [18-Oct-2004] $) $( [14-Oct-2004] $)
  $}

  $( Lemma for ~ ruc .  Helper lemma showing a tedious equality used several
     times. $)
  ruclem4 $p |- ( ( A = C /\ B = D ) ->
                if ( ( ( 1st ` A ) < B /\ B < ( 2nd ` A ) ) ,
                 <. ( ( ( 2 x. B ) + ( 2nd ` A ) ) / 3 ) ,
                    ( ( B + ( 2 x. ( 2nd ` A ) ) ) / 3 ) >. ,
                 <. ( ( ( 2 x. ( 1st ` A ) ) + ( 2nd ` A ) ) / 3 ) ,
                    ( ( ( 1st ` A ) + ( 2 x. ( 2nd ` A ) ) ) / 3 ) >. ) =
                if ( ( ( 1st ` C ) < D /\ D < ( 2nd ` C ) ) ,
                 <. ( ( ( 2 x. D ) + ( 2nd ` C ) ) / 3 ) ,
                    ( ( D + ( 2 x. ( 2nd ` C ) ) ) / 3 ) >. ,
                 <. ( ( ( 2 x. ( 1st ` C ) ) + ( 2nd ` C ) ) / 3 ) ,
                    ( ( ( 1st ` C ) + ( 2 x. ( 2nd ` C ) ) ) / 3 ) >. ) ) $=
    ( wceq c1st cfv clt wbr c2nd wa c2 cmul co caddc c3 cdiv cop cif fveq2
    breq1d breq2d anbi12d ifbid ifeq12 opreq2d opreq1d opeq12d opreq12d sylanc
    eqtrd breq2 breq1 opreq2 opreq1 ifeq1d sylan9eq ) ACEZBDEZAFGZBHIZBAJGZHIZK
    ZLBMNZVBONZPQNZBLVBMNZONZPQNZRZLUTMNZVBONZPQNZUTVHONZPQNZRZSZCFGZBHIZBCJGZH
    IZKZVEWAONZPQNZBLWAMNZONZPQNZRZLVSMNZWAONZPQNZVSWFONZPQNZRZSZVSDHIZDWAHIZKZ
    LDMNZWAONZPQNZDWFONZPQNZRZWOSZURVRWCVKVQSZWPURVDWCVKVQURVAVTVCWBURUTVSBHACF
    TZUAURVBWABHACJTZUBUCUDVKWIEVQWOEXGWPEURWCVKWIVQWOUEURVGWEVJWHURVFWDPQURVBW
    AVEOXIUFUGURVIWGPQURVHWFBOURVBWALMXIUFZUFUGUHURVNWLVPWNURVMWKPQURVLWJVBWAOU
    RUTVSLMXHUFXIUIUGURVOWMPQURUTVSVHWFOXHXJUIUGUHUJUKUSWPWSWIWOSXFUSWCWSWIWOUS
    VTWQWBWRBDVSHULBDWAHUMUCUDUSWSWIXEWOUSWEXBWHXDUSWDXAPQUSVEWTWAOBDLMUNUGUGUS
    WGXCPQBDWFOUOUGUHUPUKUQ $.
    $( [18-Oct-2004] $) $( [9-Oct-2004] $)

  ${
    ruclem5.0 $e |- F : NN --> RR $.
    ruclem5.1 $e |- C = ( { <. 1 , <. ( ( F ` 1 ) + 1 ) ,
                     ( ( F ` 1 ) + 2 ) >. >. } u. ( F |` ( NN \ { 1 } ) ) ) $.
    $( Lemma for ~ ruc .  Helper lemma showing the input function used for our
       recursive sequence builder (defined in ~ ruclem13 ) is a set. $)
    ruclem5 $p |- C e. _V $=
      ( c1 cfv caddc co c2 cop csn cn cdif cres cun cvv snex wcel cr wf nnex
      fex mp2an resexg ax-mp unex eqeltri ) AEEBFZEGHUHIGHJJZKZBLEKMZNZOPDUJULU
      IQBPRZULPRLSBTLPRUMCUALSPBUBUCBUKPUDUEUFUG $.
      $( [18-Oct-2004] $) $( [7-Oct-2004] $)

    $( Lemma for ~ ruc .  Helper lemma showing the input function used for our
       recursive sequence builder (defined in ~ ruclem13 ) matches our input
       mapping ` F ` for successor values. $)
    ruclem6 $p |- ( C |` ( NN \ { 1 } ) ) = ( F |` ( NN \ { 1 } ) ) $=
      ( cn c1 csn cdif cres cfv caddc co c2 cop cun wceq reseq1 ax-mp resundir
      c0 cin difdisj wfn wb wf1o 1nn elisseti opex f1osn f1ofn fnresdisj mpbi
      residm uneq12i uncom un0 3eqtri ) AEFGZHZIZFFBJZFKLZVAMKLZNZNGZBUSIZOZUSI
      ZVEUSIZVFUSIZOZVFAVGPUTVHPDAVGUSQRVEVFUSSVKTVFOVFTOVFVITVJVFURUSUATPZVITP
      ZUREUBVEURUCZVLVMUDURVDGZVEUEVNFVDFEUFUGVBVCUHUIURVOVEUJRURUSVEUKRULBUSUM
      UNTVFUOVFUPUQUQ $.
      $( [18-Oct-2004] $) $( [9-Oct-2004] $)

    $( Lemma for ~ ruc .  Helper lemma showing the initial value of the input
       function for our recursive sequence builder (defined in ~ ruclem13 ). $)
    ruclem7 $p |- ( C ` 1 ) =
                     <. ( ( F ` 1 ) + 1 ) , ( ( F ` 1 ) + 2 ) >. $=
      ( c1 cfv caddc co c2 cop cn 1nn elisseti opex fvsnun1 ) EEBFZEGHZPIGHZJKB
      AEKLMQRNDO $.
      $( [18-Oct-2004] $) $( [7-Oct-2004] $)

    ${
      ruclem8.a $e |- A e. NN $.
      $( Lemma for ~ ruc .  Helper lemma showing the successor value of the
         input function for our recursive sequence builder (defined in
         ~ ruclem13 ). $)
      ruclem8 $p |- ( C ` ( A + 1 ) ) = ( F ` ( A + 1 ) ) $=
        ( c1 caddc co cn csn cdif wcel cfv wceq wne eldifsn peano2nn ax-mp clt
        wbr cc0 nngt0i 0re nnrei 1re ltadd1i ax1cn addid2i breq1i bitri mpbi
        wb nngt1ne1 mpbir2an c2 cop 1nn elisseti opex fvsnun2 ) AGHIZJGKLMZVBBN
        VBCNOVCVBJMZVBGPZVBJGQAJMVDFARSZGVBTUAZVEUBATUAZVGAFUCVHUBGHIZVBTUAVGUB
        AGUDAFUEUFUGVIGVBTGUHUIUJUKULVDVGVEUMVFVBUNSULUOGGCNZGHIZVJUPHIZUQJVBCB
        GJURUSVKVLUTEVAS $.
        $( [18-Oct-2004] $) $( [14-Oct-2004] $)
    $}
  $}

  ${
    $d x y z $.  $d z A $.
    ruclem9.2 $e |- D = { <. <. x , y >. , z >. |
             ( ( x e. ( RR X. RR ) /\ y e. RR ) /\ z = A ) } $.
    $( Lemma for ~ ruc .  Helper lemma showing the operation used for our
       recursive sequence builder (defined in ~ ruclem13 ) is a set. $)
    ruclem9 $p |- D e. _V $=
      ( cr cxp reex xpex oprabex2 ) ABCGGHGDEGGIIJIFK $.
      $( [18-Oct-2004] $) $( [7-Oct-2004] $)
  $}

  ${
    $d x y $.
    ruclem10.a $e |- A e. NN $.
    ruclem.d $e |- D e. _V $.
    ruclem.c $e |- C e. _V $.
    ruclem10.3 $e |- G = ( 1st o. ( D seq1 C ) ) $.
    $( Lemma for ~ ruc .  The values of our recursive sequence builder are
       pairs of real numbers.  The values of our constructed function ` G ` are
       the first of these pairs. $)
    ruclem10 $p |- ( 1st ` ( ( D seq1 C ) ` A ) ) = ( G ` A ) $=
      ( cfv c1st cseq1 co ccom fveq1i wfun cn wfn wcel wceq cvv wfo fo1st
      fofun ax-mp seq1fn fvco2 mp3an eqtr2i ) ADIAJCBKLZMZIZAUIIJIZADUJHNJOZUIP
      QAPRUKULSTTJUAUMUBTTJUCUDCBFGUEEPAJUIUFUGUH $.
      $( [18-Oct-2004] $) $( [14-Oct-2004] $)
  $}

  ${
    $d x y $.
    ruclem11.a $e |- A e. NN $.
    ruclem11.d $e |- D e. _V $.
    ruclem11.c $e |- C e. _V $.
    ruclem11.4 $e |- H = ( 2nd o. ( D seq1 C ) ) $.
    $( Lemma for ~ ruc .  The values of our recursive sequence builder are
       pairs of real numbers.  The values of our constructed function ` H ` are
       the second of these pairs. $)
    ruclem11 $p |- ( 2nd ` ( ( D seq1 C ) ` A ) ) = ( H ` A ) $=
      ( cfv c2nd cseq1 co ccom fveq1i wfun cn wfn wcel wceq cvv wfo fo2nd
      fofun ax-mp seq1fn fvco2 mp3an eqtr2i ) ADIAJCBKLZMZIZAUIIJIZADUJHNJOZUIP
      QAPRUKULSTTJUAUMUBTTJUCUDCBFGUEEPAJUIUFUGUH $.
      $( [18-Oct-2004] $) $( [14-Oct-2004] $)
  $}

  ${
    $d x y z w v u $.
    ruclem12.2 $e |- D = { <. <. x , y >. , z >. |
             ( ( x e. ( RR X. RR ) /\ y e. RR ) /\
             z = if ( ( ( 1st ` x ) < y /\ y < ( 2nd ` x ) ) ,
                 <. ( ( ( 2 x. y ) + ( 2nd ` x ) ) / 3 ) ,
                    ( ( y + ( 2 x. ( 2nd ` x ) ) ) / 3 ) >. ,
                 <. ( ( ( 2 x. ( 1st ` x ) ) + ( 2nd ` x ) ) / 3 ) ,
                    ( ( ( 1st ` x ) + ( 2 x. ( 2nd ` x ) ) ) / 3 ) >. ) ) } $.
    $( Lemma for ~ ruc .  A helper lemma that changes bound variables. $)
    ruclem12 $p |- D = { <. <. w , v >. , u >. |
             ( ( w e. ( RR X. RR ) /\ v e. RR ) /\
             u = if ( ( ( 1st ` w ) < v /\ v < ( 2nd ` w ) ) ,
                 <. ( ( ( 2 x. v ) + ( 2nd ` w ) ) / 3 ) ,
                    ( ( v + ( 2 x. ( 2nd ` w ) ) ) / 3 ) >. ,
                 <. ( ( ( 2 x. ( 1st ` w ) ) + ( 2nd ` w ) ) / 3 ) ,
                    ( ( ( 1st ` w ) + ( 2 x. ( 2nd ` w ) ) ) / 3 ) >. ) ) } $=
      ( cv cr cxp wcel wa c1st cfv clt wbr c2nd c2 cmul co caddc c3 cdiv cop
      cif wceq copab2 eleq1 bi2anan9 ruclem4 eqeq2d anbi12d cbvoprab12v eqeq1
      anbi2d cbvoprab3v 3eqtri ) GAIZJJKZLZBIZJLZMZCIZUSNOZVBPQVBUSROZPQMSVBTUA
      VGUBUAUCUDUAVBSVGTUAZUBUAUCUDUAUESVFTUAVGUBUAUCUDUAVFVHUBUAUCUDUAUEUFZUGZ
      MZABCUHDIZUTLZEIZJLZMZVEVLNOZVNPQVNVLROZPQMSVNTUAVRUBUAUCUDUAVNSVRTUAZUBU
      AUCUDUAUESVQTUAVRUBUAUCUDUAVQVSUBUAUCUDUAUEUFZUGZMZDECUHVPFIZVTUGZMZDEFUH
      HVKWBABCDEUSVLUGZVBVNUGZMZVDVPVJWAWFVAVMWGVCVOUSVLUTUIVBVNJUIUJWHVIVTVEUS
      VBVLVNUKULUMUNWBWEDECFVEWCUGWAWDVPVEWCVTUOUPUQUR $.
      $( [18-Oct-2004] $) $( [8-Oct-2004] $)
  $}

  ${
    $d x y z w v u $.  $d w v u D $.
    ruclem13.0 $e |- F : NN --> RR $.
    ruclem13.1 $e |- C = ( { <. 1 , <. ( ( F ` 1 ) + 1 ) ,
                     ( ( F ` 1 ) + 2 ) >. >. } u. ( F |` ( NN \ { 1 } ) ) ) $.
    ruclem13.2 $e |- D = { <. <. x , y >. , z >. |
             ( ( x e. ( RR X. RR ) /\ y e. RR ) /\
             z = if ( ( ( 1st ` x ) < y /\ y < ( 2nd ` x ) ) ,
                 <. ( ( ( 2 x. y ) + ( 2nd ` x ) ) / 3 ) ,
                    ( ( y + ( 2 x. ( 2nd ` x ) ) ) / 3 ) >. ,
                 <. ( ( ( 2 x. ( 1st ` x ) ) + ( 2nd ` x ) ) / 3 ) ,
                    ( ( ( 1st ` x ) + ( 2 x. ( 2nd ` x ) ) ) / 3 ) >. ) ) } $.
    $( Lemma for ~ ruc .  A helper lemma showing the recursive sequence builder
       used for our construction maps natural numbers to pairs of reals. $)
    ruclem13 $p |- ( D seq1 C ) : NN --> ( RR X. RR ) $=
      ( cn cr cxp cseq1 co wf wfn crn wss df-f cv c1st cfv clt wbr c2nd wa c2
      cmul caddc c3 cdiv cop cif ruclem9 ruclem5 seq1fn c1 wcel csn cdif cres
      ruclem7 oprex opelxp 1nn ffvelrn mp2an 1re readdcli 2re mpbir2an eqeltri
      difss fssres ruclem6 feq1i mpbir wceq wi iftrue eleq1d opelxpi readdcl
      remulcl mpan sylan cc0 wne 3re 3pos gt0ne0ii redivcl mp3an23 syl sylan2
      sylanc ancoms adantll syl5bir wn iffalse adantrd pm2.61i elxp6 sylanb
      foprab seq1rn2 mp3an ) JKKLZEDMNZOXJJPXJQXIRZJXIXJSEDABCATZUAUBZBTZUCUDXN
      XLUEUBZUCUDUFZUGXNUHNZXOUINZUJUKNZXNUGXOUHNZUINZUJUKNZULZUGXMUHNZXOUINZUJ
      UKNZXMXTUINZUJUKNZULZUMZEIUNZDFGHUOZUPUQDUBZXIURJUQUSZUTZKDYOVAZOZXIKLXIE
      OXKYMUQFUBZUQUINZYRUGUINZULZXIDFGHVBUUAXIURYSKURYTKURYSYTKKYRUGUIVCVDYRUQ
      JKFOZUQJURYRKURGVEJKUQFVFVGZVHVIYRUGUUCVJVIVKVLYQYOKFYOVAZOZUUBYOJRUUEGJY
      NVMJKYOFVNVGYOKYPUUDDFGHVOVPVQABCXIKYJXIEIXLXMXOULVRZXMKURZXOKURZUFZUFXNK
      URZYJXIURZXLXIURUUIUUJUUKUUFXPUUIUUJUFZUUKVSXPUUKYCXIURZUULXPYJYCXIXPYCYI
      VTWAUUHUUJUUMUUGUUJUUHUUMXSKURZYBKURZUUMUUJUUHUFZXSYBKKWBUUPXRKURZUUNXQKU
      RZUUHUUQUUJXQXOWCUGKURZUUJUURVJUGXNWDWEWFUUQUJKURZUJWGWHZUUNWIUJWIWJWKZXR
      UJWLWMWNUUPYAKURZUUOUUJXTKURZUVCUUHXNXTWCUUSUUHUVDVJUGXOWDWEZWOUVCUUTUVAU
      UOWIUVBYAUJWLWMWNWPWQWRWSXPWTZUUIUUKUUJUVFUUKYIXIURZUUIUVFYJYIXIXPYCYIXAW
      AYFKURZYHKURZUVGUUIYFYHKKWBUUIYEKURZUVHYDKURZUUHUVJUUGYDXOWCUUSUUGUVKVJUG
      XMWDWEWFUVJUUTUVAUVHWIUVBYEUJWLWMWNUUIYGKURZUVIUUGUVDUVLUUHXMXTWCUVEWOUVL
      UUTUVAUVIWIUVBYGUJWLWMWNWPWSXBXCWRXLKKXDXEXFXIKEDYKYLXGXHVK $.
      $( [18-Oct-2004] $) $( [14-Oct-2004] $)
  $}

  ${
    $d x y z w v u $.  $d A w v u $.  $d C w v u $.  $d D w v u $.
    $d G w v u $.  $d H w v u $.  $d F z w v u $.  $d B w v $.
    ruclem.0 $e |- F : NN --> RR $.
    ruclem.1 $e |- C = ( { <. 1 , <. ( ( F ` 1 ) + 1 ) ,
                     ( ( F ` 1 ) + 2 ) >. >. } u. ( F |` ( NN \ { 1 } ) ) ) $.
    ruclem.2 $e |- D = { <. <. x , y >. , z >. |
             ( ( x e. ( RR X. RR ) /\ y e. RR ) /\
             z = if ( ( ( 1st ` x ) < y /\ y < ( 2nd ` x ) ) ,
                 <. ( ( ( 2 x. y ) + ( 2nd ` x ) ) / 3 ) ,
                    ( ( y + ( 2 x. ( 2nd ` x ) ) ) / 3 ) >. ,
                 <. ( ( ( 2 x. ( 1st ` x ) ) + ( 2nd ` x ) ) / 3 ) ,
                    ( ( ( 1st ` x ) + ( 2 x. ( 2nd ` x ) ) ) / 3 ) >. ) ) } $.
    ruclem.3 $e |- G = ( 1st o. ( D seq1 C ) ) $.
    ruclem.4 $e |- H = ( 2nd o. ( D seq1 C ) ) $.
    $( Lemma for ~ ruc .  A helper lemma showing the initial value of the
       recursive sequence builder used for our construction. $)
    ruclem14 $p |- ( ( D seq1 C ) ` 1 ) =
               <. ( ( F ` 1 ) + 1 ) , ( ( F ` 1 ) + 2 ) >. $=
      ( c1 cseq1 co cfv caddc c2 cop cv c1st clt wbr c2nd wa cmul c3 cdiv cif
      ruclem9 ruclem5 seq11 ruclem7 eqtri ) NEDOPQNDQNFQZNRPUPSRPTEDABCAUAZUBQZ
      BUAZUCUDUSUQUEQZUCUDUFSUSUGPUTRPUHUIPUSSUTUGPZRPUHUIPTSURUGPUTRPUHUIPURVA
      RPUHUIPTUJEKUKDFIJULUMDFIJUNUO $.
      $( [18-Oct-2004] $) $( [8-Oct-2004] $)

    ${
      ruclem15.a $e |- A e. NN $.
      $( Lemma for ~ ruc .  A helper lemma showing the successor value of the
         recursive sequence builder used for our construction. $)
      ruclem15 $p |- ( ( D seq1 C ) ` ( A + 1 ) ) =
  if ( ( ( G ` A ) < ( F ` ( A + 1 ) ) /\
   ( F ` ( A + 1 ) ) < ( H ` A ) ) ,
  <. ( ( ( 2 x. ( F ` ( A + 1 ) ) ) + ( H ` A ) ) / 3 ) ,
    ( ( ( F ` ( A + 1 ) ) + ( 2 x. ( H ` A ) ) ) / 3 ) >. ,
  <. ( ( ( 2 x. ( G ` A ) ) +
      ( H ` A ) ) / 3 ) ,
    ( ( ( G ` A ) +
      ( 2 x. ( H ` A ) ) ) / 3 ) >. ) $=
        ( vw vv vu c1 caddc co cseq1 cfv c1st clt wbr c2nd wa c2 cmul c3 cdiv
        cop cif cn wcel wceq cv ruclem9 ruclem5 seq1p1 ax-mp cr cxp wf
        ruclem13 ffvelrn mp2an ruclem8 peano2nn eqeltri opex ifex eqid ruclem4
        mpan2 mpan ruclem12 oprabval2 wb ruclem10 breq12i ruclem11 anbi12i
        ifbi opreq2i opreq12i opreq1i opeq12i ifeq12 eqtri 3eqtri ) DSTUAZFEUBU
        AZUCZDWNUCZWMEUCZFUAZWPUDUCZWQUEUFZWQWPUGUCZUEUFZUHZUIWQUJUAZXATUAZUKUL
        UAZWQUIXAUJUAZTUAZUKULUAZUMZUIWSUJUAZXATUAZUKULUAZWSXGTUAZUKULUAZUMZUNZ
        DHUCZWMGUCZUEUFZXSDIUCZUEUFZUHZUIXSUJUAZYATUAZUKULUAZXSUIYAUJUAZTUAZUKU
        LUAZUMZUIXRUJUAZYATUAZUKULUAZXRYGTUAZUKULUAZUMZUNZDUOUPZWOWRUQODFEABCAU
        RZUDUCZBURZUEUFUUAYSUGUCZUEUFUHUIUUAUJUAUUBTUAUKULUAUUAUIUUBUJUAZTUAUKU
        LUAUMUIYTUJUAUUBTUAUKULUAYTUUCTUAUKULUAUMUNFLUSZEGJKUTZVAVBWPVCVCVDZUPZ
        WQVCUPWRXQUQUOUUFWNVEYRUUGABCEFGJKLVFOUOUUFDWNVGVHWQXSVCDEGJKOVIZUOVCGV
        EWMUOUPZXSVCUPJYRUUIODVJVBUOVCWMGVGVHVKPQRWPWQUUFVCPURZUDUCZQURZUEUFUUL
        UUJUGUCZUEUFUHUIUULUJUAZUUMTUAUKULUAUULUIUUMUJUAZTUAUKULUAUMUIUUKUJUAUU
        MTUAUKULUAUUKUUOTUAUKULUAUMUNZXQFWSUULUEUFUULXAUEUFUHUUNXATUAUKULUAUULX
        GTUAUKULUAUMXPUNZXCXJXPXFXIVLXMXOVLVMUUJWPUQUULUULUQUUPUUQUQUULVNUUJUUL
        WPUULVOVPWPWPUQUULWQUQUUQXQUQWPVNWPUULWPWQVOVQABCPQRFLVRVSVHXQYCXJXPUNZ
        YQXCYCVTXQUURUQWTXTXBYBWSXRWQXSUEDEFHOUUDUUEMWAZUUHWBWQXSXAYAUEUUHDEFIO
        UUDUUENWCZWBWDXCYCXJXPWEVBXJYJUQXPYPUQUURYQUQXFYFXIYIXEYEUKULXDYDXAYATW
        QXSUIUJUUHWFUUTWGWHXHYHUKULWQXSXGYGTUUHXAYAUIUJUUTWFZWGWHWIXMYMXOYOXLYL
        UKULXKYKXAYATWSXRUIUJUUSWFUUTWGWHXNYNUKULWSXRXGYGTUUSUVAWGWHWIYCXJYJXPY
        PWJVHWKWL $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)
    $}

    $( Lemma for ~ ruc .  A helper lemma showing the initial value of our
       constructed ` G ` . $)
    ruclem16 $p |- ( G ` 1 ) = ( ( F ` 1 ) + 1 ) $=
      ( c1 cseq1 co cfv c1st caddc c2 cop ruclem14 fveq2i 1nn cv clt wbr c2nd
      wa cmul c3 cdiv cif ruclem9 ruclem5 ruclem10 oprex op1st 3eqtr3i ) NEDOPQ
      ZRQNFQZNSPZVATSPZUAZRQNGQVBUTVDRABCDEFGHIJKLMUBUCNDEGUDABCAUEZRQZBUEZUFUG
      VGVEUHQZUFUGUITVGUJPVHSPUKULPVGTVHUJPZSPUKULPUATVFUJPVHSPUKULPVFVISPUKULP
      UAUMEKUNDFIJUOLUPVBVCVANSUQURUS $.
      $( [18-Oct-2004] $) $( [9-Oct-2004] $)

    $( Lemma for ~ ruc .  A helper lemma showing our constructed function ` G `
       maps ` NN ` to real numbers. $)
    ruclem17 $p |- G : NN --> RR $=
      ( cn cr c1st cxp cres cseq1 co ccom wf f1stres ruclem13 fco mp2an crn
      wss wceq frn ax-mp cores eqtr4i feq1i mpbi ) NOPOOQZRZEDSTZUAZUBZNOGUBUPO
      UQUBNUPURUBZUTOOUCABCDEFIJKUDZNUPOUQURUEUFNOUSGUSPURUAZGURUGUPUHZUSVCUIVA
      VDVBNUPURUJUKPURUPULUKLUMUNUO $.
      $( [18-Oct-2004] $) $( [15-Oct-2004] $)

    ${
      ruclem18.a $e |- A e. NN $.
      $( Lemma for ~ ruc .  The value of our constructed function ` G ` when
         the value of the input function ` F ` lies between the previous values
         of ` G ` and ` H ` .  This assignment to ` G ` defines a new interval
         between ` G ` and ` H ` (see also ~ ruclem19 ) that avoids the value
         of ` F ` . $)
      ruclem18 $p |- (
       ( ( G ` A ) < ( F ` ( A + 1 ) ) /\ ( F ` ( A + 1 ) ) < ( H ` A ) ) ->
   ( G ` ( A + 1 ) ) = ( ( ( 2 x. ( F ` ( A + 1 ) ) ) + ( H ` A ) ) / 3 ) ) $=
        ( cfv c1 caddc co clt wbr wa cseq1 c1st c2 cmul c3 cdiv cop cif iftrue
        ruclem15 syl5eq fveq2d cn wcel peano2nn ax-mp cv c2nd ruclem9 ruclem5
        ruclem10 oprex op1st 3eqtr3g ) DHPZDQRSZGPZTUAVIDIPZTUAUBZVHFEUCSPZUDPU
        EVIUFSVJRSZUGUHSZVIUEVJUFSZRSUGUHSZUIZUDPVHHPVNVKVLVQUDVKVKVQUEVGUFSVJR
        SUGUHSVGVORSUGUHSUIZUJVQVLVKVQVRUKABCDEFGHIJKLMNOULUMUNVHEFHDUOUPVHUOUP
        ODUQURABCAUSZUDPZBUSZTUAWAVSUTPZTUAUBUEWAUFSWBRSUGUHSWAUEWBUFSZRSUGUHSU
        IUEVTUFSWBRSUGUHSVTWCRSUGUHSUIUJFLVAEGJKVBMVCVNVPVMUGUHVDVEVF $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  The value of our constructed function ` H ` when
         the value of the input function ` F ` lies between the previous values
         of ` G ` and ` H ` .  This assignment to ` H ` defines a new interval
         between ` G ` and ` H ` (see also ~ ruclem18 ) that avoids the value
         of ` F ` . $)
      ruclem19 $p |- (
       ( ( G ` A ) < ( F ` ( A + 1 ) ) /\ ( F ` ( A + 1 ) ) < ( H ` A ) ) ->
   ( H ` ( A + 1 ) ) = ( ( ( F ` ( A + 1 ) ) + ( 2 x. ( H ` A ) ) ) / 3 ) ) $=
        ( cfv c1 caddc co clt wbr wa cseq1 c2nd c2 cmul c3 cdiv cop cif iftrue
        ruclem15 syl5eq fveq2d cn wcel peano2nn ax-mp cv c1st ruclem9 ruclem5
        ruclem11 oprex op2nd 3eqtr3g ) DHPZDQRSZGPZTUAVIDIPZTUAUBZVHFEUCSPZUDPU
        EVIUFSVJRSZUGUHSZVIUEVJUFSZRSZUGUHSZUIZUDPVHIPVQVKVLVRUDVKVKVRUEVGUFSVJ
        RSUGUHSVGVORSUGUHSUIZUJVRVLVKVRVSUKABCDEFGHIJKLMNOULUMUNVHEFIDUOUPVHUOU
        PODUQURABCAUSZUTPZBUSZTUAWBVTUDPZTUAUBUEWBUFSWCRSUGUHSWBUEWCUFSZRSUGUHS
        UIUEWAUFSWCRSUGUHSWAWDRSUGUHSUIUJFLVAEGJKVBNVCVNVQVMUGUHVDVPUGUHVDVEVF
        $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  The value of our constructed function ` G ` when
         the value of the input function ` F ` does not lie between the
         previous values of ` G ` and ` H ` .  This assignment to ` G ` just
         shrinks the interval between ` G ` and ` H ` by some arbitrary
         amount. $)
      ruclem20 $p |- (
    -. ( ( G ` A ) < ( F ` ( A + 1 ) ) /\ ( F ` ( A + 1 ) ) < ( H ` A ) ) ->
   ( G ` ( A + 1 ) ) = ( ( ( 2 x. ( G ` A ) ) + ( H ` A ) ) / 3 ) ) $=
        ( cfv c1 caddc co clt wbr wa wn cseq1 c1st c2 cmul c3 cdiv cop cif
        iffalse ruclem15 syl5eq fveq2d cn wcel peano2nn ax-mp cv c2nd ruclem9
        ruclem5 ruclem10 oprex op1st 3eqtr3g ) DHPZDQRSZGPZTUAVJDIPZTUAUBZUCZVI
        FEUDSPZUEPUFVHUGSVKRSZUHUISZVHUFVKUGSZRSUHUISZUJZUEPVIHPVPVMVNVSUEVMVLU
        FVJUGSVKRSUHUISVJVQRSUHUISUJZVSUKVSVNVLVTVSULABCDEFGHIJKLMNOUMUNUOVIEFH
        DUPUQVIUPUQODURUSABCAUTZUEPZBUTZTUAWCWAVAPZTUAUBUFWCUGSWDRSUHUISWCUFWDU
        GSZRSUHUISUJUFWBUGSWDRSUHUISWBWERSUHUISUJUKFLVBEGJKVCMVDVPVRVOUHUIVEVFV
        G $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  The value of our constructed function ` H ` when
         the value of the input function ` F ` does not lie between the
         previous values of ` G ` and ` H ` .  This assignment to ` H ` just
         shrinks the interval between ` G ` and ` H ` by some arbitrary
         amount. $)
      ruclem21 $p |- (
    -. ( ( G ` A ) < ( F ` ( A + 1 ) ) /\ ( F ` ( A + 1 ) ) < ( H ` A ) ) ->
   ( H ` ( A + 1 ) ) = ( ( ( G ` A ) + ( 2 x. ( H ` A ) ) ) / 3 ) ) $=
        ( cfv c1 caddc co clt wbr wa wn cseq1 c2nd c2 cmul c3 cdiv cop cif
        iffalse ruclem15 syl5eq fveq2d cn wcel peano2nn ax-mp cv c1st ruclem9
        ruclem5 ruclem11 oprex op2nd 3eqtr3g ) DHPZDQRSZGPZTUAVJDIPZTUAUBZUCZVI
        FEUDSPZUEPUFVHUGSVKRSZUHUISZVHUFVKUGSZRSZUHUISZUJZUEPVIIPVSVMVNVTUEVMVL
        UFVJUGSVKRSUHUISVJVQRSUHUISUJZVTUKVTVNVLWAVTULABCDEFGHIJKLMNOUMUNUOVIEF
        IDUPUQVIUPUQODURUSABCAUTZVAPZBUTZTUAWDWBUEPZTUAUBUFWDUGSWERSUHUISWDUFWE
        UGSZRSUHUISUJUFWCUGSWERSUHUISWCWFRSUHUISUJUKFLVBEGJKVCNVDVPVSVOUHUIVEVR
        UHUIVEVFVG $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  Each value of our constructed function ` G ` is a
         real number. $)
      ruclem22 $p |- ( G ` A ) e. RR $=
        ( cn cr wf wcel cfv ruclem17 ffvelrn mp2an ) PQHRDPSDHTQSABCEFGHIJKLMNU
        AOPQDHUBUC $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  Each value of our constructed function ` H ` is a
         real number. $)
      ruclem23 $p |- ( H ` A ) e. RR $=
        ( cseq1 co cfv c2nd cr cv c1st clt wbr wa c2 cmul caddc c3 cdiv cop
        cif ruclem9 ruclem5 ruclem11 wcel cxp cn wf ruclem13 ffvelrn mp2an
        wceq elxp6 pm3.27bi ax-mp pm3.27i eqeltrri ) DFEPQZRZSRZDIRTDEFIOABCAUA
        ZUBRZBUAZUCUDVNVLSRZUCUDUEUFVNUGQVOUHQUIUJQVNUFVOUGQZUHQUIUJQUKUFVMUGQV
        OUHQUIUJQVMVPUHQUIUJQUKULFLUMEGJKUNNUOVJUBRZTUPZVKTUPZVJTTUQZUPZVRVSUEZ
        URVTVIUSDURUPWAABCEFGJKLUTOURVTDVIVAVBWAVJVQVKUKVCWBVJTTVDVEVFVGVH $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  A helper lemma for the induction hypothesis in
         ~ ruclem25 . $)
      ruclem24 $p |- ( ( G ` A ) < ( H ` A ) ->
                       ( G ` ( A + 1 ) ) < ( H ` ( A + 1 ) ) ) $=
        ( cfv c1 caddc co clt wbr wa wi c2 cmul c3 cdiv cn cr wf wcel peano2nn
        ax-mp ffvelrn mp2an ruclem23 ruclem2 biimpi adantl ruclem18 ruclem19
        3brtr4d a1d wn ruclem20 ruclem21 breq12d ruclem22 syl6rbbr biimpd
        pm2.61i ) DHPZDQRSZGPZTUAZVNDIPZTUAZUBZVLVPTUAZVMHPZVMIPZTUAZUCVRWBVSVR
        UDVNUESVPRSUFUGSZVNUDVPUESZRSUFUGSZVTWATVQWCWETUAZVOVQWFVNVPUHUIGUJVMUH
        UKZVNUIUKJDUHUKWGODULUMUHUIVMGUNUOABCDEFGHIJKLMNOUPZUQURUSABCDEFGHIJKLM
        NOUTABCDEFGHIJKLMNOVAVBVCVRVDZVSWBWIWBUDVLUESVPRSUFUGSZVLWDRSUFUGSZTUAV
        SWIVTWJWAWKTABCDEFGHIJKLMNOVEABCDEFGHIJKLMNOVFVGVLVPABCDEFGHIJKLMNOVHWH
        UQVIVJVK $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  At any index ` A ` , the value of ` G ` is less
         than the value of ` H ` . $)
      ruclem25 $p |- ( G ` A ) < ( H ` A ) $=
        ( vw vv cn wcel cfv clt wbr cv c1 caddc co wceq fveq2 breq12d c2 1lt2
        1re 2re cr wf 1nn ffvelrn mp2an ltadd2i mpbi ruclem16 cseq1 c2nd cop
        ruclem14 fveq2i c1st wa cmul c3 cdiv cif ruclem9 ruclem5 ruclem11
        oprex op2nd 3eqtr3i 3brtr4i wi opreq1 fveq2d imbi12d elimel ruclem24
        dedth nnind ax-mp ) DRSDHTZDITZUAUBZOPUCZHTZWLITZUAUBUDHTZUDITZUAUBQUCZ
        HTZWQITZUAUBZWQUDUEUFZHTZXAITZUAUBZWKPQDWLUDUGWMWOWNWPUAWLUDHUHWLUDIUHU
        IWLWQUGWMWRWNWSUAWLWQHUHWLWQIUHUIWLXAUGWMXBWNXCUAWLXAHUHWLXAIUHUIWLDUGW
        MWIWNWJUAWLDHUHWLDIUHUIUDGTZUDUEUFZXEUJUEUFZWOWPUAUDUJUAUBXFXGUAUBUKUDU
        JXEULUMRUNGUOUDRSXEUNSJUPRUNUDGUQURUSUTABCEFGHIJKLMNVAUDFEVBUFTZVCTXFXG
        VDZVCTWPXGXHXIVCABCEFGHIJKLMNVEVFUDEFIUPABCAUCZVGTZBUCZUAUBXLXJVCTZUAUB
        VHUJXLVIUFXMUEUFVJVKUFXLUJXMVIUFZUEUFVJVKUFVDUJXKVIUFXMUEUFVJVKUFXKXNUE
        UFVJVKUFVDVLFLVMEGJKVNNVOXFXGXEUDUEVPXEUJUEVPVQVRVSWQRSZWTXDVTXOWQUDVLZ
        HTZXPITZUAUBZXPUDUEUFZHTZXTITZUAUBZVTWQUDWQXPUGZWTXSXDYCYDWRXQWSXRUAWQX
        PHUHWQXPIUHUIYDXBYAXCYBUAYDXAXTHWQXPUDUEWAZWBYDXAXTIYEWBUIWCABCXPEFGHIJ
        KLMNWQUDRUPWDWEWFWGWH $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  Our constructed function ` G ` has an
         ever-increasing set of values. $)
      ruclem26 $p |- ( G ` A ) < ( G ` ( A + 1 ) ) $=
        ( cfv c1 caddc co clt wbr wa c2 cmul c3 cdiv ruclem22 cn cr wf wcel
        peano2nn ax-mp ffvelrn mp2an 2re remulcli ruclem23 readdcli 3re 3pos
        gt0ne0ii redivcli lttri ruclem1 sylan2b ruclem18 breqtrrd wn ruclem20
        ruclem25 mpbi syl5breqr pm2.61i ) DHPZDQRSZGPZTUAZVQDIPZTUAZUBZVOVPHPZT
        UAWAVOUCVQUDSZVSRSZUEUFSZWBTVRVQWETUAVOWETUAVTVOVQWEABCDEFGHIJKLMNOUGZU
        HUIGUJVPUHUKZVQUIUKJDUHUKWGODULUMUHUIVPGUNUOZWDUEWCVSUCVQUPWHUQABCDEFGH
        IJKLMNOURZUSUTUEUTVAVBVCVDVQVSWHWIVEVFABCDEFGHIJKLMNOVGVHWAVIUCVOUDSVSR
        SUEUFSZWBVOTABCDEFGHIJKLMNOVJVOVSTUAVOWJTUAABCDEFGHIJKLMNOVKVOVSWFWIVEV
        LVMVN $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  Our constructed function ` H ` has an
         ever-decreasing set of values. $)
      ruclem27 $p |- ( H ` ( A + 1 ) ) < ( H ` A ) $=
        ( cfv c1 caddc co clt wbr wa c2 cmul c3 cdiv ruclem19 cn cr wf wcel
        peano2nn ax-mp ffvelrn mp2an ruclem23 ruclem3 biimpi adantl eqbrtrd wn
        ruclem21 ruclem25 ruclem22 mpbi syl6eqbr pm2.61i ) DHPZDQRSZGPZTUAZVJDI
        PZTUAZUBZVIIPZVLTUAVNVOVJUCVLUDSZRSUEUFSZVLTABCDEFGHIJKLMNOUGVMVQVLTUAZ
        VKVMVRVJVLUHUIGUJVIUHUKZVJUIUKJDUHUKVSODULUMUHUIVIGUNUOABCDEFGHIJKLMNOU
        PZUQURUSUTVNVAVOVHVPRSUEUFSZVLTABCDEFGHIJKLMNOVBVHVLTUAWAVLTUAABCDEFGHI
        JKLMNOVCVHVLABCDEFGHIJKLMNOVDVTUQVEVFVG $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)
    $}

    ${
      ruclem28.a $e |- A e. NN $.
      $( Lemma for ~ ruc .  A helper lemma for ~ ruclem29 . $)
      ruclem28 $p |- -. ( ( G ` ( A + 1 ) ) < ( F ` ( A + 1 ) ) /\
                      ( F ` ( A + 1 ) ) < ( H ` ( A + 1 ) ) ) $=
        ( cfv c1 caddc co clt wbr wa wn c2 cmul c3 cdiv cn cr wf wcel peano2nn
        ax-mp ffvelrn mp2an ruclem23 ruclem1 biimpi adantl ruclem18 breqtrrd
        ruclem22 ltnsymi syl intnanrd ruclem26 lttri mpan ruclem27 mpan2
        anim12i con3i pm2.61i ) DHPZDQRSZGPZTUAZVPDIPZTUAZUBZVOHPZVPTUAZVPVOIPZ
        TUAZUBZUCVTWBWDVTVPWATUAWBUCVTVPUDVPUESVRRSUFUGSZWATVSVPWFTUAZVQVSWGVPV
        RUHUIGUJVOUHUKZVPUIUKJDUHUKWHODULUMZUHUIVOGUNUOZABCDEFGHIJKLMNOUPZUQURU
        SABCDEFGHIJKLMNOUTVAVPWAWJABCVOEFGHIJKLMNWIVBZVCVDVEWEVTWBVQWDVSVNWATUA
        WBVQABCDEFGHIJKLMNOVFVNWAVPABCDEFGHIJKLMNOVBWLWJVGVHWDWCVRTUAVSABCDEFGH
        IJKLMNOVIVPWCVRWJABCVOEFGHIJKLMNWIUPWKVGVJVKVLVM $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)

      $( Lemma for ~ ruc .  At any index ` A ` , the interval between our
         constructed functions ` G ` and ` H ` does not include the
         corresponding value of input function ` F ` .  In other words, our
         constructed functions define, by ~ ruclem26 and ~ ruclem27 , an
         ever-shrinking interval that eventually squeezes out all values of
         ` F ` . $)
      ruclem29 $p |- -. ( ( G ` A ) < ( F ` A ) /\ ( F ` A ) < ( H ` A ) ) $=
        ( vw vv cn wcel cfv clt wbr wa wn cv c1 caddc co wceq fveq2 breq12d
        anbi12d notbid cr wf 1nn ffvelrn mp2an ltp1i ruclem16 breqtrri
        ruclem22 ltnsymi ax-mp intnanr cif opreq1 fveq2d elimel ruclem28 dedth
        nn1suc ) DRSDHTZDGTZUAUBZVNDITZUAUBZUCZUDZOPUEZHTZVTGTZUAUBZWBVTITZUAUB
        ZUCZUDUFHTZUFGTZUAUBZWHUFITZUAUBZUCZUDQUEZUFUGUHZHTZWNGTZUAUBZWPWNITZUA
        UBZUCZUDZVSPQDVTUFUIZWFWLXBWCWIWEWKXBWAWGWBWHUAVTUFHUJVTUFGUJZUKXBWBWHW
        DWJUAXCVTUFIUJUKULUMVTWNUIZWFWTXDWCWQWEWSXDWAWOWBWPUAVTWNHUJVTWNGUJZUKX
        DWBWPWDWRUAXEVTWNIUJUKULUMVTDUIZWFVRXFWCVOWEVQXFWAVMWBVNUAVTDHUJVTDGUJZ
        UKXFWBVNWDVPUAXGVTDIUJUKULUMWIWKWHWGUAUBWIUDWHWHUFUGUHWGUAWHRUNGUOUFRSW
        HUNSJUPRUNUFGUQURZUSABCEFGHIJKLMNUTVAWHWGXHABCUFEFGHIJKLMNUPVBVCVDVEWMR
        SZXAXIWMUFVFZUFUGUHZHTZXKGTZUAUBZXMXKITZUAUBZUCZUDWMUFWMXJUIZWTXQXRWQXN
        WSXPXRWOXLWPXMUAXRWNXKHWMXJUFUGVGZVHXRWNXKGXSVHZUKXRWPXMWRXOUAXTXRWNXKI
        XSVHUKULUMABCXJEFGHIJKLMNWMUFRUPVIVJVKVLVD $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)

      ${
        ruclem.b $e |- B e. NN $.
        $( Lemma for ~ ruc .  A helper lemma for ~ ruclem32 . $)
        ruclem30 $p |- ( ( G ` A ) < ( G ` ( A + B ) ) ->
                         ( G ` A ) < ( G ` ( A + ( B + 1 ) ) ) ) $=
          ( cfv caddc co clt wbr c1 cn wcel nnaddcl mp2an ruclem26 nncni ax1cn
          addassi fveq2i breqtri ruclem22 peano2nn ax-mp lttri mpan2 ) DIRZDEST
          ZIRZUAUBVADEUCSTZSTZIRZUAUBUSVDUAUBVAUTUCSTZIRVDUAABCUTFGHIJKLMNODUDU
          EZEUDUEZUTUDUEPQDEUFUGZUHVEVCIDEUCDPUIEQUIUJUKULUMUSVAVDABCDFGHIJKLMN
          OPUNABCUTFGHIJKLMNOVHUNABCVCFGHIJKLMNOVFVBUDUEZVCUDUEPVGVIQEUOUPDVBUF
          UGUNUQUR $.
          $( [18-Oct-2004] $) $( [15-Oct-2004] $)

        $( Lemma for ~ ruc .  A helper lemma for ~ ruclem32 . $)
        ruclem31 $p |- ( ( H ` ( A + B ) ) < ( H ` B ) ->
                         ( H ` ( ( A + 1 ) + B ) ) < ( H ` B ) ) $=
          ( c1 caddc co cfv clt wbr nncni ax1cn add23i fveq2i cn wcel nnaddcl
          mp2an ruclem27 eqbrtri peano2nn ax-mp ruclem23 lttri mpan ) DRSTZESTZ
          JUAZDESTZJUAZUBUCVCEJUAZUBUCVAVDUBUCVAVBRSTZJUAVCUBUTVEJDREDPUDUEEQUD
          UFUGABCVBFGHIJKLMNODUHUIZEUHUIZVBUHUIPQDEUJUKZULUMVAVCVDABCUTFGHIJKLM
          NOUSUHUIZVGUTUHUIVFVIPDUNUOQUSEUJUKUPABCVBFGHIJKLMNOVHUPABCEFGHIJKLMN
          OQUPUQUR $.
          $( [18-Oct-2004] $) $( [15-Oct-2004] $)

        $( Lemma for ~ ruc .  All values of function ` G ` are less than all
           values of function ` H ` . $)
        ruclem32 $p |- ( G ` A ) < ( H ` B ) $=
          ( vw vv cfv caddc co clt wbr cn wcel cv c1 wceq opreq2 fveq2d breq2d
          ruclem26 wi cif opreq1 opreq2d imbi12d 1nn elimel ruclem30 dedth
          nnind ax-mp nnaddcl mp2an ruclem25 ruclem22 ruclem23 lttri breq1d
          ax1cn nncni addcomi fveq2i ruclem27 eqbrtri opreq1d ruclem31 ) DITZDE
          UAUBZJTZUCUDZWBEJTZUCUDZVTWDUCUDVTWAITZUCUDZWFWBUCUDWCEUEUFZWGQVTDRUG
          ZUAUBZITZUCUDVTDUHUAUBZITZUCUDVTDSUGZUAUBZITZUCUDZVTDWNUHUAUBZUAUBZIT
          ZUCUDZWGRSEWIUHUIZWKWMVTUCXBWJWLIWIUHDUAUJUKULWIWNUIZWKWPVTUCXCWJWOIW
          IWNDUAUJUKULWIWRUIZWKWTVTUCXDWJWSIWIWRDUAUJUKULWIEUIZWKWFVTUCXEWJWAIW
          IEDUAUJUKULABCDFGHIJKLMNOPUMWNUEUFZWQXAUNVTDXFWNUHUOZUAUBZITZUCUDZVTD
          XGUHUAUBZUAUBZITZUCUDZUNWNUHWNXGUIZWQXJXAXNXOWPXIVTUCXOWOXHIWNXGDUAUJ
          UKULXOWTXMVTUCXOWSXLIXOWRXKDUAWNXGUHUAUPZUQUKULURABCDXGFGHIJKLMNOPWNU
          HUEUSUTZVAVBVCVDABCWAFGHIJKLMNODUEUFZWHWAUEUFPQDEVEVFZVGVTWFWBABCDFGH
          IJKLMNOPVHZABCWAFGHIJKLMNOXSVHABCWAFGHIJKLMNOXSVIZVJVFXRWEPWIEUAUBZJT
          ZWDUCUDUHEUAUBZJTZWDUCUDWNEUAUBZJTZWDUCUDZWREUAUBZJTZWDUCUDZWERSDXBYC
          YEWDUCXBYBYDJWIUHEUAUPUKVKXCYCYGWDUCXCYBYFJWIWNEUAUPUKVKXDYCYJWDUCXDY
          BYIJWIWREUAUPUKVKWIDUIZYCWBWDUCYLYBWAJWIDEUAUPUKVKYEEUHUAUBZJTWDUCYDY
          MJUHEVLEQVMVNVOABCEFGHIJKLMNOQVPVQXFYHYKUNXGEUAUBZJTZWDUCUDZXKEUAUBZJ
          TZWDUCUDZUNWNUHXOYHYPYKYSXOYGYOWDUCXOYFYNJWNXGEUAUPUKVKXOYJYRWDUCXOYI
          YQJXOWRXKEUAXPVRUKVKURABCXGEFGHIJKLMNOXQQVSVBVCVDVTWBWDXTYAABCEFGHIJK
          LMNOQVIVJVF $.
          $( [18-Oct-2004] $) $( [15-Oct-2004] $)
      $}
    $}

    $( Lemma for ~ ruc .  The set of values of our constructed function ` G `
       is a non-empty subset of ` RR ` .  This is a helper lemma for theorems
       involving supremum. $)
    ruclem33 $p |- ( ran G C_ RR /\ ran G =/= (/) /\
                     E. w e. RR A. v e. ran G v <_ w ) $=
      ( crn cr wss c0 wne cv cle wbr wral wrex cn wf ruclem17 frn ax-mp c1 cfv
      wcel wfn ffn 1nn fnfvelrn mp2an ne0i ruclem23 wceq wb fvelrnb breq1 wa
      clt ltle cif fveq2 eleq1d elimel ruclem22 dedth jctir breq1d ruclem32
      sylc syl5cbi r19.23aiv sylbi rgen breq2 ralbidv rcla4ev 3pm3.2i ) IPZQRZW
      FSTZEUAZDUAZUBUCZEWFUDZDQUEZUFQIUGZWGABCFGHIJKLMNOUHZUFQIUIUJUKIULZWFUMZW
      HIUFUNZUKUFUMWQWNWRWOUFQIUOUJZUPUFUKIUQURWFWPUSUJUKJULZQUMZWIWTUBUCZEWFUD
      ZWMABCUKFGHIJKLMNOUPUTZXBEWFWIWFUMZWJIULZWIVAZDUFUEZXBWRXEXHVBWSDUFWIIVCU
      JXGXBDUFXGXFWTUBUCZXBWJUFUMZXFWIWTUBVDXFQUMZXAVEXFWTVFUCZXIXJXFWTVGXJXKXA
      XJXKXJWJUKVHZIULZQUMWJUKWJXMVAZXFXNQWJXMIVIZVJABCXMFGHIJKLMNOWJUKUFUPVKZV
      LVMXDVNXJXLXNWTVFUCWJUKXOXFXNWTVFXPVOABCXMUKFGHIJKLMNOXQUPVPVMVQVRVSVTWAW
      LXCDWTQWJWTVAWKXBEWFWJWTWIUBWBWCWDURWE $.
      $( [17-Jun-2007] $) $( [15-Oct-2004] $)

    ${
      $d w S $.
      ruclem.5 $e |- S = sup ( ran G , RR , < ) $.
      $( Lemma for ~ ruc .  The supremum of the set of values of our
         constructed function ` G ` is a real number. $)
      ruclem34 $p |- S e. RR $=
        ( vw vv crn cr clt csup ruclem33 suprclii eqeltri ) FHRZSTUASOPQUEABCPQ
        DEGHIJKLMNUBUCUD $.
        $( [18-Oct-2004] $) $( [15-Oct-2004] $)

      ${
        ruclem.a $e |- A e. NN $.
        $( Lemma for ~ ruc .  The supremum we have constructed lies between all
           values of the ` G ` and ` H ` functions.  Compare ~ ruclem29 , which
           states the opposite for the input function ` F ` . $)
        ruclem35 $p |- ( ( G ` A ) < S /\ S < ( H ` A ) ) $=
          ( vw vv vu cfv clt wbr c1 caddc co cle ruclem26 crn cr csup wcel cn
          wfn wf ruclem17 ffn ax-mp peano2nn fnfvelrn mp2an ruclem33 suprubii
          breqtrri ruclem22 ruclem34 ltletri wn cv wral ruclem23 wceq wrex wb
          fvelrnb breq2 notbid wa ltnsym cif fveq2 eleq1d 1nn elimel dedth
          jctir breq1d ruclem32 sylc syl5cbi r19.23aiv sylbi rgen suprnubii
          breq2i mtbir lenlti mpbir ruclem27 lelttri pm3.2i ) DIUAZGUBUCZGDJUAZ
          UBUCZXBDUDUEUFZIUAZUBUCXGGUGUCXCABCDEFHIJKLMNOQUHXGIUIZUJUBUKZGUGXGXH
          ULZXGXIUGUCIUMUNZXFUMULZXJUMUJIUOXKABCEFHIJKLMNOUPUMUJIUQURZDUMULXLQD
          USURZUMXFIUTVARSXHXGABCRSEFHIJKLMNOVBZVCURPVDXBXGGABCDEFHIJKLMNOQVEAB
          CXFEFHIJKLMNOXNVEABCEFGHIJKLMNOPVFZVGVAGXFJUAZUGUCZXQXDUBUCXEXRXQGUBU
          CZVHXSXQXIUBUCZXQUJULZXQTVIZUBUCZVHZTXHVJXTVHABCXFEFHIJKLMNOXNVKZYDTX
          HYBXHULZRVIZIUAZYBVLZRUMVMZYDXKYFYJVNXMRUMYBIVOURYIYDRUMYIXQYHUBUCZVH
          ZYDYGUMULZYIYKYCYHYBXQUBVPVQYHUJULZYAVRYHXQUBUCZYLYMYHXQVSYMYNYAYMYNY
          MYGUDVTZIUAZUJULYGUDYGYPVLZYHYQUJYGYPIWAZWBABCYPEFHIJKLMNOYGUDUMWCWDZ
          VEWEYEWFYMYOYQXQUBUCYGUDYRYHYQXQUBYSWGABCYPXFEFHIJKLMNOYTXNWHWEWIWJWK
          WLWMRSTXHXQXOWNVAGXIXQUBPWOWPGXQXPYEWQWRABCDEFHIJKLMNOQWSGXQXDXPYEABC
          DEFHIJKLMNOQVKWTVAXA $.
          $( [18-Oct-2004] $) $( [17-Oct-2004] $)

        $( Lemma for ~ ruc .  No value of ` F ` is equal to the supremum we
           have constructed. $)
        ruclem36 $p |- -. ( F ` A ) = S $=
          ( cfv wceq clt wbr wa ruclem29 ruclem35 breq2 breq1 anbi12d mpbiri
          mto ) DHRZGSZDIRZUJTUAZUJDJRZTUAZUBZABCDEFHIJKLMNOQUCUKUPULGTUAZGUNTU
          AZUBABCDEFGHIJKLMNOPQUDUKUMUQUOURUJGULTUEUJGUNTUFUGUHUI $.
          $( [18-Oct-2004] $) $( [15-Oct-2004] $)
      $}

      $( Lemma for ~ ruc .  If ` F ` is any function that maps ` NN ` into
         ` RR ` , then ` F ` cannot be onto ` RR ` . $)
      ruclem37 $p |- -. F : NN -onto-> RR $=
        ( vw cn cr wfo crn wcel cv cfv wceq wrex wn c1 cif fveq2 eqeq1d notbid
        1nn elimel ruclem36 dedth nrex wfn wb wf ffn ax-mp fvelrnb mtbir forn
        ruclem34 syl5eleqr mto ) QRGSZFGTZUAZVJPUBZGUCZFUDZPQUEZVMPQVKQUAZVMUFV
        OVKUGUHZGUCZFUDZUFVKUGVKVPUDZVMVRVSVLVQFVKVPGUIUJUKABCVPDEFGHIJKLMNOVKU
        GQULUMUNUOUPGQUQZVJVNURQRGUSVTJQRGUTVAPQFGVBVAVCVHRVIFQRGVDABCDEFGHIJKL
        MNOVEVFVG $.
        $( [18-Oct-2004] $) $( [13-Oct-2004] $)
    $}
  $}

  ${
    $d F z $.  $d x y z $.
    ruclem38.0 $e |- F : NN --> RR $.
    $( Lemma for ~ ruc .  If ` F ` is any function that maps ` NN ` into
       ` RR ` , then ` F ` cannot be onto ` RR ` .  Using ~ eqid , this lemma
       eliminates those hypotheses of ~ ruclem37 that are no longer needed. $)
    ruclem38 $p |- -. F : NN -onto-> RR $=
      ( vx vy vz c1 cfv caddc co c2 cop csn cn cdif cres cun cv cr cxp wcel wa
      c1st clt wbr c2nd cmul c3 cdiv cif wceq copab2 cseq1 ccom crn csup eqid
      ruclem37 ) CDEFFAGZFHIURJHIKKLAMFLNOPZCQZRRSTDQZRTUAEQUTUBGZVAUCUDVAUTUEG
      ZUCUDUAJVAUFIVCHIUGUHIVAJVCUFIZHIUGUHIKJVBUFIVCHIUGUHIVBVDHIUGUHIKUIUJUAC
      DEUKZUBVEUSULIZUMZUNRUCUOZAVGUEVFUMZBUSUPVEUPVGUPVIUPVHUPUQ $.
      $( [18-Oct-2004] $) $( [13-Oct-2004] $)
  $}

  $( Lemma for ~ ruc .  There is no function that maps ` NN ` onto ` RR ` .
     (Use ~ nex if you want this in the form
     ` -. E. f f : NN -onto-> RR ` .) $)
  ruclem39 $p |- -. F : NN -onto-> RR $=
    ( cn cr wfo wn wi wf fof cc0 csn cxp cif wceq foeq1 notbid wss 0re
    elisseti fconst wcel snssi ax-mp fss mp2an elimf ruclem38 dedth syl pm2.01
    ) BCADZUJEZFUKUJBCAGZUKBCAHULUKBCULABIJZKZLZDZEAUNAUOMUJUPBCAUONOUOBCAUNBUM
    UNGUMCPZBCUNGBIICQRSICTUQQICUAUBBUMCUNUCUDUEUFUGUHUJUIUB $.
    $( [18-Oct-2004] $) $( [14-Oct-2004] $)

  $( The set of natural numbers is strictly dominated by the set of real
     numbers, i.e. the real numbers are uncountable.  The proof consists of
     lemmas ~ ruclem1 through ~ ruclem39 and this final piece.  Our proof is
     based on the proof of Theorem 5.18 of [Truss] p. 114.  See ~ ruclem39 for
     the function existence version of this theorem.  For an informal
     discussion of this proof, see
     ~ http://us.metamath.org/mpegif/mmcomplex.html#uncountable . $)
  ruc $p |- NN ~< RR $=
    ( vf cn cr csdm wbr cdom cen wn brsdom cvv wcel wss reex nnssre ssdom2g
    mp2 cv wf1o wex wfo ruclem39 f1ofo mto nex bren mtbir mpbir2an ) BCDEBCFEZB
    CGEZHBCICJKBCLUHMNBCJOPUIBCAQZRZASUKAUKBCUJTUJUABCUJUBUCUDBCAMUEUFUG $.
    $( [18-Oct-2004] $) $( [13-Oct-2004] $)

  $( The set of rationals is strictly less equinumerous than the set of reals
     ( ` RR ` strictly dominates ` QQ ` ). $)
  resdomq $p |- QQ ~< RR $=
    ( cq cn cen wbr cr csdm qnnen ruc ensdomtr mp2an ) ABCDBEFDAEFDGHABEIJ $.
    $( [20-Dec-2004] $) $( [18-Dec-2004] $)

  $( There are at least aleph-one real numbers. $)
  aleph1re $p |- ( aleph ` 1o ) ~<_ RR $=
    ( c1o cale cfv cr cdom wbr csdm wn c0 csuc cn cen com aleph0 omex nnenom
    ensymi eqbrtri ruc ensdomtr mp2an wi wa alephnbtwn2 imnan mpbir ax-mp
    df-1o fveq2i breq2i mtbir cvv wcel wb fvex reex domtri ) ABCZDEFZDURGFZHZUT
    DIJZBCZGFZIBCZDGFZVDHZVEKLFKDGFVFVEMKLNKMOPQRSVEKDTUAVFVGUBVFVDUCHIDUDVFVDU
    EUFUGURVCDGAVBBUHUIUJUKURULUMDULUMUSVAUNABUOUPURDULULUQUAUF $.
    $( [3-Feb-2005] $) $( [2-Feb-2005] $)

