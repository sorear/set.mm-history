$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Infinite primes theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d m n z w v u t A $.  $d m n v u t G $.
    unbenlem.1 $e |- G =
                     ( rec ( { <. z , w >. | w = ( z + 1 ) } , 1 ) |` om ) $.
    $( Lemma for ~ unben . $)
    unbenlem $p |- ( ( A C_ NN /\ A. m e. NN E. n e. A m < n ) -> A ~~ om ) $=
      ( vt vv vu cn wss cv clt wbr wrex wral wa ccnv cima cen com cvv wcel
      cres wf1o nnex ssex wf1 c1 cle cz crab 1z om2uzf1oi wceq wb nnzrab
      f1oeq3 ax-mp mpbir f1ocnv f1of1 f1ores mpan f1oeng syl11anc adantr
      unbnn3 crn imassrn cdm dfdm4 wf f1of fdmi eqtr3i sseqtri wi cfv om2uzuzi
      syl6eleqr breq1 rexbidv rcla4v syl fvres eqeq1d biimpa adantll om2uzlt2i
      sseli sylan2 breq2 sylan9bb syldan biimparc exp44 imp31 reximdva wfun
      f1ofun funcnvres2 f1oeq1 sylib wfo f1ofo forn eleq2d wfn f1ofn fvelrnb
      bitr3d syl5 exp4b com4l imp r19.23adv syld ex com3l r19.21aiv sylancr
      entr ) CKLZDMZEMZNOZECPZDKQZRZCFSZCTZUAOZYMUBUAOZCUBUAOYEYNYJYECUCUDCYMYL
      CUEZUFZYNCKUGUHKUBYLUIZYEYQKUBYLUFZYRUBKFUFZYSYTUBUJHMUKOHULUMZFUFZABHUJF
      UNGUOKUUAUPYTUUBUQHURZKUUAUBFUSUTVAZUBKFVBUTKUBYLVCUTKUBCYLVDVEZCYMUCYPVF
      VGVHYMUBLIMZJMZUDZJYMPZIUBQYOYKIJYMVIYMYLVJZUBYLCVKFVLUUJUBFVMUBKFYTUBKFV
      NUUDUBKFVOUTVPVQVRZYKUUIIUBYEYJUUFUBUDZUUIVSUULYEYJUUIUULYEYJUUIVSUULYERZ
      YJUUFFVTZYGNOZECPZUUIUULYJUUPVSZYEUULUUNKUDUUQUULUUNUUAKABHUUFUJFUNGWAUUC
      WBYIUUPDUUNKYFUUNUPYHUUOECYFUUNYGNWCWDWEWFVHUUMUUOUUIECUULYEYGCUDZUUOUUIV
      SVSUUOUULYEUURUUIUUOUULYEUURUUIUUOUULRZUUGFYMUEZVTZYGUPZJYMPZUUIYEUURRUUS
      UVBUUHJYMUUOUULUUGYMUDZUVBUUHVSUUOUULUVDUVBUUHUULUVDRZUVBRUUHUUOUVEUVBUUG
      FVTZYGUPZUUHUUOUQUVDUVBUVGUULUVDUVBUVGUVDUVAUVFYGUUGYMFWGWHWIWJUVEUUHUUNU
      VFNOZUVGUUOUULUUGUBUDUUHUVHUQUVDABUUFUUGUJFUNGWKYMUBUUGUUKWLWMUVFYGUUNNWN
      WOWPWQWRWSWTYEUURUVCYEYMCUUTUFZUURUVCUQYEYMCYPSZUFZUVIYEYQUVKUUECYMYPVBWF
      UVJUUTUPZUVKUVIUQFXAZUVLYTUVMUUDUBKFXBUTCFXCUTYMCUVJUUTXDUTXEUVIYGUUTVJZU
      DZUURUVCUVIUVNCYGUVIYMCUUTXFUVNCUPYMCUUTXGYMCUUTXHWFXIUVIUUTYMXJUVOUVCUQY
      MCUUTXKJYMYGUUTXLWFXMWFWIXNXOXPXQXRXSXTYAXQYBYCCYMUBYDVG $.
      $( [5-May-2005] $)
  $}

  ${
    $d m n x y A $.
    $( An unbounded set of natural numbers is infinite. $)
    unben $p |- ( ( A C_ NN /\ A. m e. NN E. n e. A m < n ) -> A ~~ NN ) $=
      ( vx vy com cen wbr cn wss cv clt wrex wral wa entr c1 caddc co wceq
      copab crdg cres eqid unbenlem omex nnenom ensymi sylancl ) AFGHFIGHAIGHAI
      JBKCKLHCAMBINOAFIPDEABCEKDKQRSTDEUAQUBFUCZUJUDUEIFUFUGUHUI $.
      $( [5-May-2005] $)
  $}

  ${
    $d j k N $.  $d j M $.  $d j k K $.
    infpnlem.1 $e |- K = ( ( ! ` N ) + 1 ) $.
    $( Lemma for ~ infpn .  The smallest divisor (greater than 1) ` M ` of
       ` N ! + 1 ` is a prime greater than ` N ` . $)
    infpnlem1 $p |- ( ( N e. NN /\ M e. NN ) ->
                        ( ( ( 1 < M /\ ( K / M ) e. NN ) /\
                  A. j e. NN ( ( 1 < j /\ ( K / j ) e. NN ) -> M <_ j ) ) ->
       ( N < M /\ A. j e. NN ( ( M / j ) e. NN -> ( j = 1 \/ j = M ) ) ) ) ) $=
      ( cn wcel wa c1 clt wbr cdiv co cv cle wi wral wceq wo wn wb cr lenlt
      nnre syl2an ancoms adantr cn0 cfa cfv caddc cz facndiv nnz opreq1i
      syl5eqelr nsyl nnnn0 sylanl1 expr sylbird con4d expimpd adantrd w3a
      letri3 biimprd exp4b com3l imp32 adantll imim2d com23 cc nndivtr ex
      3com13 3expa faccl syl peano2nn syl5eqel nncn adantrl sylan2d exp4d
      com24 exp32 imp31 com14 3imp ralimdvaa adantld imp3a prime adantl
      sylibrd jcad ) DFGZCFGZHZICJKZBCLMZFGZHZIANZJKZBXFLMFGZHZCXFOKZPZAFQZHZDC
      JKZCXFLMFGZXFIRXFCRZSPAFQZXAXEXNXLXAXBXDXNXAXBHZXNXDXRXNTZCDOKZXDTZXAXTXS
      UAZXBWTWSYBCUBGZDUBGYBWTWSCDUCCUDZDUDUEUFUGXAXBXTYADUHGZWTXBXTHZYAWSYEWTH
      YFHDUIUJZIUKMZCLMZULGXDDCUMXDXCULYIXCUNBYHCLEUOUPUQDURZUSUTVAVBVCVDXAXMXG
      XFCOKZXOVEZXPPZAFQZXQXAXEXLYNXAXDXLYNPZXBXAXDYOXAXDHZXKYMAFYLYPXFFGZHZXKX
      PXGYKXOYRXKXPPZPYRYKXOXGYSXAXDYQYKXOXGYSPPZPXAYKYQXDYTXAYKYQXDYTPXAYKYQHZ
      HZXGXOXDYSUUBXGXOXDYSUUBXGXHYSXOXDHZUUBXKXIXPUUBXJXPXIWTUUAXJXPPZWSWTYKYQ
      UUDYQWTYKUUDYQWTYKXJXPYQWTHXPYKXJHZXFUBGYCXPUUEUAYQWTXFCVFXFUDYDUEVGVHVIV
      JVKVLVMXAYQUUCXHPZYKBVNGZWTYQUUFWSUUGWTYQUUFYQWTUUGUUFYQWTUUGVEUUCXHXFCBV
      OVPVQVRWSBFGUUGWSYHFBWSYGFGZYHFGWSYEUUHYJDVSVTYGWAVTEWBBWCVTUSWDWEWFWGWHW
      GWIWJWKVIWLVPWMWNWTXQYNUAWSACWOWPWQWR $.
      $( [5-May-2005] $)

    $( Lemma for ~ infpn .  For any natural number ` N ` , there exists a prime
       number ` j ` greater than ` N ` . $)
    infpnlem2 $p |- ( N e. NN -> E. j e. NN ( N < j /\
                    A. k e. NN ( ( j / k ) e. NN -> ( k = 1 \/ k = j ) ) ) ) $=
      ( cn wcel c1 cv clt wbr cdiv co wa cle wi wral wrex wceq wo cfa cfv
      caddc cn0 nnnn0 faccl syl peano2nn syl5eqel nnge1 wb nnleltp1 1nn
      sylancr mpbid syl6breqr cc cc0 wne nncn nnne0 jca divid syl6eqel breq2
      opreq2 eleq1d anbi12d rcla4ev syl12anc nnwos infpnlem1 reximdva mpd ) DFG
      ZHAIZJKZCVPLMZFGZNZHBIZJKZCWALMZFGZNZVPWAOKPBFQNZAFRZDVPJKVPWALMFGWAHSWAV
      PSTPBFQNZAFRVOVTAFRZWGVOCFGZHCJKZCCLMZFGZWIVODUAUBZHUCMZFCVOWNFGZWOFGVODU
      DGWPDUEDUFUGZWNUHUGEUIZVOHWOCJVOHWNOKZHWOJKZVOWPWSWQWNUJUGHFGWPWSWTUKVOHW
      NULUMWQUNUOEUPVOWLHFVOCUQGZCURUSZNZWLHSVOWJXCWRWJXAXBCUTCVAVBUGCVCUGUMVDV
      TWKWMNACFVPCSZVQWKVSWMVPCHJVEXDVRWLFVPCCLVFVGVHVIVJVTWEABVPWASZVQWBVSWDVP
      WAHJVEXEVRWCFVPWACLVFVGVHVKUGVOWFWHAFBCVPDEVLVMVN $.
      $( [5-May-2005] $)
  $}

  ${
    $d j k N $.
    $( There exist infinitely many prime numbers: for any natural number
       ` N ` , there exists a prime number ` j ` greater than ` N ` .  (See
       ~ infpn2 for the equinumerosity version.) $)
    infpn $p |- ( N e. NN -> E. j e. NN ( N < j /\
                    A. k e. NN ( ( j / k ) e. NN -> ( k = 1 \/ k = j ) ) ) ) $=
      ( cfa cfv c1 caddc co eqid infpnlem2 ) ABCDEFGHZCKIJ $.
      $( [1-Jun-2006] $)
  $}

  ${
    $d j k n m A $.  $d j k S $.
    infpn2.1 $e |- S = { n e. NN | ( 1 < n /\
                A. m e. NN ( ( n / m ) e. NN -> ( m = 1 \/ m = n ) ) ) } $.
    $( There exist infinitely many prime numbers: the set of all primes ` S `
       is unbounded by ~ infpn , so by ~ unben it is infinite. $)
    infpn2 $p |- S ~~ NN $=
      ( vj vk cn wss cv clt wbr wrex wral cen c1 cdiv co wcel wceq wo wi wa
      crab ssrab2 eqsstri infpn cle nnge1 adantr cr 1re lelttr mp3an1 nnre
      syl2an mpand ancld anim1d anass syl6ib reximdva mpd breq2 opreq1 eleq1d
      eqeq2 orbi2d imbi12d ralbidv anbi12d elrab2 anbi1i ancom anbi2i 3bitri
      rexbii2 sylibr rgen unben mp2an ) AGHEIZFIZJKZFALZEGMAGNKAOCIZJKZWEBIZPQZ
      GRZWGOSZWGWESZTZUAZBGMZUBZCGUCGDWOCGUDUEWDEGWAGRZWCOWBJKZWBWGPQZGRZWJWGWB
      SZTZUAZBGMZUBZUBZFGLZWDWPWCXCUBZFGLXFFBWAUFWPXGXEFGWPWBGRZUBZXGWCWQUBZXCU
      BXEXIWCXJXCXIWCWQXIOWAUGKZWCWQWPXKXHWAUHUIWAUJRZWBUJRZXKWCUBWQUAZWPXHOUJR
      XLXMXNUKOWAWBULUMWAUNWBUNUOUPUQURWCWQXCUSUTVAVBWCXEFAGWBARZWCUBXHXDUBZWCU
      BXHXDWCUBZUBXHXEUBXOXPWCWOXDCWBGAWEWBSZWFWQWNXCWEWBOJVCXRWMXBBGXRWIWSWLXA
      XRWHWRGWEWBWGPVDVEXRWKWTWJWEWBWGVFVGVHVIVJDVKVLXHXDWCUSXQXEXHXDWCVMVNVOVP
      VQVRAEFVSVT $.
      $( [5-May-2005] $)
  $}

