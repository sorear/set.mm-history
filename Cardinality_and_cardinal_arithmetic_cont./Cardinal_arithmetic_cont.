$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
       Cardinal arithmetic (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    infxpidmlem1.1 $e |- A e. V $.
    infxpidmlem1.2 $e |- B e. V $.
    $( Lemma for ~ infxpidm .  An infinite idempotent set ` x ` is equinumerous
       to the union of any two sets ` A ` and ` B ` equinumerous to it. $)
    infxpidmlem1 $p |- ( ( om ~<_ x /\ x ~~ ( x X. x ) ) ->
                     ( ( x ~~ A /\ x ~~ B ) -> x ~~ ( A u. B ) ) ) $=
      ( c1o cv csdm wbr cxp cen wa cun wi com cdom sbth cvv wcel wss ssun1
      ssdomg mp2 endomtr mpan2 ad2antrl domentr unxpdom sdomentr ax-mp syl2an
      anandis adantlr entrt xpex ensym syl visset xpen sylan2 adantll sylanc
      ex 1onn infsdomnn sylan ) FAGZHIZVGVGVGJZKIZVGBKIZVGCKIZLZVGBCMZKIZNOVGPI
      ZVHVJLZVMVOVGVNPIZVNVGPIZVOVQVMLZVGVNQVKVRVQVLVKBVNPIZVRBRSZBVNTWADBCUABV
      NRUBUCVGBVNUDUEUFVNBCJZPIZWCVGKIZVSVTVNWCVGUGVHVMWDVJVHVKVLWDFBHIZFCHIZWD
      VHVKLZVHVLLZBCUHWBWHWFNDFVGBRUIUJCRSWIWGNEFVGCRUIUJUKULUMVJVMWEVHVJVIWCKI
      ZWEVMVJWJLVGWCKIWEVGVIWCUNVGWCBCDEUOUPUQVGBVGCAURZDWKEUSUTVAVBVBVCVPFOSVH
      VDVGFWKVEUEVF $.
      $( [4-Oct-2004] $) $( [15-Sep-2004] $)
  $}

  ${
    $d x y z w f g h v u t A $.  $d x y z w v f g h t B $.
    $d x y z w v f g h t C $.  $d x y z w v u g h H $.
    infxpidmlem.1 $e |- H = { f | ( f = (/) \/ E. t ( ( om ~<_ t /\ t (_ A ) /\
                      f : ( t X. t ) -1-1-onto-> t ) ) } $.
    ${
      infxpidmlem2.2 $e |- B e. V $.
      $( Lemma for ~ infxpidm .  A necessary and sufficient condition for a
         set ` B ` to belong to ` H ` . $)
      infxpidmlem2 $p |- ( B e. H <-> ( B = (/) \/
        E. x ( ( om ~<_ x /\ x (_ A ) /\ B : ( x X. x ) -1-1-onto-> x ) ) ) $=
        ( wcel c0 wceq com cv cdom wbr wss wa cxp wf1o wex wo eqeq1 f1oeq1
        anbi2d exbidv orbi12d elab2 breq2 sseq1 anbi12d wb xpeq1 xpeq2 eqtrd
        f1oeq2 syl f1oeq3 bitrd cbvexv orbi2i bitr4 ) DFIDJKZLBMZNOZVCCPZQZVCVC
        RZVCDSZQZBTZUAZVBLAMZNOZVLCPZQZVLVLRZVLDSZQZATZUAEMZJKZVFVGVCVTSZQZBTZU
        AVKEDFHVTDKZWAVBWDVJVTDJUBWEWCVIBWEWBVHVFVGVCVTDUCUDUEUFGUGVSVJVBVRVIAB
        VLVCKZVOVFVQVHWFVMVDVNVEVLVCLNUHVLVCCUIUJWFVQVGVLDSZVHWFVPVGKVQWGUKWFVP
        VCVLRVGVLVCVLULVLVCVCUMUNVPVGVLDUOUPVLVCVGDUQURUJUSUTVA $.
        $( [4-Oct-2004] $) $( [16-Sep-2004] $)

      ${
        $d x D $.
        infxpidmlem3.3 $e |- D e. V $.
        $( Lemma for ~ infxpidm .  A sufficient condition for a set ` B ` to
           belong to ` H ` . $)
        infxpidmlem3 $p |- ( ( ( om ~<_ D /\ D (_ A ) /\
                         B : ( D X. D ) -1-1-onto-> D ) -> B e. H ) $=
          ( vx com cdom wbr wss wa cxp wf1o cv wex wcel wceq breq2 sseq1
          anbi12d wb xpeq1 xpeq2 eqtrd f1oeq2 syl f1oeq3 bitrd cla4ev c0 wo
          olc infxpidmlem2 sylibr ) KDLMZDBNZOZDDPZDCQZOZKJRZLMZVEBNZOZVEVEPZVE
          CQZOZJSZCFTZVKVDJDIVEDUAZVHVAVJVCVNVFUSVGUTVEDKLUBVEDBUCUDVNVJVBVECQZ
          VCVNVIVBUAVJVOUEVNVIDVEPVBVEDVEUFVEDDUGUHVIVBVECUIUJVEDVBCUKULUDUMVLC
          UNUAZVLUOVMVLVPUPJABCEFGHUQURUJ $.
          $( [4-Oct-2004] $) $( [16-Sep-2004] $)
      $}
    $}

    $( Lemma for ~ infxpidm .  The domain of a member of ` H` is the cross
       product of its range. $)
    infxpidmlem4 $p |- ( g e. H -> dom g = ( ran g X. ran g ) ) $=
      ( vx cv wcel c0 wceq com cdom wbr wss wa cxp wf1o wex wo cdm crn visset
      infxpidmlem2 dm0 dmeq rneq rn0 syl6eq xpeq2 syl xp0 3eqtr4a wfn ccnv
      wfun w3a f1o2 fndm xpeq1 eqtr2d sylan9eq 3adant2 sylbi adantl 19.23aiv
      jaoi ) DHZEIVHJKZLGHZMNVJBOPZVJVJQZVJVHRZPZGSZTVHUAZVHUBZVQQZKZGABVHCEFDU
      CUDVIVSVOVIJUAJVPVRUEVHJUFVIVRVQJQZJVIVQJKVRVTKVIVQJUBJVHJUGUHUIVQJVQUJUK
      VQULUIUMVNVSGVMVSVKVMVHVLUNZVHUOUPZVQVJKZUQVSVLVJVHURWAWCVSWBWAWCVPVLVRVL
      VHUSWCVRVJVQQVLVQVJVQUTVQVJVJUJVAVBVCVDVEVFVGVD $.
      $( [4-Oct-2004] $) $( [11-Sep-2004] $)

    $( Lemma for ~ infxpidm .  Two members in the range of a member of a subset
       of ` H ` form an ordered pair belonging to the domain of the union
       of the subset. $)
    infxpidmlem5 $p |- ( ( C (_ H /\ g e. C ) ->
             ( ( y e. ran g /\ z e. ran g ) -> <. y , z >. e. dom U. C ) ) $=
      ( wss cv wcel wa crn cop cdm cuni cxp wceq ssel2 infxpidmlem4 syl eleq2d
      visset opelxp syl6bb wi ciun ssiun2 dmuni syl6ssr sseld adantl sylbird )
      EHJZGKZELZMZAKZUPNZLBKZUTLMZUSVAOZUPPZLZVCEQPZLZURVEVCUTUTRZLVBURVDVHVCUR
      UPHLVDVHSEHUPTCDFGHIUAUBUCUSVAUTUTBUDUEUFUQVEVGUGUOUQVDVFVCUQVDGEVDUHVFGE
      VDUIGEUJUKULUMUN $.
      $( [4-Oct-2004] $) $( [11-Sep-2004] $)

    ${
      infxpidmlem6.2 $e |- B = ran U. C $.
      $( Lemma for ~ infxpidm .  A simple but frequently used fact. $)
      infxpidmlem6 $p |- ( y e. B <-> E. g e. C y e. ran g ) $=
        ( cv wcel crn ciun wrex cuni rnuni eqtr eleq2i eliun bitr ) AKZDLUBGEGK
        MZNZLUBUCLGEODUDUBDEPMUDJGEQRSGUBEUCTUA $.
        $( [4-Oct-2004] $) $( [12-Sep-2004] $)

      $( Lemma for ~ infxpidm .  The union of a collection ` C ` of chains in
         ` H ` is a bijection. $)
      infxpidmlem7 $p |- ( ( C (_ H /\
                         A. g e. C A. h e. C ( g (_ h \/ h (_ g ) ) ->
                         U. C : ( B X. B ) -1-1-onto-> B ) $=
        ( vx vy vz vw vv wss cv wo wral wa cuni cxp wfn ccnv wfun crn wceq
        wf1o cdm r19.26 fun11uni sylbir wcel ssel c0 com cdom wbr wex visset
        infxpidmlem2 wf1 f10 f1eq1 mpbiri wf df-f1 ffun anim1i sylbi syl f1of1
        adantl 19.23aiv jaoi syl6 r19.21aiv sylan wrex ssel2 infxpidmlem4 ra4e
        infxpidmlem6 sylibr ex ssrdv ssxp anidms eqsstrd sseld r19.23adva ciun
        dmuni eleq2i eliun bitr syl5ib adantr wrel relxp a1i cop wi sseq1
        sseq2 orbi12d rcla42v rnss anim1d infxpidmlem5 syl9r adantrl anim2d
        adantrr jaod com3l syld com13 imp r19.23advv anbi12i opelxp reeanv
        3bitr4 relssdv eqssd jca an23 df-fn anbi1i bitr4 sylib eqcomi jctir
        w3a f1o2 df-3an ) DHPZFQZGQZPZYTYSPZRZGDSZFDSZTZDUAZCCUBZUCZUUGUDUEZTZU
        UGUFZCUGZTZUUHCUUGUHZUUFUUKUUMUUFUUGUEZUUJTZUUGUIZUUHUGZTZUUKUUFUUQUUSY
        SUEZYSUDUEZTZFDSZUUEUUQYRUVDUUETUVCUUDTFDSUUQUVCUUDFDUJDFGUKULYRUVCFDYR
        YSDUMZYSHUMZUVCDHYSUNUVFYSUOUGZUPKQZUQURUVHBPTZUVHUVHUBZUVHYSUHZTZKUSZR
        UVCKABYSEHIFUTVAUVGUVCUVMUVGUOUOYSVBZUVCUVGUVNUOUOUOVBUOVCUOUOYSUOVDVEU
        VNUOUOYSVFZUVBTUVCUOUOYSVGUVOUVAUVBUOUOYSVHVIVJVKUVLUVCKUVKUVCUVIUVKUVJ
        UVHYSVBZUVCUVJUVHYSVLUVPUVJUVHYSVFZUVBTUVCUVJUVHYSVGUVQUVAUVBUVJUVHYSVH
        VIVJVKVMVNVOVJVPVQVRUUFUURUUHYRUURUUHPUUEYRLUURUUHYRLQZYSUIZUMZFDVSZUVR
        UUHUMZUVRUURUMZYRUVTUWBFDYRUVETZUVSUUHUVRUWDUVSYSUFZUWEUBZUUHUWDUVFUVSU
        WFUGDHYSVTABEFHIWAVKUVEUWFUUHPZYRUVEUWECPZUWGUVELUWECUVEUVRUWEUMZUVRCUM
        ZUVEUWITUWIFDVSUWJUWIFDWBLABCDEFHIJWCWDWEWFUWHUWGUWECUWECWGWHVKVMWIWJWK
        UWCUVRFDUVSWLZUMUWAUURUWKUVRFDWMWNFUVRDUVSWOWPWQWFWRUUFLMUUHUURUUHWSUUF
        CCWTXAUUFUVRNQZUFZUMZMQZOQZUFZUMZTZODVSNDVSZUVRUWOXBZUURUMZUXAUUHUMZUUF
        UWSUXBNODDYRUUEUWLDUMZUWPDUMZTZUWSUXBXCZXCUXFUUEYRUXGUXFUUEUWLUWPPZUWPU
        WLPZRZYRUXGXCUUCUXJUWLYTPZYTUWLPZRFGUWLUWPDDYSUWLUGUUAUXKUUBUXLYSUWLYTX
        DYSUWLYTXEXFYTUWPUGUXKUXHUXLUXIYTUWPUWLXEYTUWPUWLXDXFXGYRUXFUXJUXGYRUXF
        UXJUXGXCYRUXFTUXHUXGUXIYRUXEUXHUXGXCUXDUXHUWSUVRUWQUMZUWRTYRUXETUXBUXHU
        WNUXMUWRUXHUWMUWQUVRUWLUWPXHWJXILMABDEOHIXJXKXLYRUXDUXIUXGXCUXEUXIUWSUW
        NUWOUWMUMZTYRUXDTUXBUXIUWRUXNUWNUXIUWQUWMUWOUWPUWLXHWJXMLMABDENHIXJXKXN
        XOWEXPXQXRXSXTUWJUWOCUMZTUWNNDVSZUWRODVSZTUXCUWTUWJUXPUXOUXQLABCDENHIJW
        CMABCDEOHIJWCYAUVRUWOCCMUTYBUWNUWRNODDYCYDWQYEYFYGUUTUUPUUSTZUUJTUUKUUP
        UUJUUSYHUUIUXRUUJUUGUUHYIYJYKYLCUULJYMYNUUOUUIUUJUUMYOUUNUUHCUUGYPUUIUU
        JUUMYQWPWD $.
        $( [5-Oct-2004] $) $( [11-Sep-2004] $)

      ${
        infxpidmlem8.3 $e |- C e. V $.
        $( Lemma for ~ infxpidm .  The union of a collection of chains ` C ` in
           the collection of bijections ` H ` belongs to ` H ` .  This property
           will be needed to apply Zorn's Lemma in ~ infxpidmlem9 . $)
        infxpidmlem8 $p |- ( ( C (_ H /\
                   A. g e. C A. h e. C ( g (_ h \/ h (_ g ) ) -> U. C e. H ) $=
          ( vx vy wss cv wo wral wa cuni c0 wceq wcel wn com cdom wbr cxp wf1o
          wi wrex crn ssel2 wex visset infxpidmlem2 biimp ord wfo f1ofo forn
          syl eqcomd anim1i ancoms 19.22i cvv rnexg ax-mp breq2 sseq1 anbi12d
          ceqsexv sylib syl6 domtr ra4e infxpidmlem6 sylibr ex ssrdv ssdomg
          sylan2 expcom adantl adantrd syld r19.23adva csn uni0b dfss3 elsn
          ralbii 3bitr negbii rexnal bitr4 syl5ib pm3.27 rneq rn0 syl6eq 0ss
          a1i eqsstrd pm2.61d2 sseld jctird adantr infxpidmlem7 uniex eqeltr
          infxpidmlem3 orc ) DHNZFOZGOZNXPXONPGDQFDQZRZDSZTUAZXSHUBZXRXTUCZUDCU
          EUFZCBNZRZCCUGCXSUHZRYAXRYBYEYFXNYBYEUIXQXNYBYCYDXNXOTUAZUCZFDUJZYCYB
          XNYHYCFDXNXODUBZRZYHUDXOUKZUEUFZYLBNZRZYCYKXOHUBZYHYOUIDHXOULYPYHUDLO
          ZUEUFZYQBNZRZYQYQUGZYQXOUHZRZLUMZYOYPYGUUDYPYGUUDPLABXOEHIFUNZUOUPUQU
          UDYQYLUAZYTRZLUMYOUUCUUGLUUBYTUUGUUBUUFYTUUBYLYQUUBUUAYQXOURYLYQUAUUA
          YQXOUSUUAYQXOUTVAVBVCVDVEYTYOLYLXOVFUBYLVFUBZUUEXOVFVGVHZUUFYRYMYSYNY
          QYLUDUEVIYQYLBVJVKVLVMVNVAZYKYMYCYNYJYMYCUIXNYMYJYCYMYLCUEUFZYCYJUDYL
          CVOYJYLCNZUUKYJMYLCYJMOZYLUBZUUMCUBZYJUUNRUUNFDUJZUUOUUNFDVPMABCDEFHI
          JVQZVRVSVTUUHUULUUKUIUUIYLCVFWAVHVAWBWCWDWEWFWGYBYGFDQZUCYIXTUURXTDTW
          HZNXOUUSUBZFDQUURDWIFDUUSWJUUTYGFDFTWKWLWMWNYGFDWOWPWQXNMCBXNUUPUUMBU
          BZUUOXNUUNUVAFDYKYLBUUMYKYGYNYKYHYOYNUUJYMYNWRVNYGYLTBYGYLTUKTXOTWSWT
          XATBNYGBXBXCXDXEXFWGUUQWQVTXGXHABCDEFGHIJXIXGABXSCEHIDKXJZCXSUKZVFJXS
          VFUBUVCVFUBUVBXSVFVGVHXKXLVNXTXTYTUUAYQXSUHRLUMZPYAXTUVDXMLABXSEHIUVB
          UOVRXE $.
          $( [5-Oct-2004] $) $( [12-Sep-2004] $)
      $}
    $}

    ${
      infxpidmlem.2 $e |- A e. V $.
      $( Lemma for ~ infxpidm .  By Zorn's Lemma ~ zorn2 , the collection ` H `
         (which we show here to be a set) has a maximal element. $)
      infxpidmlem9 $p |- E. g e. H A. h e. H -. g (. h $=
        ( vz cv wss wo wral wa cuni wcel wi wpss wn wrex c0 wceq cab com cdom
        wbr cxp wf1o wex cun cvv unab eqtr4 csn df-sn p0ex eqeltrr cpw df-rex
        visset elpw anbi1i ancom an23 3bitr exbii bitr abbii pwex wf xpex
        mapex mp2an f1of adantl ss2abi ssexi abrexex2 unex eqeltr zorn2 crn
        eqid infxpidmlem8 mpg ) IJZFKDJZEJZKWHWGKLEWFMDWFMNWFOZFPQWGWHRSEFMDFTI
        DEIFFCJZUAUBZCUCZUDAJZUEUFZWMBKZNWMWMUGZWMWJUHZNZAUIZCUCZUJZUKFWKWSLCUC
        XAGWKWSCULUMWLWTUAUNWLUKCUAUOUPUQWNWQNZABURZTZCUCWTUKXDWSCXDWMXCPZXBNZA
        UIWSXBAXCUSXFWRAXFWOXBNXBWONWRXEWOXBWMBAUTZVAVBWOXBVCWNWQWOVDVEVFVGVHXB
        ACXCBHVIXBCUCWPWMWJVJZCUCZWPUKPWMUKPXIUKPWMWMXGXGVKXGWPWMUKUKCVLVMXBXHC
        WQXHWNWPWMWJVNVOVPVQVRUQVSVTWAABWIWBZWFCDEFGXJWCIUTWDWE $.
        $( [5-Oct-2004] $) $( [12-Sep-2004] $)

      $( Lemma for ~ infxpidm .  A maximal bijection ` g ` in ` H ` is
         non-empty. $)
      infxpidmlem10 $p |- ( A. h e. H -. g (. h -> ( om ~<_ A ->
                        g =/= (/) ) ) $=
        ( vv vy cv wpss wn wral c0 wne wrex com cdom wbr wcel wa wceq psseq1
        0pss syl6bb necon2bbid psseq2 negbid rcla4cva syl5bir com12 necon3d
        r19.23adva cen wss wex cxp wf1o visset infxpidmlem3 ex wi crn wfo
        f1ofo forn syl eqcomd rneq rn0 syl6eq sylan9eq infn0 syl5com adantr
        jcad 19.22dv imp an1rs endom omex xpen anidms xpomen cvv wb xpex enen1
        mpan mpbii enen2 mpbid bren sylib jca sylan 19.23aiv domen df-rex
        3imtr4 syl5 ) DKZEKZLZMZEFNZIKZOPZIFQZXCOPZRBSTZXGXIXKIFXGXHFUAZUBZXCOX
        HOXCOUCZXNXHOUCZXOXPXCXHLZMZXNXOXQXHOXOXQOXHLXIXCOXHUDXHUEUFUGXFXREXHFX
        DXHUCXEXQXDXHXCUHUIUJUKULUMUNRJKZUOTZXSBUPZUBZJUQXMXIUBZIUQZXLXJYBYDJRX
        SSTZXSXSURZXSXHUSZIUQZUBYAYDXTYEYAYHYDYEYAUBZYHYDYIYGYCIYIYGXMXIYIYGXMA
        BXHXSCFGIUTJUTZVAVBYEYGXIVCYAYGXSOPXIYEYGXHOXSOYGXPXSOUCYGXPXSXHVDZOYGY
        KXSYGYFXSXHVEYKXSUCYFXSXHVFYFXSXHVGVHVIXPYKOVDOXHOVJVKVLVMVBUMXSYJVNVOV
        PVQVRVSVTXTYEYHRXSWAXTYFXSUOTZYHXTYFRUOTZYLXTRRURZYFUOTZYMXTYORXSRXSWBY
        JWBYJWCWDYOYNRUOTZYMWEYFWFUAYOYPYMWGXSXSYJYJWHYNYFRWFWIWJWKVHXSWFUAXTYM
        YLWGYJRXSYFWFWLWJWMYFXSIYJWNWOWPWQWRJRBHWSXIIFWTXAXB $.
        $( [?] $) $( [16-Sep-2004] $)

      $( Lemma for ~ infxpidm .  A maximal bijection ` g ` in ` H ` is
         non-empty. $)
      infxpidmlem10OLD $p |- ( A. h e. H -. g (. h -> ( om ~<_ A ->
                        -. g = (/) ) ) $=
        ( vv vy cv wpss wn wral c0 wceq wrex com cdom wbr wcel wa psseq1 wne
        0pss df-ne bitr syl6bb con2bid psseq2 negbid rcla4cva syl5bir com12
        con3d r19.23adva cen wss wex cxp wf1o visset infxpidmlem3 ex wi crn
        wfo f1ofo forn syl eqcomd rneq rn0 syl6eq sylan9eq infn0OLD nsyli
        adantr jcad 19.22dv imp an1rs endom omex xpen anidms xpomen cvv wb
        xpex enen1 mpan mpbii enen2 mpbid bren sylib jca sylan 19.23aiv domen
        df-rex 3imtr4 syl5 ) DKZEKZLZMZEFNZIKZOPZMZIFQZXEOPZMZRBSTZXIXLXOIFXIXJ
        FUAZUBZXNXKXNXRXKXNXKXEXJLZMZXRXNXSXKXNXSOXJLZXLXEOXJUCYAXJOUDXLXJUEXJO
        UFUGUHUIXHXTEXJFXFXJPXGXSXFXJXEUJUKULUMUNUOUPRJKZUQTZYBBURZUBZJUSXQXLUB
        ZIUSZXPXMYEYGJRYBSTZYBYBUTZYBXJVAZIUSZUBYDYGYCYHYDYKYGYHYDUBZYKYGYLYJYF
        IYLYJXQXLYLYJXQABXJYBCFGIVBJVBZVCVDYHYJXLVEYDYJYHXLYJXKYBOPZYHYJXKYNYJX
        KYBXJVFZOYJYOYBYJYIYBXJVGYOYBPYIYBXJVHYIYBXJVIVJVKXKYOOVFOXJOVLVMVNVOVD
        YBYMVPVQUNVRVSVTWAWBYCYHYKRYBWCYCYIYBUQTZYKYCYIRUQTZYPYCRRUTZYIUQTZYQYC
        YSRYBRYBWDYMWDYMWEWFYSYRRUQTZYQWGYIWHUAYSYTYQWIYBYBYMYMWJYRYIRWHWKWLWMV
        JYBWHUAYCYQYPWIYMRYBYIWHWNWLWOYIYBIYMWPWQWRWSWTJRBHXAXLIFXBXCXD $.
        $( [5-Oct-2004] $) $( [16-Sep-2004] $)  $( ok thru here $)

      $( Lemma for ~ infxpidm .  We show that the union of a bijection ` g ` in
         ` H ` with a disjoint bijection ` u ` is a member ` h ` of ` H ` that
         is larger than (properly extends) ` g ` .  Thus if the antecedent is
         true then ` g ` cannot be maximal. $)
      infxpidmlem11 $p |- ( ( ( ( ( om ~<_ x /\ x (_ A ) /\
            g : ( x X. x ) -1-1-onto-> x ) /\ ( x ~~ y /\ y (_ ( A \ x ) ) ) /\
          u : ( ( x X. y ) u. ( ( y X. x ) u. ( y X. y ) ) ) -1-1-onto-> y ) ->
                        E. h e. H g (. h ) $=
        ( com cv cdom wbr wss wa cxp wf1o cen cdif cun wpss wrex wcel psseq2
        rcla4ev cin c0 wceq f1oun xpdisj2 xpdisj1 jca undisj2 sylib ancri
        sylan2 wb xpun unass eqtr f1oeq2 ax-mp sylibr sslin difdisj syl6ss
        ss0b adantll wi visset unex infxpidmlem3 cvv ssun1 ssdomg mp2 domtr
        mpan2 difss sstr anim2i unss anim12i anassrs sylan ex adantlr mpd
        adantrl wn disjpssOLD crn ineq12 eqeq1d wfo f1ofo forn syl syl2an wrel
        f1orel relin1 relrn0 rnin sseq2 mpbii ss0 syl5bir 3syl adantr sylbird
        imp bitrd negbid infn0OLD domentr syl5 sylanc exp43 com4t com23 imp43
        exp42 com34 imp41 ) LAMZNOZYHEPZQZYHYHRZYHGMZSZYHBMZTOZYOEYHUAZPZQZYHYO
        RZYOYHRZYOYORZUBZUBZYOCMZSZYMHMZUCZHIUDZYKYNUUFYSUUIYKYNUUFYSUUIYMUUEUB
        ZIUEZYMUUJUCZUUIYKYNUUFQZQZYSQUUHUULHUUJIUUGUUJYMUFUGUUNYRUUKYPUUNYRQYH
        YOUBZUUORZUUOUUJSZUUKUUMYRUUQYKUUMYHYOUHZUIUJZUUQYRUUMUUSQZYLUUDUBZUUOU
        UJSZUUQUUMYLUUDUHUIUJZUUSQUVBUUSYLYHUUDYOYMUUEUKUUSUVCUUSYLYTUHUIUJZYLU
        UCUHUIUJZQUVCUUSUVDUVEYHYOYHYHULUUSYLUUAUHUIUJZYLUUBUHUIUJZQUVEUUSUVFUV
        GYHYOYHYHUMYHYOYHYOUMUNYLUUAUUBUOUPUNYLYTUUCUOUPUQURUUPUVAUJUUQUVBUSUUP
        YLYTUBUUCUBUVAYHYOYHYOUTYLYTUUCVAVBUUPUVAUUOUUJVCVDVEYRUURUIPUUSYRUURYH
        YQUHUIYOYQYHVFYHEVGVHUURVIUPZURVJYKYRUUQUUKVKUUMYKYRQZUUQUUKLUUONOZUUOE
        PZQZUUQUUKUVIDEUUJUUOFIJYMUUEGVLCVLVMYHYOAVLZBVLZVMVNYIYJYRUVLYIUVJYJYR
        QZUVKYIYHUUONOZUVJYHVOUEYHUUOPUVPUVMYHYOVPYHUUOVOVQVRLYHUUOVSVTUVOYJYOE
        PZQUVKYRUVQYJYRYQEPUVQEYHWAYOYQEWBVTWCYHYOEWDUPWEWFWGWHWIWJWKYKUUMYPYRU
        ULYIUUMYPYRUULVKZVKVKYJYIYPUUMUVRUUMYRYIYPUULUUMUUSYIYPUULVKVKYRUUMUUSY
        IYPUULYMUUEUHZUIUJZUUEUIUJZWLZUULUUTYIYPQZQYMUUEWMUUTUVTUWCUUMUUSUVTUUM
        UUSYMWNZUUEWNZUHZUIUJZUVTUWDYHUJZUWEYOUJZUWGUUSUSYNUUFUWHUWIQUWFUURUIUW
        DYHUWEYOWOWPYNYLYHYMWQUWHYLYHYMWRYLYHYMWSWTUUFUUDYOUUEWQUWIUUDYOUUEWRUU
        DYOUUEWSWTZXAYNUWGUVTVKZUUFYNYMXBUVSXBZUWKYLYHYMXCYMUUEXDUWLUVTUVSWNZUI
        UJZUWGUVSXEUWGUWMUIPZUWNUWGUWMUWFPUWOYMUUEXFUWFUIUWMXGXHUWMXIWTXJXKXLXM
        XNXLUUMUWCUWBUUSUUFUWCUWBYNUUFUWCUWBUUFLYONOZUWBUWCUUFUWBYOUIUJZWLUWPUU
        FUWAUWQUUFUWAUWEUIUJZUWQUUFUUEXBUWAUWRUSUUDYOUUEXCUUEXEWTUUFUWEYOUIUWJW
        PXOXPYOUVNXQXJLYHYOXRXSXNVJWIXTYAUVHXSYBYCXLYDXTYEYFYG $.
        $( [5-Oct-2004] $) $( [16-Sep-2004] $)

      $( Lemma for ~ infxpidm . Letting ` x ` be the range of a maximal
         bijection ` g ` in ` H ` , ~ infxpidmlem11 lets us show that assuming
         ` x ~<_ ( A \ x ) ` leads to the contradiction ` E. h e. H g (. h `
         meaning ` g ` is not maximal.  Thus ` ( A \ x ) ~< x ` . This allows
         us to show that ` x ` is as big as ` A ` . Since ` x ` is idempotent,
         and ` g ` exists by Zorn's Lemma in ~ infxpidmlem9 , ` A ` is also
         idempotent. $)
      infxpidmlem12 $p |- ( om ~<_ A -> ( A X. A ) ~~ A ) $=
        ( vg vh vx vy vu cv wpss wn wral wrex com cdom wbr cxp cen wi
        infxpidmlem9 wcel c0 wceq infxpidmlem10OLD wss wa wf1o wex wo visset
        infxpidmlem2 df-or bitr cdif csdm cun entrt enref xpen mpan sylan2
        adantll mpan2 anidms jca xpex infxpidmlem1 adantr mpd unex mp2and
        ensym syl pm3.27 sylanc sylanl2 bren sylib f1oen anim2i adantlr sylan
        adantrr infxpidmlem11 ex 19.23adv cvv difexg ax-mp domen syl5ib wb
        domtri mp2an dfrex2 3imtr3g a3d sbth domentr c1o unxpdom2 ssun2 ssdomg
        mp2 undif2 breqtrr domtr 1onn infsdomnn sdomdom syl2an expcom ad2antlr
        exp41 com24 imp31 syld 19.23aiv imim2i sylbi com13 com3r r19.23aiv ) GL
        ZHLMZNHDOZGDPQBRSZBBTZBUASZUBZABCGHDEFUCYIYMGDYIYJYGDUDZYLYIYJYGUEUFZNZ
        YNYLUBABCGHDEFUGYNYPYIYLYNYPQILZRSZYQBUHZUIZYQYQTZYQYGUJZUIZIUKZUBZYPYI
        YLUBZUBYNYOUUDULUUEIABYGCDEGUMUNYOUUDUOUPUUDUUFYPUUCUUFIUUCYIBYQUQZYQUR
        SZYLUUCUUHYIUUCYQUUGRSZYHHDPZUUHNZYINUUCYQJLZUASZUULUUGUHZUIZJUKUUJUUIU
        UCUUOUUJJUUCUUOUUJUUCUUOUIZYQUULTZUULYQTZUULUULTZUSZUSZUULKLUJZKUKZUUJU
        UCUUMUVCUUNYRUUAYQUASZUIZUUMUVCUUCUVEUUMUIUVAUULUASZUVCYRYQUUAUASZUUMUV
        FUVDUVAYQUASZUUMUVFYRUVGUIZUUMUIZUVAYQUULUTUVJYQUVAUASZUVHUVJYQUUQUASZY
        QUUTUASZUVKUVGUUMUVLYRUVGUUAUUQUASZUVLUUMYQUUAUUQUTYQYQUASZUUMUVNYQIUMZ
        VAZYQYQYQUULUVPUVPUVPJUMZVBVCVDVEUVJYQUURUASZYQUUSUASZUIZUVMUVGUUMUWAYR
        UVGUUMUIUVSUVTUVGUUAUURUASZUVSUUMYQUUAUURUTUUMUVOUWBUVQYQUULYQYQUVPUVRU
        VPUVPVBVFVDUVGUUAUUSUASZUVTUUMYQUUAUUSUTUUMUWCYQUULYQUULUVPUVRUVPUVRVBV
        GVDVHVEUVIUWAUVMUBUUMIUURUUSUULYQUVRUVPVIZUULUULUVRUVRVIZVJVKVLUVIUVLUV
        MUIUVKUBUUMIUUQUUTYQUULUVPUVRVIZUURUUSUWDUWEVMZVJVKVNYQUVAUUQUUTUWFUWGV
        MVOVPUVIUUMVQVRUUAYQUVPVOVSUVAUULKUVRVTWAYRUUBUVEYSUUBUVDYRUUAYQYGYQYQU
        VPUVPVIWBZWCWDWEWFUUPUVBUUJKUUPUVBUUJIJKABCGHDEFWGWHWIVLWHWIJYQUUGBWJUD
        ZUUGWJUDZFBYQWJWKWLZWMWNYQWJUDZUWJUUIUUKWOUVPUWKYQUUGWJWJWPWQYHHDWRWSWT
        YTUVDUUHYLUBZUUBYRYSUVDUWMYRUUHUVDYSYLYRUUHUVDYSYLYRUUHUIZUVDUIZYSUIBYQ
        UASZYLBYQRSZYQBRSZUWPUWOYSBYQXABUUARSZUVDUWQUWNBUUAYQXBXCYQURSZUUGYQRSZ
        UWSYRUUHUWTUXAUIYQUUGUSZUUARSZUWSYQUUGUVPUWKXDBUXBRSUXCUWSBYQBUSZUXBRUW
        IBUXDUHBUXDRSFBYQXEBUXDWJXFXGYQBXHXIBUXBUUAXJVCVPYRXCQUDUWTXKYQXCUVPXLV
        FUUGYQXMXNWEUWLYSUWRUBUVPYQBWJXFWLXNUVDUWPYLUBUWNYSUWPUVDYLYKYQUASZYQBU
        ASZYLUWPUVDUIYKYQBUTYKUUAUASZUVDUXEUWPYKUUAYQUTUWPUXGBYQBYQFUVPFUVPVBVG
        WEUWPUXFUVDBYQUVPVOVKVRXOXPVLXQXRXSUWHVDXTYAYBYCYDXTYEYFWL $.
        $( [5-Oct-2004] $) $( [17-Sep-2004] $)
    $}
  $}

  ${
    $d f x A $.
    infxpidm.1 $e |- A e. V $.
    $( The cross product of an infinite set with itself is idempotent.  This
       theorem provides the basis for infinite cardinal arithmetic.
       Lemma 6R of [Enderton] p. 162, whose proof we follow closely.  The
       main proof consists of ~ infxpidmlem1 through ~ infxpidmlem12 .  This
       final piece eliminates the first hypothesis of ~ infxpidmlem12 . $)
    infxpidm $p |- ( om ~<_ A -> ( A X. A ) ~~ A ) $=
      ( vx vf cv c0 wceq com cdom wbr wss wa cxp wf1o wex wo cab eqid
      infxpidmlem12 ) CADDEZFGHCEZIJUAAKLUAUAMUATNLCOPDQZUBRBS $.
      $( [5-Oct-2004] $) $( [17-Sep-2004] $)
  $}

  ${
    infunabs.1 $e |- A e. V $.
    infunabs.2 $e |- B e. V $.
    $( An infinite set is equinumerous to its union with a smaller one. $)
    infunabs $p |- ( ( om ~<_ A /\ B ~<_ A ) -> ( A u. B ) ~~ A ) $=
      ( com cdom wbr wa cun cen ccda co cxp domentr c2o domtr cdadom2 xp2cda
      syl6breqr wn wcel 2onn omsdomnn ax-mp pm3.26i mpan xpdom2 syl syl2an
      infxpidm adantl sylanc cdadom3 jctir sbth ancoms uncdadom cvv wss ssun1
      ssdomg mp2 ) EAFGZBAFGZHZABIZAFGZAVFFGZHVFAJGVEVGVHVEABKLZAJGZVGVDVCVJVDV
      CHZVIAFGZAVIFGZHVJVKVLVMVIAAMZFGZVNAJGZVLVKVIVNANVIAOMZFGVQVNFGZVOVDVCVIV
      QVNPVDVIAAKLVQFBAADCCQACRSVCOAFGZVROEFGZVCVSVTEOJGTZOEUAVTWAHUBOUCUDUEOEA
      PUFOAACCUGUHUIVCVPVDACUJUKULABCDUMUNVIAUOUHUPVFVIFGVJVGABCDUQVFVIANUFUHAU
      RUAAVFUSVHCABUTAVFURVAVBUNVFAUOUH $.
      $( [7-Oct-2004] $) $( [28-Sep-2004] $)

    $( Absorption law for addition to an infinite cardinal. $)
    infcdaabs $p |- ( ( om ~<_ A /\ B ~<_ A ) -> ( A +c B ) ~~ A ) $=
      ( cdom wbr com ccda co cen wa cxp domentr c2o domtr cdadom2 xp2cda
      syl6breqr wn wcel 2onn omsdomnn ax-mp pm3.26i mpan xpdom2 syl syl2an
      infxpidm adantl sylanc cdadom3 jctir sbth ancoms ) BAEFZGAEFZABHIZAJFZUPU
      QKZURAEFZAUREFZKUSUTVAVBURAALZEFZVCAJFZVAUTURVCAMURANLZEFVFVCEFZVDUPUQURV
      FVCOUPURAAHIVFEBAADCCPACQRUQNAEFZVGNGEFZUQVHVIGNJFSZNGTVIVJKUANUBUCUDNGAO
      UENAACCUFUGUHUQVEUPACUIUJUKABCDULUMURAUNUGUO $.
      $( [23-Oct-2004] $) $( [30-Sep-2004] $)

    $( The sum of two cardinal numbers is their maximum, if one of them is
       infinite.  Proposition 10.41 of [TakeutiZaring] p. 95. $)
    infcda $p |- ( om ~<_ A -> ( A +c B ) ~~ ( A u. B ) ) $=
      ( com cdom wbr wo ccda co cun cen cvv wcel entri3 mp2an wa entrt
      infcdaabs infunabs ensym syl sylanc ex cdacomen mpan uncom syl6breq
      domtr sylan exp31 pm2.43d jaod mpi ) EAFGZBAFGZABFGZHZABIJZABKZLGZBMNAMNU
      RDCBAMMOPUOUPVAUQUOUPVAUSALGAUTLGZVAUOUPQZUSAUTRABCDSVCUTALGVBABCDTUTACUA
      UBUCUDUOUQVAUOUQUQVAEBFGZUQVAUOUQQVDUQQZUSBAKZUTLVEBAIJZVFLGZUSVFLGZVGBLG
      BVFLGZVHVEVGBVFRBADCSVEVFBLGVJBADCTVFBDUAUBUCUSVGLGVHVIABCDUEUSVGVFRUFUBB
      AUGUHEABUIUJUKULUMUN $.
      $( [29-Mar-2005] $) $( [28-Sep-2004] $)

    $( The cardinality of an infinite set does not change after subtracting
       a strictly smaller one.  Example in [Enderton] p. 164. $)
    infdif $p |- ( ( om ~<_ A /\ B ~< A ) -> ( A \ B ) ~~ A ) $=
      ( com cdom wbr csdm wa cdif cen ccda co cun endomtr infunabs ensym syl
      sdomdom sylan2 wn wo cvv wcel omex entri2 mp2an wi wss ssun1 ssdomg mp2
      wb unex domtri mpbi domsdomtr difexg ax-mp cdadom1 uncdadom domtr mpan
      undif1 syl5eqbrr ensdomtr domrefg infcdaabs mpan2 sylan ancoms syl2an
      expcom mtoi ex adantl simpll cdafi anidms an1rs con3d syl5ib mpd jaod
      mpi con2bii sylibr cdadom2 eqbrtrr 3syl sylanc cdainf domentr syldan
      difss ssdom2g jctil sbth ) EAFGZBAHGZIZABJZAFGZAXBFGZIXBAKGXAXDXCWSWTAXBX
      BLMZFGZXDAABNZKGZXGXEFGZXFXAAXGXEOWSBAFGZXHWTWSXJIXGAKGXHABCDPXGACQRBASTZ
      XABXBHGZBXBFGZXIXAXBBFGZUAZXLXAEBFGZBEHGZUBZXOEUCUDZBUCUDZXRUEDEBUCUCUFUG
      XAXPXOXQWTXPXOUHWSWTXPXOWTXPIZXNXGAHGZAXGFGZYBUAZAUCUDZAXGUIYCCABUJAXGUCU
      KULYEXGUCUDYCYDUMCABCDUNAXGUCUCUOUGUPXNYAYBXGBBLMZFGZYFAHGZYBXNYAXGYFAUQX
      NXBBNZYFXGFXNXBBLMZYFFGZYIYFFGZXBBBYEXBUCUDZCABUCURUSZDDUTYIYJFGYKYLXBBYN
      DVAZYIYJYFVBVCRABVDZVEZXPWTYHYFBKGZWTYHXPYFBAVFXPBBFGZYRXTYSDBUCVGUSBBDDV
      HVIVJVKVLVMVNVOVPXAXQXOXAXQIZWSXOWSWTXQVQYTAEHGZUAZXOWSYTXNUUAYTXNUUAXAXN
      XQUUAAYFFGZYFEHGZUUAXAXNIXQAYFEUQXHYGUUCXAXNAXGYFOXKYQVLXQUUDBBVRVSVLVTVO
      WAXSYEWSUUBUMUECEAUCUCUOUGWBWCVOWDWEXNXLYMXTXNXLUAUMYNDXBBUCUCUOUGWFWGBXB
      SXMYJXEFGZXIBXBXBDYNYNWHXGYJFGUUEXIYIXGYJFYPYOWIXGYJXEVBVCRWJWKWSXFIZXEXB
      KGZXDUUFEXBFGZUUGUUFEXEFGUUHEAXEVBXBYNWLWGUUHXBXBFGZUUGYMUUIYNXBUCVGUSXBX
      BYNYNVHVIRXFUUGXDUHWSXFUUGXDAXEXBWMVOVPWCWNYEXBAUIXCCABWOXBAUCWPULWQXBAWR
      R $.
      $( [23-Oct-2004] $) $( [22-Oct-2004] $)

    $( Cardinality ordering for an infinite set difference. $)
    infdif2 $p |- ( om ~<_ A -> ( ( A \ B ) ~<_ B <-> A ~<_ B ) ) $=
      ( com cdom wbr cdif csdm wn wa cen infdif ensym syl wi cvv wcel difexg
      ax-mp sdomentr ex adantl mpd con3d wb domtri mp2an 3imtr4g wss difss
      ssdom2g mp2 domtr mpan impbid1 ) EAFGZABHZBFGZABFGZUQBURIGZJZBAIGZJZUSUTU
      QVCVAUQVCVAUQVCKZAURLGZVAVEURALGVFABCDMURACNOVCVFVAPUQVCVFVAURQRZVCVFKVAP
      AQRZVGCABQSTZBAURQUATUBUCUDUBUEVGBQRZUSVBUFVIDURBQQUGUHVHVJUTVDUFCDABQQUG
      UHUIURAFGZUTUSVHURAUJVKCABUKURAQULUMURABUNUOUP $.
      $( [31-Mar-2007] $) $( [24-Mar-2007] $)

    $( Absorption law for multiplication with an infinite cardinal. $)
    infxpabs $p |- ( ( om ~<_ A /\ B =/= (/) /\ B ~<_ A ) ->
                     ( A X. B ) ~~ A ) $=
      ( com cdom wbr c0 wne cxp cen wa sbth domentr xpdom2 infxpidm syl2an
      ancoms xpdom3 3impa 3com23 ) EAFGZBAFGZBHIZABJZAKGZUBUCUDUFUEAFGZAUEFGUFU
      BUCLUDUEAMUCUBUGUEAAJZFGUHAKGUGUCUBUEUHANBAACCOACPQRABCSQTUA $.
      $( [29-Nov-2006] $) $( [30-Sep-2004] $)

    $( Absorption law for multiplication with an infinite cardinal. $)
    infxpabsOLD $p |- ( ( ( om ~<_ A /\ -. B = (/) ) /\ B ~<_ A ) ->
                     ( A X. B ) ~~ A ) $=
      ( com cdom wbr c0 wceq wn cxp cen wa sbth domentr xpdom2 infxpidm syl2an
      ancoms xpdom3OLD an1rs ) EAFGZBAFGZBHIJZABKZALGZUEAFGZAUEFGUFUBUCMUDUEANU
      CUBUGUEAAKZFGUHALGUGUCUBUEUHAOBAACCPACQRSABCTRUA $.
      $( [9-Oct-2004] $) $( [30-Sep-2004] $)

    $( Dominance law for multiplication with an infinite cardinal. $)
    infxpdom $p |- ( ( om ~<_ A /\ B ~<_ A ) -> ( A X. B ) ~<_ A ) $=
      ( com cdom wbr wa c0 wceq cxp xpeq2 xp0 syl6eq 0dom syl6eqbr adantl wn
      cen infxpabsOLD endom syl an1rs pm2.61dan ) EAFGZBAFGZHZBIJZABKZAFGZUHUJU
      GUHUIIAFUHUIAIKIBIALAMNAOPQUEUHRZUFUJUEUKHUFHUIASGUJABCDTUIAUAUBUCUD $.
      $( [3-Apr-2006] $) $( [26-Mar-2006] $)

    $( Absorption law for multiplication with an infinite cardinal.  Equivalent
       to Proposition 10.41 of [TakeutiZaring] p. 95. $)
    infxp $p |- ( ( om ~<_ A /\ B =/= (/) ) ->
                   ( A X. B ) ~~ ( A u. B ) ) $=
      ( com cdom wbr c0 wne wa wo cxp cun cen cvv wcel entri3 mp2an entrt
      infxpabs 3expa infunabs ensym syl adantlr sylanc ex wi w3a domtr infn0
      adantr pm3.27 3jca 3adant2 xpcomen mpan 3syl uncom syl6breq jaod mpi ) EA
      FGZBHIZJZBAFGZABFGZKZABLZABMZNGZBOPAOPVHDCBAOOQRVEVFVKVGVEVFVKVIANGZAVJNG
      ZVKVEVFJVIAVJSVCVDVFVLABCDTUAVCVFVMVDVCVFJVJANGVMABCDUBVJACUCUDUEUFUGVCVG
      VKUHVDVCVGVKVCVGJZVIBAMZVJNVNEBFGZAHIZVGUIZBALZVONGZVIVONGZVNVPVQVGEABUJV
      CVQVGACUKULVCVGUMUNVSBNGBVONGZVTVRVSBVOSBADCTVPVGWBVQVPVGJVOBNGWBBADCUBVO
      BDUCUDUOUFVIVSNGVTWAABCDUPVIVSVOSUQURBAUSUTUGULVAVB $.
      $( [1-Dec-2006] $) $( [28-Sep-2004] $)

    $( An exponentiation law for infinite cardinals.  Exercise 34 of [Enderton]
       p. 165. $)
    infmap1 $p |- ( ( ( 2o ~<_ A /\ om ~<_ B ) /\ A ~<_ B ) ->
                  ( A ^m B ) ~~ ( 2o ^m B ) ) $=
      ( cdom wbr com c2o cm co cen wa sbth domtr mapdom1 cxp infxpidm con0 2on
      elisseti enref xpex mapen mpan cpw csdm canth2 sdomdom ax-mp pw2en
      domentr mp2an oprex mapxpen 3syl syl2an ancom31s ) ABEFZGBEFZHAEFZABIJZHB
      IJZKFZVAVBEFZVBVAEFVCURUSLUTVAVBMVABBIJZEFVEVBEFZVDURUSVAVEVBNABBCDDOUSBB
      PZBKFZHVGIJZVBKFZVFBDQHHKFVHVJHHRSTZUAHHVGBVKVKBBDDUBDUCUDVEVIEFZVJVFVEVB
      BIJZEFZVMVIKFVLBVBEFZVNBBUEZEFZVPVBKFVOBVPUFFVQBDUGBVPUHUIBDUJBVPVBUKULBV
      BBDHBIUMDOUIHBBVKDDUNVEVMVIUKULVEVIVBUKUDUOUPHABVKCDOUPUQ $.
      $( [8-Oct-2004] $) $( [30-Sep-2004] $)
  $}

  ${
    $d x y A $.
    infpss.1 $e |- A e. V $.
    $( Every infinite set has an equinumerous proper subset.  Exercise 7 of
       [TakeutiZaring] p. 91. $)
    infpss $p |- ( om ~<_ A -> E. x ( x (. A /\ x ~~ A ) ) $=
      ( vy cv wcel wex com cdom wbr wpss cen wa c0 wne infn0 ne0 sylib wi csn
      cdif cvv difexg ax-mp wceq psseq1 breq1 anbi12d cla4ev cin wn snidg
      ancli elin sylibr n0i syl disj4 con2bii csdm c1o 1onn infsdomnn mpan2
      visset ensn1 ensdomtr mpan snex infdif mpdan syl2an ex 19.23aiv mpcom )
      DEZBFZDGZHBIJZAEZBKZVTBLJZMZAGZVSBNOVRBCPDBQRVQVSWDSDVQVSWDBVPTZUAZBKZWFB
      LJZWDVQVSWCWGWHMAWFBUBFWFUBFCBWEUBUCUDVTWFUEWAWGWBWHVTWFBUFVTWFBLUGUHUIVQ
      BWEUJZNUEZUKZWGVQVPWIFZWKVQVQVPWEFZMWLVQWMVPBULUMVPBWEUNUOWIVPUPUQWJWGBWE
      URUSUOVSWEBUTJZWHVSVABUTJZWNVSVAHFWOVBBVACVCVDWEVALJWOWNVPDVEVFWEVABVGVHU
      QBWECVPVIVJVKVLVMVNVO $.
      $( [?] $) $( [23-Oct-2004] $)
  $}

  ${
    $d x A $.
    iunctb.1 $e |- A e. V $.
    iunctb.2 $e |- B e. V $.
    $( The countable union of countable sets is countable (indexed union
       version of ~ unictb ). $)
    iunctb $p |- ( ( A ~<_ om /\ A. x e. A B ~<_ om ) ->
                 U_ x e. A B ~<_ om ) $=
      ( com cdom wbr wral ciun cxp domtr omex iundom cvv wcel domrefg ax-mp
      infxpdom mpan cen wb xpex xpcomen domen1 mp2an sylib syl2an ancoms ) CFGH
      ABIZBFGHZABCJZFGHZULBFKZGHUNFGHZUMUJUKULUNFLABFCDMENUKFBKZFGHZUOFFGHZUKUQ
      FOPURMFOQRFBMDSTUNOPUPUNUAHUQUOUBBFDMUCFBMDUDUPUNFOUEUFUGUHUI $.
      $( [5-Apr-2006] $) $( [26-Mar-2006] $)
  $}

  ${
    $d x A $.
    unictb.1 $e |- A e. V $.
    $( The countable union of countable sets is countable.  Theorem 6Q of
       [Enderton] p. 159.  See ~ iunctb for indexed union version. $)
    unictb $p |- ( ( A ~<_ om /\ A. x e. A x ~<_ om ) -> U. A ~<_ om ) $=
      ( com cdom wbr cv wral wa ciun cuni visset iunctb uniiun syl5eqbr ) BDEFA
      GZDEFABHIABPJDBKEABPCALMABNO $.
      $( [26-Aug-2006] $) $( [26-Mar-2006] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The union of two countable sets is countable.  (Contributed by FL,
       25-Aug-2006.) $)
    unctb $p  |- ( ( A ~<_ om /\ B ~<_ om ) -> ( A u. B ) ~<_ om ) $=
      ( vx com cdom wbr wa cpr cuni cun cvv wcel wceq uniprg reldom brrelexi
      syl2an cv wral wo breq1 biimprcd jaao visset elpr syl5ib r19.21aiv cen
      wn wrex prfi isfinite1 ax-mp pm3.26i prex unictb mpan syl eqbrtrrd ) ADEF
      ZBDEFZGZABHZIZABJZDEAKLBKLVDVEMUTVAABKKNADEOPBDEOPQVBCRZDEFZCVCSZVDDEFZVB
      VGCVCVBVFAMZVFBMZTVGVFVCLUTVJVGVAVKVJVGUTVFADEUAUBVKVGVAVFBDEUAUBUCVFABCU
      DUEUFUGVCDEFZVHVIVLDVCUHFUIZVCVFUHFCDUJVLVMGCABUKCVCULUMUNCVCABUOUPUQURUS
      $.
      $( [26-Aug-2006] $) $( [25-Aug-2006] $)
  $}

  $( There are at least aleph-one irrationals. $)
  aleph1irr $p |- ( aleph ` 1o ) ~<_ ( RR \ QQ ) $=
    ( c1o cale cfv cr cdom wbr cq cdif cen aleph1re reex com csdm cn omex
    nnenom ensymi ruc ensdomtr mp2an sdomdom ax-mp resdomq qex infdif domentr
    ) ABCZDEFDDGHZIFUGUHEFJUHDKLDEFZGDMFUHDIFLDMFZUILNIFNDMFUJNLOPQRLNDSTLDUAUB
    UCDGKUDUETQUGDUHUFT $.
    $( [3-Feb-2005] $) $( [2-Feb-2005] $)

  ${
    $d x y z w v u f A $.  $d x y z w f v u B $.  $d u v f R $.
    infmap2lem.1 $e |- A e. V $.
    infmap2lem.2 $e |- B e. V $.
    infmap2lem.3 $e |- R = { <. z , w >. | ( ( z (_ A /\ z ~~ B ) /\
                        w : B -onto-> z ) } $.
      $( Lemma for ~ infmap2 .  Technical result that is used several times. $)
    infmap2lem1 $p |- ( ( f (_ R /\ f Fn dom R ) ->
                       ( v e. { x | ( x (_ A /\ x ~~ B ) } ->
                       ( v (_ A /\ ( f ` v ) : B -onto-> v ) ) ) $=
      ( cv wss cdm wfn wa wcel cfv wfo cen wbr cab wi cop ssel copab eleq2i
      visset fvex wceq sseq1 breq1 anbi12d foeq3 foeq1 anbi2d opelopab bitr
      pm3.26 anim1i sylbi syl6 fnopfv syl5 exp3a imp wex dmeqi dmopab anass
      19.42v ensym wf1o bren f1ofo 19.22i syl pm4.71i anbi2i 3bitr4 abbii
      3eqtr cbvabv eqtr syl5ibr ) HLZGMZWFGNZOZPDLZWHQZWJEMZFWJWJWFRZSZPZWJALZE
      MZWPFTUAZPZAUBZQWGWIWKWOUCWGWIWKWOWGWJWMUDZWFQZWOWIWKPWGXBXAGQZWOWFGXAUEX
      CWLWJFTUAZPZWNPZWOXCXABLZEMZXGFTUAZPZFXGCLZSZPZBCUFZQXFGXNXAKUGXMXEFWJXKS
      ZPXFBCWJWMDUHWJWFUIXGWJUJZXJXEXLXOXPXHWLXIXDXGWJEUKXGWJFTULUMXGWJFXKUNUMX
      KWMUJXOWNXEFWJXKWMUOUPUQURXEWLWNWLXDUSUTVAVBWHWJWFVCVDVEVFWHWTWJWHXJBUBZW
      TWHXNNXMCVGZBUBXQGXNKVHXMBCVIXRXJBXJXLCVGZPXHXIXSPZPXRXJXHXIXSVJXJXLCVKXI
      XTXHXIXSXIFXGTUAZXSXGFJVLYAFXGXKVMZCVGXSFXGCBUHVNYBXLCFXGXKVOVPVAVQVRVSVT
      WAWBXJWSBAXGWPUJXHWQXIWRXGWPEUKXGWPFTULUMWCWDUGWE $.
      $( [11-Oct-2004] $) $( [1-Oct-2004] $)

    $( Lemma for ~ infmap2 .  Given the relation ` R ` , we use the Axiom of
       Choice ~ ac7g to extract a function ` f ` that provides the one-to-one
       mapping for the dominance relation. $)
    infmap2lem2 $p |- { x | ( x (_ A /\ x ~~ B ) } ~<_ ( A ^m B ) $=
      ( vf vv vu cv wss cdm wfn wa wex cen wbr cab cm co cdom cvv wcel wfo
      copab cpw cxp df-xp pwex oprex xpex eqeltrr pm3.26 visset elpw sylibr
      crn wf fof ffn syl adantl forn sseq1d biimparc jca elmap df-f bitr
      adantlr ssopab2i ssexi eqeltr ac7g ax-mp df-pw ss2abi cfv infmap2lem1
      fss sylan ancoms syl6 wceq wb pm3.27 anim12d eqeqan12d rneq syl5bi fveq2
      impbid1 dom2d mpi 19.23aiv ) JMZFNWSFOPQZJRZAMZDNZXBESTZQZAUAZDEUBUCZUDTZ
      FUEUFXAFBMZDNZXIESTZQEXICMZUGZQZBCUHZUEIXOXIDUIZUFZXLXGUFZQZBCUHZXPXGUJXT
      UEBCXPXGUKXPXGDGULZDEUBUMUNUOXNXSBCXJXMXSXKXJXMQZXQXRYBXJXQXJXMUPXIDBUQUR
      USYBXLEPZXLUTZDNZQZXRYBYCYEXMYCXJXMEXIXLVAYCEXIXLVBEXIXLVCVDVEXMYEXJXMYDX
      IDEXIXLVFVGVHVIXREDXLVAYFDEXLGHVJEDXLVKVLUSVIVMVNVOVPUEFJVQVRWTXHJWTXFUEU
      FXHXFXCAUAZXPYGUEADVSYAUOXEXCAXCXDUPVTVOWTKLXFXGKMZWSWAZLMZWSWAZUEWTYHXFU
      FZYHDNZEYHYIUGZQZYIXGUFZABCKDEFJGHIWBZYOEDYIVAZYPYNYMYREYHYIVAYMYRYNEYHDY
      IWCEYHYIVBWDWEDEYIGHVJUSWFWTYLYJXFUFZQYNEYJYKUGZQZYIYKWGZYHYJWGZWHWTYLYNY
      SYTWTYLYOYNYQYMYNWIWFWTYSYJDNZYTQYTABCLDEFJGHIWBUUDYTWIWFWJUUAUUBUUCUUAYI
      UTZYKUTZWGUUCUUBYNYTUUEYHUUFYJEYHYIVFEYJYKVFWKYIYKWLWMYHYJWSWNWOWFWPWQWRV
      R $.
      $( [11-Oct-2004] $) $( [30-Sep-2004] $)
  $}

  ${
    $d x z w A $.  $d x z w B $.
    infmap2.1 $e |- A e. V $.
    infmap2.2 $e |- B e. V $.
    $( An exponentiation law for infinite cardinals.  Similar to Lemma 6.2 of
       [Jech] p. 43.  We start with ~ infmap2lem2 and also prove the other
       direction of the dominance relation.  We obtain equinumerosity with
       Schroeder-Bernstein ~ sbth and finally eliminate the degenerate case
       ` B = (/) ` . $)
    infmap2 $p |- ( ( om ~<_ A /\ B ~<_ A ) ->
                      ( A ^m B ) ~~ { x | ( x (_ A /\ x ~~ B ) } ) $=
      ( vz vw com cdom wbr cm co cv wss cen wa cab c0 wceq wi wn cxp
      infxpabsOLD xpcomen entrt mpan syl xpex ssenen cvv wcel oprex abid2 wf
      elmap fssxp cdm wfun ffun visset fundmen ensym 3syl fdm breqtrd jca
      sylbi ss2abi eqsstr3 ssdomg mp2 domentr wfo copab eqid infmap2lem2 sbth
      mpan2 exp31 c1o map0e opreq2 breq2 anbi2d abbidv csn df-sn df1o2 en0
      anbi2i 0ss sseq1 mpbiri pm4.71ri bitr4 abbii 3eqtr4r syl6eq 3eqtr4a
      eqeng ax-mp a1d pm2.61d2 imp ) HBIJZCBIJZBCKLZAMZBNZXHCOJZPZAQZOJZXECRSZX
      FXMTXEXNUAZXFXMXEXOPXFPZXGXLIJZXMXPCBUBZBOJZXHXRNZXJPZAQZXLOJZXQXPBCUBZBO
      JZXSBCDEUCXRYDOJYEXSCBEDUDXRYDBUEUFUGAXRBCCBEDUHDUIXGYBIJZYCXQXGUJUKZXGYB
      NYFBCKULZXGXHXGUKZAQYBAXGUMYIYAAYICBXHUNZYABCXHDEUOYJXTXJCBXHUPYJXHXHUQZC
      OYJXHURYKXHOJXHYKOJCBXHUSXHAUTZVAYKXHYLVBVCCBXHVDVEVFVGVHVIXGYBUJVJVKXGYB
      XLVLUFVCXQXLXGIJXMAFGBCFMZBNYMCOJPCYMGMVMPFGVNZDEYNVOVPXGXLVQVRUGVSXNXMXF
      XNXGXLSZXMXNBRKLVTXGXLBDWACRBKWBXNXLXIXHROJZPZAQZVTXNXKYQAXNXJYPXICRXHOWC
      WDWERWFXHRSZAQVTYRARWGWHYQYSAYQXIYSPYSYPYSXIXHWIWJYSXIYSXIRBNBWKXHRBWLWMW
      NWOWPWQWRWSYGYOXMTYHXGXLUJWTXAUGXBXCXD $.
      $( [11-Oct-2004] $) $( [1-Oct-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The sum of two alephs is their maximum.  Equation 6.1 of [Jech]
       p. 42. $)
    alephadd $p |- ( ( aleph ` A ) +c ( aleph ` B ) ) ~~
                   ( ( aleph ` A ) u. ( aleph ` B ) ) $=
      ( con0 wcel cale cfv ccda co cun cen wbr wn wa wceq cdm c0 csn cxp c1o
      0ex cdaval xpundi xp0r 3eqtr2 ndmfv opreqan12d adantr adantl uneq12d un0
      syl6eq 3eqtr4a wfn alephfnon fndm ax-mp eleq2i negbii syl2anbr cvv wi
      oprex eqeng syl ex com wss alephgeom cdom fvex ssdom2g infcda sylbi
      cdacomen entrt mpan uncom syl6breq pm2.61ii ) ACDZBCDZAEFZBEFZGHZWBWCIZJK
      ZVTLZWALZWFWGWHMWDWENZWFAEOZDZLZBWJDZLZWIWGWHWLWNMZPPGHZPWDWEWPPPQZRPSQZR
      IPWQWRIZRPPPTTUAPWQWRUBWSUCUDWLWNWBPWCPGAEUEZBEUEZUFWOWEPPIPWOWBPWCPWLWBP
      NWNWTUGWNWCPNWLXAUHUIPUJUKULWKVTWJCAECUMWJCNUNCEUOUPZUQURWMWAWJCBXBUQURUS
      WDUTDWIWFVAWBWCGVBWDWEUTVCUPVDVEVTVFWBVGZWFAVHXCVFWBVIKZWFWBUTDXCXDVAAEVJ
      ZVFWBUTVKUPWBWCXEBEVJZVLVDVMWAVFWCVGZWFBVHXGVFWCVIKZWFWCUTDXGXHVAXFVFWCUT
      VKUPXHWDWCWBIZWEJXHWCWBGHZXIJKZWDXIJKZWCWBXFXEVLWDXJJKXKXLWBWCXEXFVNWDXJX
      IVOVPVDWCWBVQVRVDVMVS $.
      $( [30-Mar-2005] $) $( [29-Sep-2004] $)

    $( The product of two alephs is their maximum.  Equation 6.1 of [Jech]
       p. 42. $)
    alephmul $p |- ( ( A e. On /\ B e. On ) ->
  ( ( aleph ` A ) X. ( aleph ` B ) ) ~~ ( ( aleph ` A ) u. ( aleph ` B ) ) ) $=
      ( com cale cfv cdom wbr c0 wne cxp cun cen con0 wcel fvex infxp wss
      alephgeom cvv wi ssdom2g ax-mp sylbi infn0 syl syl2an ) CADEZFGZBDEZHIZUG
      UIJUGUIKLGAMNZBMNZUGUIADOZBDOZPUKCUGQZUHARUGSNUOUHTUMCUGSUAUBUCULCUIQZUJB
      RUPCUIFGZUJUISNUPUQTUNCUISUAUBUIUNUDUEUCUF $.
      $( [?] $) $( [29-Sep-2004] $)

    $( An exponentiation law for alephs.  Lemma 6.1 of [Jech] p. 42. $)
    alephexp1 $p |- ( ( ( A e. On /\ B e. On ) /\ A (_ B ) ->
  ( ( aleph ` A ) ^m ( aleph ` B ) ) ~~ ( 2o ^m ( aleph ` B ) ) ) $=
      ( c2o cale cfv cdom wbr com wa cm co cen con0 wcel wss fvex infmap1
      alephgeom cvv wi ssdom2g ax-mp sylbi csdm 2onn nnsdom sdomdom domtr mpan
      syl anim12i adantr alephord3 ssdomg syl6bi imp sylanc ) CADEZFGZHBDEZFGZI
      ZURUTFGZURUTJKCUTJKLGAMNZBMNZIZABOZIURUTADPZBDPZQVFVBVGVDUSVEVAVDHURFGZUS
      VDHUROZVJARURSNZVKVJTVHHURSUAUBUCCHFGZVJUSCHUDGZVMCHNVNUECUFUBCHUGUBCHURU
      HUIUJVEHUTOZVABRUTSNVOVATVIHUTSUAUBUCUKULVFVGVCVFVGURUTOZVCABUMVLVPVCTVHU
      RUTSUNUBUOUPUQ $.
      $( [8-Oct-2004] $) $( [29-Sep-2004] $)
  $}

  ${
    $d x A $.
    $( An alternate representation of a successor aleph.  Compare ~ alephsuc
       and ~ alephsuc2 .   Equality can be obtained by taking the ` card `
       of the right-hand side then using ~ alephcard and ~ carden . $)
    alephsuc3 $p |- ( A e. On ->
               ( aleph ` suc A ) ~~ { x e. On | x ~~ ( aleph ` A ) } ) $=
      ( con0 wcel cv cale cfv cen wbr crab csuc cdif cdom csdm alephsuc2
      difeq1d wceq ccrd alephcard cardval2 eqtr3 a1i difeq2d eqtrd wn wa
      difrab wb bren2 rabbii eqtr4 syl6req com fvex infdif wss sucelon
      alephgeom bitr cvv wi ssdom2g ax-mp sylbi alephordlem1 sylanc eqbrtrd
      ensym syl ) BCDZAEZBFGZHIZACJZBKZFGZHIVPVNHIVJVNVPVLLZVPHVJVQVKVLMIZACJZV
      KVLNIZACJZLZVNVJVQVSVLLWBVJVPVSVLABOPVJVLWAVSVLWAQVJVLRGVLWABSAVLTUAUBUCU
      DWBVRVTUEUFZACJVNVRVTACUGVMWCACVMWCUHVKCDVKVLUIUBUJUKULUMVPMIZVLVPNIVQVPH
      IVJVPVLVOFUNZBFUNUOVJUMVPUPZWDVJVOCDWFBUQVOURUSVPUTDWFWDVAWEUMVPUTVBVCVDB
      VEVFVGVNVPWEVHVI $.
      $( [23-Oct-2004] $) $( [23-Oct-2004] $)

    $( An expression equinumerous to 2 to an aleph power.  The proof equates
       the two laws for cardinal exponentiation ~ alephexp1 (which works if the
       base is less than or equal to the exponent) and ~ infmap2 (which works
       if the exponent is less than or equal to the base).  They can be equated
       only when the base is equal to the exponent, and this is the result.  $)
    alephexp2 $p |- ( A e. On -> ( 2o ^m ( aleph ` A ) ) ~~
          { x | ( x (_ ( aleph ` A ) /\ x ~~ ( aleph ` A ) ) } ) $=
      ( con0 wcel cale cfv cm co cv wss cen wbr wa cab c2o com cdom alephgeom
      cvv wi fvex ssdom2g ax-mp sylbi domrefg jctir infmap2 syl wb pm3.2
      pm2.43i ssid alephexp1 oprex enen1 mpan 3syl mpbid ) BCDZBEFZUTGHZAIZUTJV
      BUTKLMANZKLZOUTGHZVCKLZUSPUTQLZUTUTQLZMVDUSVGVHUSPUTJZVGBRUTSDZVIVGTBEUAZ
      PUTSUBUCUDVJVHVKUTSUEUCUFAUTUTVKVKUGUHUSUSUSMZBBJZMVAVEKLZVDVFUIZUSVLVMUS
      VLUSUSUJUKBULUFBBUMVESDVNVOOUTGUNVAVEVCSUOUPUQUR $.
      $( [23-Oct-2004] $) $( [23-Oct-2004] $)


