$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Countability of integers and rationals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( The nonnegative integers are equinumerous to the natural numbers. $)
    nn0ennn $p |- NN0 ~~ NN $=
      ( vx vy cn0 cn cv c1 caddc co cmin nn0ex nn0p1nnt nnm1nn0t wcel wceq wb
      cc wa ax1cn subaddt mp3an2 eqcom 3bitr4g axaddcom mpan eqeq2d adantl
      bitrd nncnt nn0cnt syl2an ancoms en3 ) ABCDAEZFGHZBEZFIHZJUMKUOLUODMZUMCM
      ZUMUPNZUOUNNZOZUOPMZUMPMZVAUQURVBVCQZUSUOFUMGHZNZUTVDUPUMNZVEUONZUSVFVBFP
      MZVCVGVHORUOFUMSTUMUPUAUOVEUAUBVCVFUTOVBVCVEUNUOVIVCVEUNNRFUMUCUDUEUFUGUO
      UHUMUIUJUKUL $.
      $( [2-Oct-2004] $) $( [19-Jul-2004] $)

    $( The set of natural numbers (as a subset of complex numbers) is
       equinumerous to omega (the set of finite ordinal numbers). $)
    nnenom $p |- NN ~~ om $=
      ( vy vx vz com cn0 cn nnex cv c1 caddc co wceq copab cc0 crdg cres wf1o
      cen wbr cle cz crab 0z eqid om2uzf1o wb nn0zrab f1oeq3 ax-mp mpbir omex
      f1oen nn0ennn entr2 ) DEFGDEAHBHIJKLBAMNODPZQZDERSUPDNCHTSCUAUBZUOQZBACNU
      OUCUOUDUEEUQLUPURUFCUGEUQDUOUHUIUJDEUOUKULUIUMUN $.
      $( [2-Oct-2004] $) $( [31-Jul-2004] $)
  $}

  ${
    $d x y z w v u $.
    $( The cross product of the set of natural numbers with itself is
       equinumerous to the set of natural numbers.  The key idea is to
       use ~ nn0opth2t to show that the mapping from natural numbers ` z `
       and ` w ` to ` ( ( z + w ) ^ 2 ) + w ` is one-to-one. $)
    xpnnen $p |- ( NN X. NN ) ~~ NN $=
      ( vx vy vz vw vv vu cn cxp cdom wbr cen cvv wcel nnex xpex cv cint csn
      crn cuni caddc co c2 cexp cop wceq wa elxp5 nnaddclt cn0 2nn0 nnexpclt
      mpan2 syl pm3.27 sylanc adantl sylbi wrex wb wi inteq inteqd visset
      op1stb syl6eq sneq rneqd unieqd op2nda opreq12d opreq1d eqeqan12d
      nn0opth2t nnnn0t anim12i syl2an sylan9bbr eqeq12 opth syl6bb bitr4d
      exp43 com23 r19.23aivv r19.23advv imp elxp2 syl2anb dom2 ax-mp c1 1nn
      elisseti xpsnen ensymi wss ssid snssi ssxp mp2an ssdom2g mp2 endomtr
      sbth ) GGHZGIJZGXFIJZXFGKJXFLMZXGGGNNOZABXFGAPZQZQZXKRZSZTZUAUBZUCUDUBZXP
      UAUBZBPZQZQZXTRZSZTZUAUBZUCUDUBZYEUAUBZLXKXFMZXKXMXPUEUFZXMGMZXPGMZUGZUGX
      SGMZXKGGUHYMYNYJXRGMZYLYNYMXRXPUIYMXQGMZYOXMXPUIYPUCUJMYOUKXQUCULUMUNYKYL
      UOUPUQURXKCPZDPZUEZUFZDGUSCGUSZXTEPZFPZUEZUFZFGUSEGUSZXSYHUFZXKXTUFZUTZYI
      XTXFMUUAUUFUUIUUAUUEUUIEFGGYTUUBGMZUUCGMZUGZUUEUUIVAZVACDGGYQGMZYRGMZUGZU
      ULYTUUMUUPUULYTUUEUUIUUPUULUGZYTUUEUGZUGUUGYQUUBUFYRUUCUFUGZUUHUURUUGYQYR
      UAUBZUCUDUBZYRUAUBZUUBUUCUAUBZUCUDUBZUUCUAUBZUFZUUQUUSYTUUEXSUVBYHUVEYTXR
      UVAXPYRUAYTXQUUTUCUDYTXMYQXPYRUAYTXMYSQZQYQYTXLUVGXKYSVBVCYQYRCVDZVEVFYTX
      PYSRZSZTYRYTXOUVJYTXNUVIXKYSVGVHVIYQYRUVHDVDZVJVFZVKVLUVLVKUUEYGUVDYEUUCU
      AUUEYFUVCUCUDUUEYBUUBYEUUCUAUUEYBUUDQZQUUBUUEYAUVMXTUUDVBVCUUBUUCEVDZVEVF
      UUEYEUUDRZSZTUUCUUEYDUVPUUEYCUVOXTUUDVGVHVIUUBUUCUVNFVDZVJVFZVKVLUVRVKVMY
      QUJMZYRUJMZUGUUBUJMZUUCUJMZUGUVFUUSUTUUPUULYQYRUUBUUCVNUUNUVSUUOUVTYQVOYR
      VOVPUUJUWAUUKUWBUUBVOUUCVOVPVQVRUURUUHUUSUTUUQUURUUHYSUUDUFUUSXKYSXTUUDVS
      YQYRUUBUUCUVHUVKUVQVTWAUQWBWCWDWEWFWGCDXKGGWHEFXTGGWHWIWJWKGGWLRZHZKJUWDX
      FIJZXHUWDGNGWLNWLGWMWNWOWPXIUWDXFWQZUWEXJGGWQUWCGWQZUWFGWRWLGMUWGWMWLGWSW
      KGGUWCGWTXAUWDXFLXBXCGUWDXFXDXAXFGXEXA $.
      $( [2-Oct-2004] $) $( [1-Aug-2004] $)
  $}

  $( The cross product of omega (the set of ordinal natural numbers) with
     itself is equinumerous to omega.  Exercise 1 of [Enderton] p. 133 (which
     proves this with a direct, but longer, proof; ours uses instead the
     Schroeder-Bernstein Theorem ~ sbth in ~ xpnnen ). $)
  xpomen $p |- ( om X. om ) ~~ om $=
    ( com cxp cn omex xpex cen wbr nnenom nnex xpen mp2an xpnnen entr3 entr )
    AABZCACCBZOCAADDECAFGZQPOFGHHCACAIDIDJKLMHN $.
    $( [2-Oct-2004] $) $( [23-Jul-2004] $)

  $( Lemma for ~ znnen . $)
  znnenlem $p |- ( ( ( 0 <_ x /\ -. 0 <_ y ) /\ ( x e. ZZ /\ y e. ZZ ) ) ->
                  ( x = y <-> ( 2 x. x ) = ( ( -u 2 x. y ) + 1 ) ) ) $=
    ( cc0 cv cle wbr wn wa cz wcel c2 cmul co cneg c1 caddc wne wb pm5.1 cr wi
    clt ltnet 0re ltnlet mpan2 adantr anbi1d ltletrt mp3an2 sylbird ancomsd
    3impia syl3dan3 3com12 3expia zret syl2an impcom zneo znegclt sylan2 wceq
    cc zcnt 2cn mulneg12t mpan syl adantl opreq1d neeq2d mpbird sylanc
    necon4bid ) CADZEFZCBDZEFGZHZVPIJZVRIJZHZHZVPVRKVPLMZKNVRLMZOPMZVPVRQZWEWGQ
    ZWHWIRWDWHWISWCVTWHVPTJZVRTJZVTWHUAWAWBWJWKVTWHWKWJVTWHWKWJVRVPUBFZWHVTVRVP
    UCWKWJVTWLWKWJHZVSVQWLWMVSVQHVRCUBFZVQHZWLWMWNVSVQWKWNVSRZWJWKCTJZWPUDVRCUE
    UFUGUHWKWQWJWOWLUAUDVRCVPUIUJUKULUMUNUOUPVPUQVRUQURUSWCWIVTWCWIWEKVRNZLMZOP
    MZQZWAWRIJXAWBVPWRUTVRVAVBWCWGWTWEWCWFWSOPWBWFWSVCZWAWBVRVDJZXBVRVEKVDJXCXB
    VFKVRVGVHVIVJVKVLVMVJVNVO $.
    $( [11-Jan-2008] $) $( [31-Jul-2004] $)

  $( Lemma for ~ znnen . $)
  znnenlemOLD $p |- ( ( ( 0 <_ x /\ -. 0 <_ y ) /\ ( x e. ZZ /\ y e. ZZ ) ) ->
                  ( y = x <-> ( 2 x. x ) = ( ( -u 2 x. y ) + 1 ) ) ) $=
    ( cv wceq wn c2 cmul co cneg c1 caddc wb cc0 cle wbr wa cz wcel pm5.21 cr
    wi clt 0re ltnlet mpan2 adantr ltletrt mp3an2 exp3a sylbird com23 imp3a
    ltnetOLD syld ancoms zret syl2an impcom zneoOLD znegclt sylan2 cc zcnt 2cn
    mulneg12t mpan syl adantl opreq1d eqeq2d mtbird sylanc ) BCZACZDZEZFVNGHZFI
    VMGHZJKHZDZEZVOVTLMVNNOZMVMNOEZPZVNQRZVMQRZPZPVOVTSWGWDVPVNTRZVMTRZWDVPUAZW
    EWFWIWHWJWIWHPZWDVMVNUBOZVPWKWBWCWLWKWCWBWLWKWCVMMUBOZWBWLUAWIWMWCLZWHWIMTR
    ZWNUCVMMUDUEUFWKWMWBWLWIWOWHWMWBPWLUAUCVMMVNUGUHUIUJUKULVMVNUMUNUOVNUPVMUPU
    QURWGWAWDWGVTVQFVMIZGHZJKHZDZWEWPQRWSEWFVNWPUSVMUTVAWGVSWRVQWGVRWQJKWFVRWQD
    ZWEWFVMVBRZWTVMVCFVBRXAWTVDFVMVEVFVGVHVIVJVKVHVL $.
    $( [19-Oct-2004] $) $( [31-Jul-2004] $)

  ${
    $d x y $.
    $( The set of integers and the set of natural numbers are equinumerous.
       Exercise 1 of [Gleason] p. 140. $)
    znnen $p |- ZZ ~~ NN $=
      ( vx vy cz cn0 cn cdom wbr cen cvv wcel zex cc0 cv cle c2 cmul co cneg
      c1 caddc cif wa elnn0z 2nn0 nn0mulclt mpan sylbir wb iftrue eleq1d
      adantl mpbird wn 2z znegclt ax-mp zmulclt adantr cr wi zret clt renegclt
      2pos 2re axmulgt0 mpani syl 0re ltnlet mpan2 lt0neg1t bitr3d cc wceq
      recnt 2cn mulneg12t breq2d 3imtr4d renegcl axmulrcl ltlet syld imp jca
      biimpr peano2nn0 3syl iffalse pm2.61dan eqeqan12d 2ne0 mulcant2 mp3an1
      zcnt syl2an sylan9bb bicomd ex znnenlem bitr4d znnenlemOLD ancom syl2anb
      eqcom syl6bbr ax1cn addcan2t mp3an3 negcl axmulcl negn0 bitr2d sylan9bbr
      4cases dom2 wss nn0ex nn0ssz ssdomg mp2 sbth mp2an nn0ennn entr ) CDECDFG
      ZDCFGZCDHGCIJYGKABCDLAMZNGZOYIPQZORZYIPQZSTQZUAZLBMZNGZOYPPQZYLYPPQZSTQZU
      AZIYICJZYJYODJZUUBYJUBZUUCYKDJZUUDYIDJZUUEYIUCODJUUFUUEUDOYIUEUFUGYJUUCUU
      EUHUUBYJYOYKDYJYKYNUIZUJUKULUUBYJUMZUBZUUCYNDJZUUIYMCJZLYMNGZUBZYMDJZUUJU
      UIUUKUULUUBUUKUUHYLCJZUUBUUKOCJUUOUNOUOUPYLYIUQUFURUUBUUHUULUUBYIUSJZUUHU
      ULUTYIVAUUPUUHLYMVBGZUULUUPLYIRZVBGZLOUURPQZVBGZUUHUUQUUPUURUSJZUUSUVAUTY
      IVCUVBLOVBGZUUSUVAVDOUSJUVBUVCUUSUBUVAUTVEOUURVFUFVGVHUUPYILVBGZUUHUUSUUP
      LUSJZUVDUUHUHVIYILVJVKYIVLVMUUPYMUUTLVBUUPYIVNJZYMUUTVOZYIVPOVNJZUVFUVGVQ
      OYIVRUFVHVSVTUUPYMUSJZUUQUULUTZYLUSJUUPUVIOVEWAYLYIWBUFUVEUVIUVJVILYMWCUF
      VHWDVHWEWFUUNUUMYMUCWGYMWHWIUUHUUCUUJUHUUBUUHYOYNDYJYKYNWJZUJUKULWKUUBYPC
      JZUBZYIYPVOZYOUUAVOZYJYQUVMUVNUVOUHZUTYJYQUBZUVMUVPUVQUVMUBUVOUVNUVQUVOYK
      YRVOZUVMUVNYJYQYOYKUUAYRUUGYQYRYTUIZWLUVFYPVNJZUVRUVNUHZUUBUVLUVHUVFUVTUW
      AVQOYIYPWMWNWOYIWPZYPWPZWQWRWSWTYJYQUMZUBZUVMUVPUWEUVMUBUVNYKYTVOZUVOABXA
      UWEUVOUWFUHUVMYJUWDYOYKUUAYTUUGYQYRYTWJZWLURXBWTUUHYQUBZUVMUVPUWHUVMUBZUV
      NYNYRVOZUVOUWIUVNYRYNVOZUWJYQUUHUBUVLUUBUBUVNUWKUHUWHUVMBAXCUUHYQXDUUBUVL
      XDXEYNYRXFXGUWHUVOUWJUHUVMUUHYQYOYNUUAYRUVKUVSWLURXBWTUUHUWDUBZUVMUVPUVMU
      VNYNYTVOZUWLUVOUVFUVTUVNUWMUHUUBUVLUVFUVTUBUWMYMYSVOZUVNYMVNJZYSVNJZUWMUW
      NUHZUVFUVTUWOUWPSVNJUWQXHYMYSSXIXJYLVNJZUVFUWOOVQXKZYLYIXLUFUWRUVTUWPUWSY
      LYPXLUFWQUWRUVFUVTUWNUVNUHUWSYLYIYPOVQWMXMWNWOXNUWBUWCWQUWLUVOUWMUUHUWDYO
      YNUUAYTUVKUWGWLWSXOWTXPWSXQUPDIJDCXRYHXSXTDCIYAYBCDYCYDYEYF $.
      $( [19-Oct-2004] $) $( [31-Jul-2004] $)
  $}

  ${
    $d x y z w $.
    $( The rational numbers are countable.  (This unusual proof uses the Axiom
       of Choice via ~ fodom to make it much shorter, but this theorem can also
       be proved without it.  See, for example, Exercise 2 of [Enderton]
       p. 133.) $)
    qnnen $p |- QQ ~~ NN $=
      ( vx vy vz vw cq cn cdom wbr cen cz cxp cv wcel wa cdiv co wceq copab2
      wfo wfn crn df-fo oprex eqid fnoprab2 wrex elrnoprab elq bitr4 eqriv
      mpbir2an zex nnex xpex fodom ax-mp znnen enref xpen mp2an xpnnen entr
      domentr cvv wss nnssq ssdomg mp2 sbth ) EFGHZFEGHZEFIHEJFKZGHZVLFIHVJVLEA
      LZJMBLZFMNCLVNVOOPZQNABCRZSZVMVRVQVLTVQUAZEQVLEVQUBABCJFVPVQVNVOOUCZVQUDZ
      UEDVSEDLZVSMWBVPQBFUFAJUFWBEMABCJFVPWBVQVTWAUGABWBUHUIUJUKVLEVQJFULUMUNUO
      UPVLFFKZFJFIHFFIHVLWCIHUQFUMURJFFFULUMUMUMUSUTVAVBEVLFVCUTFVDMFEVEVKUMVFF
      EVDVGVHEFVIUT $.
      $( [20-Oct-2004] $) $( [31-Jul-2004] $)
  $}

