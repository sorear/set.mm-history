$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Countability of integers and rationals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u $.
    $( The cross product of the set of natural numbers with itself is
       equinumerous to the set of natural numbers.  (Revised by Mario Carneiro,
       9-Mar-2013.) $)
    xpnnen $p |- ( NN X. NN ) ~~ NN $=
      ( cn cxp com cen wbr nnenom omex xpen mp2an xpomen entr4i entri ) AABZCCB
      ZAACDEZOMNDEFFACACGGHINCAGJFKL $.
      $( [9-Mar-2013] $) $( [1-Aug-2004] $)
  $}

  ${
    $d x y z w v u $.
    $( The cross product of the set of natural numbers with itself is
       equinumerous to the set of natural numbers.  The key idea is to use
       ~ nn0opth2 to show that the mapping from natural numbers ` z ` and ` w `
       to ` ( ( z + w ) ^ 2 ) + w ` is one-to-one. $)
    xpnnenOLD $p |- ( NN X. NN ) ~~ NN $=
      ( vx vy vz vw vv vu cn cxp cdom wbr cen cvv wcel nnex xpex cv cint csn
      crn cuni caddc co c2 cexp cop wceq wa elxp5 cn0 nnaddcl 2nn0 nnexpcl
      sylancl sylancom adantl sylbi wrex wb elxp2 wi inteq inteqd visset
      op1stb syl6eq sneq rneqd unieqd op2nda opreq12d opreq1d eqeqan12d nnnn0
      anim12i nn0opth2 syl2an sylan9bbr eqeq12 opth syl6bb bitr4d exp43 com23
      r19.23aivv r19.23advv imp syl2anb dom2 ax-mp c1 1nn elisseti xpsnen
      ensymi wss ssid snssi xpss12 mp2an ssdom2g mp2 endomtr sbth ) GGHZGIJZGXD
      IJZXDGKJXDLMZXEGGNNOZABXDGAPZQZQZXIRZSZTZUAUBZUCUDUBZXNUAUBZBPZQZQZXRRZSZ
      TZUAUBZUCUDUBZYCUAUBZLXIXDMZXIXKXNUEUFZXKGMZXNGMZUGZUGXQGMZXIGGUHYKYLYHYI
      YJXPGMZYLYKXOGMUCUIMYMXKXNUJUKXOUCULUMXPXNUJUNUOUPYGXICPZDPZUEZUFZDGUQCGU
      QZXREPZFPZUEZUFZFGUQEGUQZXQYFUFZXIXRUFZURZXRXDMCDXIGGUSEFXRGGUSYRUUCUUFYR
      UUBUUFEFGGYQYSGMZYTGMZUGZUUBUUFUTZUTCDGGYNGMZYOGMZUGZUUIYQUUJUUMUUIYQUUBU
      UFUUMUUIUGZYQUUBUGZUGUUDYNYSUFYOYTUFUGZUUEUUOUUDYNYOUAUBZUCUDUBZYOUAUBZYS
      YTUAUBZUCUDUBZYTUAUBZUFZUUNUUPYQUUBXQUUSYFUVBYQXPUURXNYOUAYQXOUUQUCUDYQXK
      YNXNYOUAYQXKYPQZQYNYQXJUVDXIYPVAVBYNYOCVCZVDVEYQXNYPRZSZTYOYQXMUVGYQXLUVF
      XIYPVFVGVHYNYOUVEDVCZVIVEZVJVKUVIVJUUBYEUVAYCYTUAUUBYDUUTUCUDUUBXTYSYCYTU
      AUUBXTUUAQZQYSUUBXSUVJXRUUAVAVBYSYTEVCZVDVEUUBYCUUARZSZTYTUUBYBUVMUUBYAUV
      LXRUUAVFVGVHYSYTUVKFVCZVIVEZVJVKUVOVJVLUUMYNUIMZYOUIMZUGYSUIMZYTUIMZUGUVC
      UUPURUUIUUKUVPUULUVQYNVMYOVMVNUUGUVRUUHUVSYSVMYTVMVNYNYOYSYTVOVPVQUUOUUEU
      UPURUUNUUOUUEYPUUAUFUUPXIYPXRUUAVRYNYOYSYTUVEUVHUVNVSVTUOWAWBWCWDWEWFWGWH
      WIGGWJRZHZKJUWAXDIJZXFUWAGNGWJNWJGWKWLWMWNXGUWAXDWOZUWBXHGGWOUVTGWOZUWCGW
      PWJGMUWDWKWJGWQWIGGUVTGWRWSUWAXDLWTXAGUWAXDXBWSXDGXCWS $.
      $( [1-Aug-2004] $)
  $}

  $( The cross product of omega (the set of ordinal natural numbers) with
     itself is equinumerous to omega.  Exercise 1 of [Enderton] p. 133 (which
     proves this with a direct, but longer, proof; ours uses instead the
     Schroeder-Bernstein Theorem ~ sbth in ~ xpnnen ). $)
  xpomenOLD $p |- ( om X. om ) ~~ om $=
    ( com cxp cn omex xpex cen wbr nnenom xpen mp2an xpnnen entr3i entri ) AABZ
    CACCBZNCAADDECAFGZPONFGHHCACADDIJKLHM $.
    $( [23-Jul-2004] $)

  $( Lemma for ~ znnen . $)
  znnenlem $p |- ( ( ( 0 <_ x /\ -. 0 <_ y ) /\ ( x e. ZZ /\ y e. ZZ ) ) ->
                  ( x = y <-> ( 2 x. x ) = ( ( -u 2 x. y ) + 1 ) ) ) $=
    ( cc0 cv cle wbr wn wa cz wcel c2 cmul co cneg c1 caddc wne cr wi zre clt
    wb 0re ltnle mpan2 adantr anbi1d ltletr mp3an2 sylbird ancomsd 3impia ltne
    syld3an3 3com12 3expia syl2an impcom znegcl zneo sylan2 wceq cc 2cn zcn
    mulneg12 sylancr adantl opreq1d neeqtrrd 2thd necon4bid ) CADZEFZCBDZEFGZHZ
    VMIJZVOIJZHZHZVMVOKVMLMZKNVOLMZOPMZWAVMVOQZWBWDQZVTVQWEVRVMRJZVORJZVQWESVSV
    MTVOTWGWHVQWEWHWGVQWEWHWGVQVOVMUAFZWEWHWGVQWIWHWGHZVPVNWIWJVPVNHVOCUAFZVNHZ
    WIWJWKVPVNWHWKVPUBZWGWHCRJZWMUCVOCUDUEUFUGWHWNWGWLWISUCVOCVMUHUIUJUKULVOVMU
    MUNUOUPUQURVTWFVQVTWBKVONZLMZOPMZWDVSVRWOIJWBWQQVOUSVMWOUTVAVTWCWPOPVSWCWPV
    BZVRVSKVCJVOVCJWRVDVOVEKVOVFVGVHVIVJVHVKVL $.
    $( [11-Jan-2008] $) $( [31-Jul-2004] $)

  ${
    $d x y $.
    $( The set of integers and the set of natural numbers are equinumerous.
       Exercise 1 of [Gleason] p. 140. $)
    znnen $p |- ZZ ~~ NN $=
      ( vx vy cz cn0 cn cdom wbr cen cvv wcel zex cc0 cv cle c2 cmul co cneg
      c1 caddc cif wa elnn0z 2nn0 nn0mulcl mpan sylbir wb iftrue eleq1d adantl
      mpbird wn 2z znegcl ax-mp zmulcl adantr cr wi zre clt renegcl 2pos 2re
      axmulgt0 mpani syl 0re ltnle mpan2 lt0neg1 bitr3d cc wceq 2cn recn
      mulneg12 sylancr breq2d 3imtr4d renegcli remulcl ltle syld imp jca
      biimpri peano2nn0 3syl iffalse pm2.61dan eqeqan12d zcn 2ne0 mulcant2i
      mp3an3 syl2an sylan9bb bicomd ex znnenlem bitr4d equcom eqcom 3bitr4g
      ancom2s ancom1s negcli mulcl ax-1cn addcan2 negne0i bitr2d sylan9bbr
      4cases dom2 wss nn0ex nn0ssz ssdomg mp2 sbth mp2an nn0ennn entri ) CDECDF
      GZDCFGZCDHGCIJYGKABCDLAMZNGZOYIPQZORZYIPQZSTQZUAZLBMZNGZOYPPQZYLYPPQZSTQZ
      UAZIYICJZYJYODJZUUBYJUBZUUCYKDJZUUDYIDJZUUEYIUCODJUUFUUEUDOYIUEUFUGYJUUCU
      UEUHUUBYJYOYKDYJYKYNUIZUJUKULUUBYJUMZUBZUUCYNDJZUUIYMCJZLYMNGZUBZYMDJZUUJ
      UUIUUKUULUUBUUKUUHYLCJZUUBUUKOCJUUOUNOUOUPYLYIUQUFURUUBUUHUULUUBYIUSJZUUH
      UULUTYIVAUUPUUHLYMVBGZUULUUPLYIRZVBGZLOUURPQZVBGZUUHUUQUUPUURUSJZUUSUVAUT
      YIVCUVBLOVBGZUUSUVAVDOUSJUVBUVCUUSUBUVAUTVEOUURVFUFVGVHUUPYILVBGZUUHUUSUU
      PLUSJZUVDUUHUHVIYILVJVKYIVLVMUUPYMUUTLVBUUPOVNJZYIVNJZYMUUTVOVPYIVQOYIVRV
      SVTWAUUPUVEYMUSJZUUQUULUTVIYLUSJUUPUVHOVEWBYLYIWCUFLYMWDVSWEVHWFWGUUNUUMY
      MUCWHYMWIWJUUHUUCUUJUHUUBUUHYOYNDYJYKYNWKZUJUKULWLUUBYPCJZUBZYIYPVOZYOUUA
      VOZYJYQUVKUVLUVMUHZUTYJYQUBZUVKUVNUVOUVKUBUVMUVLUVOUVMYKYRVOZUVKUVLYJYQYO
      YKUUAYRUUGYQYRYTUIZWMUUBUVGYPVNJZUVPUVLUHZUVJYIWNZYPWNZUVGUVRUVFUVSVPYIYP
      OWOWPWQWRWSWTXAYJYQUMZUBZUVKUVNUWCUVKUBUVLYKYTVOZUVMABXBUWCUVMUWDUHUVKYJU
      WBYOYKUUAYTUUGYQYRYTWKZWMURXCXAUUHYQUBZUVKUVNUWFUVKUBUVLYNYRVOZUVMYQUUHUV
      KUVLUWGUHZYQUUHUBZUVJUUBUWHUWIUVJUUBUBUBYPYIVOYRYNVOUVLUWGBAXBABXDYNYRXEX
      FXGXHUWFUVMUWGUHUVKUUHYQYOYNUUAYRUVIUVQWMURXCXAUUHUWBUBZUVKUVNUVKUVLYNYTV
      OZUWJUVMUUBUVGUVRUVLUWKUHUVJUVTUWAUVGUVRUBUWKYMYSVOZUVLUVGYMVNJZYSVNJZUWK
      UWLUHZUVRYLVNJZUVGUWMOVPXIZYLYIXJUFUWPUVRUWNUWQYLYPXJUFUWMUWNSVNJUWOXKYMY
      SSXLWQWRUVGUVRUWPUWLUVLUHUWQYIYPYLOVPWOXMWPWQXNWRUWJUVMUWKUUHUWBYOYNUUAYT
      UVIUWEWMWTXOXAXPWTXQUPDIJDCXRYHXSXTDCIYAYBCDYCYDYEYF $.
      $( [31-Jul-2004] $)
  $}

  ${
    $d w x y z $.  $d q r $.  $d a x y $.  $d b x y $.
    $( The rational numbers are countable.  This proof does not use the Axiom
       of Choice, even though it uses an onto function, because the base set
       ` ( ZZ X. NN ) ` is numerable.  Exercise 2 of [Enderton] p. 133.
       (Revised by Mario Carneiro, 3-Mar-2013.) $)
    qnnen $p |- QQ ~~ NN $=
      ( vq vx vy vz vw cq cn cdom wbr cen cz cxp cv con0 wrex wcel wa cdiv co
      wceq copab2 wfo com omelon omex znnen nnenom entr2i breq1 rcla4ev mp2an
      ensymi xpnum wfn crn df-fo oprex eqid fnoprab2 elrnoprab elq bitr4i
      eqriv mpbir2an zex nnex xpex fodomnum mp2 enref xpen xpnnen entri
      domentr cvv wss nnssq ssdomg sbth ) FGHIZGFHIZFGJIFKGLZHIZWBGJIVTAMZWBJIA
      NOZWBFBMZKPCMZGPQDMWFWGRSZTQBCDUAZUBZWCWDKJIZANOZWDGJIZANOZWEUCNPZUCKJIZW
      LUDKGUCUEUFUGUHWKWPAUCNWDUCKJUIUJUKWOUCGJIZWNUDGUCUEUGULWMWQAUCNWDUCGJUIU
      JUKAKGUMUKWJWIWBUNWIUOZFTWBFWIUPBCDKGWHWIWFWGRUQZWIURZUSEWRFEMZWRPXAWHTCG
      OBKOXAFPBCDKGWHXAWIWSWTUTBCXAVAVBVCVDAWBFWIKGVEVFVGVHVIWBGGLZGKGJIGGJIWBX
      BJIUFGVFVJKGGGVFVFVKUKVLVMFWBGVNUKGVOPGFVPWAVFVQGFVOVRVIFGVSUK $.
      $( [3-Mar-2013] $) $( [31-Jul-2004] $)
  $}

  ${
    $d x y z w $.
    $( The rational numbers are countable.  (This unusual proof uses the Axiom
       of Choice via ~ fodom to make it much shorter, but this theorem can also
       be proved without it.  See, for example, Exercise 2 of [Enderton]
       p. 133.) $)
    qnnenOLD $p |- QQ ~~ NN $=
      ( vx vy vz vw cq cn cdom wbr cen cz cxp cv wcel wa cdiv co wceq copab2
      wfo wfn crn df-fo oprex eqid fnoprab2 wrex elrnoprab elq bitr4i eqriv
      mpbir2an zex nnex xpex fodom ax-mp znnen enref xpen mp2an xpnnen entri
      domentr cvv wss nnssq ssdomg mp2 sbth ) EFGHZFEGHZEFIHEJFKZGHZVLFIHVJVLEA
      LZJMBLZFMNCLVNVOOPZQNABCRZSZVMVRVQVLTVQUAZEQVLEVQUBABCJFVPVQVNVOOUCZVQUDZ
      UEDVSEDLZVSMWBVPQBFUFAJUFWBEMABCJFVPWBVQVTWAUGABWBUHUIUJUKVLEVQJFULUMUNUO
      UPVLFFKZFJFIHFFIHVLWCIHUQFUMURJFFFUMUMUSUTVAVBEVLFVCUTFVDMFEVEVKUMVFFEVDV
      GVHEFVIUT $.
      $( [31-Jul-2004] $)
  $}

