$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Examples of topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c d x y z J $.  $d a b c d x y z Y $.
    $( A subspace topology is a topology.  Definition of subspace topology in
       [Munkres] p. 89. ` Y ` is normally a subset of the base set of ` J ` .
       (Contributed by FL, 15-Apr-2007.) $)
    subtop $p |- ( J e. Top -> { x | E. y e. J x = ( y i^i Y ) } e. Top ) $=
      ( va vb vz vc vd ctop wcel cv cin wceq wrex wss cuni wi wa eqeq2d cab wal
      wral cvv wb abrexexg istopg syl wex copab rnopab2 eqcomi sseq2i cima wfun
      crn funopabeq ssimaexg mp3an2 ex df-ima resopab rneqi eqtri 3eqtri eqeq2i
      cres anbi2i exbii 3imtr3g syl5bi imp unieq ciun dfiun2g eqcomd mprg incom
      inex1g a1i iuneq2i iunin2 uniiun ineq2i 3eqtr2i syl6eq anim2i eximi snssi
      csn snex sseq1 unisn ineq1d anbi12d cla4ev sylan rexlimiva uniopn rcla4ev
      vex ineq1 expimpd exlimdv impbid2 adantr mpbird uniex rexbidv elab sylibr
      eqeq1 alrimiv clelab ceqsexv cbvrexv 3bitri ineq12 inindir syl6eqr ancoms
      inopn 3expa syl2an exp43 com24 rexlimdva syl7bi com23 ralrimiva mpbir2and
      rexlimdv inex1 ) CJKZALZBLZDMZNZBCOZAUAZJKZELZYTPZUUBQZYTKZRZEUBZUUBFLZMZ
      YTKZFYTUCZEYTUCZYNYTUDKUUAUUGUULSUEBACYQJUFEFUDYTUGUHYNUUFEYNUUCUUEYNUUCS
      ZUUDYQNZBCOZUUEUUMUUOGLZCPZUUDUUPQZDMZNZSZGUIZUUMUUQUUBYRBUUPOAUAZNZSZGUI
      ZUVBYNUUCUVFUUCUUBYPCKZYRSBAUJZUPZPZYNUVFYTUVIUUBUVIYTBACYQUKULUMYNUUBYRB
      AUJZCUNZPZUUQUUBUVKUUPUNZNZSZGUIZUVJUVFYNUVMUVQYNUVKUOUVMUVQBAYQUQGCUUBJU
      VKURUSUTUVLUVIUUBUVLUVKCVGZUPUVIUVKCVAUVRUVHYRBACVBVCVDUMUVPUVEGUVOUVDUUQ
      UVNUVCUUBUVNUVKUUPVGZUPYPUUPKZYRSBAUJZUPUVCUVKUUPVAUVSUWAYRBAUUPVBVCBAUUP
      YQUKVEVFVHVIVJVKVLUVEUVAGUVDUUTUUQUVDUUDUVCQZUUSUUBUVCVMUWBBUUPYQVNZBUUPD
      YPMZVNZUUSYQUDKZUWBUWCNBUUPUWFBUUPUCUWCUWBBAUUPYQUDVOVPYPDUUPVSVQBUUPYQUW
      DYQUWDNUVTYPDVRVTWAUWEDBUUPYPVNZMDUURMUUSBUUPDYPWBUURUWGDBUUPWCWDDUURVRWE
      VEWFWGWHUHYNUUOUVBUEUUCYNUUOUVBUUNUVBBCUVGYPWJZCPZUUNUVBYPCWIUVAUWIUUNSGU
      WHYPWKUUPUWHNZUUQUWIUUTUUNUUPUWHCWLUWJUUSYQUUDUWJUURYPDUWJUURUWHQYPUUPUWH
      VMYPBXAWMWFWNTWOWPWQWRYNUVAUUOGYNUUQUUTUUOYNUUQSUURCKZUUTUUORUUPCWSUWKUUT
      UUOUUNUUTBUURCYPUURNYQUUSUUDYPUURDXBTWTUTUHXCXDXEXFXGYSUUOAUUDUUBEXAZXHYO
      UUDNYRUUNBCYOUUDYQXLXIXJXKUTXMYNUUKEYTYNUUBYTKZSZUUJFYTUWNUUHYTKZSUUIYQNZ
      BCOZUUJUWNUWOUWQUWOUUHYQNZBCOZUWNUWQYSUWSAUUHFXAYOUUHNYRUWRBCYOUUHYQXLXIX
      JYNUWMUWSUWQRYNUWSUWMUWQUWMUUBHLZDMZNZHCOZYNUWSUWQUWMYOUUBNZYSSAUIUUBYQNZ
      BCOZUXCYSAUUBXNYSUXFAUUBUWLUXDYRUXEBCYOUUBYQXLXIXOUXEUXBBHCYPUWTNYQUXAUUB
      YPUWTDXBTXPXQUWSUUHILZDMZNZICOYNUXCUWQRZUWRUXIBICYPUXGNYQUXHUUHYPUXGDXBTX
      PYNUXIUXJICYNUXCUXIUXGCKZUWQYNUXBUXIUXKUWQRRHCYNUWTCKZSZUXKUXIUXBUWQUXMUX
      KUXIUXBUWQUXMUXKSUWTUXGMZCKZUUIUXNDMZNZUWQUXIUXBSYNUXLUXKUXOUWTUXGCYBYCUX
      BUXIUXQUXBUXISUUIUXAUXHMUXPUUBUXAUUHUXHXRUWTUXGDXSXTYAUWPUXQBUXNCYPUXNNYQ
      UXPUUIYPUXNDXBTWTYDYEYFYGYFYLVKYHYIVLVKVLYSUWQAUUIUUBUUHUWLYMYOUUINYRUWPB
      CYOUUIYQXLXIXJXKYJYJYK $.
      $( [8-Apr-2007] $)
  $}

  ${
    $d x y $.
    $( The singleton of the empty set is a topology.  (Contributed by Stefan
       Allan, 3-Mar-2006.) $)
    sn0top $p |- { (/) } e. Top $=
      ( vx vy c0 csn ctop wcel cv wss cuni wi wal cin wral cvv wceq unieq elsnc
      0ex syl sylbi wa wb p0ex istopg ax-mp wo sssn elsnc2 mpbir syl6eqel unisn
      uni0 eqtr mpan2 vex uniex sylibr jaoi ax-gen elsn ineq2 in0 eqeq2i biimpi
      inex1 biimpri adantl rgen2a mpbir2an ) CDZEFZAGZVJHZVLIZVJFZJZAKZVLBGZLZV
      JFZBVJMAVJMZVJNFVKVQWAUAUBUCABNVJUDUEVPAVMVLCOZVLVJOZUFVOVLCUGWBVOWCWBVNC
      IZVJVLCPWDVJFWDCOULWDCRUHUIUJWCVNVJIZOZVOVLVJPWFVNCOZVOWFWECOWGCRUKVNWECU
      MUNVNCVLAUOZUPQUQSURTUSVTABVJVRVJFZVTVLVJFWIVRCOZVTBCUTWJVSCOZVTWJVSVLCLZ
      OZWKVRCVLVAWMWKWLCVSVLVBVCVDSVTWKVSCVLVRWHVEQVFSTVGVHVI $.
      $( [3-Mar-2006] $)
  $}

  ${
    $d x y A $.
    $( The indiscrete topology on a set ` A ` .  Part of Example 2 in [Munkres]
       p. 77.  (Contributed by FL, 16-Jul-2006; hypothesis eliminated by Stefan
       Allan, 6-Nov-2008.) $)
    indistop $p |- { (/) , A } e. Top $=
      ( vx vy cvv wcel c0 cuni wi cin wa a1i wceq wo unieq syl6eqel jaod adantr
      0ex ex syl6eq cpr ctop cv wss wal wral wb prex istopg syl sspr uni0 prid1
      csn eqeltri unisn unisng sylan9eqr prid2g eqeltrd uniprg mpan uncom eqtri
      cun un0 syl5bi alrimiv elpr simpr ineq2d simpl ineq1d incom ineq12 adantl
      vex in0 inidm ccased expdimp ralrimiv mpbir2and wn prprc2 sn0top pm2.61i
      ) ADEZFAUAZUBEZWHWJBUCZWIUDZWKGZWIEZHZBUEZWKCUCZIZWIEZCWIUFZBWIUFZWHWIDEZ
      WJWPXAJUGXBWHFAUHKBCDWIUIUJWHWOBWLWKFLZWKFUNZLZMZWKAUNZLZWKWILZMZMWHWNWKF
      AUKWHXFWNXJWHXCWNXEXCWNHWHXCWMFGZWIWKFNXKFWIULFARUMZUOOKXEWNHWHXEWMXDGZWI
      WKXDNXMFWIFRUPXLUOOKPWHXHWNXIWHXHWNWHXHJWMAWIXHWHWMXGGAWKXGNADUQURWHAWIEZ
      XHFADUSZQUTSWHXIWNWHXIJWMAWIXIWHWMWIGZAWKWINWHXPFAVEZAFDEWHXPXQLRFADDVAVB
      XQAFVEAFAVCAVFVDTURWHXNXIXOQUTSPPVGVHWHWTBWIWKWIEXCWKALZMZWHWTWKFABVQVIWH
      XSWTWHXSJZWSCWIWQWIEWQFLZWQALZMZXTWSWQFACVQVIWHXSYCWSWHXCYAXRYBWSXCYAJZWS
      HWHYDWRFWIYDWRWKFIZFYDWQFWKXCYAVJVKWKVRZTXLOKXRYAJZWSHWHYGWRFWIYGWRYEFYGW
      QFWKXRYAVJVKYFTXLOKXCYBJZWSHWHYHWRFWIYHWRFWQIZFYHWKFWQXCYBVLVMYIWQFIFFWQV
      NWQVRVDTXLOKWHXRYBJZWSWHYJJZWRAWIYKWRAAIZAYJWRYLLWHWKAWQAVOVPAVSTWHXNYJXO
      QUTSVTWAVGWBSVGWBWCWHWDWIXDUBFAWEWFOWG $.
      $( [6-Nov-2008] $)
  $}

  ${
    $d x y z A $.
    indistop.1 $e |- A e. _V $.
    $( The discrete topology on a set ` A ` .  Part of Example 2 in [Munkres]
       p. 77.  (Contributed by FL, 18-Jul-2006.) $)
    distop $p |- ~P A e. Top $=
      ( vx vy cpw ctop wcel cv wss cuni wi wal cin wral cvv wa vex elpw sylbi
      wb pwex istopg ax-mp uniss unipw syl6sseq uniex sylibr ax-gen ssinss1 a1i
      inex2 syl6ibr com12 ralrimiv rgen mpbir2an ) AEZFGZCHZURIZUTJZURGZKZCLZUT
      DHZMZURGZDURNZCURNZUROGUSVEVJPTABUACDOURUBUCVDCVAVBAIVCVAVBURJAUTURUDAUEU
      FVBAUTCQZUGRUHUIVICURUTURGZVHDURVLUTAIZVFURGZVHKUTAVKRVNVMVHVNVFAIZVMVHKV
      FADQZRVOVMVGAIZVHVMVQKVOUTVFAUJUKVGAVFUTVPULRUMSUNSUOUPUQ $.
      $( [17-Jul-2006] $)

    $( The finite complement topology on a set ` A ` .  Example 3 in [Munkres]
       p. 77.  (Contributed by FL, 15-Aug-2006.) $)
    fctop $p |- { x | ( x C_ A /\ ( ( A \ x ) e. Fin \/ x = (/) ) ) }
           e. Top $=
      ( vy vz wss cdif cfn wcel c0 wceq wo wa cin cvv ax-mp sseq1 difeq2 eleq1d
      cuni cv cab ctop wi wal wral wb abssexg istopg uniss crab simpl a1i rabab
      ss2rabi unieqi unimax 3sstr3i sstr sylancl wrex uni0c notbii rexnal ssel2
      wn bitr4i vex eqeq1 orbi12d anbi12d elab sylib simprd ord con1d imp sscon
      elssuni syl ssfi sylan2 expcom ad2antlr mpd rexlimdv syl5bi orrd sylanbrc
      exp31 uniex ax-gen ssinss1 ad2antrr cun difindi unfi syl5eqel ineq1 incom
      orcd in0 eqtri syl6eq olcd ineq2 ccase2 ad2ant2l jca anbi12i inex1 rgen2a
      3imtr4i mpbir2an ) AUAZBFZBXOGZHIZXOJKZLZMZAUBZUCIZDUAZYBFZYDTZYBIZUDZDUE
      ZYDEUAZNZYBIZEYBUFDYBUFZYBOIZYCYIYMMUGBOIZYNCXTABOUHPDEOYBUIPYHDYEYFBFZBY
      FGZHIZYFJKZLZYGYEYFYBTZFUUABFYPYDYBUJYAAOUKZTZXPAOUKZTZUUABUUBUUDFUUCUUEF
      YAXPAOYAXPUDXOOIXPXTULUMUOUUBUUDUJPUUBYBYAAUNUPYOUUEBKCABOUQPURYFUUABUSUT
      YEYRYSYEYSYRYSVFZYJJKZVFZEYDVAZYEYRUUFUUGEYDUFZVFUUIYSUUJEYDVBVCUUGEYDVDV
      GYEUUHYREYDYEYJYDIZUUHYRYEUUKMZUUHMBYJGZHIZYRUULUUHUUNUULUUNUUGUULUUNUUGU
      ULYJBFZUUNUUGLZUULYJYBIZUUOUUPMZYDYBYJVEYAUURAYJEVHXOYJKZXPUUOXTUUPXOYJBQ
      UUSXRUUNXSUUGUUSXQUUMHXOYJBRSXOYJJVIVJVKVLZVMVNVOVPVQUUKUUNYRUDYEUUHUUNUU
      KYRUUKUUNYQUUMFZYRUUKYJYFFUVAYJYDVSYJYFBVRVTUUMYQWAWBWCWDWEWJWFWGVPWHYAYP
      YTMAYFYDDVHZWKXOYFKZXPYPXTYTXOYFBQUVCXRYRXSYSUVCXQYQHXOYFBRSXOYFJVIVJVKVL
      WIWLYLDEYBYDBFZBYDGZHIZYDJKZLZMZUURMZYKBFZBYKGZHIZYKJKZLZMZYDYBIZUUQMYLUV
      JUVKUVOUVDUVKUVHUURYDYJBWMWNUVHUUPUVOUVDUUOUVFUUNUVGUUGUVOUVFUUNMZUVMUVNU
      VRUVLUVEUUMWOHBYDYJWPUVEUUMWQWRXAUVGUVNUVMUVGYKJYJNZJYDJYJWSUVSYJJNJJYJWT
      YJXBXCXDXEUUGUVNUVMUUGYKYDJNJYJJYDXFYDXBXDXEXGXHXIUVQUVIUUQUURYAUVIAYDUVB
      XOYDKZXPUVDXTUVHXOYDBQUVTXRUVFXSUVGUVTXQUVEHXOYDBRSXOYDJVIVJVKVLUUTXJYAUV
      PAYKYDYJUVBXKXOYKKZXPUVKXTUVOXOYKBQUWAXRUVMXSUVNUWAXQUVLHXOYKBRSXOYKJVIVJ
      VKVLXMXLXN $.
      $( [26-Jun-2011] $) $( [15-Aug-2006] $)

    $( The finite complement topology on a set ` A ` .  Example 3 in [Munkres]
       p. 77.  (This version of ~ fctop requires the Axiom of Infinity.)
       (Contributed by FL, 20-Aug-2006.) $)
    fctop2 $p |- { x | ( x C_ A /\ ( ( A \ x ) ~< om \/ x = (/) ) ) } e. Top $=
      ( cv wss cdif cfn wcel c0 wceq wo wa cab com csdm isfinite3 orbi1i anbi2i
      wbr ctop abbii fctop eqeltrri ) ADZBEZBUDFZGHZUDIJZKZLZAMUEUFNOSZUHKZLZAM
      TUJUMAUIULUEUGUKUHUFPQRUAABCUBUC $.
      $( [26-Jun-2011] $) $( [19-Aug-2006] $)


    $( The countable complement topology on a set ` A ` .  Example 4 in
       [Munkres] p. 77.  (Contributed by FL, 29-Aug-2006.) $)
    cctop $p |- { x | ( x C_ A /\ ( ( A \ x ) ~<_ om \/ x = (/) ) ) } e. Top $=
      ( vy vz wss cdif com cdom wbr c0 wceq wo wa wcel cuni cin wral cvv ax-mp
      cv cab ctop wi wal wb abssexg istopg uniss simpl a1i ss2rabi rabab unieqi
      crab unimax 3sstr3i sstr sylancl wn wrex uni0b dfss3 ralbii 3bitri notbii
      csn elsn rexnal bitr4i ssel2 vex sseq1 difeq2 breq1d orbi12d anbi12d elab
      eqeq1 sylib simprd ord con1d imp simpllr elssuni syl sscon difexg ssdom2g
      3syl domtr sylancom mpdan exp31 rexlimdv syl5bi orrd uniex ax-gen ssinss1
      sylanbrc ad2antrr cun difindi unctb syl5eqbr ineq1 incom in0 eqtri syl6eq
      orcd olcd ineq2 ccase2 ad2ant2l jca anbi12i inex1 3imtr4i rgen2a mpbir2an
      ) AUAZBFZBYDGZHIJZYDKLZMZNZAUBZUCOZDUAZYKFZYMPZYKOZUDZDUEZYMEUAZQZYKOZEYK
      RDYKRZYKSOZYLYRUUBNUFBSOZUUCCYIABSUGTDESYKUHTYQDYNYOBFZBYOGZHIJZYOKLZMZYP
      YNYOYKPZFUUJBFUUEYMYKUIYJASUOZPZYEASUOZPZUUJBUUKUUMFUULUUNFYJYEASYJYEUDYD
      SOYEYIUJUKULUUKUUMUITUUKYKYJAUMUNUUDUUNBLCABSUPTUQYOUUJBURUSYNUUGUUHYNUUH
      UUGUUHUTZYSKLZUTZEYMVAZYNUUGUUOUUPEYMRZUTUURUUHUUSUUHYMKVGZFYSUUTOZEYMRUU
      SYMVBEYMUUTVCUVAUUPEYMEKVHVDVEVFUUPEYMVIVJYNUUQUUGEYMYNYSYMOZUUQUUGYNUVBN
      ZUUQNZBYSGZHIJZUUGUVCUUQUVFUVCUVFUUPUVCUVFUUPUVCYSBFZUVFUUPMZUVCYSYKOZUVG
      UVHNZYMYKYSVKYJUVJAYSEVLYDYSLZYEUVGYIUVHYDYSBVMUVKYGUVFYHUUPUVKYFUVEHIYDY
      SBVNVOYDYSKVSVPVQVRZVTWAWBWCWDUVDUVFUUFUVEIJZUUGUVDUVFNZYSYOFZUUFUVEFZUVM
      UVNUVBUVOYNUVBUUQUVFWEYSYMWFWGYSYOBWHUVESOZUVPUVMUDUUDUVQCBYSSWITUUFUVESW
      JTWKUUFUVEHWLWMWNWOWPWQWCWRYJUUEUUINAYOYMDVLZWSYDYOLZYEUUEYIUUIYDYOBVMUVS
      YGUUGYHUUHUVSYFUUFHIYDYOBVNVOYDYOKVSVPVQVRXBWTUUADEYKYMBFZBYMGZHIJZYMKLZM
      ZNZUVJNZYTBFZBYTGZHIJZYTKLZMZNZYMYKOZUVINUUAUWFUWGUWKUVTUWGUWDUVJYMYSBXAX
      CUWDUVHUWKUVTUVGUWBUVFUWCUUPUWKUWBUVFNZUWIUWJUWNUWHUWAUVEXDHIBYMYSXEUWAUV
      EXFXGXMUWCUWJUWIUWCYTKYSQZKYMKYSXHUWOYSKQKKYSXIYSXJXKXLXNUUPUWJUWIUUPYTYM
      KQKYSKYMXOYMXJXLXNXPXQXRUWMUWEUVIUVJYJUWEAYMUVRYDYMLZYEUVTYIUWDYDYMBVMUWP
      YGUWBYHUWCUWPYFUWAHIYDYMBVNVOYDYMKVSVPVQVRUVLXSYJUWLAYTYMYSUVRXTYDYTLZYEU
      WGYIUWKYDYTBVMUWQYGUWIYHUWJUWQYFUWHHIYDYTBVNVOYDYTKVSVPVQVRYAYBYC $.
      $( [23-Aug-2006] $)

    $( The indiscrete topology on a set ` A ` expressed as a topological
       space.  (Contributed by FL, 19-Jul-2006.) $)
    indistpsOLD $p |- <. A , { (/) , A } >. e. TopSpOLD $=
      ( c0 cpr cuni cop ctpsOLD cun 0ex unipr uncom un0 3eqtri opeq1i wcel ctop
      indistop eltopspOLD mpbir eqeltrri ) CADZEZUAFZAUAFGUBAUAUBCAHACHACAIBJCA
      KALMNUCGOUAPOAQUARST $.
      $( [17-Jul-2006] $)

    $( The discrete topology on a set ` A ` expressed as a topological space.
       (Contributed by FL, 20-Aug-2006.) $)
    distpsOLD $p |- <. A , ~P A >. e. TopSpOLD $=
      ( cpw cuni cop ctpsOLD unipw opeq1i wcel distop eltopspOLD mpbir eqeltrri
      ctop ) ACZDZOEZAOEFPAOAGHQFIONIABJOKLM $.
      $( [19-Aug-2006] $)
  $}

  ${
    indistpsx.a $e |- A e. _V $.
    indistpsx.k $e |- K = { <. 1 , A >. , <. 9 , { (/) , A } >. } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological space,
       using explicit structure component references.  Compare with ~ indistps
       and ~ indistps2 .  The advantage of this version is that the actual
       function for the structure is evident, and ~ df-ndx is not needed, nor
       any other special definition outside of basic set theory.  The
       disadvantage is that if the indices of the component definitions
       ~ df-base and ~ df-topset are changed in the future, this theorem will
       also have to be changed.  Note:  This theorem has hard-coded structure
       indices for demonstration purposes.  It is not intended for general use;
       use ~ indistps instead.  (Contributed by FL, 19-Jul-2006.) $)
    indistpsx $p |- K e. TopSp $=
      ( vs c0 cpr cbs cfv cvv wcel wceq c1 cop c9 cnx cts opeq1i ax-mp eqcomi
      basendx df-topset ndxarg preq12i eqtr4i tpsbase prex tpstopset cuni unipr
      9nn cun 0ex uncom un0 3eqtrri indistop istpsi ) AFAGZBABHIZAJKAUTLCAUSBJB
      MANZOUSNZGPHIZANZPQIZUSNZGDVDVAVFVBVCMAUARVEOUSQOEEUBUKUCRUDUEZUFSTUSBQIZ
      USJKUSVHLFAUGAUSBJVGUHSTUSUIFAULAFULAFAUMCUJFAUNAUOUPAUQUR $.
      $( [19-Oct-2012] $)
  $}

  ${
    indistps.a $e |- A e. _V $.
    indistps.k $e |- K
      = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , { (/) , A } >. } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological space,
       using implicit structure indices.  The advantage of this version over
       ~ indistpsx is that it is independent of the indices of the component
       definitions ~ df-base and ~ df-topset , and if they are changed in the
       future, this theorem will not be affected.  The advantage over
       ~ indistps2 is that it is easy to eliminate the hypotheses with ~ eqid
       and ~ vtoclg to result in a closed theorem.  Theorems ~ indistpsALT and
       ~ indistps2ALT show that the two forms can be derived from each other.
       (Contributed by FL, 19-Jul-2006.) $)
    indistps $p |- K e. TopSp $=
      ( c0 cpr cuni cun 0ex unipr uncom un0 3eqtrri indistop eltpsi ) AEAFZBDPG
      EAHAEHAEAICJEAKALMANO $.
      $( [24-Oct-2012] $)
  $}

  ${
    indistps2.a $e |- ( Base ` K ) = A $.
    indistps2.j $e |- ( TopOpen ` K ) = { (/) , A } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological space,
       using direct component assignments.  Compare with ~ indistps .  The
       advantage of this version is that it is the shortest to state and
       easiest to work with in most situations.  Theorems ~ indistpsALT and
       ~ indistps2ALT show that the two forms can be derived from each
       other. $)
    indistps2 $p |- K e. TopSp $=
      ( cpr cuni cun 0ex cbs cfv cvv fvex eqeltrri unipr uncom 3eqtrri indistop
      c0 un0 istpsi ) ARAEZBCDUAFRAGARGARAHBIJAKCBILMNRAOASPAQT $.
      $( [24-Oct-2012] $)
  $}

  ${
    indistpsALT.a $e |- A e. _V $.
    indistpsALT.k $e |- K
      = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , { (/) , A } >. } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological
       space.  Here we show how to derive the structural version ~ indistps
       from the direct component assignment version ~ indistps2 . $)
    indistpsALT $p |- K e. TopSp $=
      ( cbs cfv cvv wcel wceq cpr tpsbase ax-mp eqcomi prex tpstopset indistps2
      c0 cts ) ABABEFZAGHASICAQAJZBGDKLMTBRFZTGHTUAIQANATBGDOLMP $.
      $( [24-Oct-2012] $)
  $}

  ${
    indistps2ALT.a $e |- ( Base ` K ) = A $.
    indistps2ALT.j $e |- ( TopOpen ` K ) = { (/) , A } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological space,
       using direct component assignments.  Here we show how to derive the
       direct component assignment version ~ indistps2 from the structural
       version ~ indistps . $)
    indistps2ALT $p |- K e. TopSp $=
      ( ctps wcel cnx cbs cfv cop cts c0 cpr fvex eqeltrri eqid indistps tpsstr
      cvv mpbir ) BEFGHIAJGKILAMZJMZEFAUBBHIASCBHNOUBPZQAUABUBCDUCRT $.
      $( [24-Oct-2012] $)
  $}

  ${
    distps.a $e |- A e. _V $.
    distps.k $e |- K
             = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , ~P A >. } $.
    $( The discrete topology on a set ` A ` expressed as a topological space.
       (Contributed by FL, 20-Aug-2006.) $)
    distps $p |- K e. TopSp $=
      ( cpw cuni unipw eqcomi distop eltpsi ) AAEZBDKFAAGHACIJ $.
      $( [19-Oct-2012] $)
  $}

  ${
    $d x y z w v u t f $.
    $( A basis for the standard topology on the reals. $)
    retopbas $p |- ran (,) e. TopBases $=
      ( vz vw vx vy vv vu vt vf cioo wcel cv wa wrex wral co wceq cxr wi ax-mp
      wb crn ctb cin wss cle wbr cif ifcl ad2ant2r adantr ancoms ad2ant2l iooin
      ineq12 sylan9eqr opreq1 eqeq2d opreq2 rcla42ev exp32 exp31 com4t rexlimdv
      syl3anc imp31 rexlimdva rexlimiv imp cxp wfn cr cpw ioof oprvelrn anbi12d
      wf ffn 3imtr4i ssid jctr eleq2 sseq1 rcla4ev syl2an 3impa rgen3 cvv iooex
      rnex isbasis2g mpbir ) IUAZUBJZAKZBKZJZWOCKZDKZUCZUDZLZBWLMZAWSNDWLNCWLNZ
      XBCDAWLWLWSWQWLJZWRWLJZWNWSJZXBXDXELZWSWLJZXFWSWSUDZLZXBXFWQWNWOIOZPZBQMZ
      AQMZWREKZFKZIOZPZFQMZEQMZLZWSGKZHKZIOZPZHQMGQMZXGXHXNXTYFXMXTYFRZAQWNQJZX
      LYGBQYHWOQJZXLYGYHYILZXLLZXSYFEQYKXOQJZLXRYFFQYJXLYLXPQJZXRYFRZRYLYMYJXLY
      NYLYMYJXLYNRYLYMLZYJLZXLXRYFYPXLXRLZLWNXOUEUFZXOWNUGZQJZWOXPUEUFZWOXPUGZQ
      JZWSYSUUBIOZPZYFYPYTYQYLYHYTYMYIYRXOWNQUHUIUJYPUUCYQYMYIUUCYLYHYIYMUUCUUA
      WOXPQUHUKULUJYQYPWSXKXQUCZUUDWQXKWRXQUNYJYOUUFUUDPWNWOXOXPUMUKUOYEUUEWSYS
      YCIOZPGHYSUUBQQYBYSPYDUUGWSYBYSYCIUPUQYCUUBPUUGUUDWSYCUUBYSIURUQUSVDUTVAV
      BVEVCVFVAVCVGVHIQQVIZVJZXGYATUUHVKVLZIVPUUIVMUUHUUJIVQSZUUIXDXNXEXTABQQWQ
      IVNEFQQWRIVNVOSUUIXHYFTUUKGHQQWSIVNSVRXFXIWSVSVTXAXJBWSWLWOWSPWPXFWTXIWOW
      SWNWAWOWSWSWBVOWCWDWEWFWLWGJWMXCTIWHWICDABWLWGWJSWK $.
      $( [6-Feb-2007] $)
  $}

  $( The standard topology on the reals.  (Contributed by FL, 4-Jun-2007.) $)
  retop $p |- ( topGen ` ran (,) ) e. Top $=
    ( cioo crn ctb wcel ctg cfv ctop retopbas tgcl ax-mp ) ABZCDKEFGDHKIJ $.
    $( [29-May-2007] $)

  $( The underlying set of the standard topology on the reals is the reals.
     (Contributed by FL, 4-Jun-2007.) $)
  uniretop $p |- RR = U. ( topGen ` ran (,) ) $=
    ( cioo crn cuni ctg cfv unirnioo ctb wcel wceq retopbas unitg ax-mp eqtr4i
    cr ) NABZCZODECZFOGHQPIJOKLM $.
    $( [29-May-2007] $)

  $( The standard topological space on the reals. $)
  retpsOLD $p |- <. RR , ( topGen ` ran (,) ) >. e. TopSpOLD $=
    ( cr cioo crn ctg cfv cop ctpsOLD wcel ctop cuni istpsOLD uniretop mpbir2an
    wceq retop ) ABCDEZFGHPIHAPJNAPKOLM $.
    $( [10-Feb-2007] $)

  ${
    retps.k $e |- K = { <. ( Base ` ndx ) , RR >. ,
                      <. ( TopOpen ` ndx ) , ( topGen ` ran (,) ) >. } $.
    $( The standard topological space on the reals. $)
    retps $p |- K e. TopSp $=
      ( cr cioo crn ctg cfv uniretop retop eltpsi ) CDEFGABHIJ $.
      $( [19-Oct-2012] $)
  $}

  $( Open intervals are open sets of the standard topology on the reals .
     (Contributed by FL, 18-Jun-2007.) $)
  iooretop $p |- ( A (,) B ) e. ( topGen ` ran (,) ) $=
    ( cxr wcel wa co crn ctg cfv wss ctb retopbas bastg ax-mp a1i cxp wfn cr c0
    cioo cpw wf ioof ffn fnoprvrn mp3an1 sseldd ndmioo ctop retop 0opn syl6eqel
    wn pm2.61i ) ACDZBCDZEZABTFZTGZHIZDUQUSUTURUSUTJZUQUSKDVALUSMNOTCCPZQZUOUPU
    RUSDVBRUAZTUBVCUCVBVDTUDNCCABTUEUFUGUQUMURSUTABUHUTUIDSUTDUJUTUKNULUN $.
    $( [21-Jun-2007] $)


