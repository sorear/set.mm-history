$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Examples of topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d J Y x y a b c d u z $.
    $( A subspace topology is a topology.  Definition of subspace topology in
       [Munkres] p. 89.  ` Y ` is normally a subset of the base set of
       ` J ` .  (Contributed by FL, 15-Apr-2007.) $)
    subtop $p |- ( J e. Top -> { x | E. y e. J x = ( y i^i Y ) } e. Top ) $=
      ( va vb vz vc vd ctop wcel cv cin wceq wrex cab wss cuni wi wal wral wa
      wex copab crn cima wfun funopabeq ssimaexg mp3an2 ex cres df-ima resopab
      rneqi eqtr sseq2i rnopab2 3eqtr eqeq2i anbi2i exbii 3imtr3g eqcomi
      syl5ib imp unieq ciun cvv dfiun2g eqcomd inex1g mprg incom a1i iuneq2i
      iunin2 uniiun ineq2i 3eqtr2 syl6eq anim2i 19.22i syl wb csn snex sseq1
      visset unisn ineq1d eqeq2d anbi12d cla4ev snssi sylan r19.23aiva uniopnt
      ineq1 rcla4ev imp3a 19.23adv impbid2 adantr mpbird uniex eqeq1 rexbidv
      elab sylibr 19.21aiv inopnt 3expa ineq12 inindir syl6eqr ancoms syl2an
      exp43 com24 r19.23adva r19.23adv cbvrexv clelab ceqsexv 3bitr syl7ib
      com23 inex1 r19.21aiva jca abrexexg istopg ) CJKZALZBLZDMZNZBCOZAPZJKZELZ
      YTQZUUBRZYTKZSZETZUUBFLZMZYTKZFYTUAZEYTUAZUBZYNUUGUULYNUUFEYNUUCUUEYNUUCU
      BZUUDYQNZBCOZUUEUUNUUPGLZCQZUUDUUQRZDMZNZUBZGUCZUUNUURUUBYRBUUQOAPZNZUBZG
      UCZUVCYNUUCUVGYNUUBYPCKZYRUBBAUDZUEZQZUVGUUCYNUUBYRBAUDZCUFZQZUURUUBUVLUU
      QUFZNZUBZGUCZUVKUVGYNUVNUVRYNUVLUGUVNUVRBAYQUHGCUUBJUVLUIUJUKUVMUVJUUBUVM
      UVLCULZUEUVJUVLCUMUVSUVIYRBACUNUOUPUQUVQUVFGUVPUVEUURUVOUVDUUBUVOUVLUUQUL
      ZUEYPUUQKZYRUBBAUDZUEUVDUVLUUQUMUVTUWBYRBAUUQUNUOBAUUQYQURUSUTVAVBVCYTUVJ
      UUBUVJYTBACYQURVDUQVEVFUVFUVBGUVEUVAUURUVEUUDUVDRZUUTUUBUVDVGUWCBUUQYQVHZ
      BUUQDYPMZVHZUUTYQVIKZUWCUWDNBUUQUWGBUUQUAUWDUWCBAUUQYQVIVJVKYPDUUQVLVMBUU
      QYQUWEYQUWENUWAYPDVNVOVPUWFDBUUQYPVHZMDUUSMUUTBUUQDYPVQUUSUWHDBUUQVRVSDUU
      SVNVTUSWAWBWCWDYNUUPUVCWEUUCYNUUPUVCUUOUVCBCYPWFZCQZUUOUVCUVHUVBUWJUUOUBG
      UWIYPWGUUQUWINZUURUWJUVAUUOUUQUWICWHUWKUUTYQUUDUWKUUSYPDUWKUUSUWIRYPUUQUW
      IVGYPBWIWJWAWKWLWMWNYPCWOWPWQYNUVBUUPGYNUURUVAUUPYNUURUVAUUPSZYNUURUBUUSC
      KZUWLUUQCWRUWMUVAUUPUUOUVABUUSCYPUUSNYQUUTUUDYPUUSDWSWLWTUKWDUKXAXBXCXDXE
      YSUUPAUUDUUBEWIZXFYOUUDNYRUUOBCYOUUDYQXGXHXIXJUKXKYNUUKEYTYNUUBYTKZUBZUUJ
      FYTUWPUUHYTKZUBUUIYQNZBCOZUUJUWPUWQUWSUWPUUHYQNZBCOZUWSUWQYNUWOUXAUWSSYNU
      XAUWOUWSYNUXAUUBHLZDMZNZHCOZUWSUWOYNUUHILZDMZNZICOUXEUWSSZUXAYNUXHUXIICYN
      UXEUXHUXFCKZUWSYNUXDUXHUXJUWSSSHCYNUXBCKZUBZUXJUXHUXDUWSUXLUXJUXHUXDUWSUX
      BUXFMZCKZUUIUXMDMZNZUWSUXLUXJUBUXHUXDUBUWRUXPBUXMCYPUXMNYQUXOUUIYPUXMDWSW
      LWTYNUXKUXJUXNUXBUXFCXLXMUXDUXHUXPUXDUXHUBUUIUXCUXGMUXOUUBUXCUUHUXGXNUXBU
      XFDXOXPXQXRXSXTYAXTYBUWTUXHBICYPUXFNYQUXGUUHYPUXFDWSWLYCVEUWOYOUUBNZYSUBA
      UCUUBYQNZBCOZUXEYSAUUBYDYSUXSAUUBUWNUXQYRUXRBCYOUUBYQXGXHYEUXRUXDBHCYPUXB
      NYQUXCUUBYPUXBDWSWLYCYFYGYHVFYSUXAAUUHFWIYOUUHNYRUWTBCYOUUHYQXGXHXIVEVFYS
      UWSAUUIUUBUUHUWNYIYOUUINYRUWRBCYOUUIYQXGXHXIXJYJYJYKYNYTVIKUUAUUMWEBACYQJ
      YLEFVIYTYMWDXE $.
      $( [17-May-2007] $) $( [8-Apr-2007] $)
  $}

  ${
    $d x y $.
    $( The singleton of the empty set is a topology.  (Contributed by Stefan
       Allan, 3-Mar-2006.) $)
    sn0top $p |- { (/) } e. Top $=
      ( vx vy c0 csn ctop wcel cv wss cuni wi wal cin wral cvv wa wb p0ex
      istopg ax-mp wceq wo sssn unieq uni0 0ex elsnc2 mpbir syl6eqel unisn
      eqtrt mpan2 visset uniex elsnc sylibr syl jaoi sylbi ax-gen elsn ineq2
      in0 eqeq2i biimp inex1 biimpr adantl rgen2a mpbir2an ) CDZEFZAGZVJHZVLIZV
      JFZJZAKZVLBGZLZVJFZBVJMAVJMZVJNFVKVQWAOPQABNVJRSVPAVMVLCTZVLVJTZUAVOVLCUB
      WBVOWCWBVNCIZVJVLCUCWDVJFWDCTUDWDCUEUFUGUHWCVNVJIZTZVOVLVJUCWFVNCTZVOWFWE
      CTWGCUEUIVNWECUJUKVNCVLAULZUMUNUOUPUQURUSVTABVJVRVJFZVTVLVJFWIVRCTZVTBCUT
      WJVSCTZVTWJVSVLCLZTZWKVRCVLVAWMWKWLCVSVLVBVCVDUPVTWKVSCVLVRWHVEUNVFUPURVG
      VHVI $.
      $( [13-Jul-2006] $) $( [3-Mar-06] $)
  $}

  ${
    $d n x y z A $.
    indistop.1 $e |- A e. V $.
    $( The indiscrete topology on a set ` A ` .  Part of Example 2 in [Munkres]
       p. 77.  (Contributed by FL, 16-Jul-2006.) $)
    indistop $p |- { (/) , A } e. Top $=
      ( vx vy c0 cpr ctop wcel cv wss cuni wi wal cin wral cvv wa wb prex
      istopg ax-mp wceq csn wo sspr unieq uni0 0ex pri1 eqeltr syl6eqel a1i
      unisn syl5eqel eqeltrd jaoi pri2 cun uncom un0 eqeltrr unipr sylbi
      ax-gen pm3.26 ineq2d in0 syl6eq ex pm3.27 ineq1d incom eqtr com12 ineq12
      ancoms inidm visset elpr syl5ib 19.21aiv df-ral 3imtr4 rgen mpbir2an ) EA
      FZGHZCIZWFJZWHKZWFHZLZCMZWHDIZNZWFHZDWFOZCWFOZWFPHWGWMWRQREASCDPWFTUAWLCW
      IWHEUBZWHEUCZUBZUDZWHAUCZUBZWHWFUBZUDZUDWKWHEAUEXBWKXFWSWKXAWSWJEKZWFWHEU
      FXGEWFUGEAUHUIZUJUKXAWJWTKZWFWHWTUFXAEWFXIEWFHXAXHULEUHUMUNUOUPXDWKXEXDWJ
      XCKZWFWHXCUFXDAWFXJAWFHXDEABUQZULABUMUNUOXEWJWFKZWFWHWFUFXEEAURZWFXLXMWFH
      XEAEURZXMWFAEUSXNAWFAUTXKUJVAULEAUHBVBUNUOUPUPVCVDWQCWFWSWHAUBZUDZWNWFHZW
      PLZDMWHWFHWQXPXRDXPWNEUBZWNAUBZUDZWPXQWSYAWPLXOYAWSWPXSWSWPLXTXSWSWPXSWSQ
      ZWOEWFYBWOWHENEYBWNEWHXSWSVEVFWHVGVHXHUKVIXTWSWPXTWSQZWOEWFYCWOEWNNZEYCWH
      EWNXTWSVJVKYDWNENEEWNVLWNVGVMVHXHUKVIUPVNYAXOWPXSXOWPLXTXSXOWPXSXOQZWOEWF
      YEWOAENZEXOXSWOYFUBWHAWNEVOVPAVGVHXHUKVIXTXOWPXTXOQZWOAWFYGWOAANZAXOXTWOY
      HUBWHAWNAVOVPAVQVHXKUKVIUPVNUPWNEADVRVSVTWAWHEACVRVSWPDWFWBWCWDWE $.
      $( [16-Jul-2006] $) $( [15-Jul-2006] $)

    $( The discrete topology on a set ` A ` .  Part of Example 2 in [Munkres]
       p. 77.  (Contributed by FL, 18-Jul-2006.) $)
    distop $p |- P~ A e. Top $=
      ( vx vy cpw ctop wcel cv wss cuni wi wal cin wral cvv wa wb pwex istopg
      ax-mp uniss unipw syl6ss visset uniex elpw sylibr ax-gen ssinss1 a1i
      inex2 syl6ibr sylbi com12 r19.21aiv rgen mpbir2an ) AEZFGZCHZURIZUTJZURGZ
      KZCLZUTDHZMZURGZDURNZCURNZUROGUSVEVJPQABRCDOURSTVDCVAVBAIVCVAVBURJAUTURUA
      AUBUCVBAUTCUDZUEUFUGUHVICURUTURGZVHDURVLUTAIZVFURGZVHKUTAVKUFVNVMVHVNVFAI
      ZVMVHKVFADUDZUFVOVMVGAIZVHVMVQKVOUTVFAUIUJVGAVFUTVPUKUFULUMUNUMUOUPUQ $.
      $( [18-Jul-2006] $) $( [17-Jul-2006] $)

    $( The finite complement topology on a set ` A ` .  Example 3 in [Munkres]
       p. 77.  (Contributed by FL, 15-Aug-2006.) $)
    fctop $p |- { x | ( x (_ A /\ ( E. n e. om ( A \ x ) ~~ n \/ x = (/) ) ) }
                e. Top $=
      ( vy vz cv wss cdif cen wbr com wrex c0 wceq wo wa cab ctop wcel cuni wi
      wal cin wral cvv wb abssexg ax-mp istopg uniss crab pm3.26 a1i ss2rabi
      rabab unieqi unimax 3sstr3 sstr mpan2 syl wn ssel2 visset sseq1 difeq2
      breq1d rexbidv eqeq1 orbi12d anbi12d elab sylib pm3.27d ord con1d imp
      ssfi elssuni sscon sylan2 expcom ad2antlr mpd exp31 r19.23adv uni0c
      negbii rexnal bitr4 syl5ib orrd jca uniex sylibr ax-gen ssinss1 ad2antrr
      cun unfi difindi breq1i rexbii orc ineq1 incom in0 eqtr syl6eq olc ineq2
      ccase2 ad2ant2l anbi12i inex1 breq1 3imtr4 rgen2a mpbir2an ) AGZBHZBYAIZC
      GZJKZCLMZYANOZPZQZARZSTZEGZYJHZYLUAZYJTZUBZEUCZYLFGZUDZYJTZFYJUEEYJUEZYJU
      FTZYKYQUUAQUGBUFTZUUBDYHABUFUHUIEFUFYJUJUIYPEYMYNBHZBYNIZYDJKZCLMZYNNOZPZ
      QZYOYMUUDUUIYMYNYJUAZHZUUDYLYJUKUULUUKBHUUDYIAUFULZUAZYBAUFULZUAZUUKBUUMU
      UOHUUNUUPHYIYBAUFYIYBUBYAUFTYBYHUMUNUOUUMUUOUKUIUUMYJYIAUPUQUUCUUPBODABUF
      URUIUSYNUUKBUTVAVBYMUUGUUHYMUUHUUGYMYRNOZVCZFYLMZUUGUUHVCZYMUURUUGFYLYMYR
      YLTZUURUUGYMUVAQZUURQBYRIZYDJKZCLMZUUGUVBUURUVEUVBUVEUUQUVBUVEUUQUVBYRBHZ
      UVEUUQPZUVBYRYJTZUVFUVGQZYLYJYRVDYIUVIAYRFVEYAYROZYBUVFYHUVGYAYRBVFUVJYFU
      VEYGUUQUVJYEUVDCLUVJYCUVCYDJYAYRBVGVHVIYAYRNVJVKVLVMZVNVOVPVQVRUVAUVEUUGU
      BYMUURUVEUVAUUGUVEUUEUVCHZUUGUVACUVCUUEVSUVAYRYNHUVLYRYLVTYRYNBWAVBWBWCWD
      WEWFWGUUTUUQFYLUEZVCUUSUUHUVMFYLWHWIUUQFYLWJWKWLVQWMWNYIUUJAYNYLEVEZWOYAY
      NOZYBUUDYHUUIYAYNBVFUVOYFUUGYGUUHUVOYEUUFCLUVOYCUUEYDJYAYNBVGVHVIYAYNNVJV
      KVLVMWPWQYTEFYJYLBHZBYLIZYDJKZCLMZYLNOZPZQZUVIQZYSBHZBYSIZYDJKZCLMZYSNOZP
      ZQZYLYJTZUVHQYTUWCUWDUWIUVPUWDUWAUVIYLYRBWRWSUWAUVGUWIUVPUVFUVSUVEUVTUUQU
      WIUVSUVEQZUWGUWIUWLUVQUVCWTZYDJKZCLMUWGCUVQUVCXAUWFUWNCLUWEUWMYDJBYLYRXBX
      CXDWPUWGUWHXEVBUVTUWHUWIUVTYSNYRUDZNYLNYRXFUWOYRNUDNNYRXGYRXHXIXJUWHUWGXK
      ZVBUUQUWHUWIUUQYSYLNUDNYRNYLXLYLXHXJUWPVBXMXNWNUWKUWBUVHUVIYIUWBAYLUVNYAY
      LOZYBUVPYHUWAYAYLBVFUWQYFUVSYGUVTUWQYEUVRCLUWQYCUVQYDJYAYLBVGVHVIYAYLNVJV
      KVLVMUVKXOYIUWJAYSYLYRUVNXPYAYSOZYBUWDYHUWIYAYSBVFUWRYFUWGYGUWHUWRYCUWEOZ
      YFUWGUGYAYSBVGUWSYEUWFCLYCUWEYDJXQVIVBYAYSNVJVKVLVMXRXSXT $.
      $( [16-Aug-2006] $) $( [15-Aug-2006] $)

    $( The finite complement topology on a set ` A ` .  Example 3 in [Munkres]
       p. 77.  (This version of ~ fctop requires the Axiom of Infinity.)
      (Contributed by FL, 20-Aug-2006.) $)
    fctop2 $p |- { x | ( x (_ A /\ ( ( A \ x ) ~< om \/ x = (/) ) ) } e. Top $=
      ( vn cv wss cdif com csdm wbr c0 wceq wo wa cab cen wrex ctop isfinite
      orbi1i anbi2i abbii fctop eqeltr ) AEZBFZBUEGZHIJZUEKLZMZNZAOUFUGDEPJDHQZ
      UIMZNZAORUKUNAUJUMUFUHULUIDUGSTUAUBABDCUCUD $.
      $( [20-Aug-2006] $) $( [19-Aug-2006] $)

    $( The countable complement topology on a set ` A ` .  Example 4 in
       [Munkres] p. 77.  (Contributed by FL, 29-Aug-2006.) $)
    cctop $p |- { x | ( x (_ A /\ ( ( A \ x ) ~<_ om \/ x = (/) ) ) } e. Top $=
      ( vy vz cv wss cdif com cdom wbr c0 wceq wo wa cab ctop wcel cuni wi wal
      cin wral cvv wb abssexg ax-mp istopg uniss crab pm3.26 a1i ss2rabi rabab
      unieqi unimax 3sstr3 sstr mpan2 syl wn wrex ssel2 visset sseq1 difeq2
      breq1d eqeq1 orbi12d anbi12d elab sylib pm3.27d ord con1d imp domtr
      pm3.27 ad2antrr elssuni sscon difexg ssdom2g 3syl sylanc mpdan exp31
      r19.23adv csn uni0b dfss3 elsn ralbii 3bitr negbii rexnal bitr4 syl5ib
      orrd jca uniex sylibr ax-gen ssinss1 cun unctb difindi syl5eqbr orc
      ineq1 incom in0 eqtr syl6eq olc ineq2 ccase2 ad2ant2l anbi12i inex1
      3imtr4 rgen2a mpbir2an ) AFZBGZBYDHZIJKZYDLMZNZOZAPZQRZDFZYKGZYMSZYKRZTZD
      UAZYMEFZUBZYKRZEYKUCDYKUCZYKUDRZYLYRUUBOUEBUDRZUUCCYIABUDUFUGDEUDYKUHUGYQ
      DYNYOBGZBYOHZIJKZYOLMZNZOZYPYNUUEUUIYNYOYKSZGZUUEYMYKUIUULUUKBGUUEYJAUDUJ
      ZSZYEAUDUJZSZUUKBUUMUUOGUUNUUPGYJYEAUDYJYETYDUDRYEYIUKULUMUUMUUOUIUGUUMYK
      YJAUNUOUUDUUPBMCABUDUPUGUQYOUUKBURUSUTYNUUGUUHYNUUHUUGYNYSLMZVAZEYMVBZUUG
      UUHVAZYNUURUUGEYMYNYSYMRZUURUUGYNUVAOZUUROZBYSHZIJKZUUGUVBUURUVEUVBUVEUUQ
      UVBUVEUUQUVBYSBGZUVEUUQNZUVBYSYKRZUVFUVGOZYMYKYSVCYJUVIAYSEVDYDYSMZYEUVFY
      IUVGYDYSBVEUVJYGUVEYHUUQUVJYFUVDIJYDYSBVFVGYDYSLVHVIVJVKZVLVMVNVOVPUUFUVD
      JKZUVEUUGUVCUVEOZUUFUVDIVQUVMYSYOGZUUFUVDGZUVLUVMUVAUVNUVBUVAUURUVEYNUVAV
      RVSYSYMVTUTYSYOBWAUVDUDRZUVOUVLTUUDUVPCBYSUDWBUGUUFUVDUDWCUGWDUVCUVEVRWEW
      FWGWHUUTUUQEYMUCZVAUUSUUHUVQUUHYMLWIZGYSUVRRZEYMUCUVQYMWJEYMUVRWKUVSUUQEY
      MELWLWMWNWOUUQEYMWPWQWRVOWSWTYJUUJAYOYMDVDZXAYDYOMZYEUUEYIUUIYDYOBVEUWAYG
      UUGYHUUHUWAYFUUFIJYDYOBVFVGYDYOLVHVIVJVKXBXCUUADEYKYMBGZBYMHZIJKZYMLMZNZO
      ZUVIOZYTBGZBYTHZIJKZYTLMZNZOZYMYKRZUVHOUUAUWHUWIUWMUWBUWIUWFUVIYMYSBXDVSU
      WFUVGUWMUWBUVFUWDUVEUWEUUQUWMUWDUVEOZUWKUWMUWPUWCUVDXEIUWJJUWCUVDXFBYMYSX
      GXHUWKUWLXIUTUWEUWLUWMUWEYTLYSUBZLYMLYSXJUWQYSLUBLLYSXKYSXLXMXNUWLUWKXOZU
      TUUQUWLUWMUUQYTYMLUBLYSLYMXPYMXLXNUWRUTXQXRWTUWOUWGUVHUVIYJUWGAYMUVTYDYMM
      ZYEUWBYIUWFYDYMBVEUWSYGUWDYHUWEUWSYFUWCIJYDYMBVFVGYDYMLVHVIVJVKUVKXSYJUWN
      AYTYMYSUVTXTYDYTMZYEUWIYIUWMYDYTBVEUWTYGUWKYHUWLUWTYFUWJIJYDYTBVFVGYDYTLV
      HVIVJVKYAYBYC $.
      $( [29-Aug-2006] $) $( [23-Aug-2006] $)

    $( The indiscrete topology on a set ` A ` expressed as a topological space.
       (Contributed by FL, 19-Jul-2006.) $)
    indistps $p |- <. A , { (/) , A } >. e. TopSp $=
      ( c0 cpr cuni cop ctps cun 0ex unipr uncom un0 3eqtr opeq1i wcel ctop
      indistop eltopsp mpbir eqeltrr ) CADZEZUAFZAUAFGUBAUAUBCAHACHACAIBJCAKALM
      NUCGOUAPOABQUARST $.
      $( [19-Jul-2006] $) $( [17-Jul-2006] $)

    $( The discrete topology on a set ` A ` expressed as a topological space.
       (Contributed by FL, 20-Aug-2006.) $)
    distps $p |- <. A , P~ A >. e. TopSp $=
      ( cpw cuni cop ctps unipw opeq1i wcel ctop distop eltopsp mpbir eqeltrr
      ) ACZDZOEZAOEFPAOAGHQFIOJIABKOLMN $.
      $( [20-Aug-2006] $) $( [19-Aug-2006] $)
  $}

  ${
    $d x y z w v u t f $.
    $( A basis for the standard topology on the reals. $)
    retopbas $p |- ran (,) e. Bases $=
      ( vz vw vx vy vv vu vt vf cioo crn ctb wcel cv cin wss wa wrex wral wceq
      eleq2 sseq1 anbi12d rcla4ev co cxr wi cle wbr cif opreq1 eqeq1d opreq2
      rcla42ev ifcl ad2ant2r adantr ancoms ad2ant2l iooint ineq12 sylan9req
      syl3anc exp32 exp31 com4t imp31 r19.23adv r19.23adva r19.23aiv imp cxp
      wfn wb cr cpw wf ioof ffn ax-mp oprvalelrn 3imtr4 ssid jctr syl2an 3impa
      rgen3 cvv iooex rnexg isbasis2g mpbir ) IJZKLZAMZBMZLZWOCMZDMZNZOZPZBWLQZ
      AWSRDWLRCWLRZXBCDAWLWLWSWQWLLZWRWLLZWNWSLZXBWSWLLZXFWSWSOZPZXBXDXEPZXFXAX
      IBWSWLWOWSSWPXFWTXHWOWSWNTWOWSWSUAUBUCWNWOIUDZWQSZBUEQZAUEQZEMZFMZIUDZWRS
      ZFUEQZEUEQZPZGMZHMZIUDZWSSZHUEQGUEQZXJXGXNXTYFXMXTYFUFZAUEWNUELZXLYGBUEYH
      WOUELZXLYGYHYIPZXLPZXSYFEUEYKXOUELZPXRYFFUEYJXLYLXPUELZXRYFUFZUFYLYMYJXLY
      NYLYMYJXLYNUFYLYMPZYJPZXLXRYFWNXOUGUHZXOWNUIZUELZWOXPUGUHZWOXPUIZUELZYRUU
      AIUDZWSSZYFYPXLXRPZPYEUUDYRYCIUDZWSSGHYRUUAUEUEYBYRSYDUUFWSYBYRYCIUJUKYCU
      UASUUFUUCWSYCUUAYRIULUKUMYPYSUUEYLYHYSYMYIYQXOWNUEUNUOUPYPUUBUUEYMYIUUBYL
      YHYIYMUUBYTWOXPUEUNUQURUPYPUUEUUCXKXQNZWSYJYOUUGUUCSWNWOXOXPUSUQXKWQXQWRU
      TVAVBVCVDVEVFVGVHVDVGVIVJIUEUEVKZVLZXJYAVMUUHVNVOZIVPUUIVQUUHUUJIVRVSZUUI
      XDXNXEXTABUEUEWQIVTEFUEUEWRIVTUBVSUUIXGYFVMUUKGHUEUEWSIVTVSWAXFXHWSWBWCWD
      WEWFWLWGLZWMXCVMIWGLUULWHIWGWIVSCDABWLWGWJVSWK $.
      $( [13-Feb-2007] $) $( [6-Feb-2007] $)
  $}

  $( The standard topology on the reals.  (Contributed by FL, 4-Jun-2007.) $)
  retop $p |- ( topGen ` ran (,) ) e. Top $=
    ( cioo crn ctb wcel ctg cfv ctop retopbas tgclt ax-mp ) ABZCDKEFGDHKIJ $.
    $( [22-Jun-2007] $) $( [29-May-2007] $)

  $( The underlying set of the standard topology on the reals is the reals.
     (Contributed by FL, 4-Jun-2007.) $)
  uniretop $p |- U. ( topGen ` ran (,) ) = RR $=
    ( cioo crn ctg cfv cuni cr ctb wcel wceq retopbas unitgt ax-mp unirnioo
    eqtr ) ABZCDEZOEZFOGHPQIJOKLMN $.
    $( [22-Jun-2007] $) $( [29-May-2007] $)

  $( The standard topological space on the reals. $)
  retps $p |- <. RR , ( topGen ` ran (,) ) >. e. TopSp $=
    ( cr cioo crn ctg cfv cop ctps wcel ctop cuni wceq istps retop uniretop
    eqcomi mpbir2an ) ABCDEZFGHQIHAQJZKAQLMRANOP $.
    $( [15-Feb-2007] $) $( [10-Feb-2007] $)

  $( Open intervals are open sets of the standard topology on the reals .
     (Contributed by FL, 18-Jun-2007.) $)
  iooretop $p |- ( A (,) B ) e. ( topGen ` ran (,) ) $=
    ( cxr wcel wa cioo co crn ctg cfv wss ctb retopbas bastgt ax-mp a1i cxp
    wfn cr cpw wf ioof ffn fnoprvalrn mp3an1 sseldd wn c0 ndmioo ctop retop
    0opnt syl6eqel pm2.61i ) ACDZBCDZEZABFGZFHZIJZDUQUSUTURUSUTKZUQUSLDVAMUSNOP
    FCCQZRZUOUPURUSDVBSTZFUAVCUBVBVDFUCOCCABFUDUEUFUQUGURUHUTABUIUTUJDUHUTDUKUT
    ULOUMUN $.
    $( [22-Jun-2007] $) $( [21-Jun-2007] $)

