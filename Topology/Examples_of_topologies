$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                        Examples of topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c d x y z J $.  $d a b c d x y z Y $.
    $( A subspace topology is a topology.  Definition of subspace topology in
       [Munkres] p. 89. ` Y ` is normally a subset of the base set of ` J ` .
       (Contributed by FL, 15-Apr-2007.) $)
    subtop $p |- ( J e. Top -> { x | E. y e. J x = ( y i^i Y ) } e. Top ) $=
      ( va vb vz vc vd ctop wcel cv cin wceq wrex cab wss cuni wi wal wral cvv
      wa wb abrexexg istopg syl wex copab crn rnopab2 eqcomi sseq2i cima wfun
      funopabeq ssimaexg mp3an2 ex cres df-ima resopab rneqi eqtri 3eqtri
      eqeq2i anbi2i exbii 3imtr3g syl5bi imp unieq ciun dfiun2g eqcomd inex1g
      mprg incom a1i iuneq2i iunin2 uniiun ineq2i 3eqtr2i syl6eq anim2i eximi
      csn snssi snex sseq1 visset unisn ineq1d eqeq2d anbi12d cla4ev sylan
      r19.23aiva uniopn ineq1 rcla4ev expimpd 19.23adv impbid2 adantr mpbird
      uniex eqeq1 rexbidv elab sylibr 19.21aiv clelab ceqsexv cbvrexv 3bitri
      inopn 3expa ineq12 inindir syl6eqr ancoms syl2an exp43 com24 r19.23adva
      r19.23adv syl7bi com23 inex1 r19.21aiva mpbir2and ) CJKZALZBLZDMZNZBCOZAP
      ZJKZELZYTQZUUBRZYTKZSZETZUUBFLZMZYTKZFYTUAZEYTUAZYNYTUBKUUAUUGUULUCUDBACY
      QJUEEFUBYTUFUGYNUUFEYNUUCUUEYNUUCUCZUUDYQNZBCOZUUEUUMUUOGLZCQZUUDUUPRZDMZ
      NZUCZGUHZUUMUUQUUBYRBUUPOAPZNZUCZGUHZUVBYNUUCUVFUUCUUBYPCKZYRUCBAUIZUJZQZ
      YNUVFYTUVIUUBUVIYTBACYQUKULUMYNUUBYRBAUIZCUNZQZUUQUUBUVKUUPUNZNZUCZGUHZUV
      JUVFYNUVMUVQYNUVKUOUVMUVQBAYQUPGCUUBJUVKUQURUSUVLUVIUUBUVLUVKCUTZUJUVIUVK
      CVAUVRUVHYRBACVBVCVDUMUVPUVEGUVOUVDUUQUVNUVCUUBUVNUVKUUPUTZUJYPUUPKZYRUCB
      AUIZUJUVCUVKUUPVAUVSUWAYRBAUUPVBVCBAUUPYQUKVEVFVGVHVIVJVKUVEUVAGUVDUUTUUQ
      UVDUUDUVCRZUUSUUBUVCVLUWBBUUPYQVMZBUUPDYPMZVMZUUSYQUBKZUWBUWCNBUUPUWFBUUP
      UAUWCUWBBAUUPYQUBVNVOYPDUUPVPVQBUUPYQUWDYQUWDNUVTYPDVRVSVTUWEDBUUPYPVMZMD
      UURMUUSBUUPDYPWAUURUWGDBUUPWBWCDUURVRWDVEWEWFWGUGYNUUOUVBUDUUCYNUUOUVBUUN
      UVBBCUVGYPWHZCQZUUNUVBYPCWIUVAUWIUUNUCGUWHYPWJUUPUWHNZUUQUWIUUTUUNUUPUWHC
      WKUWJUUSYQUUDUWJUURYPDUWJUURUWHRYPUUPUWHVLYPBWLWMWEWNWOWPWQWRWSYNUVAUUOGY
      NUUQUUTUUOYNUUQUCUURCKZUUTUUOSUUPCWTUWKUUTUUOUUNUUTBUURCYPUURNYQUUSUUDYPU
      URDXAWOXBUSUGXCXDXEXFXGYSUUOAUUDUUBEWLZXHYOUUDNYRUUNBCYOUUDYQXIXJXKXLUSXM
      YNUUKEYTYNUUBYTKZUCZUUJFYTUWNUUHYTKZUCUUIYQNZBCOZUUJUWNUWOUWQUWOUUHYQNZBC
      OZUWNUWQYSUWSAUUHFWLYOUUHNYRUWRBCYOUUHYQXIXJXKYNUWMUWSUWQSYNUWSUWMUWQUWMU
      UBHLZDMZNZHCOZYNUWSUWQUWMYOUUBNZYSUCAUHUUBYQNZBCOZUXCYSAUUBXNYSUXFAUUBUWL
      UXDYRUXEBCYOUUBYQXIXJXOUXEUXBBHCYPUWTNYQUXAUUBYPUWTDXAWOXPXQUWSUUHILZDMZN
      ZICOYNUXCUWQSZUWRUXIBICYPUXGNYQUXHUUHYPUXGDXAWOXPYNUXIUXJICYNUXCUXIUXGCKZ
      UWQYNUXBUXIUXKUWQSSHCYNUWTCKZUCZUXKUXIUXBUWQUXMUXKUXIUXBUWQUXMUXKUCUWTUXG
      MZCKZUUIUXNDMZNZUWQUXIUXBUCYNUXLUXKUXOUWTUXGCXRXSUXBUXIUXQUXBUXIUCUUIUXAU
      XHMUXPUUBUXAUUHUXHXTUWTUXGDYAYBYCUWPUXQBUXNCYPUXNNYQUXPUUIYPUXNDXAWOXBYDY
      EYFYGYFYHVJYIYJVKVJVKYSUWQAUUIUUBUUHUWLYKYOUUINYRUWPBCYOUUIYQXIXJXKXLYLYL
      YM $.
      $( [8-Apr-2007] $)
  $}

  ${
    $d x y $.
    $( The singleton of the empty set is a topology.  (Contributed by Stefan
       Allan, 3-Mar-2006.) $)
    sn0top $p |- { (/) } e. Top $=
      ( vx vy c0 csn ctop wcel cv wss cuni wi wal cin wral cvv wa wb p0ex
      istopg ax-mp wceq wo sssn unieq uni0 0ex elsnc2 mpbir syl6eqel unisn
      eqtr mpan2 visset uniex elsnc sylibr syl jaoi sylbi ax-gen elsn ineq2
      in0 eqeq2i biimpi inex1 biimpri adantl rgen2a mpbir2an ) CDZEFZAGZVJHZVLI
      ZVJFZJZAKZVLBGZLZVJFZBVJMAVJMZVJNFVKVQWAOPQABNVJRSVPAVMVLCTZVLVJTZUAVOVLC
      UBWBVOWCWBVNCIZVJVLCUCWDVJFWDCTUDWDCUEUFUGUHWCVNVJIZTZVOVLVJUCWFVNCTZVOWF
      WECTWGCUEUIVNWECUJUKVNCVLAULZUMUNUOUPUQURUSVTABVJVRVJFZVTVLVJFWIVRCTZVTBC
      UTWJVSCTZVTWJVSVLCLZTZWKVRCVLVAWMWKWLCVSVLVBVCVDUPVTWKVSCVLVRWHVEUNVFUPUR
      VGVHVI $.
      $( [3-Mar-2006] $)
  $}

  ${
    $d x y A $.
    $( The indiscrete topology on a set ` A ` .  Part of Example 2 in [Munkres]
       p. 77.  (Contributed by FL, 16-Jul-2006; hypothesis eliminated by Stefan
       Allan, 6-Nov-2008.) $)
    indistop $p |- { (/) , A } e. Top $=
      ( vx vy cvv wcel c0 cpr ctop cv wss cuni wi wal cin wral wa wb prex a1i
      istopg syl wceq csn wo sspr unieq uni0 0ex prid1 eqeltri syl6eqel unisn
      jaod unisng sylan9eqr prid2g adantr eqeltrd ex cun uniprg mpan uncom un0
      eqtri syl6eq syl5bi 19.21aiv visset elpr simpr ineq2d in0 simpl ineq1d
      incom ineq12 adantl inidm ccased expdimp r19.21aiv mpbir2and wn prprc2
      sn0top pm2.61i ) ADEZFAGZHEZWHWJBIZWIJZWKKZWIEZLZBMZWKCIZNZWIEZCWIOZBWIOZ
      WHWIDEZWJWPXAPQXBWHFARSBCDWITUAWHWOBWLWKFUBZWKFUCZUBZUDZWKAUCZUBZWKWIUBZU
      DZUDWHWNWKFAUEWHXFWNXJWHXCWNXEXCWNLWHXCWMFKZWIWKFUFXKFWIUGFAUHUIZUJUKSXEW
      NLWHXEWMXDKZWIWKXDUFXMFWIFUHULXLUJUKSUMWHXHWNXIWHXHWNWHXHPWMAWIXHWHWMXGKA
      WKXGUFADUNUOWHAWIEZXHFADUPZUQURUSWHXIWNWHXIPWMAWIXIWHWMWIKZAWKWIUFWHXPFAU
      TZAFDEWHXPXQUBUHFADDVAVBXQAFUTAFAVCAVDVEVFUOWHXNXIXOUQURUSUMUMVGVHWHWTBWI
      WKWIEXCWKAUBZUDZWHWTWKFABVIVJWHXSWTWHXSPZWSCWIWQWIEWQFUBZWQAUBZUDZXTWSWQF
      ACVIVJWHXSYCWSWHXCYAXRYBWSXCYAPZWSLWHYDWRFWIYDWRWKFNZFYDWQFWKXCYAVKVLWKVM
      ZVFXLUKSXRYAPZWSLWHYGWRFWIYGWRYEFYGWQFWKXRYAVKVLYFVFXLUKSXCYBPZWSLWHYHWRF
      WIYHWRFWQNZFYHWKFWQXCYBVNVOYIWQFNFFWQVPWQVMVEVFXLUKSWHXRYBPZWSWHYJPZWRAWI
      YKWRAANZAYJWRYLUBWHWKAWQAVQVRAVSVFWHXNYJXOUQURUSVTWAVGWBUSVGWBWCWHWDWIXDH
      FAWEWFUKWG $.
      $( [6-Nov-2008] $)
  $}

  ${
    $d x y z A $.
    indistop.1 $e |- A e. _V $.
    $( The discrete topology on a set ` A ` .  Part of Example 2 in [Munkres]
       p. 77.  (Contributed by FL, 18-Jul-2006.) $)
    distop $p |- ~P A e. Top $=
      ( vx vy cpw ctop wcel cv wss cuni wi wal cin wral cvv wa wb pwex istopg
      ax-mp uniss unipw syl6sseq visset uniex elpw sylibr ax-gen ssinss1 a1i
      inex2 syl6ibr sylbi com12 r19.21aiv rgen mpbir2an ) AEZFGZCHZURIZUTJZURGZ
      KZCLZUTDHZMZURGZDURNZCURNZUROGUSVEVJPQABRCDOURSTVDCVAVBAIVCVAVBURJAUTURUA
      AUBUCVBAUTCUDZUEUFUGUHVICURUTURGZVHDURVLUTAIZVFURGZVHKUTAVKUFVNVMVHVNVFAI
      ZVMVHKVFADUDZUFVOVMVGAIZVHVMVQKVOUTVFAUIUJVGAVFUTVPUKUFULUMUNUMUOUPUQ $.
      $( [17-Jul-2006] $)

    $( The finite complement topology on a set ` A ` .  Example 3 in [Munkres]
       p. 77.  (Contributed by FL, 15-Aug-2006.) $)
    fctop $p |- { x | ( x C_ A /\ ( ( A \ x ) e. Fin \/ x = (/) ) ) }
           e. Top $=
      ( vy vz cv wss cdif cfn wcel c0 wceq wo wa cab ctop cuni wi wal cin wral
      cvv wb abssexg ax-mp istopg uniss crab simpl a1i ss2rabi rabab unieqi
      unimax 3sstr3i sstr sylancl wn wrex uni0c notbii rexnal bitr4i ssel2
      visset sseq1 difeq2 eleq1d eqeq1 orbi12d anbi12d elab sylib simprd ord
      con1d imp elssuni sscon syl ssfi sylan2 expcom ad2antlr mpd exp31
      r19.23adv syl5bi orrd uniex sylanbrc ax-gen ssinss1 ad2antrr cun difindi
      unfi syl5eqel orcd ineq1 incom in0 eqtri syl6eq olcd ineq2 ccase2
      ad2ant2l jca anbi12i inex1 3imtr4i rgen2a mpbir2an ) AFZBGZBXOHZIJZXOKLZM
      ZNZAOZPJZDFZYBGZYDQZYBJZRZDSZYDEFZTZYBJZEYBUADYBUAZYBUBJZYCYIYMNUCBUBJZYN
      CXTABUBUDUEDEUBYBUFUEYHDYEYFBGZBYFHZIJZYFKLZMZYGYEYFYBQZGUUABGYPYDYBUGYAA
      UBUHZQZXPAUBUHZQZUUABUUBUUDGUUCUUEGYAXPAUBYAXPRXOUBJXPXTUIUJUKUUBUUDUGUEU
      UBYBYAAULUMYOUUEBLCABUBUNUEUOYFUUABUPUQYEYRYSYEYSYRYSURZYJKLZURZEYDUSZYEY
      RUUFUUGEYDUAZURUUIYSUUJEYDUTVAUUGEYDVBVCYEUUHYREYDYEYJYDJZUUHYRYEUUKNZUUH
      NBYJHZIJZYRUULUUHUUNUULUUNUUGUULUUNUUGUULYJBGZUUNUUGMZUULYJYBJZUUOUUPNZYD
      YBYJVDYAUURAYJEVEXOYJLZXPUUOXTUUPXOYJBVFUUSXRUUNXSUUGUUSXQUUMIXOYJBVGVHXO
      YJKVIVJVKVLZVMVNVOVPVQUUKUUNYRRYEUUHUUNUUKYRUUKUUNYQUUMGZYRUUKYJYFGUVAYJY
      DVRYJYFBVSVTUUMYQWAWBWCWDWEWFWGWHVPWIYAYPYTNAYFYDDVEZWJXOYFLZXPYPXTYTXOYF
      BVFUVCXRYRXSYSUVCXQYQIXOYFBVGVHXOYFKVIVJVKVLWKWLYLDEYBYDBGZBYDHZIJZYDKLZM
      ZNZUURNZYKBGZBYKHZIJZYKKLZMZNZYDYBJZUUQNYLUVJUVKUVOUVDUVKUVHUURYDYJBWMWNU
      VHUUPUVOUVDUUOUVFUUNUVGUUGUVOUVFUUNNZUVMUVNUVRUVLUVEUUMWOIBYDYJWPUVEUUMWQ
      WRWSUVGUVNUVMUVGYKKYJTZKYDKYJWTUVSYJKTKKYJXAYJXBXCXDXEUUGUVNUVMUUGYKYDKTK
      YJKYDXFYDXBXDXEXGXHXIUVQUVIUUQUURYAUVIAYDUVBXOYDLZXPUVDXTUVHXOYDBVFUVTXRU
      VFXSUVGUVTXQUVEIXOYDBVGVHXOYDKVIVJVKVLUUTXJYAUVPAYKYDYJUVBXKXOYKLZXPUVKXT
      UVOXOYKBVFUWAXRUVMXSUVNUWAXQUVLIXOYKBVGVHXOYKKVIVJVKVLXLXMXN $.
      $( [26-Jun-2011] $) $( [15-Aug-2006] $)

    $( The finite complement topology on a set ` A ` .  Example 3 in [Munkres]
       p. 77.  (This version of ~ fctop requires the Axiom of Infinity.)
       (Contributed by FL, 20-Aug-2006.) $)
    fctop2 $p |- { x | ( x C_ A /\ ( ( A \ x ) ~< om \/ x = (/) ) ) } e. Top $=
      ( cv wss cdif cfn wcel c0 wceq wo wa cab com csdm wbr ctop isfinite3
      orbi1i anbi2i abbii fctop eqeltrri ) ADZBEZBUDFZGHZUDIJZKZLZAMUEUFNOPZUHK
      ZLZAMQUJUMAUIULUEUGUKUHUFRSTUAABCUBUC $.
      $( [26-Jun-2011] $) $( [19-Aug-2006] $)


    $( The countable complement topology on a set ` A ` .  Example 4 in
       [Munkres] p. 77.  (Contributed by FL, 29-Aug-2006.) $)
    cctop $p |- { x | ( x C_ A /\ ( ( A \ x ) ~<_ om \/ x = (/) ) ) } e. Top $=
      ( vy vz cv wss cdif com cdom wbr c0 wceq wo wa cab ctop wcel cuni wi wal
      cin wral cvv wb abssexg ax-mp istopg uniss crab simpl a1i ss2rabi rabab
      unieqi unimax 3sstr3i sstr sylancl wn wrex csn uni0b dfss3 elsn ralbii
      3bitri notbii rexnal bitr4i ssel2 visset sseq1 difeq2 breq1d eqeq1
      orbi12d anbi12d elab sylib simprd ord con1d imp simpllr elssuni syl
      sscon difexg ssdom2g 3syl domtr sylancom mpdan exp31 r19.23adv syl5bi
      orrd uniex sylanbrc ax-gen ssinss1 ad2antrr cun difindi unctb syl5eqbr
      orcd ineq1 incom in0 eqtri syl6eq olcd ineq2 ccase2 ad2ant2l jca anbi12i
      inex1 3imtr4i rgen2a mpbir2an ) AFZBGZBYDHZIJKZYDLMZNZOZAPZQRZDFZYKGZYMSZ
      YKRZTZDUAZYMEFZUBZYKRZEYKUCDYKUCZYKUDRZYLYRUUBOUEBUDRZUUCCYIABUDUFUGDEUDY
      KUHUGYQDYNYOBGZBYOHZIJKZYOLMZNZYPYNYOYKSZGUUJBGUUEYMYKUIYJAUDUJZSZYEAUDUJ
      ZSZUUJBUUKUUMGUULUUNGYJYEAUDYJYETYDUDRYEYIUKULUMUUKUUMUIUGUUKYKYJAUNUOUUD
      UUNBMCABUDUPUGUQYOUUJBURUSYNUUGUUHYNUUHUUGUUHUTZYSLMZUTZEYMVAZYNUUGUUOUUP
      EYMUCZUTUURUUHUUSUUHYMLVBZGYSUUTRZEYMUCUUSYMVCEYMUUTVDUVAUUPEYMELVEVFVGVH
      UUPEYMVIVJYNUUQUUGEYMYNYSYMRZUUQUUGYNUVBOZUUQOZBYSHZIJKZUUGUVCUUQUVFUVCUV
      FUUPUVCUVFUUPUVCYSBGZUVFUUPNZUVCYSYKRZUVGUVHOZYMYKYSVKYJUVJAYSEVLYDYSMZYE
      UVGYIUVHYDYSBVMUVKYGUVFYHUUPUVKYFUVEIJYDYSBVNVOYDYSLVPVQVRVSZVTWAWBWCWDUV
      DUVFUUFUVEJKZUUGUVDUVFOZYSYOGZUUFUVEGZUVMUVNUVBUVOYNUVBUUQUVFWEYSYMWFWGYS
      YOBWHUVEUDRZUVPUVMTUUDUVQCBYSUDWIUGUUFUVEUDWJUGWKUUFUVEIWLWMWNWOWPWQWCWRY
      JUUEUUIOAYOYMDVLZWSYDYOMZYEUUEYIUUIYDYOBVMUVSYGUUGYHUUHUVSYFUUFIJYDYOBVNV
      OYDYOLVPVQVRVSWTXAUUADEYKYMBGZBYMHZIJKZYMLMZNZOZUVJOZYTBGZBYTHZIJKZYTLMZN
      ZOZYMYKRZUVIOUUAUWFUWGUWKUVTUWGUWDUVJYMYSBXBXCUWDUVHUWKUVTUVGUWBUVFUWCUUP
      UWKUWBUVFOZUWIUWJUWNUWHUWAUVEXDIJBYMYSXEUWAUVEXFXGXHUWCUWJUWIUWCYTLYSUBZL
      YMLYSXIUWOYSLUBLLYSXJYSXKXLXMXNUUPUWJUWIUUPYTYMLUBLYSLYMXOYMXKXMXNXPXQXRU
      WMUWEUVIUVJYJUWEAYMUVRYDYMMZYEUVTYIUWDYDYMBVMUWPYGUWBYHUWCUWPYFUWAIJYDYMB
      VNVOYDYMLVPVQVRVSUVLXSYJUWLAYTYMYSUVRXTYDYTMZYEUWGYIUWKYDYTBVMUWQYGUWIYHU
      WJUWQYFUWHIJYDYTBVNVOYDYTLVPVQVRVSYAYBYC $.
      $( [23-Aug-2006] $)

    $( The indiscrete topology on a set ` A ` expressed as a topological
       space.  (Contributed by FL, 19-Jul-2006.) $)
    indistpsOLD $p |- <. A , { (/) , A } >. e. TopSpOLD $=
      ( c0 cpr cuni cop ctpsOLD cun 0ex unipr uncom un0 3eqtri opeq1i wcel
      ctop indistop eltopspOLD mpbir eqeltrri ) CADZEZUAFZAUAFGUBAUAUBCAHACHACA
      IBJCAKALMNUCGOUAPOAQUARST $.
      $( [17-Jul-2006] $)

    $( The discrete topology on a set ` A ` expressed as a topological space.
       (Contributed by FL, 20-Aug-2006.) $)
    distpsOLD $p |- <. A , ~P A >. e. TopSpOLD $=
      ( cpw cuni cop ctpsOLD unipw opeq1i wcel ctop distop eltopspOLD mpbir
      eqeltrri ) ACZDZOEZAOEFPAOAGHQFIOJIABKOLMN $.
      $( [19-Aug-2006] $)
  $}

  ${
    indistpsx.a $e |- A e. _V $.
    indistpsx.k $e |- K = { <. 1 , A >. , <. 9 , { (/) , A } >. } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological space,
       using explicit structure component references.  Compare with ~ indistps
       and ~ indistps2 .  The advantage of this version is that the actual
       function for the structure is evident, and ~ df-ndx is not needed, nor
       any other special definition outside of basic set theory.  The
       disadvantage is that if the indices of the component definitions
       ~ df-base and ~ df-topset are changed in the future, this theorem will
       also have to be changed.  Note:  This theorem has hard-coded structure
       indices for demonstration purposes.  It is not intended for general use;
       use ~ indistps instead.  (Contributed by FL, 19-Jul-2006.) $)
    indistpsx $p |- K e. TopSp $=
      ( vs c0 cpr cbs cfv cvv wcel wceq c1 cop c9 cnx cts basendx opeq1i
      df-topset 9nn ndxarg preq12i eqtr4i tpsbase ax-mp eqcomi prex tpstopset
      cuni cun 0ex unipr uncom un0 3eqtrri indistop istpsi ) AFAGZBABHIZAJKAUTL
      CAUSBJBMANZOUSNZGPHIZANZPQIZUSNZGDVDVAVFVBVCMARSVEOUSQOEETUAUBSUCUDZUEUFU
      GUSBQIZUSJKUSVHLFAUHAUSBJVGUIUFUGUSUJFAUKAFUKAFAULCUMFAUNAUOUPAUQUR $.
      $( [19-Oct-2012] $)
  $}

  ${
    indistps.a $e |- A e. _V $.
    indistps.k $e |- K
      = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , { (/) , A } >. } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological space,
       using implicit structure indices.  The advantage of this version over
       ~ indistpsx is that it is independent of the indices of the component
       definitions ~ df-base and ~ df-topset , and if they are changed in the
       future, this theorem will not be affected.  The advantage over
       ~ indistps2 is that it is easy to eliminate the hypotheses with ~ eqid
       and ~ vtoclg to result in a closed theorem.  Theorems ~ indistpsALT and
       ~ indistps2ALT show that the two forms can be derived from each other.
       (Contributed by FL, 19-Jul-2006.) $)
    indistps $p |- K e. TopSp $=
      ( c0 cpr cuni cun 0ex unipr uncom un0 3eqtrri indistop eltpsi ) AEAFZBDPG
      EAHAEHAEAICJEAKALMANO $.
      $( [24-Oct-2012] $)
  $}

  ${
    indistps2.a $e |- ( Base ` K ) = A $.
    indistps2.j $e |- ( TopOpen ` K ) = { (/) , A } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological space,
       using direct component assignments.  Compare with ~ indistps .  The
       advantage of this version is that it is the shortest to state and
       easiest to work with in most situations.  Theorems ~ indistpsALT and
       ~ indistps2ALT show that the two forms can be derived from each
       other. $)
    indistps2 $p |- K e. TopSp $=
      ( c0 cpr cuni cun 0ex cbs cfv cvv fvex eqeltrri unipr uncom un0 3eqtrri
      indistop istpsi ) AEAFZBCDUAGEAHAEHAEAIBJKALCBJMNOEAPAQRAST $.
      $( [24-Oct-2012] $)
  $}

  ${
    indistpsALT.a $e |- A e. _V $.
    indistpsALT.k $e |- K
      = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , { (/) , A } >. } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological
       space.  Here we show how to derive the structural version ~ indistps
       from the direct component assignment version ~ indistps2 . $)
    indistpsALT $p |- K e. TopSp $=
      ( cbs cfv cvv wcel wceq c0 cpr tpsbase ax-mp eqcomi cts prex tpstopset
      indistps2 ) ABABEFZAGHASICAJAKZBGDLMNTBOFZTGHTUAIJAPATBGDQMNR $.
      $( [24-Oct-2012] $)
  $}

  ${
    indistps2ALT.a $e |- ( Base ` K ) = A $.
    indistps2ALT.j $e |- ( TopOpen ` K ) = { (/) , A } $.
    $( The indiscrete topology on a set ` A ` expressed as a topological space,
       using direct component assignments.  Here we show how to derive the
       direct component assignment version ~ indistps2 from the structural
       version ~ indistps . $)
    indistps2ALT $p |- K e. TopSp $=
      ( ctps wcel cnx cbs cfv cop cts c0 cpr cvv fvex eqeltrri eqid indistps
      tpsstr mpbir ) BEFGHIAJGKILAMZJMZEFAUBBHIANCBHOPUBQZRAUABUBCDUCST $.
      $( [24-Oct-2012] $)
  $}


  ${
    distps.a $e |- A e. _V $.
    distps.k $e |- K
             = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , ~P A >. } $.
    $( The discrete topology on a set ` A ` expressed as a topological space.
       (Contributed by FL, 20-Aug-2006.) $)
    distps $p |- K e. TopSp $=
      ( cpw cuni unipw eqcomi distop eltpsi ) AAEZBDKFAAGHACIJ $.
      $( [19-Oct-2012] $)
  $}

  ${
    $d x y z w v u t f $.
    $( A basis for the standard topology on the reals. $)
    retopbas $p |- ran (,) e. TopBases $=
      ( vz vw vx vy vv vu vt vf cioo crn ctb wcel cv cin wss wa wrex wral co
      wceq cxr wi cle wbr cif ifcl ad2ant2r adantr ancoms ad2ant2l iooin
      ineq12 sylan9req opreq1 eqeq1d opreq2 rcla42ev syl111anc exp32 exp31
      com4t imp31 r19.23adv r19.23adva r19.23aiv imp cxp wfn wb cr cpw wf ioof
      ffn ax-mp oprvelrn anbi12d 3imtr4i ssid jctr eleq2 sseq1 rcla4ev syl2an
      3impa rgen3 cvv iooex rnex isbasis2g mpbir ) IJZKLZAMZBMZLZWOCMZDMZNZOZPZ
      BWLQZAWSRDWLRCWLRZXBCDAWLWLWSWQWLLZWRWLLZWNWSLZXBXDXEPZWSWLLZXFWSWSOZPZXB
      XFWNWOISZWQTZBUAQZAUAQZEMZFMZISZWRTZFUAQZEUAQZPZGMZHMZISZWSTZHUAQGUAQZXGX
      HXNXTYFXMXTYFUBZAUAWNUALZXLYGBUAYHWOUALZXLYGYHYIPZXLPZXSYFEUAYKXOUALZPXRY
      FFUAYJXLYLXPUALZXRYFUBZUBYLYMYJXLYNYLYMYJXLYNUBYLYMPZYJPZXLXRYFYPXLXRPZPW
      NXOUCUDZXOWNUEZUALZWOXPUCUDZWOXPUEZUALZYSUUBISZWSTZYFYPYTYQYLYHYTYMYIYRXO
      WNUAUFUGUHYPUUCYQYMYIUUCYLYHYIYMUUCUUAWOXPUAUFUIUJUHYPYQUUDXKXQNZWSYJYOUU
      FUUDTWNWOXOXPUKUIXKWQXQWRULUMYEUUEYSYCISZWSTGHYSUUBUAUAYBYSTYDUUGWSYBYSYC
      IUNUOYCUUBTUUGUUDWSYCUUBYSIUPUOUQURUSUTVAVBVCVDUTVCVEVFIUAUAVGZVHZXGYAVIU
      UHVJVKZIVLUUIVMUUHUUJIVNVOZUUIXDXNXEXTABUAUAWQIVPEFUAUAWRIVPVQVOUUIXHYFVI
      UUKGHUAUAWSIVPVOVRXFXIWSVSVTXAXJBWSWLWOWSTWPXFWTXIWOWSWNWAWOWSWSWBVQWCWDW
      EWFWLWGLWMXCVIIWHWICDABWLWGWJVOWK $.
      $( [6-Feb-2007] $)
  $}

  $( The standard topology on the reals.  (Contributed by FL, 4-Jun-2007.) $)
  retop $p |- ( topGen ` ran (,) ) e. Top $=
    ( cioo crn ctb wcel ctg cfv ctop retopbas tgcl ax-mp ) ABZCDKEFGDHKIJ $.
    $( [29-May-2007] $)

  $( The underlying set of the standard topology on the reals is the reals.
     (Contributed by FL, 4-Jun-2007.) $)
  uniretop $p |- U. ( topGen ` ran (,) ) = RR $=
    ( cioo crn ctg cfv cuni cr ctb wcel wceq retopbas unitg ax-mp unirnioo
    eqtri ) ABZCDEZOEZFOGHPQIJOKLMN $.
    $( [29-May-2007] $)

  $( The standard topological space on the reals. $)
  retpsOLD $p |- <. RR , ( topGen ` ran (,) ) >. e. TopSpOLD $=
    ( cr cioo crn ctg cfv cop ctpsOLD wcel ctop cuni wceq istpsOLD retop
    uniretop eqcomi mpbir2an ) ABCDEZFGHQIHAQJZKAQLMRANOP $.
    $( [10-Feb-2007] $)

  ${
    retps.k $e |- K = { <. ( Base ` ndx ) , RR >. ,
                      <. ( TopOpen ` ndx ) , ( topGen ` ran (,) ) >. } $.
    $( The standard topological space on the reals. $)
    retps $p |- K e. TopSp $=
      ( cr cioo crn ctg cfv cuni uniretop eqcomi retop eltpsi ) CDEFGZABMHCIJKL
      $.
      $( [19-Oct-2012] $)
  $}

  $( Open intervals are open sets of the standard topology on the reals .
     (Contributed by FL, 18-Jun-2007.) $)
  iooretop $p |- ( A (,) B ) e. ( topGen ` ran (,) ) $=
    ( cxr wcel wa cioo co crn ctg cfv wss ctb retopbas bastg ax-mp a1i cxp wfn
    cr cpw wf ioof ffn fnoprvrn mp3an1 sseldd wn c0 ndmioo ctop retop 0opn
    syl6eqel pm2.61i ) ACDZBCDZEZABFGZFHZIJZDUQUSUTURUSUTKZUQUSLDVAMUSNOPFCCQZR
    ZUOUPURUSDVBSTZFUAVCUBVBVDFUCOCCABFUDUEUFUQUGURUHUTABUIUTUJDUHUTDUKUTULOUMU
    N $.
    $( [21-Jun-2007] $)


