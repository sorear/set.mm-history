$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Topological spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new constant symbols and define their syntactical properties. $)

  $c Top $.
  $c TopSpOLD $.
  $c TopSp $.
  $c TopOpen $.
  $c TopBases $.
  $c topGen $.

  $( Extend class notation with the class of all topologies. $)
  ctop $a class Top $.

  $( Extend class notation with the class of all topological spaces. $)
  ctpsOLD $a class TopSpOLD $.

  $( Extend class notation with the topology component of a topological
     space. $)
  cts $a class TopOpen $.

  $( Extend class notation with the class of all topological spaces. $)
  ctps $a class TopSp $.

  $( Extend class notation with the class of all topological bases. $)
  ctb $a class TopBases $.

  $( Extend class notation with a function that converts a basis to its
     corresponding topology. $)
  ctg $a class topGen $.

  ${
    $d x y z w v $.
    $( Define the (proper) class of all topologies.  See ~ istop2g for an
       alternate way to express finite intersection and ~ istps5OLD for a
       standard definition in terms of both members of a topological space. $)
    df-top $a |- Top = { x | ( A. y ( y C_ x -> U. y e. x ) /\
                           A. y e. x A. z e. x ( y i^i z ) e. x ) } $.

    $( Define the class of all topological spaces, each of which is an ordered
       pair the second of which is a topology on the first.  See ~ istps5OLD
       for a standard way to express a topological space. $)
    df-topspOLD $a |- TopSpOLD = { <. x , y >. | ( y e. Top /\ x = U. y ) } $.

    $( Define the class of all topological bases.  Equivalent to definition of
       basis in [Munkres] p. 78 (see ~ isbasis2g ).  Note that "bases" is the
       plural of "basis." $)
    df-bases $a |- TopBases = { x | A. y e. x A. z e. x ( y i^i z ) C_
                      U. ( x i^i ~P ( y i^i z ) ) } $.

    $( Define a function that converts a basis to its corresponding topology.
       Equivalent to the definition of a topology generated by a basis in
       [Munkres] p. 78 (see ~ tgval2 ).  See ~ tgval3 for an alternate
       expression for the value. $)
    df-topgen $a |- topGen = { <. x , y >. | ( x e. TopBases /\ y =
       { z | z C_ U. ( x i^i ~P z ) } ) } $.
  $}

  $( Define the topology component of a topological space (structure). $)
  df-topset $a |- TopOpen = ( f e. _V |-> ( f ` 9 ) ) $.

  $( Utility theorem: index-independent form of ~ df-topset . $)
  topsetid $p |- TopOpen = ( f e. _V |-> ( f ` ( TopOpen ` ndx ) ) ) $=
    ( cts c9 df-topset 9nn ndxid ) BCAADEF $.
    $( [20-Oct-2012] $)

  $( Utility lemma for constructing topological spaces without direct reference
     to the actual component indices. $)
  tpslem $p |- -. ( TopOpen ` ndx ) e. { ( Base ` ndx ) } $=
    ( vs cnx cts cfv cbs csn wcel wceq wne wn c9 df-topset 9nn ndxarg c1 1re
    9re 1lt9 ltneii basendx neeqtrri eqnetri df-ne mpbi elsni mto ) BCDZBEDZFGU
    GUHHZUGUHIUIJUGKUHCKAALMNKOUHOKPQRSTUAUBUGUHUCUDUGUHUEUF $.
    $( [2-Oct-2013] $)

  ${
    tpsfn.k $e |- K
         = { <. ( Base ` ndx ) , B >. , <. ( TopOpen ` ndx ) , J >. } $.
    ${
      tpsfn.d $e |- D = { ( Base ` ndx ) , ( TopOpen ` ndx ) } $.
      $( A constructed topology is a function. $)
      tpsfn $p |- K Fn D $=
        ( cnx cbs cfv csn cop cts fvex fnsn cpr cun df-pr eqtri tpslem fnunsn
        ) GHIZJZBUAAKZJZDGLIZCGLMUAAGHMNDUCUECKZOUDUFJPEUCUFQRBUAUEOUBUEJPFUAUE
        QRST $.
        $( [28-Sep-2013] $)
    $}

    $( The base set of a constructed topology. $)
    tpsbase $p |- ( B e. V -> B = ( Base ` K ) ) $=
      ( vs cbs cnx cfv cop cts cpr cvv prex eqeltri wfn wfun eqid tpsfn fnfun
      ax-mp baseid opex prid1 eleqtrri strfv ) ACGDFCHGIZAJZHKIZBJZLZMEUHUJNOCU
      GUILZPCQAULBCEULRSULCTUAFUBUHUKCUHUJUGAUCUDEUEUF $.
      $( [2-Oct-2013] $)

    $( The open sets of a constructed topology. $)
    tpstopset $p |- ( J e. V -> J = ( TopOpen ` K ) ) $=
      ( vs cts cnx cbs cfv cop cpr cvv prex eqeltri wfn wfun eqid tpsfn fnfun
      ax-mp topsetid opex prid2 eleqtrri strfv ) BCGDFCHIJZAKZHGJZBKZLZMEUHUJNO
      CUGUILZPCQAULBCEULRSULCTUAFUBUJUKCUHUJUIBUCUDEUEUF $.
      $( [2-Oct-2013] $)
  $}

  $( Define the class of all topological spaces (structures). $)
  df-topsp $a |- TopSp = { f | ( ( TopOpen ` f ) e. Top
      /\ ( Base ` f ) = U. ( TopOpen ` f ) ) } $.

  ${
    $d x y z J $.
    $( Express the predicate " ` J ` is a topology."  Note:  In the literature,
       a topology is often represented by a script letter T, which resembles
       the letter J. This confusion has led to J being used by some authors -
       e.g.  K. D. Joshi, _Introduction to General Topology_ (1983), p. 114 -
       and it is convenient for us since we later use ` T ` to represent linear
       transformations (operators).  (Contributed by Stefan Allan,
       3-Mar-2006.) $)
    istopg $p |- ( J e. A -> ( J e. Top <-> ( A. x ( x C_ J -> U. x e. J )
                  /\ A. x e. J A. y e. J ( x i^i y ) e. J ) ) ) $=
      ( vz cv wss cuni wcel wi wal cin wral wa ctop wceq sseq2 eleq2 imbi12d
      albidv raleqbi1dv anbi12d df-top elab2g ) AFZEFZGZUEHZUFIZJZAKZUEBFLZUFIZ
      BUFMZAUFMZNUEDGZUHDIZJZAKZULDIZBDMZADMZNEDOCUFDPZUKUSUOVBVCUJURAVCUGUPUIU
      QUFDUEQUFDUHRSTUNVAAUFDUMUTBUFDUFDULRUAUAUBEABUCUD $.
      $( [3-Mar-2006] $)

    $( Express the predicate " ` J ` is a topology," using "the intersection of
       the elements of any finite subcollection" instead of the intersection of
       any two elements. $)
    istop2g $p |- ( J e. A -> ( J e. Top <-> ( A. x ( x C_ J -> U. x e. J )
         /\ A. x ( ( x C_ J /\ x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( vy wcel ctop cv wss cuni wi wal cin wral wa c0 wne cfn w3a cint istopg
      fiint anbi2i syl6bb ) CBECFEAGZCHZUDICEJAKZUDDGLCEDCMACMZNUFUEUDOPUDQERUD
      SCEJAKZNADBCTUGUHUFADCUAUBUC $.
      $( [23-Dec-2008] $) $( [19-Jul-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y J $.
    $( The union of a subset of a topology is an open set.  (Contributed by
       Stefan Allan, 27-Feb-2006.) $)
    uniopn $p |- ( ( J e. Top /\ A C_ J ) -> U. A e. J ) $=
      ( vx vy ctop wcel wss cuni cv wi wal cin wral wa istopg ibi simpld cpw
      elpw2g biimpar wceq sseq1 unieq eleq1d imbi12d cla4gv syl com23 ex
      pm2.43d mpid imp ) BEFZABGZAHZBFZUMUNCIZBGZUQHZBFZJZCKZUPUMVBUQDILBFDBMCB
      MZUMVBVCNCDEBOPQUMUNVBUPJZUMUNUNVDJUMUNNZVBUNUPVEABRZFZVBUNUPJZJUMVGUNABE
      STVAVHCAVFUQAUAZURUNUTUPUQABUBVIUSUOBUQAUCUDUEUFUGUHUIUJUKUL $.
      $( [27-Feb-2006] $)

    $( The indexed union of a subset of a topology is an open set. $)
    iunopn $p |- ( ( J e. Top /\ A. x e. A B e. J ) -> U_ x e. A B e. J ) $=
      ( vy ctop wcel wral wa ciun cv wceq wrex cab cuni dfiun2g adantl wss
      uniiunlem ibi uniopn sylan2 eqeltrd ) DFGZCDGABHZIABCJZEKCLABMENZOZDUEUFU
      HLUDAEBCDPQUEUDUGDRZUHDGUEUIAEBCDDSTUGDUAUBUC $.
      $( [5-Oct-2006] $)

    $( The intersection of two open sets of a topology is also an open set. $)
    inopn $p |- ( ( J e. Top /\ A e. J /\ B e. J ) -> ( A i^i B ) e. J ) $=
      ( vx vy ctop wcel cin cv wral wa wss cuni wi wal istopg ibi simprd wceq
      ineq1 eleq1d ineq2 rcla42v syl5com 3impib ) CFGZACGZBCGZABHZCGZUFDIZEIZHZ
      CGZECJDCJZUGUHKUJUFUKCLUKMCGNDOZUOUFUPUOKDEFCPQRUNUJAULHZCGDEABCCUKASUMUQ
      CUKAULTUAULBSUQUICULBAUBUAUCUDUE $.
      $( [17-Jul-2006] $)
  $}

  $( The union of two open sets is open.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  unopn $p |- ( ( J e. Top /\ A e. J /\ B e. J ) -> ( A u. B ) e. J ) $=
    ( ctop wcel w3a cpr cuni cun wceq uniprg 3adant1 wa wss prssg ibi uniopn
    sylan2 3impb eqeltrrd ) CDEZACEZBCEZFABGZHZABIZCUBUCUEUFJUAABCCKLUAUBUCUECE
    ZUBUCMZUAUDCNZUGUHUIABCCCOPUDCQRST $.
    $( [2-Sep-2009] $)

  $( The empty set is an open subset of a topology.  (Contributed by Stefan
     Allan, 27-Feb-2006.) $)
  0opn $p |- ( J e. Top -> (/) e. J ) $=
    ( ctop wcel c0 cuni uni0 wss 0ss uniopn mpan2 syl5eqelr ) ABCZDDEZAFLDAGMAC
    AHDAIJK $.
    $( [27-Feb-2006] $)

  $( The empty set is not a topology.  (Contributed by FL, 1-Jun-2008.) $)
  0ntop $p |- -. (/) e. Top $=
    ( c0 ctop wcel noel 0opn mto ) ABCAACADAEF $.
    $( [1-Jun-2008] $)

  ${
    1open.1 $e |- X = U. J $.
    $( The underlying set of a topology is an open set. $)
    topopn $p |- ( J e. Top -> X e. J ) $=
      ( ctop wcel cuni wss ssid uniopn mpan2 syl5eqel ) ADEZBAFZACLAAGMAEAHAAIJ
      K $.
      $( [17-Jul-2006] $)

    $( A member of a topology is a subset of its underlying set. $)
    eltopss $p |- ( ( J e. Top /\ A e. J ) -> A C_ X ) $=
      ( wcel wss ctop cuni elssuni syl6sseqr adantl ) ABEZACFBGELABHCABIDJK $.
      $( [12-Sep-2006] $)
  $}

  ${
    $d x y A $.  $d x y J $.
    $( Construct a topological space from a topology and vice-versa.  We say
       that ` A ` is a topology on ` U. A ` .  (This could be proved more
       efficiently from ~ istpsOLD , but the proof here does not require the
       Axiom of Regularity.) $)
    eltopspOLD $p |- ( <. U. J , J >. e. TopSpOLD <-> J e. Top ) $=
      ( vy vx cuni cop ctpsOLD wcel cvv wa ctop wbr df-br wrel cv wceq copab
      relopab df-topspOLD releqi mpbir brrelexi sylbir uniexb sylibr jca
      uniexg elisset eqeq1 anbi2d eleq1 unieq eqeq2d anbi12d opelopabg eleq2i
      eqid biantru 3bitr4g pm5.21nii ) ADZAEZFGZUTHGZAHGZIZAJGZVBVCVDVBUTAFKVCU
      TAFLUTAFFMBNZJGZCNZVGDZOZIZCBPZMVLCBQFVMCBRZSTUAUBZVBVCVDVOAUCUDUEVFVCVDA
      JUFAJUGUEVEVAVMGVFUTUTOZIZVBVFVLVHUTVJOZIVQCBUTAHHVIUTOVKVRVHVIUTVJUHUIVG
      AOZVHVFVRVPVGAJUJVSVJUTUTVGAUKULUMUNFVMVAVNUOVPVFUTUPUQURUS $.
      $( [8-Mar-2006] $)

    $( Existence implied by membership in a topological space.  This technical
       lemma, which can help eliminate unnecessary antecedents, uses the Axiom
       of Regularity (via ~ elirr ) along with definitional tricks. $)
    tpsexOLD $p |- ( <. A , J >. e. TopSpOLD -> ( A e. _V /\ J e. _V ) ) $=
      ( vy vx cop ctpsOLD wcel cvv wbr df-br wrel cv ctop cuni wceq wa copab
      relopab df-topspOLD releqi mpbir brrelexi sylbir wn elirr simpr eqid
      topopn adantr eqeltrd mto jca elisset eleq2i eqeq1 anbi2d eleq1 unieq
      eqeq2d anbi12d opelopabg syl5bb pm5.21nii mtbir opprc2 eleq1d mtbiri
      con4i ) ABEZFGZAHGZBHGZVJABFIVKABFJABFFKCLZMGZDLZVMNZOZPZDCQZKVRDCRFVSDCS
      ZTUAZUBUCVLVJVLUDZVJAAEZFGZWDAMGZAANZOZPZWHAAGAUEWHAWFAWEWGUFWEWFAGWGAWFW
      FUGUHUIUJUKWDVKVKPZWHWDVKVKWDAAFIVKAAFJAAFWAUBUCZWJULWEWIWGWEVKVKAMUMZWKU
      LUIWDWCVSGWIWHFVSWCVTUNVRVNAVPOZPWHDCAAHHVOAOVQWLVNVOAVPUOUPVMAOZVNWEWLWG
      VMAMUQWMVPWFAVMAURUSUTVAVBVCVDWBVIWCFABVEVFVGVHUL $.
      $( [18-Jul-2006] $)

    $( Express the predicate "is a topological space." $)
    istpsOLD $p |- ( <. A , J >. e. TopSpOLD <-> ( J e. Top /\ A = U. J ) ) $=
      ( vy vx cop ctpsOLD wcel cvv wa ctop cuni wceq tpsexOLD simpr uniexg
      adantr eqeltrd elisset jca cv copab df-topspOLD eleq2i eqeq1 anbi2d
      eleq1 unieq eqeq2d anbi12d opelopabg syl5bb pm5.21nii ) ABEZFGZAHGZBHGZIZ
      BJGZABKZLZIZABMVAUOUPVAAUSHURUTNURUSHGUTBJOPQURUPUTBJRPSUNUMCTZJGZDTZVBKZ
      LZIZDCUAZGUQVAFVHUMDCUBUCVGVCAVELZIVADCABHHVDALVFVIVCVDAVEUDUEVBBLZVCURVI
      UTVBBJUFVJVEUSAVBBUGUHUIUJUKUL $.
      $( [18-Jul-2006] $)

    $( Express the predicate "is a topological space." $)
    istps2OLD $p |- ( <. A , J >. e. TopSpOLD <-> ( ( J e. Top /\ J C_ ~P A )
                  /\ ( (/) e. J /\ A e. J ) ) ) $=
      ( ctop wcel cuni wceq wa cpw wss c0 cop ctpsOLD eqimss adantl 0opn
      adantr simpr eqid topopn eqeltrrd jca32 ex unissel adantrl impbid1 eqcom
      sspwuni anbi1i 3bitr4g pm5.32i istpsOLD anass 3bitr4i ) BCDZABEZFZGUNBAHI
      ZJBDZABDZGZGZGABKLDUNUQGUTGUNUPVAUNUOAFZUOAIZUTGZUPVAUNVBVDUNVBVDUNVBGZVC
      URUSVBVCUNUOAMNUNURVBBOPVEUOABUNVBQUNUOBDVBBUOUORSPTUAUBVCUSVBURBAUCUDUEA
      UOUFUQVCUTBAUGUHUIUJABUKUNUQUTULUM $.
      $( [18-Jul-2006] $)

    $( A standard textbook definition of a topological space. $)
    istps3OLD $p |- ( <. A , J >. e. TopSpOLD <-> ( ( J C_ ~P A /\ (/) e. J /\
                 A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\
                 A. x e. J A. y e. J ( x i^i y ) e. J ) ) ) $=
      ( cop ctpsOLD wcel ctop cpw wss wa c0 w3a cv cuni wi wal cin wral
      istps2OLD anass ancom 3anass anbi1i cvv wb pwexg ssexg sylan2 3adant2
      istopg syl pm5.32i 3bitr2i 3bitri ) CDEFGDHGZDCIZJZKLDGZCDGZKZKUPURVAKZKZ
      URUSUTMZANZDJVEODGPAQVEBNRDGBDSADSKZKZCDTUPURVAUAVCVBUPKVDUPKVGUPVBUBVDVB
      UPURUSUTUCUDVDUPVFVDDUEGZUPVFUFURUTVHUSUTURUQUEGVHCDUGDUQUEUHUIUJABUEDUKU
      LUMUNUO $.
      $( [18-Jul-2006] $)

    $( A standard textbook definition of a topological space. $)
    istps4OLD $p |- ( <. A , J >. e. TopSpOLD <-> ( ( J C_ ~P A /\ (/) e. J /\
       A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\ A. x ( ( x C_ J /\
       x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( vy cop ctpsOLD wcel cpw wss c0 w3a cv cuni wi wal cin wral wa wne cfn
      cint istps3OLD fiint anbi2i bitri ) BCEFGCBHIJCGBCGKZALZCIZUGMCGNAOZUGDLP
      CGDCQACQZRZRUFUIUHUGJSUGTGKUGUACGNAOZRZRADBCUBUKUMUFUJULUIADCUCUDUDUE $.
      $( [30-Dec-2008] $) $( [19-Jul-2006] $)

    $( A standard textbook definition of a topological space ` <. A , J >. ` :
       a topology on ` A ` is a collection ` J ` of subsets of ` A ` such that
       ` (/) ` and ` A ` are in ` J ` and that is closed under union and finite
       intersection.  Definition of topological space in [Munkres] p. 76. $)
    istps5OLD $p |- ( <. A , J >. e. TopSpOLD <-> ( ( A. x e. J x C_ A /\ (/)
        e. J
                 /\ A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\
           A. x ( ( x C_ J /\ x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( cop ctpsOLD wcel cpw wss c0 w3a cv cuni wi wal wne cfn cint wa wral
      istps4OLD pwssb 3anbi1i anbi1i bitri ) BCDEFCBGHZICFZBCFZJZAKZCHZUILCFMAN
      UJUIIOUIPFJUIQCFMANRZRUIBHACSZUFUGJZUKRABCTUHUMUKUEULUFUGACBUAUBUCUD $.
      $( [23-Apr-2009] $) $( [19-Jul-2006] $)
  $}

  ${
    $d f A $.  $d f J $.  $d f K $.
    istps.a $e |- A = ( Base ` K ) $.
    istps.j $e |- J = ( TopOpen ` K ) $.
    $( Express the predicate "is a topological space." $)
    istps $p |- ( K e. TopSp <-> ( J e. Top /\ A = U. J ) ) $=
      ( vf ctps wcel cv cts cfv ctop cbs cuni wceq wa cab df-topsp eleq2i cvv
      eleq1i wn c0 0ntop fvprc eleq1d mtbiri con4i sylbi adantr fveq2 unieqd
      eqeq12d anbi12d unieqi eqeq12i anbi12i syl6bbr elab3 bitri ) CGHCFIZJKZLH
      ZVAMKZVBNZOZPZFQZHBLHZABNZOZPZGVHCFRSVGVLFCVICTHZVKVICJKZLHZVMBVNLEUAZVMV
      OVMUBZVOUCLHUDVQVNUCLCJUEUFUGUHUIUJVACOZVGVOCMKZVNNZOZPVLVRVCVOVFWAVRVBVN
      LVACJUKZUFVRVDVSVEVTVACMUKVRVBVNWBULUMUNVIVOVKWAVPAVSVJVTDBVNEUOUPUQURUSU
      T $.
      $( [20-Oct-2012] $)

    $( Express the predicate "is a topological space." $)
    istps2 $p |- ( K e. TopSp <-> ( ( J e. Top /\ J C_ ~P A )
                  /\ ( (/) e. J /\ A e. J ) ) ) $=
      ( ctop wcel cuni wceq wa cpw wss c0 ctps eqimss adantl 0opn adantr simpr
      eqid topopn eqeltrrd jca32 ex unissel adantrl impbid1 eqcom sspwuni
      anbi1i 3bitr4g pm5.32i istps anass 3bitr4i ) BFGZABHZIZJUPBAKLZMBGZABGZJZ
      JZJCNGUPUSJVBJUPURVCUPUQAIZUQALZVBJZURVCUPVDVFUPVDVFUPVDJZVEUTVAVDVEUPUQA
      OPUPUTVDBQRVGUQABUPVDSUPUQBGVDBUQUQTUARUBUCUDVEVAVDUTBAUEUFUGAUQUHUSVEVBB
      AUIUJUKULABCDEUMUPUSVBUNUO $.
      $( [20-Oct-2012] $) $( [18-Jul-2006] $)

    $d x y J $.
    $( A standard textbook definition of a topological space. $)
    istps3 $p |- ( K e. TopSp <-> ( ( J C_ ~P A /\ (/) e. J /\
                 A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\
                 A. x e. J A. y e. J ( x i^i y ) e. J ) ) ) $=
      ( ctps wcel ctop cpw wss wa c0 w3a cv cuni wi wal cin wral istps2 anass
      ancom 3anass anbi1i cvv wb pwexg ssexg sylan2 3adant2 istopg syl pm5.32i
      3bitr2i 3bitri ) EHIDJIZDCKZLZMNDIZCDIZMZMURUTVCMZMZUTVAVBOZAPZDLVGQDIRAS
      VGBPTDIBDUAADUAMZMZCDEFGUBURUTVCUCVEVDURMVFURMVIURVDUDVFVDURUTVAVBUEUFVFU
      RVHVFDUGIZURVHUHUTVBVJVAVBUTUSUGIVJCDUIDUSUGUJUKULABUGDUMUNUOUPUQ $.
      $( [20-Oct-2012] $) $( [18-Jul-2006] $)

    $( A standard textbook definition of a topological space. $)
    istps4 $p |- ( K e. TopSp <-> ( ( J C_ ~P A /\ (/) e. J /\
       A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\ A. x ( ( x C_ J /\
       x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( vy ctps wcel cpw wss c0 w3a cv cuni wi wal cin wral wa wne cfn cint
      istps3 fiint anbi2i bitri ) DHICBJKLCIBCIMZANZCKZUIOCIPAQZUIGNRCIGCSACSZT
      ZTUHUKUJUILUAUIUBIMUIUCCIPAQZTZTAGBCDEFUDUMUOUHULUNUKAGCUEUFUFUG $.
      $( [20-Oct-2012] $) $( [19-Jul-2006] $)

    $d x A $.
    $( A standard textbook definition of a topological space ` <. A , J >. ` :
       a topology on ` A ` is a collection ` J ` of subsets of ` A ` such that
       ` (/) ` and ` A ` are in ` J ` and that is closed under union and finite
       intersection.  Definition of topological space in [Munkres] p. 76. $)
    istps5 $p |- ( K e. TopSp <-> ( ( A. x e. J x C_ A /\ (/) e. J
                 /\ A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\
           A. x ( ( x C_ J /\ x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( ctps wcel cpw wss c0 w3a cv cuni wi wal wne cfn cint wa wral istps4
      pwssb 3anbi1i anbi1i bitri ) DGHCBIJZKCHZBCHZLZAMZCJZUKNCHOAPULUKKQUKRHLU
      KSCHOAPTZTUKBJACUAZUHUILZUMTABCDEFUBUJUOUMUGUNUHUIACBUCUDUEUF $.
      $( [20-Oct-2012] $) $( [19-Jul-2006] $)
  $}

  ${
    istpsi.b $e |- ( Base ` K ) = A $.
    istpsi.j $e |- ( TopOpen ` K ) = J $.
    istpsi.1 $e |- A = U. J $.
    istpsi.2 $e |- J e. Top $.
    $( Properties that determine a topological space. $)
    istpsi $p |- K e. TopSp $=
      ( ctps wcel ctop cuni wceq cbs cfv eqcomi cts istps mpbir2an ) CHIBJIABKL
      ABCCMNADOCPNBEOQGFR $.
      $( [20-Oct-2012] $)
  $}

  ${
    top2tps.j $e |- J = ( TopOpen ` K ) $.
    top2tps.p $e |- ( J e. Top -> ph ) $.
    $( Convert a topology theorem to a topological space theorem.  The second
       hypothesis is any existing theorem about ` J ` . $)
    top2tps $p |- ( K e. TopSp -> ph ) $=
      ( ctps wcel ctop cbs cfv cuni wceq wa eqid istps adantr sylbi ) CFGBHGZCI
      JZBKLZMASBCSNDORATEPQ $.
      $( [23-Oct-2012] $)
  $}

  ${
    eltps.a $e |- A e. _V $.
    eltps.j $e |- J e. _V $.
    eltps.k $e |- K
         = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , J >. } $.
    $( Express the predicate "is a topological space" using a constructed
       structure. $)
    eltps $p |- ( K e. TopSp <-> ( J e. Top /\ A = U. J ) ) $=
      ( cvv wcel cbs cfv wceq tpsbase ax-mp cts tpstopset istps ) ABCAGHACIJKDA
      BCGFLMBGHBCNJKEABCGFOMP $.
      $( [20-Oct-2012] $)
  $}

  ${
    $d a j A $.  $d j J $.
    eltpsg.k $e |- K
         = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , J >. } $.
    $( Express the predicate "is a topological space" using a constructed
       structure. $)
    eltpsg $p |- ( ( A e. B /\ J e. C )
                  -> ( K e. TopSp <-> ( J e. Top /\ A = U. J ) ) ) $=
      ( va vj ctps wcel cnx cbs cfv cop cts cpr wa ctop cuni wceq eleq1i cv wb
      opeq2 preq1d eleq1d eqeq1 anbi2d bibi12d preq2d eleq1 unieq eqeq2d
      anbi12d visset eqid eltps vtocl2g syl5bb ) EIJKLMZANZKOMZDNZPZIJZABJDCJQD
      RJZADSZTZQZEVDIFUAUTGUBZNZVBHUBZNZPZIJZVLRJZVJVLSZTZQZUCVAVMPZIJZVPAVQTZQ
      ZUCVEVIUCGHADBCVJATZVOWAVSWCWDVNVTIWDVKVAVMVJAUTUDUEUFWDVRWBVPVJAVQUGUHUI
      VLDTZWAVEWCVIWEVTVDIWEVMVCVAVLDVBUDUJUFWEVPVFWBVHVLDRUKWEVQVGAVLDULUMUNUI
      VJVLVNGUOHUOVNUPUQURUS $.
      $( [20-Oct-2012] $)
  $}

  ${
    eltpsi.k $e |- K
         = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , J >. } $.
    eltpsi.u $e |- A = U. J $.
    eltpsi.j $e |- J e. Top $.
    $( Properties that determine a topological space from a construction (using
       no explicit indices). $)
    eltpsi $p |- K e. TopSp $=
      ( ctps wcel ctop cuni wceq cvv elisseti uniex eqeltri eltps mpbir2an ) CG
      HBIHABJZKABCARLEBBIFMZNOSDPFEQ $.
      $( [20-Oct-2012] $)
  $}

  ${
    eltopspg.k $e |- K
         = { <. ( Base ` ndx ) , U. J >. , <. ( TopOpen ` ndx ) , J >. } $.
    $( Construct a topological space from a topology and vice-versa.  We say
       that ` A ` is a topology on ` U. A ` . $)
    eltopspg $p |- ( J e. V -> ( K e. TopSp <-> J e. Top ) ) $=
      ( wcel ctps ctop cuni wceq wa cvv wb uniexg eltpsg mpancom eqid biantru
      syl6bbr ) ACEZBFEZAGEZAHZUBIZJZUAUBKESTUDLACMUBKCABDNOUCUAUBPQR $.
      $( [20-Oct-2012] $)
  $}

  ${
    eltopsp.j $e |- J e. _V $.
    eltopsp.k $e |- K
         = { <. ( Base ` ndx ) , U. J >. , <. ( TopOpen ` ndx ) , J >. } $.
    $( Construct a topological space from a topology and vice-versa.  We say
       that ` A ` is a topology on ` U. A ` . $)
    eltopsp $p |- ( K e. TopSp <-> J e. Top ) $=
      ( cvv wcel ctps ctop wb eltopspg ax-mp ) AEFBGFAHFICABEDJK $.
      $( [20-Oct-2012] $)
  $}

  ${
    tpsprop.a $e |- ( Base ` K ) = A $.
    tpsprop.j $e |- ( TopOpen ` K ) = J $.
    tpsprop.b $e |- ( Base ` L ) = A $.
    tpsprop.t $e |- ( TopOpen ` L ) = J $.
    $( Any set with the same (relevant) properties as a given topological space
       is also a topological space (and vice-versa). $)
    tpsprop $p |- ( K e. TopSp <-> L e. TopSp ) $=
      ( ctps wcel ctop cuni wceq wa cbs cfv eqcomi cts istps bitr4i ) CIJBKJABL
      MNDIJABCCOPAEQCRPBFQSABDDOPAGQDRPBHQST $.
      $( [28-Oct-2012] $)
  $}

  ${
    tpsstr.a $e |- ( Base ` K ) = A $.
    tpsstr.j $e |- ( TopOpen ` K ) = J $.
    tpsstr.l $e |- L
          = { <. ( Base ` ndx ) , A >. , <. ( TopOpen ` ndx ) , J >. } $.
    $( Generalize a specific 2-element topological space ` L ` to show that any
       set ` K ` with the same (relevant) properties is also a topological
       space. $)
    tpsstr $p |- ( K e. TopSp <-> L e. TopSp ) $=
      ( ctps wcel ctop cuni wceq wa cbs cfv eqcomi cts istps cvv fvex eqeltrri
      eltps bitr4i ) CHIBJIABKLMDHIABCCNOZAEPCQOZBFPRABDUDASECNTUAUEBSFCQTUAGUB
      UC $.
      $( [28-Oct-2012] $)
  $}

