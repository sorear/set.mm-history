$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Topological spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new constant symbols and define their syntactical properties. $)

  $c Top $.
  $c TopSp $.
  $c Bases $.
  $c topGen $.

  $( Extend class notation with the class of all topologies. $)
  ctop $a class Top $.

  $( Extend class notation with the class of all topological spaces. $)
  ctps $a class TopSp $.

  $( Extend class notation with the class of all topological bases. $)
  ctb $a class Bases $.

  $( Extend class notation with a function that converts a basis to its
     corresponding topology. $)
  ctg $a class topGen $.

  ${
    $d x y z w v $.
    $( Define the (proper) class of all topologies.  See ~ istop2g for an
       alternate way to express finite intersection and ~ istps5 for a standard
       definition in terms of both members of a topological space. $)
    df-top $a |- Top = { x | ( A. y ( y C_ x -> U. y e. x ) /\
                           A. y e. x A. z e. x ( y i^i z ) e. x ) } $.

    $( Define the class of all topological spaces, each of which is an ordered
       pair the second of which is a topology on the first.  See ~ istps5 for a
       standard way to express a topological space. $)
    df-topsp $a |- TopSp = { <. x , y >. | ( y e. Top /\ x = U. y ) } $.

    $( Define the class of all topological bases.  Equivalent to definition of
       basis in [Munkres] p. 78 (see ~ isbasis2g ).  Note that "bases" is the
       plural of "basis." $)
    df-bases $a |- Bases = { x | A. y e. x A. z e. x ( y i^i z ) C_
                      U. ( x i^i ~P ( y i^i z ) ) } $.

    $( Define a function that converts a basis to its corresponding topology.
       Equivalent to the definition of a topology generated by a basis in
       [Munkres] p. 78 (see ~ tgval2 ).  See ~ tgval3 for an alternate
       expression for the value. $)
    df-topgen $a |- topGen = { <. x , y >. | ( x e. Bases /\ y =
       { z | z C_ U. ( x i^i ~P z ) } ) } $.
  $}

  ${
    $d x y z J $.
    $( Express the predicate " ` J ` is a topology."  Note:  In the literature,
       a topology is often represented by a script letter T, which resembles
       the letter J. This confusion has led to J being used by some authors -
       e.g.  K. D. Joshi, _Introduction to General Topology_ (1983), p. 114 -
       and it is convenient for us since we later use ` T ` to represent linear
       transformations (operators).  (Contributed by Stefan Allan,
       3-Mar-2006.) $)
    istopg $p |- ( J e. A -> ( J e. Top <-> ( A. x ( x C_ J -> U. x e. J )
                  /\ A. x e. J A. y e. J ( x i^i y ) e. J ) ) ) $=
      ( vz cv wss cuni wcel wi wal cin wral wa ctop wceq sseq2 eleq2 imbi12d
      albidv raleqd anbi12d df-top elab2g ) AFZEFZGZUEHZUFIZJZAKZUEBFLZUFIZBUFM
      ZAUFMZNUEDGZUHDIZJZAKZULDIZBDMZADMZNEDOCUFDPZUKUSUOVBVCUJURAVCUGUPUIUQUFD
      UEQUFDUHRSTUNVAAUFDUMUTBUFDUFDULRUAUAUBEABUCUD $.
      $( [18-Jul-2006] $) $( [3-Mar-2006] $)

    $( Express the predicate " ` J ` is a topology," using "the intersection of
       the elements of any finite subcollection" instead of the intersection of
       any two elements. $)
    istop2g $p |- ( J e. A -> ( J e. Top <-> ( A. x ( x C_ J -> U. x e. J )
         /\ A. x ( ( x C_ J /\ x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( vy wcel ctop cv wss cuni wi wal cin wral wa c0 wne cfn w3a cint istopg
      fiint anbi2i syl6bb ) CBECFEAGZCHZUDICEJAKZUDDGLCEDCMACMZNUFUEUDOPUDQERUD
      SCEJAKZNADBCTUGUHUFADCUAUBUC $.
      $( [23-Dec-2008] $) $( [19-Jul-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y J $.
    $( The union of a subset of a topology is an open set.  (Contributed by
       Stefan Allan, 27-Feb-2006.) $)
    uniopn $p |- ( ( J e. Top /\ A C_ J ) -> U. A e. J ) $=
      ( vx vy ctop wcel wss cuni cv wi wal cin wral wa istopg ibi pm3.26d cpw
      elpw2g biimpar wceq sseq1 unieq eleq1d imbi12d cla4gv syl com23 ex
      pm2.43d mpid imp ) BEFZABGZAHZBFZUMUNCIZBGZUQHZBFZJZCKZUPUMVBUQDILBFDBMCB
      MZUMVBVCNCDEBOPQUMUNVBUPJZUMUNUNVDJUMUNNZVBUNUPVEABRZFZVBUNUPJZJUMVGUNABE
      STVAVHCAVFUQAUAZURUNUTUPUQABUBVIUSUOBUQAUCUDUEUFUGUHUIUJUKUL $.
      $( [18-Jul-2006] $) $( [27-Feb-2006] $)

    $( The indexed union of a subset of a topology is an open set. $)
    iunopn $p |- ( ( J e. Top /\ A. x e. A B e. J ) -> U_ x e. A B e. J ) $=
      ( vy ctop wcel wral wa ciun cv wceq wrex cab cuni dfiun2g adantl wss
      uniopn uniiunlem ibi sylan2 eqeltrd ) DFGZCDGABHZIABCJZEKCLABMENZOZDUEUFU
      HLUDAEBCDPQUDUGDRZUHDGUEUGDSUEUIAEBCDDTUAUBUC $.
      $( [8-Oct-2006] $) $( [5-Oct-2006] $)

    $( The intersection of two open sets of a topology is also an open set. $)
    inopn $p |- ( ( J e. Top /\ A e. J /\ B e. J ) -> ( A i^i B ) e. J ) $=
      ( vx vy ctop wcel cin wa cv wral wceq ineq1 eleq1d ineq2 rcla42v wss
      cuni wi wal istopg ibi pm3.27d syl5com 3impib ) CFGZACGZBCGZABHZCGZUGUHID
      JZEJZHZCGZECKDCKZUJUFUNUJAULHZCGDEABCCUKALUMUPCUKAULMNULBLUPUICULBAONPUFU
      KCQUKRCGSDTZUOUFUQUOIDEFCUAUBUCUDUE $.
      $( [18-Jul-2006] $) $( [17-Jul-2006] $)
  $}

  $( The empty set is an open subset of a topology.  (Contributed by Stefan
     Allan, 27-Feb-2006.) $)
  0opn $p |- ( J e. Top -> (/) e. J ) $=
    ( ctop wcel c0 cuni wss 0ss uniopn mpan2 uni0 syl5eqelr ) ABCZDEZADLDAFMACA
    GDAHIJK $.
    $( [18-Jul-2006] $) $( [27-Feb-2006] $)

  ${
    1open.1 $e |- X = U. J $.
    $( The underlying set of a topology is an open set. $)
    topopn $p |- ( J e. Top -> X e. J ) $=
      ( ctop wcel cuni wss ssid uniopn mpan2 syl5eqel ) ADEZAFZABLAAGMAEAHAAIJC
      K $.
      $( [10-Oct-2006] $) $( [17-Jul-2006] $)

    $( A member of a topology is a subset of its underlying set. $)
    eltopss $p |- ( ( J e. Top /\ A e. J ) -> A C_ X ) $=
      ( wcel wss ctop cuni elssuni syl6ssr adantl ) ABEZACFBGELABHCABIDJK $.
      $( [16-Oct-2006] $) $( [12-Sep-2006] $)
  $}

  ${
    $d x y A $.  $d x y J $.
    $( Construct a topological space from a topology and vice-versa.  We say
       that ` A ` is a topology on ` U. A ` .  (This could be proved more
       efficiently from ~ istps , but the proof here does not require the Axiom
       of Regularity.) $)
    eltopsp $p |- ( <. U. J , J >. e. TopSp <-> J e. Top ) $=
      ( vy vx cuni cop ctps wcel cvv wa ctop wbr df-br wrel cv wceq copab
      relopab df-topsp releqi mpbir brrelexi sylbir uniexb sylibr jca uniexg
      elisset eqeq1 anbi2d eleq1 unieq eqeq2d anbi12d opelopabg eleq2i eqid
      biantru 3bitr4g pm5.21nii ) ADZAEZFGZUTHGZAHGZIZAJGZVBVCVDVBUTAFKVCUTAFLU
      TAFFMBNZJGZCNZVGDZOZIZCBPZMVLCBQFVMCBRZSTUAUBZVBVCVDVOAUCUDUEVFVCVDAJUFAJ
      UGUEVEVAVMGVFUTUTOZIZVBVFVLVHUTVJOZIVQCBUTAHHVIUTOVKVRVHVIUTVJUHUIVGAOZVH
      VFVRVPVGAJUJVSVJUTUTVGAUKULUMUNFVMVAVNUOVPVFUTUPUQURUS $.
      $( [14-Jul-2006] $) $( [8-Mar-2006] $)

    $( Existence implied by membership in a topological space.  This technical
       lemma, which can help eliminate unnecessary antecedents, uses the Axiom
       of Regularity (via ~ elirr ) along with definitional tricks. $)
    tpsex $p |- ( <. A , J >. e. TopSp -> ( A e. _V /\ J e. _V ) ) $=
      ( vy vx cop ctps wcel cvv wbr df-br wrel cv ctop cuni wceq wa copab
      relopab df-topsp releqi mpbir brrelexi sylbir wn elirr pm3.27 eqid
      topopn adantr eqeltrd mto jca elisset eqeq1 anbi2d eleq1 unieq eqeq2d
      anbi12d opelopabg eleq2i syl5bb pm5.21nii mtbir opprc2 eleq1d mtbiri
      con4i ) ABEZFGZAHGZBHGZVJABFIVKABFJABFFKCLZMGZDLZVMNZOZPZDCQZKVRDCRFVSDCS
      ZTUAZUBUCVLVJVLUDZVJAAEZFGZWDAMGZAANZOZPZWHAAGAUEWHAWFAWEWGUFWEWFAGWGAWFW
      FUGUHUIUJUKWDVKVKPZWHWDVKVKWDAAFIVKAAFJAAFWAUBUCZWJULWEWIWGWEVKVKAMUMZWKU
      LUIWIWCVSGWHWDVRVNAVPOZPWHDCAAHHVOAOVQWLVNVOAVPUNUOVMAOZVNWEWLWGVMAMUPWMV
      PWFAVMAUQURUSUTFVSWCVTVAVBVCVDWBVIWCFABVEVFVGVHUL $.
      $( [19-Jul-2006] $) $( [18-Jul-2006] $)

    $( Express the predicate "is a topological space." $)
    istps $p |- ( <. A , J >. e. TopSp <-> ( J e. Top /\ A = U. J ) ) $=
      ( vy vx cop ctps wcel cvv wa ctop cuni wceq tpsex pm3.27 uniexg adantr
      eqeltrd elisset jca cv copab eqeq1 anbi2d eleq1 unieq eqeq2d anbi12d
      opelopabg df-topsp eleq2i syl5bb pm5.21nii ) ABEZFGZAHGZBHGZIZBJGZABKZLZI
      ZABMVAUOUPVAAUSHURUTNURUSHGUTBJOPQURUPUTBJRPSUQUMCTZJGZDTZVBKZLZIZDCUAZGV
      AUNVGVCAVELZIVADCABHHVDALVFVIVCVDAVEUBUCVBBLZVCURVIUTVBBJUDVJVEUSAVBBUEUF
      UGUHFVHUMDCUIUJUKUL $.
      $( [20-Jul-2006] $) $( [18-Jul-2006] $)

    $( Express the predicate "is a topological space." $)
    istps2 $p |- ( <. A , J >. e. TopSp <-> ( ( J e. Top /\ J C_ ~P A )
                  /\ ( (/) e. J /\ A e. J ) ) ) $=
      ( ctop wcel cuni wceq wa cpw wss c0 cop ctps eqimss adantl 0opn adantr
      pm3.27 eqid topopn eqeltrrd jca32 ex unissel adantrl impbid1 eqcom
      sspwuni anbi1i 3bitr4g pm5.32i istps anass 3bitr4i ) BCDZABEZFZGUNBAHIZJB
      DZABDZGZGZGABKLDUNUQGUTGUNUPVAUNUOAFZUOAIZUTGZUPVAUNVBVDUNVBVDUNVBGZVCURU
      SVBVCUNUOAMNUNURVBBOPVEUOABUNVBQUNUOBDVBBUOUORSPTUAUBVCUSVBURBAUCUDUEAUOU
      FUQVCUTBAUGUHUIUJABUKUNUQUTULUM $.
      $( [28-Jul-2006] $) $( [18-Jul-2006] $)

    $( A standard textbook definition of a topological space. $)
    istps3 $p |- ( <. A , J >. e. TopSp <-> ( ( J C_ ~P A /\ (/) e. J /\
                 A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\
                 A. x e. J A. y e. J ( x i^i y ) e. J ) ) ) $=
      ( cop ctps wcel ctop cpw wss wa c0 w3a cv cuni wi wal cin wral istps2
      anass ancom 3anass anbi1i cvv wb ssexg pwexg sylan2 3adant2 istopg syl
      pm5.32i 3bitr2i 3bitri ) CDEFGDHGZDCIZJZKLDGZCDGZKZKUPURVAKZKZURUSUTMZANZ
      DJVEODGPAQVEBNRDGBDSADSKZKZCDTUPURVAUAVCVBUPKVDUPKVGUPVBUBVDVBUPURUSUTUCU
      DVDUPVFVDDUEGZUPVFUFURUTVHUSURUQUEGVHUTDUQUEUGCDUHUIUJABUEDUKULUMUNUO $.
      $( [3-Aug-2006] $) $( [18-Jul-2006] $)

    $( A standard textbook definition of a topological space. $)
    istps4 $p |- ( <. A , J >. e. TopSp <-> ( ( J C_ ~P A /\ (/) e. J /\
       A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\ A. x ( ( x C_ J /\
       x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( vy cop ctps wcel cpw wss c0 w3a cv cuni wi wal cin wral wa wne cfn
      cint istps3 fiint anbi2i bitri ) BCEFGCBHIJCGBCGKZALZCIZUGMCGNAOZUGDLPCGD
      CQACQZRZRUFUIUHUGJSUGTGKUGUACGNAOZRZRADBCUBUKUMUFUJULUIADCUCUDUDUE $.
      $( [30-Dec-2008] $) $( [19-Jul-2006] $)

    $( A standard textbook definition of a topological space ` <. A , J >. ` :
       a topology on ` A ` is a collection ` J ` of subsets of ` A ` such that
       ` (/) ` and ` A ` are in ` J ` and that is closed under union and finite
       intersection.  Definition of topological space in [Munkres] p. 76. $)
    istps5 $p |- ( <. A , J >. e. TopSp <-> ( ( A. x e. J x C_ A /\ (/) e. J
                 /\ A e. J ) /\ ( A. x ( x C_ J -> U. x e. J ) /\
           A. x ( ( x C_ J /\ x =/= (/) /\ x e. Fin ) -> |^| x e. J ) ) ) ) $=
      ( cop ctps wcel cpw wss c0 w3a cv cuni wi wal wne cfn cint wa wral
      istps4 pwssb 3anbi1i anbi1i bitri ) BCDEFCBGHZICFZBCFZJZAKZCHZUILCFMANUJU
      IIOUIPFJUIQCFMANRZRUIBHACSZUFUGJZUKRABCTUHUMUKUEULUFUGACBUAUBUCUD $.
      $( [23-Apr-2009] $) $( [19-Jul-2006] $)
  $}

