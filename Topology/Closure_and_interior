$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Closure and interior
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c int $.
  $c cls $.
  $c Clsd $.

  $( Extend class notation with the set of closed sets of a topology. $)
  ccld $a class Clsd $.

  $( Extend class notation with interior of a subset of a topology base set. $)
  cnt $a class int $.

  $( Extend class notation with closure of a subset of a topology base set. $)
  ccl $a class cls $.

  ${
    $d j x y $.
    $( Define a function on topologies whose value is the set of closed sets of
       the topology. $)
    df-cld $a |- Clsd =
                 ( j e. Top |-> { x e. ~P U. j | ( U. j \ x ) e. j } ) $.

    $( Define a function on topologies whose value is the interior function on
       the subsets of the base set.  See ~ ntrval . $)
    df-ntr $a |- int = ( j e. Top |->
                        ( x e. ~P U. j |-> U. ( j i^i ~P x ) ) ) $.

    $( Define a function on topologies whose value is the closure function on
       the subsets of the base set.  See ~ clsval . $)
    df-cls $a |- cls = ( j e. Top |-> ( x e. ~P U. j |->
                        |^| { y e. ( Clsd ` j ) | x C_ y } ) ) $.
  $}

  ${
    $d x y j J $.  $d x j X $.
    cldval.1 $e |- X = U. J $.
    $( The set of closed sets of a topology.  (Note that the set of open sets
       is just the topology itself, so we don't have a separate definition.) $)
    cldval $p |- ( J e. Top ->
               ( Clsd ` J ) = { x e. ~P X | ( X \ x ) e. J } ) $=
      ( vj ctop wcel cv cdif cpw crab cvv ccld cfv wceq topopn rabexg 3syl cuni
      pwexg unieq syl6eqr syl wb difeq1d eleq12 mpancom rabeqbidv df-cld fvmptg
      pweq mpdan ) BFGZCAHZIZBGZACJZKZLGZBMNUROUMCBGUQLGUSBCDPCBTUPAUQLQREBEHZS
      ZUNIZUTGZAVAJZKURFLMUTBOZVCUPAVDUQVEVACOVDUQOVEVABSCUTBUADUBZVACUKUCVBUOO
      VEVCUPUDVEVACUNVFUEVBUOUTBUFUGUHAEUIUJUL $.
      $( [11-Nov-2013] $) $( [2-Oct-2006] $)

    $( The interior function on the subsets of a topology's base set. $)
    ntrfval $p |- ( J e. Top -> ( int ` J ) = ( x e. ~P X |->
                     U. ( J i^i ~P x ) ) ) $=
      ( vj ctop wcel cpw cv cin cuni cmpt cvv cnt wceq topopn pwexg mptexg 3syl
      cfv unieq syl6eqr pweq syl ineq1 unieqd mpteq12dv df-ntr fvmptg mpdan ) B
      FGZACHZBAIHZJZKZLZMGZBNTUPOUKCBGULMGUQBCDPCBQAULUOMRSEBAEIZKZHZURUMJZKZLU
      PFMNURBOZAUTVBULUOVCUSCOUTULOVCUSBKCURBUADUBUSCUCUDVCVAUNURBUMUEUFUGAEUHU
      IUJ $.
      $( [11-Nov-2013] $) $( [10-Sep-2006] $)

    $( The closure function on the subsets of a topology's base set. $)
    clsfval $p |- ( J e. Top -> ( cls ` J ) = ( x e. ~P X |->
                     |^| { y e. ( Clsd ` J ) | x C_ y } ) ) $=
      ( vj ctop wcel cpw cv ccld cfv crab cint cmpt cvv ccl wceq cuni syl pwexg
      topopn mptexg 3syl unieq syl6eqr pweq fveq2 rabeq inteqd mpteq12dv df-cls
      wss fvmptg mpdan ) CGHZADIZAJBJUMZBCKLZMZNZOZPHZCQLVBRUPDCHUQPHVCCDEUBDCU
      AAUQVAPUCUDFCAFJZSZIZURBVDKLZMZNZOVBGPQVDCRZAVFVIUQVAVJVEDRVFUQRVJVECSDVD
      CUEEUFVEDUGTVJVHUTVJVGUSRVHUTRVDCKUHURBVGUSUITUJUKABFULUNUO $.
      $( [11-Nov-2013] $) $( [3-Oct-2006] $)
  $}

  ${
    $d x y z J $.  $d x y z S $.  $d x y z X $.  $d x T $.
    iscld.1 $e |- X = U. J $.
    $( The predicate " ` S ` is a closed set." $)
    iscld $p |- ( J e. Top -> ( S e. ( Clsd ` J ) <->
                 ( S C_ X /\ ( X \ S ) e. J ) ) ) $=
      ( vx ctop wcel ccld cfv cpw cdif wa wss cv crab cldval eleq2d wceq difeq2
      eleq1d elrab syl6bb wb topopn elpw2g syl anbi1d bitrd ) BFGZABHIZGZACJZGZ
      CAKZBGZLZACMZUOLUIUKACENZKZBGZEULOZGUPUIUJVAAEBCDPQUTUOEAULURARUSUNBURACS
      TUAUBUIUMUQUOUICBGUMUQUCBCDUDACBUEUFUGUH $.
      $( [11-Nov-2013] $) $( [2-Oct-2006] $)

    $( A subset of the underlying set of a topology is closed iff its
       complement is open. $)
    iscld2 $p |- ( ( J e. Top /\ S C_ X ) ->
                  ( S e. ( Clsd ` J ) <-> ( X \ S ) e. J ) ) $=
      ( ctop wcel ccld cfv wss cdif wa iscld ibar bicomd sylan9bb ) BEFABGHFACI
      ZCAJBFZKZPQABCDLPQRPQMNO $.
      $( [4-Oct-2006] $)

    $( A closed set is a subset of the underlying set of a topology. $)
    cldss $p |- ( ( J e. Top /\ S e. ( Clsd ` J ) ) -> S C_ X ) $=
      ( ctop wcel ccld cfv wss cdif iscld simprbda ) BEFABGHFACICAJBFABCDKL $.
      $( [5-Oct-2006] $)
    $( The set of closed sets is contained in the powerset of the base.
       (Contributed by Mario Carneiro, 6-Jan-2014.) $)
    cldss2 $p |- ( J e. Top -> ( Clsd ` J ) C_ ~P X ) $=
      ( vx ctop wcel ccld cfv cpw cv wa wss cldss vex elpw sylibr ex ssrdv ) AE
      FZDAGHZBIZSDJZTFZUBUAFZSUCKUBBLUDUBABCMUBBDNOPQR $.
      $( [6-Jan-2014] $)

    $( The complement of a closed set is open. $)
    cldopn $p |- ( ( J e. Top /\ S e. ( Clsd ` J ) ) -> ( X \ S ) e. J ) $=
      ( ctop wcel ccld cfv wss cdif iscld simplbda ) BEFABGHFACICAJBFABCDKL $.
      $( [5-Oct-2006] $)

    $( A subset of the underlying set of a topology is open iff its complement
       is closed. $)
    isopn2 $p |- ( ( J e. Top /\ S C_ X ) ->
                  ( S e. J <-> ( X \ S ) e. ( Clsd ` J ) ) ) $=
      ( ctop wcel wss wa cdif ccld cfv wb difss iscld2 mpan2 wceq biimpi eleq1d
      dfss4 sylan9bb bicomd ) BEFZACGZHCAIZBJKFZABFZUBUECUDIZBFZUCUFUBUDCGUEUHL
      CAMUDBCDNOUCUGABUCUGAPACSQRTUA $.
      $( [4-Oct-2006] $)

    $( The complement of an open set is closed. $)
    opncld $p |- ( ( J e. Top /\ S e. J ) -> ( X \ S ) e. ( Clsd ` J ) ) $=
      ( ctop wcel wa cdif ccld cfv simpr wss wb eltopss isopn2 syldan mpbid ) B
      EFZABFZGSCAHBIJFZRSKRSACLSTMABCDNABCDOPQ $.
      $( [6-Oct-2006] $)
    $( The difference of a closed set with an open set is open.  (Contributed
       by Mario Carneiro, 6-Jan-2014.) $)
    difopn $p |- ( ( J e. Top /\ A e. J /\ B e. ( Clsd ` J ) ) ->
                   ( A \ B ) e. J ) $=
      ( ctop wcel ccld cfv w3a cin cdif wceq cuni elssuni syl6sseqr df-ss sylib
      wss 3ad2ant2 difeq1d indif2 cldopn 3adant2 syld3an3 syl5eqelr eqeltrrd
      inopn ) CFGZACGZBCHIGZJZADKZBLZABLCULUMABULADSZUMAMUJUIUOUKUJACNDACOEPTAD
      QRUAULUNADBLZKZCADBUBUIUJUKUPCGZUQCGUIUKURUJBCDEUCUDAUPCUHUEUFUG $.
      $( [6-Jan-2014] $)

    $( The underlying set of a topology is closed.  Part of Theorem 6.1(1) of
       [Munkres] p. 93. $)
    topcld $p |- ( J e. Top -> X e. ( Clsd ` J ) ) $=
      ( ctop wcel ccld cfv wss cdif wa c0 difid 0opn syl5eqel ssid jctil mpbird
      iscld ) ADEZBAFGEBBHZBBIZAEZJSUBTSUAKABLAMNBOPBABCRQ $.
      $( [3-Oct-2006] $)

    $( The interior of a subset of a topology's base set is the union of all
       the open sets it includes.  Definition of interior of [Munkres]
       p. 94. $)
    ntrval $p |- ( ( J e. Top /\ S C_ X ) -> ( ( int ` J ) ` S ) =
                 U. ( J i^i ~P S ) ) $=
      ( vx ctop wcel wss wa cnt cfv cpw cv cin cuni cmpt wceq adantr cvv syl wb
      ntrfval fveq1d topopn elpw2g inex1g uniexg pweq ineq2d unieqd eqid fvmptg
      biimpar syl2anc eqtrd ) BFGZACHZIZABJKZKZAECLZBEMZLZNZOZPZKZBALZNZOZUPUTV
      GQUQUPAUSVFEBCDUBUCRURAVAGZVJSGZVGVJQUPVKUQUPCBGVKUQUABCDUDACBUETUMURVISG
      ZVLUPVMUQBVHFUFRVISUGTEAVEVJVASVFVBAQZVDVIVNVCVHBVBAUHUIUJVFUKULUNUO $.
      $( [11-Nov-2013] $) $( [10-Sep-2006] $)

    $( The closure of a subset of a topology's base set is the intersection of
       all the closed sets that include it.  Definition of closure of [Munkres]
       p. 94. $)
    clsval $p |- ( ( J e. Top /\ S C_ X ) -> ( ( cls ` J ) ` S ) =
                 |^| { x e. ( Clsd ` J ) | S C_ x } ) $=
      ( vy ctop wcel wss wa ccl cfv cpw cv ccld crab cint cmpt wceq cvv clsfval
      fveq1d adantr wb topopn elpw2g biimpar wrex topcld sseq2 rcla4ev intexrab
      syl sylan sylib sseq1 rabbidv inteqd eqid fvmptg syl2anc eqtrd ) CGHZBDIZ
      JZBCKLZLZBFDMZFNZANZIZACOLZPZQZRZLZBVJIZAVLPZQZVCVGVPSVDVCBVFVOFACDEUAUBU
      CVEBVHHZVSTHZVPVSSVCVTVDVCDCHVTVDUDCDEUEBDCUFUMUGVEVQAVLUHZWAVCDVLHVDWBCD
      EUIVQVDADVLVJDBUJUKUNVQAVLULUOFBVNVSVHTVOVIBSZVMVRWCVKVQAVLVIBVJUPUQURVOU
      SUTVAVB $.
      $( [11-Nov-2013] $) $( [10-Sep-2006] $)
  $}

  $( The empty set is closed.  Part of Theorem 6.1(1) of [Munkres] p. 93. $)
  0cld $p |- ( J e. Top -> (/) e. ( Clsd ` J ) ) $=
    ( ctop wcel c0 ccld cfv cuni cdif dif0 topopn wss wb 0ss eqid iscld2 mpbird
    mpan2 ) ABCZDAEFCZAGZDHZACZAUATIJRDTKSUBLTMDATTNOQP $.
    $( [4-Oct-2006] $)

  $( Closed intervals are closed sets of the standard topology on ` RR ` .
     (Contributed by FL, 14-Sep-2007.) $)
  icccld $p |- ( ( A e. RR /\ B e. RR ) -> ( A [,] B ) e.
        ( Clsd ` ( topGen ` ran (,) ) ) ) $=
    ( cr wcel wa cicc co cioo crn ctg ccld cdif cmnf cpnf retop iooretop pm3.2i
    cfv wss ovex cpr cuni difreicc ctop prss mpbi a1i uniopn eqeltrd wb iccssre
    syl uniretop iscld2 sylancr mpbird ) ACDBCDEZABFGZHIJRZKRDZCURLZUSDZUQVAMAH
    GZBNHGZUAZUBZUSABUCUQUSUDDZVEUSSZEZVFUSDVIUQVGVHOVCUSDZVDUSDZEVHVJVKMAPBNPQ
    VCVDUSMAHTBNHTUEUFQUGVEUSUHULUIUQVGURCSUTVBUJOABUKURUSCUMUNUOUP $.
    $( [14-Sep-2007] $)

  $( A closed interval is a closed set.  (Contributed by FL, 28-Jan-2009.)
     Obsolete; use ~ icccld . $)
  clint3OLD $p |- ( ( A e. RR /\ B e. RR ) -> ( A [,] B ) e.
     ( Clsd ` ( topGen ` ran (,) ) ) ) $=
    ( cr wcel wa cicc co wss cdif cioo crn ctg cfv ccld iccssre cmnf cpnf retop
    iooretop wb cpr cuni difreicc pm3.2i a1i prssg mpbii uniopn sylancr eqeltrd
    ctop syl uniretop iscld ax-mp sylanbrc ) ACDBCDEZABFGZCHZCURIZJKLMZDZURVANM
    DZABOUQUTPAJGZBQJGZUAZUBZVAABUCUQVAUKDZVFVAHZVGVADRUQVDVADZVEVADZEZVIVJVKPA
    SBQSUDZUQVLVLVITVLUQVMUEVDVEVAVAVAUFULUGVFVAUHUIUJVHVCUSVBETRURVACUMUNUOUP
    $.
    $( [5-Jan-2009] $)

  ${
    $d x A $.  $d x J $.  $d x S $.
    $( The indexed intersection of a collection ` B ( x ) ` of closed sets is
       closed.  Theorem 6.1(2) of [Munkres] p. 93. $)
    iincld $p |- ( ( J e. Top /\ A =/= (/) /\ A. x e. A B e. ( Clsd ` J ) ) ->
                  |^|_ x e. A B e. ( Clsd ` J ) ) $=
      ( ctop wcel c0 wne ccld cfv wral w3a ciin cdif wa wss ralimdv imp 3adant2
      wceq cuni ciun eqid cldss dfss4 syl6ib iineq2 syl iindif2 3ad2ant2 eqtr3d
      ex iscld simpr syl6bi iunopn syldan wb iunss cv a1i mprgbir isopn2 adantr
      difss mpan2 mpbid eqeltrd ) DEFZBGHZCDIJZFZABKZLZABCMZDUAZABVPCNZUBZNZVKV
      NABVPVQNZMZVOVSVIVMWAVOTZVJVIVMOZVTCTZABKZWBVIVMWEVIVLWDABVIVLCVPPZWDVIVL
      WFCDVPVPUCZUDULCVPUEUFQRABVTCUGUHSVJVIWAVSTVMABVPVQUIUJUKVIVMVSVKFZVJWCVR
      DFZWHVIVMVQDFZABKZWIVIVMWKVIVLWJABVIVLWFWJOWJCDVPWGUMWFWJUNUOQRABVQDUPUQV
      IWIWHURZVMVIVRVPPZWLWMVQVPPZABABVQVPUSWNAUTBFVPCVEVAVBVRDVPWGVCVFVDVGSVH
      $.
      $( [5-Oct-2006] $)

    $( The intersection of a set of closed sets is closed. $)
    intcld $p |- ( ( J e. Top /\ A =/= (/) /\ A C_ ( Clsd ` J ) ) ->
                  |^| A e. ( Clsd ` J ) ) $=
      ( vx ctop wcel c0 wne ccld cfv wss w3a cint ciin intiin wral dfss3 iincld
      cv syl3an3b syl5eqel ) BDEZAFGZABHIZJZKALCACRZMZUCCANUDUAUBUEUCECAOUFUCEC
      AUCPCAUEBQST $.
      $( [5-Oct-2006] $)

    $( The union of two closed sets is closed.  Equivalent to Theorem 6.1(3) of
       [Munkres] p. 93. $)
    uncld $p |- ( ( J e. Top /\ A e. ( Clsd ` J ) /\ B e. ( Clsd ` J ) ) ->
                 ( A u. B ) e. ( Clsd ` J ) ) $=
      ( ctop wcel ccld cfv cun wa cuni cdif difundi eqid cldopn anim12i anandis
      cin syldan wss cldss inopn 3expb syl5eqel unss sylib iscld2 mpbird 3impb
      wb ) CDEZACFGZEZBUKEZABHZUKEZUJULUMIZIZUOCJZUNKZCEZUQUSURAKZURBKZQZCURABL
      UJUPVACEZVBCEZIZVCCEZUJULUMVFUJULIZVDUJUMIZVEACURURMZNBCURVJNOPUJVDVEVGVA
      VBCUAUBRUCUJUPUNURSZUOUTUIUQAURSZBURSZIZVKUJULUMVNVHVLVIVMACURVJTBCURVJTO
      PABURUDUEUNCURVJUFRUGUH $.
      $( [10-Jun-2007] $) $( [5-Oct-2006] $)

    $( A closed subset equals its own closure. $)
    cldcls $p |- ( ( J e. Top /\ S e. ( Clsd ` J ) ) ->
                 ( ( cls ` J ) ` S ) = S ) $=
      ( vx ctop wcel ccld cfv wa cuni wss wceq eqid cldss cv crab clsval intmin
      ccl cint sylan9eq an32s mpdan ) BDEZABFGZEZHABIZJZABRGGZAKZABUFUFLZMUCUGU
      EUIUCUGHUEUHACNJCUDOSACABUFUJPCAUDQTUAUB $.
      $( [15-Mar-2007] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( The intersection of two closed sets is closed.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    incld $p |- ( ( J e. Top /\ A e. ( Clsd ` J ) /\ B e. ( Clsd ` J ) )
                                      -> ( A i^i B ) e. ( Clsd ` J ) ) $=
      ( vx ctop wcel ccld cfv w3a cpr cint cin wceq intprg 3adant1 c0 wne simp1
      wss cv preq1 neeq1d vex prnz vtoclg 3ad2ant2 wa prssg ibi intcld eqeltrrd
      syl3anc ) CEFZACGHZFZBUNFZIZABJZKZABLZUNUOUPUSUTMUMABUNUNNOUQUMURPQZURUNS
      ZUSUNFUMUOUPRUOUMVAUPDTZBJZPQVADAUNVCAMVDURPVCABUAUBVCBDUCUDUEUFUOUPVBUMU
      OUPUGVBABUNUNUNUHUIOURCUJULUK $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y z J $.  $d x P $.  $d x y z S $.  $d x U $.  $d x y z X $.
    $d x T $.
    clscld.1 $e |- X = U. J $.
    $( The closure of a subset of a topology's underlying set is closed. $)
    clscld $p |- ( ( J e. Top /\ S C_ X ) ->
                 ( ( cls ` J ) ` S ) e. ( Clsd ` J ) ) $=
      ( vx ctop wcel wss wa ccl cfv cv ccld crab cint clsval wne simpl topcld
      c0 anim1i sseq2 elrab sylibr ne0i syl ssrab2 a1i intcld syl3anc eqeltrd )
      BFGZACHZIZABJKKAELZHZEBMKZNZOZUQEABCDPUNULURTQZURUQHZUSUQGULUMRUNCURGZUTU
      NCUQGZUMIVBULVCUMBCDSUAUPUMECUQUOCAUBUCUDURCUEUFVAUNUPEUQUGUHURBUIUJUK $.
      $( [4-Oct-2006] $)

    $( The interior of a subset of a topology's underlying set is open. $)
    ntropn $p |- ( ( J e. Top /\ S C_ X ) -> ( ( int ` J ) ` S ) e. J ) $=
      ( ctop wcel wss wa cnt cfv cpw cin cuni ntrval inss1 uniopn mpan2 eqeltrd
      adantr ) BEFZACGZHABIJJBAKZLZMZBABCDNTUDBFZUATUCBGUEBUBOUCBPQSR $.
      $( [11-Nov-2013] $) $( [11-Sep-2006] $)

    $( Express closure in terms of interior. $)
    clsval2 $p |- ( ( J e. Top /\ S C_ X ) -> ( ( cls ` J ) ` S ) =
                 ( X \ ( ( int ` J ) ` ( X \ S ) ) ) ) $=
      ( vz vx wcel wss wa cdif cv cfv cint c0 wceq adantr a1i elin 3syl cvv cpw
      ctop cin cuni ccld crab cnt ccl ciin ciun wne 0opn 0elpw sylanbrc iindif2
      ne0i wrex df-rab cldopn ad2ant2r sscon ad2antll wb topopn difexg ad2antrr
      elpwg mpbird cldss dfss4 sylib eqcomd difeq2 eqeq2d rcla4ev syl2anc simpl
      cab ex simplbi opncld syl2an simprbi adantl elpwi syl difss syl6ss simplr
      ssconb mpbid eleq1 sseq2 syl5ibrcom rexlimdva impbid abbidv syl5eq inteqd
      jca anbi12d wral a1d ralrimiv dfiin2g eqtr4d uniiun difeq2i ntrval sylan2
      3eqtr4rd difeq2d clsval ) BUBGZACHZIZCBCAJZUAZUCZUDZJZAEKZHZEBUELZUFZMZCX
      QBUGLLZJABUHLLXPFXSCFKZJZUIZCFXSYHUJZJZYFYAXPNXSGZXSNUKYJYLOXPNBGZNXRGZYM
      XNYNXOBULPYOXPXQUMQNBXRRUNXSNUPFXSCYHUOSXPYFYBYIOZFXSUQZEVRZMZYJXPYEYRXPY
      EYBYDGZYCIZEVRYRYCEYDURXPUUAYQEXPUUAYQXPUUAYQXPUUAIZCYBJZXSGZYBCUUCJZOZYQ
      UUBUUCBGZUUCXRGZUUDXNYTUUGXOYCYBBCDUSUTUUBUUHUUCXQHZYCUUIXPYTAYBCVAVBXNUU
      HUUIVCZXOUUAXNCBGZUUCTGUUJBCDVDZCYBBVEUUCXQTVGSVFVHUUCBXRRUNUUBUUEYBUUBYB
      CHZUUEYBOXNYTUUMXOYCYBBCDVIUTYBCVJVKVLYPUUFFUUCXSYHUUCOYIUUEYBYHUUCCVMVNV
      OVPVSXPYPUUAFXSXPYHXSGZIZUUAYPYIYDGZAYIHZIUUOUUPUUQXPXNYHBGZUUPUUNXNXOVQU
      UNUURYHXRGZYHBXRRZVTYHBCDWAWBUUOYHXQHZUUQUUOUUSUVAUUNUUSXPUUNUURUUSUUTWCW
      DYHXQWEWFZUUOYHCHXOUVAUUQVCUUOYHXQCUVBCAWGZWHXNXOUUNWIYHACWJVPWKWTYPYTUUP
      YCUUQYBYIYDWLYBYIAWMXAWNWOWPWQWRWSXNYJYSOZXOXNUUKYITGZFXSXBUVDUULUUKUVEFX
      SUUKUVEUUNCYHBVEXCXDFEXSYITXESPXFYAYLOXPXTYKCFXSXGXHQXKXPYGXTCXOXNXQCHZYG
      XTOUVFXOUVCQXQBCDXIXJXLEABCDXMXK $.
      $( [11-Nov-2013] $) $( [10-Sep-2006] $)

    $( Interior expressed in terms of closure. $)
    ntrval2 $p |- ( ( J e. Top /\ S C_ X ) ->
                 ( ( int ` J ) ` S ) = ( X \ ( ( cls ` J ) ` ( X \ S ) ) ) ) $=
      ( ctop wcel wss wa cdif ccl cfv cnt wceq difss clsval2 mpan2 adantr dfss4
      biimpi difeq2d fveq2d adantl eqtrd ntropn eltopss syldan sylib eqtr2d ) B
      EFZACGZHZCCAIZBJKKZICCABLKZKZIZIZUOUKUMUPCUKUMCCULIZUNKZIZUPUIUMUTMZUJUIU
      LCGVACANULBCDOPQUKUSUOCUJUSUOMUIUJURAUNUJURAMACRSUAUBTUCTUKUOCGZUQUOMUIUJ
      UOBFVBABCDUDUOBCDUEUFUOCRUGUH $.
      $( [1-Oct-2007] $)

    $( Subset relationship for closure. $)
    clsss $p |- ( ( J e. Top /\ S C_ X /\ T C_ S ) -> ( ( cls ` J ) ` T ) C_
                 ( ( cls ` J ) ` S ) ) $=
      ( vx ctop wcel wss w3a cv ccld cfv crab cint ccl wi sstr2 wceq clsval syl
      adantr ss2rabdv intss 3ad2ant3 impcom 3adant1 syl2anc 3adant3 3sstr4d
      simp1 ) CGHZADIZBAIZJZBFKZIZFCLMZNZOZAUPIZFURNZOZBCPMZMZAVDMZUNULUTVCIZUM
      UNVBUSIVGUNVAUQFURUNVAUQQUPURHBAUPRUBUCVBUSUDUAUEUOULBDIZVEUTSULUMUNUKUMU
      NVHULUNUMVHBADRUFUGFBCDETUHULUMVFVCSUNFACDETUIUJ $.
      $( [10-Feb-2007] $)

    $( Subset relationship for interior. $)
    ntrss $p |- ( ( J e. Top /\ S C_ X /\ T C_ S ) -> ( ( int ` J ) ` T ) C_
                 ( ( int ` J ) ` S ) ) $=
      ( ctop wcel wss cnt cfv wa cdif ccl sscon adantl difss jctil wceq ntrval2
      sylan2 clsss 3expb syl sstr2 impcom adantrr 3sstr4d 3impb ) CFGZADHZBAHZB
      CIJZJZAULJZHUIUJUKKZKZDDBLZCMJZJZLZDDALZURJZLZUMUNUPVBUSHZUTVCHUOUIUQDHZV
      AUQHZKVDUOVFVEUKVFUJBADNODBPQUIVEVFVDUQVACDEUAUBTVBUSDNUCUOUIBDHZUMUTRUKU
      JVGBADUDUEBCDESTUIUJUNVCRUKACDESUFUGUH $.
      $( [3-Oct-2007] $)

    $( A subset of a topology's underlying set is included in its closure. $)
    sscls $p |- ( ( J e. Top /\ S C_ X ) -> S C_ ( ( cls ` J ) ` S ) ) $=
      ( vx ctop wcel wss wa cv ccld cfv crab cint ccl ssintub clsval syl5sseqr
      ) BFGACHIAEJHEBKLZMNAABOLLEASPEABCDQR $.
      $( [22-Feb-2007] $)

    $( A subset includes its interior. $)
    ntrss2 $p |- ( ( J e. Top /\ S C_ X ) -> ( ( int ` J ) ` S ) C_ S ) $=
      ( ctop wcel wss wa cnt cfv cpw cin ntrval inss2 uniss ax-mp unipw sseqtri
      cuni a1i eqsstrd ) BEFACGHZABIJJBAKZLZSZAABCDMUEAGUBUEUCSZAUDUCGUEUFGBUCN
      UDUCOPAQRTUA $.
      $( [11-Nov-2013] $) $( [3-Oct-2007] $)

    $( An open subset of a set is a subset of the set's interior.  (Contributed
       by Jeff Hankins, 31-Aug-2009.) $)
    ssntr $p |- ( ( ( J e. Top /\ S C_ X ) /\ ( O e. J /\ O C_ S ) ) -> O C_ (
    ( int ` J ) ` S ) ) $=
      ( ctop wcel wss wa cpw cin cuni cnt cfv elin elpwg pm5.32i bitr2i elssuni
      sylbi adantl wceq ntrval adantr sseqtr4d ) BFGADHIZCBGZCAHZIZICBAJZKZLZAB
      MNNZUICULHZUFUICUKGZUNUOUGCUJGZIUICBUJOUGUPUHCABPQRCUKSTUAUFUMULUBUIABDEU
      CUDUE $.
      $( [11-Nov-2013] $) $( [31-Aug-2009] $)

    $( The closure of a subset of a topological space is included in the
       space. $)
    clsss3 $p |- ( ( J e. Top /\ S C_ X ) -> ( ( cls ` J ) ` S ) C_ X ) $=
      ( ctop wcel wss ccl cfv ccld clscld cldss syldan ) BEFACGABHIIZBJIFNCGABC
      DKNBCDLM $.
      $( [26-Feb-2007] $)

    $( The interior of a subset of a topological space is included in the
       space. $)
    ntrss3 $p |- ( ( J e. Top /\ S C_ X ) -> ( ( int ` J ) ` S ) C_ X ) $=
      ( ctop wcel wss cnt cfv ntropn eltopss syldan ) BEFACGABHIIZBFMCGABCDJMBC
      DKL $.
      $( [1-Oct-2007] $)

    $( The complement of a closure is open. $)
    cmclsopn $p |- ( ( J e. Top /\ S C_ X ) ->
                 ( X \ ( ( cls ` J ) ` S ) ) e. J ) $=
      ( ctop wcel wss wa ccl cfv cdif clsval2 difeq2d wceq difss ntropn eltopss
      cnt mpan2 eqeltrd mpdan dfss4 sylib adantr ) BEFZACGZHZCABIJJZKCCCAKZBRJJ
      ZKZKZBUGUHUKCABCDLMUEULBFUFUEULUJBUEUJCGZULUJNUEUJBFZUMUEUICGUNCAOUIBCDPS
      ZUJBCDQUAUJCUBUCUOTUDT $.
      $( [11-Sep-2006] $)

    $( The complement of an interior is closed. $)
    cmntrcld $p |- ( ( J e. Top /\ S C_ X ) ->
                 ( X \ ( ( int ` J ) ` S ) ) e. ( Clsd ` J ) ) $=
      ( ctop wcel wss wa cnt cfv cdif ccl ccld ntrval2 difeq2d wceq difss mpan2
      clsss3 eqeltrd dfss4 sylib clscld adantr ) BEFZACGZHZCABIJJZKCCCAKZBLJJZK
      ZKZBMJZUGUHUKCABCDNOUEULUMFUFUEULUJUMUEUJCGZULUJPUEUICGZUNCAQZUIBCDSRUJCU
      AUBUEUOUJUMFUPUIBCDUCRTUDT $.
      $( [1-Oct-2007] $)

    $( A subset is closed iff it equals its own closure. $)
    iscld3 $p |- ( ( J e. Top /\ S C_ X ) -> ( S e. ( Clsd ` J ) <->
                 ( ( cls ` J ) ` S ) = S ) ) $=
      ( ctop wcel wss wa ccld cfv ccl wceq wi cldcls ex adantr clscld syl5ibcom
      eleq1 impbid ) BEFZACGZHZABIJZFZABKJJZALZUAUEUGMUBUAUEUGABNOPUCUFUDFUGUEA
      BCDQUFAUDSRT $.
      $( [2-Oct-2006] $)

    $( A subset is closed iff it contains its own closure. $)
    iscld4 $p |- ( ( J e. Top /\ S C_ X ) -> ( S e. ( Clsd ` J ) <->
                 ( ( cls ` J ) ` S ) C_ S ) ) $=
      ( ctop wcel wss wa ccld cfv ccl wceq iscld3 sscls biantrud syl6rbbr bitrd
      eqss ) BEFACGHZABIJFABKJJZALZTAGZABCDMSUBUBATGZHUASUCUBABCDNOTARPQ $.
      $( [31-Jan-2008] $)

    $( A subset is open iff it equals its own interior. $)
    isopn3 $p |- ( ( J e. Top /\ S C_ X ) -> ( S e. J <->
                 ( ( int ` J ) ` S ) = S ) ) $=
      ( ctop wcel wss wa cnt cfv wceq cpw cuni ntrval inss2 uniss ax-mp sseqtri
      cin unipw a1i ssid elpw2g mpbiri elin sylanbrc elssuni syl eqssd sylan9eq
      id ex ntropn eleq1 syl5ibcom impbid ) BEFACGHZABFZABIJJZAKZUQURUTUQURUSBA
      LZSZMZAABCDNURVCAVCAGURVCVAMZAVBVAGVCVDGBVAOVBVAPQATRUAURAVBFZAVCGURURAVA
      FZVEURUKURVFAAGAUBAABUCUDABVAUEUFAVBUGUHUIUJULUQUSBFUTURABCDUMUSABUNUOUP
      $.
      $( [11-Nov-2013] $) $( [9-Oct-2006] $)

    $( The closure operation is idempotent. $)
    clsidm $p |- ( ( J e. Top /\ S C_ X ) ->
               ( ( cls ` J ) ` ( ( cls ` J ) ` S ) ) = ( ( cls ` J ) ` S ) ) $=
      ( ctop wcel wss wa ccl cfv ccld wceq clscld wb clsss3 iscld3 syldan mpbid
      ) BEFZACGZHABIJZJZBKJFZUBUAJUBLZABCDMSTUBCGUCUDNABCDOUBBCDPQR $.
      $( [2-Oct-2007] $)

    $( The interior operation is idempotent. $)
    ntridm $p |- ( ( J e. Top /\ S C_ X ) ->
               ( ( int ` J ) ` ( ( int ` J ) ` S ) ) = ( ( int ` J ) ` S ) ) $=
      ( ctop wcel wss wa cnt cfv wceq ntropn wb ntrss3 isopn3 syldan mpbid ) BE
      FZACGZHABIJZJZBFZUATJUAKZABCDLRSUACGUBUCMABCDNUABCDOPQ $.
      $( [2-Oct-2007] $)

    $( The closure of a topology's underlying set is entire set. $)
    clstop $p |- ( J e. Top -> ( ( cls ` J ) ` X ) = X ) $=
      ( ctop wcel ccld cfv ccl wceq topcld wss wb ssid iscld3 mpan2 mpbid ) ADE
      ZBAFGEZBAHGGBIZABCJQBBKRSLBMBABCNOP $.
      $( [5-Oct-2007] $)

    $( The interior of a topology's underlying set is entire set. $)
    ntrtop $p |- ( J e. Top -> ( ( int ` J ) ` X ) = X ) $=
      ( ctop wcel cnt cfv wceq topopn wss wb ssid isopn3 mpan2 mpbid ) ADEZBAEZ
      BAFGGBHZABCIPBBJQRKBLBABCMNO $.
      $( [12-Sep-2006] $)

    $( A subset with an empty interior cannot cover a whole (nonempty)
       topology. $)
    0ntr $p |- ( ( ( J e. Top /\ X =/= (/) ) /\ ( S C_ X
               /\ ( ( int ` J ) ` S ) = (/) ) ) -> ( X \ S ) =/= (/) ) $=
      ( ctop wcel wss cnt cfv c0 wceq wa wne cdif ssdif0 wi eqss ntrtop syl5bir
      fveq2 sylan9eqr eqeq1d biimpd ex exp3a com34 imp32 necon3d imp an32s ) BE
      FZACGZABHIZIZJKZLZCJMZCANZJMZUKUPLZUQUSUTURJCJURJKCAGZUTCJKZCAOUKULUOVAVB
      PUKULVAUOVBUKULVAUOVBPZULVALACKZUKVCACQUKVDVCUKVDLZUOVBVEUNCJVDUKUNCUMICA
      CUMTBCDRUAUBUCUDSUEUFUGSUHUIUJ $.
      $( [12-Sep-2006] $)

    $( If a subset is included in a closed set, so is the subset's closure. $)
    clsss2 $p |- ( ( J e. Top /\ C e. ( Clsd ` J ) /\ S C_ C ) ->
                 ( ( cls ` J ) ` S ) C_ C ) $=
      ( ctop wcel ccld cfv wss w3a ccl cldss 3adant3 clsss syld3an2 wceq cldcls
      sseqtrd ) CFGZACHIGZBAJZKBCLIZIZAUCIZATADJZUAUBUDUEJTUAUFUBACDEMNABCDEOPT
      UAUEAQUBACRNS $.
      $( [22-Feb-2007] $)

    $( Membership in a closure.  Theorem 6.5(a) of [Munkres] p. 95. $)
    elcls $p |- ( ( J e. Top /\ S C_ X /\ P e. X ) -> ( P e. ( ( cls ` J ) ` S
                ) <-> A. x e. J ( P e. x -> ( x i^i S ) =/= (/) ) ) ) $=
      ( wcel wss cfv cin c0 wne wi wn wa cdif 3adant3 adantr wceq reldisj eldif
      ctop w3a ccl cv wral wrex cmclsopn biimpri 3ad2antl3 simpr sscls jca ssin
      sylib dfin4 syl6sseq wb adantl mpbird nne incom eqeq1i bitri sylibr eleq2
      ineq1 neeq1d notbid anbi12d rcla4ev syl12anc df-ne con2bii simplll opncld
      ccld adantlr biimpa adantll clsss2 sseld eldifn syl6 con2d sylan2br exp31
      syl3anc com34 imp4a rexlimdv imp 3adantl3 impbida rexanali syl6bb con4bid
      ) DUBGZCEHZBEGZUCZBCDUDIIZGZBAUEZGZXDCJZKLZMADUFZXAXCNZXEXGNZOZADUGZXHNXA
      XIXLXAXIOEXBPZDGZBXMGZXMCJZKLZNZXLXAXNXIWRWSXNWTCDEFUHQRWTWRXIXOWSXOWTXIO
      BEXBUAUIUJXAXRXIWRWSXRWTWRWSOZCXMJZKSZXRXSYACEXMPZHZXSCEXBJZYBXSWSCXBHZOC
      YDHXSWSYEWRWSUKCDEFULUMCEXBUNUOEXBUPUQWSYAYCURWRCXMETUSUTXRXPKSYAXPKVAXPX
      TKXMCVBVCVDVEQRXKXOXROAXMDXDXMSZXEXOXJXRXDXMBVFYFXGXQYFXFXPKXDXMCVGVHVIVJ
      VKVLWRWSXLXIWTXSXLXIXSXKXIADXSXDDGZXEXJXIXSYGXJXEXIXSYGXJXEXIMZXJXSYGOZCX
      DJZKSZYHYKXFKSZXJYJXFKCXDVBVCXGYLXFKVMVNVDYIYKOZXCXEYMXCBEXDPZGXENYMXBYNB
      YMWRYNDVQIGZCYNHZXBYNHWRWSYGYKVOYIYOYKWRYGYOWSXDDEFVPVRRXSYKYPYGWSYKYPWRW
      SYKYPCXDETVSVTVRYNCDEFWAWHWBBEXDWCWDWEWFWGWIWJWKWLWMWNXEXGADWOWPWQ $.
      $( [22-Feb-2007] $)

    $( Membership in a closure. $)
    elcls2 $p |- ( ( J e. Top /\ S C_ X ) -> ( P e. ( ( cls ` J ) ` S
         ) <-> ( P e. X /\ A. x e. J ( P e. x -> ( x i^i S ) =/= (/) ) ) ) ) $=
      ( ctop wcel wss wa ccl cfv cv cin c0 wne wi wral wb clsss3 pm4.71rd elcls
      ssel syl 3expa pm5.32da bitrd ) DGHZCEIZJZBCDKLLZHZBEHZULJZUMBAMZHUOCNOPQ
      ADRZJUJUKEIZULUNSCDEFTUQULUMUKEBUCUAUDUJUMULUPUHUIUMULUPSABCDEFUBUEUFUG
      $.
      $( [5-Mar-2007] $)

    $( Any open set containing a point that belongs to the closure of a subset
       intersects the subset.  One direction of Theorem 6.5(a) of [Munkres]
       p. 95. $)
    clsndisj $p |- ( ( ( J e. Top /\ S C_ X /\ P e. ( ( cls ` J ) ` S ) ) /\
               ( U e. J /\ P e. U ) ) -> ( U i^i S ) =/= (/) ) $=
      ( vx ctop wcel wss ccl cfv w3a cv cin c0 wne wi wral wa simp1 simp2 sseld
      clsss3 3impia simp3 elcls biimpa syl31anc wceq eleq2 ineq1 neeq1d imbi12d
      rcla4cv imp32 sylan ) DHIZBEJZABDKLLZIZMZAGNZIZVCBOZPQZRZGDSZCDIZACIZTCBO
      ZPQZVBURUSAEIZVAVHURUSVAUAURUSVAUBURUSVAVMURUSTUTEABDEFUDUCUEURUSVAUFURUS
      VMMVAVHGABDEFUGUHUIVHVIVJVLVGVJVLRGCDVCCUJZVDVJVFVLVCCAUKVNVEVKPVCCBULUMU
      NUOUPUQ $.
      $( [26-Feb-2007] $)

    $( A subset whose closure has an empty interior also has an empty
       interior. $)
    ntrcls0 $p |- ( ( J e. Top /\ S C_ X /\ ( ( int ` J ) `
                ( ( cls ` J ) ` S ) ) = (/) ) -> ( ( int ` J ) ` S ) = (/) ) $=
      ( ctop wcel wss ccl cfv cnt c0 wceq wa simpl clsss3 sscls syl3anc 3adant3
      w3a ntrss wb sseq2 3ad2ant3 mpbid ss0 syl ) BEFZACGZABHIIZBJIZIZKLZSZAUJI
      ZKGZUNKLUMUNUKGZUOUGUHUPULUGUHMUGUICGAUIGUPUGUHNABCDOABCDPUIABCDTQRULUGUP
      UOUAUHUKKUNUBUCUDUNUEUF $.
      $( [4-Oct-2007] $)

    $( Two ways to say that a subset has an empty interior. $)
    ntreq0 $p |- ( ( J e. Top /\ S C_ X ) -> ( ( ( int ` J ) ` S ) = (/) <->
               A. x e. J ( x C_ S -> x = (/) ) ) ) $=
      ( vy wcel wss wa cfv c0 wceq cv wi wral wex wrex wn neq0 con1bii ctop cnt
      cpw cuni ntrval eqeq1d ancom elin anbi1i anass 3bitri exbii eluni 3bitr4i
      cin df-rex rexcom4 19.42v rexbii 3bitr2i notbii ralinexa vex elpw imbi12i
      bitr3i ralbii syl6bb ) CUAGBDHIZBCUBJJZKLCBUCZUOZUDZKLZAMZBHZVOKLZNZACOZV
      IVJVMKBCDEUEUFVNVOVKGZFMZVOGZFPZIZACQZRZVTWCRZNZACOVSVNWAVMGZFPZRWFVNWJFV
      MSTWJWEWJVTWBIZACQZFPWKFPZACQWEWIWLFWBVOVLGZIZAPVOCGZWKIZAPWIWLWOWQAWOWNW
      BIWPVTIZWBIWQWBWNUGWNWRWBVOCVKUHUIWPVTWBUJUKULAWAVLUMWKACUPUNULWKAFCUQWMW
      DACVTWBFURUSUTVAVFVTWCACVBWHVRACVTVPWGVQVOBAVCVDVQWCFVOSTVEVGUTVH $.
      $( [11-Nov-2013] $) $( [3-Oct-2007] $)
  $}

  $( The closure of the empty set. $)
  cls0 $p |- ( J e. Top -> ( ( cls ` J ) ` (/) ) = (/) ) $=
    ( ctop wcel c0 ccld cfv ccl wceq 0cld cuni wss 0ss eqid iscld3 mpan2 mpbid
    wb ) ABCZDAEFCZDAGFFDHZAIRDAJZKSTQUALDAUAUAMNOP $.
    $( [2-Oct-2007] $)

  $( The interior of the empty set. $)
  ntr0 $p |- ( J e. Top -> ( ( int ` J ) ` (/) ) = (/) ) $=
    ( ctop wcel c0 cnt cfv wceq 0opn cuni wss wb 0ss eqid isopn3 mpan2 mpbid )
    ABCZDACZDAEFFDGZAHQDAIZJRSKTLDATTMNOP $.
    $( [15-Jun-2008] $) $( [2-Oct-2007] $)

  ${
    $d x y z B $.  $d x J $.  $d x y z P $.  $d x y z S $.  $d x U $.
    $d x X $.
    elcls3.1 $e |- J = ( topGen ` B ) $.
    elcls3.2 $e |- X = U. J $.
    $( Membership in a closure in terms of the members of a basis.  Theorem
       6.5(b) of [Munkres] p. 95. $)
    elcls3 $p |- ( ( B e. TopBases /\ S C_ X /\ P e. X ) -> ( P e.
  ( ( cls ` J ) ` S ) <-> A. x e. B ( P e. x -> ( x i^i S ) =/= (/) ) ) ) $=
      ( vy vz wcel cfv cv cin c0 wne wi wral wceq wa ctb wss ctop tgcl syl5eqel
      w3a ccl wb ctg elcls bastg syl6sseqr sseld imim1d ralimdv2 3ad2ant1 eleq2
      syl3an1 ineq1 neeq1d imbi12d cbvralv eleq2i tg2 syl3an2b adantlr rcla4cva
      wrex 3expb imp ssdisj necon3d syl5com exp31 imp4a rexlimdv ad2antlr exp43
      ex mpd ralrimdv syl5bi impbid bitrd ) BUAKZDFUBZCFKZUFZCDEUGLLKZCAMZKZWJD
      NZOPZQZAERZWNABRZWEEUCKWFWGWIWOUHWEEBUILZUCGBUDUEACDEFHUJURWHWOWPWEWFWOWP
      QWGWEWNWNAEBWEWJBKWJEKZWNWEBEWJWEBWQEBUKGULUMUNUOUPWEWFWPWOQWGWPCIMZKZWSD
      NZOPZQZIBRZWEWOWNXCAIBWJWSSZWKWTWMXBWJWSCUQXEWLXAOWJWSDUSUTVAVBWEXDWNAEWE
      XDWRWKWMWEXDTWRWKTZTCJMZKZXGWJUBZTZJBVHZWMWEXFXKXDWEWRWKXKWRWEWJWQKWKXKEW
      QWJGVCJWJBCVDVEVIVFXDXKWMQWEXFXDXJWMJBXDXGBKZXHXIWMXDXLXHXIWMQXDXLTZXHTXG
      DNZOPZXIWMXMXHXOXCXHXOQIXGBWSXGSZWTXHXBXOWSXGCUQXPXAXNOWSXGDUSUTVAVGVJXIW
      LOXNOXIWLOSXNOSXGWJDVKVSVLVMVNVOVPVQVTVRWAWBUPWCWD $.
      $( [26-Feb-2007] $)
  $}


