$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Closure and interior
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c int $.
  $c cls $.
  $c Clsd $.

  $( Extend class notation with the set of closed sets of a topology. $)
  ccld $a class Clsd $.

  $( Extend class notation with interior of a subset of a topology base set. $)
  cnt $a class int $.

  $( Extend class notation with closure of a subset of a topology base set. $)
  ccl $a class cls $.

  ${
    $d v w x y z $.
    $( Define a function on topologies whose value is the set of closed sets of
       the topology. $)
    df-cld $a |- Clsd = { <. z , w >. | ( z e. Top /\ w = { x |
                  ( x C_ U. z /\ ( U. z \ x ) e. z ) } ) } $.

    $( Define a function on topologies whose value is the interior function on
       the subsets of the base set.  See ~ ntrval . $)
    df-ntr $a |- int = { <. z , w >. | ( z e. Top /\ w = { <. x , y >. |
                 ( x C_ U. z /\ y = U. { v e. z | v C_ x } ) } ) } $.

    $( Define a function on topologies whose value is the closure function on
       the subsets of the base set.  See ~ clsval . $)
    df-cls $a |- cls = { <. z , w >. | ( z e. Top /\ w = { <. x , y >. |
               ( x C_ U. z /\ y = |^| { v e. ( Clsd ` z ) | x C_ v } ) } ) } $.
  $}

  ${
    $d v w x y z J $.  $d w x y z X $.
    cldval.1 $e |- X = U. J $.
    $( The set of closed sets of a topology.  (Note that the set of open sets
       is just the topology itself, so we don't have a separate definition.) $)
    cldval $p |- ( J e. Top ->
               ( Clsd ` J ) = { x | ( x C_ X /\ ( X \ x ) e. J ) } ) $=
      ( vz vw ctop wcel cv wss cdif wa cab cvv ccld cfv wceq cuni uniexg
      syl5eqel abssexg syl unieq syl6eqr sseq2d difeq1d id eleq12d anbi12d
      abbidv df-cld fvopab4g mpdan ) BGHZAIZCJZCUOKZBHZLZAMZNHZBOPUTQUNCNHVAUNB
      RZNCBGSDTURACNUAUBEFBUOEIZRZJZVDUOKZVCHZLZAMUTGNOVCBQZVHUSAVIVEUPVGURVIVD
      CUOVIVDVBCVCBUCDUDZUEVIVFUQVCBVIVDCUOVJUFVIUGUHUIUJAEFUKULUM $.
      $( [2-Oct-2006] $)

    $( The interior function on the subsets of a topology's base set. $)
    ntrfval $p |- ( J e. Top -> ( int ` J ) = { <. x , y >. | ( x C_ X /\
                     y = U. { v e. J | v C_ x } ) } ) $=
      ( vz vw ctop wcel cv wss crab cuni wceq wa copab cvv cnt cfv cpw uniexg
      syl5eqel pwexg opabex2g 3syl visset elpw anbi1i opabbii syl5eqelr unieq
      syl6eqr sseq2d rabeq unieqd eqeq2d anbi12d opabbidv df-ntr fvopab4g
      mpdan ) DIJZAKZELZBKZCKVDLZCDMZNZOZPZABQZRJDSTVLOVCVDEUAZJZVJPZABQZRVLVCE
      RJVMRJVPRJVCDNZREDIUBFUCERUDABVMVIRUEUFVOVKABVNVEVJVDEAUGUHUIUJUKGHDVDGKZ
      NZLZVFVGCVRMZNZOZPZABQVLIRSVRDOZWDVKABWEVTVEWCVJWEVSEVDWEVSVQEVRDULFUMUNW
      EWBVIVFWEWAVHVGCVRDUOUPUQURUSABGHCUTVAVB $.
      $( [10-Sep-2006] $)

    $( The closure function on the subsets of a topology's base set. $)
    clsfval $p |- ( J e. Top -> ( cls ` J ) = { <. x , y >. | ( x C_ X /\
                     y = |^| { v e. ( Clsd ` J ) | x C_ v } ) } ) $=
      ( vz vw ctop wcel cv wss ccld cfv crab cint wceq wa copab cvv ccl cpw
      cuni uniexg syl5eqel pwexg opabex2g 3syl visset elpw anbi1i opabbii
      syl5eqelr unieq syl6eqr sseq2d fveq2 rabeq syl inteqd eqeq2d anbi12d
      opabbidv df-cls fvopab4g mpdan ) DIJZAKZELZBKZVHCKLZCDMNZOZPZQZRZABSZTJDU
      ANVQQVGVHEUBZJZVORZABSZTVQVGETJVRTJWATJVGDUCZTEDIUDFUEETUFABVRVNTUGUHVTVP
      ABVSVIVOVHEAUIUJUKULUMGHDVHGKZUCZLZVJVKCWCMNZOZPZQZRZABSVQITUAWCDQZWJVPAB
      WKWEVIWIVOWKWDEVHWKWDWBEWCDUNFUOUPWKWHVNVJWKWGVMWKWFVLQWGVMQWCDMUQVKCWFVL
      URUSUTVAVBVCABGHCVDVEVF $.
      $( [3-Oct-2006] $)
  $}

  ${
    $d x y z J $.  $d x y z S $.  $d x y z X $.  $d x T $.
    iscld.1 $e |- X = U. J $.
    $( The predicate " ` S ` is a closed set." $)
    iscld $p |- ( J e. Top -> ( S e. ( Clsd ` J ) <->
                 ( S C_ X /\ ( X \ S ) e. J ) ) ) $=
      ( vx ctop wcel ccld cfv cv wss cdif wa cab cldval eleq2d cvv elisset
      adantl ssexg ancoms cuni uniexg syl5eqel sylan adantrr wceq sseq1 difeq2
      eleq1d anbi12d elabg pm5.21nd bitrd ) BFGZABHIZGAEJZCKZCUQLZBGZMZENZGZACK
      ZCALZBGZMZUOUPVBAEBCDOPUOVCVGAQGZVCVHUOAVBRSUOVDVHVFCQGZVDVHUOVDVIVHACQTU
      AUOBUBQCBFUCDUDUEUFVAVGEAQUQAUGZURVDUTVFUQACUHVJUSVEBUQACUIUJUKULUMUN $.
      $( [2-Oct-2006] $)

    $( A subset of the underlying set of a topology is closed iff its
       complement is open. $)
    iscld2 $p |- ( ( J e. Top /\ S C_ X ) ->
                  ( S e. ( Clsd ` J ) <-> ( X \ S ) e. J ) ) $=
      ( ctop wcel ccld cfv wss cdif wa iscld ibar bicomd sylan9bb ) BEFABGHFACI
      ZCAJBFZKZPQABCDLPQRPQMNO $.
      $( [4-Oct-2006] $)

    $( A closed set is a subset of the underlying set of a topology. $)
    cldss $p |- ( ( J e. Top /\ S e. ( Clsd ` J ) ) -> S C_ X ) $=
      ( ctop wcel ccld cfv wss cdif iscld simprbda ) BEFABGHFACICAJBFABCDKL $.
      $( [5-Oct-2006] $)

    $( The complement of a closed set is open. $)
    cldopn $p |- ( ( J e. Top /\ S e. ( Clsd ` J ) ) -> ( X \ S ) e. J ) $=
      ( ctop wcel ccld cfv wss cdif iscld simplbda ) BEFABGHFACICAJBFABCDKL $.
      $( [5-Oct-2006] $)

    $( A subset of the underlying set of a topology is open iff its complement
       is closed. $)
    isopn2 $p |- ( ( J e. Top /\ S C_ X ) ->
                  ( S e. J <-> ( X \ S ) e. ( Clsd ` J ) ) ) $=
      ( ctop wcel wss wa cdif ccld cfv wb difss iscld2 mpan2 wceq dfss4 biimpi
      eleq1d sylan9bb bicomd ) BEFZACGZHCAIZBJKFZABFZUBUECUDIZBFZUCUFUBUDCGUEUH
      LCAMUDBCDNOUCUGABUCUGAPACQRSTUA $.
      $( [4-Oct-2006] $)

    $( The complement of an open set is closed. $)
    opncld $p |- ( ( J e. Top /\ S e. J ) -> ( X \ S ) e. ( Clsd ` J ) ) $=
      ( ctop wcel wa cdif ccld cfv simpr wss wb eltopss isopn2 syldan mpbid )
      BEFZABFZGSCAHBIJFZRSKRSACLSTMABCDNABCDOPQ $.
      $( [6-Oct-2006] $)

    $( The underlying set of a topology is closed.  Part of Theorem 6.1(1) of
       [Munkres] p. 93. $)
    topcld $p |- ( J e. Top -> X e. ( Clsd ` J ) ) $=
      ( ctop wcel ccld cfv wss cdif wa c0 0opn difid syl5eqel ssid jctil iscld
      mpbird ) ADEZBAFGEBBHZBBIZAEZJSUBTSKAUAALBMNBOPBABCQR $.
      $( [3-Oct-2006] $)

    $( The interior of a subset of a topology's base set is the union of all
       the open sets it includes.  Definition of interior of [Munkres]
       p. 94. $)
    ntrval $p |- ( ( J e. Top /\ S C_ X ) -> ( ( int ` J ) ` S ) =
                 U. { x e. J | x C_ S } ) $=
      ( vy vz ctop wcel wss wa cnt cfv cv cpw crab cuni wceq copab ntrfval
      adantr visset elpw anbi1i opabbii syl6eqr fveq1d cvv wi uniexg syl5eqel
      elpw2g biimprd syl imp rabexg sseq2 rabbidv unieqd eqid fvopab4g
      syl11anc eqtrd ) CHIZBDJZKZBCLMZMBFNZDOZIZGNANZVHJZACPZQZRZKZFGSZMZVKBJZA
      CPZQZVFBVGVQVFVGVHDJZVOKZFGSZVQVDVGWDRVEFGACDETUAVPWCFGVJWBVOVHDFUBUCUDUE
      UFUGVFBVIIZWAUHIZVRWARVDVEWEVDDUHIZVEWEUIVDCQUHDCHUJEUKWGWEVEBDUHULUMUNUO
      VDWFVEVDVTUHIWFVSACHUPVTUHUJUNUAFGBVNWAVIUHVQVHBRZVMVTWHVLVSACVHBVKUQURUS
      VQUTVAVBVC $.
      $( [10-Sep-2006] $)

    $( The closure of a subset of a topology's base set is the intersection of
       all the closed sets that include it.  Definition of closure of [Munkres]
       p. 94. $)
    clsval $p |- ( ( J e. Top /\ S C_ X ) -> ( ( cls ` J ) ` S ) =
                 |^| { x e. ( Clsd ` J ) | S C_ x } ) $=
      ( vy vz ctop wcel wss wa ccl cfv cv cpw ccld crab cint wceq copab
      clsfval adantr visset elpw anbi1i opabbii syl6eqr fveq1d cvv elpw2g
      biimpar cuni uniexg syl5eqel sylan wrex sseq2 rcla4ev topcld intexrab
      sylib sseq1 rabbidv inteqd eqid fvopab4g syl11anc eqtrd ) CHIZBDJZKZBCLMZ
      MBFNZDOZIZGNVMANZJZACPMZQZRZSZKZFGTZMZBVPJZAVRQZRZVKBVLWCVKVLVMDJZWAKZFGT
      ZWCVIVLWJSVJFGACDEUAUBWBWIFGVOWHWAVMDFUCUDUEUFUGUHVKBVNIZWGUIIZWDWGSDUIIZ
      VJWKVIWMWKVJBDUIUJUKVICULUIDCHUMEUNUOVKWEAVRUPZWLDVRIVJWNVIWEVJADVRVPDBUQ
      URCDEUSUOWEAVRUTVAFGBVTWGVNUIWCVMBSZVSWFWOVQWEAVRVMBVPVBVCVDWCVEVFVGVH $.
      $( [10-Sep-2006] $)
  $}

  $( The empty set is closed.  Part of Theorem 6.1(1) of [Munkres] p. 93. $)
  0cld $p |- ( J e. Top -> (/) e. ( Clsd ` J ) ) $=
    ( ctop wcel c0 ccld cfv cuni cdif dif0 topopn wss wb 0ss eqid iscld2 mpan2
    mpbird ) ABCZDAEFCZAGZDHZACZAUATIJRDTKSUBLTMDATTNOPQ $.
    $( [4-Oct-2006] $)

  ${
    $d x A $.  $d x J $.  $d x S $.
    $( The indexed intersection of a collection ` B ( x ) ` of closed sets is
       closed.  Theorem 6.1(2) of [Munkres] p. 93. $)
    iincld $p |- ( ( J e. Top /\ A =/= (/) /\ A. x e. A B e. ( Clsd ` J ) ) ->
                  |^|_ x e. A B e. ( Clsd ` J ) ) $=
      ( ctop wcel c0 wne ccld cfv wral w3a ciin cuni cdif ciun wceq wa wss
      eqid cldss ex dfss4 syl6ib ralimdv imp iineq2 syl 3adant2 iindif2
      3ad2ant2 eqtr3d iscld simpr syl6bi iunopn syldan wb iunss cv difss a1i
      mprgbir isopn2 mpan2 adantr mpbid eqeltrd ) DEFZBGHZCDIJZFZABKZLZABCMZDNZ
      ABVPCOZPZOZVKVNABVPVQOZMZVOVSVIVMWAVOQZVJVIVMRZVTCQZABKZWBVIVMWEVIVLWDABV
      IVLCVPSZWDVIVLWFCDVPVPTZUAUBCVPUCUDUEUFABVTCUGUHUIVJVIWAVSQVMABVPVQUJUKUL
      VIVMVSVKFZVJWCVRDFZWHVIVMVQDFZABKZWIVIVMWKVIVLWJABVIVLWFWJRWJCDVPWGUMWFWJ
      UNUOUEUFABVQDUPUQVIWIWHURZVMVIVRVPSZWLWMVQVPSZABABVQVPUSWNAUTBFVPCVAVBVCV
      RDVPWGVDVEVFVGUIVH $.
      $( [5-Oct-2006] $)

    $( The intersection of a set of closed sets is closed. $)
    intcld $p |- ( ( J e. Top /\ A =/= (/) /\ A C_ ( Clsd ` J ) ) ->
                  |^| A e. ( Clsd ` J ) ) $=
      ( vx ctop wcel c0 wne ccld cfv wss w3a cv ciin cint wral iincld dfss3
      syl3an3b intiin syl5eqel ) BDEZAFGZABHIZJZKCACLZMZUCANUAUBUEUCECAOUFUCEUD
      CAUEBPCAUCQRCAST $.
      $( [5-Oct-2006] $)

    $( The union of two closed sets is closed.  Equivalent to Theorem 6.1(3) of
       [Munkres] p. 93. $)
    uncld $p |- ( ( J e. Top /\ A e. ( Clsd ` J ) /\ B e. ( Clsd ` J ) ) ->
                 ( A u. B ) e. ( Clsd ` J ) ) $=
      ( ctop wcel ccld cfv cun wa cuni cdif cin eqid cldopn anim12i anandis
      inopn 3expb syldan difundi syl5eqel wss wb cldss unss sylib iscld2
      mpbird 3impb ) CDEZACFGZEZBUKEZABHZUKEZUJULUMIZIZUOCJZUNKZCEZUQURAKZURBKZ
      LZCUSUJUPVACEZVBCEZIZVCCEZUJULUMVFUJULIZVDUJUMIZVEACURURMZNBCURVJNOPUJVDV
      EVGVAVBCQRSURABTUAUJUPUNURUBZUOUTUCUQAURUBZBURUBZIZVKUJULUMVNVHVLVIVMACUR
      VJUDBCURVJUDOPABURUEUFUNCURVJUGSUHUI $.
      $( [10-Jun-2007] $) $( [5-Oct-2006] $)

    $( A closed subset equals its own closure. $)
    cldcls $p |- ( ( J e. Top /\ S e. ( Clsd ` J ) ) ->
                 ( ( cls ` J ) ` S ) = S ) $=
      ( vx ctop wcel ccld cfv wa cuni wss ccl wceq eqid cldss cv crab cint
      clsval intmin sylan9eq an1rs mpdan ) BDEZABFGZEZHABIZJZABKGGZALZABUFUFMZN
      UCUGUEUIUCUGHUEUHACOJCUDPQACABUFUJRCAUDSTUAUB $.
      $( [15-Mar-2007] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( The intersection of two closed sets is closed.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    incld $p |- ( ( J e. Top /\ A e. ( Clsd ` J ) /\ B e. ( Clsd ` J ) )
                                      -> ( A i^i B ) e. ( Clsd ` J ) ) $=
      ( vx ctop wcel ccld cfv w3a cpr cint cin wceq intprg 3adant1 c0 wne wss
      simp1 cv preq1 neeq1d visset prnz vtoclg 3ad2ant2 wa prssg ibi intcld
      syl111anc eqeltrrd ) CEFZACGHZFZBUNFZIZABJZKZABLZUNUOUPUSUTMUMABUNUNNOUQU
      MURPQZURUNRZUSUNFUMUOUPSUOUMVAUPDTZBJZPQVADAUNVCAMVDURPVCABUAUBVCBDUCUDUE
      UFUOUPVBUMUOUPUGVBABUNUNUNUHUIOURCUJUKUL $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y z J $.  $d x P $.  $d x y z S $.  $d x U $.  $d x y z X $.
    $d x T $.
    clscld.1 $e |- X = U. J $.
    $( The closure of a subset of a topology's underlying set is closed. $)
    clscld $p |- ( ( J e. Top /\ S C_ X ) ->
                 ( ( cls ` J ) ` S ) e. ( Clsd ` J ) ) $=
      ( vx ctop wcel wss wa ccl cfv cv ccld crab cint clsval c0 wne simpl
      topcld anim1i sseq2 elrab sylibr ne0i syl ssrab2 a1i intcld syl111anc
      eqeltrd ) BFGZACHZIZABJKKAELZHZEBMKZNZOZUQEABCDPUNULURQRZURUQHZUSUQGULUMS
      UNCURGZUTUNCUQGZUMIVBULVCUMBCDTUAUPUMECUQUOCAUBUCUDURCUEUFVAUNUPEUQUGUHUR
      BUIUJUK $.
      $( [4-Oct-2006] $)

    $( The interior of a subset of a topology's underlying set is open. $)
    ntropn $p |- ( ( J e. Top /\ S C_ X ) -> ( ( int ` J ) ` S ) e. J ) $=
      ( vx ctop wcel wss wa cnt cfv cv crab cuni ntrval ssrab2 uniopn mpan2
      adantr eqeltrd ) BFGZACHZIABJKKELAHZEBMZNZBEABCDOUAUEBGZUBUAUDBHUFUCEBPUD
      BQRST $.
      $( [11-Sep-2006] $)

    $( Express closure in terms of interior. $)
    clsval2 $p |- ( ( J e. Top /\ S C_ X ) -> ( ( cls ` J ) ` S ) =
                 ( X \ ( ( int ` J ) ` ( X \ S ) ) ) ) $=
      ( vy vz vx ctop wcel wss wa cv cdif crab cuni ccld cfv cint cnt ccl wn
      wi wral cldopn wceq sseq1 eleq2 notbid imbi12d rcla4v syl adantrl sscon
      a1i neldif ex imim12d ad2antrl syld exp32 com34 imp3a r19.21adv adantr
      topcld sseq2 com23 imp opncld adantlr ssconb biimpd eltopss sylan an1rs
      eldifn r19.21adva jcad impbid eldif wrex ralnex imnan ancom notbii bitri
      ralbii elunirab 3bitr4ri anbi2i visset elintrab 3bitr4g eqrdv ntrval
      difss sylan2 difeq2d clsval 3eqtr4rd ) BHIZACJZKZCELZCAMZJZEBNOZMZAFLZJZF
      BPQZNRZCXEBSQQZMABTQQXCGXHXLXCGLZCIZXFXNXDIZUAZUBZEBUCZKZXJXNXIIZUBZFXKUC
      ZXNXHIZXNXLIXCXTYCXAXTYCUBXBXAXTYBFXKXAXOXSXIXKIZYBUBXAXOYEXSYBXAXOYEXSYB
      UBXAXOYEKKXSCXIMZXEJZXNYFIZUAZUBZYBXAYEXSYJUBZXOXAYEKYFBIYKXIBCDUDXRYJEYF
      BXDYFUEZXFYGXQYIXDYFXEUFYLXPYHXDYFXNUGUHUIUJUKULXOYJYBUBXAYEXOXJYGYIYAXJY
      GUBXOAXICUMUNXOYIYAXNCXIUOUPUQURUSUTVAVBVCVDXCYCXOXSXAXBYCXOUBXAYCXBXOXAC
      XKIYCXBXOUBZUBBCDVEYBYMFCXKXICUEXJXBYAXOXICAVFXICXNUGUIUJUKVGVHXCYCXREBXC
      XDBIZKZYCACXDMZJZXNYPIZUBZXRXAYNYCYSUBZXBXAYNKZYPXKIYTXDBCDVIYBYSFYPXKXIY
      PUEXJYQYAYRXIYPAVFXIYPXNUGUIUJUKVJYOXFYQYRXQXAYNXBXFYQUBZXDCJZXBUUBUUAUUC
      XBKXFYQXDACVKVLXDBCDVMVNVOYRXQUBYOXNCXDVPUNUQUSVQVRVSYDXOXNXGIZUAZKXTXNCX
      GVTUUEXSXOXPXFKZUAZEBUCUUFEBWAZUAXSUUEUUFEBWBXRUUGEBXRXFXPKZUAUUGXFXPWCUU
      IUUFXFXPWDWEWFWGUUDUUHXFEXNBWHWEWIWJWFXJFXNXKGWKWLWMWNXCXMXGCXAXECJZXMXGU
      EXBEXEBCDWOUUJXBCAWPUNWQWRFABCDWSWT $.
      $( [10-Sep-2006] $)

    $( Interior expressed in terms of closure. $)
    ntrval2 $p |- ( ( J e. Top /\ S C_ X ) ->
                 ( ( int ` J ) ` S ) = ( X \ ( ( cls ` J ) ` ( X \ S ) ) ) ) $=
      ( ctop wcel wss wa cdif ccl cfv cnt wceq difss clsval2 mpan2 adantr
      dfss4 biimpi fveq2d adantl difeq2d eqtrd ntropn eltopss syldan sylib
      eqtr2d ) BEFZACGZHZCCAIZBJKKZICCABLKZKZIZIZUOUKUMUPCUKUMCCULIZUNKZIZUPUIU
      MUTMZUJUIULCGVACANULBCDOPQUKUSUOCUJUSUOMUIUJURAUNUJURAMACRSTUAUBUCUBUKUOC
      GZUQUOMUIUJUOBFVBABCDUDUOBCDUEUFUOCRUGUH $.
      $( [1-Oct-2007] $)

    $( Subset relationship for closure. $)
    clsss $p |- ( ( J e. Top /\ S C_ X /\ T C_ S ) -> ( ( cls ` J ) ` T ) C_
                 ( ( cls ` J ) ` S ) ) $=
      ( vx ctop wcel wss w3a cv ccld cfv crab cint ccl wi sstr2 adantr
      ss2rabdv intss syl 3ad2ant3 wceq simp1 impcom 3adant1 clsval syl11anc
      3adant3 3sstr4d ) CGHZADIZBAIZJZBFKZIZFCLMZNZOZAUPIZFURNZOZBCPMZMZAVDMZUN
      ULUTVCIZUMUNVBUSIVGUNVAUQFURUNVAUQQUPURHBAUPRSTVBUSUAUBUCUOULBDIZVEUTUDUL
      UMUNUEUMUNVHULUNUMVHBADRUFUGFBCDEUHUIULUMVFVCUDUNFACDEUHUJUK $.
      $( [10-Feb-2007] $)

    $( Subset relationship for interior. $)
    ntrss $p |- ( ( J e. Top /\ S C_ X /\ T C_ S ) -> ( ( int ` J ) ` T ) C_
                 ( ( int ` J ) ` S ) ) $=
      ( ctop wcel wss cnt cfv wa cdif ccl clsss 3expb sscon adantl difss jctil
      sylan2 syl wceq ntrval2 sstr2 impcom adantrr 3sstr4d 3impb ) CFGZADHZBAHZ
      BCIJZJZAULJZHUIUJUKKZKZDDBLZCMJZJZLZDDALZURJZLZUMUNUPVBUSHZUTVCHUIUQDHZVA
      UQHZKVDUOUIVEVFVDUQVACDENOUOVFVEUKVFUJBADPQDBRSTVBUSDPUAUIBDHZUMUTUBUOBCD
      EUCUKUJVGBADUDUETUIUJUNVCUBUKACDEUCUFUGUH $.
      $( [3-Oct-2007] $)

    $( A subset of a topology's underlying set is included in its closure. $)
    sscls $p |- ( ( J e. Top /\ S C_ X ) -> S C_ ( ( cls ` J ) ` S ) ) $=
      ( vx ctop wcel wss wa cv ccld cfv crab cint ccl ssintub a1i clsval
      sseqtr4d ) BFGACHIZAAEJHEBKLZMNZABOLLAUBHTEAUAPQEABCDRS $.
      $( [22-Feb-2007] $)

    $( A subset includes its interior. $)
    ntrss2 $p |- ( ( J e. Top /\ S C_ X ) -> ( ( int ` J ) ` S ) C_ S ) $=
      ( vx vy ctop wcel wss wa cnt cfv cv crab cuni ntrval wrex elunirab wi
      ssel impcom a1i r19.23aiv sylbi ssriv eqsstrd ) BGHACIJZABKLLEMZAIZEBNOZA
      EABCDPUJAIUGFUJAFMZUJHUKUHHZUIJZEBQUKAHZUIEUKBRUMUNEBUMUNSUHBHUIULUNUHAUK
      TUAUBUCUDUEUBUF $.
      $( [3-Oct-2007] $)

    $( The closure of a subset of a topological space is included in the
       space. $)
    clsss3 $p |- ( ( J e. Top /\ S C_ X ) -> ( ( cls ` J ) ` S ) C_ X ) $=
      ( ctop wcel wss ccl cfv ccld clscld cldss syldan ) BEFACGABHIIZBJIFNCGABC
      DKNBCDLM $.
      $( [26-Feb-2007] $)

    $( The interior of a subset of a topological space is included in the
       space. $)
    ntrss3 $p |- ( ( J e. Top /\ S C_ X ) -> ( ( int ` J ) ` S ) C_ X ) $=
      ( ctop wcel wss cnt cfv ntropn eltopss syldan ) BEFACGABHIIZBFMCGABCDJMBC
      DKL $.
      $( [1-Oct-2007] $)

    $( The complement of a closure is open. $)
    cmclsopn $p |- ( ( J e. Top /\ S C_ X ) ->
                 ( X \ ( ( cls ` J ) ` S ) ) e. J ) $=
      ( ctop wcel wss wa ccl cfv cdif cnt clsval2 difeq2d wceq difss ntropn
      mpan2 eltopss mpdan dfss4 sylib eqeltrd adantr ) BEFZACGZHZCABIJJZKCCCAKZ
      BLJJZKZKZBUGUHUKCABCDMNUEULBFUFUEULUJBUEUJCGZULUJOUEUJBFZUMUEUICGUNCAPUIB
      CDQRZUJBCDSTUJCUAUBUOUCUDUC $.
      $( [11-Sep-2006] $)

    $( The complement of an interior is closed. $)
    cmntrcld $p |- ( ( J e. Top /\ S C_ X ) ->
                 ( X \ ( ( int ` J ) ` S ) ) e. ( Clsd ` J ) ) $=
      ( ctop wcel wss wa cnt cfv cdif ccl ccld ntrval2 difeq2d wceq difss
      clsss3 mpan2 dfss4 sylib clscld eqeltrd adantr ) BEFZACGZHZCABIJJZKCCCAKZ
      BLJJZKZKZBMJZUGUHUKCABCDNOUEULUMFUFUEULUJUMUEUJCGZULUJPUEUICGZUNCAQZUIBCD
      RSUJCTUAUEUOUJUMFUPUIBCDUBSUCUDUC $.
      $( [1-Oct-2007] $)

    $( A subset is closed iff it equals its own closure. $)
    iscld3 $p |- ( ( J e. Top /\ S C_ X ) -> ( S e. ( Clsd ` J ) <->
                 ( ( cls ` J ) ` S ) = S ) ) $=
      ( ctop wcel wss wa ccld cfv ccl wceq wi cldcls ex adantr eleq1 clscld
      syl5cbi impbid ) BEFZACGZHZABIJZFZABKJJZALZUAUEUGMUBUAUEUGABNOPUGUFUDFUEU
      CUFAUDQABCDRST $.
      $( [2-Oct-2006] $)

    $( A subset is closed iff it contains its own closure. $)
    iscld4 $p |- ( ( J e. Top /\ S C_ X ) -> ( S e. ( Clsd ` J ) <->
                 ( ( cls ` J ) ` S ) C_ S ) ) $=
      ( ctop wcel wss wa ccld cfv ccl wceq iscld3 sscls biantrud eqss syl6rbbr
      bitrd ) BEFACGHZABIJFABKJJZALZTAGZABCDMSUBUBATGZHUASUCUBABCDNOTAPQR $.
      $( [31-Jan-2008] $)

    $( A subset is open iff it equals its own interior. $)
    isopn3 $p |- ( ( J e. Top /\ S C_ X ) -> ( S e. J <->
                 ( ( int ` J ) ` S ) = S ) ) $=
      ( vx ctop wcel wss wa cnt cfv wceq cv crab cuni ntrval unimax sylan9eq
      ex eleq1 ntropn syl5cbi impbid ) BFGACHIZABGZABJKKZALZUDUEUGUDUEUFEMAHEBN
      OAEABCDPEABQRSUGUFBGUEUDUFABTABCDUAUBUC $.
      $( [9-Oct-2006] $)

    $( The closure operation is idempotent. $)
    clsidm $p |- ( ( J e. Top /\ S C_ X ) ->
               ( ( cls ` J ) ` ( ( cls ` J ) ` S ) ) = ( ( cls ` J ) ` S ) ) $=
      ( ctop wcel wss wa ccl cfv ccld wceq clscld wb clsss3 iscld3 syldan
      mpbid ) BEFZACGZHABIJZJZBKJFZUBUAJUBLZABCDMSTUBCGUCUDNABCDOUBBCDPQR $.
      $( [2-Oct-2007] $)

    $( The interior operation is idempotent. $)
    ntridm $p |- ( ( J e. Top /\ S C_ X ) ->
               ( ( int ` J ) ` ( ( int ` J ) ` S ) ) = ( ( int ` J ) ` S ) ) $=
      ( ctop wcel wss wa cnt cfv wceq ntropn wb ntrss3 isopn3 syldan mpbid ) BE
      FZACGZHABIJZJZBFZUATJUAKZABCDLRSUACGUBUCMABCDNUABCDOPQ $.
      $( [2-Oct-2007] $)

    $( The closure of a topology's underlying set is entire set. $)
    clstop $p |- ( J e. Top -> ( ( cls ` J ) ` X ) = X ) $=
      ( ctop wcel ccld cfv ccl wceq topcld wss wb ssid iscld3 mpan2 mpbid ) ADE
      ZBAFGEZBAHGGBIZABCJQBBKRSLBMBABCNOP $.
      $( [5-Oct-2007] $)

    $( The interior of a topology's underlying set is entire set. $)
    ntrtop $p |- ( J e. Top -> ( ( int ` J ) ` X ) = X ) $=
      ( ctop wcel cnt cfv wceq topopn wss wb ssid isopn3 mpan2 mpbid ) ADEZBAEZ
      BAFGGBHZABCIPBBJQRKBLBABCMNO $.
      $( [12-Sep-2006] $)

    $( A subset with an empty interior cannot cover a whole (nonempty)
       topology. $)
    0ntr $p |- ( ( ( J e. Top /\ X =/= (/) ) /\ ( S C_ X
               /\ ( ( int ` J ) ` S ) = (/) ) ) -> ( X \ S ) =/= (/) ) $=
      ( ctop wcel wss cnt cfv c0 wceq wa wne cdif wi fveq2 ntrtop sylan9eqr
      eqeq1d biimpd ex eqss syl5ibr exp3a com34 imp32 ssdif0 necon3d imp an1rs
      ) BEFZACGZABHIZIZJKZLZCJMZCANZJMZUKUPLZUQUSUTURJCJUTCAGZCJKZURJKUKULUOVAV
      BOUKULVAUOVBUKULVAUOVBOZUKACKZVCULVALUKVDVCUKVDLZUOVBVEUNCJVDUKUNCUMICACU
      MPBCDQRSTUAACUBUCUDUEUFCAUGUCUHUIUJ $.
      $( [12-Sep-2006] $)

    $( If a subset is included in a closed set, so is the subset's closure. $)
    clsss2 $p |- ( ( J e. Top /\ C e. ( Clsd ` J ) /\ S C_ C ) ->
                 ( ( cls ` J ) ` S ) C_ C ) $=
      ( ctop wcel ccld cfv wss w3a ccl clsss cldss 3adant3 syld3an2 wceq
      cldcls sseqtrd ) CFGZACHIGZBAJZKBCLIZIZAUCIZATADJZUBUDUEJUAABCDEMTUAUFUBA
      CDENOPTUAUEAQUBACROS $.
      $( [22-Feb-2007] $)

    $( Membership in a closure.  Theorem 6.5(a) of [Munkres] p. 95. $)
    elcls $p |- ( ( J e. Top /\ S C_ X /\ P e. X ) -> ( P e. ( ( cls ` J ) ` S
                ) <-> A. x e. J ( P e. x -> ( x i^i S ) =/= (/) ) ) ) $=
      ( ctop wcel wss w3a ccl cfv cv cin c0 wne wi wral wn wa wrex cdif
      cmclsopn 3adant3 adantr eldif biimpri 3ad2antl3 wceq simpr sscls jca
      ssin sylib dfin4 syl6ss wb reldisj adantl mpbird nne incom eqeq1i bitri
      sylibr eleq2 ineq1 neeq1d notbid anbi12d rcla4ev syl12anc ccld simplll
      opncld adantlr biimpa adantll clsss2 syl111anc sseld eldifn syl6 con2d
      df-ne con2bii sylan2br exp31 com34 imp4a r19.23adv imp 3adantl3 impbida
      rexanali syl6bb con4bid ) DGHZCEIZBEHZJZBCDKLLZHZBAMZHZXDCNZOPZQADRZXAXCS
      ZXEXGSZTZADUAZXHSXAXIXLXAXITEXBUBZDHZBXMHZXMCNZOPZSZXLXAXNXIWRWSXNWTCDEFU
      CUDUEWTWRXIXOWSXOWTXITBEXBUFUGUHXAXRXIWRWSXRWTWRWSTZCXMNZOUIZXRXSYACEXMUB
      ZIZXSCEXBNZYBXSWSCXBIZTCYDIXSWSYEWRWSUJCDEFUKULCEXBUMUNEXBUOUPWSYAYCUQWRC
      XMEURUSUTXRXPOUIYAXPOVAXPXTOXMCVBVCVDVEUDUEXKXOXRTAXMDXDXMUIZXEXOXJXRXDXM
      BVFYFXGXQYFXFXPOXDXMCVGVHVIVJVKVLWRWSXLXIWTXSXLXIXSXKXIADXSXDDHZXEXJXIXSY
      GXJXEXIXSYGXJXEXIQZXSYGTZCXDNZOUIZYHXJYIYKTZXCXEYLXCBEXDUBZHXESYLXBYMBYLW
      RYMDVMLHZCYMIZXBYMIWRWSYGYKVNYIYNYKWRYGYNWSXDDEFVOVPUEXSYKYOYGWSYKYOWRWSY
      KYOCXDEURVQVRVPYMCDEFVSVTWABEXDWBWCWDYKXFOUIZXJYJXFOCXDVBVCXGYPXFOWEWFVDW
      GWHWIWJWKWLWMWNXEXGADWOWPWQ $.
      $( [22-Feb-2007] $)

    $( Membership in a closure. $)
    elcls2 $p |- ( ( J e. Top /\ S C_ X ) -> ( P e. ( ( cls ` J ) ` S
         ) <-> ( P e. X /\ A. x e. J ( P e. x -> ( x i^i S ) =/= (/) ) ) ) ) $=
      ( ctop wcel wss wa ccl cfv cv cin c0 wne wi wral wb clsss3 ssel pm4.71rd
      syl elcls 3expa pm5.32da bitrd ) DGHZCEIZJZBCDKLLZHZBEHZULJZUMBAMZHUOCNOP
      QADRZJUJUKEIZULUNSCDEFTUQULUMUKEBUAUBUCUJUMULUPUHUIUMULUPSABCDEFUDUEUFUG
      $.
      $( [5-Mar-2007] $)

    $( Any open set containing a point that belongs to the closure of a subset
       intersects the subset.  One direction of Theorem 6.5(a) of [Munkres]
       p. 95. $)
    clsndisj $p |- ( ( ( J e. Top /\ S C_ X /\ P e. ( ( cls ` J ) ` S ) ) /\
               ( U e. J /\ P e. U ) ) -> ( U i^i S ) =/= (/) ) $=
      ( vx cv wcel cin c0 wne wi wral wa ctop wss ccl cfv w3a wceq eleq2 ineq1
      neeq1d imbi12d rcla4cv imp32 simp1 simp2 clsss3 sseld 3impia simp3 elcls
      biimpa syl31anc sylan ) AGHZIZURBJZKLZMZGDNZCDIZACIZOCBJZKLZDPIZBEQZABDRS
      SZIZTZVCVDVEVGVBVEVGMGCDURCUAZUSVEVAVGURCAUBVMUTVFKURCBUCUDUEUFUGVLVHVIAE
      IZVKVCVHVIVKUHVHVIVKUIVHVIVKVNVHVIOVJEABDEFUJUKULVHVIVKUMVHVIVNTVKVCGABDE
      FUNUOUPUQ $.
      $( [26-Feb-2007] $)

    $( A subset whose closure has an empty interior also has an empty
       interior. $)
    ntrcls0 $p |- ( ( J e. Top /\ S C_ X /\ ( ( int ` J ) `
                ( ( cls ` J ) ` S ) ) = (/) ) -> ( ( int ` J ) ` S ) = (/) ) $=
      ( ctop wcel wss ccl cfv cnt c0 wceq w3a wa simpl clsss3 sscls ntrss
      syl111anc 3adant3 wb sseq2 3ad2ant3 mpbid ss0 syl ) BEFZACGZABHIIZBJIZIZK
      LZMZAUJIZKGZUNKLUMUNUKGZUOUGUHUPULUGUHNUGUICGAUIGUPUGUHOABCDPABCDQUIABCDR
      STULUGUPUOUAUHUKKUNUBUCUDUNUEUF $.
      $( [4-Oct-2007] $)

    $( Two ways to say that a subset has an empty interior. $)
    ntreq0 $p |- ( ( J e. Top /\ S C_ X ) -> ( ( ( int ` J ) ` S ) = (/) <->
               A. x e. J ( x C_ S -> x = (/) ) ) ) $=
      ( vy ctop wcel wss wa cnt cfv c0 wceq cv crab cuni wi wral ntrval eqeq1d
      wn wal wb wrex elunirab notbii noel bibif ax-mp ralnex 3bitr4i albii
      dfcleq ralcom4 wex neq0 imbi1i 19.23v imnan 3bitr2ri con34b bitr4i
      ralbii bitri syl6bb ) CGHBDIJZBCKLLZMNAOZBIZACPQZMNZVJVIMNZRZACSZVGVHVKMA
      BCDETUAVLFOZVIHZVJJZUBZFUCZACSZVOVPVKHZVPMHZUDZFUCVSACSZFUCVLWAWDWEFWBUBZ
      VRACUEZUBWDWEWBWGVJAVPCUFUGWCUBWDWFUDVPUHWBWCUIUJVRACUKULUMFVKMUNVSAFCUOU
      LVTVNACVTVMUBZVJUBZRZVNWJVQFUPZWIRVQWIRZFUCVTWHWKWIFVIUQURVQWIFUSWLVSFVQV
      JUTUMVAVJVMVBVCVDVEVF $.
      $( [3-Oct-2007] $)
  $}

  $( The closure of the empty set. $)
  cls0 $p |- ( J e. Top -> ( ( cls ` J ) ` (/) ) = (/) ) $=
    ( ctop wcel c0 ccld cfv ccl wceq 0cld cuni wss wb 0ss eqid iscld3 mpan2
    mpbid ) ABCZDAEFCZDAGFFDHZAIRDAJZKSTLUAMDAUAUANOPQ $.
    $( [2-Oct-2007] $)

  $( The interior of the empty set. $)
  ntr0 $p |- ( J e. Top -> ( ( int ` J ) ` (/) ) = (/) ) $=
    ( ctop wcel c0 cnt cfv wceq 0opn cuni wss wb 0ss eqid isopn3 mpan2 mpbid )
    ABCZDACZDAEFFDGZAHQDAIZJRSKTLDATTMNOP $.
    $( [15-Jun-2008] $) $( [2-Oct-2007] $)

  ${
    $d x y z B $.  $d x J $.  $d x y z P $.  $d x y z S $.  $d x U $.
    $d x X $.
    elcls3.1 $e |- J = ( topGen ` B ) $.
    elcls3.2 $e |- X = U. J $.
    $( Membership in a closure in terms of the members of a basis.  Theorem
       6.5(b) of [Munkres] p. 95. $)
    elcls3 $p |- ( ( B e. TopBases /\ S C_ X /\ P e. X ) -> ( P e.
  ( ( cls ` J ) ` S ) <-> A. x e. B ( P e. x -> ( x i^i S ) =/= (/) ) ) ) $=
      ( vy vz ctb wcel wss w3a ccl cfv cv cin c0 wne wi wral ctop wb elcls ctg
      tgcl syl5eqel syl3an1 bastg syl6ssr sseld imim1d ralimdv2 3ad2ant1 wa
      wrex tg2 eleq2i syl3an2b 3expb adantlr wceq ssdisj ex necon3d eleq2
      ineq1 neeq1d imbi12d rcla4cva imp syl5com exp31 imp4a r19.23adv ad2antlr
      mpd exp43 r19.21adv cbvralv syl5ib impbid bitrd ) BKLZDFMZCFLZNZCDEOPPLZC
      AQZLZWJDRZSTZUAZAEUBZWNABUBZEUCLWFWGWIWOUDWEACDEFHUEWEBUFPZUCEBUGGUHUIWHW
      OWPWEWFWOWPUAWGWEWNWNAEBWEWJBLWJELZWNWEBEWJWEBWQEBUJGUKULUMUNUOWEWFWPWOUA
      WGWECIQZLZWSDRZSTZUAZIBUBZWOWPWEXDWNAEWEXDWRWKWMWEXDUPWRWKUPZUPCJQZLZXFWJ
      MZUPZJBUQZWMWEXEXJXDWEWRWKXJWEWJWQLWKXJWRJWJBCUREWQWJGUSUTVAVBXDXJWMUAWEX
      EXDXIWMJBXDXFBLZXGXHWMXDXKXGXHWMUAXHXFDRZSTZWMXDXKUPZXGUPXHWLSXLSXHWLSVCX
      LSVCXFWJDVDVEVFXNXGXMXCXGXMUAIXFBWSXFVCZWTXGXBXMWSXFCVGXOXAXLSWSXFDVHVIVJ
      VKVLVMVNVOVPVQVRVSVTWNXCAIBWJWSVCZWKWTWMXBWJWSCVGXPWLXASWJWSDVHVIVJWAWBUO
      WCWD $.
      $( [26-Feb-2007] $)
  $}

  ${
    txcld.1 $e |- T = ( R tX S ) $.
    $( The product of two closed sets is closed in the product topology.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    txcld $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A e. ( Clsd ` R )
                  /\ B e. ( Clsd ` S ) ) ) -> ( A X. B ) e. ( Clsd ` T ) ) $=
      ( ctop wcel wa ccld cfv cxp cuni wss cdif xpss12 ad2ant2r adantl wceq
      eqid txuni adantr sseqtr4d ex cun difeq1d difxp syl6eq txtop topopn
      ad2antlr txopn anassrs mpdan adantrr ad2antrr ancom2s adantrl unopn
      syl111anc eqeltrd adantrll adantrrl jcad iscld bi2anan9 wb syl 3imtr4d
      imp ) CGHZDGHZIZACJKHZBDJKHZIZABLZEJKHZVMACMZNZVSAOZCHZIZBDMZNZWDBOZDHZIZ
      IZVQEMZNZWJVQOZEHZIZVPVRVMWIWKWMVMWIWKVMWIIVQVSWDLZWJWIVQWONZVMVTWEWPWBWG
      AVSBWDPQRVMWJWOSZWICDEVSWDFVSTZWDTZUAZUBUCUDVMWIWMVMWCWGWMWEVMWBWGWMVTVMW
      BWGIZIZWLWAWDLZVSWFLZUEZWAWFLZUEZEXBWLWOVQOXGXBWJWOVQVMWQXAWTUBUFABVSWDUG
      UHXBEGHZXEEHZXFEHXGEHVMXHXACDEFUIZUBZXBXHXCEHZXDEHZXIXKVMWBXLWGVMWBIWDDHZ
      XLVLXNVKWBDWDWSUJUKVMWBXNXLWAWDCDEFULUMUNUOVMWGXMWBVMWGIVSCHZXMVKXOVLWGCV
      SWRUJUPVMWGXOXMVMXOWGXMVSWFCDEFULUQUMUNURXCXDEUSUTWAWFCDEFULXEXFEUSUTVAVB
      VCUDVDVKVNWCVLVOWHACVSWRVEBDWDWSVEVFVMXHVRWNVGXJVQEWJWJTVEVHVIVJ $.
      $( [2-Sep-2009] $)
  $}

