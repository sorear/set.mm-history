$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                    Hausdorff spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbol for Hausdorff space. $)
  $c Haus $.

  $( Extend class notation with the class of all Hausdorf spaces. $)
  cha $a class Haus $.

  ${
    $d j m n x y w z $.
    $( Define the class of all Hausdorff spaces.  A Hausdorff space is a
       topology in which distinct points have disjoint open neighborhoods.
       Definition of Hausdorff space in [Munkres] p. 98. $)
    df-haus $a |- Haus = { j e. Top | A. x e. U. j A. y e. U. j ( x =/= y ->
           E. n e. j E. m e. j ( x e. n /\ y e. m /\ ( n i^i m ) = (/) ) ) } $.

    $( MAY BE REVISED $)
    $( Alternate definition of the class of all Hausdorff spaces in terms
       of (not necessarily open) neighborhoods. $)
    dfhaus2 $p |- Haus = { j e. Top | A. x e. U. j A. y e. U. j ( x =/= y ->
               E. n e. ( ( nei ` j ) ` { x } ) E. m e. ( ( nei ` j ) ` { y } )
                  ( n i^i m ) = (/) ) } $=
      ( vz vw cha cv wne wcel cin c0 wceq w3a wrex wi cuni wral ctop crab csn
      cnei cfv df-haus wa wb ineq1 eqeq1d ineq2 rcla42ev opnneip 3expa
      adantlrr adantrr 3adantr3 adantlrl adantrl 3simp3 adantl syl3anc exp31
      r19.23advv adantr ad2antrr wss neii2 ex anim12d reeanv syl6ibr visset
      snss biimpr sseq0 ss2in sylan 3anim123i anassrs expcom an4 syl5ib
      r19.22sdv com12 syl6 impbid pm5.74d ralbidva rabbii eqtr ) HAIZBIZJZWKFIZ
      KZWLGIZKZWNWPLZMNZOZGCIZPZFXAPZQZBXARZSZAXESZCTUAWMEIZDIZLZMNZDWLUBZXAUCU
      DZUDZPEWKUBZXMUDZPZQZBXESZAXESZCTUAABCGFUEXGXTCTXATKZXFXSAXEYAWKXEKZUFZXD
      XRBXEYCWLXEKZUFZWMXCXQYEWMXCXQUGYEWMUFZXCXQYCXCXQQZYDWMYAYGYBYAWTXQFGXAXA
      YAWNXAKZWPXAKZUFZWTXQWNXPKZWPXNKZWSXQYAYJUFZWTUFXKWSWNXILZMNEDWNWPXPXNXHW
      NNXJYNMXHWNXIUHUIXIWPNYNWRMXIWPWNUJUIUKYMWOWQYKWSYMWOYKWQYAYHWOYKYIYAYHWO
      YKWKXAWNULUMUNUOUPYMWOWQYLWSYMWQYLWOYAYIWQYLYHYAYIWQYLWLXAWPULUMUQURUPWTW
      SYMWOWQWSUSUTVAVBVCVDVEYFXKXCEDXPXNYFXHXPKZXIXNKZUFZXOWNVFZWNXHVFZUFZXLWP
      VFZWPXIVFZUFZUFZGXAPZFXAPZXKXCQYCYQUUFQZYDWMYAUUGYBYAYQYTFXAPZUUCGXAPZUFU
      UFYAYOUUHYPUUIYAYOUUHXOFXAXHVGVHYAYPUUIXLGXAXIVGVHVIYTUUCFGXAXAVJVKVDVEXK
      UUFXCXKUUEXBFXAXKUUDWTGXAXKYRUUAUFZYSUUBUFZUFZWTUUDUULXKWTUUJUUKXKWTYRUUA
      UUKXKUFZWTYRWOUUAWQUUMWSWOYRWKWNAVLVMVNWQUUAWLWPBVLVMVNWRXJVFXKWSUUKWRXJV
      OWNXHWPXIVPVQVRUMVSVTYRYSUUAUUBWAWBWCWCWDWEVCWFVHWGWHWHWIWJ $.
      $( [8-Mar-2007] $) $( [8-Mar-2007] $)
  $}

  ${
    $d j m n x y J $.  $d j x y X $.  $d m n x y P $.  $d m n y Q $.
    ishaus.1 $e |- X = U. J $.
    $( Express the predicate " ` J ` is a Hausdorff space." $)
    ishaus $p |- ( J e. Haus <-> ( J e. Top /\ A. x e. X A. y e. X ( x =/= y ->
               E. n e. J E. m e. J ( x e. n /\ y e. m /\
                   ( n i^i m ) = (/) ) ) ) ) $=
      ( vj cv wne wcel cin c0 wceq w3a wrex wi cuni wral ctop cha unieq
      syl6eqr raleq1d rexeq1 rexeqd imbi2d ralbidv bitrd df-haus elrab2 ) AIZBI
      ZJZULDIZKUMCIZKUOUPLMNOZCHIZPZDURPZQZBURRZSZAVBSZUNUQCEPZDEPZQZBFSZAFSZHE
      TUAURENZVDVCAFSVIVJVCAVBFVJVBERFUREUBGUCZUDVJVCVHAFVJVCVABFSVHVJVABVBFVKU
      DVJVAVGBFVJUTVFUNUSVEDUREUQCUREUEUFUGUHUIUHUIABHCDUJUK $.
      $( [15-Mar-2007] $) $( [8-Mar-2007] $)

    $( Neighborhood property of a Hausdorff space. $)
    hausnei $p |- ( ( J e. Haus /\ ( P e. X /\ Q e. X /\ P =/= Q ) ) ->
           E. n e. J E. m e. J ( P e. n /\ Q e. m /\
                   ( n i^i m ) = (/) ) ) $=
      ( vx vy cha wcel wne cv cin c0 wceq w3a wrex wi wa wral neeq1 eleq1
      3anbi1d 2rexbidv imbi12d neeq2 3anbi2d rcla42v ctop ishaus pm3.27bd syl5
      ex com3r 3imp2 ) EJKZAFKZBFKZABLZADMZKZBCMZKZVAVCNOPZQZCERDERZURUSUQUTVGS
      ZURUSUQVHSURUSTHMZIMZLZVIVAKZVJVCKZVEQZCERDERZSZIFUAHFUAZVHUQVPVHAVJLZVBV
      MVEQZCERDERZSHIABFFVIAPZVKVRVOVTVIAVJUBWAVNVSDCEEWAVLVBVMVEVIAVAUCUDUEUFV
      JBPZVRUTVTVGVJBAUGWBVSVFDCEEWBVMVDVBVEVJBVCUCUHUEUFUIUQEUJKVQHICDEFGUKULU
      MUNUOUP $.
      $( [23-Apr-2007] $) $( [8-Mar-2007] $)

    $( MAY BE REVISED $)
    $( Express the predicate " ` J ` is a Hausdorff space" in terms
       of (not necessarily open) neighborhoods. $)
    ishaus2 $p |- ( J e. Haus <-> ( J e. Top /\ A. x e. X A. y e. X ( x =/= y
             -> E. n e. ( ( nei ` J ) ` { x } ) E. m e. ( ( nei ` J ) ` { y } )
                   ( n i^i m ) = (/) ) ) ) $=
      ( vj cv wne cin c0 wceq csn cnei cfv wrex wi cuni wral ctop cha unieq
      syl6eqr raleq1d fveq2 fveq1d rexeq1d rexbidv bitrd imbi2d ralbidv
      dfhaus2 elrab2 ) AIZBIZJZDICIKLMZCUPNZHIZOPZPZQZDUONZVAPZQZRZBUTSZTZAVHTZ
      UQURCUSEOPZPZQZDVDVKPZQZRZBFTZAFTZHEUAUBUTEMZVJVIAFTVRVSVIAVHFVSVHESFUTEU
      CGUDZUEVSVIVQAFVSVIVGBFTVQVSVGBVHFVTUEVSVGVPBFVSVFVOUQVSVFVCDVNQVOVSVCDVE
      VNVSVDVAVKUTEOUFZUGUHVSVCVMDVNVSURCVBVLVSUSVAVKWAUGUHUIUJUKULUJULUJABHCDU
      MUN $.
      $( [17-Mar-2007] $) $( [5-Mar-2007] $)

    $( MAY BE REVISED $)
    $( Neighborhood property of a Hausdorff space. $)
    hausnei2 $p |- ( ( J e. Haus /\ ( P e. X /\ Q e. X /\ P =/= Q ) ) ->
           E. n e. ( ( nei ` J ) ` { P } ) E. m e. ( ( nei ` J ) ` { Q } )
                   ( n i^i m ) = (/) ) $=
      ( vx vy cha wcel wne cv cin c0 wceq csn cnei cfv wrex wi wa wral neeq1
      sneq fveq2d rexeq1d imbi12d neeq2 rexbidv rcla42v ctop ishaus2 pm3.27bd
      syl5 ex com3r 3imp2 ) EJKZAFKZBFKZABLZDMCMNOPZCBQZERSZSZTZDAQZVESZTZUTVAU
      SVBVJUAZUTVAUSVKUAUTVAUBHMZIMZLZVCCVMQZVESZTZDVLQZVESZTZUAZIFUCHFUCZVKUSW
      AVKAVMLZVQDVITZUAHIABFFVLAPZVNWCVTWDVLAVMUDWEVQDVSVIWEVRVHVEVLAUEUFUGUHVM
      BPZWCVBWDVJVMBAUIWFVQVGDVIWFVCCVPVFWFVOVDVEVMBUEUFUGUJUHUKUSEULKWBHICDEFG
      UMUNUOUPUQUR $.
      $( [?] $) $( [5-Mar-2007] $)
  $}

  ${
    $d m n x y J $.  $d x y X $.
    ishausi.1 $e |- X = U. J $.
    ishausi.2 $e |- J e. Top $.
    ishausi.3 $e |- ( ( x e. X /\ y e. X /\ x =/= y ) ->
             E. n e. J E. m e. J ( x e. n /\ y e. m /\ ( n i^i m ) = (/) ) ) $.
    $( Properties that determine a Hausdorff space. $)
    ishausi $p |- J e. Haus $=
      ( cha wcel ctop cv wne cin c0 wceq w3a wrex wi wral ishaus wa 3expa ex
      rgen2 mpbir2an ) EJKELKAMZBMZNZUHDMZKUICMZKUKULOPQRCESDESZTZBFUAAFUAABCDE
      FGUBHUNABFUHFKZUIFKZUCUJUMUOUPUJUMIUDUEUFUG $.
      $( [18-Apr-2007] $) $( [8-Mar-2007] $)
  $}

  ${
    $d x y m n J $.
    $( A Hausdorff space is a topology. $)
    haustop $p |- ( J e. Haus -> J e. Top ) $=
      ( vx vy vn vm cha wcel ctop cv wne cin c0 wceq w3a wrex wi cuni wral
      eqid ishaus pm3.26bd ) AFGAHGBIZCIZJUBDIZGUCEIZGUDUEKLMNEAODAOPCAQZRBUFRB
      CEDAUFUFSTUA $.
      $( [22-Apr-2007] $) $( [5-Mar-2007] $)
  $}

  ${
    $d n x y J $.  $d n x y P $.  $d x y X $.
    snclsd.1 $e |- X = U. J $.
    $( A singleton is closed in a Hausdorff space. $)
    snclsd $p |- ( ( J e. Haus /\ P e. X ) -> { P } e. ( Cls ` J ) ) $=
      ( vx vy vn cha wcel wa csn ccld cfv ccl wceq cv cin c0 wne wi wral ctop
      wss wb elcls2 haustop snssi syl2an wrex wn w3a hausnei sseq0 sslin syl
      sylan anim2i 3impb a1i r19.23aiv r19.22si 3exp2 imp31 an1rs necon1bd
      df-ne imbi2i ralbii ralinexa bitr syl5ib ex imp3a eleq1a adantl eleq1
      biimpd disjsn necon2abii syl6ib adantr r19.21aiva jcad impbid elsn
      syl6bbr bitrd eqrdv isclsd3 mpbird ) BHIZACIZJZAKZBLMIZWNBNMMZWNOZWMEWPWN
      WMEPZWPIZWRCIZWRFPZIZXAWNQZRSZTZFBUAZJZWRWNIZBUBIZWNCUCZWSXGUDWKWLFWRWNBC
      DUEBUFZACUGZUHWMXGWRAOZXHWMXGXMWMWTXFXMWMWTXFXMTWMWTJZXBXCROZJZFBUIZUJZXM
      XFXNXQWRAWKWTWLWRASZXQTZWKWTWLXTWKWTWLXSXQWKWTWLXSUKJXBAGPZIZXAYAQZROZUKZ
      GBUIZFBUIXQWRAGFBCDULYFXPFBYEXPGBYEXPTYABIXBYBYDXPYBYDJXOXBXCYCUCZYDXOYBX
      CYCUMYBWNYAUCYGAYAUGWNYAXAUNUOUPUQURUSUTVAUOVBVCVDVEXFXBXOUJZTZFBUAXRXEYI
      FBXDYHXBXCRVFVGVHXBXOFBVIVJVKVLVMWMXMWTXFWLXMWTTWKACWRVNVOXMXFTWMXMXEFBXM
      XEXABIXMXBAXAIZXDXMXBYJWRAXAVPVQYJXCRXAAVRVSVTWAWBUSWCWDEAWEWFWGWHXIXJWOW
      QUDWKWLWNBCDWIXKXLUHWJ $.
      $( [25-Apr-2007] $) $( [5-Mar-2007] $)
  $}

  ${
    dnsconst.1 $e |- X = U. J $.
    dnsconst.2 $e |- Y = U. K $.
    $( If a continuous mapping to a Hausdorff space is constant on a dense
       subset, it is constant on the entire space.  Note that
       ` ( ( cls `` J ) `` A ) = X ` means " ` A ` is dense in ` X ` " and
       ` A (_ ( ``' F " { P } ) ` means " ` F ` is constant on ` A ` " (see
       ~ funconstss ). $)
    dnsconst $p |- ( ( ( J e. Top /\ K e. Haus /\ F e. ( J Cn K ) ) /\
  ( P e. Y /\ A (_ ( `' F " { P } ) /\ ( ( cls ` J ) ` A ) = X ) ) ->
        F : X --> { P } ) $=
      ( ctop wcel cha ccn co w3a ccnv csn cima wss ccl cfv wceq wa wfn wf cnf
      haustop syl3an2 ffn syl adantr ccld snclsd adantll 3adantl3 cnclima
      syl3anl2 syldan adantrr 3adantr3 wi clsss simpll clsdss adantlr simplr
      syl3anc clsdcls sseqtrd ex wb sseq1 ad2antll sylibd 3adantr1 mpd jca
      fconst3 sylibr ) DJKZELKZCDEMNKZOZBGKZACPBQZRZSZADTUAZUAZFUBZOZUCZCFUDZFW
      FSZUCFWECUEWLWMWNWCWMWKWCFGCUEZWMVTEJKZWBWOWACDEFGHIUFEUGZUHFGCUIUJUKWLWF
      DULUAKZWNWCWDWGWRWJWCWDWRWGWCWDWEEULUAKZWRVTWAWDWSWBWAWDWSVTBEGIUMUNUOVTW
      PWBWSWRWAWECDEUPWQUQURUSUTVTWAWKWRWNVAZWBVTWAUCWGWJWTWDVTWGWJUCZWTWAVTXAU
      CWRWIWFSZWNVTWGWRXBVAWJVTWGUCZWRXBXCWRUCZWIWFWHUAZWFVTWFFSZWGWIXESXDWFADF
      HVBVTWGWRVCVTWRXFWGWFDFHVDVEVTWGWRVFVGVTWRXEWFUBWGWFDVHVEVIVJUSWJXBWNVKVT
      WGWIFWFVLVMVNVEVOUOVPVQFBCVRVS $.
      $( [28-Apr-2007] $) $( [15-Mar-2007] $)
  $}

