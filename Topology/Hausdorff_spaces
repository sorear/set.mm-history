$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                    Hausdorff spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbol for Hausdorff space. $)
  $c Haus $.

  $( Extend class notation with the class of all Hausdorf spaces. $)
  cha $a class Haus $.

  ${
    $d j m n x y w z $.
    $( Define the class of all Hausdorff spaces.  A Hausdorff space is a
       topology in which distinct points have disjoint open neighborhoods.
       Definition of Hausdorff space in [Munkres] p. 98. $)
    df-haus $a |- Haus = { j e. Top | A. x e. U. j A. y e. U. j ( x =/= y ->
           E. n e. j E. m e. j ( x e. n /\ y e. m /\ ( n i^i m ) = (/) ) ) } $.
  $}

  ${
    $d j m n x y J $.  $d j x y X $.  $d m n x y P $.  $d m n y Q $.
    ishaus.1 $e |- X = U. J $.
    $( Express the predicate " ` J ` is a Hausdorff space." $)
    ishaus $p |- ( J e. Haus <-> ( J e. Top /\ A. x e. X A. y e. X ( x =/= y ->
               E. n e. J E. m e. J ( x e. n /\ y e. m /\
                   ( n i^i m ) = (/) ) ) ) ) $=
      ( vj cv wne wcel cin c0 wceq w3a wrex wi cuni wral ctop cha unieq
      syl6eqr rexeq rexeqbi1dv imbi2d raleqbidv df-haus elrab2 ) AIZBIZJZUJDIZK
      UKCIZKUMUNLMNOZCHIZPZDUPPZQZBUPRZSZAUTSULUOCEPZDEPZQZBFSZAFSHETUAUPENZVAV
      EAUTFVFUTERFUPEUBGUCZVFUSVDBUTFVGVFURVCULUQVBDUPEUOCUPEUDUEUFUGUGABHCDUHU
      I $.
      $( [15-Mar-2007] $) $( [8-Mar-2007] $)

    $( Neighborhood property of a Hausdorff space. $)
    hausnei $p |- ( ( J e. Haus /\ ( P e. X /\ Q e. X /\ P =/= Q ) ) ->
           E. n e. J E. m e. J ( P e. n /\ Q e. m /\
                   ( n i^i m ) = (/) ) ) $=
      ( vx vy cha wcel wne cv cin c0 wceq w3a wrex wi wa wral neeq1 eleq1
      3anbi1d 2rexbidv imbi12d neeq2 3anbi2d rcla42v ctop ishaus pm3.27bi syl5
      ex com3r 3imp2 ) EJKZAFKZBFKZABLZADMZKZBCMZKZVAVCNOPZQZCERDERZURUSUQUTVGS
      ZURUSUQVHSURUSTHMZIMZLZVIVAKZVJVCKZVEQZCERDERZSZIFUAHFUAZVHUQVPVHAVJLZVBV
      MVEQZCERDERZSHIABFFVIAPZVKVRVOVTVIAVJUBWAVNVSDCEEWAVLVBVMVEVIAVAUCUDUEUFV
      JBPZVRUTVTVGVJBAUGWBVSVFDCEEWBVMVDVBVEVJBVCUCUHUEUFUIUQEUJKVQHICDEFGUKULU
      MUNUOUP $.
      $( [23-Apr-2007] $) $( [8-Mar-2007] $)
  $}

  ${
    $d m n x y J $.  $d x y X $.
    ishausi.1 $e |- X = U. J $.
    ishausi.2 $e |- J e. Top $.
    ishausi.3 $e |- ( ( x e. X /\ y e. X /\ x =/= y ) ->
             E. n e. J E. m e. J ( x e. n /\ y e. m /\ ( n i^i m ) = (/) ) ) $.
    $( Properties that determine a Hausdorff space. $)
    ishausi $p |- J e. Haus $=
      ( cha wcel ctop cv wne cin c0 wceq w3a wrex wi wral ishaus 3expia rgen2a
      mpbir2an ) EJKELKAMZBMZNZUFDMZKUGCMZKUIUJOPQRCESDESZTZBFUAAFUAABCDEFGUBHU
      LABFUFFKUGFKUHUKIUCUDUE $.
      $( [18-Apr-2007] $) $( [8-Mar-2007] $)
  $}

  ${
    $d x y m n J $.
    $( A Hausdorff space is a topology. $)
    haustop $p |- ( J e. Haus -> J e. Top ) $=
      ( vx vy vn vm cha wcel ctop cv wne cin c0 wceq w3a wrex wi cuni wral
      eqid ishaus pm3.26bi ) AFGAHGBIZCIZJUBDIZGUCEIZGUDUEKLMNEAODAOPCAQZRBUFRB
      CEDAUFUFSTUA $.
      $( [22-Apr-2007] $) $( [5-Mar-2007] $)
  $}

  ${
    $d n x y J $.  $d n x y P $.  $d x y X $.
    sncld.1 $e |- X = U. J $.
    $( A singleton is closed in a Hausdorff space. $)
    sncld $p |- ( ( J e. Haus /\ P e. X ) -> { P } e. ( Clsd ` J ) ) $=
      ( vx vy vn cha wcel wa csn ccld cfv ccl wceq cv cin c0 wne wi wral ctop
      wss wb elcls2 haustop snssi syl2an wrex wn w3a hausnei sseq0 sslin syl
      sylan anim2i 3impb a1i r19.23aiv reximi 3exp2 imp31 an1rs necon1bd df-ne
      imbi2i ralbii ralinexa bitri syl5ib expimpd eleq1a adantl eleq1 biimpd
      disjsn necon2abii syl6ib adantr r19.21aiva jcad impbid elsn syl6bbr
      bitrd eqrdv iscld3 mpbird ) BHIZACIZJZAKZBLMIZWMBNMMZWMOZWLEWOWMWLEPZWOIZ
      WQCIZWQFPZIZWTWMQZRSZTZFBUAZJZWQWMIZBUBIZWMCUCZWRXFUDWJWKFWQWMBCDUEBUFZAC
      UGZUHWLXFWQAOZXGWLXFXLWLWSXEXLWLWSJZXAXBROZJZFBUIZUJZXLXEXMXPWQAWJWSWKWQA
      SZXPTZWJWSWKXSWJWSWKXRXPWJWSWKXRUKJXAAGPZIZWTXTQZROZUKZGBUIZFBUIXPWQAGFBC
      DULYEXOFBYDXOGBYDXOTXTBIXAYAYCXOYAYCJXNXAXBYBUCZYCXNYAXBYBUMYAWMXTUCYFAXT
      UGWMXTWTUNUOUPUQURUSUTVAUOVBVCVDVEXEXAXNUJZTZFBUAXQXDYHFBXCYGXAXBRVFVGVHX
      AXNFBVIVJVKVLWLXLWSXEWKXLWSTWJACWQVMVNXLXETWLXLXDFBXLXDWTBIXLXAAWTIZXCXLX
      AYIWQAWTVOVPYIXBRWTAVQVRVSVTWAUSWBWCEAWDWEWFWGXHXIWNWPUDWJWKWMBCDWHXJXKUH
      WI $.
      $( [25-Apr-2007] $) $( [5-Mar-2007] $)
  $}

  ${
    dnsconst.1 $e |- X = U. J $.
    dnsconst.2 $e |- Y = U. K $.
    $( If a continuous mapping to a Hausdorff space is constant on a dense
       subset, it is constant on the entire space.  Note that
       ` ( ( cls `` J ) `` A ) = X ` means " ` A ` is dense in ` X ` " and
       ` A C_ ( ``' F " { P } ) ` means " ` F ` is constant on ` A ` " (see
       ~ funconstss ). $)
    dnsconst $p |- ( ( ( J e. Top /\ K e. Haus /\ F e. ( J Cn K ) ) /\
  ( P e. Y /\ A C_ ( `' F " { P } ) /\ ( ( cls ` J ) ` A ) = X ) ) ->
        F : X --> { P } ) $=
      ( csn wf wfn ccnv cima wss ctop wcel cha ccn co w3a ccl cfv wceq wa
      fconst3 cnf haustop syl3an2 ffn syl adantr ccld sncld 3ad2antl2 cnclima
      syl3anl2 syldan 3ad2antr1 wi clsss simpll cldss adantlr simplr syl3anc
      cldcls sseqtrd ex adantrr wb sseq1 ad2antll sylibd 3adantr1 3adantl3 mpd
      sylanbrc ) FBJZCKCFLZFCMVSNZOZDPQZERQZCDESTQZUAZBGQZAWAOZADUBUCZUCZFUDZUA
      ZUEZFBCUFWFVTWLWFFGCKZVTWCEPQZWEWNWDCDEFGHIUGEUHZUIFGCUJUKULWMWADUMUCQZWB
      WFWHWGWQWKWFWGVSEUMUCQZWQWDWCWGWRWEBEGIUNUOWCWOWEWRWQWDVSCDEUPWPUQURUSWCW
      DWLWQWBUTZWEWCWDUEWHWKWSWGWCWHWKUEZWSWDWCWTUEWQWJWAOZWBWCWHWQXAUTWKWCWHUE
      ZWQXAXBWQUEZWJWAWIUCZWAWCWAFOZWHWJXDOXCWAADFHVAWCWHWQVBWCWQXEWHWADFHVCVDW
      CWHWQVEVFWCWQXDWAUDWHWADVGVDVHVIVJWKXAWBVKWCWHWJFWAVLVMVNVDVOVPVQVR $.
      $( [28-Apr-2007] $) $( [15-Mar-2007] $)
  $}

