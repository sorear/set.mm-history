$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Product topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c tX $.

  $( Extend class notation with the binary topological product operation. $)
  ctx $a class tX $.

  ${
    $d r s t u v z $.
    $( Define the binary topological product, which is homeomorphic to the
       general topological product over a two element set, but is more
       convenient to use.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    df-tx $a |- tX = { <. <. r , s >. , t >. | ( ( r e. Top /\ s e. Top ) /\
             t = ( topGen ` { z | E. u e. r E. v e. s z = ( u X. v ) } ) ) } $.
  $}

  ${
    $d R r s t u v z $.  $d S r s t u v z $.
    $( Value of the binary topological product operation.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    txval $p |- ( ( R e. Top /\ S e. Top ) -> ( R tX S ) =
                  ( topGen ` { z | E. u e. R E. v e. S z = ( u X. v ) } ) ) $=
      ( vr vs vt ctop cv cxp wceq wrex cab ctg cfv ctx fvex rexeq abbidv
      fveq2d rexbidv df-tx oprabval2 ) FGHDEIIAJCJBJKLZBGJZMZCFJZMZANZOPUEBEMZC
      DMZANZOPQUGCDMZANZOPUMORUHDLZUJUOOUPUIUNAUGCUHDSTUAUFELZUOUMOUQUNULAUQUGU
      KCDUEBUFESUBTUAABCHGFUCUD $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d R a b c d r s u v x y z $.  $d S a b c d r s u v x y z $.
    $( A lemma for theorems about product topologies.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    txbas $p |- ( ( R e. Top /\ S e. Top ) ->
                  { z | E. u e. R E. v e. S z = ( u X. v ) } e. Bases ) $=
      ( vx vy vs vr va vb vc vd ctop wcel wa cv cxp wceq wrex cab cvv cin wral
      ctb rexeq abbidv eleq1d rexbidv visset abrexex abrexex2 vtocl2g wi ineq1
      inopn 3expb ancom2s adantlr anassrs adantlrr adantrr adantll adantlrl
      adantrl inxp a1i xpeq1 eqeq2d xpeq2 rcla42ev syl111anc xpex inex1 eqeq1
      2rexbidv elab sylibr syl5cbir ineq2 biimprcd syl6 ex com24 r19.23advv
      cbvrexv rexbii bitri syl5ib imp3a anbi12i r19.21aivv fiinbas syl11anc )
      DNOZENOZPZAQZCQZBQZRZSZBETZCDTZAUAZUBOZFQZGQZUCZXEOZGXEUDFXEUDXEUEOXBBHQZ
      TZCIQZTZAUAZUBOXLCDTZAUAZUBOXFIHDENNXMDSZXOXQUBXRXNXPAXLCXMDUFUGUHXKESZXQ
      XEUBXSXPXDAXSXLXCCDXBBXKEUFUIUGUHXLCAXMIUJBAXKXAHUJUKULUMWQXJFGXEXEWQXGXA
      SZBETZCDTZXHXASZBETZCDTZPXJXGXEOZXHXEOZPWQYBYEXJWQXGJQZKQZRZSZKETZJDTZYEX
      JUNZYBWQYKYNJKDEWQYEYKYHDOZYIEOZPZXJWQXHLQZMQZRZSZMETZLDTZYKYQXJUNUNZYEWQ
      UUAUUDLMDEWQYRDOZYSEOZPZUUAUUDUNWQUUGPZYQYKUUAXJUUHYQYKUUAXJUNZUNUUHYQPZY
      KXGYTUCZXEOZUUIYKUULYJYTUCZXEOZUUJYKUUKUUMXEXGYJYTUOUHUUJUUMXASZBETCDTZUU
      NUUJYHYRUCZDOZYIYSUCZEOZUUMUUQUUSRZSZUUPUUHYOUURYPWQUUEYOUURUUFWQUUEYOUUR
      WOUUEYOPUURWPWOYOUUEUURWOYOUUEUURYHYRDUPUQURUSUTVAVBUUHYPUUTYOWQUUFYPUUTU
      UEWQUUFYPUUTWPUUFYPPUUTWOWPYPUUFUUTWPYPUUFUUTYIYSEUPUQURVCUTVDVEUVBUUJYHY
      IYRYSVFVGUUOUVBUUMUUQWTRZSCBUUQUUSDEWSUUQSXAUVCUUMWSUUQWTVHVIWTUUSSUVCUVA
      UUMWTUUSUUQVJVIVKVLXDUUPAUUMYJYTYHYIJUJKUJVMVNWRUUMSXBUUOCBDEWRUUMXAVOVPV
      QVRVSUUAXJUULUUAXIUUKXEXHYTXGVTUHWAWBWCWDWCWEYEXHWSYSRZSZMETZCDTUUCYDUVFC
      DYCUVEBMEWTYSSXAUVDXHWTYSWSVJVIWFWGUVFUUBCLDWSYRSZUVEUUAMEUVGUVDYTXHWSYRY
      SVHVIUIWFWHWIWDWEYBXGWSYIRZSZKETZCDTYMYAUVJCDXTUVIBKEWTYISXAUVHXGWTYIWSVJ
      VIWFWGUVJYLCJDWSYHSZUVIYKKEUVKUVHYJXGWSYHYIVHVIUIWFWHWIWJYFYBYGYEXDYBAXGF
      UJWRXGSXBXTCBDEWRXGXAVOVPVQXDYEAXHGUJWRXHSXBYCCBDEWRXHXAVOVPVQWKWIWLFGXEU
      BWMWN $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d R u v z $.  $d S u v z $.  $d T u v z $.
    txtop.1 $e |- T = ( R tX S ) $.
    $( The product of two topologies is a topology.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    txtop $p |- ( ( R e. Top /\ S e. Top ) -> T e. Top ) $=
      ( vz vu vv ctop wcel wa cv cxp wceq wrex cab ctg cfv ctx co txval syl5eq
      ctb txbas tgcl syl eqeltrd ) AHIBHIJZCEKFKGKLMGBNFANEOZPQZHUGABRSUICEGFAB
      TDUAUGUHUBIUIHIEGFABUCUHUDUEUF $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    txtopi.1 $e |- R e. Top $.
    txtopi.2 $e |- S e. Top $.
    $( The product of two topologies is a topology.  (Contributed by Jeff
       Madsen, 25-Jun-2011.) $)
    txtopi $p |- ( R tX S ) e. Top $=
      ( ctop wcel ctx co eqid txtop mp2an ) AEFBEFABGHZEFCDABLLIJK $.
      $( [25-Jun-2011] $) $( [15-Jun-2010] $)
  $}

  ${
    $d R u v x z $.  $d S u v x z $.  $d T u v x z $.  $d X u v x z $.
    $d Y u v x z $.
    txuni.1 $e |- T = ( R tX S ) $.
    txuni.2 $e |- X = U. R $.
    txuni.3 $e |- Y = U. S $.
    $( The underlying set of the product of two topologies.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    txuni $p |- ( ( R e. Top /\ S e. Top ) -> U. T = ( X X. Y ) ) $=
      ( vz vu vv vx ctop wcel wa cuni cv cxp wceq wrex cab ctg cfv ctx co
      txval syl5eq unieqd ctb txbas unitg syl wss wral wi xpss12 eltopss
      syl2an an4s ex sseq1 biimprcd syl6 r19.23advv visset eqeq1 2rexbidv elab
      syl5ib r19.21aiv unissb sylibr topopn adantr adantl eqidd xpeq1 eqeq2d
      xpeq2 rcla42ev syl111anc cvv wb xpexg uniexg syl5eqel elabg mpbird
      elssuni eqssd 3eqtrd ) AMNZBMNZOZCPIQZJQZKQZRZSZKBTJATZIUAZUBUCZPZXAPZDER
      ZWNCXBWNABUDUEXBCIKJABUFFUGUHWNXAUINXCXDSIKJABUJXAUKULWNXDXEWNLQZXEUMZLXA
      UNXDXEUMWNXGLXAWNXFWRSZKBTJATZXGXFXANWNXHXGJKABWNWPANZWQBNZOZWRXEUMZXHXGU
      OWNXLXMWLXJWMXKXMWPDUMWQEUMXMWLXJOWMXKOWPDWQEUPWPADGUQWQBEHUQURUSUTXHXGXM
      XFWRXEVAVBVCVDWTXIIXFLVEWOXFSWSXHJKABWOXFWRVFVGVHVIVJLXAXEVKVLWNXEXANZXEX
      DUMWNXNXEWRSZKBTJATZWNDANZEBNZXEXESZXPWLXQWMADGVMVNWMXRWLBEHVMVOWNXEVPXOX
      SXEDWQRZSJKDEABWPDSWRXTXEWPDWQVQVRWQESXTXEXEWQEDVSVRVTWAWNXEWBNZXNXPWCDWB
      NEWBNYAWLWMDEWBWBWDWLAPWBDAMWEGWFWMBPWBEBMWEHWFURWTXPIXEWBWOXESWSXOJKABWO
      XEWRVFVGWGULWHXEXAWIULWJWK $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    txunii.1 $e |- R e. Top $.
    txunii.2 $e |- S e. Top $.
    txunii.3 $e |- X = U. R $.
    txunii.4 $e |- Y = U. S $.
    $( The underlying set of the product of two topologies.  (Contributed by
       Jeff Madsen, 25-Jun-2011.) $)
    txunii $p |- ( X X. Y ) = U. ( R tX S ) $=
      ( ctx co cuni cxp ctop wcel wceq eqid txuni mp2an eqcomi ) ABIJZKZCDLZAMN
      BMNUAUBOEFABTCDTPGHQRS $.
      $( [25-Jun-2011] $) $( [15-Jun-2010] $)
  $}

  ${
    $d R u v z $.  $d S u v z $.  $d T u v z $.  $d A u v z $.  $d B u v z $.
    txopn.1 $e |- T = ( R tX S ) $.
    $( The product of two open sets is open in the product topology.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    txopn $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A e. R /\ B e. S ) )
                                                      -> ( A X. B ) e. T ) $=
      ( vz vu vv ctop wcel wa cxp cv wceq wrex cab ctg cfv wss ctb txbas bastg
      syl adantr eqid xpeq1 eqeq2d xpeq2 rcla42ev mp3an3 cvv wb xpexg eqeq1
      2rexbidv elabg mpbird adantl sseldd ctx co txval syl5eq eleqtrrd ) CJKDJK
      LZACKZBDKZLZLZABMZGNZHNZINZMZOZIDPHCPZGQZRSZEVJVRVSVKVFVRVSTZVIVFVRUAKVTG
      IHCDUBVRUCUDUEVIVKVRKZVFVIWAVKVOOZIDPHCPZVGVHVKVKOZWCVKUFWBWDVKAVNMZOHIAB
      CDVMAOVOWEVKVMAVNUGUHVNBOWEVKVKVNBAUIUHUJUKVIVKULKWAWCUMABCDUNVQWCGVKULVL
      VKOVPWBHICDVLVKVOUOUPUQUDURUSUTVFEVSOVIVFCDVAVBVSEGIHCDVCFVDUEVE $.
      $( [2-Sep-2009] $)
  $}


