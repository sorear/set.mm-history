$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Product topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c tX $.

  $( Extend class notation with the binary topological product operation. $)
  ctx $a class tX $.

  ${
    $d r s x y z $.
    $( Define the binary topological product, which is homeomorphic to the
       general topological product over a two element set, but is more
       convenient to use.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    df-tx $a |- tX = ( r e. Top , s e. Top |->
             ( topGen ` { x | E. y e. r E. z e. s x = ( y X. z ) } ) ) $.
  $}

  ${
    $d R r s t u v z $.  $d S r s t u v z $.
    $( Value of the binary topological product operation.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    txval $p |- ( ( R e. Top /\ S e. Top ) -> ( R tX S ) =
                  ( topGen ` { z | E. u e. R E. v e. S z = ( u X. v ) } ) ) $=
      ( vr vs ctop cv cxp wceq wrex cab ctg cfv ctx rexeq abbidv fveq2d rexbidv
      df-tx fvex ovmpt2 ) FGDEHHAICIBIJKZBGIZLZCFIZLZAMZNOUDBELZCDLZAMZNOPUFCDL
      ZAMZNOUGDKZUIUNNUOUHUMAUFCUGDQRSUEEKZUNULNUPUMUKAUPUFUJCDUDBUEEQTRSACBGFU
      AULNUBUC $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d R a b c d r s u v x y z $.  $d S a b c d r s u v x y z $.
    $( A lemma for theorems about product topologies.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    txbas $p |- ( ( R e. Top /\ S e. Top ) ->
                  { z | E. u e. R E. v e. S z = ( u X. v ) } e. TopBases ) $=
      ( vx vy va vb vd wcel wa cv cxp wceq wrex cvv vex wi eqeq2d vs vr vc ctop
      cab cin ctb rexeq abbidv eleq1d rexbidv abrexex abrexex2 vtocl2g 2rexbidv
      wral eqeq1 anbi12i xpeq2 cbvrexv rexbii xpeq1 bitri inopn ancom2s adantlr
      elab 3expb anassrs adantlrr adantrr adantll adantlrl adantrl a1i rcla42ev
      inxp syl3anc xpex inex1 sylibr ineq1 syl5ibrcom ineq2 biimprcd syl6 com24
      ex rexlimdvv syl5bi imp3a ralrimivv fiinbas syl2anc ) DUDKZEUDKZLZAMZCMZB
      MZNZOZBEPZCDPZAUEZQKZFMZGMZUFZXEKZGXEUPFXEUPXEUGKXBBUAMZPZCUBMZPZAUEZQKXL
      CDPZAUEZQKXFUBUADEUDUDXMDOZXOXQQXRXNXPAXLCXMDUHUIUJXKEOZXQXEQXSXPXDAXSXLX
      CCDXBBXKEUHUKUIUJXLCAXMUBRBAXKXAUARULUMUNWQXJFGXEXEXGXEKZXHXEKZLXGXAOZBEP
      ZCDPZXHXAOZBEPZCDPZLWQXJXTYDYAYGXDYDAXGFRWRXGOXBYBCBDEWRXGXAUQUOVGXDYGAXH
      GRWRXHOXBYECBDEWRXHXAUQUOVGURWQYDYGXJYDXGHMZIMZNZOZIEPZHDPZWQYGXJSZYDXGWS
      YINZOZIEPZCDPYMYCYQCDYBYPBIEWTYIOXAYOXGWTYIWSUSTUTVAYQYLCHDWSYHOZYPYKIEYR
      YOYJXGWSYHYIVBTUKUTVCWQYKYNHIDEWQYGYKYHDKZYIEKZLZXJYGXHUCMZJMZNZOZJEPZUCD
      PZWQYKUUAXJSSZYGXHWSUUCNZOZJEPZCDPUUGYFUUKCDYEUUJBJEWTUUCOXAUUIXHWTUUCWSU
      STUTVAUUKUUFCUCDWSUUBOZUUJUUEJEUULUUIUUDXHWSUUBUUCVBTUKUTVCWQUUEUUHUCJDEW
      QUUBDKZUUCEKZLZUUEUUHSWQUUOLZUUAYKUUEXJUUPUUAYKUUEXJSZSUUPUUALZYKXGUUDUFZ
      XEKZUUQUURUUTYKYJUUDUFZXEKZUURUVAXAOZBEPCDPZUVBUURYHUUBUFZDKZYIUUCUFZEKZU
      VAUVEUVGNZOZUVDUUPYSUVFYTWQUUMYSUVFUUNWQUUMYSUVFWOUUMYSLUVFWPWOYSUUMUVFWO
      YSUUMUVFYHUUBDVDVHVEVFVIVJVKUUPYTUVHYSWQUUNYTUVHUUMWQUUNYTUVHWPUUNYTLUVHW
      OWPYTUUNUVHWPYTUUNUVHYIUUCEVDVHVEVLVIVMVNUVJUURYHYIUUBUUCVQVOUVCUVJUVAUVE
      WTNZOCBUVEUVGDEWSUVEOXAUVKUVAWSUVEWTVBTWTUVGOUVKUVIUVAWTUVGUVEUSTVPVRXDUV
      DAUVAYJUUDYHYIHRIRVSVTWRUVAOXBUVCCBDEWRUVAXAUQUOVGWAYKUUSUVAXEXGYJUUDWBUJ
      WCUUEXJUUTUUEXIUUSXEXHUUDXGWDUJWEWFWHWGWHWIWJWGWIWJWKWJWLFGXEQWMWN $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d R u v z $.  $d S u v z $.  $d T u v z $.
    txtop.1 $e |- T = ( R tX S ) $.
    $( The product of two topologies is a topology.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    txtop $p |- ( ( R e. Top /\ S e. Top ) -> T e. Top ) $=
      ( vz vu vv ctop wcel wa cv cxp wceq wrex cab ctg cfv ctx co txval ctb syl
      syl5eq txbas tgcl eqeltrd ) AHIBHIJZCEKFKGKLMGBNFANEOZPQZHUGCABRSUIDEGFAB
      TUCUGUHUAIUIHIEGFABUDUHUEUBUF $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    txtopi.1 $e |- R e. Top $.
    txtopi.2 $e |- S e. Top $.
    $( The product of two topologies is a topology.  (Contributed by Jeff
       Madsen, 15-Jun-2011.) $)
    txtopi $p |- ( R tX S ) e. Top $=
      ( ctop wcel ctx co eqid txtop mp2an ) AEFBEFABGHZEFCDABLLIJK $.
      $( [15-Jun-2010] $)
  $}

  ${
    $d R u v x z $.  $d S u v x z $.  $d T u v x z $.  $d X u v x z $.
    $d Y u v x z $.
    txuni.1 $e |- T = ( R tX S ) $.
    txuni.2 $e |- X = U. R $.
    txuni.3 $e |- Y = U. S $.
    $( The underlying set of the product of two topologies.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    txuni $p |- ( ( R e. Top /\ S e. Top ) -> U. T = ( X X. Y ) ) $=
      ( vz vu vv vx ctop wcel wa cuni wceq wrex wss cvv cv cxp cab ctg co txval
      cfv ctx syl5eq unieqd ctb txbas unitg syl wral vex eqeq1 2rexbidv elab wi
      eltopss xpss12 syl2an an4s ex sseq1 biimprcd syl6 rexlimdvv syl5bi unissb
      ralrimiv sylibr topopn adantr adantl eqidd xpeq1 eqeq2d xpeq2 rcla42ev wb
      syl3anc uniexg syl5eqel xpexg elabg mpbird elssuni eqssd 3eqtrd ) AMNZBMN
      ZOZCPIUAZJUAZKUAZUBZQZKBRJARZIUCZUDUGZPZXAPZDEUBZWNCXBWNCABUHUEXBFIKJABUF
      UIUJWNXAUKNXCXDQIKJABULXAUMUNWNXDXEWNLUAZXESZLXAUOXDXESWNXGLXAXFXANXFWRQZ
      KBRJARZWNXGWTXIIXFLUPWOXFQWSXHJKABWOXFWRUQURUSWNXHXGJKABWNWPANZWQBNZOZWRX
      ESZXHXGUTWNXLXMWLXJWMXKXMWLXJOWPDSWQESXMWMXKOWPADGVAWQBEHVAWPDWQEVBVCVDVE
      XHXGXMXFWRXEVFVGVHVIVJVLLXAXEVKVMWNXEXANZXEXDSWNXNXEWRQZKBRJARZWNDANZEBNZ
      XEXEQZXPWLXQWMADGVNVOWMXRWLBEHVNVPWNXEVQXOXSXEDWQUBZQJKDEABWPDQWRXTXEWPDW
      QVRVSWQEQXTXEXEWQEDVTVSWAWCWNXETNZXNXPWBWLDTNETNYAWMWLDAPTGAMWDWEWMEBPTHB
      MWDWEDETTWFVCWTXPIXETWOXEQWSXOJKABWOXEWRUQURWGUNWHXEXAWIUNWJWK $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    txunii.1 $e |- R e. Top $.
    txunii.2 $e |- S e. Top $.
    txunii.3 $e |- X = U. R $.
    txunii.4 $e |- Y = U. S $.
    $( The underlying set of the product of two topologies.  (Contributed by
       Jeff Madsen, 15-Jun-2011.) $)
    txunii $p |- ( X X. Y ) = U. ( R tX S ) $=
      ( ctx co cuni cxp ctop wcel wceq eqid txuni mp2an eqcomi ) ABIJZKZCDLZAMN
      BMNUAUBOEFABTCDTPGHQRS $.
      $( [15-Jun-2010] $)
  $}

  ${
    $d R u v z $.  $d S u v z $.  $d T u v z $.  $d A u v z $.  $d B u v z $.
    txopn.1 $e |- T = ( R tX S ) $.
    $( The product of two open sets is open in the product topology.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    txopn $p |- ( ( ( R e. Top /\ S e. Top ) /\ ( A e. R /\ B e. S ) )
                                                      -> ( A X. B ) e. T ) $=
      ( vz vu vv ctop wcel wa cxp cv wceq wrex syl adantr eqeq2d cvv txbas eqid
      cab ctg cfv wss bastg xpeq1 xpeq2 rcla42ev mp3an3 wb xpexg eqeq1 2rexbidv
      ctb elabg mpbird adantl sseldd ctx co txval syl5eq eleqtrrd ) CJKDJKLZACK
      ZBDKZLZLZABMZGNZHNZINZMZOZIDPHCPZGUCZUDUEZEVJVRVSVKVFVRVSUFZVIVFVRUPKVTGI
      HCDUAVRUGQRVIVKVRKZVFVIWAVKVOOZIDPHCPZVGVHVKVKOZWCVKUBWBWDVKAVNMZOHIABCDV
      MAOVOWEVKVMAVNUHSVNBOWEVKVKVNBAUISUJUKVIVKTKWAWCULABCDUMVQWCGVKTVLVKOVPWB
      HICDVLVKVOUNUOUQQURUSUTVFEVSOVIVFECDVAVBVSFGIHCDVCVDRVE $.
      $( [2-Sep-2009] $)
  $}


