$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Continuity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Cn $.
  $c CnP $.

  $( Extend class notation with the set of continuous functions between
     topologies. $)
  ccn $a class Cn $.

  $( Extend class notation with the set of functions between topologies
     continuous at a point. $)
  ccnp $a class CnP $.

  ${
    $d f j k v w x y z $.
    $( Define a function on two topologies whose value is the set of continuous
       mappings from the first topology to the second.  Based on definition of
       continuous function in [Munkres] p. 102.  See ~ iscn for the predicate
       form. $)
    df-cn $a |- Cn = { <. <. j , k >. , z >. | ( ( j e. Top /\ k e. Top ) /\
             z = { f e. ( U. k ^m U. j ) | A. y e. k ( `' f " y ) e. j } ) } $.

    $( Define a function on two topologies whose value is the set of continuous
       mappings at a specified point in the first topology.  Based on Theorem
       7.2(g) of [Munkres] p. 107. $)
    df-cnp $a |- CnP = { <. <. j , k >. , z >. | ( ( j e. Top /\ k e. Top ) /\
     z = { <. x , y >. | ( x e. U. j /\ y = { f e. ( U. k ^m U. j ) | A. w e. k
      ( ( f ` x ) e. w -> E. v e. j ( x e. v /\ ( f " v ) (_ w ) ) } ) } ) } $.
  $}

  ${
    $d f j k v w x y z J $.  $d f j k w x y z K $.  $d f j k x y z X $.
    $d f k y z Y $.
    cnfval.1 $e |- X = U. J $.
    cnfval.2 $e |- Y = U. K $.
    $( The set of all continuous functions from topology ` J ` to topology
       ` K ` . $)
    cnfval $p |- ( ( J e. Top /\ K e. Top ) ->
      ( J Cn K ) = { f e. ( Y ^m X ) | A. y e. K ( `' f " y ) e. J } ) $=
      ( vj vk vz ctop cv ccnv cima wcel wral cuni cm co crab ccn oprex rabex
      wceq unieq syl6eqr opreq2d rabeq syl eleq2 ralbidv rabbisdv eqtrd
      opreq1d raleq1 df-cn oprabval2 ) IJKCDLLBMNAMOZIMZPZAJMZQZBVBRZUTRZSTZUAZ
      USCPZADQZBFESTZUAZUBVHAVBQZBVDESTZUAZVIBVJFESUCUDUTCUEZVGVCBVMUAZVNVOVFVM
      UEVGVPUEVOVEEVDSVOVECREUTCUFGUGUHVCBVFVMUIUJVOVCVLBVMVOVAVHAVBUTCUSUKULUM
      UNVBDUEZVNVLBVJUAZVKVQVMVJUEVNVRUEVQVDFESVQVDDRFVBDUFHUGUOVLBVMVJUIUJVQVL
      VIBVJVHAVBDUPUMUNAKBIJUQUR $.
      $( [17-Oct-2006] $) $( [17-Oct-2006] $)

    $( The function mapping the points in a topology ` J ` to the set of
       all functions from ` J ` to topology ` K ` continuous at that point. $)
    cnpfval $p |- ( ( J e. Top /\ K e. Top ) -> ( J CnP K ) =
         { <. x , y >. | ( x e. X /\ y = { f e. ( Y ^m X ) | A. w e. K
        ( ( f ` x ) e. w -> E. v e. J ( x e. v /\ ( f " v ) (_ w ) ) } ) } ) $=
      ( vj vk vz ctop wcel cv cfv cima wss wa wrex wi wral cm co crab wceq
      copab cvv ccnp cuni uniexg eleq1i biimpr opabex2g 3syl adantr unieq
      syl6eqr eleq2d opreq2d rabeq syl rexeq1 imbi2d ralbidv rabbisdv eqtrd
      eqeq2d anbi12d opabbidv opreq1d raleq1 anbi2d df-cnp oprabval2g mpd3an3
      ) FOPZGOPZAQZHPZBQZWAEQZRCQZPZWADQZPWDWGSWETUAZDFUBZUCZCGUDZEIHUEUFZUGZUH
      ZUAZABUIZUJPZFGUKUFWPUHVSWQVTVSFULZUJPZHUJPZWQFOUMWTWSHWRUJJUNUOABHWMUJUP
      UQURLMNFGOOWALQZULZPZWCWFWHDXAUBZUCZCMQZUDZEXFULZXBUEUFZUGZUHZUAZABUIWPUK
      WBWCWJCXFUDZEXHHUEUFZUGZUHZUAZABUIUJXAFUHZXLXQABXRXCWBXKXPXRXBHWAXRXBWRHX
      AFUSJUTZVAXRXJXOWCXRXJXGEXNUGZXOXRXIXNUHXJXTUHXRXBHXHUEXSVBXGEXIXNVCVDXRX
      GXMEXNXRXEWJCXFXRXDWIWFWHDXAFVEVFVGVHVIVJVKVLXFGUHZXQWOABYAXPWNWBYAXOWMWC
      YAXOXMEWLUGZWMYAXNWLUHXOYBUHYAXHIHUEYAXHGULIXFGUSKUTVMXMEXNWLVCVDYAXMWKEW
      LWJCXFGVNVHVIVJVOVLABNCDELMVPVQVR $.
      $( [17-Oct-2006] $) $( [17-Oct-2006] $)
  $}

  ${
    $d f x y F $.  $d f v w x y J $.  $d y w v f K $.  $d f v w x y P $.
    $d f v w y X $.  $d f v w y Y $.
    iscn.1 $e |- X = U. J $.
    iscn.2 $e |- Y = U. K $.
    $( The predicate " ` F ` is a continuous function from topology ` J ` to
       topology ` K ` ."  Definition of continuous function in [Munkres]
       p. 102. $)
    iscn $p |- ( ( J e. Top /\ K e. Top ) -> ( F e. ( J Cn K ) <->
               ( F : X --> Y /\ A. y e. K ( `' F " y ) e. J ) ) ) $=
      ( vf ctop wcel wa ccn co cv ccnv cima wral cm crab wf cnfval eleq2d wb
      cvv elmapg cuni uniexg syl5eqel syl2an ancoms anbi1d wceq cnveq imaeq1d
      eleq1d ralbidv elrab syl5bb bitrd ) CJKZDJKZLZBCDMNZKBIOZPZAOZQZCKZADRZIF
      ESNZTZKZEFBUAZBPZVGQZCKZADRZLZVCVDVLBAICDEFGHUBUCVCBVKKZVRLVSVMVCVTVNVRVB
      VAVTVNUDZFUEKEUEKWAVBVAFEBUEUEUFVBDUGUEFDJUHHUIVACUGUEECJUHGUIUJUKULVJVRI
      BVKVEBUMZVIVQADWBVHVPCWBVFVOVGVEBUNUOUPUQURUSUT $.
      $( [17-Oct-2006] $) $( [17-Oct-2006] $)

    $( The set of all functions from topology ` J ` to topology ` K ` that are
       continuous at a point ` P ` . $)
    cnpval $p |- ( ( J e. Top /\ K e. Top /\ P e. X ) -> ( ( J CnP K ) ` P ) =
         { f e. ( Y ^m X ) | A. y e. K
            ( ( f ` P ) e. y -> E. x e. J ( P e. x /\ ( f " x ) (_ y ) ) } ) $=
      ( vw vv ctop wcel w3a ccnp co cfv cv cima wss wa wrex wi wral cm crab
      wceq copab cnpfval fveq1d 3adant3 fveq2 eleq1d eleq1 anbi1d rexbidv
      imbi12d ralbidv rabbisdv eqid oprex rabex fvopab4 3ad2ant3 eqtrd ) EMNZFM
      NZCGNZOCEFPQZRZCKSZGNLSVLDSZRZBSZNZVLASZNZVMVQTVOUAZUBZAEUCZUDZBFUEZDHGUF
      QZUGZUHUBKLUIZRZCVMRZVONZCVQNZVSUBZAEUCZUDZBFUEZDWDUGZVGVHVKWGUHVIVGVHUBC
      VJWFKLBADEFGHIJUJUKULVIVGWGWOUHVHKLCWEWOGWFVLCUHZWCWNDWDWPWBWMBFWPVPWIWAW
      LWPVNWHVOVLCVMUMUNWPVTWKAEWPVRWJVSVLCVQUOUPUQURUSUTWFVAWNDWDHGUFVBVCVDVEV
      F $.
      $( [17-Oct-2006] $) $( [17-Oct-2006] $)

    $( The predicate " ` F ` is a continuous function from topology ` J ` to
       topology ` K ` at point ` P ` ."  Based on Theorem 7.2(g) of [Munkres]
       p. 107. $)
    iscnp $p |- ( ( J e. Top /\ K e. Top /\ P e. X ) ->
                   ( F e. ( ( J CnP K ) ` P ) <-> ( F : X --> Y /\ A. y e. K
          ( ( F ` P ) e. y -> E. x e. J ( P e. x /\ ( F " x ) (_ y ) ) ) ) ) $=
      ( vf ctop wcel w3a ccnp co cfv cv cima wss wa wrex wi wral cm crab wf
      cnpval eleq2d wb cvv elmapg cuni uniexg syl5eqel syl2an ancoms anbi1d
      wceq fveq1 eleq1d imaeq1 sseq1d anbi2d rexbidv imbi12d ralbidv elrab
      syl5bb 3adant3 bitrd ) ELMZFLMZCGMZNZDCEFOPQZMDCKRZQZBRZMZCARZMZVQWASZVST
      ZUAZAEUBZUCZBFUDZKHGUEPZUFZMZGHDUGZCDQZVSMZWBDWASZVSTZUAZAEUBZUCZBFUDZUAZ
      VOVPWJDABCKEFGHIJUHUIVLVMWKXAUJVNVLVMUAZDWIMZWTUAXAWKXBXCWLWTVMVLXCWLUJZH
      UKMGUKMXDVMVLHGDUKUKULVMFUMUKHFLUNJUOVLEUMUKGELUNIUOUPUQURWHWTKDWIVQDUSZW
      GWSBFXEVTWNWFWRXEVRWMVSCVQDUTVAXEWEWQAEXEWDWPWBXEWCWOVSVQDWAVBVCVDVEVFVGV
      HVIVJVK $.
      $( [17-Oct-2006] $) $( [17-Oct-2006] $)
  $}

  ${
    $d x y F $.  $d x y J $.  $d x y K $.  $d x y X $.  $d x y Y $.
    $d P x y $.
    iscnp2.1 $e |- X = U. J $.
    iscnp2.2 $e |- Y = U. K $.
    $( The predicate " ` F ` is a continuous function from topology ` J ` to
       topology ` K ` at point ` P ` ." $)
    iscnp2 $p |- ( ( J e. Top /\ K e. Top /\ P e. X ) ->
                   ( F e. ( ( J CnP K ) ` P ) <-> ( F : X --> Y /\ A. y e. K
       ( ( F ` P ) e. y -> E. x e. J ( P e. x /\ x (_ ( `' F " y ) ) ) ) ) ) $=
      ( ctop wcel w3a ccnp co cfv wf cv cima wss wa wrex wi wral ccnv iscnp wb
      wfun cdm funimass3 ffun ad2antlr eltopss adantlr wceq fdm sseqtr4d
      sylanc anbi2d rexbidva imbi2d ralbidv pm5.32da 3ad2ant1 bitrd ) EKLZFKLZC
      GLZMDCEFNOPLGHDQZCDPBRZLZCARZLZDVLSVJTZUAZAEUBZUCZBFUDZUAZVIVKVMVLDUEVJST
      ZUAZAEUBZUCZBFUDZUAZABCDEFGHIJUFVFVGVSWEUGVHVFVIVRWDVFVIUAZVQWCBFWFVPWBVK
      WFVOWAAEWFVLELZUAZVNVTVMDUHZVLDUIZTVNVTUGWHVLVJDUJVIWIVFWGGHDUKULWHVLGWJV
      FWGVLGTVIVLEGIUMUNVIWJGUOVFWGGHDUPULUQURUSUTVAVBVCVDVE $.
      $( [?] $) $( [15-May-2007] $)

    $( A continuous function is a mapping.  (Contributed by FL,
       15-Dec-2006.) $)
    cnf $p |- ( ( J e. Top /\ K e. Top /\ F e. ( J Cn K ) ) ->
                F : X --> Y ) $=
      ( vx ctop wcel ccn co wf wa ccnv cv cima wral iscn pm3.26bda 3impa ) BIJZ
      CIJZABCKLJZDEAMZUBUCNUDUEAOHPQBJHCRHABCDEFGSTUA $.
      $( [15-Dec-2006] $) $( [8-Dec-2006] $)

    $( A continuous function at point ` P ` is a mapping.  (Contributed by FL,
       31-Dec-2006.) $)
    cnpf $p |- ( ( ( J e. Top /\ K e. Top /\ P e. X ) /\
        F e. ( ( J CnP K ) ` P ) ) -> F : X --> Y ) $=
      ( vx vy ctop wcel w3a ccnp co cfv wf cv cima wss wa wrex wi wral iscnp
      pm3.26bda ) CKLDKLAELMBACDNOPLEFBQABPIRZLAJRZLBUHSUGTUAJCUBUCIDUDJIABCDEF
      GHUEUF $.
      $( [1-Jan-2007] $) $( [17-Nov-2006] $)

    $( The value of a continuous function from ` J ` to ` K ` at point ` P `
       belongs to the underlying set of topology ` K ` .  (Contributed by FL,
       31-Dec-2006.) $)
    cnpcl $p |- ( ( ( J e. Top /\ K e. Top /\ P e. X ) /\
        ( F e. ( ( J CnP K ) ` P ) /\ A e. X ) ) -> ( F ` A ) e. Y ) $=
      ( wf wcel cfv ctop w3a ccnp co wa ffvelrn cnpf adantrr simprr sylanc ) FG
      CJZAFKZACLGKDMKEMKBFKNZCBDEOPLKZUDQQFGACRUEUFUCUDBCDEFGHISTUEUFUDUAUB $.
      $( [1-Jan-2007] $) $( [27-Dec-2006] $)
  $}

  ${
    $d x y A $.  $d F y x $.  $d J x y $.  $d K y $.  $d P x y $.  $d X y $.
    cnpimaex.1 $e |- X = U. J $.
    $( Property of a function continuous at a point.  (Contributed by FL,
       31-Dec-2006.) $)
    cnpimaex $p |- ( ( ( J e. Top /\ K e. Top /\ P e. X ) /\ (
                   F e. ( ( J CnP K ) ` P ) /\ A e. K /\ ( F ` P ) e. A ) ) ->
                    E. x e. J ( P e. x /\ ( F " x ) (_ A ) ) $=
      ( vy ctop wcel w3a ccnp co cfv cv cima wss wa wrex wi wral cuni wf eqid
      iscnp pm3.27bda ex wceq eleq2 sseq2 anbi2d rexbidv imbi12d rcla4cv syl6
      3imp2 ) EJKFJKCGKLZDCEFMNOKZBFKZCDOZBKZCAPZKZDVCQZBRZSZAETZURUSVAIPZKZVDV
      EVIRZSZAETZUAZIFUBZUTVBVHUAZUAURUSVOURUSGFUCZDUDVOAICDEFGVQHVQUEUFUGUHVNV
      PIBFVIBUIZVJVBVMVHVIBVAUJVRVLVGAEVRVKVFVDVIBVEUKULUMUNUOUPUQ $.
      $( [1-Jan-2007] $) $( [22-Dec-2006] $)

    $( A restricted identity function is a continuous function.  (Contributed
       by FL, 31-Dec-2006.) $)
    idcn $p |- ( J e. Top -> ( I |` X ) e. ( J Cn J ) ) $=
      ( vy ctop wcel cid cres ccn co wf ccnv cv cima wral wa wfn crn wss
      rnresi eqimssi a1i fnresi jctil df-f sylibr wfun wceq funi wb cnvi
      eqcomi funeq ax-mp sylib funcnvres imai reseq2 syl eqtrd reseq1 syl6eq
      imaeq1d eltopss resiima pm3.27 eqeltrd r19.21aiva jca iscn anidms mpbird
      ) AEFZGBHZAAIJFZBBVNKZVNLZDMZNZAFZDAOZPZVMVPWAVMVNBQZVNRZBSZPVPVMWEWCWEVM
      WDBBTUAUBBUCUDBBVNUEUFVMVTDAVMVRAFZPZVSVRAWGVSVNVRNZVRWGVQVNVRWGVQGLZBHZV
      NWGWIUGZVQWJUHWGGUGZWKWLWGUIUBGWIUHWLWKUJWIGUKULGWIUMUNUOWKVQWIGBNZHZWJBG
      UPWKWMBUHZWNWJUHWOWKBUQUBWMBWIURUSUTUSWIGUHWJVNUHUKWIGBVAUNVBVCWGVRBSWHVR
      UHVRABCVDBVRVEUSUTVMWFVFVGVHVIVMVOWBUJDVNAABBCCVJVKVL $.
      $( [1-Jan-2007] $) $( [27-Dec-2006] $)
  $}

  ${
    $d x A $.  $d x F $.  $d x J $.  $d x K $.
    $( An open subset of the codomain of a continuous function has an open
       pre-image.  (Contributed by FL, 15-Dec-2006.) $)
    cnima $p |- ( ( ( J e. Top /\ K e. Top /\ F e. ( J Cn K ) ) /\ A e. K ) ->
                 ( `' F " A ) e. J ) $=
      ( vx ctop wcel ccn co w3a ccnv cima cv wral wi wa cuni wf eqid iscn
      pm3.27bda 3impa wceq imaeq2 eleq1d rcla4cv syl imp ) CFGZDFGZBCDHIGZJZADG
      ZBKZALZCGZULUNEMZLZCGZEDNZUMUPOUIUJUKUTUIUJPUKCQZDQZBRUTEBCDVAVBVASVBSTUA
      UBUSUPEADUQAUCURUOCUQAUNUDUEUFUGUH $.
      $( [15-Dec-2006] $) $( [8-Dec-2006] $)
  $}

  ${
    $d x F $.  $d x G $.  $d x J $.  $d x K $.  $d x L $.
    $( The composition of two continuous functions is a continuous
       function.  (Contributed by FL, 15-Dec-2006.) $)
    cnco $p |- ( ( ( J e. Top /\ K e. Top /\ L e. Top ) /\ ( F e. ( J Cn K )
                  /\ G e.  ( K Cn L ) ) ) -> ( G o. F ) e. ( J Cn L ) ) $=
      ( vx ctop wcel w3a ccn co wa ccom cuni wf ccnv cv cima wral fco eqid cnf
      3expa 3adantl1 3adantl3 syl2an ancoms anandis cnima 3simp1 ad2antrr
      3simp2 simprl adantr 3jca 3simp3 simprr sylan sylanc wceq cnvco imaeq1
      ax-mp imaco eqtr syl5eqel r19.21aiva jca wb iscn 3adant2 mpbird ) CGHZDGH
      ZEGHZIZACDJKHZBDEJKHZLZLZBAMZCEJKHZCNZENZWAOZWAPZFQZRZCHZFESZLZVTWEWJVPVQ
      VRWEVPVRLZVPVQLZWEDNZWDBOZWCWNAOZWEWLWMWCWNWDBATVNVOVRWOVMVNVOVRWOBDEWNWD
      WNUAZWDUAZUBUCUDVMVNVQWPVOVMVNVQWPACDWCWNWCUAZWQUBUCUEUFUGUHVTWIFEVTWGEHZ
      LZAPZBPZWGRZRZCWHVMVNVQIXDDHZXECHXAXDACDUIXAVMVNVQVPVMVSWTVMVNVOUJUKVPVNV
      SWTVMVNVOULZUKVTVQWTVPVQVRUMUNUOVNVOVRIWTXFVTWGBDEUIVTVNVOVRVPVNVSXGUNVPV
      OVSVMVNVOUPUNVPVQVRUQUOURUSWHXBXCMZWGRZXEWFXHUTWHXIUTBAVAWFXHWGVBVCXBXCWG
      VDVEVFVGVHVPWBWKVIZVSVMVOXJVNFWACEWCWDWSWRVJVKUNVL $.
      $( [15-Dec-2006] $) $( [8-Dec-2006] $)
  $}

  ${
    $d x y z F $.  $d x y z G $.  $d x y z J $.  $d x y z K $.  $d x y z L $.
    $d x y z P $.  $d x y z X $.
    cnpco.1 $e |- X = U. J $.
    $( MAY BE REVISED - this proof needs to be shortened significantly $)
    $( The composition of two continuous functions at point ` P ` is a
       continuous function at point ` P ` .  Bourbaki TG I.9.  (Contributed by
       FL, 31-Dec-2006.)  _Warning:  The HTML proof page is 0.5MB in
       size_. $)
    cnpco $p |- ( ( ( J e. Top /\ L e. Top /\ P e. X ) /\ ( K e. Top
        /\ F e. ( ( J CnP K ) ` P ) /\ G e.  ( ( K CnP L ) ` ( F ` P ) ) ) )
        -> ( G o. F ) e. ( ( J CnP L ) ` P ) ) $=
      ( vz vx vy ctop wcel w3a ccnp co cfv wa ccom cuni wf cv cima wss wrex wi
      wral fco eqid cnpf 3simp1 adantl 3simp2 adantr ffvelrn eleq2i pm3.2 3exp
      com34 com13 a1i 3imp sylbi com12 a1d 3imp1 syl 3simp3 syl6eleq sylanc
      3jca com14 cnpimaex ad2antrr ad2antlr simplr pm3.27 ex anim1d r19.41v
      syl6ibr sstr imass2 sylan anim2i anassrs r19.22si syl6 r19.22dva mpd c0
      wne wb 0opnt ne0i 3ad2ant1 r19.9rzv mpbird exp31 r19.21aiv wfun cdm wceq
      jca ffun 3syl fdm eqcomd eleqtrd fvco eleq1 biimpd imaco eqcomi sseq1i
      biimp anim2d r19.22sdv imim12d r19.20sdv iscnp ) DLMZFLMZAGMZNZELMZBADEOP
      QMZCABQZEFOPQMZNZRZCBSZADFOPQMZGFTZYLUAZAYLQZIUBZMZAJUBZMZYLYSUCZYQUDZRZJ
      DUEZUFZIFUGZRZYKYOUUFETZYNCUAZGUUHBUAZYOYKGUUHYNCBUHYFYCYHUUHMZNZYIUUIYKY
      HCEFUUHYNUUHUIZYNUIZUJZYKYFYCUUKYJYFYEYFYGYIUKULZYEYCYJYBYCYDUMUNZDTZUUHB
      UAZAUURMZUUKYKUURUUHABUOZYKYBYFUUTNZYGRZUUSYBYCYDYJUVCYBYDYJUVCUFZUFYCYDY
      BUVDYDUUTYBUVDUFGUURAHUPYJYBUUTUVCYFYGYIYBUUTUVCUFZUFZYIYGYFUVFYGYFUVFUFU
      FYIYBYFYGUVEYBYFUUTYGUVCYBYFUUTYGUVCUFUVBYGUQURUSUTVAUTVBUTVCVDVEVFABDEUU
      RUUHUURUIUUMUJZVGYEUUTYJYEAGUURYBYCYDVHZHVIUNVJVKZYJYIYEYFYGYIVHULZVJYKYB
      YFYDNZYGRZUUJYBYCYDYJUVLYBYDYJUVLUFUFYCYJYDYBUVLYFYGYIYDYBUVLUFUFZYIYGYFU
      VMYGYFUVMUFUFYIYBYFYDYGUVLYBYFYDYGUVLUFUVKYGUQURVLVAUTVBUTVEVFZABDEGUUHHU
      UMUJZVGVJYKYHCQZYQMZYTCBYSUCZUCZYQUDZRZJDUEZUFZIFUGUUFYKUWCIFYKYQFMZUVQUW
      BYKUWDRZUVQRZUWBUWBKEUEZUWFYHKUBZMZCUWHUCZYQUDZRZKEUEZUWGUULYIUWDUVQNUWMU
      WFKYQYHCEFUUHUUMVMUWFYFYCUUKYKYFUWDUVQUUPVNZYKYCUWDUVQUUQVNUUSUUTUUKUWFUV
      AUVBYGUUSUWFUVGUWFYBYFUUTYKYBUWDUVQYEYBYJYBYCYDUKUNVNZUWNUWFAGUURYKYDUWDU
      VQYEYDYJUVHUNZVNZHVIZVKUWEYGUVQYJYGYEUWDYFYGYIUMVOUNZVJUWRVJVKUWFYIUWDUVQ
      YKYIUWDUVQUVJVNYKUWDUVQVPUWEUVQVQVKVJUWFUWLUWBKEUWFUWHEMZRZUWLYTUVRUWHUDZ
      RZUWKRZJDUEZUWBUXAUWLUXCJDUEZUWKRUXEUXAUWIUXFUWKUXAUWIUXFUVKYGUWTUWINUXFU
      XAUWIRZJUWHABDEGHVMUXGYBYFYDUWFYBUWTUWIUWOVNUWFYFUWTUWIUWNVNUWFYDUWTUWIUW
      QVNVKUXGYGUWTUWIUWFYGUWTUWIUWSVNUWFUWTUWIVPUXAUWIVQVKVJVRVSUXCUWKJDVTWAUX
      DUWAJDYTUXBUWKUWAUXBUWKRUVTYTUVSUWJUDUWKUVTUXBUVSUWJYQWBUVRUWHCWCWDWEWFWG
      WHWIWJUWFEWKWLZUWBUWGWMYKUXHUWDUVQYJUXHYEYFYGUXHYIYFWKEMUXHEWNEWKWOVGWPUL
      VNUWBKEWQVGWRWSWTYKUWCUUEIFYKYRUVQUWBUUDYKYRUVQYKCXAZBXAZABXBZMZNYPUVPXCY
      RUVQWMYKUXIUXJUXLYKUULYIRUUIUXIYKUULYIUVIUVJXDUUOUUHYNCXEXFYKUVLUUJUXJUVN
      UVOGUUHBXEXFYKAGUXKUWPYKUVLUUJGUXKXCUVNUVOUUJUXKGGUUHBXGXHXFXIVKACBXJYPUV
      PYQXKXFXLYKUWAUUCJDYKUVTUUBYTUVTUUBUFYKUVTUUBUVSUUAYQUUAUVSCBYSXMXNXOXPVA
      XQXRXSXTWJXDYEYMUUGWMYJJIAYLDFGYNHUUNYAUNWR $.
      $( [1-Jan-2007] $) $( [16-Nov-2006] $)
  $}

  ${
    $d F x y $.  $d J x y $.  $d K x y $.  $d X x y $.  $d Y x y $.
    iscncl.1 $e |- X = U. J $.
    iscncl.2 $e |- Y = U. K $.
    $( A definition of a continuous function using closed sets.  Bourbaki
       TG I.9 Th. 1 (d).  (Contributed by FL, 30-Jan-2007.) $)
    iscncl $p |- ( ( J e. Top /\ K e. Top ) -> ( F e. ( J Cn K ) <->
    ( F : X --> Y /\ A. y e. ( Clsd ` K ) ( `' F " y ) e. ( Clsd ` J ) ) ) ) $=
      ( vx ctop wcel wa ccn co wf ccnv cv cima wral ccld cfv iscn cdif wi wceq
      fimacnv eqcomd difeq1d wfun ffun funcnvcnv imadif 3syl eqtr4d a1i com13
      imp41 imaeq2 eleq1d rcla4cva simplr cldopn adantlr sylanc adantllr
      eqeltrd exp41 adantl wb wss iscld2 cdm cnvimass fdm sseqtrd sylan2
      ad2antrr mpbird r19.21aiva opncld isopn2 impbida pm5.32da bitrd ) CJKZDJK
      ZLZBCDMNKEFBOZBPZIQZRZCKZIDSZLWHWIAQZRZCTUAZKZADTUAZSZLIBCDEFGHUBWGWHWMWS
      WGWHLZWMWSWTWMLZWQAWRXAWNWRKZLWQEWOUCZCKZWGWHWMXBXDWFWHWMXBXDUDUDUDWEWFWH
      WMXBXDWFWHLZWMLXBLXCWIFWNUCZRZCWFWHWMXBXCXGUEZWHWMXBXHUDUDUDWFXBWMWHXHWMW
      HXHUDZUDXBXIWMWHXCWIFRZWOUCZXGWHEXJWOWHXJEEFBUFUGZUHWHBUIZWIPUIZXGXKUEEFB
      UJZBUKZFWNWIULUMUNUOUOUPUOUQWFWMXBXGCKZWHWMXFDKZXQWFWMLXBLWLXQIXFDWJXFUEW
      KXGCWJXFWIURUSUTWFWMXBVAWFXBXRWMWNDFHVBVCVDVEVFVGVHUQWTWQXDVIZWMXBWEWHXSW
      FWEWOEVJXSWHWOCEGVKWHWOBVLZEWOXTVJWHBWNVMUOEFBVNZVOVPVCVQVRVSWTWSLZWLIDYB
      WJDKZLWLEWKUCZWPKZWGWHWSYCYEWFWHWSYCYEUDUDUDWEWFWHWSYCYEXEWSLYCLYDWIFWJUC
      ZRZWPWFWHWSYCYDYGUEZWHWSYCYHUDUDUDWFYCWSWHYHWSWHYHUDZUDYCYIWSWHYDXJWKUCZY
      GWHEXJWKXLUHWHXMXNYGYJUEXOXPFWJWIULUMUNUOUOUPUOUQWFWSYCYGWPKZWHWSYFWRKZYK
      WFWSLYCLWQYKAYFWRWNYFUEWOYGWPWNYFWIURUSUTWFWSYCVAWFYCYLWSWJDFHVTVCVDVEVFV
      GVHUQWTWLYEVIZWSYCWEWHYMWFWEWKEVJYMWHWKCEGWAWHWKXTEWKXTVJWHBWJVMUOYAVOVPV
      CVQVRVSWBWCWD $.
      $( [30-Jan-2007] $) $( [19-Nov-2006] $)
  $}

  ${
    $d x A $.  $d x F $.  $d x J $.  $d x K $.
    $( A closed subset of the codomain of a continuous function has a closed
       pre-image. $)
    cnclima $p |- ( ( ( J e. Top /\ K e. Top /\ F e. ( J Cn K ) ) /\
                A e. ( Clsd ` K ) ) -> ( `' F " A ) e. ( Clsd ` J ) ) $=
      ( vx ctop wcel ccn co w3a ccld cfv ccnv cima cv wral wi wa cuni wf eqid
      iscncl pm3.27bda 3impa wceq imaeq2 eleq1d rcla4cv syl imp ) CFGZDFGZBCDHI
      GZJZADKLZGZBMZANZCKLZGZUNUQEOZNZUSGZEUOPZUPUTQUKULUMVDUKULRUMCSZDSZBTVDEB
      CDVEVFVEUAVFUAUBUCUDVCUTEAUOVAAUEVBURUSVAAUQUFUGUHUIUJ $.
      $( [14-Apr-2007] $) $( [15-Mar-2007] $)
  $}

  ${
    $d J f x y $.  $d K f y $.  $d X f y $.  $d Y y $.  $d P f x y $.
    cnsscnp.1 $e |- X = U. J $.
    cnsscnp.2 $e |- Y = U. K $.
    $( The set of continuous functions is a subset of the set of continuous
       functions at a point.  (Contributed by Raph Levien, 21-Oct-2006.) $)
    cnsscnp $p |- ( ( J e. Top /\ K e. Top /\ P e. X ) ->
                  ( J Cn K ) (_ ( ( J CnP K ) ` P ) ) $=
      ( vf vy vx ctop wcel w3a ccn co ccnp cfv cv wi wa wf ccnv cima wral wss
      wrex cdm wfun wceq fvimacnv eleq2 biimprcd syl6bi ex com4r com3r imp32
      crn cin inss1 imaeq2 funimacnv sylan9eq sseq1d mpbiri adantl jctird
      exp32 com24 imp31 r19.22sdv risset syl5ib exp31 com4t imp r19.20sdv fdm
      eleq2d biimpar ffun adantr sylanc expcom imdistand wb iscn iscnp 3expa
      3imtr4d 3impa ssrdv ) BKLZCKLZADLZMHBCNOZABCPOQZWMWNWOHRZWPLZWRWQLZSWMWNT
      ZWOTDEWRUAZWRUBIRZUCZBLZICUDZTZXBAWRQXCLZAJRZLZWRXIUCZXCUEZTZJBUFZSZICUDZ
      TZWSWTWOXGXQSXAWOXBXFXPXBWOXFXPSZAWRUGZLZWRUHZXRXBWOTXTYATXEXOICXTYAXEXOS
      YAXEXTXOXTXHYAXEXNXTXHYAXEXNSXTXHTYATZXIXDUIZJBUFXNXEYBYCXMJBXTXHYAYCXMSX
      TYCYAXHXMXTYCYAXHXMSXTYCYATZTXHXJXLXTYCYAXHXJSZYCYAXTYEYAXTXHYCXJYAXTXHYC
      XJSZSYAXTTXHAXDLZYFAXCWRUJYCXJYGXIXDAUKULUMUNUOUPUQYDXLXTYDXLXCWRURZUSZXC
      UEXCYHUTYDXKYIXCYCYAXKWRXDUCYIXIXDWRVAXCWRVBVCVDVEVFVGVHVIVJVKJXDBVLVMVNV
      OUPVPVQXBXTWOXBXSDADEWRVRVSVTXBYAWODEWRWAWBWCWDWEVFXAWSXGWFWOIWRBCDEFGWGW
      BWMWNWOWTXQWFJIAWRBCDEFGWHWIWJWKWL $.
      $( [21-Oct-2006] $) $( [18-Oct-2006] $)

    $( A continuous function is continuous at all points.  One direction of
       Theorem 7.2(g) of [Munkres] p. 107.  (Contributed by Raph Levien,
       20-Nov-2006.) $)
    cncnpi $p |- ( ( ( J e. Top /\ K e. Top ) /\
             ( F e. ( J Cn K ) /\ A e. X ) ) -> F e. ( ( J CnP K ) ` A ) ) $=
      ( ctop wcel wa ccn co ccnp cfv wi w3a cnsscnp sseld 3expia com23 imp32 )
      CIJZDIJZKZBCDLMZJZAEJZBACDNMOZJZUEUHUGUJUCUDUHUGUJPUCUDUHQUFUIBACDEFGHRST
      UAUB $.
      $( [21-Nov-2006] $) $( [20-Nov-2006] $)
  $}

  ${
    $d x B $.
    $( MAY BE REVISED - use iscnp2 instead of iscnp to shorten cncnplem1 thru
       cncnplem4 ?  Or prove cncnp directly? $)
    $( Lemma for ~ cncnp2 . $)
    cncnplem1 $p |- U. { x e. A | ( ph /\ x (_ B ) } (_ B $=
      ( cv wss wa crab cuni cpw wcel cab simprr ss2abi df-rab df-pw 3sstr4
      uniss ax-mp unipw sseqtr ) ABEZDFZGZBCHZIZDJZIZDUEUGFUFUHFUBCKZUDGZBLUCBL
      UEUGUJUCBUIAUCMNUDBCOBDPQUEUGRSDTUA $.
      $( [21-Nov-2006] $) $( [20-Nov-2006] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d X x $.
    $( Lemma for ~ cncnp2 . $)
    cncnplem2 $p |- ( A. x ( x e. A -> x e. B ) -> A (_ U_ x e. A B ) $=
      ( vy cv wcel wi wal ciun wrex ax-17 hba1 hbre1 hbim visset wceq wa ra4e
      eleq1 biimpar adantr ax-4 com12 syl6bir biimpd syl6d imp31 sylanc exp31
      vtoclef eliun syl6ibr ssrdv ) AEZBFZUNCFZGZAHZDBABCIZDEZBFZURUTUSFZVAURUT
      CFZABJZVBVAURVDGZGAUTVAVEAVAAKURVDAUQALVCABMNNDOUNUTPZVAURVDUOVCVDVFVAQZU
      RQVCABRVGUOURVFUOVAUNUTBSZTUAVFVAURVCVFVAURUPVCVFVAUOURUPGVHURUOUPUQAUBUC
      UDVFUPVCUNUTCSUEUFUGUHUIUJAUTBCUKULUCUM $.
      $( [21-Nov-2006] $) $( [20-Nov-2006] $)

    $( Lemma for ~ cncnp2 . $)
    cncnplem3 $p |- ( A (_ X -> ( A. x e. X ( x e. A -> x e. B ) ->
                    A (_ U_ x e. A B ) ) $=
      ( wss cv wcel wi wral wal ciun wa ssel pm4.71rd imbi1d impexp syl6bb
      albidv df-ral syl6bbr cncnplem2 syl6bir ) BDEZAFZBGZUDCGZHZADIZUGAJZBABCK
      EUCUIUDDGZUGHZAJUHUCUGUKAUCUGUJUELZUFHUKUCUEULUFUCUEUJBDUDMNOUJUEUFPQRUGA
      DSTABCUAUB $.
      $( [21-Nov-2006] $) $( [20-Nov-2006] $)
  $}

  ${
    $d J x z $.  $d F x z $.  $d X x z $.  $d Y x z $.  $d x y z $.
    cncnplem4.1 $e |- X = U. J $.
    $( Lemma for ~ cncnp2 . $)
    cncnplem4 $p |- ( J e. Top -> ( ( F : X --> Y /\ A. x e. X ( ( F ` x ) e. y
      -> E. z e. J ( x e. z /\ ( F " z ) (_ y ) ) ) -> ( `' F " y ) e. J ) ) $=
      ( wf cv cfv wcel cima wss wa wrex wi wral ccnv crab cuni ciun ctop wceq
      wb cdm fdm syl6eq sseq2d elssuni syl5bir wfun funimass3 ffun sylan ex
      syld imp anbi2d rabbidv unieqd adantr iuneq2dv iunss cncnplem1 a1i
      mprgbir cnvimass sseli fvimacnv biimprd pm4.24 anbi1i anass bitr syl6bb
      rexbidva elunirab syl6bbr biimpd imim12d com14 mpd com13 r19.20sdv mpbii
      cncnplem3 syl eqssd eqtrd eleq1d ssrab2 uniopnt mpan2 a1d r19.21aiv
      iunopnt mpdan syl5cbi ) FGDIZAJZDKBJZLZXACJZLZDXDMXBNZOZCEPZQZAFRZOZADSXB
      MZXGCETZUAZUBZELZXLELEUCLZXKXOXLEXKXOAXLXEXDXLNZOZCETZUAZUBZXLWTXOYBUDXJW
      TAXLXNYAWTXNYAUDXAXLLZWTXMXTWTXGXSCEWTXDELZOXFXRXEWTYDXFXRUEZWTYDXDDUFZNZ
      YEWTYGXDEUAZNYDWTYFYHXDWTYFFYHFGDUGZHUHUIXDEUJUKZWTYGYEDULZYGYEWTXDXBDUMF
      GDUNZUOZUPUQURUSUTVAVBVCVBXKYBXLYBXLNZXKYNYAXLNZAXLAXLYAXLVDYOYCXECEXLVEV
      FVGVFWTXJXLYBNZWTXJYCXAYALZQZAFRZYPWTXIYRAFYCXIWTYQYCXAYFLZXIWTYQQQXLYFXA
      DXBVHZVIWTYTXIYCYQWTYTXIYRQWTYTOZYCXCXHYQUUBXCYCYKYTXCYCUEWTXAXBDVJYLUOVK
      WTXHYQQYTWTXHYQWTXHXEXSOZCEPYQWTXGUUCCEWTYDXGUUCUEZWTYDYGUUDYJWTYGUUDWTYG
      OZXGXSUUCUUEXFXRXEYMUSXSXEXEOZXROUUCXEUUFXRXEVLVMXEXEXRVNVOVPUPUQURVQXSCX
      AEVRVSVTVBWAUPWBWCWDWEWTXLFNZYSYPQWTXLYFNUUGUUAWTYFFXLYIUIWFAXLYAFWGWHUQU
      RWIWJWKXQXNELZAXLRXPXQUUHAXLXQUUHYCXQXMENUUHXGCEWLXMEWMWNWOWPAXLXNEWQWRWS
      $.
      $( [21-Nov-2006] $) $( [20-Nov-2006] $)
  $}

  ${
    $d J x y z $.  $d K x y $.  $d F x y z $.  $d X x y z $.  $d Y x y z $.
    cncnp.1 $e |- X = U. J $.
    cncnp.2 $e |- Y = U. K $.
    $( A continuous function is continuous at all points.  Theorem 7.2(g) of
       [Munkres] p. 107. $)
    cncnp $p |- ( ( J e. Top /\ K e. Top /\ F : X --> Y ) ->
                ( F e. ( J Cn K ) <-> A. x e. X F e. ( ( J CnP K ) ` x ) ) ) $=
      ( vy vz ctop wcel wf w3a ccn co cv ccnp cfv wral wi wa cnsscnp sseld
      3expia com23 r19.21adv 3adant3 cima wss wrex ccnv pm3.27 r19.20si anim2i
      ex cncnplem4 exp3a imp r19.20sdv imdistand ralcom anbi2i syl5ib sylan9r
      3adant2 wb iscnp 3expa ralbidva iscn imbi12d mpbird impbid ) CKLZDKLZEFBM
      ZNZBCDOPZLZBAQZCDRPSZLZAETZVOVPVTWDUAVQVOVPUBZVTWCAEWEWAELZVTWCVOVPWFVTWC
      UAVOVPWFNVSWBBWACDEFGHUCUDUEUFUGUHVRWDVTUAZVQWABSIQZLWAJQZLBWIUIWHUJUBJCU
      KUAZIDTZUBZAETZVQBULWHUICLZIDTZUBZUAZVOVQWQVPVQWMVQWKAETZUBZVOWPVQWMWSWMW
      RVQWLWKAEVQWKUMUNUOUPVOVQWJAETZIDTZUBWPWSVOVQXAWOVOVQXAWOUAVOVQUBWTWNIDVO
      VQWTWNUAVOVQWTWNAIJBCEFGUQURUSUTUPVAWRXAVQWJAIEDVBVCVDVEVFVOVPWGWQVGVQWEW
      DWMVTWPWEWCWLAEVOVPWFWCWLVGJIWABCDEFGHVHVIVJIBCDEFGHVKVLUHVMVN $.
      $( [15-May-2007] $) $( [15-May-2007] $)

    $( A continuous function is continuous at all points.  Theorem 7.2(g) of
       [Munkres] p. 107.  (Contributed by Raph Levien, 20-Nov-2006.) $)
    cncnp2 $p |- ( ( J e. Top /\ K e. Top /\ X =/= (/) ) ->
                ( F e. ( J Cn K ) <-> A. x e. X F e. ( ( J CnP K ) ` x ) ) ) $=
      ( ctop wcel c0 wne w3a ccn co wf cv ccnp cfv wral wa wb cnf 3expia
      pm4.71rd cncnp 3expa pm5.32da bitrd 3adant3 wi cnpf ex ralbidva r19.28zv
      sylan9bb 3impa bitr4d ) CIJZDIJZEKLZMBCDNOJZEFBPZBAQZCDROSJZAETZUAZVFUSUT
      VBVGUBVAUSUTUAZVBVCVBUAVGVHVBVCUSUTVBVCBCDEFGHUCUDUEVHVCVBVFUSUTVCVBVFUBA
      BCDEFGHUFUGUHUIUJUSUTVAVFVGUBVHVFVCVEUAZAETVAVGVHVEVIAEVHVDEJZUAVEVCUSUTV
      JVEVCUKUSUTVJMVEVCVDBCDEFGHULUMUGUEUNVCVEAEUOUPUQUR $.
      $( [21-Nov-2006] $) $( [20-Nov-2006] $)
  $}

  ${
    $d J x $.  $d B x $.  $d Y x $.  $d F x $.  $d X x $.  $d K x $.
    cnconst.1 $e |- X = U. J $.
    cnconst.2 $e |- Y = U. K $.
    $( A constant function is continuous.  (Contributed by FL, 25-Jan-2007.) $)
    cnconst $p |- ( ( ( J e. Top /\ K e. Top ) /\
      ( B e. Y /\ F : X --> { B } ) ) -> F e. ( J Cn K ) ) $=
      ( vx ctop wcel wa csn wf ccn co ccnv cv cima wral wss fss simprr snssi
      ad2antrl sylanc wi w3a c0 wceq eleq1 0opnt syl5cbi eqcom syl5ib 0ima
      eqtrt mpan2 syl5 imaeq1 cnv0 cnveq 3ad2ant1 wn crn imassrn a1i cdm fdm
      dfdm4 syl5eqr sseqtrd eqcomd adantr imadmrn eqtr4 wfo wne foconst df-ne
      sylan2br forn syl df-rn imaeq2d 3eqtrd 3adant2 imass2 3ad2ant2 eqsstrd
      eqssd 3exp 3imp1 topopn eqeltrd ex pm2.61d a1d imp32 cin fimacnvdisj
      pm3.27 disjsn biimpr incom syl5eq syl2an adantll ad2antrr r19.21aiv jca
      adantlr wb iscn mpbird ) CJKZDJKZLZAFKZEAMZBNZLZLBCDOPKZEFBNZBQZIRZSZCKZI
      DTZLZXPYBYJXQXPYBLZYDYIYAXTFUAZYDYKEXTFBUBXPXSYAUCXSYLXPYAAFUDUEUFYKYHIDY
      KYHYFDKYKAYFKZYHXPXSYAYMYHUGZXPYAYNUGXSXPYAYMYHXPYAYMUHZBUIUJZYHXPYAYPYHU
      GYMXPYEUIQZUJZYHYPXPYEUIUJZYHYRXPYGUIYFSZUJZYHYSXPYGUIUJZYHUUAXPUIYGUJZYH
      UUBUUCUICKZYHXPUIYGCUKCULZUMYGUIUNUOUUAYTUIUJUUBYFUPYGYTUIUQURUSYEUIYFUTU
      SYRYQUIUJYSVAYEYQUIUQURUSBUIVBUSVCYOYPVDZYHYOUUFLYGECXPYAYMUUFYGEUJZYAYMU
      UFUUGUGUGUGXPYAYMUUFUUGYAYMUUFUHZYGEYAYMYGEUAUUFYAYGYEVEZEYGUUIUAYAYEYFVF
      VGYABVHZEUUIEXTBVIZBVJZVKVLVCUUHEYEXTSZYGYAUUFEUUMUJYMYAUUFLZEUUJYEYEVHZS
      ZUUMYAEUUJUJUUFYAUUJEUUKVMVNUUJUUPUJUUNUUJUUIUUPUULYEVOVPVGUUNUUOXTYEUUNB
      VEZXTUUOUUNEXTBVQZUUQXTUJYABUIVRUURUUFEABVSBUIVTWAEXTBWBWCBWDVKWEWFWGYMYA
      UUMYGUAZUUFYMXTYFUAUUSAYFUDXTYFYEWHWCWIWJWKWLVGWMYOECKZUUFXPYAUUTYMCEGWNV
      CVNWOWPWQWLWRWSYKYMVDZYHYKUVALYGUICYBUVAUUBXPYAXTYFWTZUIUJUUBYBUVAEXTYFBX
      AXSYAXBUVAYFXTWTZUIUVBUVCUIUJUVAYFAXCXDXTYFXEXFXGXHXPUUDYBUVAUUEXIWOWPWQW
      RXJXKXLXRYCYJXMYBIBCDEFGHXNVNXO $.
      $( [25-Jan-2007] $) $( [15-Jan-2007] $)
  $}

