$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Limit points
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c limPt $.

  $( Extend class notation with the limit point function for topologies. $)
  clp $a class limPt $.

  ${
    $d v w x y z $.
    $( Define a function on topologies whose value is the set of limit points
       of the subsets of the base set.  See ~ lpval . $)
    df-lp $a |- limPt = { <. z , w >. | ( z e. Top /\ w = { <. x , y >. |
                         ( x C_ U. z /\ y = { v |
                 v e. ( ( cls ` J ) ` ( x \ { v } ) ) } ) } ) } $.
  $}

  ${
    $d n v w x y z J $.  $d n x y z S $.  $d n w x y z X $.  $d n x y P $.
    lpfval.1 $e |- X = U. J $.
    $( The limit point function on the subsets of a topology's base set. $)
    lpfval $p |- ( J e. Top -> ( limPt ` J ) = { <. x , y >. | ( x C_ X /\
                     y = { v |
                 v e. ( ( cls ` J ) ` ( x \ { v } ) ) } ) } ) $=
      ( vz vw ctop wcel cv wss csn cdif ccl cfv cab wceq wa copab cvv clp cpw
      cuni uniexg syl5eqel pwexg opabex2g 3syl visset elpw anbi1i opabbii
      syl5eqelr unieq syl6eqr sseq2d fveq2 fveq1d eleq2d abbidv eqeq2d anbi12d
      opabbidv df-lp fvopab4g mpdan ) DIJZAKZELZBKZCKZVIVLMNZDOPZPZJZCQZRZSZABT
      ZUAJDUBPVTRVHVIEUCZJZVRSZABTZUAVTVHEUAJWAUAJWDUAJVHDUDZUAEDIUEFUFEUAUGABW
      AVQUAUHUIWCVSABWBVJVRVIEAUJUKULUMUNGHDVIGKZUDZLZVKVLVMWFOPZPZJZCQZRZSZABT
      VTIUAUBWFDRZWNVSABWOWHVJWMVRWOWGEVIWOWGWEEWFDUOFUPUQWOWLVQVKWOWKVPCWOWJVO
      VLWOVMWIVNWFDOURUSUTVAVBVCVDABGHCWFVEVFVG $.
      $( [10-Feb-2007] $)

    $( The set of limit points of a subset of the base set of a topology.
       Alternate definition of limit point in [Munkres] p. 97. $)
    lpval $p |- ( ( J e. Top /\ S C_ X ) -> ( ( limPt ` J ) ` S ) =
                 { x | x e. ( ( cls ` J ) ` ( S \ { x } ) ) } ) $=
      ( vy vz ctop wcel wss wa clp cfv cv cpw csn cdif ccl cab wceq copab
      lpfval adantr visset elpw anbi1i opabbii syl6eqr fveq1d cvv cuni uniexg
      syl5eqel elpw2g biimpar sylan difss clsss mp3an3 sseld ss2abdv abid2
      fvex eqeltri ssex syl difeq1 fveq2d eleq2d abbidv eqid fvopab4g syl11anc
      eqtrd ) CHIZBDJZKZBCLMZMBFNZDOZIZGNANZVSWBPZQZCRMZMZIZASZTZKZFGUAZMZWBBWC
      QZWEMZIZASZVQBVRWKVQVRVSDJZWIKZFGUAZWKVOVRWSTVPFGACDEUBUCWJWRFGWAWQWIVSDF
      UDUEUFUGUHUIVQBVTIZWPUJIZWLWPTVODUJIZVPWTVOCUKUJDCHULEUMXBWTVPBDUJUNUOUPV
      QWPWBBWEMZIZASZJXAVQWOXDAVQWNXCWBVOVPWMBJWNXCJBWCUQBWMCDEURUSUTVAWPXEXEXC
      UJAXCVBBWEVCVDVEVFFGBWHWPVTUJWKVSBTZWGWOAXFWFWNWBXFWDWMWEVSBWCVGVHVIVJWKV
      KVLVMVN $.
      $( [10-Feb-2007] $)

    $( The predicate " ` P ` is a limit point of ` S ` ." $)
    islp $p |- ( ( J e. Top /\ S C_ X ) -> ( P e. ( ( limPt ` J ) ` S ) <->
               P e. ( ( cls ` J ) ` ( S \ { P } ) ) ) ) $=
      ( vx ctop wcel wss wa clp cfv cv csn cdif ccl cab lpval eleq2d elisset
      wceq id sneq difeq2d fveq2d eleq12d elab3 syl6bb ) CGHBDIJZABCKLLZHAFMZBU
      KNZOZCPLZLZHZFQZHABANZOZUNLZHZUIUJUQAFBCDERSUPVAFAAUTTUKAUAZUKAUOUTVBUBVB
      UMUSUNVBULURBUKAUCUDUEUFUGUH $.
      $( [10-Feb-2007] $)

    $( The limits points of a subset are included in the subset's closure. $)
    lpsscls $p |- ( ( J e. Top /\ S C_ X ) -> ( ( limPt ` J ) ` S ) C_
               ( ( cls ` J ) ` S ) ) $=
      ( vx ctop wcel wss wa clp cfv cv csn cdif ccl cab lpval difss clsss
      mp3an3 sseld abssdv eqsstrd ) BFGZACHZIZABJKKELZAUGMZNZBOKZKZGZEPAUJKZEAB
      CDQUFULEUMUFUKUMUGUDUEUIAHUKUMHAUHRAUIBCDSTUAUBUC $.
      $( [26-Feb-2007] $)

    $( The limits points of a subset are included in the base set. $)
    lpss $p |- ( ( J e. Top /\ S C_ X ) -> ( ( limPt ` J ) ` S ) C_ X ) $=
      ( ctop wcel wss wa clp cfv ccl lpsscls clsss3 sstrd ) BEFACGHABIJJABKJJCA
      BCDLABCDMN $.
      $( [9-Nov-2007] $)

    $( The predicate " ` P ` is a limit point of ` S ` ," in terms of
       neighborhoods.  Definition of limit point in [Munkres] p. 97.  Although
       Munkres uses open neighborhoods, it also works for our more general
       neighborhoods. $)
    islp2 $p |- ( ( J e. Top /\ S C_ X /\ P e. X ) ->
                ( P e. ( ( limPt ` J ) ` S ) <->
         A. n e. ( ( nei ` J ) ` { P } ) ( n i^i ( S \ { P } ) ) =/= (/) ) ) $=
      ( vx vy ctop wcel wss w3a clp cfv csn cdif cv ccld crab cint wa wi wal
      cin c0 wne cnei wral wb ccl islp wceq ssdifss clsval sylan2 eleq2d bitrd
      3adant3 elintrabg iscld imbi1d albidv df-ral syl5bb sylan9bbr 3adant2
      cpw wrex isneip elpwi dfss4 sylib eqcomd eleq1d adantl wn reldisj syl
      incom eqeq1i adantr eldif baibr imbi12d df-ne imbi2i con2b bitri
      ralbidva 3adant1 eleq2 sseq1 anbi12d ssid biantru syl6bbr rcla4ev anim2i
      anassrs imim1i alimi ax-17 hba1 eleq1 ineq1 neeq1d a4v eltopss ancoms
      simpl jca anim1i adantrr an4s adantlrr ssrin ssn0 ex ad2antlr imim2d
      mpid exp31 com4t imp r19.23adv a1dd imp4a 19.21ad impbid2 impexp visset
      elpw imbi1i 3bitr4ri albii bitr2i syl6bb cvv cuni uniexg syl5eqel
      3ad2ant1 difss elpw2g mpbiri difeq2 eqeq2d syl2an sseq2 ralxfrd 3bitr4d
      bitr4i syl6rbb 3bitrd ) DIJZBEKZAEJZLZABDMNNJZABAOZPZGQZKZGDRNZSTZJZUVBEK
      ZEUVBPZDJZUAZUVCAUVBJZUBZUBZGUCZCQZUVAUDZUEUFZCUUTDUGNNZUHZUUOUUPUUSUVFUI
      UUQUUOUUPUAZUUSAUVADUJNNZJUVFABDEFUKUVTUWAUVEAUUPUUOUVAEKZUWAUVEULBEUUTUM
      ZGUVADEFUNUOUPUQURUUOUUQUVFUVNUIUUPUUQUVFUVLGUVDUHZUUOUVNUVCGAUVDEUSUUOUV
      BUVDJZUVLUBZGUCUVNUWDUUOUWFUVMGUUOUWEUVJUVLUVBDEFUTVAVBUVLGUVDVCVDVEVFUUR
      UVSUVIUVLUBZGEVGZUHZUVNUURUVSUVOEKZAHQZJZUWKUVOKZUAZHDVHZUAZUVQUBZCUCZUWI
      UUOUUQUVSUWRUIUUPUUOUUQUAZUVOUVRJZUVQUBZCUCUWRUVSUWSUXAUWQCUWSUWTUWPUVQAH
      DUVOEFVIVAVBUVQCUVRVCVDVFUURUVODJZAUVOJZUVQUBZUBZCUWHUHZEEUVOPZPZDJZUVAUX
      GKZAUXGJZUBZUBZCUWHUHZUWRUWIUUPUUQUXFUXNUIUUOUUPUUQUAZUXEUXMCUWHUXOUVOUWH
      JZUAUXBUXIUXDUXLUXPUXBUXIUIUXOUXPUVOUXHDUXPUXHUVOUXPUWJUXHUVOULUVOEVJUVOE
      VKVLVMVNVOUXOUXDUXLUIUXPUXOUVPUEULZUXCVPZUBZUXLUXDUXOUXQUXJUXRUXKUUPUXQUX
      JUIUUQUUPUVAUVOUDZUEULZUXJUXQUUPUWBUYAUXJUIUWCUVAUVOEVQVRUVPUXTUEUVOUVAVS
      VTVDWAUUQUXRUXKUIUUPUXKUUQUXRAEUVOWBWCVOWDUXDUXCUXQVPZUBUXSUVQUYBUXCUVPUE
      WEWFUXCUXQWGWHVDWAWDWIWJUURUWRUWJUXBUAZUXCUAZUVQUBZCUCZUXFUURUWRUYFUWQUYE
      CUYDUWPUVQUWJUXBUXCUWPUXBUXCUAUWOUWJUWNUXCHUVODUWKUVOULZUWNUXCUVOUVOKZUAU
      XCUYGUWLUXCUWMUYHUWKUVOAWKUWKUVOUVOWLWMUYHUXCUVOWNWOWPWQWRWSWTXAUUOUUQUYF
      UWRUBUUPUWSUYFUWQCUWSCXBUYECXCUWSUYFUWJUWOUVQUWSUYFUWOUVQUBZUWJUWSUYFUYIU
      WSUYFUAUWNUVQHDUYFUWSUWKEKZUWKDJZUAZUWLUAZUWKUVAUDZUEUFZUBZUYKUWNUVQUBUBZ
      UYEUYPCHUVOUWKULZUYDUYMUVQUYOUYRUYCUYLUXCUWLUYRUWJUYJUXBUYKUVOUWKEWLUVOUW
      KDXDWMUVOUWKAWKWMUYRUVPUYNUEUVOUWKUVAXEXFWDXGUWSUYPUYQUYKUWNUWSUYPUVQUYKU
      WNUWSUYPUVQUBUYKUWNUAUWSUAZUYPUYMUVQUYKUWLUWSUYMUWMUYKUUOUWLUUQUYMUYKUUOU
      AZUWLUYMUUQUYTUYLUWLUYTUYJUYKUUOUYKUYJUWKDEFXHXIUYKUUOXJXKXLXMXNXOUYSUYOU
      VQUYMUWNUYOUVQUBZUYKUWSUWMVUAUWLUWMUYNUVPKZVUAUWKUVOUVAXPVUBUYOUVQUYNUVPX
      QXRVRVOXSXTYAYBYCYDUOYEXRYFYGYHVFYIUXFUXPUXEUBZCUCUYFUXECUWHVCVUCUYECUYCU
      XDUBUWJUXEUBUYEVUCUWJUXBUXDYJUYCUXCUVQYJUXPUWJUXEUVOECYKYLYMYNYOYPYQUUREY
      RJZUWIUXNUIUUOUUPVUDUUQUUODYSYREDIYTFUUAUUBVUDUWGUXMGCUXGUWHVUDUXGUWHJZUX
      PVUDVUEUXGEKEUVOUUCUXGEYRUUDUUEWAVUDUVHUWHJZUVBEUVHPZULZUVBUXGULZCUWHVHUV
      BUWHJZVUDVUFUVHEKEUVBUUCUVHEYRUUDUUEVUJVUGUVBVUJUVGVUGUVBULUVBEVJUVBEVKVL
      VMVUIVUHCUVHUWHUVOUVHULUXGVUGUVBUVOUVHEUUFUUGWQUUHVUIUWGUXMUIVUDVUIUVIUXI
      UVLUXLVUIUVHUXHDUVBUXGEUUFVNVUIUVCUXJUVKUXKUVBUXGUVAUUIUVBUXGAWKWDWDVOUUJ
      VRUUKUQUWIVUJUWGUBZGUCUVNUWGGUWHVCVUKUVMGVUKUVGUWGUBUVMVUJUVGUWGUVBEGYKYL
      YMUVGUVIUVLYJUULYOWHUUMUUN $.
      $( [26-Feb-2007] $)

    $( The closure of a subset of a topological space is the subset together
       with its limit points.  Theorem 6.6 of [Munkres] p. 97. $)
    clslp $p |- ( ( J e. Top /\ S C_ X ) -> ( ( cls ` J ) ` S ) =
                ( S u. ( ( limPt ` J ) ` S ) ) ) $=
      ( vx vn ctop wcel wss wa ccl cfv clp cun cv wo wn csn cdif cin c0 wne
      cnei wral wi neindisj expr adantr wb difsn ineq2d neeq1d adantl sylibrd
      ex r19.21adv simpll simplr clsss3 sseld imp islp2 syl111anc orrd elun
      sylibr ssrdv sscls lpsscls jca unss sylib eqssd ) BGHZACIZJZABKLLZAABMLLZ
      NZVPEVQVSVPEOZVQHZVTVSHZVPWAJZVTAHZVTVRHZPWBWCWDWEWCWDQZFOZAVTRZSZTZUAUBZ
      FWHBUCLLZUDZWEWCWFWKFWLWCWFWGWLHZWKUEWCWFJWNWGATZUAUBZWKWCWNWPUEWFVPWAWNW
      PVTABWGCDUFUGUHWFWKWPUIWCWFWJWOUAWFWIAWGVTAUJUKULUMUNUOUPWCVNVOVTCHZWEWMU
      IVNVOWAUQVNVOWAURVPWAWQVPVQCVTABCDUSUTVAVTAFBCDVBVCUNVDVTAVRVEVFUOVGVPAVQ
      IZVRVQIZJVSVQIVPWRWSABCDVHABCDVIVJAVRVQVKVLVM $.
      $( [26-Feb-2007] $)

    $( A point belonging to a set's closure but not the set itself is a limit
       point. $)
    islpi $p |- ( ( ( J e. Top /\ S C_ X ) /\ ( P e. ( ( cls ` J ) ` S ) /\
       -. P e. S ) ) -> P e. ( ( limPt ` J ) ` S ) ) $=
      ( ctop wcel wss wa ccl cfv wn clp wi cun clslp eleq2d wo elun df-or
      bitri syl6bb biimpd imp32 ) CFGBDHIZABCJKKZGZABGZLZABCMKKZGZUEUGUIUKNZUEU
      GABUJOZGZULUEUFUMABCDEPQUNUHUKRULABUJSUHUKTUAUBUCUD $.
      $( [8-Nov-2007] $)

    $( A subset of a topological space is closed iff it contains all its limit
       points.  Corollary 6.7 of [Munkres] p. 97. $)
    cldlp $p |- ( ( J e. Top /\ S C_ X ) -> ( S e. ( Clsd ` J ) <->
                 ( ( limPt ` J ) ` S ) C_ S ) ) $=
      ( ctop wcel wss wa ccld cfv ccl wceq clp iscld3 cun clslp eqeq1d ssequn2
      syl6bbr bitrd ) BEFACGHZABIJFABKJJZALZABMJJZAGZABCDNUAUCAUDOZALUEUAUBUFAA
      BCDPQUDARST $.
      $( [16-Jun-2008] $) $( [26-Feb-2007] $)
  $}

  ${
    $d v w x y z $.
    $( ` QQ ` is dense in the standard topology on ` RR ` . $)
    qdensere $p |- ( ( cls ` ( topGen ` ran (,) ) ) ` QQ ) = RR $=
      ( vx vy vz vv vw cq cioo crn ctg cfv ccl cr ctop wcel wss retop qssre
      cuni uniretop eqcomi clsss3 mp2an cv cin c0 wne wi wral clt wbr wa crab
      wceq cxr wrex reex rabex dfioo2 elrnoprab co iooval2 eqeq2d anbi1d ioon0
      qbtwnxr 3expia sylbid neeq1 ne0i syl5bi imp syl5 sylbird wb eleq2 qre
      biantrurd breq2 breq1 anbi12d elrab syl6rbbr sylan9bb rexbidva ad2antrl
      mpbird wn incom eqeq1i disj ralnex 3bitri necon2abii sylib exp32
      r19.23aivv sylbi rgen ctb retopbas eqid elcls3 mp3an12 mpbiri ssriv
      eqssi ) FGHZIJZKJJZLXHMNFLOZXILOPQFXHLXHRLSTZUAUBALXIAUCZLNZXLXINZXLBUCZN
      ZXOFUDZUEUFZUGZBXGUHZXSBXGXOXGNXOCUCZDUCZUIUJZYBEUCZUIUJZUKZDLULZUMZEUNUO
      CUNUOXSCEAUNUNYGXOGYFDLUPUQCEADURUSYHXSCEUNUNYAUNNZYDUNNZUKZYHXPXRYKYHXPU
      KZUKZXPAFUOZXRYMYNYAXLUIUJZXLYDUIUJZUKZAFUOZYKYLYRYKYLXOYAYDGUTZUMZXPUKZY
      RYKYTYHXPYKYSYGXODYAYDVAVBVCYKYSUEUFZYRUUAYKUUBYAYDUIUJZYRYAYDVDYIYJUUCYR
      AYAYDVEVFVGYTXPUUBYTXOUEUFUUBXPXOYSUEVHXOXLVIVJVKVLVMVKYHYNYRVNYKXPYHXPYQ
      AFYHXPXLYGNZXLFNZYQXOYGXLVOUUEYQXMYQUKUUDUUEXMYQXLVPVQYFYQDXLLYBXLUMYCYOY
      EYPYBXLYAUIVRYBXLYDUIVSVTWAWBWCWDWEWFYNXQUEXQUEUMFXOUDZUEUMXPWGAFUHYNWGXQ
      UUFUEXOFWHWIAFXOWJXPAFWKWLWMWNWOWPWQWRXGWSNXJXMXNXTVNWTQBXGXLFXHLXHXAXKXB
      XCXDXEXF $.
      $( [1-Mar-2007] $)
  $}

