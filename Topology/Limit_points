$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Limit points
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c limPt $.

  $( Extend class notation with the limit point function for topologies. $)
  clp $a class limPt $.

  ${
    $d v w x y z $.
    $( Define a function on topologies whose value is the set of limit points
       of the subsets of the base set.  See ~ lpval . $)
    df-lp $a |- limPt = ( j e. Top |-> ( x e. ~P U. j |->
                  { y | y e. ( ( cls ` j ) ` ( x \ { y } ) ) } ) ) $.
  $}

  ${
    $d j n x y J $.  $d n x y S $.  $d n j x y X $.  $d n x y P $.
    lpfval.1 $e |- X = U. J $.
    $( The limit point function on the subsets of a topology's base set. $)
    lpfval $p |- ( J e. Top -> ( limPt ` J ) = ( x e. ~P X |->
                  { y | y e. ( ( cls ` J ) ` ( x \ { y } ) ) } ) ) $=
      ( vj ctop wcel cpw cv csn cdif ccl cfv cab cmpt cvv clp wceq cuni syl6eqr
      topopn pwexg mptexg 3syl unieq fveq2 fveq1d eleq2d abbidv mpteq12dv df-lp
      pweq syl fvmptg mpdan ) CGHZADIZBJZAJUSKLZCMNZNZHZBOZPZQHZCRNVESUQDCHURQH
      VFCDEUBDCUCAURVDQUDUEFCAFJZTZIZUSUTVGMNZNZHZBOZPVEGQRVGCSZAVIVMURVDVNVHDS
      VIURSVNVHCTDVGCUFEUAVHDUMUNVNVLVCBVNVKVBUSVNUTVJVAVGCMUGUHUIUJUKABFULUOUP
      $.
      $( [11-Nov-2013] $) $( [10-Feb-2007] $)

    $( The set of limit points of a subset of the base set of a topology.
       Alternate definition of limit point in [Munkres] p. 97. $)
    lpval $p |- ( ( J e. Top /\ S C_ X ) -> ( ( limPt ` J ) ` S ) =
                 { x | x e. ( ( cls ` J ) ` ( S \ { x } ) ) } ) $=
      ( vy ctop wcel wss wa clp cfv cpw cv cdif cab wceq adantr cvv syl11anc wb
      csn ccl cmpt lpfval fveq1d topopn elpw2g syl biimpar wi difss sstr clsss3
      mpan sseld sylan2 abssdv ssexg difeq1 fveq2d eleq2d abbidv fvmptg eqtrd
      eqid ) CGHZBDIZJZBCKLZLZBFDMZANZFNZVMUBZOZCUCLZLZHZAPZUDZLZVMBVOOZVQLZHZA
      PZVGVKWBQVHVGBVJWAFACDEUEUFRVIBVLHZWFSHZWBWFQVGWGVHVGDCHZWGVHUACDEUGZBDCU
      HUIUJVIWFDIWIWHVIWEADVHVGWCDIZWEVMDHUKWCBIVHWKBVOULWCBDUMUOVGWKJWDDVMWCCD
      EUNUPUQURVGWIVHWJRWFDCUSTFBVTWFVLSWAVNBQZVSWEAWLVRWDVMWLVPWCVQVNBVOUTVAVB
      VCWAVFVDTVE $.
      $( [11-Nov-2013] $) $( [10-Feb-2007] $)

    $( The predicate " ` P ` is a limit point of ` S ` ." $)
    islp $p |- ( ( J e. Top /\ S C_ X ) -> ( P e. ( ( limPt ` J ) ` S ) <->
               P e. ( ( cls ` J ) ` ( S \ { P } ) ) ) ) $=
      ( vx ctop wcel wss wa clp cfv cv csn cdif ccl cab lpval eleq2d elisset id
      wceq sneq difeq2d fveq2d eleq12d elab3 syl6bb ) CGHBDIJZABCKLLZHAFMZBUKNZ
      OZCPLZLZHZFQZHABANZOZUNLZHZUIUJUQAFBCDERSUPVAFAAUTTUKAUBZUKAUOUTVBUAVBUMU
      SUNVBULURBUKAUCUDUEUFUGUH $.
      $( [10-Feb-2007] $)

    $( The limits points of a subset are included in the subset's closure. $)
    lpsscls $p |- ( ( J e. Top /\ S C_ X ) -> ( ( limPt ` J ) ` S ) C_
               ( ( cls ` J ) ` S ) ) $=
      ( vx ctop wcel wss wa clp cfv csn cdif ccl cab lpval difss clsss mp3an3
      cv sseld abssdv eqsstrd ) BFGZACHZIZABJKKETZAUGLZMZBNKZKZGZEOAUJKZEABCDPU
      FULEUMUFUKUMUGUDUEUIAHUKUMHAUHQAUIBCDRSUAUBUC $.
      $( [26-Feb-2007] $)

    $( The limits points of a subset are included in the base set. $)
    lpss $p |- ( ( J e. Top /\ S C_ X ) -> ( ( limPt ` J ) ` S ) C_ X ) $=
      ( ctop wcel wss wa clp cfv ccl lpsscls clsss3 sstrd ) BEFACGHABIJJABKJJCA
      BCDLABCDMN $.
      $( [9-Nov-2007] $)

    $( The predicate " ` P ` is a limit point of ` S ` ," in terms of
       neighborhoods.  Definition of limit point in [Munkres] p. 97.  Although
       Munkres uses open neighborhoods, it also works for our more general
       neighborhoods. $)
    islp2 $p |- ( ( J e. Top /\ S C_ X /\ P e. X ) ->
                ( P e. ( ( limPt ` J ) ` S ) <->
         A. n e. ( ( nei ` J ) ` { P } ) ( n i^i ( S \ { P } ) ) =/= (/) ) ) $=
      ( vx vy wcel wss cfv cdif wa wi wal c0 wral wb wceq imbi12d ctop w3a ccld
      clp csn cv crab cint cin wne cnei islp ssdifss clsval sylan2 eleq2d bitrd
      ccl 3adant3 elintrabg df-ral iscld imbi1d albidv syl5bb sylan9bbr 3adant2
      cpw wrex isneip elpwi dfss4 sylib eqcomd eleq1d adantl df-ne imbi2i con2b
      wn bitri incom eqeq1i reldisj syl adantr eldif baibr ralbidva eleq2 sseq1
      3adant1 anbi12d biantru syl6bbr rcla4ev anim2i anassrs imim1i alimi ax-17
      ssid hba1 eleq1 ineq1 neeq1d a4v eltopss ancoms simpl anim1i adantrr an4s
      jca adantlrr ssrin ssn0 ex ad2antlr embantd exp31 com4t imp rexlimdv a1dd
      imp4a alrimd impbid2 impexp visset imbi1i 3bitr4ri albii bitr2i cvv difss
      elpw elpw2g mpbiri difeq2 syl6bb cuni uniexg syl5eqel eqeq2d syl2an sseq2
      3ad2ant1 ralxfrd 3bitr4d bitr4i syl6rbb 3bitrd ) DUAIZBEJZAEIZUBZABDUDKKI
      ZABAUEZLZGUFZJZGDUCKZUGUHZIZUVAEJZEUVALZDIZMZUVBAUVAIZNZNZGOZCUFZUUTUIZPU
      JZCUUSDUKKKZQZUUNUUOUURUVERUUPUUNUUOMZUURAUUTDURKKZIUVEABDEFULUVSUVTUVDAU
      UOUUNUUTEJZUVTUVDSBEUUSUMZGUUTDEFUNUOUPUQUSUUNUUPUVEUVMRUUOUUPUVEUVKGUVCQ
      ZUUNUVMUVBGAUVCEUTUWCUVAUVCIZUVKNZGOUUNUVMUVKGUVCVAUUNUWEUVLGUUNUWDUVIUVK
      UVADEFVBVCVDVEVFVGUUQUVRUVHUVKNZGEVHZQZUVMUUQUVRUVNEJZAHUFZIZUWJUVNJZMZHD
      VIZMZUVPNZCOZUWHUUNUUPUVRUWQRUUOUVRUVNUVQIZUVPNZCOUUNUUPMZUWQUVPCUVQVAUWT
      UWSUWPCUWTUWRUWOUVPAHDUVNEFVJVCVDVEVGUUQUVNDIZAUVNIZUVPNZNZCUWGQZEEUVNLZL
      ZDIZUUTUXFJZAUXFIZNZNZCUWGQZUWQUWHUUOUUPUXEUXMRUUNUUOUUPMZUXDUXLCUWGUXNUV
      NUWGIZMUXAUXHUXCUXKUXOUXAUXHRUXNUXOUVNUXGDUXOUXGUVNUXOUWIUXGUVNSUVNEVKUVN
      EVLVMVNVOVPUXNUXCUXKRUXOUXCUVOPSZUXBVTZNZUXNUXKUXCUXBUXPVTZNUXRUVPUXSUXBU
      VOPVQVRUXBUXPVSWAUXNUXPUXIUXQUXJUUOUXPUXIRUUPUXPUUTUVNUIZPSZUUOUXIUVOUXTP
      UVNUUTWBWCUUOUWAUYAUXIRUWBUUTUVNEWDWEVEWFUUPUXQUXJRUUOUXJUUPUXQAEUVNWGWHV
      PTVEWFTWIWLUUQUWQUWIUXAMZUXBMZUVPNZCOZUXEUUQUWQUYEUWPUYDCUYCUWOUVPUWIUXAU
      XBUWOUXAUXBMUWNUWIUWMUXBHUVNDUWJUVNSZUWMUXBUVNUVNJZMUXBUYFUWKUXBUWLUYGUWJ
      UVNAWJUWJUVNUVNWKWMUYGUXBUVNXBWNWOWPWQWRWSWTUUNUUPUYEUWQNUUOUWTUYEUWPCUWT
      CXAUYDCXCUWTUYEUWIUWNUVPUWTUYEUWNUVPNZUWIUWTUYEUYHUWTUYEMUWMUVPHDUYEUWTUW
      JEJZUWJDIZMZUWKMZUWJUUTUIZPUJZNZUYJUWMUVPNNZUYDUYOCHUVNUWJSZUYCUYLUVPUYNU
      YQUYBUYKUXBUWKUYQUWIUYIUXAUYJUVNUWJEWKUVNUWJDXDWMUVNUWJAWJWMUYQUVOUYMPUVN
      UWJUUTXEXFTXGUWTUYOUYPUYJUWMUWTUYOUVPUYJUWMUWTUYOUVPNUYJUWMMUWTMUYLUYNUVP
      UYJUWKUWTUYLUWLUYJUUNUWKUUPUYLUYJUUNMZUWKUYLUUPUYRUYKUWKUYRUYIUYJUUNUYJUY
      IUWJDEFXHXIUYJUUNXJXNXKXLXMXOUWMUYNUVPNZUYJUWTUWLUYSUWKUWLUYMUVOJZUYSUWJU
      VNUUTXPUYTUYNUVPUYMUVOXQXRWEVPXSXTYAYBYCUOYDXRYEYFYGVGYHUXEUXOUXDNZCOUYEU
      XDCUWGVAVUAUYDCUYBUXCNUWIUXDNUYDVUAUWIUXAUXCYIUYBUXBUVPYIUXOUWIUXDUVNECYJ
      YQYKYLYMYNUUAUUQEYOIZUWHUXMRUUNUUOVUBUUPUUNEDUUBYOFDUAUUCUUDUUHVUBUWFUXLG
      CUXFUWGVUBUXFUWGIZUXOVUBVUCUXFEJEUVNYPUXFEYOYRYSWFVUBUVGUWGIZUVAEUVGLZSZU
      VAUXFSZCUWGVIUVAUWGIZVUBVUDUVGEJEUVAYPUVGEYOYRYSVUHVUEUVAVUHUVFVUEUVASUVA
      EVKUVAEVLVMVNVUGVUFCUVGUWGUVNUVGSUXFVUEUVAUVNUVGEYTUUEWPUUFVUGUWFUXLRVUBV
      UGUVHUXHUVKUXKVUGUVGUXGDUVAUXFEYTVOVUGUVBUXIUVJUXJUVAUXFUUTUUGUVAUXFAWJTT
      VPUUIWEUUJUQUWHVUHUWFNZGOUVMUWFGUWGVAVUIUVLGVUIUVFUWFNUVLVUHUVFUWFUVAEGYJ
      YQYKUVFUVHUVKYIUUKYMWAUULUUM $.
      $( [26-Feb-2007] $)

    $( The closure of a subset of a topological space is the subset together
       with its limit points.  Theorem 6.6 of [Munkres] p. 97. $)
    clslp $p |- ( ( J e. Top /\ S C_ X ) -> ( ( cls ` J ) ` S ) =
                ( S u. ( ( limPt ` J ) ` S ) ) ) $=
      ( vx vn ctop wcel wss wa ccl cfv cv cin c0 wne wi wb sylibrd ex clp wo wn
      cun csn cdif cnei wral neindisj expr adantr ineq2d neeq1d adantl ralrimdv
      difsn simpll simplr clsss3 sseld islp2 syl111anc orrd elun sylibr lpsscls
      imp ssrdv sscls jca unss sylib eqssd ) BGHZACIZJZABKLLZAABUALLZUDZVPEVQVS
      VPEMZVQHZVTVSHZVPWAJZVTAHZVTVRHZUBWBWCWDWEWCWDUCZFMZAVTUEZUFZNZOPZFWHBUGL
      LZUHZWEWCWFWKFWLWCWFWGWLHZWKQWCWFJWNWGANZOPZWKWCWNWPQWFVPWAWNWPVTABWGCDUI
      UJUKWFWKWPRWCWFWJWOOWFWIAWGVTAUPULUMUNSTUOWCVNVOVTCHZWEWMRVNVOWAUQVNVOWAU
      RVPWAWQVPVQCVTABCDUSUTVGVTAFBCDVAVBSVCVTAVRVDVETVHVPAVQIZVRVQIZJVSVQIVPWR
      WSABCDVIABCDVFVJAVRVQVKVLVM $.
      $( [26-Feb-2007] $)

    $( A point belonging to a set's closure but not the set itself is a limit
       point. $)
    islpi $p |- ( ( ( J e. Top /\ S C_ X ) /\ ( P e. ( ( cls ` J ) ` S ) /\
       -. P e. S ) ) -> P e. ( ( limPt ` J ) ` S ) ) $=
      ( ctop wcel wss wa ccl cfv wn clp wi cun clslp eleq2d wo elun df-or bitri
      syl6bb biimpd imp32 ) CFGBDHIZABCJKKZGZABGZLZABCMKKZGZUEUGUIUKNZUEUGABUJO
      ZGZULUEUFUMABCDEPQUNUHUKRULABUJSUHUKTUAUBUCUD $.
      $( [8-Nov-2007] $)

    $( A subset of a topological space is closed iff it contains all its limit
       points.  Corollary 6.7 of [Munkres] p. 97. $)
    cldlp $p |- ( ( J e. Top /\ S C_ X ) -> ( S e. ( Clsd ` J ) <->
                 ( ( limPt ` J ) ` S ) C_ S ) ) $=
      ( ctop wcel wss wa ccld cfv ccl wceq clp iscld3 cun clslp ssequn2 syl6bbr
      eqeq1d bitrd ) BEFACGHZABIJFABKJJZALZABMJJZAGZABCDNUAUCAUDOZALUEUAUBUFAAB
      CDPSUDAQRT $.
      $( [16-Jun-2008] $) $( [26-Feb-2007] $)
  $}

  ${
    $d v w x y z $.
    $( ` QQ ` is dense in the standard topology on ` RR ` . $)
    qdensere $p |- ( ( cls ` ( topGen ` ran (,) ) ) ` QQ ) = RR $=
      ( vx vy vz vv vw cq cioo cfv cr wcel wss cv c0 wne clt wbr wceq cxr wrex
      wa crn ctg ccl ctop retop qssre cuni uniretop eqcomi clsss3 mp2an wi wral
      cin crab reex rabex dfioo2 elrnoprab co iooval2 eqeq2d anbi1d ne0i syl5ib
      neeq1 imp ioon0 qbtwnxr 3expia sylbid syl5 sylbird wb eleq2 qre biantrurd
      breq2 breq1 elrab syl6rbbr sylan9bb rexbidva ad2antrl mpbird disjr ralnex
      anbi12d wn bitri necon2abii sylib exp32 rexlimivv sylbi rgen ctb retopbas
      eqid elcls3 mp3an12 mpbiri ssriv eqssi ) FGUAZUBHZUCHHZIXFUDJFIKZXGIKUEUF
      FXFIXFUGIUHUIZUJUKAIXGALZIJZXJXGJZXJBLZJZXMFUNZMNZULZBXEUMZXQBXEXMXEJXMCL
      ZDLZOPZXTELZOPZTZDIUOZQZERSCRSXQCEARRYEXMGYDDIUPUQCEADURUSYFXQCERRXSRJZYB
      RJZTZYFXNXPYIYFXNTZTZXNAFSZXPYKYLXSXJOPZXJYBOPZTZAFSZYIYJYPYIYJXMXSYBGUTZ
      QZXNTZYPYIYRYFXNYIYQYEXMDXSYBVAVBVCYSYQMNZYIYPYRXNYTXNXMMNYRYTXMXJVDXMYQM
      VFVEVGYIYTXSYBOPZYPXSYBVHYGYHUUAYPAXSYBVIVJVKVLVMVGYFYLYPVNYIXNYFXNYOAFYF
      XNXJYEJZXJFJZYOXMYEXJVOUUCYOXKYOTUUBUUCXKYOXJVPVQYDYODXJIXTXJQYAYMYCYNXTX
      JXSOVRXTXJYBOVSWHVTWAWBWCWDWEYLXOMXOMQXNWIAFUMYLWIAXMFWFXNAFWGWJWKWLWMWNW
      OWPXEWQJXHXKXLXRVNWRUFBXEXJFXFIXFWSXIWTXAXBXCXD $.
      $( [1-Mar-2007] $)
  $}

