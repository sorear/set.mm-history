$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Subbases for topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f g x y v w A $.  $d f g t u v w B $.
    subbas.1 $e |- A e. _V $.
    subbas.2 $e |- B = { x | E. y ( y C_ A /\ y e. Fin /\ x = |^| y ) } $.
    $( The collection of finite intersections of the elements of any set ` A `
       is a basis for a topology (on ` U. A ` by ~ subbas2 ).  The set ` A ` is
       called a subbasis for that topology.  Theorem for subbases in [Munkres]
       p. 82.  See ~ abfii1 through ~ abfii5 for other ways to express the
       collection of finite intersections. $)
    subbas $p |- B e. TopBases $=
      ( vu vt vw vv vf vg ctb wcel cv cin wss wa wrex wral cfn cint wceq w3a
      wex wi cab eleq2i visset eqeq1 3anbi3d exbidv elab sseq1 eleq1 inteq
      eqeq2d 3anbi123d cbvexv 3bitri eeanv an6 cun unex cla4ev unss biimpi
      unfi ineq12 intun syl6eqr syl3an inex1 bitr2i sylib ssid jctr eleq2
      anbi12d rcla4ev syl2an ex sylbi 19.23aivv sylbir syl2anb r19.21aiv
      rgen2a cvv wb csn df-sn snex eqeltrri abexssex 3simpb eximi ss2abi
      eqsstri ssexi isbasis2g ax-mp mpbir ) DMNZGOZHOZNZXFIOZJOZPZQZRZHDSZGXJTZ
      JDTIDTZXNIJDXHDNZXIDNZRXMGXJXPKOZCQZXRUANZXHXRUBZUCZUDZKUEZLOZCQZYEUANZXI
      YEUBZUCZUDZLUEZXEXJNZXMUFZXQXPXHBOZCQZYNUANZAOZYNUBZUCZUDZBUEZAUGZNYOYPXH
      YRUCZUDZBUEZYDDUUBXHFUHUUAUUEAXHIUIZYQXHUCZYTUUDBUUGYSUUCYOYPYQXHYRUJUKUL
      UMUUDYCBKYNXRUCZYOXSYPXTUUCYBYNXRCUNYNXRUAUOUUHYRYAXHYNXRUPUQURUSUTXQXIUU
      BNYOYPXIYRUCZUDZBUEZYKDUUBXIFUHUUAUUKAXIJUIYQXIUCZYTUUJBUULYSUUIYOYPYQXIY
      RUJUKULUMUUJYJBLYNYEUCZYOYFYPYGUUIYIYNYECUNYNYEUAUOUUMYRYHXIYNYEUPUQURUSU
      TYDYKRYCYJRZLUEKUEYMYCYJKLVAUUNYMKLUUNXSYFRZXTYGRZYBYIRZUDZYMXSXTYBYFYGYI
      VBUURYLXMUURXJDNZYLXJXJQZRZXMYLUURYOYPXJYRUCZUDZBUEZUUSXRYEVCZCQZUVEUANZX
      JUVEUBZUCZUVDUUOUUPUUQUVCUVFUVGUVIUDBUVEXRYEKUILUIVDYNUVEUCZYOUVFYPUVGUVB
      UVIYNUVECUNYNUVEUAUOUVJYRUVHXJYNUVEUPUQURVEUUOUVFXRYECVFVGXRYEVHUUQXJYAYH
      PUVHXHYAXIYHVIXRYEVJVKVLUUSXJUUBNUVDDUUBXJFUHUUAUVDAXJXHXIUUFVMYQXJUCZYTU
      VCBUVKYSUVBYOYPYQXJYRUJUKULUMVNVOYLUUTXJVPVQXLUVAHXJDXFXJUCXGYLXKUUTXFXJX
      EVRXFXJXJUNVSVTWAWBWCWDWEWFWGWHDWINXDXOWJDYOYSRZBUEZAUGZYSBACEYRWKYSAUGWI
      AYRWLYRWMWNWODUUBUVNFUUAUVMAYTUVLBYOYPYSWPWQWRWSWTIJGHDWIXAXBXC $.
      $( [13-Mar-2009] $) $( [29-Jul-2006] $)
  $}

  ${
    $d w x y A $.
    subbas2.1 $e |- B =
           { x | E. y ( y C_ A /\ y e. Fin /\ x = |^| y ) } $.
    $( The collection of finite intersections of any set (subbasis) ` A ` is a
       basis for a topology on ` U. A ` .  Justifies the definition of subbasis
       in [Munkres] p. 82 (after using ~ unitg ). $)
    subbas2 $p |- U. B = U. A $=
      ( vw cuni cv wss cfn wcel cint wceq w3a wex cab unieqi wa eluniab ssel
      com12 simpr c0 wne cvv visset eleq1 mpbii intex sylibr intssuni2 sylan2
      eqsstrd 3adant2 syl5 19.23adv imp 19.23aiv sylbi ssriv csn snssi snfi
      a1i intsn eqcomi snex sseq1 inteq eqeq2d 3anbi123d cla4ev syl111anc
      eqeq1 3anbi3d exbidv elab uniss ax-mp eqssi eqtri ) DGBHZCIZWBJKZAHZWBLZM
      ZNZBOZAPZGZCGZDWJEQWKWLFWKWLFHZWKKWMWEKZWIRZAOWMWLKZWIAWMSWOWPAWNWIWPWNWH
      WPBWNWEWLIZWPWHWQWNWPWEWLWMTUAWCWGWQWDWCWGRWEWFWLWCWGUBWGWCWBUCUDZWFWLIWG
      WFUEKZWRWGWEUEKWSAUFWEWFUEUGUHWBUIUJWBCUKULUMUNUOUPUQURUSUTCWJIWLWKIFCWJW
      MCKZWCWDWMWFMZNZBOZWMWJKWTWMVAZCIZXDJKZWMXDLZMZXCWMCVBXFWTWMVCVDXHWTXGWMW
      MFUFZVEVFVDXBXEXFXHNBXDWMVGWBXDMZWCXEWDXFXAXHWBXDCVHWBXDJUGXJWFXGWMWBXDVI
      VJVKVLVMWIXCAWMXIWEWMMZWHXBBXKWGXAWCWDWEWMWFVNVOVPVQUJUTCWJVRVSVTWA $.
      $( [2-Apr-2009] $) $( [30-Jul-2006] $)
  $}


