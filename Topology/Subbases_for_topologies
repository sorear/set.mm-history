$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Subbases for topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f g x y v w A $.  $d f g t u v w B $.
    subbas.1 $e |- A e. _V $.
    subbas.2 $e |- B = { x | E. y ( y C_ A /\ y e. Fin /\ x = |^| y ) } $.
    $( The collection of finite intersections of the elements of any set ` A `
       is a basis for a topology (on ` U. A ` by ~ subbas2 ).  The set ` A ` is
       called a subbasis for that topology.  Theorem for subbases in [Munkres]
       p. 82.  See ~ dffi2 for another way to express the collection of finite
       intersections. $)
    subbas $p |- B e. TopBases $=
      ( vu vt vw vv vf vg wcel cv wss wa cfn wceq w3a wex ctb wrex wral cint wi
      cin eleq2i visset eqeq1 3anbi3d exbidv sseq1 eleq1 inteq eqeq2d 3anbi123d
      cab elab cbvexv 3bitri eeanv an6 cun unss biimpi unfi ineq12 syl6eqr unex
      intun cla4ev syl3an inex1 bitr2i sylib ssid jctr eleq2 anbi12d rcla4ev ex
      syl2an sylbi exlimivv sylbir syl2anb ralrimiv rgen2a cvv wb snex eqeltrri
      df-sn abexssex 3simpb eximi ss2abi eqsstri ssexi isbasis2g ax-mp mpbir
      csn ) DUAMZGNZHNZMZXFINZJNZUFZOZPZHDUBZGXJUCZJDUCIDUCZXNIJDXHDMZXIDMZPXMG
      XJXPKNZCOZXRQMZXHXRUDZRZSZKTZLNZCOZYEQMZXIYEUDZRZSZLTZXEXJMZXMUEZXQXPXHBN
      ZCOZYNQMZANZYNUDZRZSZBTZAUQZMYOYPXHYRRZSZBTZYDDUUBXHFUGUUAUUEAXHIUHZYQXHR
      ZYTUUDBUUGYSUUCYOYPYQXHYRUIUJUKURUUDYCBKYNXRRZYOXSYPXTUUCYBYNXRCULYNXRQUM
      UUHYRYAXHYNXRUNUOUPUSUTXQXIUUBMYOYPXIYRRZSZBTZYKDUUBXIFUGUUAUUKAXIJUHYQXI
      RZYTUUJBUULYSUUIYOYPYQXIYRUIUJUKURUUJYJBLYNYERZYOYFYPYGUUIYIYNYECULYNYEQU
      MUUMYRYHXIYNYEUNUOUPUSUTYDYKPYCYJPZLTKTYMYCYJKLVAUUNYMKLUUNXSYFPZXTYGPZYB
      YIPZSZYMXSXTYBYFYGYIVBUURYLXMUURXJDMZYLXJXJOZPZXMYLUURYOYPXJYRRZSZBTZUUSU
      UOXRYEVCZCOZUUPUVEQMZUUQXJUVEUDZRZUVDUUOUVFXRYECVDVEXRYEVFUUQXJYAYHUFUVHX
      HYAXIYHVGXRYEVJVHUVCUVFUVGUVISBUVEXRYEKUHLUHVIYNUVERZYOUVFYPUVGUVBUVIYNUV
      ECULYNUVEQUMUVJYRUVHXJYNUVEUNUOUPVKVLUUSXJUUBMUVDDUUBXJFUGUUAUVDAXJXHXIUU
      FVMYQXJRZYTUVCBUVKYSUVBYOYPYQXJYRUIUJUKURVNVOYLUUTXJVPVQXLUVAHXJDXFXJRXGY
      LXKUUTXFXJXEVRXFXJXJULVSVTWBWAWCWDWEWFWGWHDWIMXDXOWJDYOYSPZBTZAUQZYSBACEY
      RXCYSAUQWIAYRWMYRWKWLWNDUUBUVNFUUAUVMAYTUVLBYOYPYSWOWPWQWRWSIJGHDWIWTXAXB
      $.
      $( [13-Mar-2009] $) $( [29-Jul-2006] $)
  $}

  ${
    $d w x y A $.
    subbas2.1 $e |- B =
           { x | E. y ( y C_ A /\ y e. Fin /\ x = |^| y ) } $.
    $( The collection of finite intersections of any set (subbasis) ` A ` is a
       basis for a topology on ` U. A ` .  Justifies the definition of subbasis
       in [Munkres] p. 82 (after using ~ unitg ). $)
    subbas2 $p |- U. B = U. A $=
      ( vw cuni cv wss cfn wcel cint wceq w3a wex wa cvv visset eleq1 sylibr c0
      cab unieqi eluniab simpr wne mpbii intex intssuni2 sylan2 eqsstrd 3adant2
      ssel com12 syl5 exlimdv imp exlimiv sylbi ssriv csn snssi snfi a1i eqcomi
      intsn snex sseq1 inteq eqeq2d 3anbi123d cla4ev eqeq1 3anbi3d exbidv uniss
      syl111anc elab ax-mp eqssi eqtri ) DGBHZCIZWBJKZAHZWBLZMZNZBOZAUBZGZCGZDW
      JEUCWKWLFWKWLFHZWKKWMWEKZWIPZAOWMWLKZWIAWMUDWOWPAWNWIWPWNWHWPBWHWEWLIZWNW
      PWCWGWQWDWCWGPWEWFWLWCWGUEWGWCWBUAUFZWFWLIWGWFQKZWRWGWEQKWSARWEWFQSUGWBUH
      TWBCUIUJUKULWQWNWPWEWLWMUMUNUOUPUQURUSUTCWJIWLWKIFCWJWMCKZWCWDWMWFMZNZBOZ
      WMWJKWTWMVAZCIZXDJKZWMXDLZMZXCWMCVBXFWTWMVCVDXHWTXGWMWMFRZVFVEVDXBXEXFXHN
      BXDWMVGWBXDMZWCXEWDXFXAXHWBXDCVHWBXDJSXJWFXGWMWBXDVIVJVKVLVQWIXCAWMXIWEWM
      MZWHXBBXKWGXAWCWDWEWMWFVMVNVOVRTUTCWJVPVSVTWA $.
      $( [2-Apr-2009] $) $( [30-Jul-2006] $)
  $}


