$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Subbases for topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f g x y v w A $.  $d f g t u v w B $.
    subbas.1 $e |- A e. _V $.
    subbas.2 $e |- B = { x | E. y ( y C_ A /\ y e. Fin /\ x = |^| y ) } $.
    $( The collection of finite intersections of the elements of any set ` A `
       is a basis for a topology (on ` U. A ` by ~ subbas2 ).  The set ` A ` is
       called a subbasis for that topology.  Theorem for subbases in [Munkres]
       p. 82.  See ~ dffi2 for another way to express the collection of finite
       intersections. $)
    subbas $p |- B e. TopBases $=
      ( vu vt vw vv vf vg wcel cv wss wa cfn wceq w3a wex ctb wrex wral cint wi
      cin cab eleq2i vex eqeq1 3anbi3d exbidv elab sseq1 eleq1 eqeq2d 3anbi123d
      inteq cbvexv 3bitri eeanv an6 cun unss biimpi ineq12 intun syl6eqr cla4ev
      unfi unex syl3an inex1 bitr2i sylib ssid jctr eleq2 anbi12d rcla4ev sylbi
      syl2an ex exlimivv sylbir syl2anb ralrimiv rgen2a cvv df-sn snex eqeltrri
      wb csn abexssex 3simpb eximi ss2abi eqsstri ssexi isbasis2g ax-mp mpbir )
      DUAMZGNZHNZMZXFINZJNZUFZOZPZHDUBZGXJUCZJDUCIDUCZXNIJDXHDMZXIDMZPXMGXJXPKN
      ZCOZXRQMZXHXRUDZRZSZKTZLNZCOZYEQMZXIYEUDZRZSZLTZXEXJMZXMUEZXQXPXHBNZCOZYN
      QMZANZYNUDZRZSZBTZAUGZMYOYPXHYRRZSZBTZYDDUUBXHFUHUUAUUEAXHIUIZYQXHRZYTUUD
      BUUGYSUUCYOYPYQXHYRUJUKULUMUUDYCBKYNXRRZYOXSYPXTUUCYBYNXRCUNYNXRQUOUUHYRY
      AXHYNXRURUPUQUSUTXQXIUUBMYOYPXIYRRZSZBTZYKDUUBXIFUHUUAUUKAXIJUIYQXIRZYTUU
      JBUULYSUUIYOYPYQXIYRUJUKULUMUUJYJBLYNYERZYOYFYPYGUUIYIYNYECUNYNYEQUOUUMYR
      YHXIYNYEURUPUQUSUTYDYKPYCYJPZLTKTYMYCYJKLVAUUNYMKLUUNXSYFPZXTYGPZYBYIPZSZ
      YMXSXTYBYFYGYIVBUURYLXMUURXJDMZYLXJXJOZPZXMYLUURYOYPXJYRRZSZBTZUUSUUOXRYE
      VCZCOZUUPUVEQMZUUQXJUVEUDZRZUVDUUOUVFXRYECVDVEXRYEVJUUQXJYAYHUFUVHXHYAXIY
      HVFXRYEVGVHUVCUVFUVGUVISBUVEXRYEKUILUIVKYNUVERZYOUVFYPUVGUVBUVIYNUVECUNYN
      UVEQUOUVJYRUVHXJYNUVEURUPUQVIVLUUSXJUUBMUVDDUUBXJFUHUUAUVDAXJXHXIUUFVMYQX
      JRZYTUVCBUVKYSUVBYOYPYQXJYRUJUKULUMVNVOYLUUTXJVPVQXLUVAHXJDXFXJRXGYLXKUUT
      XFXJXEVRXFXJXJUNVSVTWBWCWAWDWEWFWGWHDWIMXDXOWMDYOYSPZBTZAUGZYSBACEYRWNYSA
      UGWIAYRWJYRWKWLWODUUBUVNFUUAUVMAYTUVLBYOYPYSWPWQWRWSWTIJGHDWIXAXBXC $.
      $( [13-Mar-2009] $) $( [29-Jul-2006] $)
  $}


  ${
    $d w x y A $.
    subbas2.1 $e |- B =
           { x | E. y ( y C_ A /\ y e. Fin /\ x = |^| y ) } $.
    $( The collection of finite intersections of any set (subbasis) ` A ` is a
       basis for a topology on ` U. A ` .  Justifies the definition of subbasis
       in [Munkres] p. 82 (after using ~ unitg ). $)
    subbas2 $p |- U. B = U. A $=
      ( vw cuni cv wss cfn wcel cint wceq w3a wex wa cvv vex eleq1 sylibr simpr
      cab unieqi eluniab wne mpbii intex intssuni2 sylan2 eqsstrd 3adant2 com12
      ssel syl5 exlimdv imp exlimiv sylbi ssriv csn snssi snfi a1i intsn eqcomi
      c0 sseq1 inteq eqeq2d 3anbi123d cla4ev syl3anc eqeq1 3anbi3d exbidv uniss
      snex elab ax-mp eqssi eqtri ) DGBHZCIZWBJKZAHZWBLZMZNZBOZAUBZGZCGZDWJEUCW
      KWLFWKWLFHZWKKWMWEKZWIPZAOWMWLKZWIAWMUDWOWPAWNWIWPWNWHWPBWHWEWLIZWNWPWCWG
      WQWDWCWGPWEWFWLWCWGUAWGWCWBVFUEZWFWLIWGWFQKZWRWGWEQKWSARWEWFQSUFWBUGTWBCU
      HUIUJUKWQWNWPWEWLWMUMULUNUOUPUQURUSCWJIWLWKIFCWJWMCKZWCWDWMWFMZNZBOZWMWJK
      WTWMUTZCIZXDJKZWMXDLZMZXCWMCVAXFWTWMVBVCXHWTXGWMWMFRZVDVEVCXBXEXFXHNBXDWM
      VQWBXDMZWCXEWDXFXAXHWBXDCVGWBXDJSXJWFXGWMWBXDVHVIVJVKVLWIXCAWMXIWEWMMZWHX
      BBXKWGXAWCWDWEWMWFVMVNVOVRTUSCWJVPVSVTWA $.
      $( [2-Apr-2009] $) $( [30-Jul-2006] $)
  $}


