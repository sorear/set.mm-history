$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Neighborhoods
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c nei $.

  $( Extend class notation with neighborhood relation for topologies. $)
  cnei $a class nei $.

  ${
    $d g j y v w z $.
    $( Define a function on topologies whose value is a map from a subset to
       its neighborhoods. $)
    df-nei $a |- nei = { <. j , y >. | ( j e. Top /\ y = { <. z , v >. |
                         ( z C_ U. j /\ v = { w | ( w C_ U. j /\
                  E. g e. j ( z C_ g /\ g C_ w ) ) } ) } ) } $.
  $}

  ${
    $d g j v w y z J $.  $d g v z N $.  $d g P $.  $d g v w z S $.
    $d g j v w y z X $.
    neifval.1 $e |- X = U. J $.
    $( The neighborhood function on the subsets of a topology's base set. $)
    neifval $p |- ( J e. Top -> ( nei ` J ) = { <. z , w >. |
                         ( z C_ X /\ w = { v | ( v C_ X /\
                  E. g e. J ( z C_ g /\ g C_ v ) ) } ) } ) $=
      ( vj vy ctop wcel cv wss wa wrex cab wceq copab cvv cnei cfv cpw cuni
      uniexg syl5eqel pwexg opabex2g 3syl visset elpw anbi1i opabbii syl5eqelr
      unieq syl6eqr sseq2d rexeq anbi12d abbidv eqeq2d opabbidv df-nei
      fvopab4g mpdan ) EJKZALZFMZBLZCLZFMZVFDLZMVKVIMNZDEOZNZCPZQZNZABRZSKETUAV
      RQVEVFFUBZKZVPNZABRZSVRVEFSKVSSKWBSKVEEUCZSFEJUDGUEFSUFABVSVOSUGUHWAVQABV
      TVGVPVFFAUIUJUKULUMHIEVFHLZUCZMZVHVIWEMZVLDWDOZNZCPZQZNZABRVRJSTWDEQZWLVQ
      ABWMWFVGWKVPWMWEFVFWMWEWCFWDEUNGUOZUPWMWJVOVHWMWIVNCWMWGVJWHVMWMWEFVIWNUP
      VLDWDEUQURUSUTURVAIACBDHVBVCVD $.
      $( [11-Feb-2007] $)

    $( The neighborhood function is a function of the subsets of a topology's
       base set. $)
    neif $p |- ( J e. Top -> ( nei ` J ) Fn ~P X ) $=
      ( vz vw vv vg ctop wcel cnei cfv cpw wfn cv wss wa wrex cab wceq copab
      cvv wral cuni uniexg syl5eqel abssexg syl a1d r19.21aiv visset elpw
      anbi1i opabbii eqcomi fnopab2g sylib neifval fneq1d mpbird ) AHIZAJKZBLZM
      DNZBOZENFNZBOVCGNZOVFVEOPGAQZPFRZSZPZDETZVBMZUTVHUAIZDVBUBVLUTVMDVBUTVMVC
      VBIZUTBUAIVMUTAUCUABAHUDCUEVGFBUAUFUGUHUIDEVBVHVKVNVIPZDETVKVOVJDEVNVDVIV
      CBDUJUKULUMUNUOUPUTVBVAVKDEFGABCUQURUS $.
      $( [12-Feb-2007] $)

    $( A set with a neighborhood is a subset of the topology's base set.  (This
       theorem depends on a function's value being empty outside of its domain,
       but it will make later theorems simpler to state.) $)
    neiss2 $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) ) -> S C_ X ) $=
      ( ctop wcel cnei cfv wa cpw wss cdm elfvdm adantl wb wfn wceq neif fndm
      syl eleq2d adantr mpbid cvv cuni uniexg syl5eqel elpw2g ) BFGZCABHIZIGZJZ
      ADKZGZADLZUMAUKMZGZUOULURUJCAUKNOUJURUOPULUJUQUNAUJUKUNQUQUNRBDESUNUKTUAU
      BUCUDUJUOUPPZULUJDUEGUSUJBUFUEDBFUGEUHADUEUIUAUCUD $.
      $( [12-Feb-2007] $)

    $( The set of neighborhoods of a subset of the base set of a topology. $)
    neival $p |- ( ( J e. Top /\ S C_ X ) -> ( ( nei ` J ) ` S ) =
                 { v | ( v C_ X /\ E. g e. J ( S C_ g /\ g C_ v ) ) } ) $=
      ( vz vw ctop wcel wss wa cnei cfv cv cpw wrex cab wceq copab neifval
      adantr visset elpw anbi1i opabbii syl6eqr fveq1d cvv elpw2g biimpar cuni
      uniexg syl5eqel sylan abssexg syl sseq1 anbi1d rexbidv anbi2d abbidv
      eqid fvopab4g syl11anc eqtrd ) DIJZBEKZLZBDMNZNBGOZEPZJZHOAOZEKZVKCOZKZVP
      VNKZLZCDQZLZARZSZLZGHTZNZVOBVPKZVRLZCDQZLZARZVIBVJWEVIVJVKEKZWCLZGHTZWEVG
      VJWNSVHGHACDEFUAUBWDWMGHVMWLWCVKEGUCUDUEUFUGUHVIBVLJZWKUIJZWFWKSEUIJZVHWO
      VGWQWOVHBEUIUJUKVGDULUIEDIUMFUNZUOVGWPVHVGWQWPWRWIAEUIUPUQUBGHBWBWKVLUIWE
      VKBSZWAWJAWSVTWIVOWSVSWHCDWSVQWGVRVKBVPURUSUTVAVBWEVCVDVEVF $.
      $( [11-Feb-2007] $)

    $( The predicate " ` N ` is a neighborhood of ` S ` ."  (Contributed by FL,
       25-Sep-2006.) $)
    isnei $p |- ( ( J e. Top /\ S C_ X ) -> ( N e. ( ( nei ` J ) ` S ) <->
                 ( N C_ X /\ E. g e. J ( S C_ g /\ g C_ N ) ) ) ) $=
      ( vv ctop wcel wss wa cnei cfv cv wrex cab neival eleq2d cvv wi wb ssexg
      cuni uniexg syl5eqel sylan2 expcom adantr adantrd wceq sseq1 sseq2
      anbi2d rexbidv anbi12d elab3g syl bitrd ) CHIZAEJZKZDACLMMZIDGNZEJZABNZJZ
      VEVCJZKZBCOZKZGPZIZDEJZVFVEDJZKZBCOZKZVAVBVKDGABCEFQRVAVQDSIZTVLVQUAVAVMV
      RVPUSVMVRTUTVMUSVRVMESIVRUSDESUBUSCUCSECHUDFUEUFUGUHUIVJVQGDSVCDUJZVDVMVI
      VPVCDEUKVSVHVOBCVSVGVNVFVCDVEULUMUNUOUPUQUR $.
      $( [11-Feb-2007] $)

    $( An intuitive definition of a neighborhood in terms of interior.
       (Contributed by Szymon Jaroszewicz, 18-Dec-2007.) $)
    neiint $p |- ( ( J e. Top /\ S C_ X /\ N C_ X ) ->
           ( N e. ( ( nei ` J ) ` S ) <-> S C_ ( ( int ` J ) ` N ) ) ) $=
      ( vv vg vz ctop wcel wss w3a cv wa wrex crab cuni cnei cfv cnt ssuni
      sseq1 elrab sylan2br an12s r19.23aiva adantl wceq sseq2 anbi12d rcla4ev
      ssrab2 uniopn mpan2 3ad2ant1 unissb simprbi mprgbir jctr syl2an ex simp3
      jctild impbid2 wb isnei 3adant3 ntrval 3adant2 sseq2d 3bitr4d ) BIJZADKZC
      DKZLZVNAFMZKZVPCKZNZFBOZNZAGMZCKZGBPZQZKZCABRSSJZACBTSSZKVOWAWFVTWFVNVSWF
      FBVQVPBJZVRWFVQVPWDJWFWIVRNAVPWDUAWCVRGVPBWBVPCUBUCUDUEUFUGVOWFVTVNVOWFVT
      WEBJZWFWECKZNZVTVOWFVSWLFWEBVPWEUHVQWFVRWKVPWEAUIVPWECUBUJUKVLVMWJVNVLWDB
      KWJWCGBULWDBUMUNUOWFWKWKHMZCKZHWDHWDCUPWMWDJWMBJWNWCWNGWMBWBWMCUBUCUQURUS
      UTVAVLVMVNVBVCVDVLVMWGWAVEVNAFBCDEVFVGVOWHWEAVLVNWHWEUHVMGCBDEVHVIVJVK $.
      $( [7-Sep-2007] $)

    $( The predicate " ` N ` is a neighborhood of point ` P ` ." $)
    isneip $p |- ( ( J e. Top /\ P e. X ) -> ( N e. ( ( nei ` J ) ` { P } ) <->
                 ( N C_ X /\ E. g e. J ( P e. g /\ g C_ N ) ) ) ) $=
      ( ctop wcel wa csn cnei cfv wss cv wrex wb isnei snssi sylan2 snssg
      anbi1d rexbidv anbi2d adantl bitr4d ) CGHZAEHZIDAJZCKLLHZDEMZUHBNZMZUKDMZ
      IZBCOZIZUJAUKHZUMIZBCOZIZUFUHEMUIUPPUGUHBCDEFQAERSUGUTUPPUFUGUSUOUJUGURUN
      BCUGUQULUMAUKETUAUBUCUDUE $.
      $( [26-Feb-2007] $)

    $( A neighborhood is included in the topology's base set. $)
    neii1 $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) ) -> N C_ X ) $=
      ( vg ctop wcel cnei cfv wa wss neiss2 wi cv wrex isnei simpl syl6bi ex
      com23 imp mpd ) BGHZCABIJJHZKADLZCDLZABCDEMUDUEUFUGNUDUFUEUGUDUFUEUGNUDUF
      KUEUGAFOZLUHCLKFBPZKUGAFBCDEQUGUIRSTUAUBUC $.
      $( [12-Feb-2007] $)
  $}

  ${
    $d g J $.  $d g N $.  $d g R $.  $d g S $.
    $( Property of a neighborhood. $)
    neii2 $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) ) ->
                 E. g e. J ( S C_ g /\ g C_ N ) ) $=
      ( ctop wcel cnei cfv wa cuni wss cv wrex eqid neiss2 wi isnei simpr
      syl6bi ex com23 imp mpd ) CEFZDACGHHFZIACJZKZABLZKUHDKIBCMZACDUFUFNZOUDUE
      UGUIPUDUGUEUIUDUGUEUIPUDUGIUEDUFKZUIIUIABCDUFUJQUKUIRSTUAUBUC $.
      $( [12-Feb-2007] $)

    $( Any neighborhood of a set ` S ` is also a neighborhood of any subset
       ` R C_ S ` .  Bourbaki TG I.2.  (Contributed by FL, 25-Sep-2006.) $)
    neiss $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) /\ R C_ S ) ->
                 N e. ( ( nei ` J ) ` R ) ) $=
      ( vg ctop wcel cnei cfv wss w3a cuni cv wa wrex wb simp1 simp3 eqid
      neiss2 3adant3 sstrd isnei syl11anc neii1 neii2 wi sstr2 anim1d reximdv
      3ad2ant3 mpd mpbir2and ) CFGZDBCHIZIGZABJZKZDAUOIGZDCLZJZAEMZJZVBDJZNZECO
      ZURUNAUTJUSVAVFNPUNUPUQQURABUTUNUPUQRUNUPBUTJUQBCDUTUTSZTUAUBAECDUTVGUCUD
      UNUPVAUQBCDUTVGUEUAURBVBJZVDNZECOZVFUNUPVJUQBECDUFUAUQUNVJVFUGUPUQVIVEECU
      QVHVCVDABVBUHUIUJUKULUM $.
      $( [12-Feb-2007] $)

    $( A set is included in its neighborhoods.  Based on Bourbaki TG I.3 Viii.
       (Contributed by FL, 16-Nov-2006.) $)
    ssnei $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) ) -> S C_ N ) $=
      ( vg ctop wcel cnei cfv wa cv wss wrex neii2 sstr r19.23aivw syl ) BEFCAB
      GHHFIADJZKQCKIZDBLACKZADBCMRSDBAQCNOP $.
      $( [11-Feb-2007] $)
  $}

  $( A point belongs to any of its neighborhoods.  Based on Bourbaki TG I.3
     Viii.  (Contributed by FL, 28-Sep-2006.) $)
  elnei $p |- ( ( J e. Top /\ P e. A /\ N e. ( ( nei ` J ) ` { P } ) ) ->
              P e. N ) $=
    ( ctop wcel csn cnei cfv w3a wss ssnei 3adant2 wb snssg 3ad2ant2 mpbird )
    CEFZBAFZDBGZCHIIFZJBDFZTDKZRUAUCSTCDLMSRUBUCNUABDAOPQ $.
    $( [11-Feb-2007] $)

  $( The empty set is not a neighborhood of a nonempty set.  (Contributed by
     FL, 18-Sep-2007.) $)
  0nnei $p |- ( ( J e. Top /\ S =/= (/) ) -> -. (/) e. ( ( nei ` J ) ` S ) ) $=
    ( ctop wcel c0 wne cnei cfv wn wceq wa wss ssnei ss0b sylib ex necon3ad
    imp ) BCDZAEFEABGHHDZISTAESTAEJZSTKAELUAABEMANOPQR $.
    $( [18-Sep-2007] $)

  ${
    $d g h p v J $.  $d g M $.  $d g h p v N $.  $d g P $.  $d g h p S $.
    $d g h p X $.
    neips.1 $e |- X = U. J $.
    $( A neighborhood of a set is a neighborhood of every point in the set.
       Bourbaki TG I.2.  (Contributed by FL, 16-Nov-2006.) $)
    neips $p |- ( ( J e. Top /\ S C_ X /\ S =/= (/) ) ->
                ( N e. ( ( nei ` J ) ` S ) <->
                A. p e. S N e. ( ( nei ` J ) ` { p } ) ) ) $=
      ( vg vh vv ctop wcel wss c0 wne w3a cnei cfv cv csn wral wi neiss snssi
      syl3an3 3exp r19.21adv 3ad2ant1 wa wrex wb r19.28zv 3ad2ant3 crab cuni
      ssrab2 uniopn mpan2 ad2antrr elunii sseq1 elrab sylan2br an12s
      r19.23aiva ralimi dfss3 sylibr adantl unissb simprbi mprgbir jctir wceq
      sseq2 anbi12d rcla4ev syl11anc ex anim2d 3adant3 sylbid isneip ssel2
      sylan2 anassrs ralbidva isnei 3imtr4d impbid ) BJKZADLZAMNZOZCABPQZQKZCER
      ZSZWNQKZEATZWJWKWOWSUAWLWJWOWREAWJWOWPAKZWRWJWOWQALWRWTWQABCUBWPAUCUDUEUF
      UGWMCDLZWPGRZKZXBCLZUHZGBUIZUHZEATZXAAHRZLZXICLZUHZHBUIZUHZWSWOWMXHXAXFEA
      TZUHZXNWLWJXHXPUJWKXAXFEAUKULWJWKXPXNUAWLWJWKUHZXOXMXAXQXOXMXQXOUHZIRZCLZ
      IBUMZUNZBKZAYBLZYBCLZUHZXMWJYCWKXOWJYABLYCXTIBUOYABUPUQURXRYDYEXOYDXQXOWP
      YBKZEATYDXFYGEAXEYGGBXCXBBKZXDYGXCXBYAKYGYHXDUHWPXBYAUSXTXDIXBBXSXBCUTVAV
      BVCVDVEEAYBVFVGVHYEXKHYAHYACVIXIYAKXIBKXKXTXKIXIBXSXICUTVAVJVKVLXLYFHYBBX
      IYBVMXJYDXKYEXIYBAVNXIYBCUTVOVPVQVRVSVTWAWJWKWSXHUJWLXQWRXGEAWJWKWTWRXGUJ
      ZWJWPDKYIWKWTUHWPGBCDFWBADWPWCWDWEWFVTWJWKWOXNUJWLAHBCDFWGVTWHWI $.
      $( [11-Feb-2007] $)

    $( An open set is a neighborhood of any of its subsets.  (Contributed by
       FL, 2-Oct-2006.) $)
    opnneissb $p |- ( ( J e. Top /\ N e. J /\ S C_ X ) ->
                   ( S C_ N <-> N e. ( ( nei ` J ) ` S ) ) ) $=
      ( vg ctop wcel wss w3a cnei cfv wi wa cv wrex wb isnei ad2ant2r eltopss
      adantr ssid wceq sseq2 sseq1 anbi12d rcla4ev mpanr2 ad2ant2l mpbir2and
      exp43 3imp ssnei ex 3ad2ant1 impbid ) BGHZCBHZADIZJACIZCABKLLHZUQURUSUTVA
      MUQURUSUTVAUQURNZUSUTNZNVACDIZAFOZIZVECIZNZFBPZUQUSVAVDVINQURUTAFBCDERSVB
      VDVCCBDETUAURUTVIUQUSURUTCCIZVICUBVHUTVJNFCBVECUCVFUTVGVJVECAUDVECCUEUFUG
      UHUIUJUKULUQURVAUTMUSUQVAUTABCUMUNUOUP $.
      $( [12-Feb-2007] $)

    $( Any superset of an open set is a neighborhood of it. $)
    opnssneib $p |- ( ( J e. Top /\ S e. J /\ N C_ X ) ->
                   ( S C_ N <-> N e. ( ( nei ` J ) ` S ) ) ) $=
      ( vg ctop wcel wss w3a cnei cfv cv wa wrex wi simplr wceq sseq2 sseq1
      anbi12d ssid biantrur syl6bbr rcla4ev adantlr jca ex 3adant1 wb eltopss
      isnei syldan 3adant3 sylibrd ssnei 3ad2ant1 impbid ) BGHZABHZCDIZJZACIZCA
      BKLLHZVBVCVAAFMZIZVECIZNZFBOZNZVDUTVAVCVJPUSUTVANZVCVJVKVCNVAVIUTVAVCQUTV
      CVIVAVHVCFABVEARZVHAAIZVCNVCVLVFVMVGVCVEAASVEACTUAVMVCAUBUCUDUEUFUGUHUIUS
      UTVDVJUJZVAUSUTADIVNABDEUKAFBCDEULUMUNUOUSUTVDVCPVAUSVDVCABCUPUHUQUR $.
      $( [14-Feb-2007] $)

    $( Any subset of ` X ` containing a neigborhood of a set is a neighborhood
       of this set.  Based on Bourbaki TG I.3 Vi.  (Contributed by FL,
       2-Oct-2006.) $)
    ssnei2 $p |- ( ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) ) /\
                 ( N C_ M /\ M C_ X ) ) -> M e. ( ( nei ` J ) ` S ) ) $=
      ( vg ctop wcel cnei cfv wa wss cv wrex wb neiss2 isnei syldan adantr
      simprr sstr2 com12 anim2d reximdv impcom neii2 sylan adantrr mpbir2and )
      BHIZDABJKKZIZLZDCMZCEMZLZLCULIZUPAGNZMZUSCMZLZGBOZUNURUPVCLPZUQUKUMAEMVDA
      BDEFQAGBCEFRSTUNUOUPUAUNUOVCUPUTUSDMZLZGBOZUOVCUNUOVGVCUOVFVBGBUOVEVAUTVE
      UOVAUSDCUBUCUDUEUFAGBDUGUHUIUJ $.
      $( [13-Feb-2007] $)

    $( Any neighborhood of an element in the closure of a subset intersects the
       subset.  Part of proof of Theorem 6.6 of [Munkres] p. 97. $)
    neindisj $p |- ( ( ( J e. Top /\ S C_ X ) /\ ( P e. ( ( cls ` J ) ` S ) /\
                   N e. ( ( nei ` J ) ` { P } ) ) ) -> ( N i^i S ) =/= (/) ) $=
      ( vg ctop wcel wss ccl cfv csn cnei cin c0 wne wi wa cv wrex wb clsss3
      sseld impr isneip syldan w3a clsndisj 3anass sylanbr anassrs adantllr
      adantrr wceq ssdisj ex necon3d ad2antll mpd r19.23adva expimpd sylbid
      exp32 imp43 ) CHIZBEJZABCKLLZIZDAMCNLLIZDBOZPQZVFVGVIVJVLRVFVGVISZSZVJDEJ
      ZAGTZIZVPDJZSZGCUAZSZVLVFVMAEIZVJWAUBVFVGVIWBVFVGSVHEABCEFUCUDUEAGCDEFUFU
      GVNVOVTVLVNVOSZVSVLGCWCVPCIZSZVSVLWEVSSVPBOZPQZVLWEVQWGVRVNWDVQWGVOVNWDVQ
      WGVFVGVIUHWDVQSWGVNABVPCEFUIVFVGVIUJUKULUMUNVRWGVLRWEVQVRVKPWFPVRVKPUOWFP
      UOVPDBUPUQURUSUTUQVAVBVCVDVE $.
      $( [26-Feb-2007] $)
  $}

  $( An open set is a neighborhood of any of its subsets. $)
  opnneiss $p |- ( ( J e. Top /\ N e. J /\ S C_ N ) ->
                  N e. ( ( nei ` J ) ` S ) ) $=
    ( ctop wcel wss w3a cnei cfv simp3 cuni wb eqid opnneissb wa sstr ancoms
    eltopss sylan 3impa syld3an3 mpbid ) BDEZCBEZACFZGUECABHIIEZUCUDUEJUCUDABKZ
    FZUEUFLUEABCUGUGMZNUCUDUEUHCUGFZUEUHUCUDOUEUJUHACUGPQCBUGUIRSTUAUB $.
    $( [13-Feb-2007] $)

  $( An open set is a neighborhood of any of its members. $)
  opnneip $p |- ( ( J e. Top /\ N e. J /\ P e. N ) ->
                  N e. ( ( nei ` J ) ` { P } ) ) $=
    ( ctop wcel csn wss cnei cfv opnneiss snssi syl3an3 ) BDECBEAFZCGCMBHIIEACE
    MBCJACKL $.
    $( [8-Mar-2007] $)

  ${
    $d x J $.  $d x S $.
    $( A set is open iff it is a neighborhood of all its points.  ( Contributed
       by Jeff Hankins, 15-Sep-2009.) $)
    opnnei $p |- ( J e. Top -> ( S e. J <-> A. x e. S S e. ( (
    nei ` J ) ` { x } ) ) ) $=
      ( ctop wcel c0 wceq cv csn cnei cfv wral wb wa 0opn adantr eleq1 adantl
      mpbird rzal 2thd wn wi opnneip 3expia r19.21aiv ex cuni wss wrex wne
      df-ne r19.2z sylbir eqid neii1 a1d r19.23adv sylan9r cnt ntrss2 dfss3
      biimpri visset snss ralbii sylan2br eqssd sstr2 com12 syl5ib imp neiint
      3com23 3expa syldan ralbidva isopn3 3imtr4d com23 mpdd impbid pm2.61dan
      ) CDEZBFGZBCEZBAHZIZCJKKEZABLZMWDWENZWFWJWKWFFCEZWDWLWECOPWEWFWLMWDBFCQRS
      WEWJWDWIABTRUAWDWEUBZNZWFWJWDWFWJUCWMWDWFWJWDWFNWIABWDWFWGBEZWIWGCBUDUEUF
      UGPWNWJBCUHZUIZWFWMWJWIABUJZWDWQWMBFUKZWJWRUCBFULWSWJWRWIABUMUGUNWDWIWQAB
      WDWIWQUCWOWDWIWQWHCBWPWPUOZUPUGUQURUSWDWJWQWFUCUCWMWDWQWJWFWDWQWJWFUCWDWQ
      NZWHBCUTKKZUIZABLZXBBGZWJWFXAXDXEXAXDNXBBXAXBBUIXDBCWPWTVAPXAWGXBEZABLZBX
      BUIZXDXGXHXAXHXGABXBVBVCRXFXCABWGXBAVDZVEVFVGVHUGXAWIXCABXAWOWHWPUIZWIXCM
      ZXAWOXJXAWHBUIZXJWOWQXLXJUCWDXLWQXJWHBWPVIVJRWGBXIVEVKVLWDWQXJXKWDXJWQXKW
      HCBWPWTVMVNVOVPVQBCWPWTVRVSUGVTPWAWBWC $.
      $( [16-Sep-2009] $)
  $}

  ${
    $d J x $.  $d S x $.  $d X x $.
    tpnei.1 $e |- X = U. J $.
    $( The underlying set of a topology is a neighborhood of any of its
       subsets.  Special case of ~ opnneiss .  (Contributed by FL,
       2-Oct-2006.) $)
    tpnei $p |- ( J e. Top -> ( S C_ X <-> X e. ( ( nei ` J ) ` S ) ) ) $=
      ( ctop wcel wss cnei cfv wi topopn opnneiss 3exp mpd ssnei ex impbid ) BE
      FZACGZCABHIIFZRCBFZSTJBCDKRUASTABCLMNRTSABCOPQ $.
      $( [14-Feb-2007] $)

    $( The union of the neighborhoods of a set equals the topology's underlying
       set.  (Contributed by FL, 18-Sep-2007.) $)
    unnei $p |- ( ( J e. Top /\ S C_ X ) -> U. ( ( nei ` J ) ` S ) = X ) $=
      ( vx ctop wcel wss wa cnei cfv cuni wceq cv wral wi neii1 ex adantr
      r19.21aiv unissb sylibr tpnei biimpa unissel syl11anc ) BFGZACHZIZABJKKZL
      ZCHZCUJGZUKCMUIENZCHZEUJOULUIUOEUJUGUNUJGZUOPUHUGUPUOABUNCDQRSTEUJCUAUBUG
      UHUMABCDUCUDUJCUEUF $.
      $( [18-Sep-2007] $)
  $}

  ${
    $d g h v x y J $.  $d g h v M $.  $d g h v x y N $.  $d g h v x y S $.
    $( The intersection of two neighborhoods of a set is also a neighborhood of
       the set.  Based on Bourbaki TG I.3 Vii.  (Contributed by FL,
       28-Sep-2006.) $)
    innei $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S )
   /\ M e. ( ( nei ` J ) ` S ) ) -> ( N i^i M ) e. ( ( nei ` J ) ` S ) ) $=
      ( vg vh vv ctop wcel cnei cfv w3a cin cuni wss cv wa wrex wb eqid neiss2
      isnei syldan 3adant3 neii1 ssinss1 syl neii2 anim12i anandis wi wceq
      sseq2 sseq1 anbi12d rcla4ev inopn 3expa ssin biimpi ss2in an4s syl2an
      expr an32s r19.23adva ex imp32 3impb mpbir2and ) BHIZDABJKKZIZCVLIZLDCMZV
      LIZVOBNZOZAEPZOZVSVOOZQZEBRZVKVMVPVRWCQSZVNVKVMAVQOWDABDVQVQTZUAAEBVOVQWE
      UBUCUDVKVMVRVNVKVMQZDVQOVRABDVQWEUEDCVQUFUGUDVKVMVNWCVKVMVNQAFPZOZWGDOZQZ
      FBRZAGPZOZWLCOZQZGBRZQZWCVKVMVNWQWFWKVKVNQWPAFBDUHAGBCUHUIUJVKWKWPWCVKWJW
      PWCUKZFBVKWGBIZQZWJWRWTWJQWOWCGBWTWLBIZWJWOWCUKWTXAQZWJWOWCWGWLMZBIZAXCOZ
      XCVOOZQZWCXBWJWOQWBXGEXCBVSXCULVTXEWAXFVSXCAUMVSXCVOUNUOUPVKWSXAXDWGWLBUQ
      URWHWMWIWNXGWHWMQZXEWIWNQXFXHXEAWGWLUSUTWGDWLCVAUIVBVCVDVEVFVGVFVHUCVIVJ
      $.
      $( [12-Feb-2007] $)

    $( Only an open set is a neighborhood of itself.  (Contributed by FL,
       2-Oct-2006.) $)
    opnneiid $p |- ( J e. Top -> ( N e. ( ( nei ` J ) ` N ) <-> N e. J ) ) $=
      ( vx ctop wcel cnei cfv wa cv wss wrex neii2 wceq eleq1a eqss syl5ibr
      r19.23aiv syl ex ssid opnneiss 3exp mpii impbid ) ADEZBBAFGGEZBAEZUEUFUGU
      EUFHBCIZJUHBJHZCAKUGBCABLUIUGCAUHAEBUHMUGUIUHABNBUHOPQRSUEUGBBJZUFBTUEUGU
      JUFBABUAUBUCUD $.
      $( [13-Feb-2007] $)

    $( For any neighborhood ` N ` of ` S ` , there is a neighborhood ` x ` of
       ` S ` such that ` N ` is a neighborhood of all subsets of ` x ` .  Based
       on Bourbaki TG I.3 Viv.  (Contributed by FL, 2-Oct-2006.) $)
    neissex $p |- ( ( J e. Top /\ N e. ( ( nei ` J ) ` S ) ) ->
   E. x e. ( ( nei ` J ) ` S ) A. y ( y C_ x -> N e. ( ( nei ` J ) ` y ) ) ) $=
      ( ctop wcel cnei cfv wa cv wss wrex wi wal neii2 opnneiss 3expb adantrrr
      adantlr simplll cuni wb simpll simpr eqid neii1 adantr opnssneib
      syl111anc biimpa anasss neiss ex adantrrl 19.21aiv jca reximdv2 mpd ) DFG
      ZECDHIZIZGZJZCAKZLZVEELZJZADMBKZVELZEVIVAIGZNZBOZAVBMCADEPVDVHVMADVBVDVED
      GZVHJZVEVBGZVMJVDVOJZVPVMUTVOVPVCUTVNVFVPVGUTVNVFVPCDVEQRSTVQVLBVDVNVGVLV
      FVDVNVGJZJZVJVKVSVJJUTEVEVAIGZVJVKUTVCVRVJUAVSVTVJVDVNVGVTVDVNJZVGVTWAUTV
      NEDUBZLZVGVTUCUTVCVNUDVDVNUEVDWCVNCDEWBWBUFZUGUHVEDEWBWDUIUJUKULUHVSVJUEV
      IVEDEUMUJUNUOUPUQUNURUS $.
      $( [13-Feb-2007] $)
  $}

  $( The empty set is a neighborhood of itself.  (Contributed by FL,
     10-Dec-2006.) $)
  0nei $p |- ( J e. Top -> (/) e. ( ( nei ` J ) ` (/) ) ) $=
    ( ctop wcel c0 cnei cfv 0opn opnneiid mpbird ) ABCDDAEFFCDACAGADHI $.
    $( [14-Feb-2007] $)


