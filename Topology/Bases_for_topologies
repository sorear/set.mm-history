$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Bases for topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w x y z B $.
    $( Express the predicate " ` B ` is a basis for a topology." $)
    isbasisg $p |- ( B e. C -> ( B e. Bases <->
          A. x e. B A. y e. B ( x i^i y ) C_ U. ( B i^i ~P ( x i^i y ) ) ) ) $=
      ( vz cv cin cpw cuni wss wral ctb wceq ineq1 unieqd sseq2d raleqd
      df-bases elab2g ) AFBFGZEFZTHZGZIZJZBUAKZAUAKTCUBGZIZJZBCKZACKECLDUFUJAUA
      CUEUIBUACUACMZUDUHTUKUCUGUACUBNOPQQEABRS $.
      $( [18-Jul-2006] $) $( [17-Jul-2006] $)

    $( Express the predicate " ` B ` is a basis for a topology." $)
    isbasis2g $p |- ( B e. C -> ( B e. Bases <->
           A. x e. B A. y e. B A. z e. ( x i^i y )
              E. w e. B ( z e. w /\ w C_ ( x i^i y ) ) ) ) $=
      ( wcel ctb cv cin cpw cuni wss wral wa wrex isbasisg dfss3 wex elin
      df-pw abeq2i anbi2i bitri an12 exbii eluni df-rex 3bitr4i ralbii 2ralbii
      syl6bb ) EFGEHGAIBIJZEUMKZJZLZMZBENAENCIZDIZGZUSUMMZOZDEPZCUMNZBENAENABEF
      QUQVDABEEUQURUPGZCUMNVDCUMUPRVEVCCUMUTUSUOGZOZDSUSEGZVBOZDSVEVCVGVIDVGUTV
      HVAOZOVIVFVJUTVFVHUSUNGZOVJUSEUNTVKVAVHVADUNDUMUAUBUCUDUCUTVHVAUEUDUFDURU
      OUGVBDEUHUIUJUDUKUL $.
      $( [18-Jul-2006] $) $( [17-Jul-2006] $)

    $( Express the predicate " ` B ` is a basis for a topology."  Definition of
       basis in [Munkres] p. 78. $)
    isbasis3g $p |- ( B e. C -> ( B e. Bases <-> ( A. x e. B x C_ U. B /\
     A. x e. U. B E. y e. B x e. y /\ A. x e. B A. y e. B A. z e. ( x i^i y )
     E. w e. B ( z e. w /\ w C_ ( x i^i y ) ) ) ) ) $=
      ( wcel ctb cv cin wss wa wrex wral cuni w3a isbasis2g elssuni rgen
      eluni2 biimpi pm3.2i biantrur df-3an bitr4i syl6bb ) EFGEHGCIDIZGUGAIZBIZ
      JZKLDEMCUJNBENAENZUHEOZKZAENZUHUIGBEMZAULNZUKPZABCDEFQUKUNUPLZUKLUQURUKUN
      UPUMAEUHERSUOAULUHULGUOBUHETUASUBUCUNUPUKUDUEUF $.
      $( [18-Jul-2006] $) $( [17-Jul-2006] $)
  $}

  ${
    $d w x A $.  $d w x y z B $.  $d w x y z C $.  $d w x y z D $.
    $( Property of a basis. $)
    basis1 $p |- ( ( B e. Bases /\ C e. B /\ D e. B ) ->
                    ( C i^i D ) C_ U. ( B i^i ~P ( C i^i D ) ) ) $=
      ( vx vy ctb wcel cin cpw cuni wss wa cv wral wceq ineq1 pweq syl ineq2d
      unieqd sseq12d ineq2 rcla42v isbasisg ibi syl5com 3impib ) AFGZBAGZCAGZBC
      HZAUKIZHZJZKZUIUJLDMZEMZHZAURIZHZJZKZEANDANZUOUHVBUOBUQHZAVDIZHZJZKDEBCAA
      UPBOZURVDVAVGUPBUQPZVHUTVFVHUSVEAVHURVDOUSVEOVIURVDQRSTUAUQCOZVDUKVGUNUQC
      BUBZVJVFUMVJVEULAVJVDUKOVEULOVKVDUKQRSTUAUCUHVCDEAFUDUEUFUG $.
      $( [21-Jul-2006] $) $( [16-Jul-2006] $)

    $( Property of a basis. $)
    basis2 $p |- ( ( ( B e. Bases /\ C e. B ) /\ ( D e. B /\
          A e. ( C i^i D ) ) ) -> E. x e. B ( A e. x /\ x C_ ( C i^i D ) ) ) $=
      ( vw vy vz ctb wcel cin cv wss wa wrex wi wral isbasis2g ibi wceq wb
      ineq1 sseq2 anbi2d rexbidv raleqd syl ineq2 rcla42v eleq1 anbi1d rcla4cv
      syl6com exp3a imp43 ) CIJZDCJZECJZBDEKZJZBALZJZVAUSMZNZACOZUPUQURUTVEPZUP
      FLZVAJZVAGLZHLZKZMZNZACOZFVKQZHCQGCQZUQURNZVFPUPVPGHFACIRSVQVPVHVCNZACOZF
      USQZVFVOVTVHVADVJKZMZNZACOZFWAQZGHDECCVIDTVKWATZVOWEUAVIDVJUBVNWDFVKWAWFV
      MWCACWFVLWBVHVKWAVAUCUDUEUFUGVJETWAUSTZWEVTUAVJEDUHWDVSFWAUSWGWCVRACWGWBV
      CVHWAUSVAUCUDUEUFUGUIVSVEFBUSVGBTZVRVDACWHVHVBVCVGBVAUJUKUEULUMUGUNUO $.
      $( [24-Jul-2006] $) $( [17-Jul-2006] $)
  $}

  ${
    $d B w x y z $.  $d C w x y z $.
    $( If a set is closed under finite intersection, then it is a basis for a
       topology.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    fiinbas $p |- ( ( B e. C /\ A. x e. B A. y e. B ( x i^i y ) e. B )
                       -> B e. Bases ) $=
      ( vz vw wcel cv cin wral ctb wss wa wrex wi ssid wceq eleq2 sseq1
      anbi12d rcla4ev mpanr2 r19.21aiva a1i r19.20sdv isbasis2g sylibrd imp )
      CDGZAHBHIZCGZBCJZACJZCKGZUIUMEHZFHZGZUPUJLZMZFCNZEUJJZBCJZACJUNUIULVBACUI
      UKVABCUKVAOUIUKUTEUJUKUOUJGZUJUJLZUTUJPUSVCVDMFUJCUPUJQUQVCURVDUPUJUORUPU
      JUJSTUAUBUCUDUEUEABEFCDUFUGUH $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d y x z A $.  $d t u v w x y z B $.  $d x y C $.  $d w x y z J $.
    $( The topology generated by a basis. $)
    tgval $p |- ( B e. Bases -> ( topGen ` B ) =
                  { x | x C_ U. ( B i^i ~P x ) } ) $=
      ( vy vz ctb wcel cv cpw cin cuni wss cab cvv ctg cfv wceq wa uniexg
      abssexg uniin sstr mpan2 ssin sylibr ss2abi ssexg mpan 3syl ineq1 unieqd
      sseq2d abbidv df-topgen fvopab4g mpdan ) BEFZAGZBUQHZIZJZKZALZMFZBNOVBPUP
      BJZMFUQVDKUQURJZKZQZALZMFZVCBERVFAVDMSVBVHKVIVCVAVGAVAUQVDVEIZKZVGVAUTVJK
      VKBURTUQUTVJUAUBUQVDVEUCUDUEVBVHMUFUGUHCDBUQCGZURIZJZKZALVBEMNVLBPZVOVAAV
      PVNUTUQVPVMUSVLBURUIUJUKULCDAUMUNUO $.
      $( [19-Jul-2006] $) $( [16-Jul-2006] $)

    $( Definition of a topology generated by a basis in [Munkres] p. 78.  Later
       we show (in ~ tgcl ) that ` ( topGen `` B ) ` is indeed a topology (on
       ` U. B ` ; see ~ unitg ). $)
    tgval2 $p |- ( B e. Bases -> ( topGen ` B ) =
         { x | ( x C_ U. B /\ A. y e. x E. z e. B ( y e. z /\ z C_ x ) ) } ) $=
      ( ctb wcel ctg cfv cv cpw cin cuni wss cab wa wrex wral tgval dfss3
      inss1 uniss ax-mp sseli pm4.71ri ralbii r19.26 3bitri wex elin anbi2i
      an12 bitri exbii eluni df-rex 3bitr4i visset elpw rexbii bitr2i anbi12i
      bitr4i abbii syl6eq ) DEFDGHAIZDVEJZKZLZMZANVEDLZMZBIZCIZFZVMVEMZOZCDPZBV
      EQZOZANADRVIVSAVIVLVJFZBVEQZVLVHFZBVEQZOZVSVIWCVTWBOZBVEQWDBVEVHSWBWEBVEW
      BVTVHVJVLVGDMVHVJMDVFTVGDUAUBUCUDUEVTWBBVEUFUGVKWAVRWCBVEVJSVQWBBVEWBVNVM
      VFFZOZCDPZVQVNVMVGFZOZCUHVMDFZWGOZCUHWBWHWJWLCWJVNWKWFOZOWLWIWMVNVMDVFUIU
      JVNWKWFUKULUMCVLVGUNWGCDUOUPWGVPCDWFVOVNVMVECUQURUJUSUTUEVAVBVCVD $.
      $( [19-Jul-2006] $) $( [15-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg $p |- ( B e. Bases -> ( A e. ( topGen ` B ) <->
           A C_ U. ( B i^i ~P A ) ) ) $=
      ( vx ctb wcel ctg cfv cv cpw cin cuni wss cab tgval eleq2d cvv elisset
      adantl ssexg inex1g uniexg syl sylan2 ancoms wceq id pweq ineq2d unieqd
      sseq12d elabg pm5.21nd bitrd ) BDEZABFGZEACHZBUPIZJZKZLZCMZEZABAIZJZKZLZU
      NUOVAACBNOUNVBVFAPEZVBVGUNAVAQRVFUNVGVFVEPEZVGUNAVEPSUNVDPEVHBVCDTVDPUAUB
      UCUDUTVFCAPUPAUEZUPAUSVEVIUFVIURVDVIUQVCBUPAUGUHUIUJUKULUM $.
      $( [21-Jul-2006] $) $( [16-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg2 $p |- ( B e. Bases -> ( A e. ( topGen ` B ) <->
           ( A C_ U. B /\ A. x e. A E. y e. B ( x e. y /\ y C_ A ) ) ) ) $=
      ( vz ctb wcel ctg cfv cv cuni wss wa wrex wral cab tgval2 eleq2d cvv
      elisset adantl ssexg uniexg sylan2 ancoms adantrr wceq sseq1 sseq2
      anbi2d rexbidv raleqd anbi12d elabg pm5.21nd bitrd ) DFGZCDHIZGCEJZDKZLZA
      JBJZGZVBUSLZMZBDNZAUSOZMZEPZGZCUTLZVCVBCLZMZBDNZACOZMZUQURVICEABDQRUQVJVP
      CSGZVJVQUQCVITUAUQVKVQVOVKUQVQVKUTSGVQUQCUTSUBDFUCUDUEUFVHVPECSUSCUGZVAVK
      VGVOUSCUTUHVFVNAUSCVRVEVMBDVRVDVLVCUSCVBUIUJUKULUMUNUOUP $.
      $( [25-Jul-2006] $) $( [15-Jul-2006] $)

    $( Property of a member of a topology generated by a basis. $)
    tg1 $p |- ( ( B e. Bases /\ A e. ( topGen ` B ) ) ->
              A C_ U. B ) $=
      ( vx vy ctb wcel ctg cfv cuni wss cv wa wrex wral eltg2 pm3.26bda ) BEFAB
      GHFABIJCKDKZFQAJLDBMCANCDABOP $.
      $( [26-Jul-2006] $) $( [20-Jul-2006] $)

    $( Property of a member of a topology generated by a basis. $)
    tg2 $p |- ( ( B e. Bases /\ A e. ( topGen ` B ) /\ C e. A ) ->
               E. x e. B ( C e. x /\ x C_ A ) ) $=
      ( vy ctb wcel ctg cfv cv wss wa wrex cuni wral wi eltg2 wceq eleq1
      anbi1d rexbidv rcla4cv adantl syl6bi 3imp ) CFGZBCHIGZDBGZDAJZGZUIBKZLZAC
      MZUFUGBCNKZEJZUIGZUKLZACMZEBOZLUHUMPZEABCQUSUTUNURUMEDBUODRZUQULACVAUPUJU
      KUODUISTUAUBUCUDUE $.
      $( [26-Jul-2006] $) $( [20-Jul-2006] $)

    $( A member of a basis is a subset of the topology it generates. $)
    bastg $p |- ( B e. Bases -> B C_ ( topGen ` B ) ) $=
      ( vx ctb wcel ctg cfv cv cpw cin cuni wss wa basis1 3anidm23 inidm wceq
      pweq ax-mp ineq2i unieqi 3sstr3g ex eltg sylibrd ssrdv ) ACDZBAAEFZUFBGZA
      DZUHAUHHZIZJZKZUHUGDUFUIUMUFUILUHUHIZAUNHZIZJZUHULUFUIUNUQKAUHUHMNUHOZUPU
      KUOUJAUNUHPUOUJPURUNUHQRSTUAUBUHAUCUDUE $.
      $( [23-Jul-2006] $) $( [16-Jul-2006] $)

    $( The topology generated by a basis ` B ` is a topology on ` U. B ` .
       Importantly, this theorem means that we don't have to specify separately
       the base set for the topological space generated by a basis.  In other
       words, any member of the class ` Bases ` completely specifies the basis
       it corresponds to. $)
    unitg $p |- ( B e. Bases -> U. ( topGen ` B ) = U. B ) $=
      ( vx vy ctb wcel ctg cfv cuni cv wrex cpw cin wss wi eltg inss1 uniss
      ax-mp sstr mpan2 sseld syl6bi r19.23adv eluni2 syl5ib bastg syl impbid
      eqrdv ) ADEZBAFGZHZAHZUJBIZULEZUNUMEZUJUNCIZEZCUKJUPUOUJURUPCUKUJUQUKEUQA
      UQKZLZHZMZURUPNUQAOVBUQUMUNVBVAUMMZUQUMMUTAMVCAUSPUTAQRUQVAUMSTUAUBUCCUNU
      KUDUEUJUMULUNUJAUKMUMULMAUFAUKQUGUAUHUI $.
      $( [24-Jul-2006] $) $( [16-Jul-2006] $)

    $( Show that a basis generates a topology.  Remark in [Munkres] p. 79. $)
    tgcl $p |- ( B e. Bases -> ( topGen ` B ) e. Top ) $=
      ( vu vv vx vy vt vz vw ctg cfv ctop wcel cv wss cuni wi wal cin wral ctb
      cvv wa wb fvex istopg ax-mp wrex uniss adantl wceq unitg adantr sseqtrd
      eltg2 pm3.27 syl6bi ra4 syl6 imp31 an1rs ssel2 sylan2 an42s sstr2
      elssuni syl5com anim2d r19.22sdv ad2antrl mpd exp32 r19.23adv eluni2
      syl5ib r19.21aiv jca ex sylibrd 19.21aiv tg1 ssinss1 syl adantrr
      pm3.27bda im2anan9 elin reeanv 3imtr4g anandis basis2 adantllr adantrrr
      com12 ad2antll biimpri ss2in anim12i an4s sylanr2 r19.23adva a2d imp
      syldan r19.21aivv sylanbrc ) AIJZKLZBMZXFNZXHOZXFLZPZBQZXHCMZRZXFLZCXFSBX
      FSZATLZXFUALXGXMXQUBUCAIUDBCUAXFUEUFXRXLBXRXIXJAOZNZDMZEMZLZYBXJNZUBZEAUG
      ZDXJSZUBZXKXRXIYHXRXIUBZXTYGYIXJXFOZXSXIXJYJNXRXHXFUHUIXRYJXSUJXIAUKULUMY
      IYFDXJYIYAFMZLZFXHUGYFYAXJLYIYLYFFXHYIYKXHLZYLYFYIYMYLUBUBYCYBYKNZUBZEAUG
      ZYFXRYLXIYMYPXRYLUBYKXFLZYPXIYMUBXRYQYLYPXRYQYLYPXRYQYPDYKSZYLYPPXRYQYKXS
      NZYRUBYRDEYKAUNYSYRUOUPYPDYKUQURUSUTXHXFYKVAVBVCYMYPYFPYIYLYMYOYEEAYMYNYD
      YCYNYKXJNYDYMYBYKXJVDYKXHVEVFVGVHVIVJVKVLFYAXHVMVNVOVPVQDEXJAUNVRVSXRXPBC
      XFXFXRXHXFLZXNXFLZUBZXOXSNZYLYKXONZUBZFAUGZDXOSZUBZXPXRUUBUUHXRUUBUBZUUCU
      UGXRYTUUCUUAXRYTUBZXHXSNZUUCXHAVTXHXNXSWAWBWCUUIUUFDXOXRUUBYAXOLZYAGMZLZU
      UMXHNZUBZYAHMZLZUUQXNNZUBZUBZHAUGZGAUGZPZUULUUFPZXRYTUUAUVDUUJXRUUAUBZUBY
      AXHLZYAXNLZUBUUPGAUGZUUTHAUGZUBUULUVCUUJUVGUVIUVFUVHUVJUUJUVIDXHSZUVGUVIP
      XRYTUUKUVKDGXHAUNWDUVIDXHUQWBUVFUVJDXNSZUVHUVJPXRUUAXNXSNUVLDHXNAUNWDUVJD
      XNUQWBWEYAXHXNWFUUPUUTGHAAWGWHWIXRUVDUVEXRUULUVCUUFXRUULUVCUUFPXRUULUBZUV
      BUUFGAUVMUUMALZUBZUVAUUFHAUVOUUQALZUVAUUFUVOUVPYAUUMUUQRZLZUVQXONZUBZUUFU
      VAUVOUVPUVTUBUBYLYKUVQNZUBZFAUGZUUFUVOUVPUVRUWCUVSXRUVNUVPUVRUBUWCUULFYAA
      UUMUUQWJWKWLUVTUWCUUFPZUVOUVPUVSUWDUVRUVSUWBUUEFAUVSUWAUUDYLUWAUVSUUDYKUV
      QXOVDWMVGVHUIWNVJUUNUURUUOUUSUVTUUNUURUBZUVRUUOUUSUBUVSUVRUWEYAUUMUUQWFWO
      UUMXHUUQXNWPWQWRWSVKVLWTVQXAXBXCVOVPVQDFXOAUNVRXDXE $.
      $( [25-Jul-2006] $) $( [17-Jul-2006] $)

    $( Alternate expression for the topology generated by a basis.  Lemma 2.1
       of [Munkres] p. 80. $)
    tgval3 $p |- ( B e. Bases ->
               ( topGen ` B ) = { x | E. y ( y C_ B /\ x = U. y ) } ) $=
      ( vz vw ctb wcel cv wss cuni wceq wa wex ctg cfv cab cvv wi visset
      abssexg ax-mp hbab1 ax-17 hbss hbuni hbeq hban sseq1 unieq eqeq2d
      anbi12d cla4egf pm3.27 abssi wrex wral eltg2 pm3.27bda elequ1 anbi1d
      rexbidv rcla4cv df-rex an12 ancom anbi2i bitri exbii syl6ib syl ssel
      impcom adantrr 19.23aiv impbid1 eluniab syl6bbr eqrdv sylancr ex simprr
      ctop uniopn tgcl adantr sstr bastg sylan2 ancoms sylanc eqeltrd 19.23adv
      impbid abbi2dv ) CFGZBHZCIZAHZWPJZKZLZBMZACNOZWOWRXCGZXBWOXDXBWPWRIZWPCGZ
      LZBPZCIZWRXHJZKZXBWOXDLZXHQGZXIXKLZXBRWRQGXMASXFBWRQTUAXAXNBDXHQXGBDUBZXI
      XKBBDXHCXODHZCGBUCUDBDDWRXJXPWRGZBUCBDXHXOUEUFUGWPXHKZWQXIWTXKWPXHCUHXRWS
      XJWRWPXHUIUJUKULUAXGBCXEXFUMUNXLDWRXJXLXQXPWPGZXGLZBMZXPXJGXLXQYAXLEHZWPG
      ZXELZBCUOZEWRUPZXQYARWOXDWRCJIYFEBWRCUQURYFXQXSXELZBCUOZYAYEYHEXPWRYBXPKZ
      YDYGBCYIYCXSXEEDBUSUTVAVBYHXFYGLZBMYAYGBCVCYJXTBYJXSXFXELZLXTXFXSXEVDYKXG
      XSXFXEVEVFVGVHVGVIVJXTXQBXSXEXQXFXEXSXQWPWRXPVKVLVMVNVOXGBXPVPVQVRVSVTWOX
      AXDBWOXAXDWOXALWRWSXCWOWQWTWAWOWQWSXCGZWTXCWBGZWPXCIZYLWOWQLWPXCWCWOYMWQC
      WDWEWQWOYNWQCXCIYNWOWPCXCWFCWGWHWIWJVMWKVTWLWMWN $.
      $( [25-Jul-2006] $) $( [17-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg3 $p |- ( B e. Bases -> ( A e. ( topGen ` B ) <->
           E. x ( x C_ B /\ A = U. x ) ) ) $=
      ( vy ctb wcel ctg cfv cv wss cuni wceq wa wex cab tgval3 eleq2d cvv
      visset uniex eleq1 mpbiri adantl 19.23aiv eqeq1 anbi2d exbidv elab3
      syl6bb ) CEFZBCGHZFBAIZCJZDIZULKZLZMZANZDOZFUMBUOLZMZANZUJUKUSBDACPQURVBD
      BVABRFZAUTVCUMUTVCUORFULASTBUORUAUBUCUDUNBLZUQVAAVDUPUTUMUNBUOUEUFUGUHUI
      $.
      $( [26-Jul-2006] $) $( [15-Jul-2006] $)

    $( A topology is its own basis. $)
    topbas $p |- ( J e. Top -> J e. Bases ) $=
      ( vz vw vx vy ctop wcel ctb cv cin wss wa wrex wral wceq eleq2 sseq1
      anbi12d rcla4ev inopn 3expb adantr pm3.27 ssid jctir sylanc exp31
      r19.21adv r19.21aivv isbasis2g mpbird ) AFGZAHGBIZCIZGZUNDIZEIZJZKZLZCAMZ
      BURNZEANDANULVBDEAAULUPAGZUQAGZLZVABURULVEUMURGZVAURAGZVFURURKZLZVAULVELZ
      VFLZUTVICURAUNUROUOVFUSVHUNURUMPUNURURQRSVJVGVFULVCVDVGUPUQATUAUBVKVFVHVJ
      VFUCURUDUEUFUGUHUIDEBCAFUJUK $.
      $( [22-Jul-2006] $) $( [17-Jul-2006] $)

    $( A topology is its own basis. $)
    tgtop $p |- ( J e. Top -> ( topGen ` J ) = J ) $=
      ( vx vy ctop wcel ctg cfv cv wss cuni wceq wa wex ctb wb topbas eltg3
      syl pm3.27 uniopn adantr eqeltrd expl 19.23adv sylbid ssrdv bastg eqssd
      ) ADEZAFGZAUIBUJAUIBHZUJEZCHZAIZUKUMJZKZLZCMZUKAEZUIANEZULUROAPZCUKAQRUIU
      QUSCUIUNUPUSUIUNLZUPLUKUOAVBUPSVBUOAEUPUMATUAUBUCUDUEUFUIUTAUJIVAAUGRUH
      $.
      $( [27-Jul-2006] $) $( [18-Jul-2006] $)

    $( Membership in a topology, expressed without quantifiers. $)
    eltop $p |- ( J e. Top -> ( A e. J <-> A C_ U. ( J i^i ~P A ) ) ) $=
      ( ctop wcel ctg cfv cpw cin cuni wss tgtop eleq2d ctb wb topbas eltg syl
      bitr3d ) BCDZABEFZDZABDABAGHIJZSTBABKLSBMDUAUBNBOABPQR $.
      $( [2-Jan-2007] $) $( [19-Jul-2006] $)

    $( Membership in a topology. $)
    eltop2 $p |- ( J e. Top -> ( A e. J <->
               ( A C_ U. J /\ A. x e. A E. y e. J ( x e. y /\ y C_ A ) ) ) ) $=
      ( ctop wcel ctg cfv cuni wss cv wa wrex wral tgtop eleq2d ctb wb topbas
      eltg2 syl bitr3d ) DEFZCDGHZFZCDFCDIJAKBKZFUFCJLBDMACNLZUCUDDCDOPUCDQFUEU
      GRDSABCDTUAUB $.
      $( [23-Aug-2007] $) $( [19-Jul-2006] $)

    $( Membership in a topology. $)
    eltop3 $p |- ( J e. Top -> ( A e. J <-> E. x ( x C_ J /\ A = U. x ) ) ) $=
      ( ctop wcel ctg cfv cv wss cuni wceq wa wex tgtop eleq2d ctb wb topbas
      eltg3 syl bitr3d ) CDEZBCFGZEZBCEAHZCIBUEJKLAMZUBUCCBCNOUBCPEUDUFQCRABCST
      UA $.
      $( [29-Mar-2008] $) $( [19-Jul-2006] $)
  $}

  $( The topology generator function is idempotent. $)
  tgidm $p |- ( B e. Bases ->
               ( topGen ` ( topGen ` B ) ) = ( topGen ` B ) ) $=
    ( ctb wcel ctg cfv ctop wceq tgcl tgtop syl ) ABCADEZFCKDEKGAHKIJ $.
    $( [9-Jan-2007] $) $( [18-Jul-2006] $)

  $( Two ways to express that a basis is a topology. $)
  bastop $p |- ( B e. Bases -> ( B e. Top <-> ( topGen ` B ) = B ) ) $=
    ( ctb wcel ctop ctg cfv wceq tgtop wa pm3.27 tgcl adantr eqeltrrd ex
    impbid2 ) ABCZADCZAEFZAGZAHPSQPSIRADPSJPRDCSAKLMNO $.
    $( [14-Aug-2007] $) $( [18-Jul-2006] $)

  $( The topology generation function is one-to-one when applied to completed
     topologies. $)
  tgtop11 $p |- ( ( J e. Top /\ K e. Top /\ ( topGen ` J ) = ( topGen ` K ) )
                 -> J = K ) $=
    ( ctop wcel ctg cfv wceq w3a 3simp3 tgtop 3ad2ant1 3ad2ant2 3eqtr3d ) ACDZB
    CDZAEFZBEFZGZHPQABNORINOPAGRAJKONQBGRBJLM $.
    $( [11-Jun-2007] $) $( [18-Jul-2006] $)

  $( The singleton of the empty set is the only topology possible for an empty
     underlying set. $)
  0top $p |- ( J e. Top -> ( U. J = (/) <-> J = { (/) } ) ) $=
    ( ctop wcel c0 csn wceq wo cuni olc wn 0opn n0i syl pm2.21d idd jaod
    impbid2 wss uni0b sssn bitr2i syl6rbb ) ABCZADEZFZADFZUEGZAHDFZUCUEUGUEUFIU
    CUFUEUEUCUFUEUCDACUFJAKADLMNUCUEOPQUHAUDRUGASADTUAUB $.
    $( [24-Mar-2008] $) $( [9-Sep-2006] $)

  ${
    $d u v w x y z B $.  $d u v w x y z C $.  $d v w x y z J $.
    $( Subset relation for generated topologies. $)
    tgss $p |- ( ( B e. Bases /\ C e. Bases /\ B C_ C ) ->
               ( topGen ` B ) C_ ( topGen ` C ) ) $=
      ( vx ctb wcel wss w3a ctg cfv cv cpw cin cuni wi sstr2 ssrin uniss syl
      syl5com 3ad2ant3 wb eltg 3ad2ant1 3ad2ant2 3imtr4d ssrdv ) ADEZBDEZABFZGZ
      CAHIZBHIZUJCJZAUMKZLZMZFZUMBUNLZMZFZUMUKEZUMULEZUIUGUQUTNUHUQUPUSFZUTUIUM
      UPUSOUIUOURFVCABUNPUOURQRSTUGUHVAUQUAUIUMAUBUCUHUGVBUTUAUIUMBUBUDUEUF $.
      $( [10-May-2007] $) $( [7-May-2007] $)

    $( A criterion for determining whether one topology is finer than another,
       based on a comparison of their bases.  Lemma 2.2 of [Munkres] p. 80. $)
    tgss2 $p |- ( ( B e. Bases /\ C e. Bases /\ U. B = U. C ) ->
                    ( ( topGen ` B ) C_ ( topGen ` C ) <->
     A. x e. U. B A. y e. B ( x e. y -> E. z e. C ( x e. z /\ z C_ y ) ) ) ) $=
      ( vu vw vv ctb wcel cuni wceq w3a ctg cfv wss cv wa wrex wi wral tg2
      simpllr sstr bastg sylan sseld imp adantllr adantrl adantrr simprr
      syl3anc exp43 r19.21advv 3adant3 eltg2 tg1 adantll simpll sseqtrd
      elssuni elequ1 anbi1d rexbidv imbi12d elequ2 sseq2 anbi2d rcla42v ex
      com4l imp32 adantrrr mpd an1s sstr2 com12 anim2d r19.22sdv adantl
      ad2antll exp32 r19.23adv 3expa syl5 exp4c imp42 r19.21aiva jca syl5bir
      3imp 3comr expdimp ssrdv impbid ) DIJZEIJZDKZEKZLZMZDNOZENOZPZAQZBQZJZXFC
      QZJZXIXGPZRZCESZTZBDUAAWSUAZWQWRXEXOTXAWQWRRZXEXNABWSDXPXEXFWSJZXGDJZRZXH
      XMWRXGXDJZXHXMXPXERZXSXHRZRCXGEXFUBWQWRXEYBUCYAXSXTXHYAXRXTXQWQXEXRXTWRWQ
      XERZXRXTYCDXDXGDXCPXEDXDPWQDXCXDUDDUEUFUGUHUIUJUKYAXSXHULUMUNUOUPXBXOXEXB
      XORFXCXDXBXOFQZXCJZYDXDJZWRXAWQXOYERZYFTZWRXAWQYHWRXAWQYGYFWRYFYDWTPZGQZX
      IJZXIYDPZRZCESZGYDUAZRXAWQRZYGRZGCYDEUQYQYIYOYPYEYIXOYPYERYDWSWTWQYEYDWSP
      XAYDDURUSXAWQYEUTVAUJWQYGYOXAWQYGRYNGYDWQXOYEYJYDJZYNXOWQYEYRYNTTXOWQYEYR
      YNXOYJHQZJZYSYDPZRZHDSZYNWQYERYRRXOUUBYNHDXOYSDJZUUBYNXOUUDUUBRRYKXIYSPZR
      ZCESZYNUUDXOUUBUUGUUDXOUUBRRYJWSJZUUGUUDUUBUUHXOUUDYTUUHUUAUUDYTUUHUUDYSW
      SYJYSDVBUGUHUKUJUUDXOYTUUHUUGTZUUAUUDXOYTUUIUUHUUDXOYTUUGUUHUUDXOYTUUGTZT
      XNUUJYJXGJZYKXKRZCESZTABYJYSWSDXFYJLZXHUUKXMUUMAGBVCUUNXLUULCEUUNXJYKXKAG
      CVCVDVEVFXGYSLZUUKYTUUMUUGBHGVGUUOUULUUFCEUUOXKUUEYKXGYSXIVHVIVEVFVJVKVLV
      MVNVOVPUUBUUGYNTZXOUUDUUAUUPYTUUAUUFYMCEUUAUUEYLYKUUEUUAYLXIYSYDVQVRVSVTW
      AWBVOWCWDWQYEYRUUCHYDDYJUBWEWFWGVRWHWIUSWJWKWGWLWMWNWOVKWP $.
      $( [27-Jul-2006] $) $( [20-Jul-2006] $)

    $( A criterion for determining whether one topology is finer than another.
       Lemma 2.2 of [Munkres] p. 80 using abbreviations. $)
    tgss3 $p |- ( ( B e. Bases /\ C e. Bases /\ U. B = U. C ) ->
                    ( ( topGen ` B ) C_ ( topGen ` C ) <->
                  A. x e. B ( U. B i^i x ) C_ U. ( C i^i ~P x ) ) ) $=
      ( vy vz ctb wcel cuni wceq w3a ctg cfv wss cv wa wrex wi wral cin cpw
      tgss2 ralcom wal elin wex anass anbi1i ancom bitr3i visset elpw anbi2i
      bitri 3bitr3ri exbii df-rex eluni 3bitr4ri imbi12i impexp albii dfss2
      df-ral ralbii syl6bb ) BFGCFGBHZCHIJBKLCKLMDNZANZGZVGENZGZVJVHMZOZECPZQZA
      BRDVFRZVFVHSZCVHTZSZHZMZABRZDAEBCUAVPVODVFRZABRWBVODAVFBUBWCWAABVGVQGZVGV
      TGZQZDUCVGVFGZVOQZDUCWAWCWFWHDWFWGVIOZVNQWHWDWIWEVNVGVFVHUDVJCGZVMOZEUEVK
      VJVSGZOZEUEVNWEWKWMEWJVJVRGZOZVKOZWJWNVKOZOWMWKWJWNVKUFWPWLVKOWMWLWOVKVJC
      VRUDUGWLVKUHUIWQVMWJWQVKWNOVMWNVKUHWNVLVKVJVHEUJUKULUMULUNUOVMECUPEVGVSUQ
      URUSWGVIVNUTUMVADVQVTVBVODVFVCURVDUMVE $.
      $( [3-Apr-2008] $) $( [20-Jul-2006] $)

    $( Given a topology ` J ` , show that a subset ` B ` satisfying the third
       antecedent is a basis for it.  Lemma 2.3 of [Munkres] p. 81. $)
    basgen2 $p |- ( ( J e. Top /\ B C_ J /\ A. x e. J A. y e. x E. z e. B
            ( y e. z /\ z C_ x ) ) -> ( B e. Bases /\ ( topGen ` B ) = J ) ) $=
      ( vw vv ctop wcel wss cv wa wrex wral ctb ctg cfv wceq cin wi inopn
      3expb ssel anim12d imp sylan2 anassrs sseq2 anbi2d rexbidv raleqd rcla4v
      syl r19.21advva cvv wb ssexg ancoms isbasis2g sylibrd cuni wex eltg3
      adantl pm3.27 uniopn sstr adantr eqeltrd exp42 com23 imp4b 19.23adv
      sylbid ssrdv syldan tgtop ad2antrr tgss2 3expa biimpar topbas eqid
      topopn raleq1 elequ1 rcla4cv eluni2 syl6ibr syl6 pm3.26 r19.22si
      r19.20si syl5 adantlr uniss ad2antlr eqssd jca31 ax-1 r19.20i2 ralcom
      sylib sylanc eqsstr3d jca 3impa ) EHIZDEJZBKZCKZIZXKAKZJZLZCDMZBXMNZAENZD
      OIZDPQZERZLXHXILZXRLZXSYAYBXRXSYBXRXLXKFKZGKZSZJZLZCDMZBYFNZGDNFDNZXSYBXR
      YJFGDDYBYDDIZYEDIZLZLYFEIZXRYJTXHXIYNYOXHYDEIZYEEIZLZYOXIYNLXHYPYQYOYDYEE
      UAUBXIYNYRXIYLYPYMYQDEYDUCDEYEUCUDUEUFUGXQYJAYFEXPYIBXMYFXMYFRZXOYHCDYSXN
      YGXLXMYFXKUHUIUJUKULUMUNYBDUOIZXSYKUPXIXHYTDEHUQURFGBCDUOUSUMUTUEZYCXTEYB
      XRXSXTEJUUAYBXSLZAXTEUUBXMXTIZXJDJZXMXJVAZRZLZBVBZXMEIZXSUUCUUHUPYBBXMDVC
      VDYBUUHUUITXSYBUUGUUIBXHXIUUDUUFUUIXHUUDXIUUFUUITXHUUDXIUUFUUIXHUUDXILZLZ
      UUFLXMUUEEUUKUUFVEUUKUUEEIZUUFXHXJEJUULUUJXJEVFXJDEVGUFVHVIVJVKVLVMVHVNVO
      VPYCEEPQZXTXHUUMERXIXREVQVREOIZXSLEVAZDVAZRZLZXJXMIXPTZAENBUUONZUUMXTJZYC
      UURUVAUUTUUNXSUUQUVAUUTUPBACEDVSVTWAYCUUNXSUUQXHUUNXIXREWBVRUUAYCUUOUUPXH
      XRUUOUUPJXIXHXRLFUUOUUPXHXRYDUUOIZYDUUPIZTZXHUUOEIZXRUVDTEUUOUUOWCWDUVEXL
      CDMZBXMNZAENZUVDXRUVEUVHUVFBUUONZUVDUVGUVIAUUOEUVFBXMUUOWEULUVIUVBYDXKIZC
      DMZUVCUVFUVKBYDUUOXJYDRXLUVJCDBFCWFUJWGCYDDWHWIWJXQUVGAEXPUVFBXMXOXLCDXLX
      NWKWLWMWMWNUMUEVOWOXIUUPUUOJXHXRDEWPWQWRWSXRUUTYBXRUUSBUUONZAENUUTXQUVLAE
      XPUUSBXMUUOUUSXJUUOIWTXAWMUUSABEUUOXBXCVDXDXEWRXFXG $.
      $( [27-Jul-2006] $) $( [20-Jul-2006] $)

    $( Given a topology ` J ` , show that a subset ` B ` satisfying the third
       antecedent is a basis for it.  Lemma 2.3 of [Munkres] p. 81 using
       abbreviations. $)
    basgen $p |- ( ( J e. Top /\ B C_ J /\ A. x e. J
          x C_ U. ( B i^i ~P x ) ) -> ( B e. Bases /\ ( topGen ` B ) = J ) ) $=
      ( vy vz ctop wcel wss cv wa wrex wral ctb ctg cfv wceq cpw cin cuni
      basgen2 dfss3 wex elin visset elpw anbi2i bitri an12 exbii eluni df-rex
      3bitr4i ralbii syl3an3b ) CFGBCHDIZEIZGZUPAIZHZJZEBKZDURLZACLBMGBNOCPJURB
      URQZRZSZHZACLADEBCTVFVBACVFUOVEGZDURLVBDURVEUAVGVADURUQUPVDGZJZEUBUPBGZUT
      JZEUBVGVAVIVKEVIUQVJUSJZJVKVHVLUQVHVJUPVCGZJVLUPBVCUCVMUSVJUPUREUDUEUFUGU
      FUQVJUSUHUGUIEUOVDUJUTEBUKULUMUGUMUN $.
      $( [16-Apr-2008] $) $( [22-Jul-2006] $)

    $( Conditions that determine the equality of two generated topologies. $)
    2basgen $p |- ( ( ( B e. Bases /\ C e. Bases ) /\ ( B C_ C /\ C C_
                  ( topGen ` B ) ) ) -> ( topGen ` B ) = ( topGen ` C ) ) $=
      ( ctb wcel wa wss ctg cfv tgss 3expa adantrr ctop tgcl topbas syl
      syl3an2 3com12 adantrl wceq tgidm ad2antrr sseqtrd eqssd ) ACDZBCDZEZABFZ
      BAGHZFZEZEZUHBGHZUFUGUHULFZUIUDUEUGUMABIJKUKULUHGHZUHUFUIULUNFZUGUDUEUIUO
      UEUDUIUOUEUHCDZUIUOUDBUHIUDUHLDUPAMUHNOPQJRUDUNUHSUEUJATUAUBUC $.
      $( [11-May-2007] $) $( [8-May-2007] $)
  $}

  ${
    $d u v w x y z B $.  $d v w x y z J $.
    $( A subset of a topology is a basis for the topology iff every member of
       the topology is a union of members of the basis.  We use the
       idiom " ` B e. Bases /\ ( topGen `` B ) = J ` " to express " ` B ` is a
       basis for topology ` J ` ," since we do not have a separate notation for
       this.  Definition 15.35 of [Schechter] p. 428. $)
    bastop1 $p |- ( ( J e. Top /\ B C_ J ) -> ( ( B e. Bases /\
      ( topGen ` B ) = J ) <-> A. x e. J E. y ( y C_ B /\ x = U. y ) ) ) $=
      ( vz vw ctop wcel wss wa ctb ctg cfv wceq cv cuni wex wral eleq2 bicomd
      eltg3 sylan9bbr biimpd r19.21aiv wrex basgen2 3expia wi sseq2 biimpar
      elssuni sylan2 a1d ancld r19.22dva eluni2 syl5ib sylbid ssrexv sylan9r
      19.23aiv r19.20si syl5 impbid2 ) DGHZCDIZJZCKHZCLMZDNZJZBOZCIZAOZVLPZNZJZ
      BQZADRZVKVRADVKVNDHZVRVJVTVNVIHZVHVRVJWAVTVIDVNSTBVNCUAUBUCUDVGEOZFOZHZWC
      VNIZJZFCUEZEVNRZADRZVKVSVEVFWIVKAEFCDUFUGVRWHADVRWGEVNVQWBVNHZWGUHBVPWJWF
      FVLUEZVMWGVPWJWBVOHZWKVNVOWBSVPWDFVLUEWKWLVPWDWFFVLVPWCVLHZJZWDWEWNWEWDVP
      WCVOIZWEWMVPWEWOVNVOWCUIUJWCVLUKULUMUNUOFWBVLUPUQURWFFVLCUSUTVAUDVBVCVD
      $.
      $( [3-Feb-2008] $) $( [2-Feb-2008] $)

    $( A version of ~ bastop1 that doesn't have ` B C_ J ` in the
       antecedent. $)
    bastop2 $p |- ( J e. Top -> ( ( B e. Bases /\ ( topGen ` B ) = J ) <->
           ( B C_ J /\ A. x e. J E. y ( y C_ B /\ x = U. y ) ) ) ) $=
      ( ctop wcel wss ctb ctg cfv wceq wa cv cuni wex wral bastop1 pm5.32da
      bastg adantr pm3.27 sseqtrd pm4.71ri syl5bb ) DEFZCDGZCHFZCIJZDKZLZLUFBMZ
      CGAMUKNKLBOADPZLUJUEUFUJULABCDQRUJUFUJCUHDUGCUHGUICSTUGUIUAUBUCUD $.
      $( [17-Apr-2008] $) $( [3-Feb-2008] $)
  $}

