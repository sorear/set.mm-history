$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Bases for topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w x y z B $.
    $( Express the predicate " ` B ` is a basis for a topology." $)
    isbasisg $p |- ( B e. C -> ( B e. Bases <->
          A. x e. B A. y e. B ( x i^i y ) (_ U. ( B i^i P~ ( x i^i y ) ) ) ) $=
      ( vz cv cin cpw cuni wss wral ctb wceq ineq1 unieqd sseq2d raleqd
      df-bases elab2g ) AFBFGZEFZTHZGZIZJZBUAKZAUAKTCUBGZIZJZBCKZACKECLDUFUJAUA
      CUEUIBUACUACMZUDUHTUKUCUGUACUBNOPQQEABRS $.
      $( [18-Jul-2006] $) $( [17-Jul-2006] $)

    $( Express the predicate " ` B ` is a basis for a topology." $)
    isbasis2g $p |- ( B e. C -> ( B e. Bases <->
           A. x e. B A. y e. B A. z e. ( x i^i y )
              E. w e. B ( z e. w /\ w (_ ( x i^i y ) ) ) ) $=
      ( wcel ctb cv cin cpw cuni wss wral wa wrex isbasisg dfss3 wex elin
      df-pw abeq2i anbi2i bitr an12 exbii eluni df-rex 3bitr4 ralbii 2ralbii
      syl6bb ) EFGEHGAIBIJZEUMKZJZLZMZBENAENCIZDIZGZUSUMMZOZDEPZCUMNZBENAENABEF
      QUQVDABEEUQURUPGZCUMNVDCUMUPRVEVCCUMUTUSUOGZOZDSUSEGZVBOZDSVEVCVGVIDVGUTV
      HVAOZOVIVFVJUTVFVHUSUNGZOVJUSEUNTVKVAVHVADUNDUMUAUBUCUDUCUTVHVAUEUDUFDURU
      OUGVBDEUHUIUJUDUKUL $.
      $( [18-Jul-2006] $) $( [17-Jul-2006] $)

    $( Express the predicate " ` B ` is a basis for a topology."  Definition of
       basis in [Munkres] p. 78. $)
    isbasis3g $p |- ( B e. C -> ( B e. Bases <-> ( A. x e. B x (_ U. B /\
     A. x e. U. B E. y e. B x e. y /\ A. x e. B A. y e. B A. z e. ( x i^i y )
     E. w e. B ( z e. w /\ w (_ ( x i^i y ) ) ) ) ) $=
      ( wcel ctb cv cin wss wa wrex wral cuni w3a isbasis2g elssuni rgen
      eluni2 biimp pm3.2i biantrur df-3an bitr4 syl6bb ) EFGEHGCIDIZGUGAIZBIZJZ
      KLDEMCUJNBENAENZUHEOZKZAENZUHUIGBEMZAULNZUKPZABCDEFQUKUNUPLZUKLUQURUKUNUP
      UMAEUHERSUOAULUHULGUOBUHETUASUBUCUNUPUKUDUEUF $.
      $( [18-Jul-2006] $) $( [17-Jul-2006] $)
  $}

  ${
    $d w x A $.  $d w x y z B $.  $d w x y z C $.  $d w x y z D $.
    $( Property of a basis. $)
    basis1t $p |- ( ( B e. Bases /\ C e. B /\ D e. B ) ->
                    ( C i^i D ) (_  U. ( B i^i P~ ( C i^i D ) ) ) $=
      ( vx vy ctb wcel cin cpw cuni wss wa cv wral wceq ineq1 pweq syl ineq2d
      unieqd sseq12d ineq2 rcla42v isbasisg ibi syl5com 3impib ) AFGZBAGZCAGZBC
      HZAUKIZHZJZKZUIUJLDMZEMZHZAURIZHZJZKZEANDANZUOUHVBUOBUQHZAVDIZHZJZKDEBCAA
      UPBOZURVDVAVGUPBUQPZVHUTVFVHUSVEAVHURVDOUSVEOVIURVDQRSTUAUQCOZVDUKVGUNUQC
      BUBZVJVFUMVJVEULAVJVDUKOVEULOVKVDUKQRSTUAUCUHVCDEAFUDUEUFUG $.
      $( [21-Jul-2006] $) $( [16-Jul-2006] $)

    $( Property of a basis. $)
    basis2t $p |- ( ( ( B e. Bases /\ C e. B ) /\ ( D e. B /\
          A e. ( C i^i D ) ) ) -> E. x e. B ( A e. x /\ x (_ ( C i^i D ) ) ) $=
      ( vw vy vz ctb wcel cin cv wss wa wrex wi wral isbasis2g ibi wceq wb
      ineq1 sseq2 anbi2d rexbidv raleqd syl ineq2 rcla42v eleq1 anbi1d rcla4cv
      syl6com exp3a imp43 ) CIJZDCJZECJZBDEKZJZBALZJZVAUSMZNZACOZUPUQURUTVEPZUP
      FLZVAJZVAGLZHLZKZMZNZACOZFVKQZHCQGCQZUQURNZVFPUPVPGHFACIRSVQVPVHVCNZACOZF
      USQZVFVOVTVHVADVJKZMZNZACOZFWAQZGHDECCVIDTVKWATZVOWEUAVIDVJUBVNWDFVKWAWFV
      MWCACWFVLWBVHVKWAVAUCUDUEUFUGVJETWAUSTZWEVTUAVJEDUHWDVSFWAUSWGWCVRACWGWBV
      CVHWAUSVAUCUDUEUFUGUIVSVEFBUSVGBTZVRVDACWHVHVBVCVGBVAUJUKUEULUMUGUNUO $.
      $( [24-Jul-2006] $) $( [17-Jul-2006] $)
  $}

  ${
    $d x y z A $.  $d w x y z B $.  $d x y C $.  $d w J $.
    tgvalt.1 $e |- J = ( topGen ` B ) $.
    $( The topology generated by a basis. $)
    tgvalt $p |- ( B e. Bases -> J = { x | x (_ U. ( B i^i P~ x ) } ) $=
      ( vy vz ctb wcel ctg cfv cv cpw cin cuni wss cab cvv wceq wa uniexg
      abssexg uniin sstr mpan2 ssin sylibr ss2abi ssexg mpan 3syl ineq1 unieqd
      sseq2d abbidv df-topgen fvopab4g mpdan syl5eq ) BGHZBIJZAKZBVALZMZNZOZAPZ
      CUSVFQHZUTVFRUSBNZQHVAVHOVAVBNZOZSZAPZQHZVGBGTVJAVHQUAVFVLOVMVGVEVKAVEVAV
      HVIMZOZVKVEVDVNOVOBVBUBVAVDVNUCUDVAVHVIUEUFUGVFVLQUHUIUJEFBVAEKZVBMZNZOZA
      PVFGQIVPBRZVSVEAVTVRVDVAVTVQVCVPBVBUKULUMUNEFAUOUPUQDUR $.
      $( [26-Feb-2007] $) $( [16-Jul-2006] $)

    $( Definition of a topology generated by a basis in [Munkres] p. 78.
       Later we show (in ~ tgclt ) that ` ( topGen `` B ) ` is indeed a
       topology (on ` U. B ` ; see ~ unitgt ). $)
    tgval2t $p |- ( B e. Bases -> J =
         { x | ( x (_ U. B /\ A. y e. x E. z e. B ( y e. z /\ z (_ x ) ) } ) $=
      ( ctb wcel cv cpw cin cuni wss cab wa wrex wral tgvalt dfss3 inss1 uniss
      ax-mp sseli pm4.71ri ralbii r19.26 3bitr wex elin anbi2i an12 bitr exbii
      eluni df-rex 3bitr4 visset elpw rexbii bitr2 anbi12i bitr4 abbii syl6eq
      ) DGHEAIZDVEJZKZLZMZANVEDLZMZBIZCIZHZVMVEMZOZCDPZBVEQZOZANADEFRVIVSAVIVLV
      JHZBVEQZVLVHHZBVEQZOZVSVIWCVTWBOZBVEQWDBVEVHSWBWEBVEWBVTVHVJVLVGDMVHVJMDV
      FTVGDUAUBUCUDUEVTWBBVEUFUGVKWAVRWCBVEVJSVQWBBVEWBVNVMVFHZOZCDPZVQVNVMVGHZ
      OZCUHVMDHZWGOZCUHWBWHWJWLCWJVNWKWFOZOWLWIWMVNVMDVFUIUJVNWKWFUKULUMCVLVGUN
      WGCDUOUPWGVPCDWFVOVNVMVECUQURUJUSUTUEVAVBVCVD $.
      $( [2-Mar-2007] $) $( [15-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltgt $p |- ( B e. Bases -> ( A e. J <-> A (_ U. ( B i^i P~ A ) ) ) $=
      ( vx ctb wcel cv cpw cin cuni wss cab tgvalt eleq2d cvv elisset adantl
      ssexg ancoms inex1g uniexg syl sylan wceq id pweq ineq2d unieqd sseq12d
      elabg pm5.21nd bitrd ) BFGZACGAEHZBUOIZJZKZLZEMZGZABAIZJZKZLZUNCUTAEBCDNO
      UNVAVEAPGZVAVFUNAUTQRVDPGZVEVFUNVEVGVFAVDPSTUNVCPGVGBVBFUAVCPUBUCUDUSVEEA
      PUOAUEZUOAURVDVHUFVHUQVCVHUPVBBUOAUGUHUIUJUKULUM $.
      $( [6-Mar-2007] $) $( [26-Feb-2007] $)

    $( Membership in a topology generated by a basis. $)
    eltg2t $p |- ( B e. Bases -> ( A e. J <->
           ( A (_ U. B /\ A. x e. A E. y e. B ( x e. y /\ y (_ A ) ) ) ) $=
      ( vz ctb wcel cv cuni wss wa wrex wral cab tgval2t eleq2d cvv elisset
      adantl ssexg uniexg sylan2 ancoms adantrr wceq sseq1 sseq2 anbi2d
      rexbidv raleqd anbi12d elabg pm5.21nd bitrd ) DHIZCEICGJZDKZLZAJBJZIZVAUR
      LZMZBDNZAUROZMZGPZIZCUSLZVBVACLZMZBDNZACOZMZUQEVHCGABDEFQRUQVIVOCSIZVIVPU
      QCVHTUAUQVJVPVNVJUQVPVJUSSIVPUQCUSSUBDHUCUDUEUFVGVOGCSURCUGZUTVJVFVNURCUS
      UHVEVMAURCVQVDVLBDVQVCVKVBURCVAUIUJUKULUMUNUOUP $.
      $( [9-Mar-2007] $) $( [15-Jul-2006] $)

    $( Property of a member of a topology generated by a basis. $)
    tg2t $p |- ( ( B e. Bases /\ A e. J /\ C e. A ) ->
               E. x e. B ( C e. x /\ x (_ A ) ) $=
      ( vy ctb wcel cv wss wa wrex cuni wral wi eltg2t wceq eleq1 anbi1d
      rexbidv rcla4cv adantl syl6bi 3imp ) CHIZBEIZDBIZDAJZIZUIBKZLZACMZUFUGBCN
      KZGJZUIIZUKLZACMZGBOZLUHUMPZGABCEFQUSUTUNURUMGDBUODRZUQULACVAUPUJUKUODUIS
      TUAUBUCUDUE $.
      $( [10-Mar-2007] $) $( [20-Jul-2006] $)

    $( A member of a basis is a subset of the topology it generates. $)
    bastgt $p |- ( B e. Bases -> B (_ J ) $=
      ( vw ctb wcel cv cpw cin cuni wss wa basis1t 3anidm23 inidm wceq pweq
      ax-mp ineq2i unieqi 3sstr3g ex eltgt sylibrd ssrdv ) AEFZDABUFDGZAFZUGAUG
      HZIZJZKZUGBFUFUHULUFUHLUGUGIZAUMHZIZJZUGUKUFUHUMUPKAUGUGMNUGOZUOUJUNUIAUM
      UGPUNUIPUQUMUGQRSTUAUBUGABCUCUDUE $.
      $( [11-Mar-2007] $) $( [16-Jul-2006] $)
  $}

  ${
    $d y x z A $.  $d t u v w x y z B $.  $d x y C $.  $d w x y z J $.
    $( The topology generated by a basis. $)
    tgvaltOLD $p |- ( B e. Bases -> ( topGen ` B ) =
                  { x | x (_ U. ( B i^i P~ x ) } ) $=
      ( vy vz ctb wcel cv cpw cin cuni wss cab cvv ctg cfv wceq wa uniexg
      abssexg uniin sstr mpan2 ssin sylibr ss2abi ssexg mpan 3syl ineq1 unieqd
      sseq2d abbidv df-topgen fvopab4g mpdan ) BEFZAGZBUQHZIZJZKZALZMFZBNOVBPUP
      BJZMFUQVDKUQURJZKZQZALZMFZVCBERVFAVDMSVBVHKVIVCVAVGAVAUQVDVEIZKZVGVAUTVJK
      VKBURTUQUTVJUAUBUQVDVEUCUDUEVBVHMUFUGUHCDBUQCGZURIZJZKZALVBEMNVLBPZVOVAAV
      PVNUTUQVPVMUSVLBURUIUJUKULCDAUMUNUO $.
      $( [19-Jul-2006] $) $( [16-Jul-2006] $)

    $( Definition of a topology generated by a basis in [Munkres] p. 78.
       Later we show (in ~ tgclt ) that ` ( topGen `` B ) ` is indeed a
       topology (on ` U. B ` ; see ~ unitgt ). $)
    tgval2tOLD $p |- ( B e. Bases -> ( topGen ` B ) =
         { x | ( x (_ U. B /\ A. y e. x E. z e. B ( y e. z /\ z (_ x ) ) } ) $=
      ( ctb wcel ctg cfv cv cpw cin cuni wss cab wa wrex wral tgvaltOLD dfss3
      inss1 uniss ax-mp sseli pm4.71ri ralbii r19.26 3bitr wex elin anbi2i
      an12 bitr exbii eluni df-rex 3bitr4 visset elpw rexbii bitr2 anbi12i
      bitr4 abbii syl6eq ) DEFDGHAIZDVEJZKZLZMZANVEDLZMZBIZCIZFZVMVEMZOZCDPZBVE
      QZOZANADRVIVSAVIVLVJFZBVEQZVLVHFZBVEQZOZVSVIWCVTWBOZBVEQWDBVEVHSWBWEBVEWB
      VTVHVJVLVGDMVHVJMDVFTVGDUAUBUCUDUEVTWBBVEUFUGVKWAVRWCBVEVJSVQWBBVEWBVNVMV
      FFZOZCDPZVQVNVMVGFZOZCUHVMDFZWGOZCUHWBWHWJWLCWJVNWKWFOZOWLWIWMVNVMDVFUIUJ
      VNWKWFUKULUMCVLVGUNWGCDUOUPWGVPCDWFVOVNVMVECUQURUJUSUTUEVAVBVCVD $.
      $( [19-Jul-2006] $) $( [15-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltgtOLD $p |- ( B e. Bases -> ( A e. ( topGen ` B ) <->
           A (_ U. ( B i^i P~ A ) ) ) $=
      ( vx ctb wcel ctg cfv cv cpw cin cuni wss cab tgvaltOLD eleq2d cvv
      elisset adantl ssexg ancoms inex1g uniexg syl sylan wceq id pweq ineq2d
      unieqd sseq12d elabg pm5.21nd bitrd ) BDEZABFGZEACHZBUPIZJZKZLZCMZEZABAIZ
      JZKZLZUNUOVAACBNOUNVBVFAPEZVBVGUNAVAQRVEPEZVFVGUNVFVHVGAVEPSTUNVDPEVHBVCD
      UAVDPUBUCUDUTVFCAPUPAUEZUPAUSVEVIUFVIURVDVIUQVCBUPAUGUHUIUJUKULUM $.
      $( [21-Jul-2006] $) $( [16-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg2tOLD $p |- ( B e. Bases -> ( A e. ( topGen ` B ) <->
           ( A (_ U. B /\ A. x e. A E. y e. B ( x e. y /\ y (_ A ) ) ) ) $=
      ( vz ctb wcel ctg cfv cv cuni wss wa wrex wral cab tgval2tOLD eleq2d cvv
      elisset adantl ssexg uniexg sylan2 ancoms adantrr wceq sseq1 sseq2
      anbi2d rexbidv raleqd anbi12d elabg pm5.21nd bitrd ) DFGZCDHIZGCEJZDKZLZA
      JBJZGZVBUSLZMZBDNZAUSOZMZEPZGZCUTLZVCVBCLZMZBDNZACOZMZUQURVICEABDQRUQVJVP
      CSGZVJVQUQCVITUAUQVKVQVOVKUQVQVKUTSGVQUQCUTSUBDFUCUDUEUFVHVPECSUSCUGZVAVK
      VGVOUSCUTUHVFVNAUSCVRVEVMBDVRVDVLVCUSCVBUIUJUKULUMUNUOUP $.
      $( [25-Jul-2006] $) $( [15-Jul-2006] $)

    $( Property of a member of a topology generated by a basis. $)
    tg1t $p |- ( ( B e. Bases /\ A e. ( topGen ` B ) ) ->
              A (_ U. B ) $=
      ( vx vy ctb wcel ctg cfv cuni wss cv wa wrex wral eltg2tOLD pm3.26
      syl6bi imp ) BEFZABGHFZABIJZSTUACKDKZFUBAJLDBMCANZLUACDABOUAUCPQR $.
      $( [26-Jul-2006] $) $( [20-Jul-2006] $)

    $( Property of a member of a topology generated by a basis. $)
    tg2tOLD $p |- ( ( B e. Bases /\ A e. ( topGen ` B ) /\ C e. A ) ->
               E. x e. B ( C e. x /\ x (_ A ) ) $=
      ( vy ctb wcel ctg cfv cv wss wa wrex cuni wral wi eltg2tOLD wceq eleq1
      anbi1d rexbidv rcla4cv adantl syl6bi 3imp ) CFGZBCHIGZDBGZDAJZGZUIBKZLZAC
      MZUFUGBCNKZEJZUIGZUKLZACMZEBOZLUHUMPZEABCQUSUTUNURUMEDBUODRZUQULACVAUPUJU
      KUODUISTUAUBUCUDUE $.
      $( [26-Jul-2006] $) $( [20-Jul-2006] $)

    $( A member of a basis is a subset of the topology it generates. $)
    bastgtOLD $p |- ( B e. Bases -> B (_ ( topGen ` B ) ) $=
      ( vx ctb wcel ctg cfv cv cpw cin cuni wss wa basis1t 3anidm23 inidm wceq
      pweq ax-mp ineq2i unieqi 3sstr3g ex eltgtOLD sylibrd ssrdv ) ACDZBAAEFZUF
      BGZADZUHAUHHZIZJZKZUHUGDUFUIUMUFUILUHUHIZAUNHZIZJZUHULUFUIUNUQKAUHUHMNUHO
      ZUPUKUOUJAUNUHPUOUJPURUNUHQRSTUAUBUHAUCUDUE $.
      $( [23-Jul-2006] $) $( [16-Jul-2006] $)

    $( The topology generated by a basis ` B ` is a topology on ` U. B ` .
       Importantly, this theorem means that we don't have to specify separately
       the base set for the topological space generated by a basis.  In other
       words, any member of the class ` Bases ` completely specifies the basis
       it corresponds to. $)
    unitgt $p |- ( B e. Bases -> U. ( topGen ` B ) = U. B ) $=
      ( vx vy ctb wcel ctg cfv cuni cv wrex cpw cin wss wi eltgtOLD inss1
      uniss ax-mp sstr mpan2 sseld syl6bi r19.23adv eluni2 syl5ib bastgtOLD
      syl impbid eqrdv ) ADEZBAFGZHZAHZUJBIZULEZUNUMEZUJUNCIZEZCUKJUPUOUJURUPCU
      KUJUQUKEUQAUQKZLZHZMZURUPNUQAOVBUQUMUNVBVAUMMZUQUMMUTAMVCAUSPUTAQRUQVAUMS
      TUAUBUCCUNUKUDUEUJUMULUNUJAUKMUMULMAUFAUKQUGUAUHUI $.
      $( [24-Jul-2006] $) $( [16-Jul-2006] $)

    $( MAY BE REVISED $)
    $( The empty set is a member of a topology generated by a basis. $)
    0eltgt $p |- ( B e. Bases -> (/) e. ( topGen ` B ) ) $=
      ( ctb wcel c0 ctg cfv cpw cin cuni wss 0ss eltgtOLD mpbiri ) ABCDAEFCDADG
      HIZJNKDALM $.
      $( [9-Aug-2006] $) $( [17-Jul-2006] $)

    $( MAY BE REVISED $)
    $( ` U. B ` is a member of a topology generated by a basis ` B ` . $)
    unieltgt $p |- ( B e. Bases -> U. B e. ( topGen ` B ) ) $=
      ( ctb wcel cuni ctg cfv cpw cin wss wceq pwuni df-ss mpbi unieqi
      eqimss2i eltgtOLD mpbiri ) ABCADZAEFCRARGZHZDZIUARTAASITAJAKASLMNORAPQ $.
      $( [12-Nov-2006] $) $( [17-Jul-2006] $)

    $( Show that a basis generates a topology.  Remark in [Munkres] p. 79. $)
    tgclt $p |- ( B e. Bases -> ( topGen ` B ) e. Top ) $=
      ( vu vv vx vy vt vz vw ctb wcel cv ctg cfv wss cuni wi wal cin wral wa
      ctop wrex uniss adantl wceq unitgt adantr sseqtrd eltg2tOLD pm3.27
      syl6bi ra4 syl6 imp31 an1rs ssel2 sylan2 an42s sstr2 elssuni syl5com
      anim2d r19.22sdv ad2antrl mpd exp32 r19.23adv eluni2 syl5ib r19.21aiv
      jca ex sylibrd 19.21aiv tg1t ssinss1 syl adantrr biimpa pm3.27d im2anan9
      elin reeanv 3imtr4g anandis basis2t adantllr adantrrr com12 ad2antll
      biimpr ss2in anim12i an4s sylanr2 r19.23adva a2d imp syldan r19.21aivv
      cvv wb fvex istopg ax-mp sylibr ) AIJZBKZALMZNZXHOZXIJZPZBQZXHCKZRZXIJZCX
      ISBXISZTZXIUAJZXGXNXRXGXMBXGXJXKAOZNZDKZEKZJZYDXKNZTZEAUBZDXKSZTZXLXGXJYJ
      XGXJTZYBYIYKXKXIOZYAXJXKYLNXGXHXIUCUDXGYLYAUEXJAUFUGUHYKYHDXKYKYCFKZJZFXH
      UBYHYCXKJYKYNYHFXHYKYMXHJZYNYHYKYOYNTTYEYDYMNZTZEAUBZYHXGYNXJYOYRXGYNTYMX
      IJZYRXJYOTXGYSYNYRXGYSYNYRXGYSYRDYMSZYNYRPXGYSYMYANZYTTYTDEYMAUIUUAYTUJUK
      YRDYMULUMUNUOXHXIYMUPUQURYOYRYHPYKYNYOYQYGEAYOYPYFYEYPYMXKNYFYOYDYMXKUSYM
      XHUTVAVBVCVDVEVFVGFYCXHVHVIVJVKVLDEXKAUIVMVNXGXQBCXIXIXGXHXIJZXOXIJZTZXPY
      ANZYNYMXPNZTZFAUBZDXPSZTZXQXGUUDUUJXGUUDTZUUEUUIXGUUBUUEUUCXGUUBTZXHYANZU
      UEXHAVOXHXOYAVPVQVRUUKUUHDXPXGUUDYCXPJZYCGKZJZUUOXHNZTZYCHKZJZUUSXONZTZTZ
      HAUBZGAUBZPZUUNUUHPZXGUUBUUCUVFUULXGUUCTZTYCXHJZYCXOJZTUURGAUBZUVBHAUBZTU
      UNUVEUULUVIUVKUVHUVJUVLUULUVKDXHSZUVIUVKPUULUUMUVMXGUUBUUMUVMTDGXHAUIVSVT
      UVKDXHULVQUVHUVLDXOSZUVJUVLPUVHXOYANZUVNXGUUCUVOUVNTDHXOAUIVSVTUVLDXOULVQ
      WAYCXHXOWBUURUVBGHAAWCWDWEXGUVFUVGXGUUNUVEUUHXGUUNUVEUUHPXGUUNTZUVDUUHGAU
      VPUUOAJZTZUVCUUHHAUVRUUSAJZUVCUUHUVRUVSYCUUOUUSRZJZUVTXPNZTZUUHUVCUVRUVSU
      WCTTYNYMUVTNZTZFAUBZUUHUVRUVSUWAUWFUWBXGUVQUVSUWATUWFUUNFYCAUUOUUSWFWGWHU
      WCUWFUUHPZUVRUVSUWBUWGUWAUWBUWEUUGFAUWBUWDUUFYNUWDUWBUUFYMUVTXPUSWIVBVCUD
      WJVEUUPUUTUUQUVAUWCUUPUUTTZUWAUUQUVATUWBUWAUWHYCUUOUUSWBWKUUOXHUUSXOWLWMW
      NWOVFVGWPVLWQWRWSVJVKVLDFXPAUIVMWTVKXIXAJXTXSXBALXCBCXAXIXDXEXF $.
      $( [25-Jul-2006] $) $( [17-Jul-2006] $)

    $( Alternate expression for the topology generated by a basis.  Lemma 2.1
       of [Munkres] p. 80. $)
    tgval3t $p |- ( B e. Bases ->
               ( topGen ` B ) = { x | E. y ( y (_ B /\ x = U. y ) } ) $=
      ( vz vw ctb wcel cv wss cuni wceq wa wex ctg cfv cab wrex wral wi
      eltg2tOLD biimpa pm3.27d elequ1 anbi1d rexbidv rcla4cv df-rex an12 ancom
      anbi2i bitr exbii syl6ib syl ssel impcom adantrr 19.23aiv impbid1
      eluniab syl6bbr eqrdv pm3.27 abssi jctil cvv visset abssexg ax-mp hbab1
      ax-17 hbss hbuni hbeq hban sseq1 unieq eqeq2d anbi12d cla4egf ex simprr
      ctop uniopnt tgclt adantr sstr bastgtOLD sylan2 ancoms sylanc eqeltrd
      19.23adv impbid abbi2dv ) CFGZBHZCIZAHZWQJZKZLZBMZACNOZWPWSXDGZXCWPXEXCWP
      XELZWQWSIZWQCGZLZBPZCIZWSXJJZKZLZXCXFXMXKXFDWSXLXFDHZWSGZXOWQGZXILZBMZXOX
      LGXFXPXSXFEHZWQGZXGLZBCQZEWSRZXPXSSXFWSCJIZYDWPXEYEYDLEBWSCTUAUBYDXPXQXGL
      ZBCQZXSYCYGEXOWSXTXOKZYBYFBCYHYAXQXGEDBUCUDUEUFYGXHYFLZBMXSYFBCUGYIXRBYIX
      QXHXGLZLXRXHXQXGUHYJXIXQXHXGUIUJUKULUKUMUNXRXPBXQXGXPXHXGXQXPWQWSXOUOUPUQ
      URUSXIBXOUTVAVBXIBCXGXHVCVDVEXJVFGZXNXCSWSVFGYKAVGXHBWSVFVHVIXBXNBDXJVFXI
      BDVJZXKXMBBDXJCYLXOCGBVKVLBDDWSXLXPBVKBDXJYLVMVNVOWQXJKZWRXKXAXMWQXJCVPYM
      WTXLWSWQXJVQVRVSVTVIUNWAWPXBXEBWPXBXEWPXBLWSWTXDWPWRXAWBWPWRWTXDGZXAXDWCG
      ZWQXDIZYNWPWRLWQXDWDWPYOWRCWEWFWRWPYPWRCXDIYPWPWQCXDWGCWHWIWJWKUQWLWAWMWN
      WO $.
      $( [25-Jul-2006] $) $( [17-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg3t $p |- ( B e. Bases -> ( A e. ( topGen ` B ) <->
           E. x ( x (_ B /\ A = U. x ) ) ) $=
      ( vy ctb wcel ctg cfv cv wss cuni wceq wa wex cab tgval3t eleq2d cvv
      visset uniex eleq1 mpbiri adantl 19.23aiv eqeq1 anbi2d exbidv elab3
      syl6bb ) CEFZBCGHZFBAIZCJZDIZULKZLZMZANZDOZFUMBUOLZMZANZUJUKUSBDACPQURVBD
      BVABRFZAUTVCUMUTVCUORFULASTBUORUAUBUCUDUNBLZUQVAAVDUPUTUMUNBUOUEUFUGUHUI
      $.
      $( [26-Jul-2006] $) $( [15-Jul-2006] $)

    $( A topology is its own basis. $)
    topbast $p |- ( J e. Top -> J e. Bases ) $=
      ( vz vw vx vy ctop wcel ctb cv cin wss wa wrex wral wceq eleq2 sseq1
      anbi12d rcla4ev inopnt 3expb adantr pm3.27 ssid jctir sylanc exp31
      r19.21adv r19.21aivv isbasis2g mpbird ) AFGZAHGBIZCIZGZUNDIZEIZJZKZLZCAMZ
      BURNZEANDANULVBDEAAULUPAGZUQAGZLZVABURULVEUMURGZVAURAGZVFURURKZLZVAULVELZ
      VFLZUTVICURAUNUROUOVFUSVHUNURUMPUNURURQRSVJVGVFULVCVDVGUPUQATUAUBVKVFVHVJ
      VFUCURUDUEUFUGUHUIDEBCAFUJUK $.
      $( [22-Jul-2006] $) $( [17-Jul-2006] $)

    $( A topology is its own basis. $)
    tgtopt $p |- ( J e. Top -> ( topGen ` J ) = J ) $=
      ( vx vy ctop wcel ctg cfv cv wss cuni wceq wa wex ctb wb topbast eltg3t
      syl pm3.27 uniopnt adantr eqeltrd anasss ex 19.23adv sylbid ssrdv
      bastgtOLD eqssd ) ADEZAFGZAUJBUKAUJBHZUKEZCHZAIZULUNJZKZLZCMZULAEZUJANEZU
      MUSOAPZCULAQRUJURUTCUJURUTUJUOUQUTUJUOLZUQLULUPAVCUQSVCUPAEUQUNATUAUBUCUD
      UEUFUGUJVAAUKIVBAUHRUI $.
      $( [27-Jul-2006] $) $( [18-Jul-2006] $)

    $( Membership in a topology, expressed without quantifiers. $)
    eltopt $p |- ( J e. Top -> ( A e. J <-> A (_ U. ( J i^i P~ A ) ) ) $=
      ( ctop wcel ctg cfv cpw cin cuni wss tgtopt eleq2d ctb wb topbast
      eltgtOLD syl bitr3d ) BCDZABEFZDZABDABAGHIJZSTBABKLSBMDUAUBNBOABPQR $.
      $( [2-Jan-2007] $) $( [19-Jul-2006] $)

    $( Membership in a topology. $)
    eltop2t $p |- ( J e. Top -> ( A e. J <->
               ( A (_ U. J /\ A. x e. A E. y e. J ( x e. y /\ y (_ A ) ) ) ) $=
      ( ctop wcel ctg cfv cuni wss cv wa wrex wral tgtopt eleq2d ctb wb
      topbast eltg2tOLD syl bitr3d ) DEFZCDGHZFZCDFCDIJAKBKZFUFCJLBDMACNLZUCUDD
      CDOPUCDQFUEUGRDSABCDTUAUB $.
      $( [?] $) $( [19-Jul-2006] $)

    $( Membership in a topology. $)
    eltop3t $p |- ( J e. Top -> ( A e. J <-> E. x ( x (_ J /\ A = U. x ) ) ) $=
      ( ctop wcel ctg cfv cv wss cuni wceq wa wex tgtopt eleq2d ctb wb topbast
      eltg3t syl bitr3d ) CDEZBCFGZEZBCEAHZCIBUEJKLAMZUBUCCBCNOUBCPEUDUFQCRABCS
      TUA $.
      $( [?] $) $( [19-Jul-2006] $)
  $}

  $( The topology generator function is idempotent. $)
  tgidmt $p |- ( B e. Bases ->
               ( topGen ` ( topGen ` B ) ) = ( topGen ` B ) ) $=
    ( ctb wcel ctg cfv ctop wceq tgclt tgtopt syl ) ABCADEZFCKDEKGAHKIJ $.
    $( [9-Jan-2007] $) $( [18-Jul-2006] $)

  $( Two ways to express that a basis is a topology. $)
  bastopt $p |- ( B e. Bases -> ( B e. Top <-> ( topGen ` B ) = B ) ) $=
    ( ctb wcel ctop ctg cfv wceq tgtopt wa pm3.27 tgclt adantr eqeltrrd ex
    impbid2 ) ABCZADCZAEFZAGZAHPSQPSIRADPSJPRDCSAKLMNO $.
    $( [?] $) $( [18-Jul-2006] $)

  $( The topology generation function is one-to-one when applied to completed
     topologies. $)
  tgtop11t $p |- ( ( J e. Top /\ K e. Top /\ ( topGen ` J ) = ( topGen ` K ) )
                 -> J = K ) $=
    ( ctop wcel ctg cfv wceq w3a 3simp3 tgtopt 3ad2ant1 3ad2ant2 3eqtr3d ) ACDZ
    BCDZAEFZBEFZGZHPQABNORINOPAGRAJKONQBGRBJLM $.
    $( [?] $) $( [18-Jul-2006] $)

  $( The singleton of the empty set is the only topology possible for an empty
     underlying set set. $)
  0top $p |- ( J e. Top -> ( U. J = (/) <-> J = { (/) } ) ) $=
    ( ctop wcel c0 csn wceq wo cuni olc wn 0opnt n0i syl pm2.21d idd jaod
    impbid2 wss uni0b sssn bitr2 syl6rbb ) ABCZADEZFZADFZUEGZAHDFZUCUEUGUEUFIUC
    UFUEUEUCUFUEUCDACUFJAKADLMNUCUEOPQUHAUDRUGASADTUAUB $.
    $( [?] $) $( [9-Sep-2006] $)

  ${
    $d u v w x y z B $.  $d u v w x y z C $.  $d v w x y z J $.
    $( A criterion for determining whether one topology is finer than another,
       based on a comparison of their bases.  Lemma 2.2 of [Munkres] p. 80. $)
    topss2t $p |- ( ( B e. Bases /\ C e. Bases /\ U. B = U. C ) ->
                    ( ( topGen ` B ) (_ ( topGen ` C ) <->
     A. x e. U. B A. y e. B ( x e. y -> E. z e. C ( x e. z /\ z (_ y ) ) ) ) $=
      ( vu vw vv ctb wcel cuni wceq w3a ctg cfv wss cv wa wrex wi wral tg2tOLD
      pm3.27 ad2antrr sstr bastgtOLD sylan sseld imp adantllr adantrl adantrr
      simprr syl3anc exp43 r19.21advv 3adant3 eltg2tOLD tg1t adantll simpll
      sseqtrd elssuni elequ1 anbi1d rexbidv imbi12d elequ2 sseq2 anbi2d
      rcla42v ex com4l imp32 adantrrr mpd an1s sstr2 com12 anim2d r19.22sdv
      adantl ad2antll exp32 r19.23adv 3expa syl5 exp4c imp42 r19.21aiva jca
      syl5bir 3imp 3comr exp3a ssrdv impbid ) DIJZEIJZDKZEKZLZMZDNOZENOZPZAQZBQ
      ZJZXGCQZJZXJXHPZRZCESZTZBDUAAWTUAZWRWSXFXPTXBWRWSRZXFXOABWTDXQXFXGWTJZXHD
      JZRZXIXNWSXHXEJZXIXNXQXFRZXTXIRZRCXHEXGUBXQWSXFYCWRWSUCUDYBXTYAXIYBXSYAXR
      WRXFXSYAWSWRXFRZXSYAYDDXEXHDXDPXFDXEPWRDXDXEUEDUFUGUHUIUJUKULYBXTXIUMUNUO
      UPUQXCXPXFXCXPRFXDXEXCXPFQZXDJZYEXEJZTXCXPYFYGWSXBWRXPYFRZYGTZWSXBWRYIWSX
      BWRYHYGWSYGYEXAPZGQZXJJZXJYEPZRZCESZGYEUAZRXBWRRZYHRZGCYEEURYRYJYPYQYFYJX
      PYQYFRYEWTXAWRYFYEWTPXBYEDUSUTXBWRYFVAVBUKWRYHYPXBWRYHRYOGYEWRXPYFYKYEJZY
      OXPWRYFYSYOTTXPWRYFYSYOXPYKHQZJZYTYEPZRZHDSZYOWRYFRYSRXPUUCYOHDXPYTDJZUUC
      YOXPUUEUUCRRYLXJYTPZRZCESZYOUUEXPUUCUUHUUEXPUUCRRYKWTJZUUHUUEUUCUUIXPUUEU
      UAUUIUUBUUEUUAUUIUUEYTWTYKYTDVCUHUIULUKUUEXPUUAUUIUUHTZUUBUUEXPUUAUUJUUIU
      UEXPUUAUUHUUIUUEXPUUAUUHTZTXOUUKYKXHJZYLXLRZCESZTABYKYTWTDXGYKLZXIUULXNUU
      NAGBVDUUOXMUUMCEUUOXKYLXLAGCVDVEVFVGXHYTLZUULUUAUUNUUHBHGVHUUPUUMUUGCEUUP
      XLUUFYLXHYTXJVIVJVFVGVKVLVMVNVOVPVQUUCUUHYOTZXPUUEUUBUUQUUAUUBUUGYNCEUUBU
      UFYMYLUUFUUBYMXJYTYEVRVSVTWAWBWCVPWDWEWRYFYSUUDHYEDYKUBWFWGWHVSWIWJUTWKWL
      WHWMWNWOUIWPVLWQ $.
      $( [27-Jul-2006] $) $( [20-Jul-2006] $)

    $( A criterion for determining whether one topology is finer than
       another.  Lemma 2.2 of [Munkres] p. 80 using abbreviations. $)
    topsst $p |- ( ( B e. Bases /\ C e. Bases /\ U. B = U. C ) ->
                    ( ( topGen ` B ) (_ ( topGen ` C ) <->
                  A. x e. B ( U. B i^i x ) (_ U. ( C i^i P~ x ) ) ) $=
      ( vy vz ctb wcel cuni wceq w3a ctg cfv wss cv wa wrex wi wral cin cpw
      topss2t ralcom wal elin wex anass anbi1i ancom bitr3 visset elpw anbi2i
      bitr 3bitr3r exbii df-rex eluni 3bitr4r imbi12i impexp albii dfss2
      df-ral ralbii syl6bb ) BFGCFGBHZCHIJBKLCKLMDNZANZGZVGENZGZVJVHMZOZECPZQZA
      BRDVFRZVFVHSZCVHTZSZHZMZABRZDAEBCUAVPVODVFRZABRWBVODAVFBUBWCWAABVGVQGZVGV
      TGZQZDUCVGVFGZVOQZDUCWAWCWFWHDWFWGVIOZVNQWHWDWIWEVNVGVFVHUDVJCGZVMOZEUEVK
      VJVSGZOZEUEVNWEWKWMEWJVJVRGZOZVKOZWJWNVKOZOWMWKWJWNVKUFWPWLVKOWMWLWOVKVJC
      VRUDUGWLVKUHUIWQVMWJWQVKWNOVMWNVKUHWNVLVKVJVHEUJUKULUMULUNUOVMECUPEVGVSUQ
      URUSWGVIVNUTUMVADVQVTVBVODVFVCURVDUMVE $.
      $( [?] $) $( [20-Jul-2006] $)

    $( Given a topology ` J ` , show that a subset ` B ` satisfying the
       third antecedent is a basis for it.  Lemma 2.3 of [Munkres] p. 81. $)
    basgen2t $p |- ( ( J e. Top /\ B (_ J /\ A. x e. J A. y e. x E. z e. B
            ( y e. z /\ z (_ x ) ) -> ( B e. Bases /\ ( topGen ` B ) = J ) ) $=
      ( vw vv ctop wcel wss cv wa wrex wral ctb ctg cfv wceq cin wi inopnt
      3expb ssel anim12d imp sylan2 anassrs sseq2 anbi2d rexbidv raleqd rcla4v
      syl ex com23 r19.21advv cvv wb ssexg ancoms isbasis2g sylibrd cuni wex
      eltg3t adantl pm3.27 uniopnt sstr adantr eqeltrd exp42 imp4b 19.23adv
      sylbid ssrdv syldan tgtopt ad2antrr topss2t 3expa biimpar topbast jca
      eqid 1open raleq1 elequ1 rcla4cv eluni2 syl6ibr syl6 pm3.26 r19.22si
      r19.20si syl5 adantlr uniss ad2antlr eqssd ax-1 r19.20i2 ralcom sylib
      sylanc eqsstr3d 3impa ) EHIZDEJZBKZCKZIZXKAKZJZLZCDMZBXMNZAENZDOIZDPQZERZ
      LXHXILZXRLZXSYAYBXRXSYBXRXLXKFKZGKZSZJZLZCDMZBYFNZGDNFDNZXSYBXRYJFGDDYBYD
      DIZYEDIZLZXRYJYBYNXRYJTZYBYNLYFEIZYOXHXIYNYPXHYDEIZYEEIZLZYPXIYNLXHYQYRYP
      YDYEEUAUBXIYNYSXIYLYQYMYRDEYDUCDEYEUCUDUEUFUGXQYJAYFEXPYIBXMYFXMYFRZXOYHC
      DYTXNYGXLXMYFXKUHUIUJUKULUMUNUOUPYBDUQIZXSYKURXIXHUUADEHUSUTFGBCDUQVAUMVB
      UEZYCXTEYBXRXSXTEJUUBYBXSLZAXTEUUCXMXTIZXJDJZXMXJVCZRZLZBVDZXMEIZXSUUDUUI
      URYBBXMDVEVFYBUUIUUJTXSYBUUHUUJBXHXIUUEUUGUUJXHUUEXIUUGUUJTXHUUEXIUUGUUJX
      HUUEXILZLZUUGLXMUUFEUULUUGVGUULUUFEIZUUGXHXJEJUUMUUKXJEVHXJDEVIUFVJVKVLUO
      VMVNVJVOVPVQYCEEPQZXTXHUUNERXIXREVRVSEOIZXSLZEVCZDVCZRZLZXJXMIXPTZAENBUUQ
      NZUUNXTJZYCUUTUVCUVBUUOXSUUSUVCUVBURBACEDVTWAWBYCUUPUUSYCUUOXSXHUUOXIXREW
      CVSUUBWDYCUUQUURXHXRUUQUURJXIXHXRLFUUQUURXHXRYDUUQIZYDUURIZTZXHUUQEIZXRUV
      FTEUUQUUQWEWFUVGXLCDMZBXMNZAENZUVFXRUVGUVJUVHBUUQNZUVFUVIUVKAUUQEUVHBXMUU
      QWGULUVKUVDYDXKIZCDMZUVEUVHUVMBYDUUQXJYDRXLUVLCDBFCWHUJWICYDDWJWKWLXQUVIA
      EXPUVHBXMXOXLCDXLXNWMWNWOWOWPUMUEVPWQXIUURUUQJXHXRDEWRWSWTWDXRUVBYBXRUVAB
      UUQNZAENUVBXQUVNAEXPUVABXMUUQUVAXJUUQIXAXBWOUVAABEUUQXCXDVFXEXFWTWDXG $.
      $( [27-Jul-2006] $) $( [20-Jul-2006] $)

    $( Given a topology ` J ` , show that a subset ` B ` satisfying the
       third antecedent is a basis for it.  Lemma 2.3 of [Munkres] p. 81 using
       abbreviations. $)
    basgent $p |- ( ( J e. Top /\ B (_ J /\ A. x e. J
          x (_ U. ( B i^i P~ x ) ) -> ( B e. Bases /\ ( topGen ` B ) = J ) ) $=
      ( vy vz ctop wcel wss cv wa wrex wral ctb ctg cfv wceq cpw cin cuni
      basgen2t dfss3 wex elin visset elpw anbi2i bitr an12 exbii eluni df-rex
      3bitr4 ralbii syl3an3b ) CFGBCHDIZEIZGZUPAIZHZJZEBKZDURLZACLBMGBNOCPJURBU
      RQZRZSZHZACLADEBCTVFVBACVFUOVEGZDURLVBDURVEUAVGVADURUQUPVDGZJZEUBUPBGZUTJ
      ZEUBVGVAVIVKEVIUQVJUSJZJVKVHVLUQVHVJUPVCGZJVLUPBVCUCVMUSVJUPUREUDUEUFUGUF
      UQVJUSUHUGUIEUOVDUJUTEBUKULUMUGUMUN $.
      $( [?] $) $( [22-Jul-2006] $)
  $}


