$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      TopBases for topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w x y z B $.
    $( Express the predicate " ` B ` is a basis for a topology." $)
    isbasisg $p |- ( B e. C -> ( B e. TopBases <->
          A. x e. B A. y e. B ( x i^i y ) C_ U. ( B i^i ~P ( x i^i y ) ) ) ) $=
      ( vz cv cin cpw cuni wss wral ctb wceq ineq1 unieqd sseq2d raleqbi1dv
      df-bases elab2g ) AFBFGZEFZTHZGZIZJZBUAKZAUAKTCUBGZIZJZBCKZACKECLDUFUJAUA
      CUEUIBUACUACMZUDUHTUKUCUGUACUBNOPQQEABRS $.
      $( [17-Jul-2006] $)

    $( Express the predicate " ` B ` is a basis for a topology." $)
    isbasis2g $p |- ( B e. C -> ( B e. TopBases <->
           A. x e. B A. y e. B A. z e. ( x i^i y )
              E. w e. B ( z e. w /\ w C_ ( x i^i y ) ) ) ) $=
      ( wcel ctb cv cin cpw cuni wss wral wa wrex isbasisg dfss3 wex elin
      df-pw abeq2i anbi2i bitri an12 exbii eluni df-rex 3bitr4i ralbii 2ralbii
      syl6bb ) EFGEHGAIBIJZEUMKZJZLZMZBENAENCIZDIZGZUSUMMZOZDEPZCUMNZBENAENABEF
      QUQVDABEEUQURUPGZCUMNVDCUMUPRVEVCCUMUTUSUOGZOZDSUSEGZVBOZDSVEVCVGVIDVGUTV
      HVAOZOVIVFVJUTVFVHUSUNGZOVJUSEUNTVKVAVHVADUNDUMUAUBUCUDUCUTVHVAUEUDUFDURU
      OUGVBDEUHUIUJUDUKUL $.
      $( [17-Jul-2006] $)

    $( Express the predicate " ` B ` is a basis for a topology."  Definition of
       basis in [Munkres] p. 78. $)
    isbasis3g $p |- ( B e. C -> ( B e. TopBases <-> ( A. x e. B x C_ U. B /\
     A. x e. U. B E. y e. B x e. y /\ A. x e. B A. y e. B A. z e. ( x i^i y )
     E. w e. B ( z e. w /\ w C_ ( x i^i y ) ) ) ) ) $=
      ( wcel ctb cv cin wss wa wrex wral cuni w3a isbasis2g elssuni rgen
      eluni2 biimpi pm3.2i biantrur df-3an bitr4i syl6bb ) EFGEHGCIDIZGUGAIZBIZ
      JZKLDEMCUJNBENAENZUHEOZKZAENZUHUIGBEMZAULNZUKPZABCDEFQUKUNUPLZUKLUQURUKUN
      UPUMAEUHERSUOAULUHULGUOBUHETUASUBUCUNUPUKUDUEUF $.
      $( [17-Jul-2006] $)
  $}

  ${
    $d w x A $.  $d w x y z B $.  $d w x y z C $.  $d w x y z D $.
    $( Property of a basis. $)
    basis1 $p |- ( ( B e. TopBases /\ C e. B /\ D e. B ) ->
                    ( C i^i D ) C_ U. ( B i^i ~P ( C i^i D ) ) ) $=
      ( vx vy ctb wcel cin cpw cuni wss cv wral wa isbasisg ibi wceq ineq1
      pweqd ineq2d unieqd sseq12d ineq2 rcla42v syl5com 3impib ) AFGZBAGZCAGZBC
      HZAUJIZHZJZKZUGDLZELZHZAUQIZHZJZKZEAMDAMZUHUINUNUGVBDEAFOPVAUNBUPHZAVCIZH
      ZJZKDEBCAAUOBQZUQVCUTVFUOBUPRZVGUSVEVGURVDAVGUQVCVHSTUAUBUPCQZVCUJVFUMUPC
      BUCZVIVEULVIVDUKAVIVCUJVJSTUAUBUDUEUF $.
      $( [16-Jul-2006] $)

    $( Property of a basis. $)
    basis2 $p |- ( ( ( B e. TopBases /\ C e. B ) /\ ( D e. B /\
          A e. ( C i^i D ) ) ) -> E. x e. B ( A e. x /\ x C_ ( C i^i D ) ) ) $=
      ( vw vy vz ctb wcel cin cv wss wa wrex wi wral isbasis2g ibi wceq wb
      ineq1 sseq2 anbi2d rexbidv raleqbi1dv syl ineq2 rcla42v eleq1 anbi1d
      rcla4cv syl6com exp3a imp43 ) CIJZDCJZECJZBDEKZJZBALZJZVAUSMZNZACOZUPUQUR
      UTVEPZUPFLZVAJZVAGLZHLZKZMZNZACOZFVKQZHCQGCQZUQURNZVFPUPVPGHFACIRSVQVPVHV
      CNZACOZFUSQZVFVOVTVHVADVJKZMZNZACOZFWAQZGHDECCVIDTVKWATZVOWEUAVIDVJUBVNWD
      FVKWAWFVMWCACWFVLWBVHVKWAVAUCUDUEUFUGVJETWAUSTZWEVTUAVJEDUHWDVSFWAUSWGWCV
      RACWGWBVCVHWAUSVAUCUDUEUFUGUIVSVEFBUSVGBTZVRVDACWHVHVBVCVGBVAUJUKUEULUMUG
      UNUO $.
      $( [17-Jul-2006] $)
  $}

  ${
    $d B w x y z $.  $d C w x y z $.
    $( If a set is closed under finite intersection, then it is a basis for a
       topology.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    fiinbas $p |- ( ( B e. C /\ A. x e. B A. y e. B ( x i^i y ) e. B )
                       -> B e. TopBases ) $=
      ( vz vw wcel cv cin wral ctb wss wa wrex wi ssid wceq eleq2 sseq1
      anbi12d rcla4ev mpanr2 r19.21aiva a1i ralimdv isbasis2g sylibrd imp ) CDG
      ZAHBHIZCGZBCJZACJZCKGZUIUMEHZFHZGZUPUJLZMZFCNZEUJJZBCJZACJUNUIULVBACUIUKV
      ABCUKVAOUIUKUTEUJUKUOUJGZUJUJLZUTUJPUSVCVDMFUJCUPUJQUQVCURVDUPUJUORUPUJUJ
      STUAUBUCUDUEUEABEFCDUFUGUH $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d y x z A $.  $d t u v w x y z B $.  $d x y C $.  $d w x y z J $.
    $( The topology generated by a basis. $)
    tgval $p |- ( B e. TopBases -> ( topGen ` B ) =
                  { x | x C_ U. ( B i^i ~P x ) } ) $=
      ( vy ctb wcel cv cpw cin cuni wss cab cvv ctg cfv wceq wa uniexg abssexg
      uniin sstr mpan2 ssin sylibr ss2abi ssexg mpan 3syl ineq1 unieqd sseq2d
      abbidv df-topgen fvmptg mpdan ) BDEZAFZBUPGZHZIZJZAKZLEZBMNVAOUOBIZLEUPVC
      JUPUQIZJZPZAKZLEZVBBDQVEAVCLRVAVGJVHVBUTVFAUTUPVCVDHZJZVFUTUSVIJVJBUQSUPU
      SVITUAUPVCVDUBUCUDVAVGLUEUFUGCBUPCFZUQHZIZJZAKVADLMVKBOZVNUTAVOVMUSUPVOVL
      URVKBUQUHUIUJUKCAULUMUN $.
      $( [10-Nov-2013] $) $( [16-Jul-2006] $)

    $( Definition of a topology generated by a basis in [Munkres] p. 78.  Later
       we show (in ~ tgcl ) that ` ( topGen `` B ) ` is indeed a topology (on
       ` U. B ` ; see ~ unitg ). $)
    tgval2 $p |- ( B e. TopBases -> ( topGen ` B ) =
         { x | ( x C_ U. B /\ A. y e. x E. z e. B ( y e. z /\ z C_ x ) ) } ) $=
      ( ctb wcel ctg cfv cv cpw cin cuni wss cab wa wrex wral tgval dfss3
      inss1 uniss ax-mp sseli pm4.71ri ralbii r19.26 3bitri wex elin anbi2i
      an12 bitri exbii eluni df-rex 3bitr4i visset elpw rexbii bitr2i anbi12i
      bitr4i abbii syl6eq ) DEFDGHAIZDVEJZKZLZMZANVEDLZMZBIZCIZFZVMVEMZOZCDPZBV
      EQZOZANADRVIVSAVIVLVJFZBVEQZVLVHFZBVEQZOZVSVIWCVTWBOZBVEQWDBVEVHSWBWEBVEW
      BVTVHVJVLVGDMVHVJMDVFTVGDUAUBUCUDUEVTWBBVEUFUGVKWAVRWCBVEVJSVQWBBVEWBVNVM
      VFFZOZCDPZVQVNVMVGFZOZCUHVMDFZWGOZCUHWBWHWJWLCWJVNWKWFOZOWLWIWMVNVMDVFUIU
      JVNWKWFUKULUMCVLVGUNWGCDUOUPWGVPCDWFVOVNVMVECUQURUJUSUTUEVAVBVCVD $.
      $( [15-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg $p |- ( B e. TopBases -> ( A e. ( topGen ` B ) <->
           A C_ U. ( B i^i ~P A ) ) ) $=
      ( vx ctb wcel ctg cfv cv cpw cin cuni wss cab tgval eleq2d cvv elisset
      adantl inex1g uniexg syl ssexg sylan2 ancoms wceq id pweq ineq2d unieqd
      sseq12d elabg pm5.21nd bitrd ) BDEZABFGZEACHZBUPIZJZKZLZCMZEZABAIZJZKZLZU
      NUOVAACBNOUNVBVFAPEZVBVGUNAVAQRVFUNVGUNVFVEPEZVGUNVDPEVHBVCDSVDPTUAAVEPUB
      UCUDUTVFCAPUPAUEZUPAUSVEVIUFVIURVDVIUQVCBUPAUGUHUIUJUKULUM $.
      $( [16-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg2 $p |- ( B e. TopBases -> ( A e. ( topGen ` B ) <->
           ( A C_ U. B /\ A. x e. A E. y e. B ( x e. y /\ y C_ A ) ) ) ) $=
      ( vz ctb wcel ctg cfv cv cuni wss wa wrex wral cab tgval2 eleq2d cvv
      elisset adantl uniexg ssexg sylan2 ancoms adantrr wceq sseq1 sseq2
      anbi2d rexbidv raleqbi1dv anbi12d elabg pm5.21nd bitrd ) DFGZCDHIZGCEJZDK
      ZLZAJBJZGZVBUSLZMZBDNZAUSOZMZEPZGZCUTLZVCVBCLZMZBDNZACOZMZUQURVICEABDQRUQ
      VJVPCSGZVJVQUQCVITUAUQVKVQVOVKUQVQUQVKUTSGVQDFUBCUTSUCUDUEUFVHVPECSUSCUGZ
      VAVKVGVOUSCUTUHVFVNAUSCVRVEVMBDVRVDVLVCUSCVBUIUJUKULUMUNUOUP $.
      $( [15-Jul-2006] $)

    $( Property of a member of a topology generated by a basis. $)
    tg1 $p |- ( ( B e. TopBases /\ A e. ( topGen ` B ) ) ->
              A C_ U. B ) $=
      ( vx vy ctb wcel ctg cfv cuni wss cv wa wrex wral eltg2 simprbda ) BEFABG
      HFABIJCKDKZFQAJLDBMCANCDABOP $.
      $( [20-Jul-2006] $)

    $( Property of a member of a topology generated by a basis. $)
    tg2 $p |- ( ( B e. TopBases /\ A e. ( topGen ` B ) /\ C e. A ) ->
               E. x e. B ( C e. x /\ x C_ A ) ) $=
      ( vy ctb wcel ctg cfv cv wss wa wrex cuni wral wi eltg2 wceq eleq1
      anbi1d rexbidv rcla4cv adantl syl6bi 3imp ) CFGZBCHIGZDBGZDAJZGZUIBKZLZAC
      MZUFUGBCNKZEJZUIGZUKLZACMZEBOZLUHUMPZEABCQUSUTUNURUMEDBUODRZUQULACVAUPUJU
      KUODUISTUAUBUCUDUE $.
      $( [20-Jul-2006] $)

    $( A member of a basis is a subset of the topology it generates. $)
    bastg $p |- ( B e. TopBases -> B C_ ( topGen ` B ) ) $=
      ( vx ctb wcel ctg cfv cv cpw cin cuni wss wa basis1 3anidm23 inidm pweqi
      ineq2i unieqi 3sstr3g ex eltg sylibrd ssrdv ) ACDZBAAEFZUDBGZADZUFAUFHZIZ
      JZKZUFUEDUDUGUKUDUGLUFUFIZAULHZIZJZUFUJUDUGULUOKAUFUFMNUFOZUNUIUMUHAULUFU
      PPQRSTUFAUAUBUC $.
      $( [16-Jul-2006] $)

    $( The topology generated by a basis ` B ` is a topology on ` U. B ` .
       Importantly, this theorem means that we don't have to specify separately
       the base set for the topological space generated by a basis.  In other
       words, any member of the class ` TopBases ` completely specifies the
       basis it corresponds to. $)
    unitg $p |- ( B e. TopBases -> U. ( topGen ` B ) = U. B ) $=
      ( vx vy ctb wcel ctg cfv cuni cv wrex eluni2 cpw cin wss wi eltg inss1
      uniss ax-mp sstr mpan2 sseld syl6bi r19.23adv syl5bi bastg syl impbid
      eqrdv ) ADEZBAFGZHZAHZUJBIZULEZUNUMEZUOUNCIZEZCUKJUJUPCUNUKKUJURUPCUKUJUQ
      UKEUQAUQLZMZHZNZURUPOUQAPVBUQUMUNVBVAUMNZUQUMNUTANVCAUSQUTARSUQVAUMTUAUBU
      CUDUEUJUMULUNUJAUKNUMULNAUFAUKRUGUBUHUI $.
      $( [16-Jul-2006] $)

    $( Show that a basis generates a topology.  Remark in [Munkres] p. 79. $)
    tgcl $p |- ( B e. TopBases -> ( topGen ` B ) e. Top ) $=
      ( vu vv vx vy vt vz vw ctb wcel cv ctg cfv wss cuni wi wal cin wral ctop
      wa wrex uniss adantl wceq unitg adantr sseqtrd eluni2 ssel2 eltg2 simpr
      syl6bi ra4 syl6 imp31 an32s sylan2 an42s elssuni sstr2 syl5com anim2d
      reximdv ad2antrl mpd exp32 r19.23adv syl5bi r19.21aiv jca ex sylibrd
      19.21aiv tg1 ssinss1 syl adantrr simplbda im2anan9 elin reeanv 3imtr4g
      anandis biimpri ss2in anim12i an4s basis2 adantllr adantrrr com12
      ad2antll sylanr2 r19.23adva a2d imp syldan r19.21aivv cvv wb fvex istopg
      ax-mp sylanbrc ) AIJZBKZALMZNZXGOZXHJZPZBQZXGCKZRZXHJZCXHSBXHSZXHTJZXFXLB
      XFXIXJAOZNZDKZEKZJZYBXJNZUAZEAUBZDXJSZUAZXKXFXIYHXFXIUAZXTYGYIXJXHOZXSXIX
      JYJNXFXGXHUCUDXFYJXSUEXIAUFUGUHYIYFDXJYAXJJYAFKZJZFXGUBYIYFFYAXGUIYIYLYFF
      XGYIYKXGJZYLYFYIYMYLUAUAYCYBYKNZUAZEAUBZYFXFYLXIYMYPXIYMUAXFYLUAYKXHJZYPX
      GXHYKUJXFYQYLYPXFYQYLYPXFYQYPDYKSZYLYPPXFYQYKXSNZYRUAYRDEYKAUKYSYRULUMYPD
      YKUNUOUPUQURUSYMYPYFPYIYLYMYOYEEAYMYNYDYCYMYKXJNYNYDYKXGUTYBYKXJVAVBVCVDV
      EVFVGVHVIVJVKVLDEXJAUKVMVNXFXPBCXHXHXFXGXHJZXNXHJZUAZXOXSNZYLYKXONZUAZFAU
      BZDXOSZUAZXPXFUUBUUHXFUUBUAZUUCUUGXFYTUUCUUAXFYTUAZXGXSNZUUCXGAVOXGXNXSVP
      VQVRUUIUUFDXOXFUUBYAXOJZYAGKZJZUUMXGNZUAZYAHKZJZUUQXNNZUAZUAZHAUBZGAUBZPZ
      UULUUFPZXFYTUUAUVDUUJXFUUAUAZUAYAXGJZYAXNJZUAUUPGAUBZUUTHAUBZUAUULUVCUUJU
      VGUVIUVFUVHUVJUUJUVIDXGSZUVGUVIPXFYTUUKUVKDGXGAUKVSUVIDXGUNVQUVFUVJDXNSZU
      VHUVJPXFUUAXNXSNUVLDHXNAUKVSUVJDXNUNVQVTYAXGXNWAUUPUUTGHAAWBWCWDXFUVDUVEX
      FUULUVCUUFXFUULUVCUUFPXFUULUAZUVBUUFGAUVMUUMAJZUAZUVAUUFHAUVOUUQAJZUVAUUF
      UVAUVOUVPYAUUMUUQRZJZUVQXONZUAZUUFUUNUURUUOUUSUVTUUNUURUAZUVRUUOUUSUAUVSU
      VRUWAYAUUMUUQWAWEUUMXGUUQXNWFWGWHUVOUVPUVTUAUAYLYKUVQNZUAZFAUBZUUFUVOUVPU
      VRUWDUVSXFUVNUVPUVRUAUWDUULFYAAUUMUUQWIWJWKUVTUWDUUFPZUVOUVPUVSUWEUVRUVSU
      WCUUEFAUVSUWBUUDYLUWBUVSUUDYKUVQXOVAWLVCVDUDWMVFWNVGVHWOVLWPWQWRVJVKVLDFX
      OAUKVMWSXHWTJXRXMXQUAXAALXBBCWTXHXCXDXE $.
      $( [17-Jul-2006] $)

    $( Alternate expression for the topology generated by a basis.  Lemma 2.1
       of [Munkres] p. 80. $)
    tgval3 $p |- ( B e. TopBases ->
               ( topGen ` B ) = { x | E. y ( y C_ B /\ x = U. y ) } ) $=
      ( vz vw ctb wcel cv wss cuni wceq wa wex ctg cfv cab simpr abssi wrex
      wral wi eltg2 simplbda elequ1 anbi1d rexbidv rcla4cv df-rex an12 ancom
      anbi2i bitri exbii syl6ib syl ssel impcom adantrr 19.23aiv impbid1
      eluniab syl6bbr eqrdv cvv visset abssexg ax-mp hbab1 ax-17 hbss hbuni
      hbeq hban sseq1 unieq eqeq2d anbi12d cla4egf sylancr ex simprr ctop tgcl
      adantr bastg sstr sylan2 ancoms uniopn syl11anc eqeltrd 19.23adv impbid
      abbi2dv ) CFGZBHZCIZAHZWPJZKZLZBMZACNOZWOWRXCGZXBWOXDXBWOXDLZWPWRIZWPCGZL
      ZBPZCIZWRXIJZKZXBXHBCXFXGQRXEDWRXKXEDHZWRGZXMWPGZXHLZBMZXMXKGXEXNXQXEEHZW
      PGZXFLZBCSZEWRTZXNXQUAWOXDWRCJIYBEBWRCUBUCYBXNXOXFLZBCSZXQYAYDEXMWRXRXMKZ
      XTYCBCYEXSXOXFEDBUDUEUFUGYDXGYCLZBMXQYCBCUHYFXPBYFXOXGXFLZLXPXGXOXFUIYGXH
      XOXGXFUJUKULUMULUNUOXPXNBXOXFXNXGXFXOXNWPWRXMUPUQURUSUTXHBXMVAVBVCXIVDGZX
      JXLLZXBUAWRVDGYHAVEXGBWRVDVFVGXAYIBDXIVDXHBDVHZXJXLBBDXICYJXMCGBVIVJBDDWR
      XKXNBVIBDXIYJVKVLVMWPXIKZWQXJWTXLWPXICVNYKWSXKWRWPXIVOVPVQVRVGVSVTWOXAXDB
      WOXAXDWOXALWRWSXCWOWQWTWAWOWQWSXCGZWTWOWQLXCWBGZWPXCIZYLWOYMWQCWCWDWQWOYN
      WOWQCXCIYNCWEWPCXCWFWGWHWPXCWIWJURWKVTWLWMWN $.
      $( [17-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg3 $p |- ( B e. TopBases -> ( A e. ( topGen ` B ) <->
           E. x ( x C_ B /\ A = U. x ) ) ) $=
      ( vy ctb wcel ctg cfv cv wss cuni wceq wa wex cab tgval3 eleq2d cvv
      visset uniex eleq1 mpbiri adantl 19.23aiv eqeq1 anbi2d exbidv elab3
      syl6bb ) CEFZBCGHZFBAIZCJZDIZULKZLZMZANZDOZFUMBUOLZMZANZUJUKUSBDACPQURVBD
      BVABRFZAUTVCUMUTVCUORFULASTBUORUAUBUCUDUNBLZUQVAAVDUPUTUMUNBUOUEUFUGUHUI
      $.
      $( [15-Jul-2006] $)

    $( A topology is its own basis. $)
    topbas $p |- ( J e. Top -> J e. TopBases ) $=
      ( vz vw vx vy ctop wcel ctb cv cin wss wa wrex wral inopn 3expb adantr
      simpr ssid jctir wceq eleq2 sseq1 anbi12d rcla4ev syl11anc exp31
      r19.21adv r19.21aivv isbasis2g mpbird ) AFGZAHGBIZCIZGZUNDIZEIZJZKZLZCAMZ
      BURNZEANDANULVBDEAAULUPAGZUQAGZLZVABURULVEUMURGZVAULVELZVFLZURAGZVFURURKZ
      LZVAVGVIVFULVCVDVIUPUQAOPQVHVFVJVGVFRURSTUTVKCURAUNURUAUOVFUSVJUNURUMUBUN
      URURUCUDUEUFUGUHUIDEBCAFUJUK $.
      $( [17-Jul-2006] $)

    $( A topology is its own basis. $)
    tgtop $p |- ( J e. Top -> ( topGen ` J ) = J ) $=
      ( vx vy ctop wcel ctg cfv cv wss cuni wceq wa wex ctb wb topbas eltg3
      syl simpr uniopn adantr eqeltrd expl 19.23adv sylbid ssrdv bastg eqssd )
      ADEZAFGZAUIBUJAUIBHZUJEZCHZAIZUKUMJZKZLZCMZUKAEZUIANEZULUROAPZCUKAQRUIUQU
      SCUIUNUPUSUIUNLZUPLUKUOAVBUPSVBUOAEUPUMATUAUBUCUDUEUFUIUTAUJIVAAUGRUH $.
      $( [18-Jul-2006] $)

    $( Membership in a topology, expressed without quantifiers. $)
    eltop $p |- ( J e. Top -> ( A e. J <-> A C_ U. ( J i^i ~P A ) ) ) $=
      ( ctop wcel ctg cfv cpw cin cuni wss tgtop eleq2d ctb wb topbas eltg syl
      bitr3d ) BCDZABEFZDZABDABAGHIJZSTBABKLSBMDUAUBNBOABPQR $.
      $( [19-Jul-2006] $)

    $( Membership in a topology. $)
    eltop2 $p |- ( J e. Top -> ( A e. J <->
               ( A C_ U. J /\ A. x e. A E. y e. J ( x e. y /\ y C_ A ) ) ) ) $=
      ( ctop wcel ctg cfv cuni wss cv wa wrex wral tgtop eleq2d ctb wb topbas
      eltg2 syl bitr3d ) DEFZCDGHZFZCDFCDIJAKBKZFUFCJLBDMACNLZUCUDDCDOPUCDQFUEU
      GRDSABCDTUAUB $.
      $( [23-Aug-2007] $) $( [19-Jul-2006] $)

    $( Membership in a topology. $)
    eltop3 $p |- ( J e. Top -> ( A e. J <-> E. x ( x C_ J /\ A = U. x ) ) ) $=
      ( ctop wcel ctg cfv cv wss cuni wceq wa wex tgtop eleq2d ctb wb topbas
      eltg3 syl bitr3d ) CDEZBCFGZEZBCEAHZCIBUEJKLAMZUBUCCBCNOUBCPEUDUFQCRABCST
      UA $.
      $( [29-Mar-2008] $) $( [19-Jul-2006] $)
  $}

  $( The topology generator function is idempotent. $)
  tgidm $p |- ( B e. TopBases ->
               ( topGen ` ( topGen ` B ) ) = ( topGen ` B ) ) $=
    ( ctb wcel ctg cfv ctop wceq tgcl tgtop syl ) ABCADEZFCKDEKGAHKIJ $.
    $( [18-Jul-2006] $)

  $( Two ways to express that a basis is a topology. $)
  bastop $p |- ( B e. TopBases -> ( B e. Top <-> ( topGen ` B ) = B ) ) $=
    ( ctb wcel ctop ctg cfv wceq tgtop wa simpr tgcl adantr eqeltrrd ex
    impbid2 ) ABCZADCZAEFZAGZAHPSQPSIRADPSJPRDCSAKLMNO $.
    $( [14-Aug-2007] $) $( [18-Jul-2006] $)

  $( The topology generation function is one-to-one when applied to completed
     topologies. $)
  tgtop11 $p |- ( ( J e. Top /\ K e. Top /\ ( topGen ` J ) = ( topGen ` K ) )
                 -> J = K ) $=
    ( ctop wcel ctg cfv wceq w3a simp3 tgtop 3ad2ant1 3ad2ant2 3eqtr3d ) ACDZBC
    DZAEFZBEFZGZHPQABNORINOPAGRAJKONQBGRBJLM $.
    $( [11-Jun-2007] $) $( [18-Jul-2006] $)

  $( The singleton of the empty set is the only topology possible for an empty
     underlying set. $)
  0top $p |- ( J e. Top -> ( U. J = (/) <-> J = { (/) } ) ) $=
    ( ctop wcel c0 csn wceq wo cuni olc wn 0opn n0i syl pm2.21d idd jaod
    impbid2 wss uni0b sssn bitr2i syl6rbb ) ABCZADEZFZADFZUEGZAHDFZUCUEUGUEUFIU
    CUFUEUEUCUFUEUCDACUFJAKADLMNUCUEOPQUHAUDRUGASADTUAUB $.
    $( [24-Mar-2008] $) $( [9-Sep-2006] $)

  ${
    $d u v w x y z B $.  $d u v w x y z C $.  $d v w x y z J $.
    $( Subset relation for generated topologies. $)
    tgss $p |- ( ( B e. TopBases /\ C e. TopBases /\ B C_ C ) ->
               ( topGen ` B ) C_ ( topGen ` C ) ) $=
      ( vx ctb wcel wss w3a ctg cfv cv cpw cin cuni wi ssrin uniss syl sstr2
      syl5com 3ad2ant3 wb eltg 3ad2ant1 3ad2ant2 3imtr4d ssrdv ) ADEZBDEZABFZGZ
      CAHIZBHIZUJCJZAUMKZLZMZFZUMBUNLZMZFZUMUKEZUMULEZUIUGUQUTNUHUIUPUSFZUQUTUI
      UOURFVCABUNOUOURPQUMUPUSRSTUGUHVAUQUAUIUMAUBUCUHUGVBUTUAUIUMBUBUDUEUF $.
      $( [7-May-2007] $)

    $( A criterion for determining whether one topology is finer than another,
       based on a comparison of their bases.  Lemma 2.2 of [Munkres] p. 80. $)
    tgss2 $p |- ( ( B e. TopBases /\ C e. TopBases /\ U. B = U. C ) ->
                    ( ( topGen ` B ) C_ ( topGen ` C ) <->
     A. x e. U. B A. y e. B ( x e. y -> E. z e. C ( x e. z /\ z C_ y ) ) ) ) $=
      ( vu vw vv ctb wcel cuni wceq w3a ctg cfv wss cv wa wrex wi wral simpllr
      bastg sstr sylan sseld imp adantllr adantrl adantrr simprr tg2 syl111anc
      exp43 r19.21advv 3adant3 tg1 adantll simpll sseqtrd 3expa elssuni elequ1
      anbi1d rexbidv imbi12d elequ2 sseq2 anbi2d rcla42v ex com4l imp32
      adantrrr mpd an12s sstr2 com12 anim2d reximdv adantl ad2antll exp32
      r19.23adv syl5 exp4c imp42 r19.21aiva jca eltg2 syl5ibr 3imp 3comr
      expdimp ssrdv impbid ) DIJZEIJZDKZEKZLZMZDNOZENOZPZAQZBQZJZXFCQZJZXIXGPZR
      ZCESZTZBDUAAWSUAZWQWRXEXOTXAWQWRRZXEXNABWSDXPXEXFWSJZXGDJZRZXHXMXPXERZXSX
      HRZRWRXGXDJZXHXMWQWRXEYAUBXTXSYBXHXTXRYBXQWQXEXRYBWRWQXERZXRYBYCDXDXGWQDX
      CPXEDXDPDUCDXCXDUDUEUFUGUHUIUJXTXSXHUKCXGEXFULUMUNUOUPXBXOXEXBXORFXCXDXBX
      OFQZXCJZYDXDJZWRXAWQXOYERZYFTZWRXAWQYHWRXAWQYGYFXAWQRZYGRZYFWRYDWTPZGQZXI
      JZXIYDPZRZCESZGYDUAZRYJYKYQYIYEYKXOYIYERYDWSWTWQYEYDWSPXAYDDUQURXAWQYEUSU
      TUIWQYGYQXAWQYGRYPGYDWQXOYEYLYDJZYPXOWQYEYRYPTTXOWQYEYRYPWQYERYRRYLHQZJZY
      SYDPZRZHDSZXOYPWQYEYRUUCHYDDYLULVAXOUUBYPHDXOYSDJZUUBYPXOUUDUUBRRYMXIYSPZ
      RZCESZYPUUDXOUUBUUGUUDXOUUBRRYLWSJZUUGUUDUUBUUHXOUUDYTUUHUUAUUDYTUUHUUDYS
      WSYLYSDVBUFUGUJUIUUDXOYTUUHUUGTZUUAUUDXOYTUUIUUHUUDXOYTUUGUUHUUDXOYTUUGTZ
      TXNUUJYLXGJZYMXKRZCESZTABYLYSWSDXFYLLZXHUUKXMUUMAGBVCUUNXLUULCEUUNXJYMXKA
      GCVCVDVEVFXGYSLZUUKYTUUMUUGBHGVGUUOUULUUFCEUUOXKUUEYMXGYSXIVHVIVEVFVJVKVL
      VMVNVOVPUUBUUGYPTZXOUUDUUAUUPYTUUAUUFYOCEUUAUUEYNYMUUEUUAYNXIYSYDVQVRVSVT
      WAWBVOWCWDWEWFVRWGWHURWIGCYDEWJWKWFWLWMWNWOVKWP $.
      $( [20-Jul-2006] $)

    $( A criterion for determining whether one topology is finer than another.
       Lemma 2.2 of [Munkres] p. 80 using abbreviations. $)
    tgss3 $p |- ( ( B e. TopBases /\ C e. TopBases /\ U. B = U. C ) ->
                    ( ( topGen ` B ) C_ ( topGen ` C ) <->
                  A. x e. B ( U. B i^i x ) C_ U. ( C i^i ~P x ) ) ) $=
      ( vy vz ctb wcel cuni wceq w3a ctg cfv wss cv wa wrex wi wral cin cpw
      tgss2 ralcom wal elin wex anass anbi1i ancom bitr3i visset elpw anbi2i
      bitri 3bitr3ri exbii df-rex eluni 3bitr4ri imbi12i impexp albii dfss2
      df-ral ralbii syl6bb ) BFGCFGBHZCHIJBKLCKLMDNZANZGZVGENZGZVJVHMZOZECPZQZA
      BRDVFRZVFVHSZCVHTZSZHZMZABRZDAEBCUAVPVODVFRZABRWBVODAVFBUBWCWAABVGVQGZVGV
      TGZQZDUCVGVFGZVOQZDUCWAWCWFWHDWFWGVIOZVNQWHWDWIWEVNVGVFVHUDVJCGZVMOZEUEVK
      VJVSGZOZEUEVNWEWKWMEWJVJVRGZOZVKOZWJWNVKOZOWMWKWJWNVKUFWPWLVKOWMWLWOVKVJC
      VRUDUGWLVKUHUIWQVMWJWQVKWNOVMWNVKUHWNVLVKVJVHEUJUKULUMULUNUOVMECUPEVGVSUQ
      URUSWGVIVNUTUMVADVQVTVBVODVFVCURVDUMVE $.
      $( [3-Apr-2008] $) $( [20-Jul-2006] $)

    $( Given a topology ` J ` , show that a subset ` B ` satisfying the third
       antecedent is a basis for it.  Lemma 2.3 of [Munkres] p. 81. $)
    basgen2 $p |- ( ( J e. Top /\ B C_ J /\ A. x e. J A. y e. x E. z e. B
            ( y e. z /\ z C_ x ) ) -> ( B e. TopBases /\ ( topGen ` B ) = J ) )
        $=
      ( vw vv ctop wcel wss cv wa wrex wral ctb ctg cfv wceq cin wi ssel
      anim12d imp inopn 3expb sylan2 anassrs sseq2 anbi2d rexbidv raleqbi1dv
      rcla4v syl r19.21advva cvv wb ssexg ancoms isbasis2g sylibrd cuni wex
      eltg3 adantl simpr sstr uniopn adantr eqeltrd exp42 com23 imp4b 19.23adv
      sylbid ssrdv syldan tgtop ad2antrr topbas eqid topopn simpl reximi
      ralimi raleq elequ1 rcla4cv eluni2 syl6ibr syl6 syl2im adantlr uniss
      ad2antlr eqssd jca31 ax-1 ralimi2 ralcom sylib tgss2 3expa biimpar
      syl11anc eqsstr3d jca 3impa ) EHIZDEJZBKZCKZIZXKAKZJZLZCDMZBXMNZAENZDOIZD
      PQZERZLXHXILZXRLZXSYAYBXRXSYBXRXLXKFKZGKZSZJZLZCDMZBYFNZGDNFDNZXSYBXRYJFG
      DDYBYDDIZYEDIZLZLYFEIZXRYJTXHXIYNYOXIYNLXHYDEIZYEEIZLZYOXIYNYRXIYLYPYMYQD
      EYDUADEYEUAUBUCXHYPYQYOYDYEEUDUEUFUGXQYJAYFEXPYIBXMYFXMYFRZXOYHCDYSXNYGXL
      XMYFXKUHUIUJUKULUMUNYBDUOIZXSYKUPXIXHYTDEHUQURFGBCDUOUSUMUTUCZYCXTEYBXRXS
      XTEJUUAYBXSLZAXTEUUBXMXTIZXJDJZXMXJVAZRZLZBVBZXMEIZXSUUCUUHUPYBBXMDVCVDYB
      UUHUUITXSYBUUGUUIBXHXIUUDUUFUUIXHUUDXIUUFUUITXHUUDXIUUFUUIXHUUDXILZLZUUFL
      XMUUEEUUKUUFVEUUKUUEEIZUUFUUJXHXJEJUULXJDEVFXJEVGUFVHVIVJVKVLVMVHVNVOVPYC
      EEPQZXTXHUUMERXIXREVQVRYCEOIZXSLEVAZDVAZRZLZXJXMIXPTZAENBUUONZUUMXTJZYCUU
      NXSUUQXHUUNXIXREVSVRUUAYCUUOUUPXHXRUUOUUPJXIXHXRLFUUOUUPXHXRYDUUOIZYDUUPI
      ZTZXHUUOEIZXRXLCDMZBXMNZAENZUVDEUUOUUOVTWAXQUVGAEXPUVFBXMXOXLCDXLXNWBWCWD
      WDUVEUVHUVFBUUONZUVDUVGUVIAUUOEUVFBXMUUOWEULUVIUVBYDXKIZCDMZUVCUVFUVKBYDU
      UOXJYDRXLUVJCDBFCWFUJWGCYDDWHWIWJWKUCVOWLXIUUPUUOJXHXRDEWMWNWOWPXRUUTYBXR
      UUSBUUONZAENUUTXQUVLAEXPUUSBXMUUOUUSXJUUOIWQWRWDUUSABEUUOWSWTVDUURUVAUUTU
      UNXSUUQUVAUUTUPBACEDXAXBXCXDXEWOXFXG $.
      $( [20-Jul-2006] $)

    $( Given a topology ` J ` , show that a subset ` B ` satisfying the third
       antecedent is a basis for it.  Lemma 2.3 of [Munkres] p. 81 using
       abbreviations. $)
    basgen $p |- ( ( J e. Top /\ B C_ J /\ A. x e. J
          x C_ U. ( B i^i ~P x ) ) -> ( B e. TopBases /\ ( topGen ` B ) = J ) )
        $=
      ( vy vz cv cpw cin cuni wss wral ctop wcel wa wrex ctb ctg cfv wceq
      dfss3 wex elin visset elpw anbi2i bitri an12 exbii eluni df-rex 3bitr4i
      ralbii basgen2 syl3an3b ) AFZBUOGZHZIZJZACKCLMBCJDFZEFZMZVAUOJZNZEBOZDUOK
      ZACKBPMBQRCSNUSVFACUSUTURMZDUOKVFDUOURTVGVEDUOVBVAUQMZNZEUAVABMZVDNZEUAVG
      VEVIVKEVIVBVJVCNZNVKVHVLVBVHVJVAUPMZNVLVABUPUBVMVCVJVAUOEUCUDUEUFUEVBVJVC
      UGUFUHEUTUQUIVDEBUJUKULUFULADEBCUMUN $.
      $( [16-Apr-2008] $) $( [22-Jul-2006] $)

    $( Conditions that determine the equality of two generated topologies. $)
    2basgen $p |- ( ( ( B e. TopBases /\ C e. TopBases ) /\ ( B C_ C /\ C C_
                  ( topGen ` B ) ) ) -> ( topGen ` B ) = ( topGen ` C ) ) $=
      ( ctb wcel wa wss ctg cfv tgss 3expa adantrr ctop tgcl topbas syl
      syl3an2 3com12 adantrl wceq tgidm ad2antrr sseqtrd eqssd ) ACDZBCDZEZABFZ
      BAGHZFZEZEZUHBGHZUFUGUHULFZUIUDUEUGUMABIJKUKULUHGHZUHUFUIULUNFZUGUDUEUIUO
      UEUDUIUOUDUEUHCDZUIUOUDUHLDUPAMUHNOBUHIPQJRUDUNUHSUEUJATUAUBUC $.
      $( [8-May-2007] $)
  $}

  ${
    $d u v w x y z B $.  $d v w x y z J $.
    $( A subset of a topology is a basis for the topology iff every member of
       the topology is a union of members of the basis.  We use the
       idiom " ` B e. TopBases /\ ( topGen `` B ) = J ` " to express " ` B ` is
       a basis for topology ` J ` ," since we do not have a separate notation
       for this.  Definition 15.35 of [Schechter] p. 428. $)
    bastop1 $p |- ( ( J e. Top /\ B C_ J ) -> ( ( B e. TopBases /\
      ( topGen ` B ) = J ) <-> A. x e. J E. y ( y C_ B /\ x = U. y ) ) ) $=
      ( vz vw ctop wcel wss wa ctb ctg cfv wceq cv cuni wex wral eleq2 bicomd
      eltg3 sylan9bbr biimpd r19.21aiv wrex wi eluni2 elssuni sseq2 biimpar
      sylan2 a1d ancld reximdva syl5bi sylbid ssrexv sylan9r 19.23aiv ralimi
      basgen2 3expia syl5 impbid2 ) DGHZCDIZJZCKHZCLMZDNZJZBOZCIZAOZVLPZNZJZBQZ
      ADRZVKVRADVKVNDHZVRVJVTVNVIHZVHVRVJWAVTVIDVNSTBVNCUAUBUCUDVSEOZFOZHZWCVNI
      ZJZFCUEZEVNRZADRZVGVKVRWHADVRWGEVNVQWBVNHZWGUFBVPWJWFFVLUEZVMWGVPWJWBVOHZ
      WKVNVOWBSWLWDFVLUEVPWKFWBVLUGVPWDWFFVLVPWCVLHZJZWDWEWNWEWDWMVPWCVOIZWEWCV
      LUHVPWEWOVNVOWCUIUJUKULUMUNUOUPWFFVLCUQURUSUDUTVEVFWIVKAEFCDVAVBVCVD $.
      $( [2-Feb-2008] $)

    $( A version of ~ bastop1 that doesn't have ` B C_ J ` in the
       antecedent. $)
    bastop2 $p |- ( J e. Top -> ( ( B e. TopBases /\ ( topGen ` B ) = J ) <->
           ( B C_ J /\ A. x e. J E. y ( y C_ B /\ x = U. y ) ) ) ) $=
      ( ctb wcel ctg cfv wceq wa wss ctop cv cuni wex wral bastg adantr simpr
      sseqtrd pm4.71ri bastop1 pm5.32da syl5bb ) CEFZCGHZDIZJZCDKZUHJDLFZUIBMZC
      KAMUKNIJBOADPZJUHUIUHCUFDUECUFKUGCQRUEUGSTUAUJUIUHULABCDUBUCUD $.
      $( [3-Feb-2008] $)
  $}

