$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      TopBases for topologies
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w x y z B $.
    $( Express the predicate " ` B ` is a basis for a topology." $)
    isbasisg $p |- ( B e. C -> ( B e. TopBases <->
          A. x e. B A. y e. B ( x i^i y ) C_ U. ( B i^i ~P ( x i^i y ) ) ) ) $=
      ( vz cv cin cpw cuni wss wral ctb ineq1 unieqd sseq2d raleqbi1dv df-bases
      wceq elab2g ) AFBFGZEFZTHZGZIZJZBUAKZAUAKTCUBGZIZJZBCKZACKECLDUFUJAUACUEU
      IBUACUACRZUDUHTUKUCUGUACUBMNOPPEABQS $.
      $( [17-Jul-2006] $)

    $( Express the predicate " ` B ` is a basis for a topology." $)
    isbasis2g $p |- ( B e. C -> ( B e. TopBases <->
           A. x e. B A. y e. B A. z e. ( x i^i y )
              E. w e. B ( z e. w /\ w C_ ( x i^i y ) ) ) ) $=
      ( wcel ctb cv cin cpw cuni wss wral wa wrex isbasisg wex anbi2i bitri
      dfss3 elin df-pw abeq2i exbii eluni df-rex 3bitr4i ralbii 2ralbii syl6bb
      an12 ) EFGEHGAIBIJZEUMKZJZLZMZBENAENCIZDIZGZUSUMMZOZDEPZCUMNZBENAENABEFQU
      QVDABEEUQURUPGZCUMNVDCUMUPUAVEVCCUMUTUSUOGZOZDRUSEGZVBOZDRVEVCVGVIDVGUTVH
      VAOZOVIVFVJUTVFVHUSUNGZOVJUSEUNUBVKVAVHVADUNDUMUCUDSTSUTVHVAULTUEDURUOUFV
      BDEUGUHUITUJUK $.
      $( [17-Jul-2006] $)

    $( Express the predicate " ` B ` is a basis for a topology."  Definition of
       basis in [Munkres] p. 78. $)
    isbasis3g $p |- ( B e. C -> ( B e. TopBases <-> ( A. x e. B x C_ U. B /\
     A. x e. U. B E. y e. B x e. y /\ A. x e. B A. y e. B A. z e. ( x i^i y )
     E. w e. B ( z e. w /\ w C_ ( x i^i y ) ) ) ) ) $=
      ( wcel ctb cv cin wss wa wrex wral cuni w3a isbasis2g elssuni rgen eluni2
      biimpi pm3.2i biantrur df-3an bitr4i syl6bb ) EFGEHGCIDIZGUGAIZBIZJZKLDEM
      CUJNBENAENZUHEOZKZAENZUHUIGBEMZAULNZUKPZABCDEFQUKUNUPLZUKLUQURUKUNUPUMAEU
      HERSUOAULUHULGUOBUHETUASUBUCUNUPUKUDUEUF $.
      $( [17-Jul-2006] $)
  $}

  ${
    $d w x A $.  $d w x y z B $.  $d w x y z C $.  $d w x y z D $.
    $( Property of a basis. $)
    basis1 $p |- ( ( B e. TopBases /\ C e. B /\ D e. B ) ->
                    ( C i^i D ) C_ U. ( B i^i ~P ( C i^i D ) ) ) $=
      ( vx vy ctb wcel cin cpw cuni wss cv wral wa isbasisg pweqd ineq2d unieqd
      wceq sseq12d ibi ineq1 ineq2 rcla42v syl5com 3impib ) AFGZBAGZCAGZBCHZAUJ
      IZHZJZKZUGDLZELZHZAUQIZHZJZKZEAMDAMZUHUINUNUGVBDEAFOUAVAUNBUPHZAVCIZHZJZK
      DEBCAAUOBSZUQVCUTVFUOBUPUBZVGUSVEVGURVDAVGUQVCVHPQRTUPCSZVCUJVFUMUPCBUCZV
      IVEULVIVDUKAVIVCUJVJPQRTUDUEUF $.
      $( [16-Jul-2006] $)

    $( Property of a basis. $)
    basis2 $p |- ( ( ( B e. TopBases /\ C e. B ) /\ ( D e. B /\
          A e. ( C i^i D ) ) ) -> E. x e. B ( A e. x /\ x C_ ( C i^i D ) ) ) $=
      ( vw vy vz ctb wcel cin cv wss wa wrex wi wral wceq rexbidv syl isbasis2g
      ibi wb ineq1 sseq2 anbi2d raleqbi1dv ineq2 rcla42v anbi1d rcla4cv syl6com
      eleq1 exp3a imp43 ) CIJZDCJZECJZBDEKZJZBALZJZVAUSMZNZACOZUPUQURUTVEPZUPFL
      ZVAJZVAGLZHLZKZMZNZACOZFVKQZHCQGCQZUQURNZVFPUPVPGHFACIUAUBVQVPVHVCNZACOZF
      USQZVFVOVTVHVADVJKZMZNZACOZFWAQZGHDECCVIDRVKWARZVOWEUCVIDVJUDVNWDFVKWAWFV
      MWCACWFVLWBVHVKWAVAUEUFSUGTVJERWAUSRZWEVTUCVJEDUHWDVSFWAUSWGWCVRACWGWBVCV
      HWAUSVAUEUFSUGTUIVSVEFBUSVGBRZVRVDACWHVHVBVCVGBVAUMUJSUKULTUNUO $.
      $( [17-Jul-2006] $)
  $}

  ${
    $d B w x y z $.  $d C w x y z $.
    $( If a set is closed under finite intersection, then it is a basis for a
       topology.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    fiinbas $p |- ( ( B e. C /\ A. x e. B A. y e. B ( x i^i y ) e. B )
                       -> B e. TopBases ) $=
      ( vz vw wcel cv cin wral ctb wss wa wrex wi ssid wceq eleq2 sseq1 ralimdv
      anbi12d rcla4ev mpanr2 r19.21aiva a1i isbasis2g sylibrd imp ) CDGZAHBHIZC
      GZBCJZACJZCKGZUIUMEHZFHZGZUPUJLZMZFCNZEUJJZBCJZACJUNUIULVBACUIUKVABCUKVAO
      UIUKUTEUJUKUOUJGZUJUJLZUTUJPUSVCVDMFUJCUPUJQUQVCURVDUPUJUORUPUJUJSUAUBUCU
      DUETTABEFCDUFUGUH $.
      $( [18-Apr-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d y x z A $.  $d t u v w x y z B $.  $d x y C $.  $d w x y z J $.
    $( The topology generated by a basis. $)
    tgval $p |- ( B e. TopBases -> ( topGen ` B ) =
                  { x | x C_ U. ( B i^i ~P x ) } ) $=
      ( vy ctb wcel cv cpw cin cuni wss cab cvv ctg cfv wa uniexg abssexg uniin
      wceq sstr mpan2 ssin sylibr ss2abi ssexg mpan 3syl ineq1 unieqd df-topgen
      sseq2d abbidv fvmptg mpdan ) BDEZAFZBUPGZHZIZJZAKZLEZBMNVASUOBIZLEUPVCJUP
      UQIZJZOZAKZLEZVBBDPVEAVCLQVAVGJVHVBUTVFAUTUPVCVDHZJZVFUTUSVIJVJBUQRUPUSVI
      TUAUPVCVDUBUCUDVAVGLUEUFUGCBUPCFZUQHZIZJZAKVADLMVKBSZVNUTAVOVMUSUPVOVLURV
      KBUQUHUIUKULCAUJUMUN $.
      $( [10-Nov-2013] $) $( [16-Jul-2006] $)

    $( Definition of a topology generated by a basis in [Munkres] p. 78.  Later
       we show (in ~ tgcl ) that ` ( topGen `` B ) ` is indeed a topology (on
       ` U. B ` ; see ~ unitg ). $)
    tgval2 $p |- ( B e. TopBases -> ( topGen ` B ) =
         { x | ( x C_ U. B /\ A. y e. x E. z e. B ( y e. z /\ z C_ x ) ) } ) $=
      ( ctb wcel ctg cfv cv cpw cuni wss cab wrex wral dfss3 ralbii wex anbi2i
      wa tgval inss1 uniss ax-mp sseli pm4.71ri r19.26 3bitri bitri exbii eluni
      elin an12 df-rex 3bitr4i visset rexbii bitr2i anbi12i bitr4i abbii syl6eq
      cin elpw ) DEFDGHAIZDVEJZVCZKZLZAMVEDKZLZBIZCIZFZVMVELZTZCDNZBVEOZTZAMADU
      AVIVSAVIVLVJFZBVEOZVLVHFZBVEOZTZVSVIWCVTWBTZBVEOWDBVEVHPWBWEBVEWBVTVHVJVL
      VGDLVHVJLDVFUBVGDUCUDUEUFQVTWBBVEUGUHVKWAVRWCBVEVJPVQWBBVEWBVNVMVFFZTZCDN
      ZVQVNVMVGFZTZCRVMDFZWGTZCRWBWHWJWLCWJVNWKWFTZTWLWIWMVNVMDVFULSVNWKWFUMUIU
      JCVLVGUKWGCDUNUOWGVPCDWFVOVNVMVECUPVDSUQURQUSUTVAVB $.
      $( [15-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg $p |- ( B e. TopBases -> ( A e. ( topGen ` B ) <->
           A C_ U. ( B i^i ~P A ) ) ) $=
      ( vx ctb wcel ctg cfv cv cpw cin cuni wss cab tgval eleq2d elisset adantl
      cvv inex1g uniexg syl ssexg sylan2 ancoms wceq pweq ineq2d unieqd sseq12d
      id elabg pm5.21nd bitrd ) BDEZABFGZEACHZBUPIZJZKZLZCMZEZABAIZJZKZLZUNUOVA
      ACBNOUNVBVFAREZVBVGUNAVAPQVFUNVGUNVFVEREZVGUNVDREVHBVCDSVDRTUAAVERUBUCUDU
      TVFCARUPAUEZUPAUSVEVIUJVIURVDVIUQVCBUPAUFUGUHUIUKULUM $.
      $( [16-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg2 $p |- ( B e. TopBases -> ( A e. ( topGen ` B ) <->
           ( A C_ U. B /\ A. x e. A E. y e. B ( x e. y /\ y C_ A ) ) ) ) $=
      ( vz ctb wcel ctg cfv cv cuni wss wrex wral cab tgval2 eleq2d cvv elisset
      wa adantl uniexg ssexg sylan2 ancoms adantrr wceq sseq1 anbi2d raleqbi1dv
      sseq2 rexbidv anbi12d elabg pm5.21nd bitrd ) DFGZCDHIZGCEJZDKZLZAJBJZGZVB
      USLZTZBDMZAUSNZTZEOZGZCUTLZVCVBCLZTZBDMZACNZTZUQURVICEABDPQUQVJVPCRGZVJVQ
      UQCVISUAUQVKVQVOVKUQVQUQVKUTRGVQDFUBCUTRUCUDUEUFVHVPECRUSCUGZVAVKVGVOUSCU
      TUHVFVNAUSCVRVEVMBDVRVDVLVCUSCVBUKUIULUJUMUNUOUP $.
      $( [15-Jul-2006] $)

    $( Property of a member of a topology generated by a basis. $)
    tg1 $p |- ( ( B e. TopBases /\ A e. ( topGen ` B ) ) ->
              A C_ U. B ) $=
      ( vx vy ctb wcel ctg cfv cuni wss cv wa wrex wral eltg2 simprbda ) BEFABG
      HFABIJCKDKZFQAJLDBMCANCDABOP $.
      $( [20-Jul-2006] $)

    $( Property of a member of a topology generated by a basis. $)
    tg2 $p |- ( ( B e. TopBases /\ A e. ( topGen ` B ) /\ C e. A ) ->
               E. x e. B ( C e. x /\ x C_ A ) ) $=
      ( vy ctb wcel ctg cfv cv wss wa wrex cuni wral wi eltg2 wceq eleq1 anbi1d
      rexbidv rcla4cv adantl syl6bi 3imp ) CFGZBCHIGZDBGZDAJZGZUIBKZLZACMZUFUGB
      CNKZEJZUIGZUKLZACMZEBOZLUHUMPZEABCQUSUTUNURUMEDBUODRZUQULACVAUPUJUKUODUIS
      TUAUBUCUDUE $.
      $( [20-Jul-2006] $)

    $( A member of a basis is a subset of the topology it generates. $)
    bastg $p |- ( B e. TopBases -> B C_ ( topGen ` B ) ) $=
      ( vx ctb wcel ctg cfv cv cpw cin cuni wss wa basis1 3anidm23 inidm ineq2i
      pweqi unieqi 3sstr3g ex eltg sylibrd ssrdv ) ACDZBAAEFZUDBGZADZUFAUFHZIZJ
      ZKZUFUEDUDUGUKUDUGLUFUFIZAULHZIZJZUFUJUDUGULUOKAUFUFMNUFOZUNUIUMUHAULUFUP
      QPRSTUFAUAUBUC $.
      $( [16-Jul-2006] $)

    $( The topology generated by a basis ` B ` is a topology on ` U. B ` .
       Importantly, this theorem means that we don't have to specify separately
       the base set for the topological space generated by a basis.  In other
       words, any member of the class ` TopBases ` completely specifies the
       basis it corresponds to. $)
    unitg $p |- ( B e. TopBases -> U. ( topGen ` B ) = U. B ) $=
      ( vx vy ctb wcel ctg cfv cuni cv wrex eluni2 cpw cin wss eltg inss1 uniss
      wi ax-mp sseld sstr mpan2 syl6bi r19.23adv syl5bi bastg syl impbid eqrdv
      ) ADEZBAFGZHZAHZUJBIZULEZUNUMEZUOUNCIZEZCUKJUJUPCUNUKKUJURUPCUKUJUQUKEUQA
      UQLZMZHZNZURUPRUQAOVBUQUMUNVBVAUMNZUQUMNUTANVCAUSPUTAQSUQVAUMUAUBTUCUDUEU
      JUMULUNUJAUKNUMULNAUFAUKQUGTUHUI $.
      $( [16-Jul-2006] $)

    $( Show that a basis generates a topology.  Remark in [Munkres] p. 79. $)
    tgcl $p |- ( B e. TopBases -> ( topGen ` B ) e. Top ) $=
      ( vu vv vx vy vt vz vw wcel cv wss cuni wi wral wa wrex eltg2 ra4 ex syl
      ctb ctg cfv wal ctop uniss adantl unitg adantr sseqtrd eluni2 ssel2 simpr
      wceq syl6bi imp31 an32s sylan2 an42s elssuni sstr2 syl5com anim2d reximdv
      cin syl6 ad2antrl mpd r19.23adv syl5bi r19.21aiv jca sylibrd 19.21aiv tg1
      exp32 ssinss1 adantrr simplbda im2anan9 elin reeanv 3imtr4g anandis ss2in
      biimpri anim12i an4s basis2 adantllr adantrrr ad2antll sylanr2 r19.23adva
      com12 a2d imp syldan r19.21aivv cvv wb fvex istopg ax-mp sylanbrc ) AUAIZ
      BJZAUBUCZKZXGLZXHIZMZBUDZXGCJZVEZXHIZCXHNBXHNZXHUEIZXFXLBXFXIXJALZKZDJZEJ
      ZIZYBXJKZOZEAPZDXJNZOZXKXFXIYHXFXIOZXTYGYIXJXHLZXSXIXJYJKXFXGXHUFUGXFYJXS
      UNXIAUHUIUJYIYFDXJYAXJIYAFJZIZFXGPYIYFFYAXGUKYIYLYFFXGYIYKXGIZYLYFYIYMYLO
      OYCYBYKKZOZEAPZYFXFYLXIYMYPXIYMOXFYLOYKXHIZYPXGXHYKULXFYQYLYPXFYQYLYPXFYQ
      YPDYKNZYLYPMXFYQYKXSKZYROYRDEYKAQYSYRUMUOYPDYKRVFUPUQURUSYMYPYFMYIYLYMYOY
      EEAYMYNYDYCYMYKXJKYNYDYKXGUTYBYKXJVAVBVCVDVGVHVPVIVJVKVLSDEXJAQVMVNXFXPBC
      XHXHXFXGXHIZXNXHIZOZXOXSKZYLYKXOKZOZFAPZDXONZOZXPXFUUBUUHXFUUBOZUUCUUGXFY
      TUUCUUAXFYTOZXGXSKZUUCXGAVOXGXNXSVQTVRUUIUUFDXOXFUUBYAXOIZYAGJZIZUUMXGKZO
      ZYAHJZIZUUQXNKZOZOZHAPZGAPZMZUULUUFMZXFYTUUAUVDUUJXFUUAOZOYAXGIZYAXNIZOUU
      PGAPZUUTHAPZOUULUVCUUJUVGUVIUVFUVHUVJUUJUVIDXGNZUVGUVIMXFYTUUKUVKDGXGAQVS
      UVIDXGRTUVFUVJDXNNZUVHUVJMXFUUAXNXSKUVLDHXNAQVSUVJDXNRTVTYAXGXNWAUUPUUTGH
      AAWBWCWDXFUVDUVEXFUULUVCUUFXFUULUVCUUFMXFUULOZUVBUUFGAUVMUUMAIZOZUVAUUFHA
      UVOUUQAIZUVAUUFUVAUVOUVPYAUUMUUQVEZIZUVQXOKZOZUUFUUNUURUUOUUSUVTUUNUUROZU
      VRUUOUUSOUVSUVRUWAYAUUMUUQWAWFUUMXGUUQXNWEWGWHUVOUVPUVTOOYLYKUVQKZOZFAPZU
      UFUVOUVPUVRUWDUVSXFUVNUVPUVROUWDUULFYAAUUMUUQWIWJWKUVTUWDUUFMZUVOUVPUVSUW
      EUVRUVSUWCUUEFAUVSUWBUUDYLUWBUVSUUDYKUVQXOVAWOVCVDUGWLVHWMVPVIWNSWPWQWRVK
      VLSDFXOAQVMWSXHWTIXRXMXQOXAAUBXBBCWTXHXCXDXE $.
      $( [17-Jul-2006] $)

    $( Alternate expression for the topology generated by a basis.  Lemma 2.1
       of [Munkres] p. 80. $)
    tgval3 $p |- ( B e. TopBases ->
               ( topGen ` B ) = { x | E. y ( y C_ B /\ x = U. y ) } ) $=
      ( vz vw wcel cv wss cuni wceq wa wex wrex wi bitri adantrr ax-mp ax-17 ex
      cvv ctb ctg cfv cab simpr abssi wral eltg2 simplbda elequ1 anbi1d rexbidv
      rcla4cv df-rex an12 ancom anbi2i exbii syl6ib syl impcom 19.23aiv impbid1
      ssel eluniab syl6bbr eqrdv abssexg hbab1 hbss hbuni hbeq hban sseq1 unieq
      visset eqeq2d anbi12d cla4egf sylancr simprr ctop tgcl adantr sstr sylan2
      bastg ancoms uniopn syl11anc eqeltrd 19.23adv impbid abbi2dv ) CUAFZBGZCH
      ZAGZWPIZJZKZBLZACUBUCZWOWRXCFZXBWOXDXBWOXDKZWPWRHZWPCFZKZBUDZCHZWRXIIZJZX
      BXHBCXFXGUEUFXEDWRXKXEDGZWRFZXMWPFZXHKZBLZXMXKFXEXNXQXEEGZWPFZXFKZBCMZEWR
      UGZXNXQNWOXDWRCIHYBEBWRCUHUIYBXNXOXFKZBCMZXQYAYDEXMWRXRXMJZXTYCBCYEXSXOXF
      EDBUJUKULUMYDXGYCKZBLXQYCBCUNYFXPBYFXOXGXFKZKXPXGXOXFUOYGXHXOXGXFUPUQOURO
      USUTXPXNBXOXFXNXGXFXOXNWPWRXMVDVAPVBVCXHBXMVEVFVGXITFZXJXLKZXBNWRTFYHAVPX
      GBWRTVHQXAYIBDXITXHBDVIZXJXLBBDXICYJXMCFBRVJBDDWRXKXNBRBDXIYJVKVLVMWPXIJZ
      WQXJWTXLWPXICVNYKWSXKWRWPXIVOVQVRVSQVTSWOXAXDBWOXAXDWOXAKWRWSXCWOWQWTWAWO
      WQWSXCFZWTWOWQKXCWBFZWPXCHZYLWOYMWQCWCWDWQWOYNWOWQCXCHYNCWGWPCXCWEWFWHWPX
      CWIWJPWKSWLWMWN $.
      $( [17-Jul-2006] $)

    $( Membership in a topology generated by a basis. $)
    eltg3 $p |- ( B e. TopBases -> ( A e. ( topGen ` B ) <->
           E. x ( x C_ B /\ A = U. x ) ) ) $=
      ( vy ctb wcel ctg cfv cv wss cuni wceq wa wex cab tgval3 eleq2d cvv uniex
      visset eleq1 mpbiri adantl 19.23aiv eqeq1 anbi2d exbidv elab3 syl6bb ) CE
      FZBCGHZFBAIZCJZDIZULKZLZMZANZDOZFUMBUOLZMZANZUJUKUSBDACPQURVBDBVABRFZAUTV
      CUMUTVCUORFULATSBUORUAUBUCUDUNBLZUQVAAVDUPUTUMUNBUOUEUFUGUHUI $.
      $( [15-Jul-2006] $)

    $( A topology is its own basis. $)
    topbas $p |- ( J e. Top -> J e. TopBases ) $=
      ( vz vw vx vy ctop wcel ctb cv cin wss wrex wral inopn 3expb adantr simpr
      wa ssid jctir wceq eleq2 sseq1 anbi12d rcla4ev exp31 r19.21adv r19.21aivv
      syl11anc isbasis2g mpbird ) AFGZAHGBIZCIZGZUNDIZEIZJZKZRZCALZBURMZEAMDAMU
      LVBDEAAULUPAGZUQAGZRZVABURULVEUMURGZVAULVERZVFRZURAGZVFURURKZRZVAVGVIVFUL
      VCVDVIUPUQANOPVHVFVJVGVFQURSTUTVKCURAUNURUAUOVFUSVJUNURUMUBUNURURUCUDUEUI
      UFUGUHDEBCAFUJUK $.
      $( [17-Jul-2006] $)

    $( A topology is its own basis. $)
    tgtop $p |- ( J e. Top -> ( topGen ` J ) = J ) $=
      ( vx vy ctop wcel ctg cfv cv wss cuni wceq wa wex ctb wb topbas eltg3 syl
      simpr uniopn adantr eqeltrd expl 19.23adv sylbid ssrdv bastg eqssd ) ADEZ
      AFGZAUIBUJAUIBHZUJEZCHZAIZUKUMJZKZLZCMZUKAEZUIANEZULUROAPZCUKAQRUIUQUSCUI
      UNUPUSUIUNLZUPLUKUOAVBUPSVBUOAEUPUMATUAUBUCUDUEUFUIUTAUJIVAAUGRUH $.
      $( [18-Jul-2006] $)

    $( Membership in a topology, expressed without quantifiers. $)
    eltop $p |- ( J e. Top -> ( A e. J <-> A C_ U. ( J i^i ~P A ) ) ) $=
      ( ctop wcel ctg cfv cpw cin cuni wss eleq2d ctb wb topbas eltg syl bitr3d
      tgtop ) BCDZABEFZDZABDABAGHIJZSTBABRKSBLDUAUBMBNABOPQ $.
      $( [19-Jul-2006] $)

    $( Membership in a topology. $)
    eltop2 $p |- ( J e. Top -> ( A e. J <->
               ( A C_ U. J /\ A. x e. A E. y e. J ( x e. y /\ y C_ A ) ) ) ) $=
      ( ctop wcel ctg cfv cuni wss cv wa wrex wral tgtop eleq2d wb topbas eltg2
      ctb syl bitr3d ) DEFZCDGHZFZCDFCDIJAKBKZFUFCJLBDMACNLZUCUDDCDOPUCDTFUEUGQ
      DRABCDSUAUB $.
      $( [23-Aug-2007] $) $( [19-Jul-2006] $)

    $( Membership in a topology. $)
    eltop3 $p |- ( J e. Top -> ( A e. J <-> E. x ( x C_ J /\ A = U. x ) ) ) $=
      ( ctop wcel ctg cfv cv wss cuni wceq wa wex tgtop eleq2d ctb topbas eltg3
      wb syl bitr3d ) CDEZBCFGZEZBCEAHZCIBUEJKLAMZUBUCCBCNOUBCPEUDUFSCQABCRTUA
      $.
      $( [29-Mar-2008] $) $( [19-Jul-2006] $)
  $}

  $( The topology generator function is idempotent. $)
  tgidm $p |- ( B e. TopBases ->
               ( topGen ` ( topGen ` B ) ) = ( topGen ` B ) ) $=
    ( ctb wcel ctg cfv ctop wceq tgcl tgtop syl ) ABCADEZFCKDEKGAHKIJ $.
    $( [18-Jul-2006] $)

  $( Two ways to express that a basis is a topology. $)
  bastop $p |- ( B e. TopBases -> ( B e. Top <-> ( topGen ` B ) = B ) ) $=
    ( ctb wcel ctop ctg cfv wceq tgtop wa simpr tgcl adantr eqeltrrd ex impbid2
    ) ABCZADCZAEFZAGZAHPSQPSIRADPSJPRDCSAKLMNO $.
    $( [14-Aug-2007] $) $( [18-Jul-2006] $)

  $( The topology generation function is one-to-one when applied to completed
     topologies. $)
  tgtop11 $p |- ( ( J e. Top /\ K e. Top /\ ( topGen ` J ) = ( topGen ` K ) )
                 -> J = K ) $=
    ( ctop wcel ctg cfv wceq w3a simp3 tgtop 3ad2ant1 3ad2ant2 3eqtr3d ) ACDZBC
    DZAEFZBEFZGZHPQABNORINOPAGRAJKONQBGRBJLM $.
    $( [11-Jun-2007] $) $( [18-Jul-2006] $)

  $( The singleton of the empty set is the only topology possible for an empty
     underlying set. $)
  0top $p |- ( J e. Top -> ( U. J = (/) <-> J = { (/) } ) ) $=
    ( ctop wcel c0 csn wceq wo cuni olc wn 0opn n0i syl pm2.21d idd impbid2 wss
    jaod uni0b sssn bitr2i syl6rbb ) ABCZADEZFZADFZUEGZAHDFZUCUEUGUEUFIUCUFUEUE
    UCUFUEUCDACUFJAKADLMNUCUEORPUHAUDQUGASADTUAUB $.
    $( [24-Mar-2008] $) $( [9-Sep-2006] $)

  ${
    $d u v w x y z B $.  $d u v w x y z C $.  $d v w x y z J $.
    $( Subset relation for generated topologies. $)
    tgss $p |- ( ( B e. TopBases /\ C e. TopBases /\ B C_ C ) ->
               ( topGen ` B ) C_ ( topGen ` C ) ) $=
      ( vx ctb wcel wss w3a ctg cfv cv cpw cin cuni wi ssrin uniss syl sstr2 wb
      eltg syl5com 3ad2ant3 3ad2ant1 3ad2ant2 3imtr4d ssrdv ) ADEZBDEZABFZGZCAH
      IZBHIZUJCJZAUMKZLZMZFZUMBUNLZMZFZUMUKEZUMULEZUIUGUQUTNUHUIUPUSFZUQUTUIUOU
      RFVCABUNOUOURPQUMUPUSRUAUBUGUHVAUQSUIUMATUCUHUGVBUTSUIUMBTUDUEUF $.
      $( [7-May-2007] $)

    $( A criterion for determining whether one topology is finer than another,
       based on a comparison of their bases.  Lemma 2.2 of [Munkres] p. 80. $)
    tgss2 $p |- ( ( B e. TopBases /\ C e. TopBases /\ U. B = U. C ) ->
                    ( ( topGen ` B ) C_ ( topGen ` C ) <->
     A. x e. U. B A. y e. B ( x e. y -> E. z e. C ( x e. z /\ z C_ y ) ) ) ) $=
      ( vu vw vv ctb wcel cuni wceq ctg wss cv wa wrex wi wral adantrl w3a sstr
      cfv simpllr bastg sylan sseld imp adantllr adantrr simprr syl111anc exp43
      tg2 r19.21advv 3adant3 adantll simpll sseqtrd 3expa elssuni elequ1 anbi1d
      rexbidv imbi12d elequ2 sseq2 anbi2d rcla42v ex com4l imp32 adantrrr an12s
      tg1 sstr2 com12 anim2d reximdv adantl ad2antll exp32 r19.23adv syl5 exp4c
      mpd imp42 r19.21aiva jca eltg2 syl5ibr 3imp 3comr expdimp ssrdv impbid )
      DIJZEIJZDKZEKZLZUAZDMUCZEMUCZNZAOZBOZJZXFCOZJZXIXGNZPZCEQZRZBDSAWSSZWQWRX
      EXORXAWQWRPZXEXNABWSDXPXEXFWSJZXGDJZPZXHXMXPXEPZXSXHPZPWRXGXDJZXHXMWQWRXE
      YAUDXTXSYBXHXTXRYBXQWQXEXRYBWRWQXEPZXRYBYCDXDXGWQDXCNXEDXDNDUEDXCXDUBUFUG
      UHUITUJXTXSXHUKCXGEXFUNULUMUOUPXBXOXEXBXOPFXCXDXBXOFOZXCJZYDXDJZWRXAWQXOY
      EPZYFRZWRXAWQYHWRXAWQYGYFXAWQPZYGPZYFWRYDWTNZGOZXIJZXIYDNZPZCEQZGYDSZPYJY
      KYQYIYEYKXOYIYEPYDWSWTWQYEYDWSNXAYDDVOUQXAWQYEURUSTWQYGYQXAWQYGPYPGYDWQXO
      YEYLYDJZYPXOWQYEYRYPRRXOWQYEYRYPWQYEPYRPYLHOZJZYSYDNZPZHDQZXOYPWQYEYRUUCH
      YDDYLUNUTXOUUBYPHDXOYSDJZUUBYPXOUUDUUBPPYMXIYSNZPZCEQZYPUUDXOUUBUUGUUDXOU
      UBPPYLWSJZUUGUUDUUBUUHXOUUDYTUUHUUAUUDYTUUHUUDYSWSYLYSDVAUGUHUJTUUDXOYTUU
      HUUGRZUUAUUDXOYTUUIUUHUUDXOYTUUGUUHUUDXOYTUUGRZRXNUUJYLXGJZYMXKPZCEQZRABY
      LYSWSDXFYLLZXHUUKXMUUMAGBVBUUNXLUULCEUUNXJYMXKAGCVBVCVDVEXGYSLZUUKYTUUMUU
      GBHGVFUUOUULUUFCEUUOXKUUEYMXGYSXIVGVHVDVEVIVJVKVLVMWFVNUUBUUGYPRZXOUUDUUA
      UUPYTUUAUUFYOCEUUAUUEYNYMUUEUUAYNXIYSYDVPVQVRVSVTWAWFWBWCWDWEVQWGWHUQWIGC
      YDEWJWKWEWLWMWNWOVJWP $.
      $( [20-Jul-2006] $)

    $( A criterion for determining whether one topology is finer than another.
       Lemma 2.2 of [Munkres] p. 80 using abbreviations. $)
    tgss3 $p |- ( ( B e. TopBases /\ C e. TopBases /\ U. B = U. C ) ->
                    ( ( topGen ` B ) C_ ( topGen ` C ) <->
                  A. x e. B ( U. B i^i x ) C_ U. ( C i^i ~P x ) ) ) $=
      ( vy vz ctb wcel cuni ctg cfv wss cv wa wi wral cin wal elin wex bitri
      wceq w3a wrex tgss2 ralcom anass anbi1i ancom bitr3i visset elpw 3bitr3ri
      cpw anbi2i exbii df-rex eluni 3bitr4ri imbi12i impexp albii df-ral ralbii
      dfss2 syl6bb ) BFGCFGBHZCHUAUBBIJCIJKDLZALZGZVGELZGZVJVHKZMZECUCZNZABODVF
      OZVFVHPZCVHUMZPZHZKZABOZDAEBCUDVPVODVFOZABOWBVODAVFBUEWCWAABVGVQGZVGVTGZN
      ZDQVGVFGZVONZDQWAWCWFWHDWFWGVIMZVNNWHWDWIWEVNVGVFVHRVJCGZVMMZESVKVJVSGZMZ
      ESVNWEWKWMEWJVJVRGZMZVKMZWJWNVKMZMWMWKWJWNVKUFWPWLVKMWMWLWOVKVJCVRRUGWLVK
      UHUIWQVMWJWQVKWNMVMWNVKUHWNVLVKVJVHEUJUKUNTUNULUOVMECUPEVGVSUQURUSWGVIVNU
      TTVADVQVTVDVODVFVBURVCTVE $.
      $( [3-Apr-2008] $) $( [20-Jul-2006] $)

    $( Given a topology ` J ` , show that a subset ` B ` satisfying the third
       antecedent is a basis for it.  Lemma 2.3 of [Munkres] p. 81. $)
    basgen2 $p |- ( ( J e. Top /\ B C_ J /\ A. x e. J A. y e. x E. z e. B
            ( y e. z /\ z C_ x ) ) -> ( B e. TopBases /\ ( topGen ` B ) = J ) )
        $=
      ( vw vv ctop wcel wss cv wa wrex wral wceq wi imp wb cuni ralimi ctb ssel
      ctg cfv anim12d inopn 3expb sylan2 anassrs sseq2 anbi2d raleqbi1dv rcla4v
      cin rexbidv syl r19.21advva cvv ssexg ancoms isbasis2g sylibrd wex adantl
      eltg3 simpr uniopn adantr eqeltrd exp42 com23 imp4b 19.23adv sylbid ssrdv
      sstr syldan tgtop ad2antrr topbas eqid topopn simpl reximi elequ1 rcla4cv
      raleq eluni2 syl6ibr syl6 adantlr uniss ad2antlr eqssd jca31 ax-1 ralimi2
      syl2im ralcom sylib tgss2 3expa biimpar syl11anc eqsstr3d jca 3impa ) EHI
      ZDEJZBKZCKZIZXKAKZJZLZCDMZBXMNZAENZDUAIZDUCUDZEOZLXHXILZXRLZXSYAYBXRXSYBX
      RXLXKFKZGKZUNZJZLZCDMZBYFNZGDNFDNZXSYBXRYJFGDDYBYDDIZYEDIZLZLYFEIZXRYJPXH
      XIYNYOXIYNLXHYDEIZYEEIZLZYOXIYNYRXIYLYPYMYQDEYDUBDEYEUBUEQXHYPYQYOYDYEEUF
      UGUHUIXQYJAYFEXPYIBXMYFXMYFOZXOYHCDYSXNYGXLXMYFXKUJUKUOULUMUPUQYBDURIZXSY
      KRXIXHYTDEHUSUTFGBCDURVAUPVBQZYCXTEYBXRXSXTEJUUAYBXSLZAXTEUUBXMXTIZXJDJZX
      MXJSZOZLZBVCZXMEIZXSUUCUUHRYBBXMDVEVDYBUUHUUIPXSYBUUGUUIBXHXIUUDUUFUUIXHU
      UDXIUUFUUIPXHUUDXIUUFUUIXHUUDXILZLZUUFLXMUUEEUUKUUFVFUUKUUEEIZUUFUUJXHXJE
      JUULXJDEVPXJEVGUHVHVIVJVKVLVMVHVNVOVQYCEEUCUDZXTXHUUMEOXIXREVRVSYCEUAIZXS
      LESZDSZOZLZXJXMIXPPZAENBUUONZUUMXTJZYCUUNXSUUQXHUUNXIXREVTVSUUAYCUUOUUPXH
      XRUUOUUPJXIXHXRLFUUOUUPXHXRYDUUOIZYDUUPIZPZXHUUOEIZXRXLCDMZBXMNZAENZUVDEU
      UOUUOWAWBXQUVGAEXPUVFBXMXOXLCDXLXNWCWDTTUVEUVHUVFBUUONZUVDUVGUVIAUUOEUVFB
      XMUUOWGUMUVIUVBYDXKIZCDMZUVCUVFUVKBYDUUOXJYDOXLUVJCDBFCWEUOWFCYDDWHWIWJWR
      QVOWKXIUUPUUOJXHXRDEWLWMWNWOXRUUTYBXRUUSBUUONZAENUUTXQUVLAEXPUUSBXMUUOUUS
      XJUUOIWPWQTUUSABEUUOWSWTVDUURUVAUUTUUNXSUUQUVAUUTRBACEDXAXBXCXDXEWNXFXG
      $.
      $( [20-Jul-2006] $)

    $( Given a topology ` J ` , show that a subset ` B ` satisfying the third
       antecedent is a basis for it.  Lemma 2.3 of [Munkres] p. 81 using
       abbreviations. $)
    basgen $p |- ( ( J e. Top /\ B C_ J /\ A. x e. J
          x C_ U. ( B i^i ~P x ) ) -> ( B e. TopBases /\ ( topGen ` B ) = J ) )
        $=
      ( vy vz cv cpw cin cuni wss wral ctop wcel wa ctb wex anbi2i bitri ralbii
      wrex ctg cfv wceq dfss3 elin visset elpw an12 exbii eluni 3bitr4i basgen2
      df-rex syl3an3b ) AFZBUOGZHZIZJZACKCLMBCJDFZEFZMZVAUOJZNZEBTZDUOKZACKBOMB
      UAUBCUCNUSVFACUSUTURMZDUOKVFDUOURUDVGVEDUOVBVAUQMZNZEPVABMZVDNZEPVGVEVIVK
      EVIVBVJVCNZNVKVHVLVBVHVJVAUPMZNVLVABUPUEVMVCVJVAUOEUFUGQRQVBVJVCUHRUIEUTU
      QUJVDEBUMUKSRSADEBCULUN $.
      $( [16-Apr-2008] $) $( [22-Jul-2006] $)

    $( Conditions that determine the equality of two generated topologies. $)
    2basgen $p |- ( ( ( B e. TopBases /\ C e. TopBases ) /\ ( B C_ C /\ C C_
                  ( topGen ` B ) ) ) -> ( topGen ` B ) = ( topGen ` C ) ) $=
      ( ctb wcel wa wss ctg cfv tgss 3expa adantrr ctop tgcl topbas syl syl3an2
      3com12 adantrl wceq tgidm ad2antrr sseqtrd eqssd ) ACDZBCDZEZABFZBAGHZFZE
      ZEZUHBGHZUFUGUHULFZUIUDUEUGUMABIJKUKULUHGHZUHUFUIULUNFZUGUDUEUIUOUEUDUIUO
      UDUEUHCDZUIUOUDUHLDUPAMUHNOBUHIPQJRUDUNUHSUEUJATUAUBUC $.
      $( [8-May-2007] $)
  $}

  ${
    $d u v w x y z B $.  $d v w x y z J $.
    $( A subset of a topology is a basis for the topology iff every member of
       the topology is a union of members of the basis.  We use the
       idiom " ` B e. TopBases /\ ( topGen `` B ) = J ` " to express " ` B ` is
       a basis for topology ` J ` ," since we do not have a separate notation
       for this.  Definition 15.35 of [Schechter] p. 428. $)
    bastop1 $p |- ( ( J e. Top /\ B C_ J ) -> ( ( B e. TopBases /\
      ( topGen ` B ) = J ) <-> A. x e. J E. y ( y C_ B /\ x = U. y ) ) ) $=
      ( vz vw ctop wcel wss wa ctb ctg cfv wceq cuni wral eleq2 r19.21aiv wrex
      cv wex bicomd eltg3 sylan9bbr biimpd wi eluni2 elssuni biimpar sylan2 a1d
      sseq2 ancld reximdva syl5bi sylbid ssrexv sylan9r 19.23aiv ralimi basgen2
      3expia syl5 impbid2 ) DGHZCDIZJZCKHZCLMZDNZJZBTZCIZATZVLOZNZJZBUAZADPZVKV
      RADVKVNDHZVRVJVTVNVIHZVHVRVJWAVTVIDVNQUBBVNCUCUDUERVSETZFTZHZWCVNIZJZFCSZ
      EVNPZADPZVGVKVRWHADVRWGEVNVQWBVNHZWGUFBVPWJWFFVLSZVMWGVPWJWBVOHZWKVNVOWBQ
      WLWDFVLSVPWKFWBVLUGVPWDWFFVLVPWCVLHZJZWDWEWNWEWDWMVPWCVOIZWEWCVLUHVPWEWOV
      NVOWCULUIUJUKUMUNUOUPWFFVLCUQURUSRUTVEVFWIVKAEFCDVAVBVCVD $.
      $( [2-Feb-2008] $)

    $( A version of ~ bastop1 that doesn't have ` B C_ J ` in the
       antecedent. $)
    bastop2 $p |- ( J e. Top -> ( ( B e. TopBases /\ ( topGen ` B ) = J ) <->
           ( B C_ J /\ A. x e. J E. y ( y C_ B /\ x = U. y ) ) ) ) $=
      ( ctb wcel ctg cfv wceq wa wss ctop cv cuni wex wral bastg adantr sseqtrd
      simpr pm4.71ri bastop1 pm5.32da syl5bb ) CEFZCGHZDIZJZCDKZUHJDLFZUIBMZCKA
      MUKNIJBOADPZJUHUIUHCUFDUECUFKUGCQRUEUGTSUAUJUIUHULABCDUBUCUD $.
      $( [3-Feb-2008] $)
  $}

