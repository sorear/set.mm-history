$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Ordered pair theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    opth.1 $e |- A e. V $.
    opth.2 $e |- B e. V $.
    opth.3 $e |- D e. V $.
    $( The ordered pair theorem.  If two ordered pairs are equal, their first
       elements are equal and their second elements are equal.  Exercise
       6 of [TakeutiZaring] p. 16.  Note that ` C ` is not required to be
       a set due to a peculiarity of our specific ordered pair definition. $)
    opth $p |- ( <. A , B >. = <. C , D >. <-> ( A = C /\ B = D ) ) $=
      ( cop wceq wa csn wcel cpr wo opi1 eleq2 mpbiri snex elop sylib snid
      pri1 jaoi syl elsnc eqeq1 opeq1 syl5bi df-op eqeq12i prex preqr2 sylbi
      eqcomd syl6 jcai opeq12 impbi ) ABHZCDHZIZACIZBDIZJVAVBVCVAACKZLZVBVAVDAK
      ZIZVDABMZIZNZVEVAVDUSLZVJVAVKVDUTLCDOUSUTVDPQVDABCRSTVGVEVIVGVEAVFLAEUAVD
      VFAPQVIVEAVHLABEUBVDVHAPQUCUDACEUETVAVBUTCBHZIZVCVAUSVLIVMVBUSUTVLUFACBUG
      UHVMCDMZCBMZIZVCVMVDVNMZVDVOMZIVPUTVQVLVRCDUICBUIUJVNVOVDCDUKCBUKULUMVPDB
      DBCGFULUNUDUOUPABCDUQUR $.
      $( [28-May-1995] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x D $.
    opthg.1 $e |- A e. V $.
    opthg.2 $e |- B e. V $.
    $( Ordered pair theorem. $)
    opthg $p |- ( D e. R -> ( <. A , B >. = <. C , D >. <->
               ( A = C /\ B = D ) ) ) $=
      ( vx cop cv wceq wa opeq2 eqeq2d eqeq2 anbi2d visset opth vtoclbg ) ABIZC
      HJZIZKACKZBUAKZLTCDIZKUCBDKZLHDEUADKZUBUETUADCMNUGUDUFUCUADBOPABCUAFGHQRS
      $.
      $( [30-Apr-1995] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.  $d x y T $.
    $( Ordered pair theorem.  ` C ` is not required to be a set under our
       specific ordered pair definition. $)
    opthgg $p |- ( ( A e. R /\ B e. S /\ D e. T ) ->
                 ( <. A , B >. = <. C , D >. <-> ( A = C /\ B = D ) ) ) $=
      ( vx vy wcel cop wceq wa wb cv wi opeq1 eqeq1d eqeq1 anbi1d bibi12d
      imbi2d opeq2 anbi2d visset opthg vtocl2g 3impia ) AEJBFJDGJZABKZCDKZLZACL
      ZBDLZMZNZUIHOZIOZKZUKLZUQCLZURDLZMZNZPUIAURKZUKLZUMVBMZNZPUIUPPHIABEFUQAL
      ZVDVHUIVIUTVFVCVGVIUSVEUKUQAURQRVIVAUMVBUQACSTUAUBURBLZVHUPUIVJVFULVGUOVJ
      VEUJUKURBAUCRVJVBUNUMURBDSUDUAUBUQURCDGHUEIUEUFUGUH $.
      $( [15-Oct-2005] $) $( [14-Oct-2005] $)
  $}

  ${
    $v F $.
    $v G $.
    otthg.cF $f class F $.
    otthg.cG $f class G $.
    otthg.1 $e |- A e. V $.
    otthg.2 $e |- B e. V $.
    otthg.3 $e |- R e. V $.
    $( Ordered triple theorem. $)
    otthg $p |- ( ( D e. F /\ S e. G ) ->
                ( <. <. A , B >. , R >. = <. <. C , D >. , S >. <->
                ( A = C /\ B = D /\ R = S ) ) ) $=
      ( wcel wa cop wceq w3a opex opthg anbi1d sylan9bbr df-3an syl6bbr ) DGLZF
      HLZMABNZENCDNZFNOZACOZBDOZMZEFOZMZUHUIUKPUDUGUEUFOZUKMUCULUEEUFFHABQKRUCU
      MUJUKABCDGIJRSTUHUIUKUAUB $.
      $( [1-May-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    eqvinop.1 $e |- B e. V $.
    eqvinop.2 $e |- C e. V $.
    $( A variable introduction law for ordered pairs.  Analogue of Lemma 15
       of [Monk2] p. 109. $)
    eqvinop $p |- ( A = <. B , C >. <-> E. x E. y ( A = <. x , y >. /\
                  <. x , y >. = <. B , C >. ) ) $=
      ( cv cop wceq wa wex visset opth anbi2i ancom anass 3bitr exbii 19.42v
      opeq2 eqeq2d ceqsexv opeq1 bitr2 ) CAHZBHZIZJZUHDEIZJZKZBLZALUFDJZCUFEIZJ
      ZKZALCUJJZUMUQAUMUNUGEJZUIKZKZBLUNUTBLZKUQULVABULUIUNUSKZKVCUIKVAUKVCUIUF
      UGDEAMBMGNOUIVCPUNUSUIQRSUNUTBTVBUPUNUIUPBEGUSUHUOCUGEUFUAUBUCORSUPURADFU
      NUOUJCUFDEUDUBUCUE $.
      $( [28-May-1995] $)
  $}

  ${
    $d x y z w A $.  $d z w ph $.
    $( Substitution of class ` A ` for ordered pair ` <. x , y >. ` . $)
    copsexg $p |- ( A = <. x , y >. ->
                  ( ph <-> E. x E. y ( A = <. x , y >. /\ ph ) ) ) $=
      ( vz vw cv cop wceq wa wex wb wi visset eqvinop eqcom opth bitr ceqex
      sylan9bbr sylbi anbi1i anass exbii 19.42v syl6bbr eqeq1 anbi1d 2exbidv
      bibi2d imbi12d mpbiri adantr 19.23aivv pm2.43i ) DBGZCGZHZIZAUSAJZCKBKZLZ
      USDEGZFGZHZIZVEURIZJZFKEKUSVBMZEFDUPUQBNZCNZOVHVIEFVFVIVGVFVIVGAVGAJZCKZB
      KZLZMVGAUPVCIZUQVDIZAJZCKZJZBKZVNVGVPVQJZAWALVGURVEIWBVEURPUPUQVCVDVJVKFN
      QRZVQAVSVPWAACVDSVSBVCSTUAVMVTBVMVPVRJZCKVTVLWDCVLWBAJWDVGWBAWCUBVPVQAUCR
      UDVPVRCUERUDUFVFUSVGVBVODVEURUGZVFVAVNAVFUTVLBCVFUSVGAWEUHUIUJUKULUMUNUAU
      O $.
      $( [27-Dec-1996] $)
  $}

  ${
    $d x y ps $.  $d x y A $.  $d x y B $.
    copsex2g.1 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( Implicit substitution inference for ordered pairs. $)
    copsex2g $p |- ( ( A e. C /\ B e. D ) ->
                  ( E. x E. y ( <. A , B >. = <. x , y >. /\ ph ) <-> ps ) ) $=
      ( cv wceq wex cop wa wb wcel eeanv hbe1 ax-17 hbbi hbex opeq12 copsexg
      eqcoms syl bitr3d 19.23ai sylbir elex syl2an ) CJZEKZCLZDJZFKZDLZEFMZUKUN
      MZKANZDLZCLZBOZEGPFHPUMUPNULUONZDLZCLVBULUOCDQVDVBCVABCUTCRBCSTVCVBDVABDU
      TDCUSDRUABDSTVCAVABVCURUQKAVAOZUKUNEFUBVEUQURACDUQUCUDUEIUFUGUGUHCEGUIDFH
      UIUJ $.
      $( [28-May-1995] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d x y z w D $.
    $d x y z w ps $. $d x y z w R $.  $d x y z w S $.
    copsex4g.1 $e |- ( ( ( x = A /\ y = B ) /\ ( z = C /\ w = D ) ) ->
                     ( ph <-> ps ) ) $.
    $( An implicit substitution inference for 2 ordered pairs. $)
    copsex4g $p |- ( ( ( A e. R /\ B e. S ) /\ ( C e. R /\ D e. S ) ) ->
                      ( E. x E. y E. z E. w ( ( <. A , B >. = <. x , y >. /\
                      <. C , D >. = <. z , w >. ) /\ ph ) <-> ps ) ) $=
      ( wcel wa cop cv wceq wex wb visset opthg eqcom syl5bb adantl bi2anan9
      anbi1d 4exbidv id cgsex4g bitrd ) GKNZHLNZOZIKNZJLNZOZOZGHPZCQZDQZPZRZIJP
      ZEQZFQZPZRZOZAOZFSESDSCSUTGRVAHROZVEIRVFJROZOZAOZFSESDSCSBURVJVNCDEFURVIV
      MAUNVCVKUQVHVLUMVCVKTULUMVBUSRVKVCUTVAGHLCUADUAUBUSVBUCUDUEUPVHVLTUOUPVGV
      DRVLVHVEVFIJLEUAFUAUBVDVGUCUDUEUFUGUHABVMCDEFGHIJKLVMUIMUJUK $.
      $( [5-Aug-1995] $)
  $}

  $( An ordered pair is not empty. $)
  opnz $p |- -. <. A , B >. = (/) $=
    ( csn cop wcel c0 wceq wn opi1 n0i ax-mp ) ACZABDZEMFGHABIMLJK $.
    $( [24-Jan-2004] $) $( [24-Jan-2004] $)

  ${
    $d x A $.  $d x B $.
    $( A property of an ordered pair of proper classes (due to our
       particular definition of ordered pair). $)
    opprc1b $p |- ( -. A e. V <-> (/) e. <. A , B >. ) $=
      ( vx cvv wcel wn c0 cop csn cpr opprc1 0ex pri1 syl5eleqr cv wceq opeq1
      eleq2d negbid wo wne visset snnz df-ne mpbi eqcom mtbi prnz pm3.2ni elop
      mtbir vtoclg con2i impbi ) ADEZFZGABHZEZUPGBIZJUQGABKGUSLMNUOURGCOZBHZEZF
      URFCADUTAPZVBURVCVAUQGUTABQRSVBGUTIZPZGUTBJZPZTVEVGVDGPZVEVDGUAVHFUTCUBZU
      CVDGUDUEVDGUFUGVFGPZVGVFGUAVJFUTBVIUHVFGUDUEVFGUFUGUIGUTBLUJUKULUMUN $.
      $( [15-Nov-1994] $)
  $}

  $( A property of an ordered pair of proper classes (due to our
     particular definition of ordered pair). $)
  opprc3 $p |- ( ( -. A e. V /\ -. B e. V ) <-> <. A , B >. = { (/) } ) $=
    ( cvv wcel wn wa cop c0 csn wceq cpr opprc2 opprc1 snprc preq2 sylbi eqtrd
    sylan9eqr dfsn2 syl6eqr 0ex snid eleq2 mpbiri opprc1b sylibr eqeq1d snex
    preqr2 eqeq2i 3imtr4 syl6bi anc2li mpcom impbi ) ACDEZBCDEZFZABGZHIZJZURUSH
    HKZUTUQUPUSAAGZVBABLUPVCHAIZKZVBAAMUPVDHJVEVBJANVDHHOPQRHSZTUPVAURVAHUSDZUP
    VAVGHUTDHUAUBUSUTHUCUDABUEUFUPVAUQUPVAHBIZKZUTJZUQUPUSVIUTABMUGVIVBJVHHJVJU
    QVHHHBUHUAUIUTVBVIVFUJBNUKULUMUNUO $.
    $( [17-May-1998] $)

  ${
    $d x A $. $d x B $. $d x C $.  $d x D $.
    opth2.1 $e |- B e. V $.
    opth2.2 $e |- D e. V $.
    $( Equality of the second members of equal ordered pairs.  Because of our
       particular ordered pair definition, equality holds whether or not
       the first members are sets. $)
    opth2 $p |- ( <. A , B >. = <. C , D >. -> B = D ) $=
      ( vx cvv wcel cop wceq wi cv opeq1 eqeq1d imbi1d visset opth pm3.27bd
      vtoclg wn wa c0 nelneq2 opprc1b con1bii bicomi syl2anb pm2.21d csn cpr
      opprc1 eqeqan12d snex preqr2 sneqr syl syl6bi pm2.61dan pm2.61i ) AHIZABJ
      ZCDJZKZBDKZLZGMZBJZVCKZVELVFGAHVGAKZVIVDVEVJVHVBVCVGABNOPVIVGCKVEVGBCDGQE
      FRSTVAUAZCHIZVFVKVLUBVDVEUCVBIUCVCIZUAZVDUAVKVLUCVBVCUDABUEVNVLVLVMCDUEUF
      UGUHUIVKVLUAZUBVDUCBUJZUKZUCDUJZUKZKZVEVKVOVBVQVCVSABULCDULUMVTVPVRKVEVPV
      RUCBUNDUNUOBDEUPUQURUSUT $.
      $( [12-Apr-2004] $) $( [10-Apr-2004] $)
  $}

  ${
    $d x y w A $.  $d y z w B $.  $d x y z w $.
    $( "At most one" property of an ordered pair.  The proof is a little tricky
       because we do not place any restrictions on class ` B ` . $)
    moop2 $p |- E* x A = <. B , x >. $=
      ( vz vy vw cv cop wceq wmo wcel wsbc cab wa wi wal eqtr2t visset opth2
      syl gen2 ax-17 hbs1 hbab hbop hbeq sbab opeq1d opeq2 eqtrd eqeq2d mo4f
      mpbir ) BCAGZHZIZAJUPBDGCKZAEGZLZDMZURHZIZNZUNURIZOZEPAPVEAEVCUOVAIVDBUOV
      AQCUNUTURARERSTUAUPVBAEAFFBVAFGZBKAUBAFUTURUSADFUQAEUCUDVFURKAUBUEUFVDUOV
      ABVDUOUTUNHVAVDCUTUNAEDCUGUHUNURUTUIUJUKULUM $.
      $( [13-Apr-2004] $) $( [11-Apr-2004] $)
  $}

  ${
    $d x y z A $.
    mosubop.1 $e |- E* x ph $.
    $( "At most one" remains true inside order pair quantification. $)
    mosubop $p |- E* x E. y E. z ( A = <. y , z >. /\ ph ) $=
      ( cv cop wceq wex wa wmo hbe1 hbmo hbex ax-17 copsexg mobid mpbii
      19.23ai wn pm3.26 19.22i2 19.23aiv con3i exmo ori syl pm2.61i ) ECGDGHIZD
      JZCJZUJAKZDJZCJZBLZUKUPCUOCBUNCMNUJUPDUODBUNDCUMDMONUJABLUPFUJAUOBUJBPACD
      EQRSTTULUAUOBJZUAUPUQULUOULBUMUJCDUJAUBUCUDUEUQUPUOBUFUGUHUI $.
      $( [28-May-1995] $)
  $}

  ${
    $d x ph $.  $d x A $.  $d x y $.
    $( Transfer existential uniqueness to second member of an ordered pair. $)
    euop2 $p |- ( E! x E. y ( x = <. A , y >. /\ ph ) <-> E! y ph ) $=
      ( cv cop opex moop2 euxfr2 ) ABCDCEZFDJGCBEDHI $.
      $( [14-Apr-2004] $) $( [10-Apr-2004] $)
  $}

  ${
    opthw.1 $e |- A e. V $.
    opthw.2 $e |- B e. V $.
    $( Justification theorem for the ordered pair definition in Norbert Wiener,
       "A simplification of the logic of relations," _Proc. of the Cambridge
       Philos. Soc_., 1914, vol. 17, pp.387-390.  It is also shown as a
       definition in [Enderton] p. 36 and as Exercise 4.8(b) of [Mendelson]
       p. 230.   It is meaningful only for classes that exist as sets (i.e.
       are not proper classes).  See ~ df-op for other ordered pair
       definitions. $)
    opthwiener $p |- ( { { { A } , (/) } , { { B } } } =
                    { { { C } , (/) } , { { D } } } <-> ( A = C /\ B = D ) ) $=
      ( csn c0 cpr wceq wa id wo wcel snex pri2 eleq2 mpbii elpr sylib wn wb
      0ex snnzOLD elsnc eqcom bitr mtbir nelneq2 mp2an mtbi biorf ax-mp sylibr
      preq2 syl eqtr4d prex preqr1 sneqr 3syl jca sneq preq1 sylan9eq impbi )
      AGZHIZBGZGZIZCGZHIZDGZGZIZJZACJZBDJZKVQVRVSVQVGVLJZVRVQVHVMJZVTVQVKVMVJIZ
      JZWAVQVKVPWBVQLVQVJVOJZWBVPJZVQVJVMJZWDMZWDVQVJVPNZWGVQVJVKNWHVHVJVIOZPVK
      VPVJQRVJVMVOWISTWFUAWDWGUBVMVJJZWFHVMNHVJNZUAWJUAVLHUCPWKVIHJZBFUDWKHVIJW
      LHVIUCUEHVIUFUGUHHVMVJUIUJVMVJUFUKWFWDULUMUNZVJVOVMUOZUPUQVHVMVJVGHURVLHU
      RUSUPVGVLHAOCOUSUPACEUTUPVQWDVIVNJZVSWMVIVNBOUTBDFUTVAVBVRVSVKWBVPVRVTWAW
      CACVCVGVLHVDVHVMVJVDVAVSWOWDWEBDVCVIVNVCWNVAVEVF $.
      $( [28-Sep-2003] $)
  $}

  $( The union of an ordered pair.  Theorem 65 of [Suppes] p. 39. $)
  uniop $p |- U. <. A , B >. = { A , B } $=
    ( cop cuni csn cpr cun df-op unieqi snex prex unipr wss wceq snsspr
    ssequn1 mpbi 3eqtr ) ABCZDAEZABFZFZDTUAGZUASUBABHITUAAJABKLTUAMUCUANABOTUAP
    QR $.
    $( [27-Aug-2004] $) $( [17-Aug-2004] $)

