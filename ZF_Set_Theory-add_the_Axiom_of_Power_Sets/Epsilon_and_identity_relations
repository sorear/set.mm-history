$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Epsilon and identity relations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new constant symbols. $)
  $c E $. $( Letter E (for epsilon relation) $)
  $c I $.  $( Letter I (for identity relation) $)

  $( Extend class notation to include the epsilon relation. $)
  cep $a class E $.

  $( Extend the definition of a class to include identity relation. $)
  cid $a class I $.

  ${
    $d x y $.
    $( Define the epsilon relation.  Similar to Definition 6.22 of
       [TakeutiZaring] p. 30. $)
    df-eprel $a |- E = { <. x , y >. | x e. y } $.
  $}

  ${
    $d x y A $.  $d x y B $.
    epelc.1 $e |- A e. V $.
    epelc.2 $e |- B e. V $.
    $( The epsilon relation and the membership relation are the same. $)
    epelc $p |- ( A E B <-> A e. B ) $=
      ( vx vy cv wcel cep eleq1 eleq2 df-eprel brab ) EGZFGZHAOHABHEFABICDNAOJO
      BAKEFLM $.
      $( [13-Aug-1995] $)
  $}

  ${
    $( The epsilon relation and the membership relation are the same. $)
    epel $p |- ( x E y <-> x e. y ) $=
      ( cv visset epelc ) ACBCADBDE $.
      $( [13-Aug-1995] $)
  $}

  ${
    $d x y $.
    $( Define the identity relation.  Definition 9.15 of [Quine] p. 64. $)
    df-id $a |- I = { <. x , y >. | x = y } $.
  $}

  ${
    $d x y z A $.  $d x y B $.
    $( Alternate definition of the identity relation. $)
    dfid2 $p |- I = { <. x , x >. | x = x } $=
      ( vz vy cid cv cop wceq wa wex cab copab df-id df-opab ancom equcom
      anbi1i bitr exbii visset opeq2 eqeq2d ceqsexv equid biantru 3bitr hbe1
      19.9r abbii 3eqtr eqtr4 ) DBEZAEZULFZGZULULGZHZAIZAIZBJZUOAAKDULCEZGZACKU
      KULUTFZGZVAHZCIZAIZBJUSACLVAACBMVFURBVFUQURVEUPAVEUTULGZVCHZCIUNUPVDVHCVD
      VAVCHVHVCVANVAVGVCACOPQRVCUNCULASVGVBUMUKUTULULTUAUBUOUNAUCUDUERUQAUPAUFU
      GQUHUIUOAABMUJ $.
      $( [15-Mar-2007] $) $( [15-Mar-2007] $)

    $( For sets, the identity relation is the same as equality. $)
    ideqg $p |- ( ( A e. C /\ B e. D ) -> ( A I B <-> A = B ) ) $=
      ( vx vy cv wceq cid eqeq1 eqeq2 df-id brabg ) EGZFGZHAOHABHEFABCDINAOJOBA
      KEFLM $.
      $( [2-May-2004] $) $( [30-Apr-2004] $)
  $}

  ${
    ideq.1 $e |- A e. V $.
    ideq.2 $e |- B e. V $.
    $( For sets, the identity relation is the same as equality. $)
    ideq $p |- ( A I B <-> A = B ) $=
      ( cvv wcel cid wbr wceq wb ideqg mp2an ) AEFBEFABGHABIJCDABEEKL $.
      $( [13-Aug-1995] $)
  $}

