$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Epsilon and identity relations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new constant symbols. $)
  $c E $. $( Letter E (for epsilon relation) $)
  $c I $.  $( Letter I (for identity relation) $)

  $( Extend class notation to include the epsilon relation. $)
  cep $a class E $.

  $( Extend the definition of a class to include identity relation. $)
  cid $a class I $.

  ${
    $d x y $.
    $( Define the epsilon relation.  Similar to Definition 6.22 of
       [TakeutiZaring] p. 30. $)
    df-eprel $a |- E = { <. x , y >. | x e. y } $.
  $}

  ${
    $d x y A $.  $d x y B $.
    epelc.1 $e |- A e. V $.
    epelc.2 $e |- B e. V $.
    $( The epsilon relation and the membership relation are the same. $)
    epelc $p |- ( A E B <-> A e. B ) $=
      ( vx vy cv wcel cep eleq1 eleq2 df-eprel brab ) EGZFGZHAOHABHEFABICDNAOJO
      BAKEFLM $.
      $( [13-Aug-1995] $)
  $}

  ${
    $( The epsilon relation and the membership relation are the same. $)
    epel $p |- ( x E y <-> x e. y ) $=
      ( cv visset epelc ) ACBCADBDE $.
      $( [13-Aug-1995] $)
  $}

  ${
    $d x y $.
    $( Define the identity relation.  Definition 9.15 of [Quine] p. 64. $)
    df-id $a |- I = { <. x , y >. | x = y } $.
  $}

  ${
    $d x y A $.  $d x y B $.
    $( For sets, the identity relation is the same as equality. $)
    ideqg $p |- ( ( A e. C /\ B e. D ) -> ( A I B <-> A = B ) ) $=
      ( vx vy cv wceq cid eqeq1 eqeq2 df-id brabg ) EGZFGZHAOHABHEFABCDINAOJOBA
      KEFLM $.
      $( [2-May-2004] $) $( [30-Apr-2004] $)
  $}

  ${
    $d v w z x $.  $d v w z y $.
    $( A stronger version of ~ df-id that doesn't require ` x ` and ` y ` to be
       distinct.  Ordinarily, we wouldn't use this as a definition, since
       non-distinct dummy variables would make soundness verification more
       difficult (as the proof here shows).  The proof can be instructive in
       showing how distinct variable requirements may be eliminated, a task
       that is not necessarily obvious. $)
    dfid3 $p |- I = { <. x , y >. | x = y } $=
      ( vw vz vv cid cv cop wceq wa wex cab copab df-id df-opab wal wb opeq2
      eqeq2d equequ2 anbi12d a4s drex1 drex2 ancom equcom anbi1i bitr exbii
      visset ceqsexv equid biantru 3bitr hbe1 19.9 bitr4 syl5bb wn hbnae wcel
      wi ax-17 a1i dveel2 nalequcoms hbopd hbeqd dveeq1 hband cbvexd exbid
      pm2.61i abbii 3eqtr eqtr4 ) FCGZAGZBGZHZIZVRVSIZJZBKZAKZCLZWBABMFVRDGZIZA
      DMVQVRWGHZIZWHJZDKZAKZCLWFADNWHADCOWMWECWBAPZWMWEQWNVQVRVRHZIZVRVRIZJZAKZ
      AKZWEWMWSWDABAWRWCABWBWRWCQAWBWPWAWQWBWBWOVTVQVRVSVRRSABATUAUBUCUDWMWSWTW
      LWRAWLWGVRIZWJJZDKWPWRWKXBDWKWHWJJXBWJWHUEWHXAWJADUFUGUHUIWJWPDVRAUJXAWIW
      OVQWGVRVRRSUKWQWPAULUMUNUIWSAWRAUOUPUQURWNUSZWLWDAABAUTXCWKWCDBABBUTZXCWJ
      WHBXCBEVQWIXDEGZVQVAZXFBPVBXCXFBVCVDXCBEVRWGXDXEVRVAZXGBPVBBABAEVEVFXEWGV
      AZXHBPVBXCXHBVCVDVGVHWHWHBPVBBABADVIVFVJWGVSIZWKWCQVBXCXIWJWAWHWBXIWIVTVQ
      WGVSVRRSDBATUAVDVKVLVMVNVOWBABCOVP $.
      $( [5-Feb-2008] $) $( [5-Feb-2008] $)
  $}

  $( Alternate definition of the identity relation. $)
  dfid2 $p |- I = { <. x , x >. | x = x } $=
    ( dfid3 ) AAB $.
    $( [15-Mar-2007] $) $( [15-Mar-2007] $)

  ${
    ideq.1 $e |- A e. V $.
    ideq.2 $e |- B e. V $.
    $( For sets, the identity relation is the same as equality. $)
    ideq $p |- ( A I B <-> A = B ) $=
      ( cvv wcel cid wbr wceq wb ideqg mp2an ) AEFBEFABGHABIJCDABEEKL $.
      $( [13-Aug-1995] $)
  $}

