$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Epsilon and identity relations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new constant symbols. $)
  $c _E $. $( Letter E (for epsilon relation) $)
  $c _I $.  $( Letter I (for identity relation) $)

  $( Extend class notation to include the epsilon relation. $)
  cep $a class _E $.

  $( Extend the definition of a class to include identity relation. $)
  cid $a class _I $.

  ${
    $d x y $.
    $( Define the epsilon relation.  Similar to Definition 6.22 of
       [TakeutiZaring] p. 30. $)
    df-eprel $a |- _E = { <. x , y >. | x e. y } $.
  $}

  ${
    $d x y A $.  $d x y B $.
    epelc.1 $e |- A e. _V $.
    epelc.2 $e |- B e. _V $.
    $( The epsilon relation and the membership relation are the same. $)
    epelc $p |- ( A _E B <-> A e. B ) $=
      ( vx vy cv wcel cep eleq1 eleq2 df-eprel brab ) EGZFGZHAOHABHEFABICDNAOJO
      BAKEFLM $.
      $( [13-Aug-1995] $)
  $}

  ${
    $( The epsilon relation and the membership relation are the same. $)
    epel $p |- ( x _E y <-> x e. y ) $=
      ( cv visset epelc ) ACBCADBDE $.
      $( [13-Aug-1995] $)
  $}

  ${
    $d x y $.
    $( Define the identity relation.  Definition 9.15 of [Quine] p. 64. $)
    df-id $a |- _I = { <. x , y >. | x = y } $.
  $}

  ${
    $d w z x $.  $d w z y $.
    $( A stronger version of ~ df-id that doesn't require ` x ` and ` y ` to be
       distinct.  Ordinarily, we wouldn't use this as a definition, since
       non-distinct dummy variables would make soundness verification more
       difficult (as the proof here shows).  The proof can be instructive in
       showing how distinct variable requirements may be eliminated, a task
       that is not necessarily obvious. $)
    dfid3 $p |- _I = { <. x , y >. | x = y } $=
      ( vw vz cid cv cop wceq wa wex cab copab df-opab wal exbii eqeq2d equequ2
      wb opeq2 wi df-id ancom equcom anbi1i bitri visset ceqsexv biantru 3bitri
      equid hbe1 19.9 bitr4i anbi12d a4s drex1 drex2 syl5bb wn hbnae nalequcoms
      wcel dveel2 a17d hbopd hbeleqd dveeq1 hband a1i cbvexd exbid abbii 3eqtri
      pm2.61i eqtr4i ) ECFZAFZBFZGZHZVQVRHZIZBJZAJZCKZWAABLEVQDFZHZADLVPVQWFGZH
      ZWGIZDJZAJZCKWEADUAWGADCMWLWDCWAANZWLWDRWLVPVQVQGZHZVQVQHZIZAJZAJZWMWDWLW
      RWSWKWQAWKWFVQHZWIIZDJWOWQWJXADWJWGWIIXAWIWGUBWGWTWIADUCUDUEOWIWODVQAUFWT
      WHWNVPWFVQVQSPUGWPWOAUJUHUIOWRAWQAUKULUMWRWCABAWQWBABWAWQWBRAWAWOVTWPWAWA
      WNVSVPVQVRVQSPABAQUNUOUPUQURWMUSZWKWCAABAUTXBWJWBDBABBUTZXBWIWGBXBBCWHXCX
      BBCVQWFXCVPVQVBZXDBNTBABACVCVAXBVPWFVBBVDVEVFWGWGBNTBABADVGVAVHWFVRHZWJWB
      RTXBXEWIVTWGWAXEWHVSVPWFVRVQSPDBAQUNVIVJVKVNVLVMWAABCMVO $.
      $( [5-Feb-2008] $)
  $}

  $( Alternate definition of the identity relation. $)
  dfid2 $p |- _I = { <. x , x >. | x = x } $=
    ( dfid3 ) AAB $.
    $( [15-Mar-2007] $)

