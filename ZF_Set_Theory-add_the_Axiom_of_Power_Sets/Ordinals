$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordinals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c Ord $. $( Ordinal predicate $)
  $c On $. $( The class of ordinal numbers $)
  $c Lim $. $( Limit ordinal predicate $)
  $c suc $. $( Successor function (read:  'successor of') $)

  $( Extend the definition of a wff to include the ordinal predicate. $)
  word $a wff Ord A $.

  $( Extend the definition of a class to include the class of all ordinal
     numbers.  (The 0 in the name prevents creating a file called con.html,
     which causes problems in Windows.) $)
  con0 $a class On $.

  $( Extend the definition of a wff to include the limit ordinal predicate. $)
  wlim $a wff Lim A $.

  $( Extend class notation to include the successor function. $)
  csuc $a class suc A $.

  $( Define the ordinal predicate, which is true for a class that is transitive
     and is well-ordered by the epsilon relation.  Variant of definition of
     [BellMachover] p. 468. $)
  df-ord $a |- ( Ord A <-> ( Tr A /\ _E We A ) ) $.

  $( Define the class of all ordinal numbers.  Definition 7.11 of
     [TakeutiZaring] p. 38. $)
  df-on $a |- On = { x | Ord x } $.

  $( Define the limit ordinal predicate, which is true for a non-empty ordinal
     that is not a successor (i.e. that is the union of itself).  Our
     definition combines the definition of Lim of [BellMachover] p. 471 and
     Exercise 1 of [TakeutiZaring] p. 42.  See ~ dflim2 , ~ dflim3 , and dflim4
     for alternate definitions. $)
  df-lim $a |- ( Lim A <-> ( Ord A /\ A =/= (/) /\ A = U. A ) ) $.

  $( Define the successor of a class.  When applied to an ordinal number, the
     successor means the same thing as "plus 1" (see ~ oa1suc ).  Definition
     7.22 of [TakeutiZaring] p. 41, who use "+ 1" to denote this function.  Our
     definition is a generalization to classes.  Although it is not
     conventional to use it with proper classes, it has no affect on a proper
     class ( ~ sucprc ), so that the successor of any ordinal class is still an
     ordinal class ( ~ ordsuc ), simplifying certain proofs.  Some authors
     denote the successor operation with a prime (apostrophe-like) symbol, such
     as Definition 6 of [Suppes] p. 134 and the definition of successor in
     [Mendelson] p. 246 (who uses the symbol "Suc" as a predicate to mean "is a
     successor ordinal").  The definition of successor of [Enderton] p. 68
     denotes the operation with a plus-sign superscript. $)
  df-suc $a |- suc A = ( A u. { A } ) $.

  $( Equality theorem for the ordinal predicate. $)
  ordeq $p |- ( A = B -> ( Ord A <-> Ord B ) ) $=
    ( wceq wtr cep wwe wa word treq weeq2 anbi12d df-ord 3bitr4g ) ABCZADZAEFZG
    BDZBEFZGAHBHNOQPRABIABEJKALBLM $.
    $( [17-Sep-1993] $)

  ${
    $d x A $.
    $( An ordinal number is an ordinal set. $)
    elong $p |- ( A e. B -> ( A e. On <-> Ord A ) ) $=
      ( vx cv word con0 ordeq df-on elab2g ) CDZEAECAFBJAGCHI $.
      $( [5-Jun-1994] $)
  $}

  ${
    elon.1 $e |- A e. _V $.
    $( An ordinal number is an ordinal set. $)
    elon $p |- ( A e. On <-> Ord A ) $=
      ( cvv wcel con0 word wb elong ax-mp ) ACDAEDAFGBACHI $.
      $( [5-Jun-1994] $)
  $}

  $( An ordinal number has the ordinal property. $)
  eloni $p |- ( A e. On -> Ord A ) $=
    ( con0 wcel word elong ibi ) ABCADABEF $.
    $( [5-Jun-1994] $)

  $( An ordinal number is an ordinal set. $)
  elon2 $p |- ( A e. On <-> ( Ord A /\ A e. _V ) ) $=
    ( con0 wcel word cvv wa eloni elisset jca elong biimparc impbii ) ABCZADZAE
    CZFMNOAGABHIOMNAEJKL $.
    $( [8-Feb-2004] $)

  $( Equality theorem for the limit predicate.  (The proof was shortened by
     Andrew Salmon, 25-Jul-2011.) $)
  limeq $p |- ( A = B -> ( Lim A <-> Lim B ) ) $=
    ( wceq word c0 wne cuni w3a wlim ordeq neeq1 id unieq eqeq12d 3anbi123d
    df-lim 3bitr4g ) ABCZADZAEFZAAGZCZHBDZBEFZBBGZCZHAIBIRSUCTUDUBUFABJABEKRABU
    AUERLABMNOAPBPQ $.
    $( [25-Jul-2011] $) $( [22-Apr-1994] $)

  $( Equality theorem for the limit predicate. $)
  limeqOLD $p |- ( A = B -> ( Lim A <-> Lim B ) ) $=
    ( wceq word c0 wne cuni w3a wlim ordeq neeq1 unieq eqeq2d eqeq1 bitrd
    3anbi123d df-lim 3bitr4g ) ABCZADZAEFZAAGZCZHBDZBEFZBBGZCZHAIBISTUDUAUEUCUG
    ABJABEKSUCAUFCUGSUBUFAABLMABUFNOPAQBQR $.
    $( [22-Apr-1994] $)

  $( Epsilon well orders every ordinal.  Proposition 7.4 of [TakeutiZaring]
     p. 36. $)
  ordwe $p |- ( Ord A -> _E We A ) $=
    ( word wtr cep wwe df-ord simprbi ) ABACADEAFG $.
    $( [3-Apr-1994] $)

  $( An ordinal class is transitive. $)
  ordtr $p |- ( Ord A -> Tr A ) $=
    ( word wtr cep wwe df-ord simplbi ) ABACADEAFG $.
    $( [3-Apr-1994] $)

  $( Epsilon is well-founded on an ordinal class. $)
  ordfr $p |- ( Ord A -> _E Fr A ) $=
    ( word cep wwe wfr ordwe wefr syl ) ABACDACEAFACGH $.
    $( [22-Apr-1994] $)

  $( An element of an ordinal class is a subset of it. $)
  ordelss $p |- ( ( Ord A /\ B e. A ) -> B C_ A ) $=
    ( wtr wcel wss word trss imp ordtr sylan ) ACZBADZBAEZAFKLMABGHAIJ $.
    $( [30-May-1994] $)

  $( A transitive subclass of an ordinal class is ordinal. $)
  trssord $p |- ( ( Tr A /\ A C_ B /\ Ord B ) -> Ord A ) $=
    ( wtr wss word wa cep wwe w3a wess imp ordwe sylan2 anim2i 3anass df-ord
    3imtr4i ) ACZABDZBEZFZFRAGHZFRSTIAEUAUBRSBGHZUBTSUCUBABGJKBLMNRSTOAPQ $.
    $( [29-May-1994] $)

  $( Epsilon irreflexivity of ordinals: no ordinal class is a member of
     itself.  Theorem 2.2(i) of [BellMachover] p. 469, generalized to classes.
     We prove this without invoking the Axiom of Regularity. $)
  ordirr $p |- ( Ord A -> -. A e. A ) $=
    ( word cep wfr wcel wn ordfr efrirr syl ) ABACDAAEFAGAHI $.
    $( [2-Jan-1994] $)

  $( A member of an ordinal class is not equal to it. $)
  nordeq $p |- ( ( Ord A /\ B e. A ) -> A =/= B ) $=
    ( word wcel wne wceq wn eleq1 notbid ordirr syl5cbi necon2ad imp ) ACZBADZA
    BENOABABFZAADZGOGNPQOABAHIAJKLM $.
    $( [3-Jun-2007] $) $( [25-May-1998] $)

  $( An ordinal class cannot an element of one of its members.  Variant of
     first part of Theorem 2.2(vii) of [BellMachover] p. 469. $)
  ordn2lp $p |- ( Ord A -> -. ( A e. B /\ B e. A ) ) $=
    ( word wcel wa ordirr wtr wi ordtr trel syl mtod ) ACZABDBADEZAADZAFMAGNOHA
    IAABJKL $.
    $( [3-Apr-1994] $)

  ${
    $d x B $.
    $( A subclass (possibly proper) of an ordinal class has a minimal element.
       Proposition 7.5 of [TakeutiZaring] p. 36.  (Some distinct variable
       restrictions were removed by David Abernethy, 16-Mar-2011.) $)
    tz7.5 $p |- ( ( Ord A /\ B C_ A /\ B =/= (/) ) ->
               E. x e. B ( B i^i x ) = (/) ) $=
      ( cep wwe wss c0 wne cv cin wceq wrex word wefrc ordwe syl3an1 ) BDECBFCG
      HCAIJGKACLBMABCNBOP $.
      $( [22-Mar-2011] $) $( [18-Feb-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( An element of an ordinal class is ordinal.  Proposition 7.6 of
       [TakeutiZaring] p. 36. $)
    ordelord $p |- ( ( Ord A /\ B e. A ) -> Ord B ) $=
      ( vx vz vy word wcel cv wa wi wceq eleq1 anbi2d ordeq imbi12d wtr cep
      wwe df-ord wal simpll w3a ordtr trel3 syl 3anrot 3anass bitr3i syl5ibr
      exp3a imp31 trel com23 adantrl simplr wetrep ordwe sylan syl13anc ex
      pm2.43d 19.21aivv dftr2 sylibr wss trss wess syl5com syld imp sylanbrc
      vtoclg anabsi7 ) AFZBAGZBFZVNCHZAGZIZVQFZJVNVOIZVPJCBAVQBKZVSWAVTVPWBVRVO
      VNVQBALMVQBNOVTVQPZVQQRZVSVQSVSDHZEHZGZWFVQGZIZWEVQGZJZETDTWCVSWKDEVSWIWJ
      VSWIWKVSWIIVNWEAGZWFAGZVRWKVNVRWIUAVNVRWIWLVNVRWIWLVNWGWHVRUBZWLVRWIIZVNA
      PZWNWLJAUCZAWEWFVQUDUEWNVRWGWHUBWOVRWGWHUFVRWGWHUGUHUIUJUKVSWHWMWGVNVRWHW
      MVNWHVRWMVNWHVRWMVNWPWHVRIWMJWQAWFVQULUEUJUMUKUNVNVRWIUOAQRZWLWMVRUBWKVND
      ECAUPAUQZURUSUTVAVBDEVQVCVDVNVRWDVNVRVQAVEZWDVNWPVRWTJWQAVQVFUEWTWRWDVNVQ
      AQVGWSVHVIVJVKVLVM $.
      $( [23-Apr-1994] $)
  $}

  ${
    $d x y $.
    $( The class of all ordinal numbers is transitive. $)
    tron $p |- Tr On $=
      ( vx vy con0 wtr cv wss dftr3 wcel word ordelord visset elon sylanb ex
      syl6ibr ssrdv mprgbir ) CDAEZCFACACGRCHZBRCSBEZRHZTIZTCHSUAUBRIUAUBSRTJRA
      KLMNTBKLOPQ $.
      $( [4-May-2009] $)
  $}

  $( An element of an ordinal class is an ordinal number. $)
  ordelon $p |- ( ( Ord A /\ B e. A ) -> B e. On ) $=
    ( word wcel wa con0 ordelord wb elong adantl mpbird ) ACZBADZEBFDZBCZABGMNO
    HLBAIJK $.
    $( [26-Oct-2003] $)

  $( An element of an ordinal number is an ordinal number.  Theorem 2.2(iii) of
     [BellMachover] p. 469. $)
  onelon $p |- ( ( A e. On /\ B e. A ) -> B e. On ) $=
    ( word wcel con0 ordelon eloni sylan ) ACBADBEDAEDABFAGH $.
    $( [26-Oct-2003] $)

  ${
    $d x y A $.  $d x y B $.
    $( Proposition 7.7 of [TakeutiZaring] p. 37. $)
    tz7.7 $p |- ( ( Ord A /\ Tr B ) ->
                ( B e. A <-> ( B C_ A /\ B =/= A ) ) ) $=
      ( vx vy word wtr wa wcel wss wne wi cep wfr ordtr ordfr tz7.2 3exp sylc
      adantr cdif c0 cv cin wceq wrex trss eldifi syl5 difin0ss com12 syl6 syl
      ad2antrr imp32 wn eleq1 biimpcd eldifn nsyli imp adantll adantl trel
      exp3a com23 ex adantld w3o wwe wecmpep ordwe ssel2 anim12i syl2an
      adantlr ecase23d exp44 com34 imp31 ssrdv adantrr eqssd ad2antrl eqeltrrd
      exp32 r19.23adv difss tz7.5 mp3an2 exp4b adantrd pm2.43i pssdifn0 syl7
      exp4a pm2.43d imp3a impbid ) AEZBFZGZBAHZBAIZBAJZGZWSXBXEKZWTWSAFZALMZXFA
      NZAOXGXHXBXEABPQRSXAXCXDXBXAXCXDXBKXAXCXCXDXBXAXCABTZUAJZXBXEXAXCXKXBKZKZ
      XAWSXMWTXAXCWSXLXAXCWSXKXBXAXCGZXJCUBZUCUAUDZCXJUEZXBWSXKGXNXPXBCXJXNXOXJ
      HZXPXBXNXRXPGGZXOBAXSXOBXNXRXPXOBIZWSXRXPXTKZKZWTXCWSXGYBXIXGXRXOAIZYAXGX
      OAHZYCXRAXOUFXOABUGZUHXPYCXTABXOUIUJUKULUMUNXNXRBXOIXPXNXRGDBXOXAXCXRDUBZ
      BHZYFXOHZKXAXCYGXRYHXAXCYGXRYHXAXCYGGZXRGZGYHYFXOUDZXOYFHZYJYKUOZXAYGXRYM
      XCYGXRYMYGYKXOBHZXRYKYGYNYFXOBUPUQXOABURZUSUTVAVBWTYJYLUOZWSWTYIXRYPWTYGX
      RYPKZXCWTYGYQWTYGGYLYNXRWTYGYLYNKWTYLYGYNWTYLYGYNBXOYFVCVDVEUTYOUSVFVGUNV
      AWSYJYHYKYLVHZWTALVIYFAHZYDGYRWSYJDCAVJAVKYIYSXRYDBAYFVLYEVMVNVOVPVQVRVSV
      TWAWBXRYDXNXPYEWCWDWEWFWSXJAIXKXQABWGCAXJWHWIUHWJVEWKWLBAWMWNWOWPWQWR $.
      $( [28-May-2007] $) $( [5-May-1994] $)
  $}

  $( Corollary 7.8 of [TakeutiZaring] p. 37. $)
  ordelssne $p |- ( ( Ord A /\ Ord B ) ->
              ( A e. B <-> ( A C_ B /\ A =/= B ) ) ) $=
    ( word wcel wss wne wa wb wtr tz7.7 ordtr sylan2 ancoms ) BCZACZABDABEABFGH
    ZNAIPOBAJAKLM $.
    $( [1-Jun-2007] $) $( [25-Nov-1995] $)

  $( Corollary 7.8 of [TakeutiZaring] p. 37. $)
  ordelpss $p |- ( ( Ord A /\ Ord B ) -> ( A e. B <-> A C. B ) ) $=
    ( word wa wcel wss wne wpss ordelssne df-pss syl6bbr ) ACBCDABEABFABGDABHAB
    IABJK $.
    $( [17-Jun-1998] $)

  $( For ordinal classes, subclass is equivalent to membership or equality.
     (The proof was shortened by Andrew Salmon, 25-Jul-2011.) $)
  ordsseleq $p |- ( ( Ord A /\ Ord B ) ->
                  ( A C_ B <-> ( A e. B \/ A = B ) ) ) $=
    ( word wa wcel wceq wo wpss wss ordelpss orbi1d sspss syl6rbbr ) ACBCDZABEZ
    ABFZGABHZPGABINOQPABJKABLM $.
    $( [25-Jul-2011] $) $( [25-Nov-1995] $)

  $( For ordinal classes, subclass is equivalent to membership or equality. $)
  ordsseleqOLD $p |- ( ( Ord A /\ Ord B ) ->
                  ( A C_ B <-> ( A e. B \/ A = B ) ) ) $=
    ( word wa wss wcel wceq wo wne ordelssne biimprd expdimp necon1bd orrd ex
    wi simpl syl6bi eqimss jctir jaob sylibr impbid ) ACBCDZABEZABFZABGZHZUDUEU
    HUDUEDZUFUGUIUFABUDUEABIZUFUDUFUEUJDZABJZKLMNOUDUFUEPZUGUEPZDUHUEPUDUMUNUDU
    FUKUEULUEUJQRABSTUFUEUGUAUBUC $.
    $( [25-Nov-1995] $)

  $( The intersection of two ordinal classes is ordinal.  Proposition 7.9 of
     [TakeutiZaring] p. 37. $)
  ordin $p |- ( ( Ord A /\ Ord B ) -> Ord ( A i^i B ) ) $=
    ( word cin wtr trin ordtr syl2an wss inss2 trssord mp3an2 sylancom ) ACZBCZ
    ABDZEZPCZAEBEQNOABFAGBGHQPBIORABJPBKLM $.
    $( [9-May-1994] $)

  ${
    $d x y A $.  $d x y B $.
    $( The intersection of two ordinal numbers is an ordinal number. $)
    onin $p |- ( ( A e. On /\ B e. On ) -> ( A i^i B ) e. On ) $=
      ( con0 wcel wa cin word ordin eloni syl2an cvv wb simpl inex1g elong
      3syl mpbird ) ACDZBCDZEZABFZCDZUAGZAGBGUCRSABHAIBIJTRUAKDUBUCLRSMABCNUAKO
      PQ $.
      $( [7-Apr-1995] $)
  $}

  $( A trichotomy law for ordinals.  Proposition 7.10 of [TakeutiZaring]
     p. 38.  (The proof was shortened by Andrew Salmon, 25-Jul-2011.) $)
  ordtri3or $p |- ( ( Ord A /\ Ord B ) -> ( A e. B \/ A = B \/ B e. A ) ) $=
    ( word wa wcel wceq w3o wpss wss wo cin wn ordin ordirr syl elin incom
    eleq1i anbi2i bitri notbii ianor sylib inss1 ordsseleq mpbii sylan anabss1
    ord df-ss syl6ibr anabss4 orim12d mpd sspsstri ordelpss biidd wb ancoms
    3orbi123d mpbird ) ACZBCZDZABEZABFZBAEZGABHZVFBAHZGZVDABIZBAIZJZVJVDABKZAEZ
    LZBAKZBEZLZJZVMVDVNVNEZLZVTVDVNCZWBABMZVNNOWBVOVRDZLVTWAWEWAVOVNBEZDWEVNABP
    WFVRVOVNVQBABQRSTUAVOVRUBTUCVDVPVKVSVLVDVPVNAFZVKVDVOWGVBVCVOWGJZWCVBWHVDWC
    VBDVNAIWHABUDVNAUEUFWDUGUHUIABUJUKVDVSVQBFZVLVDVRWIVBVCVRWIJZVQCZVCWJVCVBDW
    KVCDVQBIWJBAUDVQBUEUFBAMUGULUIBAUJUKUMUNABUOUCVDVEVHVFVFVGVIABUPVDVFUQVCVBV
    GVIURBAUPUSUTVA $.
    $( [25-Jul-2011] $) $( [10-May-1994] $)

  $( A trichotomy law for ordinals.  Proposition 7.10 of [TakeutiZaring]
     p. 38. $)
  ordtri3orOLD $p |- ( ( Ord A /\ Ord B ) -> ( A e. B \/ A = B \/ B e. A ) ) $=
    ( word wa wcel wceq wo w3o wss cin wn ordin ordirr syl elin incom eleq1i
    anbi2i bitri notbii ianor sylib inss1 ordsseleq mpbii sylan anabss1 ord
    df-ss syl6ibr anabss4 orim12d mpd wb ancoms orbi12d mpbid df-3or or23
    orordir eqcom orbi2i 3bitri sylibr ) ACZBCZDZABEZABFZGZBAEZBAFZGZGZVHVIVKHZ
    VGABIZBAIZGZVNVGABJZAEZKZBAJZBEZKZGZVRVGVSVSEZKZWEVGVSCZWGABLZVSMNWGVTWCDZK
    WEWFWJWFVTVSBEZDWJVSABOWKWCVTVSWBBABPQRSTVTWCUASUBVGWAVPWDVQVGWAVSAFZVPVGVT
    WLVEVFVTWLGZWHVEWMVGWHVEDVSAIWMABUCVSAUDUEWIUFUGUHABUIUJVGWDWBBFZVQVGWCWNVE
    VFWCWNGZWBCZVFWOVFVEDWPVFDWBBIWOBAUCWBBUDUEBALUFUKUHBAUIUJULUMVGVPVJVQVMABU
    DVFVEVQVMUNBAUDUOUPUQVOVHVKGVIGZVJVKVIGZGVNVOVJVKGWQVHVIVKURVHVIVKUSSVHVKVI
    UTWRVMVJVIVLVKABVAVBVBVCVD $.
    $( [29-Jun-2008] $) $( [10-May-1994] $)

  $( A trichotomy law for ordinals.  (The proof was shortened by Andrew Salmon,
     25-Jul-2011.) $)
  ordtri1 $p |- ( ( Ord A /\ Ord B ) -> ( A C_ B <-> -. B e. A ) ) $=
    ( word wa wss wcel wceq wo wn ordsseleq wi ordn2lp imnan sylibr eleq2
    notbid ordirr syl5cbir jaao w3o ordtri3or df-3or sylib pm1.4 syl ord
    impbid bitrd ) ACZBCZDZABEABFZABGZHZBAFZIZABJUKUNUPUIULUPUJUMUIULUODIULUPKA
    BLULUOMNUMUPBBFZIUJUMUOUQABBOPBQRSUKUOUNUKUNUOHZUOUNHUKULUMUOTURABUAULUMUOU
    BUCUNUOUDUEUFUGUH $.
    $( [25-Jul-2011] $) $( [25-Mar-1995] $)

  $( A trichotomy law for ordinals. $)
  ordtri1OLD $p |- ( ( Ord A /\ Ord B ) -> ( A C_ B <-> -. B e. A ) ) $=
    ( word wa wss wcel wceq wo wn ordsseleq wi ordn2lp imnan sylibr eleq2
    notbid ordirr syl5cbir jaao w3o ordtri3or df-3or orcom df-or 3bitri sylib
    impbid bitrd ) ACZBCZDZABEABFZABGZHZBAFZIZABJUKUNUPUIULUPUJUMUIULUODIULUPKA
    BLULUOMNUMUPBBFZIUJUMUOUQABBOPBQRSUKULUMUOTZUPUNKZABUAURUNUOHUOUNHUSULUMUOU
    BUNUOUCUOUNUDUEUFUGUH $.
    $( [25-Mar-1995] $)

  $( A trichotomy law for ordinal numbers. $)
  ontri1 $p |- ( ( A e. On /\ B e. On ) -> ( A C_ B <-> -. B e. A ) ) $=
    ( word wss wcel wn wb con0 ordtri1 eloni syl2an ) ACBCABDBAEFGAHEBHEABIAJBJ
    K $.
    $( [6-Nov-2003] $)

  $( A trichotomy law for ordinals. $)
  ordtri2 $p |- ( ( Ord A /\ Ord B ) ->
               ( A e. B <-> -. ( A = B \/ B e. A ) ) ) $=
    ( word wa wceq wcel wo wn wb wss ordsseleq eqcom orbi2i orcom bitri syl6bb
    ordtri1 bitr3d ancoms con2bid ) ACZBCZDABEZBAFZGZABFZUBUAUEUFHZIUBUADZBAJZU
    EUGUHUIUDBAEZGZUEBAKUKUDUCGUEUJUCUDBALMUDUCNOPBAQRST $.
    $( [25-Nov-1995] $)

  $( A trichotomy law for ordinals.  (The proof was shortened by Andrew Salmon,
     25-Jul-2011.) $)
  ordtri3 $p |- ( ( Ord A /\ Ord B ) ->
               ( A = B <-> -. ( A e. B \/ B e. A ) ) ) $=
    ( word wa wceq wcel wo wn eleq2 notbid ordirr syl5bi syl5bir anim12d com12
    pm4.56 syl6ib w3o ordtri3or df-3or sylib or23 ord impbid ) ACZBCZDZABEZABFZ
    BAFZGZHZUGUHUIHZUJHZDZULUHUGUOUHUEUMUFUNUHAAFZHUMUEUHUPUIABAIJAKLUHUNBBFZHU
    FUHUJUQABBIJBKMNOUIUJPQUGUKUHUGUIUHGUJGZUKUHGUGUIUHUJRURABSUIUHUJTUAUIUHUJU
    BUAUCUD $.
    $( [25-Jul-2011] $) $( [18-Oct-1995] $)

  $( A trichotomy law for ordinals. $)
  ordtri3OLD $p |- ( ( Ord A /\ Ord B ) ->
               ( A = B <-> -. ( A e. B \/ B e. A ) ) ) $=
    ( word wa wceq wcel wo wn eleq2 notbid ordirr syl5bi syl5bir anim12d ioran
    syl6ibr com12 w3o wi ordtri3or df-3or or23 df-or 3bitri sylib impbid ) ACZB
    CZDZABEZABFZBAFZGZHZUJUIUNUJUIUKHZULHZDUNUJUGUOUHUPUJAAFZHUOUGUJUQUKABAIJAK
    LUJUPBBFZHUHUJULURABBIJBKMNUKULOPQUIUKUJULRZUNUJSZABTUSUKUJGULGUMUJGUTUKUJU
    LUAUKUJULUBUMUJUCUDUEUF $.
    $( [18-Oct-1995] $)

  $( A trichotomy law for ordinals.  (The proof was shortened by Andrew Salmon,
     25-Jul-2011.) $)
  ordtri4 $p |- ( ( Ord A /\ Ord B ) ->
               ( A = B <-> ( A C_ B /\ -. A e. B ) ) ) $=
    ( word wa wss wcel wn wceq wb ordtri1 ancoms anbi2d eqss syl5bb ) ACZBCZDZA
    BEZBAEZDRABFGZDABHQSTRPOSTIBAJKLABMN $.
    $( [25-Jul-2011] $) $( [1-Nov-2003] $)

  $( A trichotomy law for ordinals. $)
  ordtri4OLD $p |- ( ( Ord A /\ Ord B ) ->
               ( A = B <-> ( A C_ B /\ -. A e. B ) ) ) $=
    ( word wa wceq wcel wo wn wss ordtri3 ordtri1 anbi1d ancom ioran bitr4i
    syl6bb bitr4d ) ACBCDZABEABFZBAFZGHZABIZSHZDZABJRUDTHZUCDZUARUBUEUCABKLUFUC
    UEDUAUEUCMSTNOPQ $.
    $( [1-Nov-2003] $)

  $( An ordinal class and its singleton are disjoint. $)
  orddisj $p |- ( Ord A -> ( A i^i { A } ) = (/) ) $=
    ( word wcel wn csn cin c0 wceq ordirr disjsn sylibr ) ABAACDAAEFGHAIAAJK $.
    $( [19-May-1998] $)

  ${
    $d x y z $.
    $( The ordinal class is founded.  This lemma is needed for ~ ordon in order
       to eliminate the need for the Axiom of Regularity. $)
    onfr $p |- _E Fr On $=
      ( vx vz vy con0 cep wfr cv wss c0 wne wa cin wceq wrex wi dfepfr wcel
      wex ineq2 eqeq1d rcla4ev expcom a1d word ssel visset elon syl6ib wtr
      ordfr ordtr inss2 inex1 epfrc mp3an2 ex ax-17 hbre1 inss1 sseli trss
      syl5 sseqin2 inass syl5req sylbi biimprcd sylan9 imp anim12i exp32
      pm2.43b com23 ra4e syl8 r19.23ad syl11anc syl6 com3r pm2.61ine 19.23adv
      n0 syl5ib mpgbir ) DEFAGZDHZWEIJZKWEBGZLZIMZBWENZOAABDPWFWGWKWFCGZWEQZCRW
      KWGWFWMWKCWFWMWKOZOWEWLLZIWOIMZWNWFWMWPWKWJWPBWLWEWHWLMWIWOIWHWLWESTUAUBU
      CWFWMWOIJZWKWFWMWLUDZWQWKOZWFWMWLDQWRWEDWLUEWLCUFUGUHWRWLEFZWLUIZWSWLUJWL
      UKWTWQWOWHLZIMZBWONZXAWKWTWQXDWTWOWLHWQXDWEWLULZBWLWOWEWLAUFUMUNUOUPXAXCW
      KBWOXABUQWJBWEURXAWHWOQZXCWHWEQZWJKZWKXAXCXFXHXAXCXFXHOXAXCKZXFXHXFXIXFXH
      XFXGXIXFKWJWOWEWHWEWLUSUTXIXFWJXAXFWHWLHZXCWJXAWHWLQXJXFWLWHVAWOWLWHXEUTV
      BXJWJXCXJWIXBIXJWLWHLZWHMZWIXBMWHWLVCXLWEXKLWIXBXKWHWESWEWLWHVDVEVFTVGVHV
      IVJVKVLUPVMWJBWEVNVOVPVHVQVRVSVTWACWEWBWCVIWD $.
      $( [17-May-1994] $)
  $}

  $( Relationship between membership and proper subset of an ordinal number. $)
  onelpss $p |- ( ( A e. On /\ B e. On ) ->
               ( A e. B <-> ( A C_ B /\ A =/= B ) ) ) $=
    ( word wcel wss wne wa wb con0 ordelssne eloni syl2an ) ACBCABDABEABFGHAIDB
    IDABJAKBKL $.
    $( [2-Jun-2007] $) $( [15-Sep-1995] $)

  $( Relationship between subset and membership of an ordinal number. $)
  onsseleq $p |- ( ( A e. On /\ B e. On ) ->
                 ( A C_ B <-> ( A e. B \/ A = B ) ) ) $=
    ( word wss wcel wceq wo wb con0 ordsseleq eloni syl2an ) ACBCABDABEABFGHAIE
    BIEABJAKBKL $.
    $( [15-Sep-1995] $)

  $( An element of an ordinal number is a subset of the number.  (The proof was
     shortened by Andrew Salmon, 25-Jul-2011.) $)
  onelss $p |- ( A e. On -> ( B e. A -> B C_ A ) ) $=
    ( con0 wcel word wss wi eloni ordelss ex syl ) ACDAEZBADZBAFZGAHLMNABIJK $.
    $( [25-Jul-2011] $) $( [5-Jun-1994] $)

  $( An element of an ordinal number is a subset of the number. $)
  onelssOLD $p |- ( A e. On -> ( B e. A -> B C_ A ) ) $=
    ( con0 wcel word wtr wss wi eloni ordtr trss 3syl ) ACDAEAFBADBAGHAIAJABKL
    $.
    $( [5-Jun-1994] $)

  $( Transitive law for ordinal classes. $)
  ordtr1 $p |- ( Ord C -> ( ( A e. B /\ B e. C ) -> A e. C ) ) $=
    ( word wtr wcel wa wi ordtr trel syl ) CDCEABFBCFGACFHCICABJK $.
    $( [12-Dec-2004] $)

  $( Transitive law for ordinal classes.  (The proof was shortened by Andrew
     Salmon, 25-Jul-2011.) $)
  ordtr2 $p |- ( ( Ord A /\ Ord C ) -> ( ( A C_ B /\ B e. C ) -> A e. C ) ) $=
    ( word wa wss wcel wpss wi ordelord ex ancld anc2li wb ordelpss ancoms
    sspsstr expcom syl6bi com23 imp32 com12 syl9 imp3a adantl sylibrd ) ADZCDZE
    ABFZBCGZEZACHZACGUHUKULIUGUHUIUJULUHUJUHUJBDZEZEZUIULUHUJUNUHUJUMUHUJUMCBJK
    LMUOUIULUHUJUMUIULIZUHUMUJUPUHUMUJUPIUHUMEUJBCHZUPUMUHUJUQNBCOPUIUQULABCQRS
    KTUAUBUCUDUEACOUF $.
    $( [25-Jul-2011] $) $( [12-Dec-2004] $)

  $( Transitive law for ordinal classes. $)
  ordtr2OLD $p |- ( ( Ord A /\ Ord C ) -> ( ( A C_ B /\ B e. C ) -> A e. C ) )
      $=
    ( word wa wss wcel wi wceq wo ordsseleq biimpd ordtr1 exp3a eleq1a com12
    a1i jaod com23 imp syl9 ex ordelord syl5 pm2.43d expdimp imp3a ) ADZCDZEZAB
    FZBCGZACGZUJULUKUMUHUIULUKUMHZUHUIULEZUNUHBDZUOUNHZUOUHUPUQUHUPEZUKABGZABIZ
    JZUOUMURUKVAABKLUIULVAUMHUIVAULUMUIUSULUMHZUTUIUSULUMABCMNUTVBHUIULUTUMBCAO
    PQRSTUAUBCBUCUDUEUFSUG $.
    $( [12-Dec-2004] $)

  $( Transitive law for ordinal numbers.  Theorem 7M(b) of [Enderton]
     p. 192. $)
  ontr1 $p |- ( C e. On -> ( ( A e. B /\ B e. C ) -> A e. C ) ) $=
    ( con0 wcel word wa wi eloni ordtr1 syl ) CDECFABEBCEGACEHCIABCJK $.
    $( [11-Aug-1994] $)

  $( Transitive law for ordinal numbers.  Exercise 3 of [TakeutiZaring]
     p. 40. $)
  ontr2 $p |- ( ( A e. On /\ C e. On ) ->
              ( ( A C_ B /\ B e. C ) -> A e. C ) ) $=
    ( word wss wcel wa wi con0 ordtr2 eloni syl2an ) ADCDABEBCFGACFHAIFCIFABCJA
    KCKL $.
    $( [6-Nov-2003] $)

  ${
    $d x y A $.  $d x y B $.
    $( The union of an ordinal stays the same if a subset equal to one of its
       elements is removed. $)
    ordunidif $p |- ( ( Ord A /\ B e. A ) -> U. ( A \ B ) = U. A ) $=
      ( vx vy word wcel wa cv wss cdif wrex wral cuni wceq wi con0 ordelon
      onelss syl wn eldif simplbi2 eloni ordirr syl5 adantl mpd jctild adantr
      sseq2 rcla4ev syl6 biimpri ssid jctir ex pm2.61d r19.21aiva unidif ) AEZB
      AFZGZCHZDHZIZDABJZKZCALVFMAMNVBVGCAVBVCAFZGZVCBFZVGVIVJBVFFZVCBIZGZVGVBVJ
      VMOVHVBVJVLVKVBBPFZVJVLOABQZBVCRSVBVNVKVOVAVNVKOUTVABBFTZVKVNVKVAVPBABUAU
      BVNBEVPBUCBUDSUEUFUGUHUIVEVLDBVFVDBVCUJUKULVHVJTZVGOVBVHVQVCVFFZVCVCIZGZV
      GVHVQVTVHVQGZVRVSVRWAVCABUAUMVCUNUOUPVEVSDVCVFVDVCVCUJUKULUFUQURCDABUSS
      $.
      $( [10-Dec-2004] $)
  $}

  ${
    $d x ps $.  $d x A $.
    onintss.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( If a property is true for an ordinal number, then the minimum ordinal
       number for which it is true is smaller or equal.  Theorem Schema 61 of
       [Suppes] p. 228. $)
    onintss $p |- ( A e. On -> ( ps -> |^| { x e. On | ph } C_ A ) ) $=
      ( con0 wcel crab cint wss wa elrab intss1 sylbir ex ) DFGZBACFHZIDJZPBKDQ
      GRABCDFELDQMNO $.
      $( [3-Oct-2003] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A way to show that an ordinal number equals the minimum of a collection
       of ordinal numbers: it must be in the collection, and it must not be
       larger than any member of the collection. $)
    oneqmini $p |- ( B C_ On -> ( ( A e. B /\ A. x e. A -. x e. B )
                    -> A = |^| B ) ) $=
      ( con0 wss wcel cv wn wral wa cint wceq wi wb ssel anim12d ontri1 syl6
      expdimp pm5.74d con2b syl6bb ralbidv2 ssint syl5bb biimprd expimpd
      intss1 a1i adantrd jcad eqss syl6ibr ) CDEZBCFZAGZCFZHZABIZJZBCKZEZVABEZJ
      BVALUNUTVBVCUNUOUSVBUNUOJZVBUSVDBUPEZACIUSVBVDVEURACBVDUQVEMUQUPBFZHZMVFU
      RMVDUQVEVGUNUOUQVEVGNZUNUOUQJBDFZUPDFZJVHUNUOVIUQVJCDBOCDUPOPBUPQRSTUQVFU
      AUBUCABCUDUEUFUGUNUOVCUSUOVCMUNBCUHUIUJUKBVAULUM $.
      $( [14-Nov-2003] $)
  $}

  $( The empty set is an ordinal class. $)
  ord0 $p |- Ord (/) $=
    ( c0 word wtr cep wwe df-ord tr0 we0 mpbir2an ) ABACADEAFGDHI $.
    $( [11-May-1994] $)

  $( The empty set is an ordinal number.  Corollary 7N(b) of [Enderton]
     p. 193. $)
  0elon $p |- (/) e. On $=
    ( c0 con0 wcel word ord0 0ex elon mpbir ) ABCADEAFGH $.
    $( [17-Sep-1993] $)

  $( A non-empty ordinal contains the empty set. $)
  ord0eln0 $p |- ( Ord A -> ( (/) e. A <-> A =/= (/) ) ) $=
    ( word c0 wcel wne ne0i wceq wn wo ord0 wa noel ordtri2 con2bid mpbiri
    mpan2 ord df-ne syl5ib impbid2 ) ABZCADZACEZACFUAACGZHUBUCUAUDUBUACBZUDUBIZ
    JUAUEKZUFACDZHALUGUHUFACMNOPQACRST $.
    $( [5-Apr-2007] $) $( [25-Nov-1995] $)

  $( An ordinal number contains zero iff it is nonzero. $)
  on0eln0 $p |- ( A e. On -> ( (/) e. A <-> A =/= (/) ) ) $=
    ( con0 wcel word c0 wne wb eloni ord0eln0 syl ) ABCADEACAEFGAHAIJ $.
    $( [30-Apr-2007] $) $( [6-Dec-2004] $)

  $( An alternate definition of a limit ordinal. $)
  dflim2 $p |- ( Lim A <-> ( Ord A /\ (/) e. A /\ A = U. A ) ) $=
    ( wlim word c0 wne cuni wceq w3a wcel df-lim wa ord0eln0 anbi1d pm5.32i
    3anass 3bitr4i bitr4i ) ABACZADEZAAFGZHZRDAIZTHZAJRUBTKZKRSTKZKUCUARUDUERUB
    STALMNRUBTORSTOPQ $.
    $( [4-Nov-2004] $)

  $( The intersection of the class of ordinal numbers is the empty set. $)
  inton $p |- |^| On = (/) $=
    ( c0 con0 wcel cint wceq 0elon int0el ax-mp ) ABCBDAEFBGH $.
    $( [20-Oct-2003] $)

  $( The empty set is not a limit ordinal.  (The proof was shortened by Andrew
     Salmon, 25-Jul-2011.) $)
  nlim0 $p |- -. Lim (/) $=
    ( c0 wlim word wcel cuni wceq w3a noel simp2 mto dflim2 mtbir ) ABACZAADZAA
    EFZGZPNAHMNOIJAKL $.
    $( [25-Jul-2011] $) $( [24-Mar-1995] $)

  $( The empty set is not a limit ordinal. $)
  nlim0OLD $p |- -. Lim (/) $=
    ( c0 wceq wlim wn eqid word wne cuni df-lim simp2bi necon2bi ax-mp ) AABACZ
    DAEMAAMAFAAGAAHBAIJKL $.
    $( [24-Mar-1995] $)

  $( A limit ordinal is ordinal. $)
  limord $p |- ( Lim A -> Ord A ) $=
    ( wlim word c0 wne cuni wceq df-lim simp1bi ) ABACADEAAFGAHI $.
    $( [4-May-1995] $)

  $( A limit ordinal is its own supremum (union). $)
  limuni $p |- ( Lim A -> A = U. A ) $=
    ( wlim word c0 wne cuni wceq df-lim simp3bi ) ABACADEAAFGAHI $.
    $( [4-May-1995] $)

  $( The union of a limit ordinal is a limit ordinal. $)
  limuni2 $p |- ( Lim A -> Lim U. A ) $=
    ( wlim cuni wceq wb limuni limeq syl ibi ) ABZACZBZJAKDJLEAFAKGHI $.
    $( [19-Sep-2006] $)

  $( A limit ordinal contains the empty set. $)
  0ellim $p |- ( Lim A -> (/) e. A ) $=
    ( wlim c0 wcel wne wceq nlim0 limeq mtbiri necon2ai word wb limord
    ord0eln0 syl mpbird ) ABZCADZACEZQACACFQCBGACHIJQAKRSLAMANOP $.
    $( [15-May-1994] $)

  $( A limit ordinal class that is also a set is an ordinal number. $)
  limelon $p |- ( ( A e. B /\ Lim A ) -> A e. On ) $=
    ( wcel wlim con0 word elong limord syl5bir imp ) ABCZADZAECZKMAFLABGAHIJ $.
    $( [26-Apr-2004] $)

  $( The class of all ordinal numbers in not empty. $)
  onn0 $p |- On =/= (/) $=
    ( c0 con0 wcel wne 0elon ne0i ax-mp ) ABCBADEBAFG $.
    $( [5-Apr-2007] $) $( [17-Sep-1995] $)

  $( Equality of successors.  (The proof was shortened by Andrew Salmon,
     25-Jul-2011.) $)
  suceq $p |- ( A = B -> suc A = suc B ) $=
    ( wceq csn cun csuc id sneq uneq12d df-suc 3eqtr4g ) ABCZAADZEBBDZEAFBFLABM
    NLGABHIAJBJK $.
    $( [25-Jul-2011] $) $( [30-Aug-1993] $)

  $( Equality of successors. $)
  suceqOLD $p |- ( A = B -> suc A = suc B ) $=
    ( wceq csn cun csuc sneq uneq2d uneq1 eqtrd df-suc 3eqtr4g ) ABCZAADZEZBBDZ
    EZAFBFMOAPEQMNPAABGHABPIJAKBKL $.
    $( [30-Aug-1993] $)

  $( Membership in a successor.  This one-way implication does not require that
     either ` A ` or ` B ` be sets. $)
  elsuci $p |- ( A e. suc B -> ( A e. B \/ A = B ) ) $=
    ( csuc wcel csn wo wceq cun df-suc eleq2i elun bitri elsni orim2i sylbi )
    ABCZDZABDZABEZDZFZRABGZFQABSHZDUAPUCABIJABSKLTUBRABMNO $.
    $( [6-Jun-1994] $)

  $( Membership in a successor.  Exercise 5 of [TakeutiZaring] p. 17. $)
  elsucg $p |- ( A e. C -> ( A e. suc B <-> ( A e. B \/ A = B ) ) ) $=
    ( wcel csn wo wceq csuc elsncg orbi2d cun df-suc eleq2i elun bitri syl5bb
    ) ACDZABDZABEZDZFZRABGZFABHZDZQTUBRABCIJUDABSKZDUAUCUEABLMABSNOP $.
    $( [15-Sep-1995] $)

  $( Variant of membership in a successor, requiring that ` B ` rather than
     ` A ` be a set. $)
  elsuc2g $p |- ( B e. C -> ( A e. suc B <-> ( A e. B \/ A = B ) ) ) $=
    ( wcel csn cun wceq wo csuc elsnc2g orbi2d elun syl5bb df-suc eleq2i ) BCDZ
    ABBEZFZDZABDZABGZHZABIZDPTAQDZHUBSPUDUATABCJKABQLMUCRABNOM $.
    $( [28-Oct-2003] $)

  ${
    elsuc.1 $e |- A e. _V $.
    $( Membership in a successor.  Exercise 5 of [TakeutiZaring] p. 17. $)
    elsuc $p |- ( A e. suc B <-> ( A e. B \/ A = B ) ) $=
      ( cvv wcel csuc wceq wo wb elsucg ax-mp ) ADEABFEABEABGHICABDJK $.
      $( [15-Sep-2003] $)

    $( Membership in a successor. $)
    elsuc2 $p |- ( B e. suc A <-> ( B e. A \/ B = A ) ) $=
      ( cvv wcel csuc wceq wo wb elsuc2g ax-mp ) ADEBAFEBAEBAGHICBADJK $.
      $( [15-Sep-2003] $)
  $}

  ${
    $d y A $.  $d x y $.
    hbsuc.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for successor. $)
    hbsuc $p |- ( y e. suc A -> A. x y e. suc A ) $=
      ( cv wcel wceq wo wal csuc hbeleq hbor visset elsuc albii 3imtr4i ) BEZCF
      ZQCGZHZTAIQCJFZUAAIRSADABCDKLQCBMNZUATAUBOP $.
      $( [15-Sep-2003] $)
  $}

  $( Membership in a successor. $)
  elelsuc $p |- ( A e. B -> A e. suc B ) $=
    ( wcel csuc wceq wo orc elsucg mpbird ) ABCZABDCJABEZFJKGABBHI $.
    $( [20-Jun-1998] $)

  ${
    $d x y A $.  $d x B $.
    $( Membership of a successor in another class. $)
    sucel $p |- ( suc A e. B <->
                E. x e. B A. y ( y e. x <-> ( y e. A \/ y = A ) ) ) $=
      ( csuc wcel cv wceq wrex wo wb wal risset dfcleq visset elsuc bibi2i
      albii bitri rexbii ) CEZDFAGZUAHZADIBGZUBFZUDCFUDCHJZKZBLZADIAUADMUCUHADU
      CUEUDUAFZKZBLUHBUBUANUJUGBUIUFUEUDCBOPQRSTS $.
      $( [29-Jun-2004] $)
  $}

  $( The successor of the empty set. $)
  suc0 $p |- suc (/) = { (/) } $=
    ( c0 csuc csn cun df-suc uncom un0 3eqtri ) ABAACZDIADIAEAIFIGH $.
    $( [1-Feb-2005] $)

  $( A proper class is its own successor. $)
  sucprc $p |- ( -. A e. _V -> suc A = A ) $=
    ( cvv wcel wn csuc c0 cun csn wceq snprc uneq2 sylbi df-suc syl5eq un0
    syl6eq ) ABCDZAEZAFGZAQAAHZGZSRQTFIUASIAJTFAKLAMNAOP $.
    $( [3-Apr-1995] $)

  ${
    unisuc.1 $e |- A e. _V $.
    $( A transitive class is equal to the union of its successor.  Combines
       Theorem 4E of [Enderton] p. 72 and Exercise 6 of [Enderton] p. 73. $)
    unisuc $p |- ( Tr A <-> U. suc A = A ) $=
      ( cuni wss cun wceq wtr csuc ssequn1 df-tr csn df-suc unieqi uniun unisn
      uneq2i 3eqtri eqeq1i 3bitr4i ) ACZADTAEZAFAGAHZCZAFTAIAJUCUAAUCAAKZEZCTUD
      CZEUAUBUEALMAUDNUFATABOPQRS $.
      $( [22-Sep-2004] $) $( [30-Aug-1993] $)
  $}

  $( A class is included in its own successor.  Part of Proposition 7.23 of
     [TakeutiZaring] p. 41 (generalized to arbitrary classes). $)
  sssucid $p |- A C_ suc A $=
    ( csn cun csuc ssun1 df-suc sseqtr4i ) AAABZCADAHEAFG $.
    $( [31-May-1994] $)

  $( Part of Proposition 7.23 of [TakeutiZaring] p. 41 (generalized).  (The
     proof was shortened by Scott Fenton, 20-Feb-2012.) $)
  sucidg $p |- ( A e. B -> A e. suc A ) $=
    ( wcel csuc wceq wo eqid olci elsucg mpbiri ) ABCAADCAACZAAEZFLKAGHAABIJ $.
    $( [20-Feb-2012] $) $( [25-Mar-1995] $)

  ${
    sucid.1 $e |- A e. _V $.
    $( A set belongs to its successor.  (The proof was shortened by Scott
       Fenton, 18-Feb-2012.) $)
    sucid $p |- A e. suc A $=
      ( cvv wcel csuc sucidg ax-mp ) ACDAAEDBACFG $.
      $( [20-Feb-2012] $) $( [22-Jun-1994] $)

    $( A set belongs to its successor.  (The proof was shortened by Alan Sare,
       18-Feb-2012.)  This proof was automatically generated from ~ sucidVD
       using translate_without_overwriting.cmd and minimizing. $)
    sucidOLD $p |- A e. suc A $=
      ( csn cun csuc wcel snid elun2 ax-mp df-suc eleqtrri ) AAACZDZAEALFAMFABG
      ALAHIAJK $.
      $( [18-Feb-2012] $) $( [22-Jun-1994] $)

    $( A set belongs to its successor.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    sucidOLDOLD $p |- A e. suc A $=
      ( csn cun csuc wcel wo snid olci elun mpbir df-suc eleqtrri ) AAACZDZAEAO
      FAAFZANFZGQPABHIAANJKALM $.
      $( [25-Jul-2011] $) $( [22-Jun-1994] $)

    $( A set belongs to its successor. $)
    sucidOLDOLDOLD $p |- A e. suc A $=
      ( csn wcel csuc snid wo cun df-suc eleq2i elun bitri biimpri olcs ax-mp
      ) AACZDZAAEZDZABFAADZQSSTQGZSAAPHZDUARUBAAIJAAPKLMNO $.
      $( [22-Jun-1994] $)
  $}

  ${
    $d x A $.
    $( Part of Proposition 7.23 of [TakeutiZaring] p. 41 (generalized). $)
    sucidgOLD $p |- ( A e. B -> A e. suc A ) $=
      ( vx cv csuc wcel wceq id suceq eleq12d visset sucid vtoclg ) CDZNEZFAAEZ
      FCABNAGZNAOPQHNAIJNCKLM $.
      $( [25-Mar-1995] $)
  $}

  $( No successor is empty. $)
  nsuceq0 $p |- suc A =/= (/) $=
    ( csuc c0 wne wceq wn cvv wcel noel eleq2 sucidg syl5cbi mtoi sucprc
    eqeq1d 0ex eleq1 mpbiri syl6bi con3d pm2.43i pm2.61i df-ne mpbir ) ABZCDUEC
    EZFZAGHZUGUHUFACHZAIUFAUEHUIUHUECAJAGKLMUHFZUGUJUFUHUJUFACEZUHUJUEACANOUKUH
    CGHPACGQRSTUAUBUECUCUD $.
    $( [16-Jan-2007] $) $( [3-Apr-1995] $)

  ${
    eqelsuc.1 $e |- A e. _V $.
    $( A set belongs to the successor of an equal set. $)
    eqelsuc $p |- ( A = B -> A e. suc B ) $=
      ( wceq csuc suceq sucid syl5eleq ) ABDAEBEAABFACGH $.
      $( [18-Aug-1994] $)
  $}

  ${
    $d z y A $.
    $( The successor of a transtive class is transitive.  (Contributed by Alan
       Sare, 11-Apr-2009.) $)
    suctr $p |- ( Tr A -> Tr suc A ) $=
      ( vz vy wtr cv wcel csuc wa wi wal wceq wo simpr visset elsuc sylib
      eleq2 simpl syl5cbi elelsuc syl6 trel exp3a adantrd syl8 jao mpdi
      19.21aivv dftr2 sylibr ) ADZBEZCEZFZUMAGZFZHZULUOFZIZCJBJUODUKUSBCUKUQUMA
      FZUMAKZLZURUQUPVBUNUPMUMACNOPUKUQVAURIZVBURIZUQVAULAFZURVAUNVEUQUMAULQUNU
      PRSULATZUAUKUQUTURIVCVDIUKUQUTVEURUKUNUTVEIUPUKUNUTVEAULUMUBUCUDVFUEUTURV
      AUFUAUGUGUHBCUOUIUJ $.
      $( [11-Apr-2009] $)
  $}

  $( A set whose successor belongs to a transitive class also belongs.  (The
     proof was shortened by Andrew Salmon, 12-Aug-2011.) $)
  trsuc $p |- ( ( Tr A /\ suc B e. A ) -> B e. A ) $=
    ( wtr csuc wcel wa trel cvv wss sssucid ssexg mpan sucidg syl ancri syl5
    imp ) ACZBDZAEZBAEZRBSEZTFUATABSGTUBTBHEZUBBSITUCBJBSAKLBHMNOPQ $.
    $( [12-Aug-2011] $) $( [5-Sep-2003] $)

  $( A set whose successor belongs to a transitive class also belongs. $)
  trsucOLD $p |- ( ( Tr A /\ suc B e. A ) -> B e. A ) $=
    ( wtr csuc wcel cvv wi trel exp3a sucidg syl5com wn sucprc eleq1d biimpd
    a1d pm2.61i imp ) ACZBDZAEZBAEZBFEZSUAUBGZGSBTEZUDUCSUEUAUBABTHIBFJKUCLZUDS
    UFUAUBUFTBABMNOPQR $.
    $( [5-Sep-2003] $)

  ${
    $d x y A $.
    $( The successor of a transitive set is transitive.  (Contributed by Scott
       Fenton, 21-Feb-2011.) $)
    trsuc2 $p |- ( Tr A -> Tr suc A ) $=
      ( vx vy wtr cv wcel csn wo wa wi wal csuc wceq trel orc syl6 a1i jaod
      eleq2 biimpac orim2i syl5 elsn orbi2i syl6ibr anbi2i syl5ib andi
      19.21aivv cun wb df-suc treq ax-mp dftr2 elun imbi12i albii bitri sylibr
      ) ADZBEZCEZFZVCAFZVCAGZFZHZIZVBAFZVBVFFZHZJZCKZBKZALZDZVAVMBCVAVDVEIZVDVG
      IZHZVLVIVAVRVDVCAMZIZHZVLVTVAWCVJVBAMZHZVLVAVRVJHWEWCVAVRWEVJVAVRVJWEAVBV
      CNVJWDOZPVJWEJVAWFQRWBVJVRWAVDVJVCAVBSTUAUBVKWDVJBAUCUDUEVSWBVRVGWAVDCAUC
      UFUDUGVDVEVGUHUGUIVQAVFUJZDZVOVPWGMVQWHUKAULVPWGUMUNWHVDVCWGFZIZVBWGFZJZC
      KZBKVOBCWGUOWMVNBWLVMCWJVIWKVLWIVHVDVCAVFUPUFVBAVFUPUQURURUSUSUT $.
      $( [21-Feb-2011] $)
  $}

  $( A member of the successor of a transitive class is a subclass of it. $)
  trsucss $p |- ( Tr A -> ( B e. suc A -> B C_ A ) ) $=
    ( wtr wcel wceq wo wss csuc trss wi eqimss a1i jaod elsuci syl5 ) ACZBADZBA
    EZFBAGZBAHDPQSRABIRSJPBAKLMBANO $.
    $( [4-Oct-2003] $)

  $( A subset of an ordinal belongs to its successor. $)
  ordsssuc $p |- ( ( A e. On /\ Ord B ) -> ( A C_ B <-> A e. suc B ) ) $=
    ( con0 wcel word wa wss wceq wo csuc wb ordsseleq eloni sylan elsucg
    adantr bitr4d ) ACDZBEZFABGZABDABHIZABJDZAESTUAKRABLAMNRUBUAKSABCOPQ $.
    $( [28-Nov-2003] $)

  $( A subset of an ordinal number belongs to its successor. $)
  onsssuc $p |- ( ( A e. On /\ B e. On ) -> ( A C_ B <-> A e. suc B ) ) $=
    ( con0 wcel word wss csuc wb ordsssuc eloni sylan2 ) ACDBEABFABGDHBCDABIBJK
    $.
    $( [15-Sep-1995] $)

  $( An ordinal subset of an ordinal number belongs to its successor.  (The
     proof was shortened by Andrew Salmon, 12-Aug-2011.) $)
  ordsssuc2 $p |- ( ( Ord A /\ B e. On ) -> ( A C_ B <-> A e. suc B ) ) $=
    ( cvv wcel word con0 wa wss csuc wb wi elong biimprd anim1d onsssuc syl6
    wn annim ssexg ex elisset a1d pm5.21ni sylbi expcom adantld pm2.61i ) ACDZA
    EZBFDZGZABHZABIZDZJZKUHUKAFDZUJGUOUHUIUPUJUHUPUIACLMNABOPUHQZUJUOUIUJUQUOUJ
    UQGUJUHKZQUOUJUHRULURUNULUJUHABFSTUNUHUJAUMUAUBUCUDUEUFUG $.
    $( [12-Aug-2011] $) $( [1-Feb-2005] $)

  $( An ordinal subset of an ordinal number belongs to its successor. $)
  ordsssuc2OLD $p |- ( ( Ord A /\ B e. On ) -> ( A C_ B <-> A e. suc B ) ) $=
    ( word con0 wcel wa wss csuc wi cvv wb ssexg elong syl onsssuc biimpd ex
    com13 imp sylbird exbiri com3r com3l impbid ) ACZBDEZFABGZABHZEZUEUFUGUIIZU
    GUFUEUIUGUFUEUIIUGUFFZUEADEZUIUKAJEULUEKABDLAJMNUGUFULUIIULUFUGUIULUFUJULUF
    FUGUIABOZPQRSTQRSUEUFUIUGIUIUEUFUGUIUEULUFUGIAUHMULUFUIUGULUFUGUIUMUAUBTUCS
    UD $.
    $( [1-Feb-2005] $)

  ${
    $d x A $.  $d x B $.
    $( When its successor is subtracted from a class of ordinal numbers, an
       ordinal number is less than the minimum of the resulting subclass. $)
    onmindif $p |- ( ( A C_ On /\ B e. On ) -> B e. |^| ( A \ suc B ) ) $=
      ( vx con0 wss wcel wa csuc cdif cint cv wral wn wi wb ontri1 onsssuc
      bitr3d con1bid ssel2 sylan biimpd exp31 com23 imp4b eldif syl5ib
      r19.21aiv elintg adantl mpbird ) ADEZBDFZGZBABHZIZJFZBCKZFZCUPLZUNUSCUPUN
      URAFZURUOFZMZGUSURUPFULUMVAVCUSULVAUMVCUSNZULVAUMVDULVAGZUMGVCUSURDFZUMVC
      USOVEVFUMGZUSVBVGURBEUSMVBURBPURBQRSADURTUAUBUCUDUEURAUOUFUGUHUMUQUTOULCB
      UPDUIUJUK $.
      $( [1-Dec-2003] $)
  $}

  $( There is no set between an ordinal class and its successor.  Generalized
     Proposition 7.25 of [TakeutiZaring] p. 41. $)
  ordnbtwn $p |- ( Ord A -> -. ( A e. B /\ B e. suc A ) ) $=
    ( word wcel wa wo csuc wn ioran ordn2lp ordirr sylanbrc wceq elsuci anim2i
    andi sylib eleq2 biimpac orim2i syl nsyl ) ACZABDZBADZEZAADZFZUDBAGDZEZUHHU
    FHUGHUCUFUGIABJAKLUJUFUDBAMZEZFZUHUJUDUEUKFZEUMUIUNUDBANOUDUEUKPQULUGUFUKUD
    UGBAARSTUAUB $.
    $( [21-Jun-1998] $)

  $( There is no set between an ordinal number and its successor.  Proposition
     7.25 of [TakeutiZaring] p. 41. $)
  onnbtwn $p |- ( A e. On -> -. ( A e. B /\ B e. suc A ) ) $=
    ( con0 wcel word csuc wa wn eloni ordnbtwn syl ) ACDAEABDBAFDGHAIABJK $.
    $( [9-Jun-1994] $)

  $( A set whose successor is a subset of another class is a member of that
     class. $)
  sucssel $p |- ( A e. C -> ( suc A C_ B -> A e. B ) ) $=
    ( csuc wss wcel ssel sucidg syl5com ) ADZBEAJFABFACFJBAGACHI $.
    $( [16-Sep-1995] $)

  $( Ordinal derived from its successor. $)
  orddif $p |- ( Ord A -> A = ( suc A \ { A } ) ) $=
    ( word csn cin c0 wceq csuc cdif orddisj disj3 cun df-suc difeq1i difun2
    eqtri eqeq2i bitr4i sylib ) ABAACZDEFZAAGZSHZFZAITAASHZFUCASJUBUDAUBASKZSHU
    DUAUESALMASNOPQR $.
    $( [20-May-1998] $)

  $( An ordinal class includes its union. $)
  orduniss $p |- ( Ord A -> U. A C_ A ) $=
    ( word wtr cuni wss ordtr df-tr sylib ) ABACADAEAFAGH $.
    $( [13-Sep-2003] $)

  $( A trichotomy law for ordinal classes.  (The proof was shortened by Andrew
     Salmon, 12-Aug-2011.) $)
  ordtri2or $p |- ( ( Ord A /\ Ord B ) -> ( A e. B \/ B C_ A ) ) $=
    ( word wa wcel wss wn wb ordtri1 ancoms biimprd orrd ) ACZBCZDZABEZBAFZOQPG
    ZNMQRHBAIJKL $.
    $( [12-Aug-2011] $) $( [13-Sep-2003] $)

  $( A trichotomy law for ordinal classes. $)
  ordtri2orOLD $p |- ( ( Ord A /\ Ord B ) -> ( A e. B \/ B C_ A ) ) $=
    ( word wa wcel wss wo wceq w3o ordtri3or 3orass sylib wb ordsseleq ancoms
    orcom eqcom orbi1i bitri syl6bb orbi2d mpbird ) ACZBCZDZABEZBAFZGUFABHZBAEZ
    GZGZUEUFUHUIIUKABJUFUHUIKLUEUGUJUFUEUGUIBAHZGZUJUDUCUGUMMBANOUMULUIGUJUIULP
    ULUHUIBAQRSTUAUB $.
    $( [13-Sep-2003] $)

  $( A trichotomy law for ordinal classes. $)
  ordtri2or2 $p |- ( ( Ord A /\ Ord B ) -> ( A C_ B \/ B C_ A ) ) $=
    ( word wa wcel wss wo ordtri2or wi ordelss ex orim1d adantl mpd ) ACZBCZDAB
    EZBAFZGZABFZRGZABHPSUAIOPQTRPQTBAJKLMN $.
    $( [2-Nov-2003] $)

  $( Property of a subclass of the maximum (i.e. union) of two ordinals. $)
  ordssun $p |- ( ( Ord B /\ Ord C ) ->
               ( A C_ ( B u. C ) <-> ( A C_ B \/ A C_ C ) ) ) $=
    ( word wa cun wss wo wi ordtri2or2 wceq wb ssequn1 sseq2 sylbi olc syl6bi
    ssequn2 orc jaoi syl ssun impbid1 ) BDCDEZABCFZGZABGZACGZHZUDBCGZCBGZHUFUII
    ZBCJUJULUKUJUFUHUIUJUECKUFUHLBCMUECANOUHUGPQUKUFUGUIUKUEBKUFUGLCBRUEBANOUGU
    HSQTUAABCUBUC $.
    $( [28-Nov-2003] $)

  $( The maximum (i.e. union) of two ordinals is either one or the other.
     Similar to Exercise 14 of [TakeutiZaring] p. 40. $)
  ordequn $p |- ( ( Ord B /\ Ord C ) ->
               ( A = ( B u. C ) -> ( A = B \/ A = C ) ) ) $=
    ( word wa wss wo cun wceq wi ordtri2or2 wb ssequn1 eqeq2 sylbi olc syl6bi
    ssequn2 orc jaoi syl ) BDCDEBCFZCBFZGABCHZIZABIZACIZGZJZBCKUBUIUCUBUEUGUHUB
    UDCIUEUGLBCMUDCANOUGUFPQUCUEUFUHUCUDBIUEUFLCBRUDBANOUFUGSQTUA $.
    $( [28-Nov-2003] $)

  $( The maximum (i.e. union) of two ordinals is ordinal.  Exercise 12 of
     [TakeutiZaring] p. 40. $)
  ordun $p |- ( ( Ord A /\ Ord B ) -> Ord ( A u. B ) ) $=
    ( word wa cun wceq wo eqid ordequn mpi ordeq biimprcd jaao mpd ) ACZBCZDZAB
    EZAFZRBFZGZRCZQRRFUARHRABIJOSUBPTSUBORAKLTUBPRBKLMN $.
    $( [28-Nov-2003] $)

  ${
    $d x A $.  $d x B $.
    $( A subclass relationship for union and successor of ordinal classes. $)
    ordunisssuc $p |- ( ( A C_ On /\ Ord B ) ->
                      ( U. A C_ B <-> A C_ suc B ) ) $=
      ( vx con0 wss word wa cv wral csuc wcel cuni wb ordsssuc ssel2 sylan
      an1rs ralbidva unissb dfss3 3bitr4g ) ADEZBFZGZCHZBEZCAIUEBJZKZCAIALBEAUG
      EUDUFUHCAUBUEAKZUCUFUHMZUEDKUCUJUBUIGUEBNADUEOPQRCABSCAUGTUA $.
      $( [28-Nov-2003] $)
  $}

  $( The successor operation behaves like a one-to-one function.  Compare
     Exercise 16 of [Enderton] p. 194. $)
  suc11 $p |- ( ( A e. On /\ B e. On ) -> ( suc A = suc B <-> A = B ) ) $=
    ( con0 wcel wa csuc wceq wn wo wi word eloni ordn2lp ianor sylib syl
    adantr wss sucssel eqimss syl5 elsuci ord com12 syl9 eqimss2 eqcom syl6ib
    jaao mpd suceq impbid1 ) ACDZBCDZEZAFZBFZGZABGZUOABDZHZBADZHZIZURUSJZUMVDUN
    UMAKZVDALVFUTVBEHVDABMUTVBNOPQUMVAVEUNVCUMURAUQDZVAUSUMUPUQRVGURAUQCSUPUQTU
    AVGVAUSVGUTUSABUBUCUDUEUNURBUPDZVCUSUNUQUPRVHURBUPCSUQUPUFUAVCVHBAGZUSVHVCV
    IVHVBVIBAUBUCUDBAUGUHUEUIUJABUKUL $.
    $( [3-Sep-2003] $)

  ${
    on.1 $e |- A e. On $.
    $( An ordinal number is an ordinal class. $)
    onordi $p |- Ord A $=
      ( con0 wcel word eloni ax-mp ) ACDAEBAFG $.
      $( [11-Jun-1994] $)

    $( An ordinal number is a transitive class. $)
    ontrci $p |- Tr A $=
      ( word wtr onordi ordtr ax-mp ) ACADABEAFG $.
      $( [11-Jun-1994] $)

    $( An ordinal number is not a member of itself.  Theorem 7M(c) of
       [Enderton] p. 192. $)
    onirri $p |- -. A e. A $=
      ( word wcel wn onordi ordirr ax-mp ) ACAADEABFAGH $.
      $( [11-Jun-1994] $)

    $( A member of an ordinal number is an ordinal number.  Theorem 7M(a) of
       [Enderton] p. 192. $)
    oneli $p |- ( B e. A -> B e. On ) $=
      ( con0 wcel onelon mpan ) ADEBAEBDECABFG $.
      $( [11-Jun-1994] $)

    $( A member of an ordinal number is a subset of it. $)
    onelssi $p |- ( B e. A -> B C_ A ) $=
      ( con0 wcel wss wi onelss ax-mp ) ADEBAEBAFGCABHI $.
      $( [11-Aug-1994] $)

    $( An ordering law for ordinal numbers. $)
    onssneli $p |- ( A C_ B -> -. B e. A ) $=
      ( wcel wss con0 word wn oneli eloni ordirr 3syl ssel com12 mtod con2i )
      BADZABEZQRBBDZQBFDBGSHABCIBJBKLRQSABBMNOP $.
      $( [13-Jun-1994] $)

    $( An ordering law for ordinal numbers. $)
    onssnel2i $p |- ( B C_ A -> -. A e. B ) $=
      ( wss wcel onirri ssel mtoi ) BADABEAAEACFBAAGH $.
      $( [13-Jun-1994] $)

    $( An element of an ordinal number equals the intersection with it. $)
    onelini $p |- ( B e. A -> B = ( B i^i A ) ) $=
      ( wcel wss cin wceq onelssi dfss sylib ) BADBAEBBAFGABCHBAIJ $.
      $( [11-Jun-1994] $)

    $( An ordinal number equals its union with any element. $)
    oneluni $p |- ( B e. A -> ( A u. B ) = A ) $=
      ( wcel wss cun wceq onelssi ssequn2 sylib ) BADBAEABFAGABCHBAIJ $.
      $( [23-Sep-2004] $) $( [13-Jun-1994] $)

    $( An ordinal number is equal to the union of its successor. $)
    onunisuci $p |- U. suc A = A $=
      ( wtr csuc cuni wceq ontrci con0 elisseti unisuc mpbi ) ACADEAFABGAAHBIJK
      $.
      $( [22-Sep-2004] $) $( [12-Jun-1994] $)

    ${
      on.2 $e |- B e. On $.
      $( Subset is equivalent to membership or equality for ordinal numbers. $)
      onsseli $p |- ( A C_ B <-> ( A e. B \/ A = B ) ) $=
        ( con0 wcel wss wceq wo wb onsseleq mp2an ) AEFBEFABGABFABHIJCDABKL $.
        $( [15-Sep-1995] $)

      $( The union of two ordinal numbers is an ordinal number. $)
      onun2i $p |- ( A u. B ) e. On $=
        ( wcel wss wo cun con0 word onordi ordtri2or mp2an oneluni syl6eqel
        wceq ssequn1 eleq1 mpbiri sylbi jaoi ax-mp ) BAEZABFZGZABHZIEZBJAJUEBDK
        ACKBALMUCUGUDUCUFAIABCNCOUDUFBPZUGABQUHUGBIEDUFBIRSTUAUB $.
        $( [13-Jun-1994] $)
    $}
  $}

  $( An ordinal equal to its own union is either zero or a limit ordinal. $)
  unizlim $p |- ( Ord A -> ( A = U. A <-> ( A = (/) \/ Lim A ) ) ) $=
    ( word cuni wceq c0 wlim wo wa wn wi wne w3a df-lim biimpri 3exp df-ne
    syl5ibr com23 imp orrd ex uni0 eqcomi id unieq 3eqtr4a limuni jaoi impbid1
    ) ABZAACZDZAEDZAFZGZUJULUOUJULHUMUNUJULUMIZUNJUJUPULUNUJAEKZULUNJUPUJUQULUN
    UNUJUQULLAMNOAEPQRSTUAUMULUNUMEECZAUKUREUBUCUMUDAEUEUFAUGUHUI $.
    $( [1-Oct-2003] $)

  $( An ordinal number either equals zero or contains zero. $)
  on0eqel $p |- ( A e. On -> ( A = (/) \/ (/) e. A ) ) $=
    ( con0 wcel c0 wceq wo wss 0ss wb 0elon onsseleq mpan mpbii eqcom orbi2i
    orcom bitri sylib ) ABCZDACZDAEZFZADEZTFZSDAGZUBAHDBCSUEUBIJDAKLMUBTUCFUDUA
    UCTDANOTUCPQR $.
    $( [1-Jun-2004] $)

  $( The singleton of the singleton of the empty set is not an ordinal (nor a
     natural number by ~ omsson ).  It can be used to represent an "undefined"
     value for a partial operation on natural or ordinal numbers.  See also
     ~ onxpdisj .  (The proof was shortened by Andrew Salmon, 12-Aug-2011.) $)
  snsn0non $p |- -. { { (/) } } e. On $=
    ( c0 csn con0 wcel wceq wo wn p0ex snid n0i ax-mp 0ex eqcom mtbir elsnc
    pm3.2ni on0eqel mto ) ABZBZCDTAEZATDZFUAUBSTDUAGSHITSJKUBASEZUCSAEZASDUDGAL
    ISAJKASMNASLONPTQR $.
    $( [12-Aug-2011] $) $( [21-May-2004] $)

  $( The singleton of the singleton of the empty set is not an ordinal (nor a
     natural number by ~ omsson ).  It can be used to represent an "undefined"
     value for a partial operation on natural or ordinal numbers.  See also
     ~ onxpdisj . $)
  snsn0nonOLD $p |- -. { { (/) } } e. On $=
    ( c0 csn con0 wcel wss 0ex snnz wceq elsnc eqcom bitri nemtbir snid ssel
    mpi mto p0ex onelss ) ABZBZCDZSTEZUBATDZUCSAAFGUCASHSAHASFIASJKLUBASDUCAFMS
    TANOPUASTDUBSQMTSROP $.
    $( [21-May-2004] $)


