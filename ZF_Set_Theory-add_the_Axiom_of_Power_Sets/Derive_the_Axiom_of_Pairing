$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Derive the Axiom of Pairing
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x z w v $.  $d y z w v $.
    $( The Axiom of Pairing of Zermelo-Fraenkel set theory.  Axiom 2 of
       [TakeutiZaring] p. 15.  In some textbooks this is stated as a separate
       axiom; here we show it is redundant since it can be derived from the
       other axioms.

       This theorem should not be referenced by any proof other than ~ axpr .
       Instead, use ~ zfpair2 below so that the uses of the Axiom of Pairing
       can be more easily identified. $)
    zfpair $p |- { x , y } e. _V $=
      ( vw vz vv cv cpr wceq wo cab cvv dfpr2 c0 csn wa wex 19.43 prlem2 exbii
      19.41v 0ex isseti mpbiran p0ex orbi12i 3bitr3ri abbii pp0ex eqeltrri wi
      wal equequ2 0inp0 prlem1 19.21adv a4imev orcom con2i syl7bi jaoi zfrep4
      eqeltri ) AFZBFZGCFZVCHZVEVDHZIZCJZKCVCVDLVIDFZMHZVJMNZHZIZVKVFOZVMVGOZIZ
      OZDPZCJKVHVSCVQDPVODPZVPDPZIVSVHVOVPDQVQVRDVKVFVMVGRSVTVFWAVGVTVKDPVFVKVF
      DTDMUAUBUCWAVMDPVGVMVGDTDVLUDUBUCUEUFUGVNVQDCEMVLGVNDJKDMVLLUHUIVKVQVEEFZ
      HZUJZCUKZEPVMVKWEEAWBVCHZVKWDCWFVKVFVMVGWCEACULVJUMZUNUOUPVMWEEBWBVDHZVMW
      DCVQVPVOIWHVMWCVOVPUQWHVMVGVKVFWCEBCULVKVMWGURUNUSUOUPUTVAVBVB $.
      $( [18-Oct-1995] $)

    $( Unabbreviated version of the Axiom of Pairing of ZF set theory, derived
       as a theorem from the other axioms.

       This theorem should not be referenced by any proof.  Instead, use
       ~ ax-pr below so that the uses of the Axiom of Pairing can be more
       easily identified. $)
    axpr $p |- E. z A. w ( ( w = x \/ w = y ) -> w e. z ) $=
      ( cv cpr wceq wex wo wcel wi wal zfpair isseti wb dfcleq visset elpr
      bibi2i bi2 sylbi alimi eximi ax-mp ) CEZAEZBEZFZGZCHDEZUFGUJUGGIZUJUEJZKZ
      DLZCHCUHABMNUIUNCUIULUJUHJZOZDLUNDUEUHPUPUMDUPULUKOUMUOUKULUJUFUGDQRSULUK
      TUAUBUAUCUD $.
      $( [14-Nov-2006] $)

    $( The Axiom of Pairing of ZF set theory.  It was derived as theorem ~ axpr
       above and is therefore redundant, but we state it as a separate axiom
       here so that its uses can be identified more easily. $)
    ax-pr $a |- E. z A. w ( ( w = x \/ w = y ) -> w e. z ) $.

    $( Derive the abbreviated version of the Axiom of Pairing from ~ ax-pr .
       See ~ zfpair for its derivation from the other axioms. $)
    zfpair2 $p |- { x , y } e. _V $=
      ( vz vw cv cpr wceq wex wcel wo wb wal ax-pr bm1.3ii dfcleq visset elpr
      bibi2i albii bitri exbii mpbir issetri ) CAEZBEZFZCEZUFGZCHDEZUGIZUIUDGUI
      UEGJZKZDLZCHUKCDABCDMNUHUMCUHUJUIUFIZKZDLUMDUGUFOUOULDUNUKUJUIUDUEDPQRSTU
      AUBUC $.
      $( [14-Nov-2006] $)
  $}

  ${
    $d x A $.
    $( A singleton is a set.  Theorem 7.13 of [Quine] p. 51, but proved using
       only Extensionality and Pairing.  See also ~ snexALT .  (Contributed by
       Mario Carneiro, 19-May-2013.) $)
    snex $p |- { A } e. _V $=
      ( vx cvv wcel csn cpr dfsn2 cv wceq preq12 anidms eleq1d zfpair2 vtoclg
      syl5eqel wn c0 snprc biimpi 0ex syl6eqel pm2.61i ) ACDZAEZCDUCUDAAFZCAGBH
      ZUFFZCDUECDBACUFAIZUGUECUHUGUEIUFUFAAJKLBBMNOUCPZUDQCUIUDQIARSTUAUB $.
      $( [19-May-2013] $) $( [7-Aug-1994] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The Axiom of Pairing using class variables.  Theorem 7.13 of [Quine]
       p. 51.  By virtue of its definition, an unordered pair remains a set
       (even though no longer a pair) even when its components are proper
       classes (see ~ prprc ), so we can dispense with hypotheses requiring
       them to be sets. $)
    prex $p |- { A , B } e. _V $=
      ( vx vy cvv wcel cpr wi cv wceq preq2 eleq1d zfpair2 vtoclg preq1 syl5ib
      vtocleg wn csn prprc1 snex syl6eqel prprc2 pm2.61nii ) AEFZBEFZABGZEFZUFU
      HHCAEUFCIZBGZEFZUIAJZUHUIDIZGZEFUKDBEUMBJUNUJEUMBUIKLCDMNULUJUGEUIABOLPQU
      ERUGBSEABTBUAUBUFRUGASEABUCAUAUBUD $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x y $.
    $( Every set is an element of some other set.  This has a shorter proof
       than ~ el but uses more axioms. $)
    elALT $p |- E. y x e. y $=
      ( cv csn wcel wex visset snid snex eleq2 cla4ev ax-mp ) ACZMDZEZMBCZEZBFM
      AGHQOBNMIPNMJKL $.
      $( [4-Jan-2002] $)
  $}

  ${
    $( A singleton of a set belongs to the power class of a class containing
       the set.  (Contributed by Alan Sare, 25-Aug-2011.) $)
    snelpwi $p |- ( A e. B -> { A } e. ~P B ) $=
      ( wcel csn wss cpw snssi snex elpw sylibr ) ABCADZBEKBFCABGKBAHIJ $.
      $( [25-Aug-2011] $)
  $}

  ${
    snelpw.1 $e |- A e. _V $.
    $( A singleton of a set belongs to the power class of a class containing
       the set. $)
    snelpw $p |- ( A e. B <-> { A } e. ~P B ) $=
      ( wcel csn wss cpw snss snex elpw bitr4i ) ABDAEZBFLBGDABCHLBAIJK $.
      $( [1-Apr-1998] $)
  $}

  ${
    $d x y z $.
    $( A theorem similar to extensionality, requiring the existence of a
       singleton.  Exercise 8 of [TakeutiZaring] p. 16. $)
    rext $p |- ( A. z ( x e. z -> y e. z ) -> x = y ) $=
      ( cv wcel wi wal csn wceq visset snid snex eleq2 imbi12d cla4v mpi elsn
      equcomi sylbi syl ) ADZCDZEZBDZUBEZFZCGZUDUAHZEZUAUDIZUGUAUHEZUIUAAJKUFUK
      UIFCUHUALUBUHIUCUKUEUIUBUHUAMUBUHUDMNOPUIUDUAIUJBUAQBARST $.
      $( [10-Aug-1993] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Classes are subclasses if and only if their power classes are
       subclasses.  Exercise 18 of [TakeutiZaring] p. 18. $)
    sspwb $p |- ( A C_ B <-> ~P A C_ ~P B ) $=
      ( vx wss cpw cv wcel sstr2 com12 visset elpw 3imtr4g ssrdv csn ssel snex
      snss bitr4i 3imtr3g impbii ) ABDZAEZBEZDZUACUBUCUACFZADZUEBDZUEUBGUEUCGUF
      UAUGUEABHIUEACJZKUEBUHKLMUDCABUDUENZUBGZUIUCGZUEAGZUEBGZUBUCUIOUJUIADULUI
      AUEPZKUEAUHQRUKUIBDUMUIBUNKUEBUHQRSMT $.
      $( [13-Oct-1996] $)
  $}

  ${
    $d A x y $.
    $( A class equals the union of its power class.  Exercise 6(a) of
       [Enderton] p. 38.  (The proof was shortened by Alan Sare,
       28-Dec-2008.) $)
    unipw $p |- U. ~P A = A $=
      ( vx vy cpw cuni cv wcel wa wex eluni wss wi visset elpw ssel sylbi
      impcom 19.23aiv csn snid snelpwi elunii sylancr impbii eqriv ) BADZEZABFZ
      UGGZUHAGZUIUHCFZGZUKUFGZHZCIUJCUHUFJUNUJCUMULUJUMUKAKULUJLUKACMNUKAUHOPQR
      PUJUHUHSZGUOUFGUIUHBMTUHAUAUHUOUFUBUCUDUE $.
      $( [29-Dec-2008] $) $( [14-Oct-1996] $)
  $}

  $( Membership of a power class.  Exercise 10 of [Enderton] p. 26. $)
  pwel $p |- ( A e. B -> ~P A e. ~P ~P U. B ) $=
    ( wcel cpw cuni wss elssuni sspwb sylib cvv wb pwexg elpwg syl mpbird ) ABC
    ZADZBEZDZDCZQSFZPARFUAABGARHIPQJCTUAKABLQSJMNO $.
    $( [13-Jan-2007] $)

  ${
    $d A x $.  $d B x $.
    $( An extensionality-like principle defining subclass in terms of
       subsets. $)
    ssextss $p |- ( A C_ B <-> A. x ( x C_ A -> x C_ B ) ) $=
      ( wss cpw cv wcel wi wal sspwb dfss2 visset elpw imbi12i albii 3bitri )
      BCDBEZCEZDAFZQGZSRGZHZAISBDZSCDZHZAIBCJAQRKUBUEATUCUAUDSBALZMSCUFMNOP $.
      $( [30-Jun-2004] $)

    $( An extensionality-like principle that uses the subset instead of the
       membership relation: two classes are equal iff they have the same
       subsets. $)
    ssext $p |- ( A = B <-> A. x ( x C_ A <-> x C_ B ) ) $=
      ( wss wa cv wi wal wceq wb ssextss anbi12i eqss albiim 3bitr4i ) BCDZCBDZ
      EAFZBDZRCDZGAHZTSGAHZEBCISTJAHPUAQUBABCKACBKLBCMSTANO $.
      $( [30-Jun-2004] $)

    $( Negation of subclass relationship.  Compare ~ nss .  (The proof was
       shortened by Andrew Salmon, 25-Jul-2011.) $)
    nssss $p |- ( -. A C_ B <-> E. x ( x C_ A /\ -. x C_ B ) ) $=
      ( wss wn cv wi wal wa wex ssextss notbii exanali bitr4i ) BCDZEAFZBDZPCDZ
      GAHZEQREIAJOSABCKLQRAMN $.
      $( [25-Jul-2011] $) $( [30-Jun-2004] $)
  $}

  $( Classes are equal if and only if their power classes are equal.  Exercise
     19 of [TakeutiZaring] p. 18. $)
  pweqb $p |- ( A = B <-> ~P A = ~P B ) $=
    ( wss wa cpw wceq sspwb anbi12i eqss 3bitr4i ) ABCZBACZDAEZBEZCZNMCZDABFMNF
    KOLPABGBAGHABIMNIJ $.
    $( [13-Oct-1996] $)

  ${
    $d x A $.
    intid.1 $e |- A e. _V $.
    $( The intersection of all sets to which a set belongs is the singleton of
       that set. $)
    intid $p |- |^| { x | A e. x } = { A } $=
      ( cv wcel cab cint csn cvv wss snex eleq2 snid intmin3 ax-mp wi elintab
      id mpgbir snssi eqssi ) BADZEZAFGZBHZUEIEUDUEJBKUCBUEEAUEIUBUEBLBCMNOBUDE
      ZUEUDJUFUCUCPAUCABCQUCRSBUDTOUA $.
      $( [5-Jun-2009] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( "At most one" existence implies a class abstraction exists. $)
    moabex $p |- ( E* x ph -> { x | ph } e. _V ) $=
      ( vy wmo cv wceq wi wal wex cab cvv wcel ax-17 mo2 wa hba1 wb pm4.71
      biimpi a4s ancom syl6bb abbid csn df-sn snex eqeltrri simpl ss2abi ssexi
      syl6eqel 19.23aiv sylbi ) ABDABECEZFZGZBHZCIABJZKLZABCACMNUQUSCUQURUOAOZB
      JZKUQAUTBUPBPUQAAUOOZUTUPAVBQZBUPVCAUORSTAUOUAUBUCVAUOBJZUNUDVDKBUNUEUNUF
      UGUTUOBUOAUHUIUJUKULUM $.
      $( [30-Dec-1996] $)
  $}

  $( The abstraction of a wff with existential uniqueness exists. $)
  euabex $p |- ( E! x ph -> { x | ph } e. _V ) $=
    ( weu wmo cab cvv wcel eumo moabex syl ) ABCABDABEFGABHABIJ $.
    $( [25-Nov-1994] $)

  ${
    $d x y A $.
    $( A non-empty class (even if proper) has a non-empty subset. $)
    nnullss $p |- ( A =/= (/) -> E. x ( x C_ A /\ x =/= (/) ) ) $=
      ( vy c0 wne cv wcel wex wss wa n0 csn visset snss snnz snex wceq sseq1
      neeq1 anbi12d cla4ev mpan2 sylbi 19.23aiv ) BDECFZBGZCHAFZBIZUGDEZJZAHZCB
      KUFUKCUFUELZBIZUKUEBCMZNUMULDEZUKUEUNOUJUMUOJAULUEPUGULQUHUMUIUOUGULBRUGU
      LDSTUAUBUCUDUC $.
      $( [27-May-2007] $) $( [23-Aug-2003] $)
  $}

  ${
    $d x y z A $.  $d y z ph $.
    $( Restricted existence in a class (even if proper) implies restricted
       existence in a subset. $)
    exss $p |- ( E. x e. A ph -> E. y ( y C_ A /\ E. x e. y ph ) ) $=
      ( vz wrex cv wcel wa cab wex wss crab c0 wne df-rab neeq1i rabn0 n0
      3bitr3i csn visset snss ssab2 sstr2 mpi sylbi wsbc simpr wceq equsb1
      elsn sbbii mpbir jctil df-clab sban bitri eleq2i 3bitri 3imtr4i ne0i syl
      sylib snex sseq1 rexeq anbi12d cla4ev syl11anc 19.23aiv ) ABDFZEGZBGZDHZA
      IZBJZHZEKZCGZDLZABVTFZIZCKZABDMZNOVQNOVLVSWEVQNABDPQABDREVQSTVRWDEVRVMUAZ
      DLZABWFFZWDVRWFVQLZWGVMVQEUBUCWIVQDLWGABDUDWFVQDUEUFUGVRABWFMZNOZWHVRVMWJ
      HZWKVOBVMUHZABVMUHZIZVNWFHZBVMUHZWNIZVRWLWOWNWQWMWNUIWQVNVMUJZBVMUHBEUKWP
      WSBEBVMULUMUNUOVRVPBVMUHWOVPEBUPVOABEUQURWLVMWPAIZBJZHWTBVMUHWRWJXAVMABWF
      PUSWTEBUPWPABEUQUTVAWJVMVBVCABWFRVDWCWGWHICWFVMVEVTWFUJWAWGWBWHVTWFDVFABV
      TWFVGVHVIVJVKUG $.
      $( [23-Aug-2003] $)
  $}

  $( An ordered pair of classes is a set.  Exercise 7 of [TakeutiZaring]
     p. 16. $)
  opex $p |- <. A , B >. e. _V $=
    ( cop csn cpr cvv df-op prex eqeltri ) ABCADZABEZEFABGJKHI $.
    $( [18-Aug-1993] $)

  ${
    elop.1 $e |- A e. _V $.
    $( An ordered pair has two elements.  Exercise 3 of [TakeutiZaring]
       p. 15. $)
    elop $p |- ( A e. <. B , C >. <-> ( A = { B } \/ A = { B , C } ) ) $=
      ( cop wcel csn cpr wceq wo df-op eleq2i elpr bitri ) ABCEZFABGZBCHZHZFAPI
      AQIJORABCKLAPQDMN $.
      $( [5-Aug-1993] $)
  $}

  $( One of the two elements in an ordered pair. $)
  opi1 $p |- { A } e. <. A , B >. $=
    ( csn cpr cop snex prid1 df-op eleqtrri ) ACZJABDZDABEJKAFGABHI $.
    $( [5-Aug-1993] $)

  $( One of the two elements of an ordered pair. $)
  opi2 $p |- { A , B } e. <. A , B >. $=
    ( cpr csn cop prex prid2 df-op eleqtrri ) ABCZADZJCABEKJABFGABHI $.
    $( [5-Aug-1993] $)

