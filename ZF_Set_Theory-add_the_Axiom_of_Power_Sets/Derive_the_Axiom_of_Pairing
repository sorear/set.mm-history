$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Derive the Axiom of Pairing
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x z w v $.  $d y z w v $.
    $( The Axiom of Pairing of Zermelo-Fraenkel set theory.  Axiom 2 of
       [TakeutiZaring] p. 15.  In some textbooks this is stated as a separate
       axiom; here we show it is redundant since it can be derived from the
       other axioms.

       This theorem should not be referenced by any proof other than ~ axpr .
       Instead, use ~ zfpair2 below so that the uses of the Axiom of Pairing
       can be more easily identified. $)
    zfpair $p |- { x , y } e. _V $=
      ( vw vz vv cv cpr wceq wo cab cvv dfpr2 c0 csn wa wex 19.43 prlem2 exbii
      19.41v 0ex isseti mpbiran p0ex orbi12i 3bitr3ri abbii pp0ex eqeltrri wi
      wal equequ2 0inp0 prlem1 19.21adv a4imev con2i orcom syl7ib jaoi zfrep4
      eqeltri ) AFZBFZGCFZVCHZVEVDHZIZCJZKCVCVDLVIDFZMHZVJMNZHZIZVKVFOZVMVGOZIZ
      OZDPZCJKVHVSCVQDPVODPZVPDPZIVSVHVOVPDQVQVRDVKVFVMVGRSVTVFWAVGVTVKDPVFVKVF
      DTDMUAUBUCWAVMDPVGVMVGDTDVLUDUBUCUEUFUGVNVQDCEMVLGVNDJKDMVLLUHUIVKVQVEEFZ
      HZUJZCUKZEPVMVKWEEAWBVCHZVKWDCWFVKVFVMVGWCEACULVJUMZUNUOUPVMWEEBWBVDHZVMW
      DCWHVMVPVOIWCVQWHVMVGVKVFWCEBCULVKVMWGUQUNVOVPURUSUOUPUTVAVBVB $.
      $( [18-Oct-1995] $)

    $( Unabbreviated version of the Axiom of Pairing of ZF set theory, derived
       as a theorem from the other axioms.

       This theorem should not be referenced by any proof.  Instead, use
       ~ ax-pr below so that the uses of the Axiom of Pairing can be more
       easily identified. $)
    axpr $p |- E. z A. w ( ( w = x \/ w = y ) -> w e. z ) $=
      ( cv cpr wceq wex wo wcel wi wal zfpair isseti wb dfcleq visset elpr
      bibi2i bi2 sylbi 19.20i 19.22i ax-mp ) CEZAEZBEZFZGZCHDEZUFGUJUGGIZUJUEJZ
      KZDLZCHCUHABMNUIUNCUIULUJUHJZOZDLUNDUEUHPUPUMDUPULUKOUMUOUKULUJUFUGDQRSUL
      UKTUAUBUAUCUD $.
      $( [14-Nov-2006] $) $( [14-Nov-2006] $)

    $( The Axiom of Pairing of ZF set theory.  It was derived as theorem ~ axpr
       above and is therefore redundant, but we state it as a separate axiom
       here so that its uses can be identified more easily. $)
    ax-pr $a |- E. z A. w ( ( w = x \/ w = y ) -> w e. z ) $.

    $( Derive the abbreviated version of the Axiom of Pairing from ~ ax-pr .
       See ~ zfpair for its derivation from the other axioms. $)
    zfpair2 $p |- { x , y } e. _V $=
      ( vz vw cv cpr wceq wex wcel wo wb wal ax-pr bm1.3ii dfcleq visset elpr
      bibi2i albii bitri exbii mpbir issetri ) CAEZBEZFZCEZUFGZCHDEZUGIZUIUDGUI
      UEGJZKZDLZCHUKCDABCDMNUHUMCUHUJUIUFIZKZDLUMDUGUFOUOULDUNUKUJUIUDUEDPQRSTU
      AUBUC $.
      $( [15-Nov-2006] $) $( [14-Nov-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The Axiom of Pairing using class variables.  Theorem 7.13 of [Quine]
       p. 51.  By virtue of its definition, an unordered pair remains a set
       (even though no longer a pair) even when its components are proper
       classes (see ~ prprc ), so we can dispense with hypotheses requiring
       them to be sets. $)
    prex $p |- { A , B } e. _V $=
      ( vx vy cvv wcel cpr wi cv wceq preq1 eleq1d preq2 zfpair2 vtoclg syl5bi
      vtocleg wn csn prprc1 snex syl6eqel prprc2 pm2.61nii ) AEFZBEFZABGZEFZUFU
      HHCAECIZAJZUIBGZEFZUHUFUJUKUGEUIABKLUIDIZGZEFULDBEUMBJUNUKEUMBUIMLCDNOPQU
      ERUGBSEABTBUAUBUFRUGASEABUCAUAUBUD $.
      $( [5-Aug-1993] $)
  $}

  $( An ordered pair of classes is a set.  Exercise 7 of [TakeutiZaring]
     p. 16. $)
  opex $p |- <. A , B >. e. _V $=
    ( cop csn cpr cvv df-op prex eqeltri ) ABCADZABEZEFABGJKHI $.
    $( [18-Aug-1993] $)

  ${
    elop.1 $e |- A e. _V $.
    $( An ordered pair has two elements.  Exercise 3 of [TakeutiZaring]
       p. 15. $)
    elop $p |- ( A e. <. B , C >. <-> ( A = { B } \/ A = { B , C } ) ) $=
      ( cop wcel csn cpr wceq wo df-op eleq2i elpr bitri ) ABCEZFABGZBCHZHZFAPI
      AQIJORABCKLAPQDMN $.
      $( [5-Aug-1993] $)
  $}

  $( One of the two elements in an ordered pair. $)
  opi1 $p |- { A } e. <. A , B >. $=
    ( csn cpr cop snex prid1 df-op eleqtrri ) ACZJABDZDABEJKAFGABHI $.
    $( [5-Aug-1993] $)

  $( One of the two elements of an ordered pair. $)
  opi2 $p |- { A , B } e. <. A , B >. $=
    ( cpr csn cop prex prid2 df-op eleqtrri ) ABCZADZJCABEKJABFGABHI $.
    $( [5-Aug-1993] $)

