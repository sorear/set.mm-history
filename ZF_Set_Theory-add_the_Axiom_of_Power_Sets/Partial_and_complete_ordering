$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                  Partial and complete ordering
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$(
We have not yet defined relations ( ~ df-rel ), but here but introduce a
few related notions we will use to develop ordinals.  The class variable
` R ` is no different from other class variables, but it reminds us that
normally it represents what we will later call a "relation."
$)

  $( Declare new constant symbols. $)
  $c Po $.  $( Partial ordering predicate symbol (read: 'partial ordering'). $)
  $c Or $.  $( Strict complete ordering predicate symbol (read: 'orders'). $)

  $( Extend wff notation to include the strict partial ordering predicate.
     Read:  '` R ` is a partial order on ` A ` .' $)
  wpo $a wff R Po A $.

  $( Extend wff notation to include the strict complete ordering predicate.
     Read:  '` R ` orders ` A ` .' $)
  wor $a wff R Or A $.

  ${
    $d x y z R $.  $d x y z A $.
    $( Define a strict partial order relation.  Definition of [Enderton]
       p. 168. $)
    df-po $a |- ( R Po A <-> A. x e. A A. y e. A A. z e. A
                ( -. x R x /\ ( ( x R y /\ y R z ) -> x R z ) ) ) $.
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    $( Subset theorem for the partial ordering predicate. $)
    poss $p |- ( A (_ B -> ( R Po B -> R Po A ) ) $=
      ( vx vy vz wss cv wbr wn wa wi wral wpo wcel w3a wal ssel 3anim123d
      imim1d 19.20dv r3al 3imtr4g df-po ) ABGZDHZUFCIJUFEHZCIUGFHZCIKUFUHCILKZF
      BMEBMDBMZUIFAMEAMDAMZBCNACNUEUFBOZUGBOZUHBOZPZUILZFQZEQZDQUFAOZUGAOZUHAOZ
      PZUILZFQZEQZDQUJUKUEURVEDUEUQVDEUEUPVCFUEVBUOUIUEUSULUTUMVAUNABUFRABUGRAB
      UHRSTUAUAUAUIDEFBBBUBUIDEFAAAUBUCDEFBCUDDEFACUDUC $.
      $( [27-Mar-1997] $)
  $}

  ${
    $d x y z R $.  $d x y z S $.  $d x y z A $.
    $( Equality theorem for partial ordering predicate. $)
    poeq1 $p |- ( R = S -> ( R Po A <-> S Po A ) ) $=
      ( vx vy vz wceq cv wbr wn wa wi wral wpo breq negbid anbi12d imbi12d
      ralbidv 2ralbidv df-po 3bitr4g ) BCGZDHZUDBIZJZUDEHZBIZUGFHZBIZKZUDUIBIZL
      ZKZFAMZEAMDAMUDUDCIZJZUDUGCIZUGUICIZKZUDUICIZLZKZFAMZEAMDAMABNACNUCUOVDDE
      AAUCUNVCFAUCUFUQUMVBUCUEUPUDUDBCOPUCUKUTULVAUCUHURUJUSUDUGBCOUGUIBCOQUDUI
      BCORQSTDEFABUADEFACUAUB $.
      $( [27-Mar-1997] $)
  $}

  $( Equality theorem for partial ordering predicate. $)
  poeq2 $p |- ( A = B -> ( R Po A <-> R Po B ) ) $=
    ( wceq wpo wss wa wi wb poss anim12i eqss bi 3imtr4 bicomd ) ABDZBCEZACEZAB
    FZBAFZGQRHZRQHZGPQRISUATUBABCJBACJKABLQRMNO $.
    $( [27-Mar-1997] $)

  ${
    $d x y z R $.  $d x y z A $.
    $d x y z B $.  $d x y z C $.  $d x y z D $.
    $( Properties of partial order relation in class notation. $)
    pocl $p |- ( R Po A -> ( ( B e. A /\ C e. A /\ D e. A ) ->
               ( -. B R B /\ ( ( B R C /\ C R D ) -> B R D ) ) ) ) $=
      ( vx vy vz wcel w3a wpo wbr wn wa wi cv wceq id breq12d negbid breq1
      anbi1d imbi12d anbi12d imbi2d breq2 imbi1d anbi2d wal wral df-po r3al
      bitr biimp 19.21bbi 19.21bi com12 vtocl3ga ) BAICAIDAIJAEKZBBELZMZBCELZCD
      ELZNZBDELZOZNZUSFPZVHELZMZVHGPZELZVKHPZELZNZVHVMELZOZNZOUSVABVKELZVNNZBVM
      ELZOZNZOUSVAVBCVMELZNZWAOZNZOUSVGOFGHBCDAAAVHBQZVRWCUSWHVJVAVQWBWHVIUTWHV
      HBVHBEWHRZWISTWHVOVTVPWAWHVLVSVNVHBVKEUAUBVHBVMEUAUCUDUEVKCQZWCWGUSWJWBWF
      VAWJVTWEWAWJVSVBVNWDVKCBEUFVKCVMEUAUDUGUHUEVMDQZWGVGUSWKWFVFVAWKWEVDWAVEW
      KWDVCVBVMDCEUFUHVMDBEUFUCUHUEUSVHAIVKAIVMAIJZVRUSWLVROZHUSWMHUIZFGUSWNGUI
      FUIZUSVRHAUJGAUJFAUJWOFGHAEUKVRFGHAAAULUMUNUOUPUQURUQ $.
      $( [27-Mar-1997] $)
  $}

  $( A partial order relation is irreflexive. $)
  poirr $p |- ( ( R Po A /\ B e. A ) -> -. B R B ) $=
    ( wpo wcel w3a wbr wn wa wi pocl imp pm3.26d df-3an anabs1 anidm 3bitrr
    sylan2b ) ACDZBAEZTTFZBBCGZHZTSUAIUCUBUBIUBJZSUAUCUDIABBBCKLMUATTIZTIUETTTT
    NTTOTPQR $.
    $( [27-Mar-1997] $)

  $( A partial order relation is a transitive relation. $)
  potr $p |- ( ( R Po A /\ ( B e. A /\ C e. A /\ D e. A ) ) ->
             ( ( B R C /\ C R D ) -> B R D ) ) $=
    ( wpo wcel w3a wa wbr wn wi pocl imp pm3.27d ) AEFZBAGCAGDAGHZIBBEJKZBCEJCD
    EJIBDEJLZPQRSIABCDEMNO $.
    $( [27-Mar-1997] $)

  $( A partial order relation has no 2-cycle loops. $)
  po2nr $p |- ( ( R Po A /\ ( B e. A /\ C e. A ) ) ->
              -. ( B R C /\ C R B ) ) $=
    ( wpo wcel wa wbr wn poirr adantrr wi potr 3exp2 com34 pm2.43d imp32 mtod
    ) ADEZBAFZCAFZGGBCDHCBDHGZBBDHZSTUCIUAABDJKSTUAUBUCLZSTUAUDLSTUATUDSTUATUDA
    BCBDMNOPQR $.
    $( [27-Mar-1997] $)

  $( A partial order relation has no 3-cycle loops. $)
  po3nr $p |- ( ( R Po A /\ ( B e. A /\ C e. A /\ D e. A ) ) ->
             -. ( B R C /\ C R D /\ D R B ) ) $=
    ( wpo wcel w3a wa wbr wn po2nr 3adantr2 potr anim1d df-3an syl5ib mtod ) AE
    FZBAGZCAGZDAGZHIZBCEJZCDEJZDBEJZHZBDEJZUFIZSTUBUIKUAABDELMUCUDUEIZUFIUIUGUC
    UJUHUFABCDENOUDUEUFPQR $.
    $( [27-Mar-1997] $)

  ${
    $d x y z R $.
    $( Any relation is a partial ordering of the empty set. $)
    po0 $p |- R Po (/) $=
      ( vx vy vz c0 wpo cv wbr wn wa wi wral df-po wcel noel pm2.21i mprgbir )
      EAFBGZRAHIRCGZAHSDGZAHJRTAHKJDELCELZBEBCDEAMRENUAROPQ $.
      $( [28-Mar-1997] $)
  $}

  ${
    $d x y R $.  $d x y A $.
    $( Define a strict complete (linear) order relation. $)
    df-so $a |- ( R Or A <-> ( R Po A /\ A. x e. A A. y e. A
                ( x R y \/ x = y \/ y R x ) ) ) $.
  $}

  ${
    $d x y R $.  $d x y A $.
    $( A strict linear order is a strict partial order. $)
    sopo $p |- ( R Or A -> R Po A ) $=
      ( vx vy wor wpo cv wbr wceq w3o wral df-so pm3.26bi ) ABEABFCGZDGZBHNOION
      BHJDAKCAKCDABLM $.
      $( [28-Mar-1997] $)
  $}

  ${
    $d x y R $.  $d x y A $.  $d x y B $.
    $( Subset theorem for the strict ordering predicate. $)
    soss $p |- ( A (_ B -> ( R Or B -> R Or A ) ) $=
      ( vx vy wss wpo cv wbr wceq w3o wral wa wor poss wcel wi wal ssel
      anim12d imim1d 19.20dv r2al 3imtr4g df-so ) ABFZBCGZDHZEHZCIUHUIJUIUHCIKZ
      EBLDBLZMACGZUJEALDALZMBCNACNUFUGULUKUMABCOUFUHBPZUIBPZMZUJQZERZDRUHAPZUIA
      PZMZUJQZERZDRUKUMUFURVCDUFUQVBEUFVAUPUJUFUSUNUTUOABUHSABUISTUAUBUBUJDEBBU
      CUJDEAAUCUDTDEBCUEDEACUEUD $.
      $( [16-Mar-1997] $)
  $}

  ${
    $d x y R $.  $d x y S $.  $d x y A $.
    $( Equality theorem for the strict ordering predicate. $)
    soeq1 $p |- ( R = S -> ( R Or A <-> S Or A ) ) $=
      ( vx vy wceq wpo cv wbr w3o wral wa wor poeq1 breq pm4.2i 3orbi123d
      2ralbidv anbi12d df-so 3bitr4g ) BCFZABGZDHZEHZBIZUDUEFZUEUDBIZJZEAKDAKZL
      ACGZUDUECIZUGUEUDCIZJZEAKDAKZLABMACMUBUCUKUJUOABCNUBUIUNDEAAUBUFULUGUGUHU
      MUDUEBCOUBUGPUEUDBCOQRSDEABTDEACTUA $.
      $( [16-Mar-1997] $)
  $}

  $( Equality theorem for the strict ordering predicate. $)
  soeq2 $p |- ( A = B -> ( R Or A <-> R Or B ) ) $=
    ( wceq wor wss wa wi wb soss anim12i eqss bi 3imtr4 bicomd ) ABDZBCEZACEZAB
    FZBAFZGQRHZRQHZGPQRISUATUBABCJBACJKABLQRMNO $.
    $( [16-Mar-1997] $)

  $( A strict order relation is irreflexive. $)
  sonr $p |- ( ( R Or A /\ B e. A ) -> -. B R B ) $=
    ( wpo wcel wbr wn wor poirr sopo sylan ) ACDBAEBBCFGACHABCIACJK $.
    $( [24-Nov-1995] $)

  $( A strict order relation is a transitive relation. $)
  sotr $p |- ( ( R Or A /\ ( B e. A /\ C e. A /\ D e. A ) ) ->
             ( ( B R C /\ C R D ) -> B R D ) ) $=
    ( wpo wcel w3a wbr wa wi wor potr sopo sylan ) AEFBAGCAGDAGHBCEICDEIJBDEIKA
    ELABCDEMAENO $.
    $( [21-Jan-1996] $)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y R $.

    $( A strict order relation is linear (satisfies trichotomy). $)
    solin $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              ( B R C \/ B = C \/ C R B ) ) $=
      ( vx vy wcel wa wor wbr wceq w3o cv wi breq1 eqeq1 breq2 3orbi123d
      imbi2d eqeq2 wpo wral df-so ra42 adantl sylbi com12 vtocl2ga impcom ) BAG
      CAGHADIZBCDJZBCKZCBDJZLZUJEMZFMZDJZUOUPKZUPUODJZLZNUJBUPDJZBUPKZUPBDJZLZN
      UJUNNEFBCAAUOBKZUTVDUJVEUQVAURVBUSVCUOBUPDOUOBUPPUOBUPDQRSUPCKZVDUNUJVFVA
      UKVBULVCUMUPCBDQUPCBTUPCBDORSUJUOAGUPAGHZUTUJADUAZUTFAUBEAUBZHVGUTNZEFADU
      CVIVJVHUTEFAAUDUEUFUGUHUI $.
      $( [21-Jan-1996] $)
  $}

  $( A strict order relation has no 2-cycle loops. $)
  so2nr $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              -. ( B R C /\ C R B ) ) $=
    ( wpo wcel wa wbr wn wor po2nr sopo sylan ) ADEBAFCAFGBCDHCBDHGIADJABCDKADL
    M $.
    $( [21-Jan-1996] $)

  $( A strict order relation has no 3-cycle loops. $)
  so3nr $p |- ( ( R Or A /\ ( B e. A /\ C e. A /\ D e. A ) ) ->
             -. ( B R C /\ C R D /\ D R B ) ) $=
    ( wpo wcel w3a wbr wn wor po3nr sopo sylan ) AEFBAGCAGDAGHBCEICDEIDBEIHJAEK
    ABCDELAEMN $.
    $( [21-Jan-1996] $)

  $( A strict order relation satisfies strict trichotomy. $)
  sotric $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              ( B R C <-> -. ( B = C \/ C R B ) ) ) $=
    ( wor wcel wa wceq wbr wo wn wi breq2 negbid sonr syl5cbi adantrr so2nr
    imnan sylibr con2d jaod w3o solin 3orass df-or bitr sylib impbid con2bid )
    ADEZBAFZCAFZGGZBCHZCBDIZJZBCDIZUNUQURKZUNUOUSUPUKULUOUSLUMUOBBDIZKUSUKULGUO
    UTURBCBDMNABDOPQUNURUPUNURUPGKURUPKLABCDRURUPSTUAUBUNURUOUPUCZUSUQLZABCDUDV
    AURUQJVBURUOUPUEURUQUFUGUHUIUJ $.
    $( [19-Feb-1996] $)

  $( Trichotomy law for strict order relation. $)
  sotrieq $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              ( B = C <-> -. ( B R C \/ C R B ) ) ) $=
    ( wor wcel wa wceq wbr wn wo wi breq2 negbid sonr syl5bi syl5bir anim12d
    com12 anandis sotric con2bid biimpar ord con1d ex imp3a impbid ioran
    syl6bbr ) ADEZBAFZCAFZGGZBCHZBCDIZJZCBDIZJZGZUPURKJUNUOUTUKULUMUOUTLUOUKULG
    ZUKUMGZGUTUOVAUQVBUSUOBBDIZJUQVAUOVCUPBCBDMNABDOPUOUSCCDIZJVBUOURVDBCCDMNAC
    DOQRSTUNUQUSUOUNUQUSUOLUNUQGZUOURVEUOURUNUOURKZUQUNUPVFABCDUAUBUCUDUEUFUGUH
    UPURUIUJ $.
    $( [9-Apr-1996] $)

  $( Trichotomy law for strict order relation. $)
  sotrieq2 $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              ( B = C <-> ( -. B R C /\ -. C R B ) ) ) $=
    ( wor wcel wa wceq wbr wo wn sotrieq ioran syl6bb ) ADEBAFCAFGGBCHBCDIZCBDI
    ZJKOKPKGABCDLOPMN $.
    $( [5-May-1999] $)

  ${
    $d x y z R $.  $d x y z A $.
    itlso.1 $e |- ( x e. A -> -. x R x ) $.
    itlso.2 $e |- ( ( x e. A /\ y e. A /\ z e. A ) ->
                  ( ( x R y /\ y R z ) -> x R z ) ) $.
    itlso.3 $e |- ( ( x e. A /\ y e. A ) -> ( x R y \/ x = y \/ y R x ) ) $.
    $( A irreflexive, transitive, linear relation is a strict ordering. $)
    itlso $p |- R Or A $=
      ( wor wpo cv wbr wceq w3o wral df-so wn wa wi wcel w3a 3ad2ant1 jca
      rgen3 df-po mpbir rgen2a mpbir2an ) DEIDEJZAKZBKZELZUJUKMUKUJELNZBDOADOAB
      DEPUIUJUJELQZULUKCKZELRUJUOELSZRZCDOBDOADOUQABCDDDUJDTZUKDTZUODTZUAUNUPUR
      USUNUTFUBGUCUDABCDEUEUFUMABDHUGUH $.
      $( [21-Jan-1996] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.
    so.1 $e |- ( ( x e. A /\ y e. A /\ z e. A ) ->
                  ( ( x R y <-> -. ( x = y \/ y R x ) ) /\
                 ( ( x R y /\ y R z ) -> x R z ) ) ) $.
    $( Deduce strict ordering from its properties. $)
    so $p |- R Or A $=
      ( cv wcel wbr wn wa wceq wo eqid orci wb wi eleq1 anbi2d eqeq2 breq1
      orbi12d breq2 negbid bibi12d imbi12d w3a 3anbi3d imbi1d pm3.26d chvarv
      3anidm23 con2bid mpbii anidms pm3.27d w3o biimprd 3orass df-or bitr
      sylibr itlso ) ABCDEAGZDHZVDVDEIZJZVEVEKZVDVDLZVFMZVGVIVFVDNOVEBGZDHZKZVD
      VKLZVKVDEIZMZVDVKEIZJZPZQVHVJVGPZQBAVKVDLZVMVHVSVTWAVLVEVEVKVDDRSWAVPVJVR
      VGWAVNVIVOVFVKVDVDTVKVDVDEUAUBWAVQVFVKVDVDEUCUDUEUFVMVQVPVEVLVQVPJPZVEVLC
      GZDHZUGZWBQVEVLVLUGZWBQCBWCVKLZWEWFWBWGWDVLVEVLWCVKDRUHUIWEWBVQVKWCEIKVDW
      CEIQZFUJUKULUMZUKUNUOWEWBWHFUPVMVRVPQZVQVNVOUQZVMVPVRWIURWKVQVPMWJVQVNVOU
      SVQVPUTVAVBVC $.
      $( [29-Jan-1996] $)
  $}

  ${
    $d x y R $.
    $( Any relation is a strict ordering of the empty set. $)
    so0 $p |- R Or (/) $=
      ( vx vy c0 wor cv wbr wceq w3o wral wpo df-so po0 mpbiran wcel noel
      pm2.21i mprgbir ) DAEZBFZCFZAGTUAHUATAGICDJZBDSDAKUBBDJBCDALAMNTDOUBTPQR
      $.
      $( [16-Mar-1997] $)
  $}


