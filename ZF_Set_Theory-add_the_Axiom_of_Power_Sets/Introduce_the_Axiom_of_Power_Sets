$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Introduce the Axiom of Power Sets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Axiom of Power Sets.  An axiom of Zermelo-Fraenkel set theory.  It
       states that a set ` y ` exists that includes the power set of a given
       set ` x ` i.e. contains every subset of ` x ` .  The variant ~ axpow2
       uses explicit subset notation.  A version using class notation is
       ~ pwex . $)
    ax-pow $a |- E. y A. z ( A. w ( w e. z -> w e. x ) -> z e. y ) $.

    $( Axiom of Power Sets expressed with fewest number of different
       variables. $)
    zfpow $p |- E. x A. y ( A. x ( x e. y -> x e. z ) -> y e. x ) $=
      ( vw cv wcel wi wal wex ax-pow wceq elequ1 imbi12d cbvalv imbi1i albii
      exbii mpbi ) DEZBEZFZSCEZFZGZDHZTAEZFZGZBHZAIUFTFZUFUBFZGZAHZUGGZBHZAICAB
      DJUIUOAUHUNBUEUMUGUDULDASUFKUAUJUCUKDABLDACLMNOPQR $.
      $( [14-Aug-2003] $)

    $( A variant of the Axiom of Power Sets ~ ax-pow using subset notation.
       Problem in {BellMachover] p. 466. $)
    axpow2 $p |- E. y A. z ( z C_ x -> z e. y ) $=
      ( vw cv wss wcel wi wal wex ax-pow dfss2 imbi1i albii exbii mpbir ) CEZAE
      ZFZQBEGZHZCIZBJDEZQGUCRGHDIZTHZCIZBJABCDKUBUFBUAUECSUDTDQRLMNOP $.
      $( [4-Jun-2006] $)

    $( A variant of the Axiom of Power Sets ~ ax-pow .  For any set ` x ` ,
       there exists a set ` y ` whose members are exactly the subsets of ` x `
       i.e. the power set of ` x ` .  Axiom Pow of [BellMachover] p. 466. $)
    axpow3 $p |- E. y A. z ( z C_ x <-> z e. y ) $=
      ( cv wss wcel wb wal wex axpow2 bm1.3ii bicom albii exbii mpbir ) CDZADEZ
      PBDFZGZCHZBIRQGZCHZBIQBCABCJKTUBBSUACQRLMNO $.
      $( [26-Jun-2009] $) $( [4-Jun-2006] $)
  $}

  ${
    $d w x y z $.
    $( Every set is an element of some other set.  See ~ elALT for a shorter
       proof using more axioms.  (The proof was shortened by Andrew Salmon,
       25-Jul-2011.) $)
    el $p |- E. y x e. y $=
      ( vz cv wcel wi wal wex zfpow wceq ax-14 19.21aiv ax-13 imim2d pm2.27
      sylsyld a4imv eximi ax-mp ) BDZCDZETADZEFZBGZUATEZFZCGZBHUBTEZBHBCAIUGUHB
      UFUHCAUAUBJZUDUFUDUHFUHUIUCBCABKLUIUEUHUDCABMNUDUHOPQRS $.
      $( [25-Jul-2011] $) $( [4-Jan-2002] $)
  $}

  ${
    $d A x y z $.
    zfpowcl.1 $e |- A e. _V $.
    $( Power set axiom expressed in class notation.  Axiom 4 of [TakeutiZaring]
       p. 17.  (The proof was shortened by Andrew Salmon, 25-Jul-2011.) $)
    pwex $p |- ~P A e. _V $=
      ( vz vy vx cv cpw cvv wcel wceq pweq eleq1d wss cab wex wb wal axpow2
      bm1.3ii abeq2 exbii mpbir isset df-pw eleq1i vtocl ) CFZGZHIZAGZHICABUGAJ
      UHUJHUGAKLUIDFZUGMZDNZHIZUNEFZUMJZEOZUQUKUOIULPDQZEOULEDCEDRSUPUREULDUOTU
      AUBEUMUCUBUHUMHDUGUDUEUBUF $.
      $( [25-Jul-2011] $) $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.
    $( Power set axiom expressed in class notation, with the sethood
       requirement as an antecedent.  Axiom 4 of [TakeutiZaring] p. 17. $)
    pwexg $p |- ( A e. V -> ~P A e. _V ) $=
      ( vx cv cpw cvv wcel wceq pweq eleq1d visset pwex vtoclg ) CDZEZFGAEZFGCA
      BNAHOPFNAIJNCKLM $.
      $( [30-Oct-2003] $)

    $( Existence of a class of subsets.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    abssexg $p |- ( A e. V -> { x | ( x C_ A /\ ph ) } e. _V ) $=
      ( wcel cpw cvv cv wss wa cab pwexg df-pw eleq1i simpl ss2abi ssexg mpan
      sylbi syl ) CDECFZGEZBHCIZAJZBKZGEZCDLUBUCBKZGEZUFUAUGGBCMNUEUGIUHUFUDUCB
      UCAOPUEUGGQRST $.
      $( [25-Jul-2011] $) $( [15-Jul-2006] $)
  $}

  ${
    $d x y A $.
    $( A singleton is a set.  Theorem 7.13 of [Quine] p. 51, but proved using
       only Extensionality, Power Set, and Separation.  Unlike the proof of
       ~ zfpair , Replacement is not needed.  (The proof was shortened by
       Andrew Salmon, 25-Jul-2011.)  See also ~ snex . $)
    snexALT $p |- { A } e. _V $=
      ( cpw cvv wcel csn wss snsspw ssexg mpan wn pwexg con3i c0 wceq snprc
      biimpi 0ex syl6eqel syl pm2.61i ) ABZCDZAEZCDZUCUAFUBUDAGUCUACHIUBJACDZJZ
      UDUEUBACKLUFUCMCUFUCMNAOPQRST $.
      $( [25-Jul-2011] $) $( [7-Aug-1994] $)
  $}

  $( The power set of the empty set (the ordinal 1) is a set. $)
  p0ex $p |- { (/) } e. _V $=
    ( c0 cpw csn cvv pw0 0ex pwex eqeltrri ) ABACDEAFGH $.
    $( [23-Dec-1993] $)

  $( The power set of the empty set (the ordinal 1) is a set.  This has a
     shorter proof than ~ p0ex but uses more axioms. $)
  p0exALT $p |- { (/) } e. _V $=
    ( c0 snexALT ) AB $.
    $( [23-Dec-1993] $)

  $( The power set of the power set of the empty set (the ordinal 2) is a
     set. $)
  pp0ex $p |- { (/) , { (/) } } e. _V $=
    ( c0 csn cpw cpr cvv pwpw0 p0ex pwex eqeltrri ) ABZCAJDEFJGHI $.
    $( [5-Aug-1993] $)

  $( The ordinal number 3 is a set, proved without the Axiom of Union
     ~ ax-un . $)
  ord3ex $p |- { (/) , { (/) } , { (/) , { (/) } } } e. _V $=
    ( c0 csn cpr ctp cun cvv df-tp cpw pwpr pp0ex pwex eqeltrri wss snsspr2
    unss2 ax-mp ssexi eqeltri ) AABZASCZDTTBZEZFASTGUBTSBZTCZEZTHUEFASITJKLUAUD
    MUBUEMUCTNUAUDTOPQR $.
    $( [2-May-2009] $)

  ${
    $d w x y z $.
    $( At least two sets exist (or in terms of first-order logic, the universe
       of discourse has two or more objects).  Note that we may not substitute
       the same variable for both ` x ` and ` y ` (as indicated by the distinct
       variable requirement), for otherwise we would contradict ~ stdpc6 .

       This theorem is proved directly from set theory axioms (no set theory
       definitions) and does not use ~ ax-16 , ~ ax-ext , or ~ ax-sep .  See
       ~ dtruALT for a shorter proof using these axioms. $)
    dtru $p |- -. A. x x = y $=
      ( vw vz cv wceq wn wex wal wcel wa eeanv wi ax-pow id ax-gen elequ2
      imbi1d albidv elequ1 imbi12d a4v mpi eximi ax-mp ax-nul ax-4 mpbir2an
      ax-14 com12 con3d imp 2eximi equequ2 notbid ax-8 a4imev syl6bi a1d
      pm2.61i 19.23aivv exnal mpbi ) AEZBEZFZGZAHZVFAIGCEZDEZFZGZDHCHZVHVDVIJZV
      DVJJZGZKZDHCHZVMVRVNCHZVPDHZVNVPCDLVEVJJZVEVDJZMZBIZVJVIJZMZDIZCHVSACDBNW
      GVNCWGWBWBMZBIZVNWHBWBOPWFWIVNMDAVJVDFZWDWIWEVNWJWCWHBWJWAWBWBDABQRSDACTU
      AUBUCUDUEVPAIZDHVTDAUFWKVPDVPAUGUDUEUHVQVLCDVNVPVLVNVKVOVKVNVOCDAUIUJUKUL
      UMUEVLVHCDVJVEFZVLVHMWLVLVIVEFZGZVHWLVKWMDBCUNUOWNVGACVDVIFVFWMACBUPUKUQU
      RWLGZVHVLWOVGADVDVJFVFWLADBUPUKUQUSUTVAUEVFAVBVC $.
      $( [7-Nov-2006] $)
  $}

  ${
    $d x y $.
    $( This theorem shows that axiom ~ ax-16 is redundant in the presence of
       theorem ~ dtru , which states simply that at least two things exist.
       This justifies the remark at
       ~ http://us.metamath.org/mpegif/mmzfcnd.html#twoness (which links to
       this theorem). $)
    ax16b $p |- ( A. x x = y -> ( ph -> A. x ph ) ) $=
      ( cv wceq wal wi dtru pm2.21i ) BDCDEBFAABFGBCHI $.
      $( [7-Nov-2006] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( Existential uniqueness implies there is a value for which the wff
       argument is false. $)
    eunex $p |- ( E! x ph -> E. x -. ph ) $=
      ( vy wex cv wceq wi wal wa wn weu dtru alim mtoi 19.23aiv adantl ax-17
      eu3 exnal 3imtr4i ) ABDZABECEFZGBHZCDZIABHZJZABKAJBDUDUFUAUCUFCUCUEUBBHBC
      LAUBBMNOPABCACQRABST $.
      $( [24-Oct-2010] $)
  $}

  ${
    $d A x $.
    $( A class is a subclass of the power class of its union.  Exercise 6(b) of
       [Enderton] p. 38. $)
    pwuni $p |- A C_ ~P U. A $=
      ( vx cuni cpw cv wcel wss elssuni visset elpw sylibr ssriv ) BAACZDZBEZAF
      OMGONFOAHOMBIJKL $.
      $( [14-Oct-1996] $)
  $}

  ${
    $d x y $.
    $( A version of ~ dtru ("two things exist") with a shorter proof that uses
       more axioms but may be easier to understand.

       Assuming that ZF set theory is consistent, we cannot prove this theorem
       unless we specify that ` x ` and ` y ` be distinct.  Specifically,
       theorem ~ cla4ev requires that ` x ` must not occur in the subexpression
       ` -. y = { (/) } ` in step 4 nor in the subexpression ` -. y = (/) ` in
       step 9.  The proof verifier will require that ` x ` and ` y ` be in a
       distinct variable group to ensure this.  You can check this by deleting
       the $d statement in set.mm and rerunning the verifier, which will print
       a detailed explanation of the distinct variable violation. $)
    dtruALT $p |- -. A. x x = y $=
      ( cv wceq wn wex wal c0 csn 0inp0 p0ex eqeq2 notbid cla4ev syl 0ex
      pm2.61i exnal eqcom albii notbii bitri mpbi ) BCZACZDZEZAFZUEUDDZAGZEZUDH
      DZUHULUDHIZDZEZUHUDJUGUOAUMKUEUMDUFUNUEUMUDLMNOUGULEAHPUEHDUFULUEHUDLMNQU
      HUFAGZEUKUFARUPUJUFUIAUDUESTUAUBUC $.
      $( [15-Jul-1994] $)
  $}

  ${
    $d x y $.
    dtrucor.1 $e |- x = y $.
    $( Corollary of ~ dtru .  This example illustrates the danger of blindly
       trusting the standard Deduction Theorem without accounting for free
       variables: the theorem form of this deduction is not valid, as shown by
       ~ dtrucor2 . $)
    dtrucor $p |- x =/= y $=
      ( cv wceq wne wal dtru pm2.21i mpg ) ADZBDZEZKLFZAMAGNABHICJ $.
      $( [27-Jun-2002] $)
  $}

  ${
    dtrucor2.1 $e |- ( x = y -> x =/= y ) $.
    $( The theorem form of the deduction ~ dtrucor leads to a contradiction, as
       mentioned in the "Wrong!" example at
       ~ http://us.metamath.org/mpegif/mmdeduction.html#bad . $)
    dtrucor2 $p |- ( ph /\ -. ph ) $=
      ( cv wceq wex wn wa a9e wi necon2bi pm2.01 ax-mp nex pm2.24ii ) BEZCEZFZB
      GAAHIBCJSBSSHZKTSQRDLSMNOP $.
      $( [20-Oct-2007] $)
  $}


  ${
    $d x y $.
    $( Demonstration of a theorem (scheme) that requires (meta)variables ` x `
       and ` y ` to be distinct, but no others.  It bundles the theorem schemes
       ` E. x ( x = y -> x e. x ) ` and ` E. x ( x = y -> y e. x ) ` .  Compare
       ~ dvdemo2 .  ("Bundles" is a term introduced by Raph Levien.) $)
    dvdemo1 $p |- E. x ( x = y -> z e. x ) $=
      ( cv wceq wn wex wcel wi wal dtru exnal mpbir pm2.21 eximi ax-mp ) ADZBDE
      ZFZAGZRCDQHZIZAGTRAJFABKRALMSUBARUANOP $.
      $( [1-Dec-2006] $)
  $}

  ${
    $d x z $.
    $( Demonstration of a theorem (scheme) that requires (meta)variables ` x `
       and ` z ` to be distinct, but no others.  It bundles the theorem schemes
       ` E. x ( x = x -> z e. x ) ` and ` E. x ( x = y -> y e. x ) ` .  Compare
       ~ dvdemo1 . $)
    dvdemo2 $p |- E. x ( x = y -> z e. x ) $=
      ( cv wcel wex wceq wi el ax-1 eximi ax-mp ) CDADZEZAFMBDGZNHZAFCAINPANOJK
      L $.
      $( [1-Dec-2006] $)
  $}

