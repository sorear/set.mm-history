$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Introduce the Axiom of Power Sets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Axiom of Power Sets.  An axiom of Zermelo-Fraenkel set theory.  It
       states that a set ` y ` exists that includes the power set of a given
       set ` x ` i.e. the collection of all subsets of ` x ` .  The variant
       ~ axpow2 states that the power set itself exists.  A version using
       class notation is ~ pwex . $)
    ax-pow $a |- E. y A. z ( A. w ( w e. z -> w e. x ) -> z e. y ) $.

    $( Axiom of Power Sets expressed with fewest number of different
       variables. $)
    axpow $p |- E. x A. y ( A. x ( x e. y -> x e. z ) -> y e. x ) $=
      ( vw cv wcel wi wal wex ax-pow wceq elequ1 imbi12d cbvalv imbi1i albii
      exbii mpbi ) DEZBEZFZSCEZFZGZDHZTAEZFZGZBHZAIUFTFZUFUBFZGZAHZUGGZBHZAICAB
      DJUIUOAUHUNBUEUMUGUDULDASUFKUAUJUCUKDABLDACLMNOPQR $.
      $( [14-Aug-2003] $)

    $( A variant of the Axiom of Power Sets ~ ax-pow .  For any set ` x ` ,
       there exists a set ` y ` whose members are exactly the subsets
       of ` x ` i.e. the power set of ` x ` .  Axiom Pow of [BellMachover]
       p. 466. $)
    axpow2 $p |- E. y A. z ( z e. y <-> A. w ( w e. z -> w e. x ) ) $=
      ( cv wcel wi wal ax-pow bm1.3ii ) DEZCEFKAEFGDHBCABCDIJ $.
      $( [6-Jun-2006] $) $( [4-Jun-2006] $)
  $}

  ${
    $d A x y z $.
    zfpowcl.1 $e |- A e. V $.
    $( Power set axiom expressed in class notation.  Axiom 4 of [TakeutiZaring]
       p. 17. $)
    pwex $p |- P~ A e. V $=
      ( vz vx vy cv cpw cvv wcel wceq pweq eleq1d wex wss wb wal wi axpow
      dfss2 imbi1i albii exbii mpbir bm1.3ii cab df-pw eqeq2i abeq2 bitr
      issetri vtocl ) CFZGZHIAGZHICABULAJUMUNHULAKLDUMDFZUMJZDMEFZUOIZUQULNZOEP
      ZDMUSDEUSURQZEPZDMUOUQIUOULIQDPZURQZEPZDMDECRVBVEDVAVDEUSVCURDUQULSTUAUBU
      CUDUPUTDUPUOUSEUEZJUTUMVFUOEULUFUGUSEUOUHUIUBUCUJUK $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.
    $( Power set axiom expressed in class notation, with the sethood
       requirement as an antecedent.  Axiom 4 of [TakeutiZaring] p. 17. $)
    pwexg $p |- ( A e. B -> P~ A e. V ) $=
      ( vx cv cpw cvv wcel wceq pweq eleq1d visset pwex vtoclg ) CDZEZFGAEZFGCA
      BNAHOPFNAIJNCKLM $.
      $( [30-Oct-2003] $) $( [30-Oct-2003] $)

    $( Existence of a class of subsets. $)
    abssexg $p |- ( A e. B -> { x | ( x (_ A /\ ph ) } e. V ) $=
      ( wcel cpw crab cvv cv wss wa cab pwexg rabexg syl df-rab visset elpw
      anbi1i abbii eqtr2 syl5eqel ) CDEZABCFZGZHBIZCJZAKZBLZUCUDHEUEHECDMABUDHN
      OUEUFUDEZAKZBLUIABUDPUKUHBUJUGAUFCBQRSTUAUB $.
      $( [15-Jul-2006] $) $( [15-Jul-2006] $)
  $}

  ${
    $d w x y z $.
    $( A version of ~ dtru ("two things exist") proved directly from the
       axioms (no set theory definitions). $)
    dtruALT $p |- -. A. x x = y $=
      ( vw vz cv wceq wn wex wal wcel wa eeanv wi ax-pow id ax-gen elequ2
      imbi1d albidv elequ1 imbi12d a4b1 mpi 19.22i ax-mp ax-nul ax-4 mpbir2an
      ax-14 com12 con3d imp 19.22i2 equequ2 negbid ax-8 a4w syl6bi a1d pm2.61i
      19.23aivv exnal mpbi ) AEZBEZFZGZAHZVFAIGCEZDEZFZGZDHCHZVHVDVIJZVDVJJZGZK
      ZDHCHZVMVRVNCHZVPDHZVNVPCDLVEVJJZVEVDJZMZBIZVJVIJZMZDIZCHVSACDBNWGVNCWGWB
      WBMZBIZVNWHBWBOPWFWIVNMDAVJVDFZWDWIWEVNWJWCWHBWJWAWBWBDABQRSDACTUAUBUCUDU
      EVPAIZDHVTDAUFWKVPDVPAUGUDUEUHVQVLCDVNVPVLVNVKVOVKVNVOCDAUIUJUKULUMUEVLVH
      CDVJVEFZVLVHMWLVLVIVEFZGZVHWLVKWMDBCUNUOWNVGACVDVIFVFWMACBUPUKUQURWLGZVHV
      LWOVGADVDVJFVFWLADBUPUKUQUSUTVAUEVFAVBVC $.
      $( [8-Nov-2006] $) $( [7-Nov-2006] $)
  $}

  ${
    $d x y $.
    $( This theorem shows that axiom ~ ax-16 is redundant in the presence of
       theorem ~ dtruALT , which states simply that at least two things exist.
       This justifies the remark at
       ~ http://us2.metamath.org:8888/mpegif/mmzfcnd.html#twoness (which links
       to this theorem). $)
    ax16b $p |- ( A. x x = y -> ( ph -> A. x ph ) ) $=
      ( cv wceq wal wi dtruALT pm2.21i ) BDCDEBFAABFGBCHI $.
      $( [8-Nov-2006] $) $( [7-Nov-2006] $)
  $}

  ${
    $d x A $.
    $( A singleton is a set.  Theorem 7.13 of [Quine] p. 51, but proved using
       only Extensionality, Power Set, and Separation.  Unlike the proof of
       ~ prex , Replacement is not needed. $)
    snex $p |- { A } e. V $=
      ( vx cvv wcel csn cv wceq sneq eleq1d cpw visset pwex snsspw ssexi
      vtoclg wn c0 snprc 0ex eleq1 mpbiri sylbi pm2.61i ) ACDZAEZCDZBFZEZCDUFBA
      CUGAGUHUECUGAHIUHUGJUGBKLUGMNOUDPUEQGZUFARUIUFQCDSUEQCTUAUBUC $.
      $( [7-Aug-1994] $)
  $}

  ${
    $d x y $.
    $( Every set is an element of some other set. $)
    el $p |- E. y x e. y $=
      ( cv csn wcel wex visset snid snex eleq2 cla4ev ax-mp ) ACZMDZEZMBCZEZBFM
      AGHQOBNMIPNMJKL $.
      $( [4-Jan-2002] $)
  $}

  ${
    snelpw.1 $e |- A e. V $.
    $( A singleton of a set belongs to the power class of a class containing
       the set. $)
    snelpw $p |- ( A e. B <-> { A } e. P~ B ) $=
      ( wcel csn wss cpw snss snex elpw bitr4 ) ABDAEZBFLBGDABCHLBAIJK $.
      $( [1-Apr-1998] $)
  $}

  ${
    $d x A $.
    $( Substitution expressed in terms of quantification over a singleton. $)
    sbcsng $p |- ( A e. B -> ( [ A / x ] ph <-> A. x e. { A } ph ) ) $=
      ( wcel wsbc cv wceq wi wal csn wral sbc6g df-ral elsn imbi1i albii bitr2
      syl6bb ) CDEABCFBGZCHZAIZBJZABCKZLZABCDMUETUDEZAIZBJUCABUDNUGUBBUFUAABCOP
      QRS $.
      $( [22-Dec-2005] $) $( [14-Dec-2005] $)
  $}

  ${
    $d x y z $.
    $( A theorem similar to extensionality, requiring the existence of a
       singleton.  Exercise 8 of [TakeutiZaring] p. 16. $)
    rext $p |- ( A. z ( x e. z -> y e. z ) -> x = y ) $=
      ( cv wcel wi wal csn wceq visset snid snex eleq2 imbi12d cla4v mpi elsn
      equcomi sylbi syl ) ADZCDZEZBDZUBEZFZCGZUDUAHZEZUAUDIZUGUAUHEZUIUAAJKUFUK
      UIFCUHUALUBUHIUCUKUEUIUBUHUAMUBUHUDMNOPUIUDUAIUJBUAQBARST $.
      $( [10-Aug-1993] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Classes are subclasses if and only if their power classes are
       subclasses.  Exercise 18 of [TakeutiZaring] p. 18. $)
    sspwb $p |- ( A (_ B <-> P~ A (_ P~ B ) $=
      ( vx wss cpw cv wcel sstr2 com12 visset elpw 3imtr4g ssrdv csn ssel snex
      snss bitr4 3imtr3g impbi ) ABDZAEZBEZDZUACUBUCUACFZADZUEBDZUEUBGUEUCGUFUA
      UGUEABHIUEACJZKUEBUHKLMUDCABUDUENZUBGZUIUCGZUEAGZUEBGZUBUCUIOUJUIADULUIAU
      EPZKUEAUHQRUKUIBDUMUIBUNKUEBUHQRSMT $.
      $( [13-Oct-1996] $)
  $}

  ${
    $d A x y $.
    $( A class equals the union of its power class.  Exercise 6(a) of
       [Enderton] p. 38. $)
    unipw $p |- U. P~ A = A $=
      ( vx vy cpw cuni cv wcel wa wex eluni wss wi visset elpw ssel sylbi
      impcom 19.23aiv ssriv csn snid snex wceq eleq2 eleq1 anbi12d cla4ev mpan
      snelpw 3imtr4 eqssi ) ADZEZABUMABFZUMGZUNCFZGZUPULGZHZCIZUNAGZCUNULJZUSVA
      CURUQVAURUPAKUQVALUPACMNUPAUNOPQRPSBAUMUNTZULGZUTVAUOUNVCGZVDUTUNBMZUAUSV
      EVDHCVCUNUBUPVCUCUQVEURVDUPVCUNUDUPVCULUEUFUGUHUNAVFUIVBUJSUK $.
      $( [14-Oct-1996] $)
  $}

  ${
    $d A x $.
    $( A class is a subclass of the power class of its union.  Exercise 6(b)
       of [Enderton] p. 38. $)
    pwuni $p |- A (_ P~ U. A $=
      ( vx cuni cpw cv wcel wss elssuni visset elpw sylibr ssriv ) BAACZDZBEZAF
      OMGONFOAHOMBIJKL $.
      $( [14-Oct-1996] $)
  $}

  $( Subclass relationship for power class and union. $)
  sspwuni $p |- ( A (_ P~ B <-> U. A (_ B ) $=
    ( cpw wss cuni uniss unipw syl6ss sspwb pwuni sstr mpan sylbi impbi ) ABCZD
    ZAEZBDZPQOEBAOFBGHRQCZODZPQBIASDTPAJASOKLMN $.
    $( [19-Jul-2006] $) $( [18-Jul-2006] $)

  $( Membership of a power class.  Exercise 10 of [Enderton] p. 26. $)
  pwel $p |- ( A e. B -> P~ A e. P~ P~ U. B ) $=
    ( wcel cpw cuni wss elssuni sspwb sylib cvv wb pwexg elpwg syl mpbird ) ABC
    ZADZBEZDZDCZQSFZPARFUAABGARHIPQJCTUAKABLQSJMNO $.
    $( [13-Jan-2007] $) $( [13-Jan-2007] $)

  ${
    $d x A $.  $d x B $.
    $( Two ways of expressing a collection of subclasses. $)
    pwssb $p |- ( A (_ P~ B <-> A. x e. A x (_ B ) $=
      ( cpw wss cuni cv wral sspwuni unissb bitr ) BCDEBFCEAGCEABHBCIABCJK $.
      $( [9-Jan-2007] $) $( [19-Jul-2006] $)
  $}

  $( Relationship for power class and union. $)
  elpwuni $p |- ( B e. A -> ( A (_ P~ B <-> U. A = B ) ) $=
    ( wcel cuni wss wceq cpw unissel expcom eqimss impbid1 sspwuni syl5bb ) BAC
    ZADZBEZOBFZABGENPQPNQABHIOBJKABLM $.
    $( [?] $) $( [18-Jul-2006] $)

  ${
    $d A x $.  $d B x $.
    $( An extensionality-like principle defining subclass in terms of
       subsets. $)
    ssextss $p |- ( A (_ B <-> A. x ( x (_ A -> x (_ B ) ) $=
      ( wss cpw cv wcel wi wal sspwb dfss2 visset elpw imbi12i albii 3bitr ) BC
      DBEZCEZDAFZQGZSRGZHZAISBDZSCDZHZAIBCJAQRKUBUEATUCUAUDSBALZMSCUFMNOP $.
      $( [1-Jul-2004] $) $( [30-Jun-2004] $)

    $( An extensionality-like principle that uses the subset instead of the
       membership relation:  two classes are equal iff they have the same
       subsets. $)
    ssext $p |- ( A = B <-> A. x ( x (_ A <-> x (_ B ) ) $=
      ( wss wa cv wi wal wceq wb ssextss anbi12i eqss albi 3bitr4 ) BCDZCBDZEAF
      ZBDZRCDZGAHZTSGAHZEBCISTJAHPUAQUBABCKACBKLBCMSTANO $.
      $( [1-Jul-2004] $) $( [30-Jun-2004] $)

    $( Negation of subclass relationship.  Compare ~ nss . $)
    nssss $p |- ( -. A (_ B <-> E. x ( x (_ A /\ -. x (_ B ) ) $=
      ( cv wss wi wn wex wal wa exnal annim exbii ssextss negbii 3bitr4r ) ADZB
      EZQCEZFZGZAHTAIZGRSGJZAHBCEZGTAKUCUAARSLMUDUBABCNOP $.
      $( [2-Jul-2004] $) $( [30-Jun-2004] $)
  $}

  $( Classes are equal if and only if their power classes are equal.  Exercise
     19 of [TakeutiZaring] p. 18. $)
  pweqb $p |- ( A = B <-> P~ A = P~ B ) $=
    ( wss wa cpw wceq sspwb anbi12i eqss 3bitr4 ) ABCZBACZDAEZBEZCZNMCZDABFMNFK
    OLPABGBAGHABIMNIJ $.
    $( [13-Oct-1996] $)

  ${
    $d x y $.  $d y ph $.
    $( "At most one" existence implies a class abstraction exists. $)
    moabex $p |- ( E* x ph -> { x | ph } e. V ) $=
      ( vy wmo cv wceq wi wal wex cab cvv wcel ax-17 mo2 wa hba1 wb pm4.71
      biimp a4s ancom syl6bb abbid csn df-sn snex eqeltrr pm3.26 ss2abi ssexi
      syl6eqel 19.23aiv sylbi ) ABDABECEZFZGZBHZCIABJZKLZABCACMNUQUSCUQURUOAOZB
      JZKUQAUTBUPBPUQAAUOOZUTUPAVBQZBUPVCAUORSTAUOUAUBUCVAUOBJZUNUDVDKBUNUEUNUF
      UGUTUOBUOAUHUIUJUKULUM $.
      $( [30-Dec-1996] $)
  $}

  $( The abstraction of a wff with existential uniqueness exists. $)
  euabex $p |- ( E! x ph -> { x | ph } e. V ) $=
    ( weu wmo cab cvv wcel eumo moabex syl ) ABCABDABEFGABHABIJ $.
    $( [25-Nov-1994] $)

  ${
    $d x y A $.
    $( A non-empty class (even if proper) has a non-empty subset. $)
    nnullss $p |- ( A =/= (/) -> E. x ( x (_ A /\ x =/= (/) ) ) $=
      ( vy c0 wne cv wcel wex wss wa ne0 csn visset snss snnz snex wceq sseq1
      neeq1 anbi12d cla4ev mpan2 sylbi 19.23aiv ) BDECFZBGZCHAFZBIZUGDEZJZAHZCB
      KUFUKCUFUELZBIZUKUEBCMZNUMULDEZUKUEUNOUJUMUOJAULUEPUGULQUHUMUIUOUGULBRUGU
      LDSTUAUBUCUDUC $.
      $( [?] $) $( [23-Aug-2003] $)
  $}

  ${
    $d x y z A $.  $d y z ph $.
    $( Restricted existence in a class (even if proper) implies restricted
       existence in a subset. $)
    exss $p |- ( E. x e. A ph -> E. y ( y (_ A /\ E. x e. y ph ) ) $=
      ( vz wrex cv wcel wa cab wex wss crab c0 wne df-rab neeq1i rabn0 ne0
      3bitr3 csn snex wceq sseq1 rexeq1 anbi12d cla4ev visset snss ssab2 sstr2
      mpi sylbi wsbc pm3.27 equsb1 elsn sbbii mpbir jctil df-clab sban bitr
      eleq2i 3bitr 3imtr4 ne0i syl sylib sylanc 19.23aiv ) ABDFZEGZBGZDHZAIZBJZ
      HZEKZCGZDLZABVTFZIZCKZABDMZNOVQNOVLVSWEVQNABDPQABDREVQSTVRWDEVMUAZDLZABWF
      FZWDVRWCWGWHICWFVMUBVTWFUCWAWGWBWHVTWFDUDABVTWFUEUFUGVRWFVQLZWGVMVQEUHUIW
      IVQDLWGABDUJWFVQDUKULUMVRABWFMZNOZWHVRVMWJHZWKVOBVMUNZABVMUNZIZVNWFHZBVMU
      NZWNIZVRWLWOWNWQWMWNUOWQVNVMUCZBVMUNBEUPWPWSBEBVMUQURUSUTVRVPBVMUNWOVPEBV
      AVOABEVBVCWLVMWPAIZBJZHWTBVMUNWRWJXAVMABWFPVDWTEBVAWPABEVBVEVFWJVMVGVHABW
      FRVIVJVKUM $.
      $( [23-Aug-2003] $)
  $}

  $( The power set of the empty set is a set. $)
  p0ex $p |- { (/) } e. V $=
    ( c0 snex ) AB $.
    $( [23-Dec-1993] $)

  $( The power set of the power set of the empty set is a set. $)
  pp0ex $p |- { (/) , { (/) } } e. V $=
    ( c0 csn cpw cpr cvv pwpw0 p0ex pwex eqeltrr ) ABZCAJDEFJGHI $.
    $( [5-Aug-1993] $)

  ${
    $d x y $.
    $( At least two sets exist (or in terms of first-order logic, the universe
       of discourse has two or more objects).  Note that we may not substitute
       the same variable for both ` x ` and ` y ` (as indicated by the distinct
       variable requirement), for otherwise we would contradict ~ stdpc6 .
       Assuming that ZF set theory is consistent, we cannot prove this theorem
       unless we specify that ` x ` and ` y ` be distinct.  Specifically,
       theorem ~ cla4ev requires that ` x ` must not occur in the subexpression
       ` -. y = { (/) } ` in step 4 nor in the subexpression ` -. y = (/) ` in
       step 9.  The proof verifier will require that ` x ` and ` y ` be in a
       distinct variable group to ensure this.  You can check this by deleting
       the $d statement in set.mm and rerunning the verifier.

       See ~ dtruALT for a version proved without using ~ ax-16 , ~ ax-ext ,
       or ~ ax-sep . $)
    dtru $p |- -. A. x x = y $=
      ( cv wceq wn wex wal c0 csn 0inp0 p0ex eqeq2 negbid cla4ev syl 0ex
      pm2.61i exnal eqcom albii negbii bitr mpbi ) BCZACZDZEZAFZUEUDDZAGZEZUDHD
      ZUHULUDHIZDZEZUHUDJUGUOAUMKUEUMDUFUNUEUMUDLMNOUGULEAHPUEHDUFULUEHUDLMNQUH
      UFAGZEUKUFARUPUJUFUIAUDUESTUAUBUC $.
      $( [15-Jul-1994] $)
  $}

  ${
    $d x y $.
    dtrucor.1 $e |- x = y $.
    $( Corollary of ~ dtru .  This example illustrates the danger of blindly
       trusting the standard Deduction Theorem without accounting for free
       variables: the theorem form of this deduction is not valid. $)
    dtrucor $p |- x =/= y $=
      ( cv wceq wne wal dtru pm2.21i mpg ) ADZBDZEZKLFZAMAGNABHICJ $.
      $( [27-Jun-2002] $)
  $}

  ${
    $d x y $.
    $( Demonstration of a theorem that requires ` x ` and ` y ` to be
       distinct, but no others.  Compare ~ dvdemo2 . $)
    dvdemo1 $p |- E. x ( x = y -> z e. x ) $=
      ( cv wceq wn wex wcel wi wal dtru exnal mpbir pm2.21 19.22i ax-mp ) ADZBD
      EZFZAGZRCDQHZIZAGTRAJFABKRALMSUBARUANOP $.
      $( [1-Dec-2006] $) $( [1-Dec-2006] $)
  $}

  ${
    $d x z $.
    $( Demonstration of a theorem that requires ` x ` and ` z ` to be
       distinct, but no others.  Compare ~ dvdemo1 . $)
    dvdemo2 $p |- E. x ( x = y -> z e. x ) $=
      ( cv wcel wex wceq wi el ax-1 19.22i ax-mp ) CDADZEZAFMBDGZNHZAFCAINPANOJ
      KL $.
      $( [1-Dec-2006] $) $( [1-Dec-2006] $)
  $}

