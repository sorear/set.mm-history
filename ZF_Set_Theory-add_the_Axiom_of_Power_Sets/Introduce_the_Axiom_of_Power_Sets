$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Introduce the Axiom of Power Sets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Axiom of Power Sets.  An axiom of Zermelo-Fraenkel set theory.  It
       states that a set ` y ` exists that includes the power set of a given
       set ` x ` i.e. contains every subset of ` x ` .  The variant ~ axpow2
       uses explicit subset notation.  A version using class notation is
       ~ pwex . $)
    ax-pow $a |- E. y A. z ( A. w ( w e. z -> w e. x ) -> z e. y ) $.

    $( Axiom of Power Sets expressed with fewest number of different
       variables. $)
    zfpow $p |- E. x A. y ( A. x ( x e. y -> x e. z ) -> y e. x ) $=
      ( vw cv wcel wi wal wex ax-pow wceq elequ1 imbi12d cbvalv imbi1i albii
      exbii mpbi ) DEZBEZFZSCEZFZGZDHZTAEZFZGZBHZAIUFTFZUFUBFZGZAHZUGGZBHZAICAB
      DJUIUOAUHUNBUEUMUGUDULDASUFKUAUJUCUKDABLDACLMNOPQR $.
      $( [14-Aug-2003] $)

    $( A variant of the Axiom of Power Sets ~ ax-pow using subset notation.
       Problem in {BellMachover] p. 466. $)
    axpow2 $p |- E. y A. z ( z C_ x -> z e. y ) $=
      ( vw cv wss wcel wi wal wex ax-pow dfss2 imbi1i albii exbii mpbir ) CEZAE
      ZFZQBEGZHZCIZBJDEZQGUCRGHDIZTHZCIZBJABCDKUBUFBUAUECSUDTDQRLMNOP $.
      $( [6-Jun-2006] $) $( [4-Jun-2006] $)

    $( A variant of the Axiom of Power Sets ~ ax-pow .  For any set ` x ` ,
       there exists a set ` y ` whose members are exactly the subsets of ` x `
       i.e. the power set of ` x ` .  Axiom Pow of [BellMachover] p. 466. $)
    axpow3 $p |- E. y A. z ( z C_ x <-> z e. y ) $=
      ( cv wss wcel wb wal wex axpow2 bm1.3ii bicom albii exbii mpbir ) CDZADEZ
      PBDFZGZCHZBIRQGZCHZBIQBCABCJKTUBBSUACQRLMNO $.
      $( [26-Jun-2009] $) $( [4-Jun-2006] $)
  $}

  ${
    $d w x y z $.
    $( Every set is an element of some other set.  See ~ elALT for a shorter
       proof using more axioms.  (The proof was shortened by Andrew Salmon,
       25-Jul-2011.) $)
    el $p |- E. y x e. y $=
      ( vz cv wcel wi wal wex zfpow wceq ax-13 imim2d ax-14 19.21aiv pm2.27
      syl syld a4imv eximi ax-mp ) BDZCDZEUAADZEFZBGZUBUAEZFZCGZBHUCUAEZBHBCAIU
      HUIBUGUICAUBUCJZUGUEUIFZUIUJUFUIUECABKLUJUEUKUIFUJUDBCABMNUEUIOPQRST $.
      $( [25-Jul-2011] $) $( [4-Jan-2002] $)

    $( Every set is an element of some other set.  See ~ elALT for a shorter
       proof using more axioms. $)
    elOLD $p |- E. y x e. y $=
      ( vw vz cv wcel wi wal wex ax-pow id ax-gen wceq elequ2 imbi1d albidv
      elequ1 imbi12d a4v mpi eximi ax-mp ) CEZDEZFZUCAEZFZGZCHZUDBEZFZGZDHZBIUF
      UJFZBIABDCJUMUNBUMUGUGGZCHZUNUOCUGKLULUPUNGDAUDUFMZUIUPUKUNUQUHUOCUQUEUGU
      GDACNOPDABQRSTUAUB $.
      $( [22-Jun-2009] $) $( [4-Jan-2002] $)
  $}

  ${
    $d A x y z $.
    zfpowcl.1 $e |- A e. _V $.
    $( Power set axiom expressed in class notation.  Axiom 4 of [TakeutiZaring]
       p. 17.  (The proof was shortened by Andrew Salmon, 25-Jul-2011.) $)
    pwex $p |- ~P A e. _V $=
      ( vz vy vx cv cpw cvv wcel wceq pweq eleq1d wss cab wex wb wal axpow2
      bm1.3ii abeq2 exbii mpbir isset df-pw eleq1i vtocl ) CFZGZHIZAGZHICABUGAJ
      UHUJHUGAKLUIDFZUGMZDNZHIZUNEFZUMJZEOZUQUKUOIULPDQZEOULEDCEDRSUPUREULDUOTU
      AUBEUMUCUBUHUMHDUGUDUEUBUF $.
      $( [25-Jul-2011] $) $( [5-Aug-1993] $)

    $( Power set axiom expressed in class notation.  Axiom 4 of [TakeutiZaring]
       p. 17. $)
    pwexOLD $p |- ~P A e. _V $=
      ( vz vx vy cv cpw cvv wcel wceq pweq eleq1d wex wss wb wal wi zfpow
      dfss2 imbi1i albii exbii mpbir bm1.3ii cab df-pw eqeq2i abeq2 bitri
      issetri vtocl ) CFZGZHIAGZHICABULAJUMUNHULAKLDUMDFZUMJZDMEFZUOIZUQULNZOEP
      ZDMUSDEUSURQZEPZDMUOUQIUOULIQDPZURQZEPZDMDECRVBVEDVAVDEUSVCURDUQULSTUAUBU
      CUDUPUTDUPUOUSEUEZJUTUMVFUOEULUFUGUSEUOUHUIUBUCUJUK $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.
    $( Power set axiom expressed in class notation, with the sethood
       requirement as an antecedent.  Axiom 4 of [TakeutiZaring] p. 17. $)
    pwexg $p |- ( A e. B -> ~P A e. _V ) $=
      ( vx cv cpw cvv wcel wceq pweq eleq1d visset pwex vtoclg ) CDZEZFGAEZFGCA
      BNAHOPFNAIJNCKLM $.
      $( [30-Oct-2003] $)

    $( Existence of a class of subsets.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    abssexg $p |- ( A e. B -> { x | ( x C_ A /\ ph ) } e. _V ) $=
      ( wcel cpw cvv cv wss wa cab pwexg df-pw eleq1i pm3.26 ss2abi ssexg mpan
      sylbi syl ) CDECFZGEZBHCIZAJZBKZGEZCDLUBUCBKZGEZUFUAUGGBCMNUEUGIUHUFUDUCB
      UCAOPUEUGGQRST $.
      $( [25-Jul-2011] $) $( [15-Jul-2006] $)

    $( Existence of a class of subsets. $)
    abssexgOLD $p |- ( A e. B -> { x | ( x C_ A /\ ph ) } e. _V ) $=
      ( wcel cpw crab cvv cv wss wa cab pwexg rabexg syl df-rab visset elpw
      anbi1i abbii eqtr2i syl5eqel ) CDEZABCFZGZHBIZCJZAKZBLZUCUDHEUEHECDMABUDH
      NOUEUFUDEZAKZBLUIABUDPUKUHBUJUGAUFCBQRSTUAUB $.
      $( [15-Jul-2006] $)
  $}

  ${
    $d x y A $.
    $( A singleton is a set.  Theorem 7.13 of [Quine] p. 51, but proved using
       only Extensionality, Power Set, and Separation.  Unlike the proof of
       ~ zfpair , Replacement is not needed.  (The proof was shortened by
       Andrew Salmon, 25-Jul-2011.) $)
    snex $p |- { A } e. _V $=
      ( cpw cvv wcel csn wss snsspw ssexg mpan wn pwexg con3i c0 wceq snprc
      biimpi 0ex syl6eqel syl pm2.61i ) ABZCDZAEZCDZUCUAFUBUDAGUCUACHIUBJACDZJZ
      UDUEUBACKLUFUCMCUFUCMNAOPQRST $.
      $( [25-Jul-2011] $) $( [7-Aug-1994] $)

    $( A singleton is a set.  Theorem 7.13 of [Quine] p. 51, but proved using
       only Extensionality, Power Set, and Separation.  Unlike the proof of
       ~ zfpair , Replacement is not needed. $)
    snexOLD $p |- { A } e. _V $=
      ( vx vy cvv wcel csn cv wceq sneq eleq1d cpw visset pwex snsspw ssexi
      vtoclg wn c0 snprc weu wal axnul zfnuleu eq0 eubii mpbir eueq eleq1
      mpbiri sylbi pm2.61i ) ADEZAFZDEZBGZFZDEUNBADUOAHUPUMDUOAIJUPUOKUOBLMUONO
      PULQUMRHZUNASUQUNRDEZURUORHZBTZUTCGUOEQCUAZBTBCBCUBUCUSVABCUOUDUEUFBRUGUF
      UMRDUHUIUJUK $.
      $( [7-Aug-1994] $)
  $}

  ${
    $d x y $.
    $( Every set is an element of some other set.  This has a shorter proof
       than ~ el but uses more axioms. $)
    elALT $p |- E. y x e. y $=
      ( cv csn wcel wex visset snid snex eleq2 cla4ev ax-mp ) ACZMDZEZMBCZEZBFM
      AGHQOBNMIPNMJKL $.
      $( [4-Jan-2002] $)
  $}

  $( The power set of the empty set (the ordinal 1) is a set. $)
  p0ex $p |- { (/) } e. _V $=
    ( c0 snex ) AB $.
    $( [23-Dec-1993] $)

  $( The power set of the power set of the empty set (the ordinal 2) is a
     set. $)
  pp0ex $p |- { (/) , { (/) } } e. _V $=
    ( c0 csn cpw cpr cvv pwpw0 p0ex pwex eqeltrri ) ABZCAJDEFJGHI $.
    $( [5-Aug-1993] $)

  $( The ordinal number 3 is a set, proved without the Axiom of Union
     ~ ax-un . $)
  ord3ex $p |- { (/) , { (/) } , { (/) , { (/) } } } e. _V $=
    ( c0 csn cpr ctp cun cvv df-tp cpw pwpr pp0ex pwex eqeltrri wss snsspr2
    unss2 ax-mp ssexi eqeltri ) AABZASCZDTTBZEZFASTGUBTSBZTCZEZTHUEFASITJKLUAUD
    MUBUEMUCTNUAUDTOPQR $.
    $( [4-May-2009] $) $( [2-May-2009] $)

  ${
    $d w x y z $.
    $( At least two sets exist (or in terms of first-order logic, the universe
       of discourse has two or more objects).  Note that we may not substitute
       the same variable for both ` x ` and ` y ` (as indicated by the distinct
       variable requirement), for otherwise we would contradict ~ stdpc6 .
       Assuming that ZF set theory is consistent, we cannot prove this theorem
       unless we specify that ` x ` and ` y ` be distinct.  Specifically,
       theorem ~ cla4ev requires that ` x ` must not occur in the subexpression
       ` -. y = { (/) } ` in step 4 nor in the subexpression ` -. y = (/) ` in
       step 9.  The proof verifier will require that ` x ` and ` y ` be in a
       distinct variable group to ensure this.  You can check this by deleting
       the $d statement in set.mm and rerunning the verifier, which will print
       a detailed explanation of the distinct variable violation.

       This theorem is proved directly from set theory axioms (no set theory
       definitions) and does not use ~ ax-16 , ~ ax-ext , or ~ ax-sep .  See
       ~ dtruALT for a shorter proof using these axioms. $)
    dtru $p |- -. A. x x = y $=
      ( vw vz cv wceq wn wex wal wcel wa eeanv wi ax-pow id ax-gen elequ2
      imbi1d albidv elequ1 imbi12d a4v mpi eximi ax-mp ax-nul ax-4 mpbir2an
      ax-14 com12 con3d imp 2eximi equequ2 notbid ax-8 a4imev syl6bi a1d
      pm2.61i 19.23aivv exnal mpbi ) AEZBEZFZGZAHZVFAIGCEZDEZFZGZDHCHZVHVDVIJZV
      DVJJZGZKZDHCHZVMVRVNCHZVPDHZVNVPCDLVEVJJZVEVDJZMZBIZVJVIJZMZDIZCHVSACDBNW
      GVNCWGWBWBMZBIZVNWHBWBOPWFWIVNMDAVJVDFZWDWIWEVNWJWCWHBWJWAWBWBDABQRSDACTU
      AUBUCUDUEVPAIZDHVTDAUFWKVPDVPAUGUDUEUHVQVLCDVNVPVLVNVKVOVKVNVOCDAUIUJUKUL
      UMUEVLVHCDVJVEFZVLVHMWLVLVIVEFZGZVHWLVKWMDBCUNUOWNVGACVDVIFVFWMACBUPUKUQU
      RWLGZVHVLWOVGADVDVJFVFWLADBUPUKUQUSUTVAUEVFAVBVC $.
      $( [8-Nov-2006] $) $( [7-Nov-2006] $)
  $}

  ${
    $d x y $.
    $( This theorem shows that axiom ~ ax-16 is redundant in the presence of
       theorem ~ dtru , which states simply that at least two things exist.
       This justifies the remark at
       ~ http://us.metamath.org/mpegif/mmzfcnd.html#twoness (which links to
       this theorem). $)
    ax16b $p |- ( A. x x = y -> ( ph -> A. x ph ) ) $=
      ( cv wceq wal wi dtru pm2.21i ) BDCDEBFAABFGBCHI $.
      $( [8-Nov-2006] $) $( [7-Nov-2006] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( Existential uniqueness implies there is a value for which the wff
       argument is false. $)
    eunex $p |- ( E! x ph -> E. x -. ph ) $=
      ( vy wex cv wceq wi wal wa wn weu dtru alim mtoi 19.23aiv adantl ax-17
      eu3 exnal 3imtr4i ) ABDZABECEFZGBHZCDZIABHZJZABKAJBDUDUFUAUCUFCUCUEUBBHBC
      LAUBBMNOPABCACQRABST $.
      $( [29-Oct-2010] $) $( [24-Oct-2010] $)
  $}

  ${
    snelpw.1 $e |- A e. _V $.
    $( A singleton of a set belongs to the power class of a class containing
       the set. $)
    snelpw $p |- ( A e. B <-> { A } e. ~P B ) $=
      ( wcel csn wss cpw snss snex elpw bitr4i ) ABDAEZBFLBGDABCHLBAIJK $.
      $( [1-Apr-1998] $)
  $}

  ${
    $d x y z $.
    $( A theorem similar to extensionality, requiring the existence of a
       singleton.  Exercise 8 of [TakeutiZaring] p. 16. $)
    rext $p |- ( A. z ( x e. z -> y e. z ) -> x = y ) $=
      ( cv wcel wi wal csn wceq visset snid snex eleq2 imbi12d cla4v mpi elsn
      equcomi sylbi syl ) ADZCDZEZBDZUBEZFZCGZUDUAHZEZUAUDIZUGUAUHEZUIUAAJKUFUK
      UIFCUHUALUBUHIUCUKUEUIUBUHUAMUBUHUDMNOPUIUDUAIUJBUAQBARST $.
      $( [10-Aug-1993] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Classes are subclasses if and only if their power classes are
       subclasses.  Exercise 18 of [TakeutiZaring] p. 18. $)
    sspwb $p |- ( A C_ B <-> ~P A C_ ~P B ) $=
      ( vx wss cpw cv wcel sstr2 com12 visset elpw 3imtr4g ssrdv csn ssel snex
      snss bitr4i 3imtr3g impbii ) ABDZAEZBEZDZUACUBUCUACFZADZUEBDZUEUBGUEUCGUF
      UAUGUEABHIUEACJZKUEBUHKLMUDCABUDUENZUBGZUIUCGZUEAGZUEBGZUBUCUIOUJUIADULUI
      AUEPZKUEAUHQRUKUIBDUMUIBUNKUEBUHQRSMT $.
      $( [13-Oct-1996] $)
  $}

  ${
    $d A x y $.
    $( A class equals the union of its power class.  Exercise 6(a) of
       [Enderton] p. 38.  (The proof was shortened by Alan Sare,
       28-Dec-2008.) $)
    unipw $p |- U. ~P A = A $=
      ( vx vy cpw cuni cv wcel wa wex eluni wss wi visset elpw ssel sylbi
      impcom 19.23aiv csn elunii snid snelpw biimpi sylancr impbii eqriv ) BADZ
      EZABFZUHGZUIAGZUJUICFZGZULUGGZHZCIUKCUIUGJUOUKCUNUMUKUNULAKUMUKLULACMNULA
      UIOPQRPUIUISZGUPUGGZUJUKUIUPUGTUIBMZUAUKUQUIAURUBUCUDUEUF $.
      $( [29-Dec-2008] $) $( [14-Oct-1996] $)
  $}

  ${
    $d A x $.
    $( A class is a subclass of the power class of its union.  Exercise 6(b) of
       [Enderton] p. 38. $)
    pwuni $p |- A C_ ~P U. A $=
      ( vx cuni cpw cv wcel wss elssuni visset elpw sylibr ssriv ) BAACZDZBEZAF
      OMGONFOAHOMBIJKL $.
      $( [14-Oct-1996] $)
  $}

  $( Membership of a power class.  Exercise 10 of [Enderton] p. 26. $)
  pwel $p |- ( A e. B -> ~P A e. ~P ~P U. B ) $=
    ( wcel cpw cuni wss elssuni sspwb sylib cvv wb pwexg elpwg syl mpbird ) ABC
    ZADZBEZDZDCZQSFZPARFUAABGARHIPQJCTUAKABLQSJMNO $.
    $( [13-Jan-2007] $)

  ${
    $d A x $.  $d B x $.
    $( An extensionality-like principle defining subclass in terms of
       subsets. $)
    ssextss $p |- ( A C_ B <-> A. x ( x C_ A -> x C_ B ) ) $=
      ( wss cpw cv wcel wi wal sspwb dfss2 visset elpw imbi12i albii 3bitri )
      BCDBEZCEZDAFZQGZSRGZHZAISBDZSCDZHZAIBCJAQRKUBUEATUCUAUDSBALZMSCUFMNOP $.
      $( [1-Jul-2004] $) $( [30-Jun-2004] $)

    $( An extensionality-like principle that uses the subset instead of the
       membership relation: two classes are equal iff they have the same
       subsets. $)
    ssext $p |- ( A = B <-> A. x ( x C_ A <-> x C_ B ) ) $=
      ( wss wa cv wi wal wceq wb ssextss anbi12i eqss albiim 3bitr4i ) BCDZCBDZ
      EAFZBDZRCDZGAHZTSGAHZEBCISTJAHPUAQUBABCKACBKLBCMSTANO $.
      $( [1-Jul-2004] $) $( [30-Jun-2004] $)

    $( Negation of subclass relationship.  Compare ~ nss .  (The proof was
       shortened by Andrew Salmon, 25-Jul-2011.) $)
    nssss $p |- ( -. A C_ B <-> E. x ( x C_ A /\ -. x C_ B ) ) $=
      ( wss wn cv wi wal wa wex ssextss notbii exanali bitr4i ) BCDZEAFZBDZPCDZ
      GAHZEQREIAJOSABCKLQRAMN $.
      $( [25-Jul-2011] $) $( [30-Jun-2004] $)

    $( Negation of subclass relationship.  Compare ~ nss . $)
    nssssOLD $p |- ( -. A C_ B <-> E. x ( x C_ A /\ -. x C_ B ) ) $=
      ( cv wss wi wn wex wal wa exnal annim exbii ssextss notbii 3bitr4ri ) ADZ
      BEZQCEZFZGZAHTAIZGRSGJZAHBCEZGTAKUCUAARSLMUDUBABCNOP $.
      $( [2-Jul-2004] $) $( [30-Jun-2004] $)
  $}

  $( Classes are equal if and only if their power classes are equal.  Exercise
     19 of [TakeutiZaring] p. 18. $)
  pweqb $p |- ( A = B <-> ~P A = ~P B ) $=
    ( wss wa cpw wceq sspwb anbi12i eqss 3bitr4i ) ABCZBACZDAEZBEZCZNMCZDABFMNF
    KOLPABGBAGHABIMNIJ $.
    $( [13-Oct-1996] $)

  ${
    $d x A $.
    intid.1 $e |- A e. _V $.
    $( The intersection of all sets to which a set belongs is the singleton of
       that set. $)
    intid $p |- |^| { x | A e. x } = { A } $=
      ( cv wcel cab cint csn cvv wss snex eleq2 snid intmin3 ax-mp wi elintab
      id mpgbir snssi eqssi ) BADZEZAFGZBHZUEIEUDUEJBKUCBUEEAUEIUBUEBLBCMNOBUDE
      ZUEUDJUFUCUCPAUCABCQUCRSBUDTOUA $.
      $( [5-Jun-2009] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( "At most one" existence implies a class abstraction exists. $)
    moabex $p |- ( E* x ph -> { x | ph } e. _V ) $=
      ( vy wmo cv wceq wi wal wex cab cvv wcel ax-17 mo2 wa hba1 wb pm4.71
      biimpi a4s ancom syl6bb abbid csn df-sn snex eqeltrri pm3.26 ss2abi
      ssexi syl6eqel 19.23aiv sylbi ) ABDABECEZFZGZBHZCIABJZKLZABCACMNUQUSCUQUR
      UOAOZBJZKUQAUTBUPBPUQAAUOOZUTUPAVBQZBUPVCAUORSTAUOUAUBUCVAUOBJZUNUDVDKBUN
      UEUNUFUGUTUOBUOAUHUIUJUKULUM $.
      $( [30-Dec-1996] $)
  $}

  $( The abstraction of a wff with existential uniqueness exists. $)
  euabex $p |- ( E! x ph -> { x | ph } e. _V ) $=
    ( weu wmo cab cvv wcel eumo moabex syl ) ABCABDABEFGABHABIJ $.
    $( [25-Nov-1994] $)

  ${
    $d x y A $.
    $( A non-empty class (even if proper) has a non-empty subset. $)
    nnullss $p |- ( A =/= (/) -> E. x ( x C_ A /\ x =/= (/) ) ) $=
      ( vy c0 wne cv wcel wex wss wa n0 csn visset snss snnz snex wceq sseq1
      neeq1 anbi12d cla4ev mpan2 sylbi 19.23aiv ) BDECFZBGZCHAFZBIZUGDEZJZAHZCB
      KUFUKCUFUELZBIZUKUEBCMZNUMULDEZUKUEUNOUJUMUOJAULUEPUGULQUHUMUIUOUGULBRUGU
      LDSTUAUBUCUDUC $.
      $( [27-May-2007] $) $( [23-Aug-2003] $)
  $}

  ${
    $d x y z A $.  $d y z ph $.
    $( Restricted existence in a class (even if proper) implies restricted
       existence in a subset. $)
    exss $p |- ( E. x e. A ph -> E. y ( y C_ A /\ E. x e. y ph ) ) $=
      ( vz wrex cv wcel wa cab wex wss crab c0 wne df-rab neeq1i rabn0 n0
      3bitr3i csn snex wceq sseq1 rexeq anbi12d cla4ev visset snss ssab2 sstr2
      mpi sylbi wsbc pm3.27 equsb1 elsn sbbii mpbir jctil df-clab sban bitri
      eleq2i 3bitri 3imtr4i ne0i syl sylib sylanc 19.23aiv ) ABDFZEGZBGZDHZAIZB
      JZHZEKZCGZDLZABVTFZIZCKZABDMZNOVQNOVLVSWEVQNABDPQABDREVQSTVRWDEVMUAZDLZAB
      WFFZWDVRWCWGWHICWFVMUBVTWFUCWAWGWBWHVTWFDUDABVTWFUEUFUGVRWFVQLZWGVMVQEUHU
      IWIVQDLWGABDUJWFVQDUKULUMVRABWFMZNOZWHVRVMWJHZWKVOBVMUNZABVMUNZIZVNWFHZBV
      MUNZWNIZVRWLWOWNWQWMWNUOWQVNVMUCZBVMUNBEUPWPWSBEBVMUQURUSUTVRVPBVMUNWOVPE
      BVAVOABEVBVCWLVMWPAIZBJZHWTBVMUNWRWJXAVMABWFPVDWTEBVAWPABEVBVEVFWJVMVGVHA
      BWFRVIVJVKUM $.
      $( [23-Aug-2003] $)
  $}

  ${
    $d x y $.
    $( A version of ~ dtru ("two things exist") with a shorter proof using more
       axioms. $)
    dtruALT $p |- -. A. x x = y $=
      ( cv wceq wn wex wal c0 csn 0inp0 p0ex eqeq2 notbid cla4ev syl 0ex
      pm2.61i exnal eqcom albii notbii bitri mpbi ) BCZACZDZEZAFZUEUDDZAGZEZUDH
      DZUHULUDHIZDZEZUHUDJUGUOAUMKUEUMDUFUNUEUMUDLMNOUGULEAHPUEHDUFULUEHUDLMNQU
      HUFAGZEUKUFARUPUJUFUIAUDUESTUAUBUC $.
      $( [15-Jul-1994] $)
  $}

  ${
    $d x y $.
    dtrucor.1 $e |- x = y $.
    $( Corollary of ~ dtru .  This example illustrates the danger of blindly
       trusting the standard Deduction Theorem without accounting for free
       variables: the theorem form of this deduction is not valid, as shown by
       ~ dtrucor2 . $)
    dtrucor $p |- x =/= y $=
      ( cv wceq wne wal dtru pm2.21i mpg ) ADZBDZEZKLFZAMAGNABHICJ $.
      $( [27-Jun-2002] $)
  $}

  ${
    dtrucor2.1 $e |- ( x = y -> x =/= y ) $.
    $( The theorem form of the deduction ~ dtrucor leads to a contradiction, as
       mentioned in the "Wrong!" example at
       ~ http://us.metamath.org/mpegif/mmdeduction.html#bad . $)
    dtrucor2 $p |- ( ph /\ -. ph ) $=
      ( cv wceq wex wn wa a9e wi necon2bi pm2.01 ax-mp nex pm2.24ii ) BEZCEZFZB
      GAAHIBCJSBSSHZKTSQRDLSMNOP $.
      $( [20-Oct-2007] $)
  $}


  ${
    $d x y $.
    $( Demonstration of a theorem that requires ` x ` and ` y ` to be distinct,
       but no others.  Compare ~ dvdemo2 . $)
    dvdemo1 $p |- E. x ( x = y -> z e. x ) $=
      ( cv wceq wn wex wcel wi wal dtru exnal mpbir pm2.21 eximi ax-mp ) ADZBDE
      ZFZAGZRCDQHZIZAGTRAJFABKRALMSUBARUANOP $.
      $( [1-Dec-2006] $)
  $}

  ${
    $d x z $.
    $( Demonstration of a theorem that requires ` x ` and ` z ` to be distinct,
       but no others.  Compare ~ dvdemo1 . $)
    dvdemo2 $p |- E. x ( x = y -> z e. x ) $=
      ( cv wcel wex wceq wi el ax-1 eximi ax-mp ) CDADZEZAFMBDGZNHZAFCAINPANOJK
      L $.
      $( [1-Dec-2006] $)
  $}

