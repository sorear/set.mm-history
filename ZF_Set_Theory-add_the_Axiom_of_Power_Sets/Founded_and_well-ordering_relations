$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Founded and well-ordering relations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new constant symbols. $)
  $c Fr $. $( Founded predicate symbol (read: 'founded'). $)
  $c We $. $( Well-ordering predicate symbol (read: 'well orders') $)

  $( Extend wff notation to include the founded predicate.  Read:  ' ` R ` is a
     founded relation on ` A ` .' $)
  wfr $a wff R Fr A $.

  $( Extend wff notation to include the well-ordering predicate.
     Read:  ' ` R ` well-orders ` A ` .' $)
  wwe $a wff R We A $.

  ${
    $d x y z R $.  $d x y z A $.
    $( Define the founded relation predicate.  For alternate definitions, see
       ~ dffr2 and ~ dffr3 . $)
    df-fr $a |- ( R Fr A <-> A. x ( ( x C_ A /\ x =/= (/) ) ->
                E. y e. x A. z e. x -. z R y ) ) $.
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    $( Property of founded relation (one direction of definition). $)
    fri $p |- ( ( ( B e. C /\ R Fr A ) /\ ( B C_ A /\ B =/= (/) ) ) ->
                E. x e. B A. y e. B -. y R x ) $=
      ( vz wcel wfr wss c0 wne wa cv wbr wn wral wrex wi wal wceq sseq1 neeq1
      anbi12d raleq rexeqbi1dv imbi12d cla4gv df-fr syl5ib imp31 ) DEHZCFIZDCJZ
      DKLZMZBNANFOPZBDQZADRZULGNZCJZUTKLZMZUQBUTQZAUTRZSZGTUPUSSZUMVFVGGDEUTDUA
      ZVCUPVEUSVHVAUNVBUOUTDCUBUTDKUCUDVDURAUTDUQBUTDUEUFUGUHGABCFUIUJUK $.
      $( [3-Apr-2007] $) $( [18-Mar-1997] $)
  $}

  ${
    $d x y w v R $.  $d y z w v R $.  $d x w v A $.
    $( Alternate definition of founded relation.  Similar to Definition 6.21 of
       [TakeutiZaring] p. 30.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    dffr2 $p |- ( R Fr A <-> A. x ( ( x C_ A /\ x =/= (/) ) ->
                E. y e. x ( x i^i { z | z R y } ) = (/) ) ) $=
      ( vv vw wfr cv wss c0 wne wa wbr wn wral wrex wi wal cab cin wceq df-fr
      wcel breq2 brab1 syl5bbr notbid ralbidv disj syl5bb cbvrexv imbi2i albii
      bitr4i ) DEHAIZDJUPKLMZFIZGIZENZOZFUPPZGUPQZRZASUQUPCIBIZENCTZUAKUBZBUPQZ
      RZASAGFDEUCVIVDAVHVCUQVGVBBGUPVEUSUBZURVFUDZOZFUPPVBVGVJVLVAFUPVJVKUTVJUR
      VEENUTVKVEUSUREUEFCVEEUFUGUHUIFUPVFUJUKULUMUNUO $.
      $( [27-Aug-2011] $) $( [17-Feb-2004] $)
  $}

  ${
    $d x y z R $.  $d z A $.  $d x z B $.
    frc.1 $e |- B e. _V $.
    $( Property of founded relation (one direction of definition using class
       variables). $)
    frc $p |- ( ( R Fr A /\ B C_ A /\ B =/= (/) ) ->
                E. x e. B ( B i^i { y | y R x } ) = (/) ) $=
      ( vz wfr wss c0 wne cv wbr cab cin wceq wrex wa wi wal dffr2 sseq1 neeq1
      anbi12d ineq1 eqeq1d rexeqbi1dv imbi12d cla4v sylbi 3impib ) CEHZDCIZDJKZ
      DBLALEMBNZOZJPZADQZULGLZCIZUSJKZRZUSUOOZJPZAUSQZSZGTUMUNRZURSZGABCEUAVFVH
      GDFUSDPZVBVGVEURVIUTUMVAUNUSDCUBUSDJUCUDVDUQAUSDVIVCUPJUSDUOUEUFUGUHUIUJU
      K $.
      $( [1-Feb-2007] $) $( [17-Feb-2004] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    $( Subset theorem for the founded predicate.  Exercise 1 of [TakeutiZaring]
       p. 31.  (The proof was shortened by Andrew Salmon, 25-Jul-2011.) $)
    frss $p |- ( A C_ B -> ( R Fr B -> R Fr A ) ) $=
      ( vx vz vy wss cv c0 wne wa wbr wn wral wrex wi wal wfr sstr2 com12
      anim1d imim1d alimdv df-fr 3imtr4g ) ABGZDHZBGZUGIJZKZEHFHCLMEUGNFUGOZPZD
      QUGAGZUIKZUKPZDQBCRACRUFULUODUFUNUJUKUFUMUHUIUMUFUHUGABSTUAUBUCDFEBCUDDFE
      ACUDUE $.
      $( [25-Jul-2011] $) $( [3-Apr-1994] $)
  $}

  ${
    $d x y z R $.  $d x y z S $.  $d x y z A $.
    $( Equality theorem for the founded predicate. $)
    freq1 $p |- ( R = S -> ( R Fr A <-> S Fr A ) ) $=
      ( vx vz vy wceq cv wss c0 wne wa wbr wn wral wrex wi wal wfr breq notbid
      rexralbidv imbi2d albidv df-fr 3bitr4g ) BCGZDHZAIUHJKLZEHZFHZBMZNZEUHOFU
      HPZQZDRUIUJUKCMZNZEUHOFUHPZQZDRABSACSUGUOUSDUGUNURUIUGUMUQFEUHUHUGULUPUJU
      KBCTUAUBUCUDDFEABUEDFEACUEUF $.
      $( [9-Mar-1997] $)
  $}

  $( Equality theorem for the founded predicate. $)
  freq2 $p |- ( A = B -> ( R Fr A <-> R Fr B ) ) $=
    ( wceq wfr wss wa wi wb frss anim12i eqss dfbi2 3imtr4i bicomd ) ABDZBCEZAC
    EZABFZBAFZGQRHZRQHZGPQRISUATUBABCJBACJKABLQRMNO $.
    $( [3-Apr-1994] $)

  ${
    $d y z R $.  $d x y z $.  $d y z A $.
    $( A founded relation is irreflexive.  Special case of Proposition 6.23 of
       [TakeutiZaring] p. 30. $)
    frirr $p |- ( ( R Fr A /\ x e. A ) -> -. x R x ) $=
      ( vz vy wfr cv wcel wbr wn csn cab cin c0 wceq wrex wne visset snnz wss
      wi snex frc 3exp snss syl5ib mpii elsn breq2 abbidv ineq2d eqeq1d wa
      breq1 elab biimpri snid jctil elin sylibr n0i syl con2i syl6bi sylbi
      r19.23aiv syl6 imp ) BCFZAGZBHZVJVJCIZJZVIVKVJKZDGZEGZCIZDLZMZNOZEVNPZVMV
      IVKVNNQZWAVJARZSVIVNBTZWBWAUAVKVIWDWBWAEDBVNCVJUBUCUDVJBWCUEUFUGVTVMEVNVP
      VNHVPVJOZVTVMUAEVJUHWEVTVNVOVJCIZDLZMZNOZVMWEVSWHNWEVRWGVNWEVQWFDVPVJVOCU
      IUJUKULVLWIVLVJWHHZWIJVLVJVNHZVJWGHZUMWJVLWLWKWLVLWFVLDVJWCVOVJVJCUNUOUPV
      JWCUQURVJVNWGUSUTWHVJVAVBVCVDVEVFVGVH $.
      $( [2-Jan-1994] $)
  $}

  ${
    $d z w R $.  $d z w x $.  $d z w y $.  $d z w A $.
    $( A founded relation has no 2-cycle loops.  Special case of Proposition
       6.23 of [TakeutiZaring] p. 30. $)
    fr2nr $p |- ( ( R Fr A /\ ( x e. A /\ y e. A ) ) ->
                -. ( x R y /\ y R x ) ) $=
      ( vz vw wfr cv wcel wbr wa wn cpr wss cab cin c0 wceq wrex wne visset
      prnz zfpair2 frc mp3an3 wo breq2 abbidv ineq2d neeq1d brab1 prid2 inelcm
      mpan sylbi syl5cbir prid1 jaao elpr syl5ib com12 necon2bd r19.23aiv syl
      prss sylan2b ancom2s ) CDGZBHZCIZAHZCIZVKVIDJZVIVKDJZKZLZVHVIVKMZCNZVPVJV
      LKVHVRKVQEHZFHZDJZEOZPZQRZFVQSZVPVHVRVQQTWEVIVKBUAZUBFECVQDBAUCUDUEWDVPFV
      QVTVQIZVOWCQVOWGWCQTZVOVTVIRZVTVKRZUFWHWGVMWIWHVNWJWIWHVQVSVIDJZEOZPZQTZV
      MWIWCWMQWIWBWLVQWIWAWKEVTVIVSDUGUHUIUJVMVKWLIZWNAEVIDUKVKVQIWOWNVIVKAUAZU
      LVKVQWLUMUNUOUPWJWHVQVSVKDJZEOZPZQTZVNWJWCWSQWJWBWRVQWJWAWQEVTVKVSDUGUHUI
      UJVNVIWRIZWTBEVKDUKVIVQIXAWTVIVKWFUQVIVQWRUMUNUOUPURVTVIVKFUAUSUTVAVBVCVD
      VIVKCWFWPVEVFVG $.
      $( [30-May-1994] $)
  $}

  ${
    $d x y z R $.
    $( Any relation is founded on the empty set. $)
    fr0 $p |- R Fr (/) $=
      ( vx vz vy c0 wfr cv wss wne wa wbr cab cin wceq wrex wi dffr2 wn ss0
      nne sylibr imnan mpbi pm2.21i mpgbir ) EAFBGZEHZUFEIZJZUFCGDGAKCLMENDUFOZ
      PBBDCEAQUIUJUGUHRZPUIRUGUFENUKUFSUFETUAUGUHUBUCUDUE $.
      $( [17-Sep-1993] $)
  $}

  ${
    $d x A $.
    $( Irreflexivitiy of the epsilon relation: a class founded by epsilon is
       not a member of itself. $)
    efrirr $p |- ( _E Fr A -> -. A e. A ) $=
      ( vx cep wfr wcel wn cv wi wceq freq2 eleq1 eleq2 bitrd notbid imbi12d
      wbr frirr ex epel notbii syl6ib pm2.01d vtoclg com12 ) ACDZAAEZUFUEUFFZBG
      ZCDZUHUHEZFZHUEUGHBAAUHAIZUIUEUKUGUHACJULUJUFULUJAUHEUFUHAUHKUHAALMNOUIUJ
      UIUJUHUHCPZFZUKUIUJUNBUHCQRUMUJBBSTUAUBUCUDUB $.
      $( [18-Apr-1994] $)
  $}

  $( A set founded by epsilon contains no 2-cycle loops. $)
  efrn2lp $p |- ( ( _E Fr A /\ ( x e. A /\ y e. A ) ) ->
                -. ( x e. y /\ y e. x ) ) $=
    ( cep wfr cv wcel wa wbr wn fr2nr epel anbi12i notbii sylib ) CDEAFZCGBFZCG
    HHPQDIZQPDIZHZJPQGZQPGZHZJABCDKTUCRUASUBABLBALMNO $.
    $( [19-Apr-1994] $)

  $( Similar to Theorem 7.2 of [TakeutiZaring] p. 35, of except that the Axiom
     of Regularity is not required due to antecedent ` _E Fr A ` . $)
  tz7.2 $p |- ( ( Tr A /\ _E Fr A /\ B e. A ) -> ( B C_ A /\ B =/= A ) ) $=
    ( wtr cep wfr wcel wss wne wa trss wceq wn eleq1 notbid efrirr syl5cbir
    necon2ad anim12ii 3impia ) ACZADEZBAFZBAGZBAHZITUBUCUAUDABJUAUBBABAKZUBLAAF
    ZLUAUEUBUFBAAMNAOPQRS $.
    $( [28-Apr-2007] $) $( [4-May-1994] $)

  ${
    $d x y z $.  $d x z A $.
    $( An alternate way of saying that the epsilon relation is founded.  (The
       proof was shortened by Andrew Salmon, 25-Jul-2011.) $)
    dfepfr $p |- ( _E Fr A <-> A. x ( ( x C_ A /\ x =/= (/) ) ->
                  E. y e. x ( x i^i y ) = (/) ) ) $=
      ( vz cep wfr cv wss c0 wne wa wbr cab cin wceq wrex wi wal dffr2 wcel wb
      abeq1 epel mpgbir ineq2i eqeq1i rexbii imbi2i albii bitri ) CEFAGZCHUKIJK
      ZUKDGZBGZELZDMZNZIOZBUKPZQZARULUKUNNZIOZBUKPZQZARABDCESUTVDAUSVCULURVBBUK
      UQVAIUPUNUKUPUNOUOUMUNTUADUODUNUBDBUCUDUEUFUGUHUIUJ $.
      $( [25-Jul-2011] $) $( [17-Feb-2004] $)
  $}

  ${
    $d x y B $.
    epfrc.1 $e |- B e. _V $.
    $( A subset of an epsilon-founded class has a minimal element.  (A distinct
       variable restriction was removed by David Abernethy, 22-Feb-2011.) $)
    epfrc $p |- ( ( _E Fr A /\ B C_ A /\ B =/= (/) ) ->
                  E. x e. B ( B i^i x ) = (/) ) $=
      ( vy cep wfr wss c0 wne w3a cv wbr cab cin wceq wrex frc wcel epel abbii
      abid2 eqtr2i ineq2i eqeq1i rexbii sylibr ) BFGCBHCIJKCELZALZFMZENZOZIPZAC
      QCUIOZIPZACQAEBCFDRUOUMACUNULIUIUKCUKUHUISZENUIUJUPEEATUAEUIUBUCUDUEUFUG
      $.
      $( [22-Feb-2011] $) $( [17-Feb-2004] $)
  $}

  $( Define the well-ordering predicate.  For an alternate definition, see
     ~ dfwe2 . $)
  df-we $a |- ( R We A <-> ( R Fr A /\ R Or A ) ) $.

  $( Subset theorem for the well-ordering predicate.  Exercise 4 of
     [TakeutiZaring] p. 31. $)
  wess $p |- ( A C_ B -> ( R We B -> R We A ) ) $=
    ( wss wfr wor wa wwe frss soss anim12d df-we 3imtr4g ) ABDZBCEZBCFZGACEZACF
    ZGBCHACHNOQPRABCIABCJKBCLACLM $.
    $( [19-Apr-1994] $)

  $( Equality theorem for the well-ordering predicate. $)
  weeq1 $p |- ( R = S -> ( R We A <-> S We A ) ) $=
    ( wceq wfr wor wa wwe freq1 soeq1 anbi12d df-we 3bitr4g ) BCDZABEZABFZGACEZ
    ACFZGABHACHNOQPRABCIABCJKABLACLM $.
    $( [9-Mar-1997] $)

  $( Equality theorem for the well-ordering predicate. $)
  weeq2 $p |- ( A = B -> ( R We A <-> R We B ) ) $=
    ( wceq wfr wor wa wwe freq2 soeq2 anbi12d df-we 3bitr4g ) ABDZACEZACFZGBCEZ
    BCFZGACHBCHNOQPRABCIABCJKACLBCLM $.
    $( [3-Apr-1994] $)

  $( A well-ordering is founded. $)
  wefr $p |- ( R We A -> R Fr A ) $=
    ( wwe wfr wor df-we simplbi ) ABCABDABEABFG $.
    $( [22-Apr-1994] $)

  $( A well-ordering is a strict ordering. $)
  weso $p |- ( R We A -> R Or A ) $=
    ( wwe wfr wor df-we simprbi ) ABCABDABEABFG $.
    $( [16-Mar-1997] $)

  $( The elements of an epsilon well-ordering are comparable. $)
  wecmpep $p |- ( ( _E We A /\ ( x e. A /\ y e. A ) ) ->
                 ( x e. y \/ x = y \/ y e. x ) ) $=
    ( cep wor cv wcel wa wceq w3o wwe wbr solin epel biid 3orbi123i sylib weso
    sylan ) CDEZAFZCGBFZCGHZUAUBGZUAUBIZUBUAGZJZCDKTUCHUAUBDLZUEUBUADLZJUGCUAUB
    DMUHUDUEUEUIUFABNUEOBANPQCDRS $.
    $( [17-May-1994] $)

  $( An epsilon well-ordering is a transitive relation. $)
  wetrep $p |- ( ( _E We A /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
             ( ( x e. y /\ y e. z ) -> x e. z ) ) $=
    ( cep wwe cv wcel w3a wa wbr wor wi sotr weso sylan epel anbi12i 3imtr3g )
    DEFZAGZDHBGZDHCGZDHIZJUAUBEKZUBUCEKZJZUAUCEKZUAUBHZUBUCHZJUAUCHDELUDUGUHMTD
    UAUBUCENDEOPUEUIUFUJABQBCQRACQS $.
    $( [22-Apr-1994] $)

  ${
    $d x y z R $.  $d y z A $.  $d x y z B $.
    $( A non-empty (possibly proper) subclass of a class well-ordered by ` _E `
       has a minimal element.  Special case of Proposition 6.26 of
       [TakeutiZaring] p. 31. $)
    wefrc $p |- ( ( _E We A /\ B C_ A /\ B =/= (/) ) ->
               E. x e. B ( B i^i x ) = (/) ) $=
      ( vy vz cep wwe wss c0 wne cv cin wceq wrex wi wess wcel wex wa ineq2
      eqeq1d rcla4ev ex adantl inss1 wfr visset inex2 epfrc wefr syl3an1 3exp
      mpi elin anbi1i anass bitri rexbii2 syl6ib adantr wral w3a wetrep exp3a
      df-3an 3anrot bitr3i sylan2b exp44 imp com34 imp3a syl5ib imp4a com23
      r19.21adv dfss3 syl6ibr dfss in32 eqeq2i biimpi biimprd syl6 reximdvai
      syld pm2.61dne 19.23adv n0 syl6com 3imp ) BFGZCBHZCIJZCAKZLZIMZACNZWMWLCF
      GZWNWROCBFPWSDKZCQZDRWRWNWSXAWRDWSXAWRWSXASZWRCWTLZIXAXCIMZWROWSXAXDWRWQX
      DAWTCWOWTMWPXCIWOWTCTUAUBUCUDXBXCIJZWOWTQZXCWOLZIMZSZACNZWRWSXEXJOXAWSXEX
      HAXCNZXJWSXCCHZXEXKOCWTUEWSXLXEXKCFUFXLXEXKWSACXCWTCDUGUHUICFUJUKULUMXHXI
      AXCCWOXCQZXHSWOCQZXFSZXHSXNXISXMXOXHWOCWTUNUOXNXFXHUPUQURUSUTXBXIWQACXBXN
      XFXHWQXBXNXFXHWQOZXBXOWPWTHZXPXBXOEKZWTQZEWPVAXQXBXOXSEWPXBXRWPQZXOXSXBXT
      XNXFXSXBXRCQZXRWOQZSXNXFXSOZOZXTXBYAYBYDXBYAXNYBYCWSXAYAXNYBYCOZOOWSXAYAX
      NYEWSYAXNXAVBZYEXAYASXNSZWSYFSYBXFXSEADCVCVDYGXAYAXNVBYFXAYAXNVEXAYAXNVFV
      GVHVIVJVKVLXRCWOUNVMVNVOVPEWPWTVQVRXQWQXHXQWPXGIXQWPXGMZXQWPWPWTLZMYHWPWT
      VSYIXGWPCWOWTVTWAUQWBUAWCWDVDVNWEWFWGUCWHDCWIVMWJWK $.
      $( [18-Mar-2007] $) $( [17-Feb-2004] $)
  $}

  $( Any relation is a well-ordering of the empty set. $)
  we0 $p |- R We (/) $=
    ( c0 wwe wfr wor df-we fr0 so0 mpbir2an ) BACBADBAEBAFAGAHI $.
    $( [16-Mar-1997] $)

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    wereu.1 $e |- B e. _V $.
    $( A subset of a well-ordered set has a unique minimal element. $)
    wereu $p |- ( ( R We A /\ B C_ A /\ B =/= (/) ) ->
                E! x e. B A. y e. B -. y R x ) $=
      ( vz cv wbr wn wral wreu wrex wa wceq wi wwe wss c0 wne w3a breq2 notbid
      ralbidv reu4 wfr cvv wcel fri mpanl1 3impb wefr syl3an1 wo w3o wor solin
      weso sylan df-3or or23 df-or 3bitri sylib ioran syl5ibr wess impcom
      breq1 rcla4v im2anan9 ancomsd imp syl5 exp4b pm2.43d 3adant3 r19.21aivv
      sylanbrc ) BHZAHZEIZJZBDKZADLWDADMZWDVTGHZEIZJZBDKZNZWAWFOZPZGDKADKCEQZDC
      RZDSTZUAZWDWIAGDWKWCWHBDWKWBWGWAWFVTEUBUCUDUECEUFZWNWOWEWMWQWNWOWEDUGUHWQ
      WNWONWEFABCDUGEUIUJUKCEULUMWPWLAGDDWMWNWADUHZWFDUHZNZWLPWOWMWNNZWTWLXAWTW
      TWJWKXAWTNWAWFEIZJZWFWAEIZJZNZWKWTWJNDEQZWTXFWKPXAXGWTNZXBXDUNZJZWKXFXHXB
      WKXDUOZXJWKPZDEUPWTXKXGDWAWFEUQDEURUSXKXBWKUNXDUNXIWKUNXLXBWKXDUTXBWKXDVA
      XIWKVBVCVDXBXDVEVFWNWMXGDCEVGVHUSWTWJXFWTWIWDXFWRWIXCWSWDXEWHXCBWADVTWAOW
      GXBVTWAWFEVIUCVJWCXEBWFDVTWFOWBXDVTWFWAEVIUCVJVKVLVMVNVOVPVQVRVS $.
      $( [5-Apr-2007] $) $( [18-Mar-1997] $)

    $( The unique minimal element of a subset of a well-ordered set. $)
    wereucl $p |- ( ( R We A /\ B C_ A /\ B =/= (/) ) ->
                U. { x e. B | A. y e. B -. y R x } e. B ) $=
      ( wwe wss c0 wne w3a cv wbr wn wral wreu crab cuni wcel wereu reucl syl
      ) CEGDCHDIJKBLALEMNBDOZADPUCADQRDSABCDEFTUCADUAUB $.
      $( [23-Sep-2007] $)
  $}


