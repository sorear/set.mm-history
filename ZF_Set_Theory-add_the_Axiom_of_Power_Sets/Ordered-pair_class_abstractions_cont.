$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Ordered-pair class abstractions (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v $.  $d ph z w v $.
    $( The law of concretion.  Special case of Theorem 9.5 of [Quine] p. 61. $)
    opabid $p |- ( <. x , y >. e. { <. x , y >. | ph } <-> ph ) $=
      ( vz vw vv cv cop wceq wa wex cab wcel copab clelab df-opab eleq2i wsbc
      19.41v anass exbii eqcom opex eqvinc visset opth 3bitr3 anbi1i 3bitr3r
      2exbii sbel2x excom exdistr2 3bitr4 ax-17 hbs1 hban hbex opeq1 eqeq2d
      sbequ12 anbi12d exbidv cbvex opeq2 bitr anbi2i bitr4 ) BGZCGZHZDGZVKIZAJZ
      CKZBKZDLZMVMVPJZDKZVKABCNZMAVPDVKOVTVQVKABCDPQAVMVLEGZFGZHZIZABWARZCWBRZJ
      ZFKZEKZJZDKZVSWAVIIWBVJIJZWFJZFKEKVMWGJZDKZFKZEKZAWKWMWOEFVMWDJZWFJZDKWRD
      KZWFJWOWMWRWFDSWSWNDVMWDWFTUAWTWLWFVKWCIWCVKIWTWLVKWCUBDVKWCVIVJUCUDWAWBV
      IVJEUEFUECUEUFUGUHUIUJAEFBCUKWNFKZEKDKXADKZEKWKWQXADEULVMWGDEFUMXBWPEWNDF
      ULUAUGUNVRWJDVPWIVMVPVLWAVJHZIZWEJZCKZEKWIVOXFBEVOEUOXEBCXDWEBXDBUOABEUPU
      QURVIWAIZVNXECXGVMXDAWEXGVKXCVLVIWAVJUSUTABEVAVBVCVDXFWHEXEWGCFXEFUOWDWFC
      WDCUOWECFUPUQVJWBIZXDWDWEWFXHXCWCVLVJWBWAVEUTWECFVAVBVDUAVFVGUAVHUN $.
      $( [14-Apr-1995] $)
  $}

  ${
    $d x z A $.  $d y z A $.  $d z ph $.
    $( Membership in a class abstraction of pairs. $)
    elopab $p |- ( A e. { <. x , y >. | ph } <->
                 E. x E. y ( A = <. x , y >. /\ ph ) ) $=
      ( vz copab wcel cvv cv cop wceq wa wex elisset opex eleq1 mpbiri adantr
      19.23aivv eqeq1 anbi1d 2exbidv df-opab abeq2i vtoclbg pm5.21nii ) DABCFZG
      ZDHGZDBIZCIZJZKZALZCMBMZDUGNUNUIBCUMUIAUMUIULHGUJUKODULHPQRSEIZUGGUPULKZA
      LZCMBMZUHUOEDHUPDUGPUPDKZURUNBCUTUQUMAUPDULTUAUBUSEUGABCEUCUDUEUF $.
      $( [24-Mar-1998] $)
  $}

  ${
    $d x z w $. $d y z w $.
    hbopab.1 $e |- ( ph -> A. z ph ) $.
    $( Bound-variable hypothesis builder for class abstraction. $)
    hbopab $p |- ( w e. { <. x , y >. | ph } ->
                 A. z w e. { <. x , y >. | ph } ) $=
      ( cv cop wceq wa wex wal copab wcel ax-17 hban hbex elopab albii 3imtr4
      ) EGZBGCGHIZAJZCKZBKZUEDLUAABCMNZUFDLUDDBUCDCUBADUBDOFPQQABCUARZUFUEDUGST
      $.
      $( [1-Sep-1999] $)
  $}

  ${
    $d x w $.  $d y w $.  $d x z $.  $d w ph $.
    $( The first abstraction variable in an ordered-pair class abstraction
       (class builder) is effectively not free. $)
    hbopab1 $p |- ( z e. { <. x , y >. | ph } ->
                 A. x z e. { <. x , y >. | ph } ) $=
      ( vw cv cop wceq wa wex cab wcel wal copab hbe1 hbab df-opab eleq2i
      albii 3imtr4 ) DFZEFBFCFGHAICJZBJZEKZLZUEBMUAABCNZLZUGBMUCBEDUBBOPUFUDUAA
      BCEQRZUGUEBUHST $.
      $( [16-May-1995] $)
  $}

  ${
    $d x w $.  $d y w $.  $d y z $.  $d w ph $.
    $( The second abstraction variable in an ordered-pair class abstraction
       (class builder) is effectively not free. $)
    hbopab2 $p |- ( z e. { <. x , y >. | ph } ->
                 A. y z e. { <. x , y >. | ph } ) $=
      ( vw cv cop wceq wa wex cab wcel wal copab hbe1 hbex hbab df-opab eleq2i
      albii 3imtr4 ) DFZEFBFCFGHAIZCJZBJZEKZLZUGCMUBABCNZLZUICMUECEDUDCBUCCOPQU
      HUFUBABCERSZUIUGCUJTUA $.
      $( [16-May-1995] $)
  $}


  ${
    $d x y z v $.  $d x y w v $.  $d v ph $.
    $( The law of concretion in terms of substitutions. $)
    opabsb $p |- ( <. z , w >. e. { <. x , y >. | ph }
                 <-> [ w / y ] [ z / x ] ph ) $=
      ( vv cv wceq wex cop copab wcel wsbc wb a9e ax-17 hbopab2 hbel hbs1 hbbi
      wi hbopab1 hbsb hbim wa opeq12 eleq1d opabid syl5bbr sbequ12 sylan9bb
      bitr3d ex 19.23ai ax-mp ) CGZEGZHZCIDGZUQJZABCKZLZABUSMZCUQMZNZCEOURVECVB
      VDCCFFUTVAFGUTLZCPABCFQRVCCESTBGZUSHZBIURVEUAZBDOVHVIBURVEBURBPVBVDBBFFUT
      VAVFBPABCFUBRVCCEBABDSUCTUDVHURVEVHURUEZAVBVDVJVGUPJZVALVBAVJVKUTVAVGUPUS
      UQUFUGABCUHUIVHAVCURVDABDUJVCCEUJUKULUMUNUOUNUO $.
      $( [30-Sep-2002] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y ch $.  $d z ph $.
    opelopabg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopabg.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61. $)
    opelopabg $p |- ( ( A e. C /\ B e. D ) ->
                    ( <. A , B >. e. { <. x , y >. | ph } <-> ch ) ) $=
      ( wcel wa cop cv wceq wex copab sylan9bb copsex2g elopab syl5bb ) FHLGILM
      FGNZDOZEOZNPAMEQDQCUCADERLACDEFGHIUDFPABUEGPCJKSTADEUCUAUB $.
      $( [15-Mar-2007] $) $( [28-May-1995] $)

    ${
      brabg.5 $e |- R = { <. x , y >. | ph } $.
      $( The law of concretion for a binary relation. $)
      brabg $p |- ( ( A e. C /\ B e. D ) -> ( A R B <-> ch ) ) $=
        ( wcel wa cop copab wbr opelopabg df-br eleq2i bitr syl5bb ) FHNGINOFGP
        ZADEQZNZCFGJRZABCDEFGHIKLSUGUDJNUFFGJTJUEUDMUAUBUC $.
        $( [16-Aug-1999] $)
    $}
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ch $.
    opelopab.1 $e |- A e. V $.
    opelopab.2 $e |- B e. V $.
    opelopab.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopab.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61. $)
    opelopab $p |- ( <. A , B >. e. { <. x , y >. | ph } <-> ch ) $=
      ( cvv wcel cop copab wb opelopabg mp2an ) FLMGLMFGNADEOMCPHIABCDEFGLLJKQR
      $.
      $( [16-May-1995] $)

    ${
      brab.5 $e |- R = { <. x , y >. | ph } $.
      $( The law of concretion for a binary relation. $)
      brab $p |- ( A R B <-> ch ) $=
        ( cvv wcel wbr wb brabg mp2an ) FNOGNOFGHPCQIJABCDEFGNNHKLMRS $.
        $( [16-Aug-1999] $)
    $}
  $}

  ${
    $d ph z $.  $d ps z $.  $d x y z $.
    $( Equivalence of ordered pair abstraction subclass and implication. $)
    ssopab2 $p |- ( { <. x , y >. | ph } (_ { <. x , y >. | ps } <->
               A. x A. y ( ph -> ps ) ) $=
      ( vz copab wss wi wal hbopab1 hbss hbopab2 cv cop wceq wex opex isseti
      wa cab copsexg imbi12d ss2ab ax-4 sylbi syl5bir df-opab sseq12i syl5ib
      19.23aiv ax-mp 19.21ai hba1 anim2d 19.22d a4s 19.21aiv sylibr 3sstr4g
      impbi ) ACDFZBCDFZGZABHZDIZCIZVCVECCEVAVBACDEJBCDEJKVCVDDDEVAVBACDELBCDEL
      KEMZCMZDMZNZOZEPVCVDHZEVJVHVIQRVKVLEVKVKASZDPZCPZETZVKBSZDPZCPZETZGZVDVCV
      KVDVOVSHZWAVKAVOBVSACDVGUABCDVGUAUBWAWBEIZWBVOVSEUCZWBEUDUEUFVAVPVBVTACDE
      UGZBCDEUGZUHUIUJUKULULVFVPVTVAVBVFWCWAVFWBEVFVNVRCVECUMVEVNVRHCVEVMVQDVDD
      UMVEABVKVDDUDUNUOUPUOUQWDURWEWFUSUT $.
      $( [27-Dec-1996] $)
  $}

  ${
    $d ph z $.  $d ps z $.  $d x y z $.
    ssopab2i.1 $e |- ( ph -> ps ) $.
    $( Inference of ordered pair abstraction subclass from implication. $)
    ssopab2i $p |- { <. x , y >. | ph } (_ { <. x , y >. | ps } $=
      ( copab wss wi wal ssopab2 ax-gen mpgbir ) ACDFBCDFGABHZDICABCDJMDEKL $.
      $( [5-Apr-1995] $)
  $}

