$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Ordered-pair class abstractions (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x z w v $.  $d y z w v $.  $d ph z w v $.
    $( The law of concretion.  Special case of Theorem 9.5 of [Quine] p. 61.
       (The proof was shortened by Andrew Salmon, 25-Jul-2011.) $)
    opabid $p |- ( <. x , y >. e. { <. x , y >. | ph } <-> ph ) $=
      ( vz cv cop wceq wa wex copab opex copsexg bicomd df-opab elab2 ) DEZBEZC
      EZFZGZAHCIBIZADSABCJQRKTAUAABCPLMABCDNO $.
      $( [25-Jul-2011] $) $( [14-Apr-1995] $)

    $d x y $.
    $( The law of concretion.  Special case of Theorem 9.5 of [Quine] p. 61. $)
    opabidOLD $p |- ( <. x , y >. e. { <. x , y >. | ph } <-> ph ) $=
      ( vz vw vv cv cop wceq wa wex cab wcel copab clelab df-opab eleq2i wsbc
      19.41v anass exbii eqcom opex eqvinc visset opth 3bitr3i anbi1i 3bitr3ri
      2exbii sbel2x excom exdistr2 3bitr4i ax-17 hbs1 hban hbex opeq1 eqeq2d
      sbequ12 anbi12d exbidv cbvex opeq2 bitri anbi2i bitr4i ) BGZCGZHZDGZVKIZA
      JZCKZBKZDLZMVMVPJZDKZVKABCNZMAVPDVKOVTVQVKABCDPQAVMVLEGZFGZHZIZABWARZCWBR
      ZJZFKZEKZJZDKZVSWAVIIWBVJIJZWFJZFKEKVMWGJZDKZFKZEKZAWKWMWOEFVMWDJZWFJZDKW
      RDKZWFJWOWMWRWFDSWSWNDVMWDWFTUAWTWLWFVKWCIWCVKIWTWLVKWCUBDVKWCVIVJUCUDWAW
      BVIVJEUEFUECUEUFUGUHUIUJAEFBCUKWNFKZEKDKXADKZEKWKWQXADEULVMWGDEFUMXBWPEWN
      DFULUAUGUNVRWJDVPWIVMVPVLWAVJHZIZWEJZCKZEKWIVOXFBEVOEUOXEBCXDWEBXDBUOABEU
      PUQURVIWAIZVNXECXGVMXDAWEXGVKXCVLVIWAVJUSUTABEVAVBVCVDXFWHEXEWGCFXEFUOWDW
      FCWDCUOWECFUPUQVJWBIZXDWDWEWFXHXCWCVLVJWBWAVEUTWECFVAVBVDUAVFVGUAVHUN $.
      $( [14-Apr-1995] $)
  $}

  ${
    $d x z A $.  $d y z A $.  $d z ph $.
    $( Membership in a class abstraction of pairs. $)
    elopab $p |- ( A e. { <. x , y >. | ph } <->
                 E. x E. y ( A = <. x , y >. /\ ph ) ) $=
      ( vz copab wcel cvv cv cop wceq wa wex elisset opex eleq1 mpbiri adantr
      19.23aivv eqeq1 anbi1d 2exbidv df-opab abeq2i vtoclbg pm5.21nii ) DABCFZG
      ZDHGZDBIZCIZJZKZALZCMBMZDUGNUNUIBCUMUIAUMUIULHGUJUKODULHPQRSEIZUGGUPULKZA
      LZCMBMZUHUOEDHUPDUGPUPDKZURUNBCUTUQUMAUPDULTUAUBUSEUGABCEUCUDUEUF $.
      $( [24-Mar-1998] $)
  $}

  ${
    $d x z v $.  $d y z v $.  $d w z $.  $d ph v $.
    hbopab.1 $e |- ( ph -> A. z ph ) $.
    $( Bound-variable hypothesis builder for class abstraction.  (Unnecessary
       distinct variable restrictions were removed by Andrew Salmon,
       11-Jul-2011.) $)
    hbopab $p |- ( w e. { <. x , y >. | ph } ->
                 A. z w e. { <. x , y >. | ph } ) $=
      ( vv copab cv cop wceq wa wex cab df-opab ax-17 hban hbex hbab hbxfr ) DE
      ABCHGIBICIJKZALZCMZBMZGNABCGOUDDGEUCDBUBDCUAADUADPFQRRST $.
      $( [25-Jul-2011] $) $( [1-Sep-1999] $)

    $d x z w $.  $d y z w $.
    $( Bound-variable hypothesis builder for class abstraction. $)
    hbopabOLD $p |- ( w e. { <. x , y >. | ph } ->
                 A. z w e. { <. x , y >. | ph } ) $=
      ( cv cop wceq wa wex wal copab wcel ax-17 hban hbex elopab albii 3imtr4i
      ) EGZBGCGHIZAJZCKZBKZUEDLUAABCMNZUFDLUDDBUCDCUBADUBDOFPQQABCUARZUFUEDUGST
      $.
      $( [1-Sep-1999] $)
  $}

  ${
    $d x w $.  $d y w $.  $d x z $.  $d w ph $.
    $( The first abstraction variable in an ordered-pair class abstraction
       (class builder) is effectively not free. $)
    hbopab1 $p |- ( z e. { <. x , y >. | ph } ->
                 A. x z e. { <. x , y >. | ph } ) $=
      ( vw copab cv cop wceq wa wex cab df-opab hbe1 hbab hbxfr ) BDABCFEGBGCGH
      IAJCKZBKZELABCEMRBEDQBNOP $.
      $( [16-May-1995] $)
  $}

  ${
    $d x w $.  $d y w $.  $d y z $.  $d w ph $.
    $( The second abstraction variable in an ordered-pair class abstraction
       (class builder) is effectively not free. $)
    hbopab2 $p |- ( z e. { <. x , y >. | ph } ->
                 A. y z e. { <. x , y >. | ph } ) $=
      ( vw copab cv cop wceq wa wex cab df-opab hbe1 hbex hbab hbxfr ) CDABCFEG
      BGCGHIAJZCKZBKZELABCEMTCEDSCBRCNOPQ $.
      $( [16-May-1995] $)
  $}


  ${
    $d x y z v $.  $d x y w v $.  $d v ph $.
    $( The law of concretion in terms of substitutions.  (The proof was
       shortened by Andrew Salmon, 25-Jul-2011.) $)
    opelopabsb $p |- ( <. z , w >. e. { <. x , y >. | ph }
                 <-> [ w / y ] [ z / x ] ph ) $=
      ( cv cop wceq wa wex copab wcel wsbc excom visset opth ancom equcom
      anbi12i 3bitri anbi1i 2exbii bitri elopab 2sb5 3bitr4i ) DFZEFZGZBFZCFZGH
      ZAIZCJBJZUKUHHZUJUGHZIZAIZBJCJZUIABCKLABUGMCUHMUNUMBJCJUSUMBCNUMURCBULUQA
      ULUGUJHZUHUKHZIVAUTIUQUGUHUJUKDOEOCOPUTVAQVAUOUTUPECRDBRSTUAUBUCABCUIUDAC
      BEDUEUF $.
      $( [25-Jul-2011] $) $( [30-Sep-2002] $)

    $( The law of concretion in terms of substitutions. $)
    opelopabsbOLD $p |- ( <. z , w >. e. { <. x , y >. | ph }
                 <-> [ w / y ] [ z / x ] ph ) $=
      ( vv cv wceq wex cop copab wcel wsbc wb a9e ax-17 hbopab2 hbel hbs1 hbbi
      wi hbopab1 hbsb hbim wa opeq12 eleq1d opabid syl5bbr sbequ12 sylan9bb
      bitr3d ex 19.23ai ax-mp ) CGZEGZHZCIDGZUQJZABCKZLZABUSMZCUQMZNZCEOURVECVB
      VDCCFFUTVAFGUTLZCPABCFQRVCCESTBGZUSHZBIURVEUAZBDOVHVIBURVEBURBPVBVDBBFFUT
      VAVFBPABCFUBRVCCEBABDSUCTUDVHURVEVHURUEZAVBVDVJVGUPJZVALVBAVJVKUTVAVGUPUS
      UQUFUGABCUHUIVHAVCURVDABDUJVCCEUJUKULUMUNUOUNUO $.
      $( [30-Sep-2002] $)

    ${
      brabsb.1 $e |- R = { <. x , y >. | ph } $.
      $( The law of concretion in terms of substitutions. $)
      brabsb $p |- ( z R w <-> [ w / y ] [ z / x ] ph ) $=
        ( cv wbr copab cop wcel wsbc breqi df-br opelopabsb 3bitri ) DHZEHZFIRS
        ABCJZIRSKTLABRMCSMRSFTGNRSTOABCDEPQ $.
        $( [17-Mar-2008] $)
    $}
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y ch $.  $d z ph $.
    opelopabg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopabg.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61. $)
    opelopabg $p |- ( ( A e. C /\ B e. D ) ->
                    ( <. A , B >. e. { <. x , y >. | ph } <-> ch ) ) $=
      ( wcel wa cop cv wceq wex copab sylan9bb copsex2g elopab syl5bb ) FHLGILM
      FGNZDOZEOZNPAMEQDQCUCADERLACDEFGHIUDFPABUEGPCJKSTADEUCUAUB $.
      $( [15-Mar-2007] $) $( [28-May-1995] $)

    ${
      brabg.5 $e |- R = { <. x , y >. | ph } $.
      $( The law of concretion for a binary relation. $)
      brabg $p |- ( ( A e. C /\ B e. D ) -> ( A R B <-> ch ) ) $=
        ( wcel wa cop copab wbr opelopabg df-br eleq2i bitri syl5bb ) FHNGINOFG
        PZADEQZNZCFGJRZABCDEFGHIKLSUGUDJNUFFGJTJUEUDMUAUBUC $.
        $( [16-Aug-1999] $)
    $}
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.  $d x y ch $.
    opelopab2.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopab2.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( Ordered pair membership in an ordered pair class abstraction. $)
    opelopab2 $p |- ( ( A e. C /\ B e. D ) -> ( <. A , B >. e.
                 { <. x , y >. | ( ( x e. C /\ y e. D ) /\ ph ) } <-> ch ) ) $=
      ( wcel wa cop cv copab wceq eleq1 anbi1d anbi12d anbi2d opelopabg pm3.27
      pm3.2 impbid2 bitrd ) FHLZGILZMZFGNDOZHLZEOZILZMZAMZDEPLUICMZCUOUGUMMZBMU
      PDEFGHIUJFQZUNUQABURUKUGUMUJFHRSJTULGQZUQUIBCUSUMUHUGULGIRUAKTUBUIUPCUICU
      CUICUDUEUF $.
      $( [31-Jan-2008] $) $( [14-Oct-2007] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ch $.
    opelopab.1 $e |- A e. _V $.
    opelopab.2 $e |- B e. _V $.
    opelopab.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopab.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61. $)
    opelopab $p |- ( <. A , B >. e. { <. x , y >. | ph } <-> ch ) $=
      ( cvv wcel cop copab wb opelopabg mp2an ) FLMGLMFGNADEOMCPHIABCDEFGLLJKQR
      $.
      $( [16-May-1995] $)

    ${
      brab.5 $e |- R = { <. x , y >. | ph } $.
      $( The law of concretion for a binary relation. $)
      brab $p |- ( A R B <-> ch ) $=
        ( cvv wcel wbr wb brabg mp2an ) FNOGNOFGHPCQIJABCDEFGNNHKLMRS $.
        $( [16-Aug-1999] $)
    $}
  $}

  ${
    $d w x y z A $.  $d w y z B $.  $d w z ch $.  $d w z ph $.
    opelopabf.x $e |- ( ps -> A. x ps ) $.
    opelopabf.y $e |- ( ch -> A. y ch ) $.
    opelopabf.1 $e |- A e. _V $.
    opelopabf.2 $e |- B e. _V $.
    opelopabf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopabf.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61.  This version of
       ~ opelopab uses bound-variable hypotheses in place of distinct variable
       conditions." $)
    opelopabf $p |- ( <. A , B >. e. { <. x , y >. | ph } <-> ch ) $=
      ( vz vw cop copab wcel cv wsbc ax-17 hbs1 hbsb wceq sbequ12 sylan9bb
      cbvopab eleq2i sbhypf sbbid opelopab bitri ) FGPZADEQZRUMADNSZTZEOSZTZNOQ
      ZRCUNUSUMAURDENOANUAAOUAUPEODADNUBUCUPEOUBDSUOUDAUPESUQUDURADNUEUPEOUEUFU
      GUHURBEUQTCNOFGJKUOFUDZUPBEOUTEUAABDNFHLUIUJBCEOGIMUIUKUL $.
      $( [8-Jan-2009] $) $( [19-Dec-2008] $)
  $}

  ${
    $d ph z $.  $d ps z $.  $d x z $.  $d y z $.
    $( Equivalence of ordered pair abstraction subclass and implication. $)
    ssopab2 $p |- ( { <. x , y >. | ph } C_ { <. x , y >. | ps } <->
               A. x A. y ( ph -> ps ) ) $=
      ( vz copab wss wi wal hbopab1 hbss hbopab2 cv cop wceq wex opex isseti
      wa cab copsexg imbi12d ss2ab ax-4 sylbi syl5bir df-opab sseq12i syl5ib
      19.23aiv ax-mp 19.21ai hba1 anim2d eximd a4s ss2abdv 3sstr4g impbii ) ACD
      FZBCDFZGZABHZDIZCIZVBVDCCEUTVAACDEJBCDEJKVBVCDDEUTVAACDELBCDELKEMZCMZDMZN
      ZOZEPVBVCHZEVIVGVHQRVJVKEVJVJASZDPZCPZETZVJBSZDPZCPZETZGZVCVBVJVCVNVRHZVT
      VJAVNBVRACDVFUABCDVFUAUBVTWAEIWAVNVREUCWAEUDUEUFUTVOVAVSACDEUGZBCDEUGZUHU
      IUJUKULULVEVOVSUTVAVEVNVREVEVMVQCVDCUMVDVMVQHCVDVLVPDVCDUMVDABVJVCDUDUNUO
      UPUOUQWBWCURUS $.
      $( [27-Aug-2011] $) $( [27-Dec-1996] $)
  $}

  ${
    $d ph z $.  $d ps z $.  $d x y z $.
    ssopab2i.1 $e |- ( ph -> ps ) $.
    $( Inference of ordered pair abstraction subclass from implication. $)
    ssopab2i $p |- { <. x , y >. | ph } C_ { <. x , y >. | ps } $=
      ( copab wss wi wal ssopab2 ax-gen mpgbir ) ACDFBCDFGABHZDICABCDJMDEKL $.
      $( [5-Apr-1995] $)
  $}

  ${
    $d z ph $.  $d z x $.  $d z y $.
    $( Non-empty ordered pair class abstraction. $)
    opabn0 $p |- ( { <. x , y >. | ph } =/= (/) <-> E. x E. y ph ) $=
      ( vz copab c0 wne cv wcel wex cop wceq wa n0 elopab exbii exrot3 19.41v
      opex isseti mpbiran 2exbii bitri 3bitri ) ABCEZFGDHZUEIZDJUFBHZCHZKZLZAMZ
      CJBJZDJZACJBJZDUENUGUMDABCUFOPUNULDJZCJBJUOULDBCQUPABCUPUKDJAUKADRDUJUHUI
      STUAUBUCUD $.
      $( [10-Nov-2007] $) $( [10-Oct-2007] $)
  $}

