$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Power class of union and intersection
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.  $d B x y $.
    $( The power class of the intersection of two classes is the intersection
       of their power classes.  Exercise 4.12(j) of [Mendelson] p. 235. $)
    pwin $p |- ~P ( A i^i B ) = ( ~P A i^i ~P B ) $=
      ( vx cpw cin cv wss wcel ssin visset elpw anbi12i 3bitr4i ineqri eqcomi
      wa ) ADZBDZEABEZDZCQRTCFZAGZUABGZPUASGUAQHZUARHZPUATHUAABIUDUBUEUCUAACJZK
      UABUFKLUASUFKMNO $.
      $( [23-Nov-2003] $)

    $( The power class of the union of two classes includes the union of their
       power classes.  Exercise 4.12(k) of [Mendelson] p. 235. $)
    pwunss $p |- ( ~P A u. ~P B ) C_ ~P ( A u. B ) $=
      ( vx cpw cun cv wo wcel ssun elun visset elpw orbi12i bitri 3imtr4i ssriv
      wss ) CADZBDZEZABEZDZCFZAQZUCBQZGZUCUAQUCTHZUCUBHUCABIUGUCRHZUCSHZGUFUCRS
      JUHUDUIUEUCACKZLUCBUJLMNUCUAUJLOP $.
      $( [23-Nov-2003] $)

    $( The power class of the union of two classes is a subset of the union of
       their power classes, iff one class is a subclass of the other.  Exercise
       4.12(l) of [Mendelson] p. 235. $)
    pwssun $p |- ( ( A C_ B \/ B C_ A ) <->
               ~P ( A u. B ) C_ ( ~P A u. ~P B ) ) $=
      ( vy vx wss wo cun cpw wceq pweq eqimss syl sylbi orim12i wn wa wcel elpw
      wi com23 ssequn2 ssequn1 orcoms ssun cv cpr visset unss12 syl2anb zfpair2
      csn snss df-pr sseq1i bitr2i sylib ssel syl5 exp3a imp31 elun prss bitr4i
      simprbi simplbi ord ex imp ssrdv exp31 con1b syl6ib orrd impbii ) ABEZBAE
      ZFZABGZHZAHZBHZGZEZVQVSVTEZVSWAEZFZWCVPVOWFVPWDVOWEVPVRAIZWDBAUAWGVSVTIWD
      VRAJVSVTKLMVOVRBIZWEABUBWHVSWAIWEVRBJVSWAKLMNUCVSVTWAUDLWCVOVPWCVOOZVPWCW
      IPCBAWCWICUEZBQZWJAQZSWCWKWIWLWCWKWLOZVOSWIWLSWCWKWMVOWCWKPZWMPDABWNWMDUE
      ZAQZWOBQZSWNWPWMWQWNWPWMWQSWNWPPZWLWQWRWOWJUFZVTQZWSWAQZFZWLWQFWRWSWBQZXB
      WCWKWPXCWCWPWKXCWCWPWKXCWPWKPZWSVSQZWCXCXDWOUKZWJUKZGZVREZXEWPXFAEXGBEXIW
      KWOADUGZULWJBCUGZULXFAXGBUHUIXEWSVREXIWSVRDCUJZRWSXHVRWOWJUMUNUOUPVSWBWSU
      QURUSTUTWSVTWAVAUPWTWLXAWQWTWPWLWTWSAEWPWLPWSAXLRWOWJAXJXKVBVCVDXAWQWKXAW
      SBEWQWKPWSBXLRWOWJBXJXKVBVCVENLVFVGTVHVIVJWLVOVKVLTVHVIVGVMVN $.
      $( [23-Nov-2003] $)

    $( Break up the power class of a union into a union of smaller classes. $)
    pwundif $p |- ~P ( A u. B ) =
                  ( ( ~P ( A u. B ) \ ~P A ) u. ~P A ) $=
      ( vx cun cpw cdif cv wcel wss visset elpw wo wn elun eldif notbii anbi12i
      wa bitri orbi12i ordir pm2.1 biantru id ssun3 jaoi impbii 3bitr2i 3bitrri
      orc eqriv ) CABDZEZUMAEZFZUNDZCGZUMHZUQULIZUQUPHZUQULCJZKZUTUQUOHZUQUNHZL
      USUQAIZMZRZVELZUSUQUOUNNVCVGVDVEVCURVDMZRVGUQUMUNOURUSVIVFVBVDVEUQAVAKZPQ
      SVJTVHUSVELZVFVELZRVKUSUSVFVEUAVLVKVEUBUCVKUSUSUSVEUSUDUQABUEUFUSVEUJUGUH
      UISUK $.
      $( [25-Mar-2007] $)
  $}

  $( The power class of the union of two classes equals the union of their
     power classes, iff one class is a subclass of the other.  Part of Exercise
     7(b) of [Enderton] p. 28. $)
  pwun $p |- ( ( A C_ B \/ B C_ A ) <->
             ~P ( A u. B ) = ( ~P A u. ~P B ) ) $=
    ( cun cpw wss wa wo wceq pwunss biantru pwssun eqss 3bitr4i ) ABCDZADBDCZEZ
    PONEZFABEBAEGNOHQPABIJABKNOLM $.
    $( [23-Nov-2003] $)

