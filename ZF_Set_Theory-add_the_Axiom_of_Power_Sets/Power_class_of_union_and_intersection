$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Power class of union and intersection
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.  $d B x y $.
    $( The power class of the intersection of two classes is the intersection
       of their power classes.  Exercise 4.12(j) of [Mendelson] p. 235. $)
    pwin $p |- P~ ( A i^i B ) = ( P~ A i^i P~ B ) $=
      ( vx cpw cin cv wss wa wcel ssin visset elpw anbi12i 3bitr4i ineqri
      eqcomi ) ADZBDZEABEZDZCQRTCFZAGZUABGZHUASGUAQIZUARIZHUATIUAABJUDUBUEUCUAA
      CKZLUABUFLMUASUFLNOP $.
      $( [23-Nov-2003] $) $( [23-Nov-2003] $)

    $( The power class of the union of two classes includes the union of their
       power classes.  Exercise 4.12(k) of [Mendelson] p. 235. $)
    pwunss $p |- ( P~ A u. P~ B ) (_ P~ ( A u. B ) $=
      ( vx cpw cun cv wss wo wcel ssun elun visset elpw orbi12i bitri 3imtr4i
      ssriv ) CADZBDZEZABEZDZCFZAGZUCBGZHZUCUAGUCTIZUCUBIUCABJUGUCRIZUCSIZHUFUC
      RSKUHUDUIUEUCACLZMUCBUJMNOUCUAUJMPQ $.
      $( [23-Nov-2003] $) $( [23-Nov-2003] $)

    $( The power class of the union of two classes is a subset of the union of
       their power classes, iff one class is a subclass of the other.  Exercise
       4.12(l) of [Mendelson] p. 235. $)
    pwssun $p |- ( ( A (_ B \/ B (_ A ) <->
               P~ ( A u. B ) (_ ( P~ A u. P~ B ) ) $=
      ( vy vx wss wo cun cpw orcom wceq ssequn2 pweq eqimss syl sylbi ssequn1
      orim12i ssun wn wa cv wcel wi cpr ssel csn unss12 visset snss syl2anb
      zfpair2 elpw df-pr sseq1i bitr2i sylib syl5 exp3a com23 imp31 elun prss
      bitr4i pm3.27bi pm3.26bi ord ex imp ssrdv exp31 con1b syl6ib orrd impbii
      ) ABEZBAEZFZABGZHZAHZBHZGZEZVQVSVTEZVSWAEZFZWCVQVPVOFWFVOVPIVPWDVOWEVPVRA
      JZWDBAKWGVSVTJWDVRALVSVTMNOVOVRBJZWEABPWHVSWAJWEVRBLVSWAMNOQOVSVTWARNWCVO
      VPWCVOSZVPWCWITCBAWCWICUAZBUBZWJAUBZUCWCWKWIWLWCWKWLSZVOUCWIWLUCWCWKWMVOW
      CWKTZWMTDABWNWMDUAZAUBZWOBUBZUCWNWPWMWQWNWPWMWQUCWNWPTZWLWQWRWOWJUDZVTUBZ
      WSWAUBZFZWLWQFWRWSWBUBZXBWCWKWPXCWCWPWKXCWCWPWKXCWCWSVSUBZXCWPWKTZVSWBWSU
      EXEWOUFZWJUFZGZVREZXDXFAEXGBEXIWPWKXFAXGBUGWOADUHZUIWJBCUHZUIUJXDWSVREXIW
      SVRDCUKZULWSXHVRWOWJUMUNUOUPUQURUSUTWSVTWAVAUPWTWLXAWQWTWPWLWTWSAEWPWLTWS
      AXLULWOWJAXJXKVBVCVDXAWQWKXAWSBEWQWKTWSBXLULWOWJBXJXKVBVCVEQNVFVGUSVHVIVJ
      WLVOVKVLUSVHVIVGVMVN $.
      $( [23-Nov-2003] $) $( [23-Nov-2003] $)

    $( Break up the power class of a union into a union of smaller classes. $)
    pwundif $p |- P~ ( A u. B ) =
                  ( ( P~ ( A u. B ) \ P~ A ) u. P~ A ) $=
      ( vx cun cpw cdif cv wcel wss visset elpw wo wn wa elun eldif notbii
      anbi12i bitri orbi12i ordir pm2.1 biantru id ssun3 jaoi orc impbii
      3bitr2i 3bitrri eqriv ) CABDZEZUMAEZFZUNDZCGZUMHZUQULIZUQUPHZUQULCJZKZUTU
      QUOHZUQUNHZLUSUQAIZMZNZVELZUSUQUOUNOVCVGVDVEVCURVDMZNVGUQUMUNPURUSVIVFVBV
      DVEUQAVAKZQRSVJTVHUSVELZVFVELZNVKUSUSVFVEUAVLVKVEUBUCVKUSUSUSVEUSUDUQABUE
      UFUSVEUGUHUIUJSUK $.
      $( [28-Mar-2007] $) $( [25-Mar-2007] $)
  $}

  $( The power class of the union of two classes equals the union of their
     power classes, iff one class is a subclass of the other.  Part of
     Exercise 7(b) of [Enderton] p. 28. $)
  pwun $p |- ( ( A (_ B \/ B (_ A ) <->
             P~ ( A u. B ) = ( P~ A u. P~ B ) ) $=
    ( cun cpw wss wa wo wceq pwunss biantru pwssun eqss 3bitr4i ) ABCDZADBDCZEZ
    PONEZFABEBAEGNOHQPABIJABKNOLM $.
    $( [23-Nov-2003] $) $( [23-Nov-2003] $)

