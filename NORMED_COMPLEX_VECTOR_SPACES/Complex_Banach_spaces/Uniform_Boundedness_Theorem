$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          Uniform Boundedness Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d c k n r x y z A $.  $d c k n r t x z D $.  $d k n t x y J $.
    $d d k t x z K $.  $d c d k m n r t u x y z N $.  $d t z P $.
    $d c k n r t x y ph $.  $d d t x z R $.  $d c d k m n r t u x y z T $.
    $d c d n r t x y z U $.  $d c d n r t x y W $.  $d c d k m n r t x y z X $.
    ubth.1 $e |- X = ( BaseSet ` U ) $.
    ubth.2 $e |- N = ( norm ` W ) $.
    ${
      ubthlem.3 $e |- D = ( IndMet ` U ) $.
      ubthlem.4 $e |- J = ( MetOpen ` D ) $.
      ubthlem.5 $e |- U e. CBan $.
      ubthlem.6 $e |- W e. NrmCVec $.
      ubthlem.7 $e |- ( ph -> T C_ ( U BLnOp W ) ) $.
      ${
        ubthlem.8 $e |- ( ph -> A. x e. X E. c e. RR A. t e. T
                                ( N ` ( t ` x ) ) <_ c ) $.
        ubthlem.9 $e |- A = ( k e. NN |-> { z e. X |
                              A. t e. T ( N ` ( t ` z ) ) <_ k } ) $.
        $( Lemma for ~ ubth .  The function ` A ` exhibits a countable
           collection of sets that are closed, being the inverse image under
           ` t ` of the closed ball of radius ` k ` , and by assumption they
           cover ` X ` .  Thus by the Baire Category theorem ~ bcth2 , for some
           ` n ` the set ` A `` n ` has an interior, meaning that there is a
           closed ball ` { z e. X | ( y D z ) <_ r } ` in the set.
           (Contributed by Mario Carneiro, 11-Jan-2014.) $)
        ubthlem1 $p |- ( ph -> E. n e. NN E. y e. X E. r e. RR+
                        { z e. X | ( y D z ) <_ r } C_ ( A ` n ) ) $=
          ( cv cfv cnt c0 wne cn wrex co cle wbr crab wss crp ccld wf cuni wceq
          wral wcel wa rzal ralrimivw rabid2 sylibr eqcomd eleq1d iinrab adantl
          crn ciin id ccnv cba cima cims copn cblo sselda ccn eqid cbn cnv bnnv
          ax-mp blocn2 ctop cme cms cmsmeti opntop imsmet imsbai uniopn2 iscncl
          wb bncms mp2an sylib syl simpld adantlr ffvelrn sylan biantrurd fveq2
          breq1d elrab syl6bbr pm5.32da fveq2d a1i wfn ffn 3syl cr cn0v mp3an12
          nvzcl mp3an1 clt wi simpr syl2an adantr sylancr cvv elssuni rexlimdva
          ralimdva sseld mpd wex sstr2 ex elpreima 3bitr4d eqrdv nnre nvnd mpan
          ad2antlr metsym eqtr4d rabbiia simprd imaeq2 rcla4v eqeltrd ralrimiva
          blcld iincld syl2anr eqeltrrd topcld pm2.61ne fmptd cpw cldss2 syl6ss
          sylc sspwuni arch ltle impr an32s nvcl simpllr simplrl syl3anc mpan2d
          frn letr expr eqeltri rabex fvmpt2 mpan2 eleq2d ralbidv syl6bb bicomd
          fvex sylan9bbr fnfvelrn sylbird syl6d dfss3 ne0i mp2b mpanl12 syl2anc
          eqssd bcth2 sseli elpwi ntrss3 cbl ntropn opni2 syl6sseqr cdiv ntrss2
          c2 syl5com ad2antrr w3a jctil rphalfcl rphalflt 3jca blsscls2 rabbidv
          breq2 sseq1d rcla4ev 3syld syldan jcad eximdv df-rex 3imtr4g reximdva
          n0 ) AKUGZFUHZLUIUHUHZUJUKZKULUMZCUGZDUGZGUNZPUGZUOUPZDOUQZUYKURZPUSU
          MZCOUMZKULUMAULLUTUHZFVAZFVOZVBZOVCZUYNAJULUYPEUGZUHZMUHZJUGZUOUPZEHV
          DZDOUQZVUDFAVULULVEZVFZVUOVUDVEOVUDVEZHUJHUJVCZVUOOVUDVUSOVUOVUSVUNDO
          VDOVUOVCVUSVUNDOVUMEHVGVHVUNDOVIVJVKVLVUQHUJUKZVFEHVUMDOUQZVPZVUOVUDV
          UTVVBVUOVCVUQVUMEDHOVMVNVUTVUTVVAVUDVEZEHVDZVVBVUDVEZVUQVUTVQVUQVVCEH
          VUQVUIHVEZVFZVVAVUIVRZUYOMUHZVULUOUPZCNVSUHZUQZVTZVUDVVGBVVAVVMVVGBUG
          ZOVEZVVNVUIUHZMUHZVULUOUPZVFZVVOVVPVVLVEZVFZVVNVVAVEZVVNVVMVEZVVGVVOV
          VRVVTVVGVVOVFZVVRVVPVVKVEZVVRVFVVTVWDVWEVVRVVGOVVKVUIVAZVVOVWEAVVFVWF
          VUPAVVFVFZVWFVVHVVNVTZVUDVEZBNWAUHZWBUHZUTUHZVDZVWGVUIINWCUNZVEZVWFVW
          MVFZAHVWNVUIUDWDVWOVUILVWKWEUNVEZVWPVWNGVWJVUIILVWKNTVWJWFZUAVWKWFZVW
          NWFIWGVEZIWHVEZUBIWIWJZUCWKLWLVEZVWKWLVEZVWQVWPXAGWMVEZVXCGVWTGWNVEZU
          BGITXBWJZWOZGLUAWPWJZVWJWMVEZVXDNWHVEZVXJUCVWJNVWRWQWJZVWJVWKVWSWPWJB
          VUILVWKOVVKVXEOLVBZVCVXHGLOGIORTVXBWRZUAWSWJZVXJVVKVWKVBVCVXLVWJVWKVV
          KVWJNVVKVVKWFZVWRUCWRZVWSWSWJWTXCXDXEZXFZXGZOVVKVVNVUIXHZXIXJVVJVVRCV
          VPVVKUYOVVPVCVVIVVQVULUOUYOVVPMXKXLXMXNXOVWBVVSXAVVGVUMVVRDVVNOUYPVVN
          VCZVUKVVQVULUOVYBVUJVVPMUYPVVNVUIXKXPXLZXMXQVVGVWFVUIOXRVWCVWAXAVXTOV
          VKVUIXSOVVNVVLVUIUUAXTUUBUUCVVGVVLVWLVEZVWMVVMVUDVEZVVGVULYAVEZVYDVUP
          VYFAVVFVULUUDZUUGVXJNYBUHZVVKVEZVYFVYDVXLVXKVYIUCNVVKVYHVXPVYHWFZYDWJ
          ZCVWJVYHVULVVLVWKVVKVXQVWSVVJVYHUYOVWJUNZVULUOUPCVVKUYOVVKVEZVVIVYLVU
          LUOVYMVVIUYOVYHVWJUNZVYLVXKVYMVVIVYNVCUCUYOVWJNMVVKVYHVXPVYJSVWRUUEUU
          FVXJVYIVYMVYLVYNVCVXLVYKVYHUYOVWJVVKVXQUUHYCUUIXLUUJUUPYCXEAVVFVWMVUP
          VWGVWFVWMVXRUUKXGVWIVYEBVVLVWLVVNVVLVCVWHVVMVUDVVNVVLVVHUULVLUUMUVFUU
          NUUOVXCVUTVVDVVEVXIEHVVALUUQYEUURUUSVURVUQVXCVURVXILOVXOUUTWJXQUVAUFU
          VBZAVUGOAVUFOUVCZURVUGOURAVUFVUDVYPAVUEVUFVUDURVYOULVUDFUVQXEVXCVUDVY
          PURVXILOVXOUVDWJZUVEVUFOUVGXDAVVNVUGVEZBOVDZOVUGURAVVQQUGZUOUPZEHVDZQ
          YAUMZBOVDVYSUEAWUCVYRBOAVVOVFZWUBVYRQYAWUDVYTYAVEZVFZVYTVULYFUPZJULUM
          ZWUBVYRYGZWUEWUHWUDVYTJUVHVNWUFWUGWUIJULWUFVUPVFWUGWUBVVREHVDZVYRWUFV
          UPWUGWUBWUJYGWUFVUPWUGVFZVFZWUAVVREHWULVVFVFZWUAVYTVULUOUPZVVRWULWUNV
          VFWUFVUPWUGWUNWUFWUEVYFWUGWUNYGVUPWUDWUEYHVYGVYTVULUVIYIUVJYJWUMVVQYA
          VEZWUEVYFWUAWUNVFVVRYGWUFVVFWUOWUKWUDVVFWUOWUEWUDVVFVFVXKVWEWUOUCAVVF
          VVOVWEVWGVWFVVOVWEVXSVYAXIUVKVVPNMVVKVXPSUVLYKXGXGWUDWUEWUKVVFUVMWUMV
          UPVYFWUFVUPWUGVVFUVNVYGXEVVQVYTVULUVRUVOUVPYOUVSWUDVUPWUJVYRYGWUEWUDV
          UPVFWUJVVNVULFUHZVEZVYRVUPWUQVVOWUJVFZWUDWUJVUPWUQVVNVUOVEWURVUPWUPVU
          OVVNVUPVUOYLVEWUPVUOVCVUNDOOIVSUHYLRIVSUWHUVTUWAJULVUOYLFUFUWBUWCUWDV
          UNWUJDVVNOVYBVUMVVREHVYCUWEXMUWFWUDWUJWURWUDVVOWUJAVVOYHXJUWGUWIWUDFU
          LXRZVUPWUQVYRYGAWUSVVOAVUEWUSVYOULVUDFXSXEYJWUSVUPVFZWUPVUGVVNWUTWUPV
          UFVEWUPVUGURULVULFUWJWUPVUFYMXEYPXIUWKXGUWLYNYQYNYOYQBOVUGUWMVJUWRVXF
          OUJUKZVUEVUHVFUYNVXGVXAIYBUHZOVEWVAVXBIOWVBRWVBWFYDOWVBUWNUWOGKLFOVXN
          UAUWSUWPUWQAUYMVUCKULAUYJULVEZVFZUYOUYLVEZCYRUYOOVEZVUBVFZCYRUYMVUCWV
          DWVEWVGCWVDWVEWVFVUBWVDUYLOUYOWVDVXCUYKOURZUYLOURZVXIAVUEWVCWVHVYOVUE
          WVCVFUYKVUDVEUYKVYPVEWVHULVUDUYJFXHVUDVYPUYKVYQUWTUYKOUXAXTXIZUYKLOVX
          OUXBYKYPWVDWVEVUBWVDWVEVFUYOVVNGUXCUHUNZUYLURZBUSUMZVUBWVDUYLLVEZWVEW
          VMWVDVXCWVHWVNVXIWVJUYKLOVXOUXDYKZVXEWVNWVEWVMVXHBUYLGUYOLUAUXEYEXIWV
          DWVEWVFWVMVUBYGWVDUYLOUYOWVDWVNWVIWVOWVNUYLVXMOUYLLYMVXOUXFXEWDWVDWVF
          VFZWVLVUBBUSWVPVVNUSVEZVFZWVLWVKUYKURZUYQVVNUXIUXGUNZUOUPZDOUQZUYKURZ
          VUBWVDWVLWVSYGWVFWVQWVDUYLUYKURZWVLWVSWVDVXCWVHWWDVXIWVJUYKLOVXOUXHYK
          WVKUYLUYKYSUXJUXKWVRWWBWVKURZWVSWWCYGWVPVXEWVFVFWVTUSVEZWVQWVTVVNYFUP
          ZUXLWWEWVQWVPWVFVXEWVDWVFYHVXHUXMWVQWWFWVQWWGVVNUXNZWVQVQVVNUXOUXPDGU
          YOWVTWWBVVNLOVXNUAWWBWFUXQYIWWBWVKUYKYSXEWVRWWFWWCVUBYGWVQWWFWVPWWHVN
          WWFWWCVUBVUAWWCPWVTUSUYRWVTVCZUYTWWBUYKWWIUYSWWADOUYRWVTUYQUOUXSUXRUX
          TUYAYTXEUYBYNUYCYQYTUYDUYECUYLUYIVUBCOUYFUYGUYHYQ $.
          $( [10-Jan-2014] $)

        ubthlem.10 $e |- ( ph -> K e. NN ) $.
        ubthlem.11 $e |- ( ph -> P e. X ) $.
        ubthlem.12 $e |- ( ph -> R e. RR+ ) $.
        ubthlem.13 $e |- ( ph -> { z e. X | ( P D z ) <_ R } C_ ( A ` K ) ) $.
        $( Lemma for ~ ubth .  Given that there is a closed ball
           ` B ( P , R ) ` in ` A `` K ` , for any ` x e. B ( 0 , 1 ) ` , we
           have ` P + R x. x e. B ( P , R ) ` and ` P e. B ( P , R ) ` , so
           both of these have ` norm ( t ( z ) ) <_ K ` and so ` norm ( t ( x `
           ` ) ) <_ ( norm ( t ( P ) ) + norm ( t ( P + R x. x ) ) ) / R <_ ( `
           ` K + K ) / R ` , which is our desired uniform bound.  (Contributed
           by Mario Carneiro, 11-Jan-2014.) $)
        ubthlem2 $p |- ( ph -> E. d e. RR A. t e. T
                         ( ( U normOp W ) ` t ) <_ d ) $=
          ( caddc co cdiv cr wcel cv cnmo cfv cle wbr wral wrex crp cn nnrp syl
          rpaddcl syl2anc rpdivcl rpre wa cnm c1 wi cns cpv crab rabss ad2antrr
          wss sylib cnv cbn bnnv ax-mp rpcn simpr eqid nvscl syl3anc nvgcl wceq
          a1i oveq2 breq1d eleq1 imbi12d rcla4cv sylc cmul cnsb cme cms cmsmeti
          bncms imsbai metsym imsdval nvpncan2 fveq2d 3eqtrd cc0 rprege0 nvsge0
          cc eqtrd mulid1 eqcomd breq12d wb nvcl mpan breq2 ralbidv fveq2 elrab
          clt cba sylancr adantr ccld ctop opntop cuni uniopn2 ffvelrn syl32anc
          syld ralrimiva adantl 1re elrp lemul2 bitr4d rabbidv cvv fvex eqeltri
          rabex fvmpt eleq2d syl6bb 3imtr3d ra4 com12 ad2antlr eqbrtrd sylanbrc
          met0 rpge0 sseldd eleqtrd simprd r19.21bi wf ccnv cima cims copn cblo
          sselda ccn blocn2 imsmet iscncl mp2an simpld nnre le2add mpan2d bloln
          syl22anc clno mp3an12 lnosub lnomul 3eqtr3d nvmtri eqbrtrrd ax-mulrcl
          sylan readdcl letr mpand lemuldiv2 sylibd adantld cxr nmoubi mpbird
          rexr rcla4ev ) AMMULUMZHUNUMZUOUPZDUQZJOURUMZUSZUXEUTVAZDIVBZUXIRUQZU
          TVAZDIVBZRUOVCAUXEVDUPZUXFAUXDVDUPZHVDUPZUXOAMVDUPZUXRUXPAMVEUPZUXRUH
          MVFVGZUXTMMVHVIZUJUXDHVJVIUXEVKVGZAUXJDIAUXGIUPZVLZUXJBUQZJVMUSZUSZVN
          UTVAZUYEUXGUSZNUSZUXEUTVAZVOZBPVBZUYDUYLBPUYDUYEPUPZVLZUYHGHUYEJVPUSZ
          UMZJVQUSZUMZPUPZUYSUXGUSZNUSZMUTVAZDIVBZVLZUYKUYOGUYSFUMZHUTVAZUYSMEU
          SZUPZUYHVUEUYOGCUQZFUMZHUTVAZVUJVUHUPZVOZCPVBZUYTVUGVUIVOZAVUOUYCUYNA
          VULCPVRZVUHWAVUOUKVULCPVUHVSWBVTUYOJWCUPZGPUPZUYQPUPZUYTVURUYOJWDUPZV
          URUCJWEWFZWNZAVUSUYCUYNUIVTZUYOVURHXPUPZUYNVUTVVCUYOUXQVVEAUXQUYCUYNU
          JVTZHWGVGZUYDUYNWHZHUYEUYPJPSUYPWIZWJWKZGUYQJUYRPSUYRWIZWLWKZVUNVUPCU
          YSPVUJUYSWMZVULVUGVUMVUIVVMVUKVUFHUTVUJUYSGFWOWPVUJUYSVUHWQWRWSWTUYOV
          UGHUYGXAUMZHVNXAUMZUTVAZUYHUYOVUFVVNHVVOUTUYOVUFUYQUYFUSZVVNUYOVUFUYS
          GFUMZUYSGJXBUSZUMZUYFUSZVVQUYOFXCUPZVUSUYTVUFVVRWMVWBUYOFVVAFXDUPUCFJ
          UAXFWFXEZWNVVDVVLGUYSFPFJPSUAVVBXGZXHWKUYOVURUYTVUSVVRVWAWMVVCVVLVVDU
          YSGFJVVSUYFPSVVSWIZUYFWIZUAXIWKUYOVVTUYQUYFUYOVURVUSVUTVVTUYQWMVVCVVD
          VVJGUYQJUYRVVSPSVVKVWEXJWKZXKXLUYOVURHUOUPZXMHUTVAZVLZUYNVVQVVNWMVVCU
          YOUXQVWJVVFHXNVGZVVHHUYEUYPJUYFPSVVIVWFXOWKXQUYOVVOHUYOVVEVVOHWMVVGHX
          RVGXSXTUYOUYGUOUPZVNUOUPZVWHXMHYHVAVLZUYHVVPYAUYNVWLUYDVURUYNVWLVVBUY
          EJUYFPSVWFYBYCUUAVWMUYOUUBWNUYOUXQVWNVVFHUUCWBZUYGVNHUUDWKUUEAVUIVUEY
          AUYCUYNAVUIUYSVUJUXGUSZNUSZMUTVAZDIVBZCPVRZUPVUEAVUHVWTUYSAUXSVUHVWTW
          MUHKMVWQKUQZUTVAZDIVBZCPVRVWTVEEVXAMWMZVXCVWSCPVXDVXBVWRDIVXAMVWQUTYD
          YEUUFUGVWSCPPJYIUSUUGSJYIUUHUUIUUJUUKVGZUULVWSVUDCUYSPVVMVWRVUCDIVVMV
          WQVUBMUTVVMVWPVUANVUJUYSUXGYFXKWPYEYGUUMVTUUNUYOVUDUYKUYTUYOVUDVUCUYK
          UYCVUDVUCVOAUYNVUDUYCVUCVUCDIUUOUUPUUQUYOVUCHUYJXAUMZUXDUTVAZUYKUYOVU
          CVUBGUXGUSZNUSZULUMZUXDUTVAZVXGUYOVUCVXIMUTVAZVXKUYDVXLUYNAVXLDIAVUSV
          XLDIVBZAGVWTUPVUSVXMVLAGVUHVWTAVUQVUHGUKAVUSGGFUMZHUTVAZGVUQUPUIAVXNX
          MHUTAVWBVUSVXNXMWMVWCUIGFPVWDUUTYJAUXQVWIUJHUVAVGUURVULVXOCGPVUJGWMZV
          UKVXNHUTVUJGGFWOWPYGUUSUVBVXEUVCVWSVXMCGPVXPVWRVXLDIVXPVWQVXIMUTVXPVW
          PVXHNVUJGUXGYFXKWPYEYGWBUVDUVEYKUYOVUBUOUPZVXIUOUPZMUOUPZVXSVUCVXLVLV
          XKVOUYOOWCUPZVUAOYIUSZUPZVXQUDUYOPVYAUXGUVFZUYTVYBUYDVYCUYNUYDVYCUXGU
          VGUYEUVHLYLUSUPBOUVIUSZUVJUSZYLUSVBZUYDUXGJOUVKUMZUPZVYCVYFVLZAIVYGUX
          GUEUVLZVYHUXGLVYEUVMUMUPZVYIVYGFVYDUXGJLVYEOUAVYDWIZUBVYEWIZVYGWIZVVB
          UDUVNLYMUPZVYEYMUPZVYKVYIYAVWBVYOVWCFLUBYNWFVYDXCUPZVYPVXTVYQUDVYDOVY
          LUVOWFZVYDVYEVYMYNWFBUXGLVYEPVYAVWBPLYOWMVWCFLPVWDUBYPWFVYQVYAVYEYOWM
          VYRVYDVYEVYAVYDOVYAVYAWIZVYLUDXGVYMYPWFUVPUVQWBVGUVRZYKZVVLPVYAUYSUXG
          YQVIZVUAONVYAVYSTYBYJZUYOVXTVXHVYAUPZVXRUDUYOVYCVUSWUDWUAVVDPVYAGUXGY
          QVIZVXHONVYAVYSTYBYJZAVXSUYCUYNAUXSVXSUHMUVSVGVTZWUGVUBVXIMMUVTUWCUWA
          UYOVXFVXJUTVAZVXKVXGUYOVUAVXHOXBUSZUMZNUSZVXFVXJUTUYOWUKHUYIOVPUSZUMZ
          NUSZVXFUYOWUJWUMNUYOVVTUXGUSZUYQUXGUSZWUJWUMUYOVVTUYQUXGVWGXKUYOVURVX
          TUXGJOUWDUMZUPZUYTVUSWUOWUJWMVVCVXTUYOUDWNZUYDWURUYNUYDVYHWURVYJVURVX
          TVYHWURVVBUDVYGUXGJWUQOWUQWIZVYNUWBUWEVGYKZVVLVVDUYSGUXGJWUQVVSWUIOPS
          VWEWUIWIZWUTUWFYRUYOVURVXTWURVVEUYNWUPWUMWMVVCWUSWVAVVGVVHHUYEUYPWULU
          XGJWUQOPSVVIWULWIZWUTUWGYRUWHXKUYOVXTVWJUYIVYAUPZWUNVXFWMWUSVWKUYDVYC
          UYNWVDVYTPVYAUYEUXGYQUWLZHUYIWULONVYAVYSWVCTXOWKXQUYOVXTVYBWUDWUKVXJU
          TVAWUSWUBWUEVUAVXHOWUINVYAVYSWVBTUWIWKUWJUYOVXFUOUPZVXJUOUPZUXDUOUPZW
          UHVXKVLVXGVOUYOVWHUYJUOUPZWVFUYOUXQVWHVVFHVKVGUYOVXTWVDWVIUDWVEUYIONV
          YAVYSTYBYJZHUYJUWKVIUYOVXQVXRWVGWUCWUFVUBVXIUWMVIAWVHUYCUYNAUXPWVHUYA
          UXDVKVGVTZVXFVXJUXDUWNWKUWOYSUYOWVIWVHVWNVXGUYKYAWVJWVKVWOUYJUXDHUWPW
          KUWQYSUWRYSYTUYDVYCUXEUWSUPZUXJUYMYAVYTAWVLUYCAUXFWVLUYBUXEUXBVGYKBUX
          EUXGJUYFNUXHOPVYASVYSVWFTUXHWIVVBUDUWTVIUXAYTUXNUXKRUXEUOUXLUXEWMUXMU
          XJDIUXLUXEUXIUTYDYEUXCVI $.
          $( [11-Jan-2014] $)
      $}

      $d d ph $.
      $( Lemma for ~ ubth .  Prove the reverse implication, using ~ nmblolbi .
         (Contributed by Mario Carneiro, 11-Jan-2014.) $)
      ubthlem3 $p |- ( ph ->
                ( A. x e. X E. c e. RR A. t e. T ( N ` ( t ` x ) ) <_ c <->
                  E. d e. RR A. t e. T ( ( U normOp W ) ` t ) <_ d ) ) $=
        ( cle vz vu vy vr vn vm vk cv cfv wbr wral cr wrex cnmo co fveq1 fveq2d
        wceq breq1d cbvralv breq2 ralbidv syl5bb cbvrexv fveq2 rexralbidv wa cn
        crab cmpt wss cblo adantr simpr cbvrabv rabbidv syl5eq cbvmptv ubthlem1
        crp wcel ad3antrrr ad2antrr simplrl simplrr simprl simprr ubthlem2 expr
        sylib rexlimdva rexlimdvva mpd ex syl5bir cnm cmul cba adantlr ad2ant2r
        wf sselda cnv cbn bnnv ax-mp eqid blof mp3an12 syl ffvelrn syl2anc nvcl
        simplr mpan cxr cmnf clt nmoxr simpllr nmogtmnf xrre syl22anc ax-mulrcl
        ad2antlr nmblolbi cc0 nvge0 jca lemul1a syl31anc letrd ralimdva rcla4ev
        syl2an wi syld ralrimdva impbid ) ABUHZCUHZUIZHUIZKUHZTUJZCEUKZKULUMZBJ
        UKZUUAFIUNUOZUIZLUHZTUJZCEUKZLULUMZUUHUAUHZUBUHZUIZHUIZUUKTUJZUBEUKZLUL
        UMZUAJUKZAUUNUVAUUGUABJUVAUUOUUAUIZHUIZUUDTUJZCEUKZKULUMUUOYTURZUUGUUTU
        VFLKULUUTUVDUUKTUJZCEUKUUKUUDURZUVFUUSUVHUBCEUUPUUAURZUURUVDUUKTUVJUUQU
        VCHUUOUUPUUAUPUQUSUTUVIUVHUVECEUUKUUDUVDTVAVBVCVDUVGUVEUUEKCULEUVGUVDUU
        CUUDTUVGUVCUUBHUUOYTUUAVEUQUSVFVCUTZAUVBUUNAUVBVGZUCUHZUUODUOUDUHZTUJUA
        JVIUEUHZUFVHUUKUUPUIZHUIZUFUHZTUJZUBEUKZLJVIZVJZUIVKZUDVTUMZUCJUMUEVHUM
        UUNUVLBUCUACUWBDEFUGUEGHIJUDKMNOPQRAEFIVLUOZVKZUVBSVMUVLUVBUUHAUVBVNUVK
        WJZUFUGVHUWAUVDUGUHZTUJZCEUKZUAJVIZUVRUWHURZUWAUVDUVRTUJZCEUKZUAJVIUWKU
        VTUWNLUAJUVTUUKUUAUIZHUIZUVRTUJZCEUKUUKUUOURZUWNUVSUWQUBCEUVJUVQUWPUVRT
        UVJUVPUWOHUUKUUPUUAUPUQUSUTUWRUWQUWMCEUWRUWPUVDUVRTUWRUWOUVCHUUKUUOUUAV
        EUQUSVBVCVOUWLUWNUWJUAJUWLUWMUWICEUVRUWHUVDTVAVBVPVQVRZVSUVLUWDUUNUEUCV
        HJUVLUVOVHWAZUVMJWAZVGZVGZUWCUUNUDVTUXCUVNVTWAZUWCUUNUXCUXDUWCVGZVGBUAC
        UWBDUVMUVNEFUGGUVOHIJKLMNOPQRAUWFUVBUXBUXESWBUVLUUHUXBUXEUWGWCUWSUVLUWT
        UXAUXEWDUVLUWTUXAUXEWEUXCUXDUWCWFUXCUXDUWCWGWHWIWKWLWMWNWOAUUMUUHLULAUU
        KULWAZVGZUUMUUGBJUXGYTJWAZVGZUUMUUCUUKYTFWPUIZUIZWQUOZTUJZCEUKZUUGUXIUU
        LUXMCEUXIUUAEWAZUULUXMUXIUXOUULVGZVGZUUCUUJUXKWQUOZUXLUXQUUBIWRUIZWAZUU
        CULWAZUXQJUXSUUAXAZUXHUXTUXQUUAUWEWAZUYBUXGUXOUYCUXHUULAUXOUYCUXFAEUWEU
        UASXBWSWTZFXCWAZIXCWAZUYCUYBFXDWAUYEQFXEXFZRUWEUUAFIJUXSMUXSXGZUWEXGZXH
        XIXJZUXGUXHUXPXNZJUXSYTUUAXKXLUYFUXTUYARUUBIHUXSUYHNXMXOXJUXQUUJULWAZUX
        KULWAZUXRULWAUXQUUJXPWAZUXFXQUUJXRUJZUULUYLUXQUYBUYNUYJUYEUYFUYBUYNUYGR
        UUAFUUIIJUXSMUYHUUIXGZXSXIXJAUXFUXHUXPXTZUXQUYBUYOUYJUYEUYFUYBUYOUYGRUU
        AFUUIIJUXSMUYHUYPYAXIXJUXIUXOUULWGZUUJUUKYBYCZUXHUYMUXGUXPUYEUXHUYMUYGY
        TFUXJJMUXJXGZXMXOZYEUUJUXKYDXLUXIUXLULWAZUXPUXGUXFUYMVUBUXHAUXFVNVUAUUK
        UXKYDYOZVMUXQUYCUXHUUCUXRTUJUYDUYKYTUWEUUAFUXJHUUIIJMUYTNUYPUYIUYGRYFXL
        UXQUYLUXFUYMYGUXKTUJZVGZUULUXRUXLTUJUYSUYQUXHVUEUXGUXPUXHUYMVUDVUAUYEUX
        HVUDUYGYTFUXJJMUYTYHXOYIYEUYRUUJUUKUXKYJYKYLWIYMUXIVUBUXNUUGYPVUCVUBUXN
        UUGUUFUXNKUXLULUUDUXLURUUEUXMCEUUDUXLUUCTVAVBYNWNXJYQYRWKYS $.
        $( [11-Jan-2014] $)
    $}

    ubth.3 $e |- M = ( U normOp W ) $.
    $( Uniform Boundedness Theorem, also called the Banach-Steinhaus Theorem.
       Let ` T ` be a collection of bounded linear operators on a Banach
       space.  If, for every vector ` x ` , the norms of the operators' values
       are bounded, then the operators' norms are also bounded.  Theorem 4.7-3
       of [Kreyszig] p. 249.  See also
       ~ http://en.wikipedia.org/wiki/Uniform_boundedness_principle .  (The
       proof was shortened by Mario Carneiro, 11-Jan-2014.) $)
    ubth $p |- ( ( U e. CBan /\ W e. NrmCVec /\ T C_ ( U BLnOp W ) ) ->
                 ( A. x e. X E. c e. RR A. t e. T ( N ` ( t ` x ) ) <_ c <->
                   E. d e. RR A. t e. T ( M ` t ) <_ d ) ) $=
      ( cblo co cfv cle wbr wral cr cbn wcel cnv wss cv wrex wb caddc cmul cabs
      cop cif cba cnmo cnm wceq oveq1 sseq2d fveq2 syl5eq raleqdv fveq1d breq1d
      wi rexralbidv bibi12d imbi12d oveq2 ralbidv cims copn eqid elimel elimnvu
      cnbn id ubthlem3 dedth2h 3impia ) DUAUBZGUCUBZCDGNOZUDZAUEBUEZPZFPZIUEZQR
      ZBCSITUFZAHSZWDEPZJUEZQRZBCSJTUFZUGZVTWAWCWOVDCVTDUHUIUKUJUKZULZGNOZUDZWI
      AWQUMPZSZWDWQGUNOZPZWLQRZBCSJTUFZUGZVDCWQWAGWPULZNOZUDZWEXGUOPZPZWGQRZBCS
      ITUFZAWTSZWDWQXGUNOZPZWLQRZBCSJTUFZUGZVDDGWPWPDWQUPZWCWSWOXFXTWBWRCDWQGNU
      QURXTWJXAWNXEXTWIAHWTXTHDUMPWTKDWQUMUSUTVAXTWMXDJBTCXTWKXCWLQXTWDEXBXTEDG
      UNOXBMDWQGUNUQUTVBVCVEVFVGGXGUPZWSXIXFXSYAWRXHCGXGWQNVHURYAXAXNXEXRYAWIXM
      AWTYAWHXLIBTCYAWFXKWGQYAWEFXJYAFGUOPXJLGXGUOUSUTVBVCVEVIYAXDXQJBTCYAXCXPW
      LQYAWDXBXOGXGWQUNVHVBVCVEVFVGXIABWQVJPZCWQYBVKPZXJXGWTIJWTVLXJVLYBVLYCVLD
      WPUAWPWPVLVOVMGVNXIVPVQVRVS $.
      $( [11-Jan-2014] $) $( [7-Nov-2007] $)
  $}


