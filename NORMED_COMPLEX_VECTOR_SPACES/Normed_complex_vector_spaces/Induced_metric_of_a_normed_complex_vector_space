$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Induced metric of a normed complex vector space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d u U $.
    imsval.3 $e |- M = ( -v ` U ) $.
    imsval.6 $e |- N = ( norm ` U ) $.
    imsval.8 $e |- D = ( IndMet ` U ) $.
    $( Value of the induced metric of a normed complex vector space. $)
    imsval $p |- ( U e. NrmCVec -> D = ( N o. M ) ) $=
      ( vu cnv wcel cims cfv cnm cnsb ccom cv wceq fveq2 coeq12d fvex coeq12i
      df-ims coex fvmpt 3eqtr4g ) BIJBKLBMLZBNLZOZADCOHBHPZMLZUINLZOUHIKUIBQUJU
      FUKUGUIBMRUIBNRSHUBUFUGBMTBNTUCUDGDUFCUGFEUAUE $.
      $( [16-Nov-2013] $) $( [11-Sep-2007] $)
  $}

  ${
    imsdval.1 $e |- X = ( BaseSet ` U ) $.
    imsdval.3 $e |- M = ( -v ` U ) $.
    imsdval.6 $e |- N = ( norm ` U ) $.
    imsdval.8 $e |- D = ( IndMet ` U ) $.
    $( Value of the induced metric (distance function) of a normed complex
       vector space.  Equation 1 of [Kreyszig] p. 59. $)
    imsdval $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                  ( A D B ) = ( N ` ( A M B ) ) ) $=
      ( cnv wcel w3a cop cfv co ccom wceq df-ov imsval 3ad2ant1 fveq1d cxp nvmf
      wf wa opelxpi fvco3 syl2an 3impb eqtrd fveq2i 3eqtr4g ) DLMZAGMZBGMZNZABO
      ZCPZUSEPZFPZABCQABEQZFPURUTUSFERZPZVBURUSCVDUOUPCVDSUQCDEFIJKUAUBUCUOUPUQ
      VEVBSZUOGGUDZGEUFUSVGMVFUPUQUGDEGHIUEABGGUHVGGUSFEUIUJUKULABCTVCVAFABETUM
      UN $.
      $( [27-Dec-2014] $) $( [11-Sep-2007] $)
  $}

  ${
    imsdval2.1 $e |- X = ( BaseSet ` U ) $.
    imsdval2.2 $e |- G = ( +v ` U ) $.
    imsdval2.4 $e |- S = ( .s ` U ) $.
    imsdval2.6 $e |- N = ( norm ` U ) $.
    imsdval2.8 $e |- D = ( IndMet ` U ) $.
    $( MAY BE REVISED $)
    $( Value of the distance function of the induced metric of a normed complex
       vector space.  Equation 1 of [Kreyszig] p. 59. $)
    imsdval2 $p |- ( ( U e. NrmCVec /\ A e. X /\ B e. X ) ->
                  ( A D B ) = ( N ` ( A G ( -u 1 S B ) ) ) ) $=
      ( cnv wcel w3a co cnsb cfv c1 cneg eqid imsdval nvmval fveq2d eqtrd ) ENO
      AHOBHOPZABCQABERSZQZGSATUABDQFQZGSABCEUHGHIUHUBZLMUCUGUIUJGABDEFUHHIJKUKU
      DUEUF $.
      $( [11-Sep-2007] $) $( [28-Nov-2006] $)
  $}

  ${
    nvnd.1 $e |- X = ( BaseSet ` U ) $.
    nvnd.5 $e |- Z = ( 0vec ` U ) $.
    nvnd.6 $e |- N = ( norm ` U ) $.
    nvnd.8 $e |- D = ( IndMet ` U ) $.
    $( The norm of a normed complex vector space expressed in terms of the
       distance function of its induced metric.  Problem 1 of [Kreyszig]
       p. 63. $)
    nvnd $p |- ( ( U e. NrmCVec /\ A e. X ) -> ( N ` A ) = ( A D Z ) ) $=
      ( cnv wcel wa co cfv wceq adantr eqid mpd3an3 c1 cnsb imsdval cneg nvmval
      nvzcl cns cpv ax-1cn negcli nvsz mpan2 oveq2d nv0rid 3eqtrd fveq2d eqtr2d
      cc ) CKLZAELZMZAFBNZAFCUAOZNZDOZADOURUSFELZVAVDPURVEUSCEFGHUEQZAFBCVBDEGV
      BRZIJUBSUTVCADUTVCATUCZFCUFOZNZCUGOZNZAFVKNZAURUSVEVCVLPVFAFVICVKVBEGVKRZ
      VIRZVGUDSURVLVMPUSURVJFAVKURVHUQLVJFPTUHUIVHVICFVOHUJUKULQACVKEFGVNHUMUNU
      OUP $.
      $( [6-Jan-2008] $) $( [4-Dec-2006] $)
  $}

  ${
    $d x y z U $.  $d x y z X $.
    imsdfn.1 $e |- X = ( BaseSet ` U ) $.
    imsdfn.8 $e |- D = ( IndMet ` U ) $.
    $( Mapping for the induced metric distance function of a normed complex
       vector space. $)
    imsdf $p |- ( U e. NrmCVec -> D : ( X X. X ) --> RR ) $=
      ( cnv wcel cxp cr wf cnm cfv cnsb ccom eqid nvf nvmf fco syl2anc imsval
      feq1d mpbird ) BFGZCCHZIAJUDIBKLZBMLZNZJZUCCIUEJUDCUFJUHBUECDUEOZPBUFCDUF
      OZQUDCIUEUFRSUCUDIAUGABUFUEUJUIETUAUB $.
      $( [30-Sep-2007] $) $( [29-Nov-2006] $)

    $( Base set of the induced metric space of a normed complex vector
       space. $)
    imsba $p |- ( U e. NrmCVec -> X = dom dom D ) $=
      ( cnv wcel cxp cr wf cdm wceq imsdf fdm dmeq dmxpid syl6req 3syl ) BFGCCH
      ZIAJAKZSLZCTKZLABCDEMSIANUAUBSKCTSOCPQR $.
      $( [4-Sep-2007] $)

    ${
      imsbai.u $e |- U e. NrmCVec $.
      $( Base set of the induced metric space of a normed complex vector
         space. $)
      imsbai $p |- X = dom dom D $=
        ( cnv wcel cdm wceq imsba ax-mp ) BGHCAIIJFABCDEKL $.
        $( [19-Dec-2007] $)
    $}
  $}

  ${
    $d x y z D $.  $d x y z X $.
    imsmetlem.1 $e |- X = ( BaseSet ` U ) $.
    imsmetlem.2 $e |- G = ( +v ` U ) $.
    imsmetlem.7 $e |- M = ( inv ` G ) $.
    imsmetlem.4 $e |- S = ( .s ` U ) $.
    imsmetlem.5 $e |- Z = ( 0vec ` U ) $.
    imsmetlem.6 $e |- N = ( norm ` U ) $.
    imsmetlem.8 $e |- D = ( IndMet ` U ) $.
    imsmetlem.9 $e |- U e. NrmCVec $.
    $( Lemma for ~ imsmet . $)
    imsmetlem $p |- D e. Met $=
      ( wcel co wceq mp3an1 vx vy vz cba cfv cvv eqeltri cnv cxp cr imsdf ax-mp
      fvex wf cv wa cc0 c1 cneg imsdval2 eqeq1d wb cc ax-1cn negcli nvscl nvgcl
      mp3an12 sylan2 nvz sylancr nvzcl nvrcan mpan mp3an2 sylancom simpl adantl
      w3a simpr nvass syl3anc nvlinv oveq2d nv0rid adantr 3eqtrd nv0lid eqeq12d
      bitr3d caddc cle wbr syl2anc 3adant3 3adant2 nvtri 3adant1 simp1 3ad2ant3
      3bitrd oveq1d eqtr3d fveq2d eqtr4d nvdif eqtrd oveq12d 3brtr4d ismeti
      3coml ) UAUBUCAGGCUDUEUFICUDUMUGCUHQZGGUIUJAUNPACGIOUKULUAUOZGQZUBUOZGQZU
      PZXMXOARZUQSXMURUSZXOBRZDRZFUEZUQSZYAHSZXMXOSZXQXRYBUQXLXNXPXRYBSZPXMXOAB
      CDFGIJLNOUTTZVAXQXLYAGQZYCYDVBPXPXNXTGQZYHXLXSVCQZXPYIPURVDVEZXSXOBCGILVF
      VHZXLXNYIYHPXMXTCDGIJVGTVIZYACFGHIMNVJVKXQYAXODRZHXODRZSZYDYEXNXPYHYPYDVB
      ZYMYHHGQZXPYQXLYRPCGHIMVLULXLYHYRXPVSYQPYAHXOCDGIJVMVNVOVPXQYNXMYOXOXQYNX
      MXTXODRZDRZXMHDRZXMXQXNYIXPYNYTSZXNXPVQXPYIXNYLVRXNXPVTXLXNYIXPVSUUBPXMXT
      XOCDGIJWAVNWBXQYSHXMDXPYSHSZXNXLXPUUCPXOBCDGHIJLMWCVNVRWDXNUUAXMSZXPXLXNU
      UDPXMCDGHIJMWEVNZWFWGXPYOXOSZXNXLXPUUFPXOCDGHIJMWHVNVRWIWJXAUCUOZGQZXNXPX
      RUUGXMARZUUGXOARZWKRZWLWMUUHXNXPVSZXMXSUUGBRZDRZUUGXTDRZDRZFUEZUUNFUEZUUO
      FUEZWKRZXRUUKWLUULUUNGQZUUOGQZUUQUUTWLWMZUUHXNUVAXPUUHXNUPZXNUUMGQZUVAUUH
      XNVTZUUHUVEXNXLYJUUHUVEPYKXSUUGBCGILVFVHWFZXLXNUVEUVAPXMUUMCDGIJVGTWNWOZU
      UHXPUVBXNXPUUHYIUVBYLXLUUHYIUVBPUUGXTCDGIJVGTVIWPXLUVAUVBUVCPUUNUUOCDFGIJ
      NWQTWNUULXRYBUUQXNXPYFUUHYGWRUULUUPYAFUULUUNUUGDRZXTDRZUUPYAUULUVAUUHYIUV
      JUUPSZUVHUUHXNXPWSXPUUHYIXNYLWTXLUVAUUHYIVSUVKPUUNUUGXTCDGIJWAVNWBUULUVIX
      MXTDUUHXNUVIXMSXPUVDUVIXMUUMUUGDRZDRZUUAXMUVDXNUVEUUHUVIUVMSZUVFUVGUUHXNV
      QXLXNUVEUUHVSUVNPXMUUMUUGCDGIJWAVNWBUVDUVLHXMDUUHUVLHSZXNXLUUHUVOPUUGBCDG
      HIJLMWCVNWFWDXNUUDUUHUUEVRWGWOXBXCXDXEUULUUIUURUUJUUSWKUUHXNUUIUURSXPUVDU
      UIUUGXSXMBRDRFUEZUURXLUUHXNUUIUVPSPUUGXMABCDFGIJLNOUTTXLUUHXNUVPUURSPUUGX
      MBCDFGIJLNXFTXGWOUUHXPUUJUUSSZXNXLUUHXPUVQPUUGXOABCDFGIJLNOUTTWPXHXIXKXJ
      $.
      $( [2-Oct-2007] $) $( [29-Nov-2006] $)
  $}

  ${
    imsmet.8 $e |- D = ( IndMet ` U ) $.
    $( The induced metric of a normed complex vector space is a metric space.
       Part of Definition 2.2-1 of [Kreyszig] p. 58. $)
    imsmet $p |- ( U e. NrmCVec -> D e. Met ) $=
      ( cnv wcel cims cfv cme caddc cmul cop cabs cif wceq fveq2 eleq1d cns cpv
      cgn eqid cnm cba cn0v elimnvu imsmetlem dedth syl5eqel ) BDEZABFGZHCUHUIH
      EUHBIJKLKZMZFGZHEBUJBUKNUIULHBUKFOPULUKQGZUKUKRGZUNSGZUKUAGZUKUBGZUKUCGZU
      QTUNTUOTUMTURTUPTULTBUDUEUFUG $.
      $( [2-Oct-2007] $) $( [4-Dec-2006] $)
  $}

  ${
    nvelbl.1 $e |- X = ( BaseSet ` U ) $.
    nvelbl.3 $e |- M = ( -v ` U ) $.
    nvelbl.6 $e |- N = ( norm ` U ) $.
    nvelbl.8 $e |- D = ( IndMet ` U ) $.
    $( Membership of a vector in a ball. $)
    nvelbl $p |- ( ( ( U e. NrmCVec /\ R e. RR+ ) /\ ( P e. X /\ A e. X ) ) ->
                   ( A e. ( P ( ball ` D ) R ) <-> ( N ` ( A M P ) ) < R ) ) $=
      ( wcel wa cfv co clt wbr cdm wceq cnv crp cbl imsba adantr eleq2d anbi12d
      biimpa cme imsmet eqid elbl3 sylanl1 syldan imsdval 3com23 adantlr breq1d
      wb 3expb bitrd ) EUAMZDUBMZNZCHMZAHMZNZNZACDBUCOPMZACBPZDQRZACFPGOZDQRVDV
      GCBSSZMZAVMMZNZVIVKUSZVDVGVPVDVEVNVFVOVDHVMCVBHVMTVCBEHILUDUEZUFVDHVMAVRU
      FUGUHVBBUIMVCVPVQBELUJABCDVMVMUKULUMUNVHVJVLDQVBVGVJVLTZVCVBVEVFVSVBVFVEV
      SACBEFGHIJKLUOUPUTUQURVA $.
      $( [10-Jan-2014] $) $( [27-Dec-2007] $)
  $}

  ${
    nvelbl2.1 $e |- X = ( BaseSet ` U ) $.
    nvelbl2.2 $e |- G = ( +v ` U ) $.
    nvelbl2.6 $e |- N = ( norm ` U ) $.
    nvelbl2.8 $e |- D = ( IndMet ` U ) $.
    $( Membership of an off-center vector in a ball. $)
    nvelbl2 $p |- ( ( ( U e. NrmCVec /\ R e. RR+ ) /\ ( P e. X /\ A e. X ) ) ->
                   ( ( P G A ) e. ( P ( ball ` D ) R ) <-> ( N ` A ) < R ) ) $=
      ( wcel wa co cfv clt wbr 3expb adantlr cnv crp cbl cnsb simprl nvgcl eqid
      wb jca nvelbl syldan wceq nvpncan2 fveq2d breq1d bitrd ) EUAMZDUBMZNZCHMZ
      AHMZNZNZCAFOZCDBUCPOMZVDCEUDPZOZGPZDQRZAGPZDQRUSVBUTVDHMZNVEVIUHVCUTVKUSU
      TVAUEUQVBVKURUQUTVAVKCAEFHIJUFSTUIVDBCDEVFGHIVFUGZKLUJUKVCVHVJDQVCVGAGUQV
      BVGAULZURUQUTVAVMCAEFVFHIJVLUMSTUNUOUP $.
      $( [10-Jan-2014] $) $( [27-Dec-2007] $)
  $}

  ${
    nvlmcl.1 $e |- X = ( BaseSet ` U ) $.
    nvlmcl.2 $e |- D = ( IndMet ` U ) $.
    nvlmcl.3 $e |- J = ( MetOpen ` D ) $.
    nvlmcl.4 $e |- P e. _V $.
    $( Closure of the limit of a converging vector sequence.  (Revised by Mario
       Carneiro, 5-May-2014.) $)
    nvlmcl $p |- ( ( U e. NrmCVec /\ F ( ~~>t ` J ) P ) -> P e. X ) $=
      ( cnv wcel clm cfv cvv syl adantr eqid wceq cdm wbr wa cuni imsmet opntop
      ctop cme a1i simpr lmcl syl3anc imsba uniopn2 eqtrd eleqtrrd ) CKLZDBEMNU
      AZUBZBEUCZFUREUFLZBOLZUQBUSLUPUTUQUPAUGLZUTACHUDZAEIUEPQVAURJUHUPUQUIBDEO
      USUSRUJUKUPFUSSUQUPFATTZUSACFGHULUPVBVDUSSVCAEVDVDRIUMPUNQUO $.
      $( [5-May-2014] $) $( [26-Dec-2007] $)

    $d k D $.  $d k F $.  $d k J $.  $d k P $.  $d k ph $.  $d k R $.
    $d k U $.
    nvlmle.5 $e |- N = ( norm ` U ) $.
    nvlmle.6 $e |- ( ph -> U e. NrmCVec ) $.
    nvlmle.7 $e |- ( ph -> F : NN --> X ) $.
    nvlmle.8 $e |- ( ph -> F ( ~~>t ` J ) P ) $.
    nvlmle.9 $e |- ( ph -> R e. RR ) $.
    nvlmle.10 $e |- ( ( ph /\ k e. NN ) -> ( N ` ( F ` k ) ) <_ R ) $.
    $( If the norm of each member of a converging sequence is less than or
       equal to a given amount, so is the norm of the convergence value.
       (Revised by Mario Carneiro, 5-May-2014.) $)
    nvlmle $p |- ( ph -> ( N ` P ) <_ R ) $=
      ( cfv cn0v cle cnv wcel wceq clm wbr nvlmcl syl2anc eqid nvnd cme cdm syl
      co imsmet imsba eleqtrd nvzcl metsym syl3anc eqtrd c1 cvv cn a1i cz 1z cv
      nnuz wa adantr wf ffvelrn sylan eqbrtrrd lmle eqbrtrd ) ACIUAZEUBUAZCBUPZ
      DUCAVTCWABUPZWBAEUDUEZCJUEZVTWCUFPAWDGCHUGUAUHWEPRBCEGHJKLMNUIUJZCBEIJWAK
      WAUKZOLULUJABUMUEZCBUNUNZUEWAWIUEZWCWBUFAWDWHPBELUQUOZACJWIWFAWDJWIUFZPBE
      JKLURUOZUSAWAJWIAWDWAJUEPEJWAKWGUTUOWMUSZCWABWIWIUKZVAVBVCABCWADFGHVDVEWI
      VFVKWOMWKCVEUEANVGVDVHUEAVIVGRWNSAFVJZVFUEZVLZWPGUAZIUAZWAWSBUPZDUCWRWTWS
      WABUPZXAWRWDWSJUEZWTXBUFAWDWQPVMAVFJGVNWQXCQVFJWPGVOVPZWSBEIJWAKWGOLULUJW
      RWHWSWIUEWJXBXAUFAWHWQWKVMWRWSJWIXDAWLWQWMVMUSAWJWQWNVMWSWABWIWOVAVBVCTVQ
      VRVS $.
      $( [5-May-2014] $) $( [25-Dec-2007] $)
  $}

  ${
    $d t u v w x y z $.
    cnims.6 $e |- U = <. <. + , x. >. , abs >. $.
    cnims.7 $e |- D = ( abs o. - ) $.
    $( The metric induced on the complex numbers. ~ cnmet proves that it is a
       metric.  (Contributed by Steve Rodriguez, 5-Dec-2006; revised by NM
       15-Jan-2008.) $)
    cnims $p |- D = ( IndMet ` U ) $=
      ( cabs cmin ccom cims cfv wcel wceq cnnv cnnvm cnnvnm imsval ax-mp eqtr4i
      cnv eqid ) AEFGZBHIZDBRJUATKBCLUABFEBCMBCNUASOPQ $.
      $( [15-Jan-2008] $) $( [28-Nov-2006] $)
  $}

  ${
    $d s t w x y z C $.  $d w x y z D $.  $d w x y z G $.  $d s t w x y z X $.
    $d w x y z J $.  $d s t U $.
    vacn.c $e |- C = ( IndMet ` U ) $.
    vacn.j $e |- J = ( MetOpen ` C ) $.
    vacn.g $e |- G = ( +v ` U ) $.
    ${
      vacn.x $e |- X = ( BaseSet ` U ) $.
      vacn.n $e |- N = ( norm ` U ) $.
      vacn.u $e |- U e. NrmCVec $.
      vacn.d $e |- D = ( s e. ( X X. X ) , t e. ( X X. X ) |->
                        sup ( { ( ( 1st ` s ) C ( 1st ` t ) ) ,
                                ( ( 2nd ` s ) C ( 2nd ` t ) ) } , RR , < ) ) $.
      $( Lemma for ~ vacn . $)
      vacnlem $p |- G e. ( ( J tX J ) Cn J ) $=
        ( co wcel cfv syl3anc vx vw vz vy ctx ccn cxp wf cv clt wbr wi wral crp
        wrex cnv nvgf ax-mp wa c2 cdiv rphalfcl adantl c2nd c1st cle cif imsbai
        wceq cme imsmet eqid metxpdval2 adantlr breq1d cr wb a1i xp2nd ad2antrr
        metcl xp1st rpre ad2antlr rehalfcl maxlt bitrd caddc lt2add syl22anc cc
        syl recnd 2halves breq2d nvmcl nvtri 1st2nd2 fveq2d df-ov syl6eqr nvgcl
        eqeltrd imsdval nvaddsub4 syl122anc oveq12d nvcom 3eqtr4d eqtrd 3brtr4d
        cnsb readdcl syl2anc lelttr mpand sylbid ralrimiva breq2 imbi1d ralbidv
        cop syld rcla4ev rgen2 metxp csup ltso supex dmmpt2 dmeqi dmxpid eqtr2i
        cdm cpr txmet metcn mp2an mpbir2an ) EFFUEQZFUFQRZHHUGZHEUHZUAUIZUBUIZC
        QZUCUIZUJUKZUUDESZUUEESZBQZUDUIZUJUKZULZUBUUBUMZUCUNUOZUDUNUMUAUUBUMZDU
        PRZUUCODEHMLUQURUUPUAUDUUBUNUUDUUBRZUULUNRZUSZUULUTVAQZUNRZUUFUVBUJUKZU
        UMULZUBUUBUMZUUPUUTUVCUUSUULVBVCUVAUVEUBUUBUVAUUEUUBRZUSZUVDUUDVDSZUUEV
        DSZBQZUVBUJUKUUDVESZUUEVESZBQZUVBUJUKUSZUUMUVHUVDUVKUVNVFUKUVNUVKVGZUVB
        UJUKZUVOUVHUUFUVPUVBUJUUSUVGUUFUVPVIUUTIABBCUUDUUEUVLUVIUVMUVJHHBDHMJOV
        HZUVRUURBVJRZOBDJVKURZUVTPUVLVLUVIVLUVMVLUVJVLVMVNVOUVHUVKVPRZUVNVPRZUV
        BVPRZUVQUVOVQUVHUVSUVIHRZUVJHRZUWAUVSUVHUVTVRZUUSUWDUUTUVGUUDHHVSVTZUVG
        UWEUVAUUEHHVSVCZUVIUVJBHUVRWATZUVHUVSUVLHRZUVMHRZUWBUWFUUSUWJUUTUVGUUDH
        HWBVTZUVGUWKUVAUUEHHWBVCZUVLUVMBHUVRWATZUVHUULVPRZUWCUUTUWOUUSUVGUULWCW
        DZUULWEWLZUVKUVNUVBWFTWGUVHUVOUVKUVNWHQZUVBUVBWHQZUJUKZUUMUVHUWAUWBUWCU
        WCUVOUWTULUWIUWNUWQUWQUVKUVNUVBUVBWIWJUVHUWTUWRUULUJUKZUUMUVHUWSUULUWRU
        JUVHUULWKRUWSUULVIUVHUULUWPWMUULWNWLWOUVHUUKUWRVFUKZUXAUUMUVHUVIUVJDXLS
        ZQZUVLUVMUXCQZEQZGSZUXDGSZUXEGSZWHQZUUKUWRVFUVHUURUXDHRZUXEHRZUXGUXJVFU
        KUURUVHOVRZUVHUURUWDUWEUXKUXMUWGUWHUVIUVJDUXCHMUXCVLZWPTZUVHUURUWJUWKUX
        LUXMUWLUWMUVLUVMDUXCHMUXNWPTZUXDUXEDEGHMLNWQTUVHUUKUUIUUJUXCQZGSZUXGUVH
        UURUUIHRZUUJHRZUUKUXRVIUXMUVHUUIUVLUVIEQZHUVHUUIUVLUVIYBZESUYAUVHUUDUYB
        EUUSUUDUYBVIUUTUVGUUDHHWRVTWSUVLUVIEWTXAZUVHUURUWJUWDUYAHRUXMUWLUWGUVLU
        VIDEHMLXBTXCZUVHUUJUVMUVJEQZHUVHUUJUVMUVJYBZESUYEUVHUUEUYFEUVGUUEUYFVIU
        VAUUEHHWRVCWSUVMUVJEWTXAZUVHUURUWKUWEUYEHRUXMUWMUWHUVMUVJDEHMLXBTXCZUUI
        UUJBDUXCGHMUXNNJXDTUVHUXQUXFGUVHUYAUYEUXCQZUXEUXDEQZUXQUXFUVHUURUWJUWDU
        WKUWEUYIUYJVIUXMUWLUWGUWMUWHUVLUVIUVMUVJDEUXCHMLUXNXEXFUVHUUIUYAUUJUYEU
        XCUYCUYGXGUVHUURUXKUXLUXFUYJVIUXMUXOUXPUXDUXEDEHMLXHTXIWSXJUVHUVKUXHUVN
        UXIWHUVHUURUWDUWEUVKUXHVIUXMUWGUWHUVIUVJBDUXCGHMUXNNJXDTUVHUURUWJUWKUVN
        UXIVIUXMUWLUWMUVLUVMBDUXCGHMUXNNJXDTXGXKUVHUUKVPRZUWRVPRZUWOUXBUXAUSUUM
        ULUVHUVSUXSUXTUYKUWFUYDUYHUUIUUJBHUVRWATUVHUWAUWBUYLUWIUWNUVKUVNXMXNUWP
        UUKUWRUULXOTXPXQYCXQXRUUOUVFUCUVBUNUUGUVBVIZUUNUVEUBUUBUYMUUHUVDUUMUUGU
        VBUUFUJXSXTYAYDXNYECVJRUVSUUAUUCUUQUSVQIABBCHHUVRUVRUVTUVTPYFUVTUAUDUCU
        BCBEYTFUUBHCYNZYNUUBUUBUGZYNUUBUYNUYOIAUUBUUBIUIZVESAUIZVESBQUYPVDSUYQV
        DSBQYOZVPUJYGCPVPUYRUJYHYIYJYKUUBYLYMFFCBBHHIAKKUVRUVRUVTUVTPYPUVRKYQYR
        YS $.
        $( [5-May-2014] $) $( [12-Jun-2009] $)
    $}

    $( Vector addition is jointly continuous in both arguments.  (Contributed
       by Jeffrey Hankins, 16-Jun-2009.)  (The proof was shortened by Mario
       Carneiro, 5-May-2014.) $)
    vacn $p |- ( U e. NrmCVec -> G e. ( ( J tX J ) Cn J ) ) $=
      ( vt vs wcel ctx co ccn cop cpv cfv cims copn syl5eq eqid caddc cmul cabs
      cnv cif wceq fveq2 fveq2d oveq12d eleq12d cba cxp cv c1st c2nd cpr cr clt
      csup cmpt2 cnm elimnvu vacnlem dedth ) BUDJZCDDKLZDMLZJVEBUAUBNUCNZUEZOPZ
      VIQPZRPZVLKLZVLMLZJBVHBVIUFZCVJVGVNVOCBOPVJGBVIOUGSVOVFVMDVLMVODVLDVLKVOD
      ARPVLFVOAVKRVOABQPVKEBVIQUGSUHSZVPUIVPUIUJHVKIHVIUKPZVQULZVRIUMZUNPHUMZUN
      PVKLVSUOPVTUOPVKLUPUQURUSUTZVIVJVLVIVAPZVQIVKTVLTVJTVQTWBTBVBWATVCVD $.
      $( [5-May-2014] $) $( [16-Jun-2009] $)
  $}

  ${
    $d d e x y C $.  $d d e x y D $.  $d d e x y J $.  $d e x y K $.
    $d d e x y N $.  $d e x y U $.
    nmcn.1 $e |- N = ( norm ` U ) $.
    nmcn.2 $e |- C = ( IndMet ` U ) $.
    nmcn.3 $e |- D = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    nmcn.j $e |- J = ( MetOpen ` C ) $.
    nmcn.k $e |- K = ( MetOpen ` D ) $.
    $( The norm of a normed complex vector space is a continuous function.
       (The proof was shortened by Mario Carneiro, 10-Jan-2014.) $)
    nmcn $p |- ( U e. NrmCVec -> N e. ( J Cn K ) ) $=
      ( vx vy vd wcel co cr cfv crp wa ve cnv ccn cdm wf cv clt wbr wi wral cba
      wrex eqid nvf imsba feq2d mpbid simprr cle eleq2d anbi12d nvcl ex anim12d
      w3a sylbird cme remet remetba mp3an1 syl6 3impib imsmet syl3an1 cmin cabs
      metcl cneg cns cpv nvabs wceq remetdval syl imsdval2 3brtr4d 3expib jca31
      c1 3expa rpre lelttr expdimp an32s syl2an ralrimdva breq2 ralbidv rcla4ev
      impr imbi1d syl2anc ralrimivva wb metcn sylancl mpbir2and ) CUBOZFDEUCPOZ
      AUDUDZQFUEZLUFZMUFZAPZNUFZUGUHZXLFRZXMFRZBPZUAUFZUGUHZUIZMXJUJZNSULZUASUJ
      LXJUJZXHCUKRZQFUEXKCFYFYFUMZGUNXHYFXJQFACYFYGHUOZUPUQXHYDLUAXJSXHXLXJOZXT
      SOZTTYJXNXTUGUHZYAUIZMXJUJZYDXHYIYJURXHYIYJYMXHYITZYJYLMXJYNXMXJOZTZYJYLY
      PXSQOZXNQOZTZXSXNUSUHZTZXTQOZYLYJXHYIYOUUAXHYIYOVEYQYRYTXHYIYOYQXHYIYOTZX
      QQOZXRQOZTZYQXHUUCXLYFOZXMYFOZTZUUFXHUUGYIUUHYOXHYFXJXLYHUTXHYFXJXMYHUTVA
      ZXHUUGUUDUUHUUEXHUUGUUDXLCFYFYGGVBVCXHUUHUUEXMCFYFYGGVBVCVDZVFBVGOZUUDUUE
      YQBIVHZXQXRBQBIVIZVQVJVKVLXHAVGOZYIYOYRACHVMZXLXMAXJXJUMZVQVNXHYIYOYTXHUU
      CUUIYTUUJXHUUGUUHYTXHUUGUUHVEZXQXRVOPVPRZXLWIVRXMCVSRZPCVTRZPFRXSXNUSXLXM
      UUTCUVAFYFYGUVAUMZUUTUMZGWAUURUUFXSUUSWBXHUUGUUHUUFUUKVLXQXRBIWCWDXLXMAUU
      TCUVAFYFYGUVBUVCGHWEWFWGVFVLWHWJXTWKYSUUBYTYLYSUUBTYTYKYAYQYRUUBYTYKTYAUI
      XSXNXTWLWJWMWNWOVCWPWTYCYMNXTSXOXTWBZYBYLMXJUVDXPYKYAXOXTXNUGWQXAWRWSXBXC
      XHUUOUULXIXKYETXDUUPUUMLUANMABFDEXJQUUQJUUNKXEXFXG $.
      $( [10-Jan-2014] $) $( [16-May-2007] $)
  $}

  ${
    nmcnc.1 $e |- N = ( norm ` U ) $.
    nmcnc.2 $e |- C = ( IndMet ` U ) $.
    nmcnc.3 $e |- D = ( abs o. - ) $.
    nmcnc.j $e |- J = ( MetOpen ` C ) $.
    nmcnc.k $e |- K = ( MetOpen ` D ) $.
    $( The norm of a normed complex vector space is a continuous function to
       ` CC ` .  (For ` RR ` , see ~ nmcn .) $)
    nmcnc $p |- ( U e. NrmCVec -> N e. ( J Cn K ) ) $=
      ( cnv wcel cme cabs cr ccn co eqid a1i cmin ccom cxp cres w3a wss wa copn
      cfv imsmet remet cnmet 3jca resss sseqtr4i jctir nmcn metcnss sseld sylc
      ) CLMZANMZOUAUBZPPUCZUDZNMZBNMZUEZVEBUFZUGZFDVEUHUIZQRZMFDEQRZMVAVHVIVAVB
      VFVGACHUJVFVAVEVESZUKTVGVABIULTUMVEVCBVCVDUNIUOUPAVECDVKFGHVNJVKSZUQVJVLV
      MFAVEBDVKEJVOKURUSUT $.
      $( [12-Aug-2007] $)
  $}

  ${
    abscn.1 $e |- C = ( abs o. - ) $.
    abscn.2 $e |- R = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    abscn.3 $e |- J = ( MetOpen ` C ) $.
    abscn.4 $e |- K = ( MetOpen ` R ) $.
    $( The absolute value function on complex numbers is continuous.  (The
       proof was shortened by Mario Carneiro, 10-Jan-2014.) $)
    abscn $p |- abs e. ( J Cn K ) $=
      ( caddc cmul cop cabs cnv wcel ccn co eqid cnnv cnnvnm cnims nmcn ax-mp )
      IJKLKZMNLCDOPNUCUCQZRABUCCDLUCUDSAUCUDETFGHUAUB $.
      $( [10-Jan-2014] $) $( [22-Aug-2007] $)
  $}

  ${
    $d s t w x y z C $.  $d s t x y D $.  $d w x y z J $.  $d s t w x y z X $.
    $d w x y z K $.  $d w x y z R $.  $d w x y z S $.  $d s t x y U $.
    $d w z T $.
    smcn.c $e |- C = ( IndMet ` U ) $.
    smcn.j $e |- J = ( MetOpen ` C ) $.
    smcn.s $e |- S = ( .s ` U ) $.
    smcn.d $e |- D = ( abs o. - ) $.
    smcn.k $e |- K = ( MetOpen ` D ) $.
    ${
      smcn.x $e |- X = ( BaseSet ` U ) $.
      smcn.n $e |- N = ( norm ` U ) $.
      smcn.u $e |- U e. NrmCVec $.
      smcn.r $e |- R = ( s e. ( CC X. X ) , t e. ( CC X. X ) |->
                        sup ( { ( ( 1st ` s ) D ( 1st ` t ) ) ,
                                ( ( 2nd ` s ) C ( 2nd ` t ) ) } , RR , < ) ) $.
      smcn.t $e |- T = ( 1 / ( 1 + ( ( ( ( N ` ( 2nd ` x ) ) +
                         ( abs ` ( 1st ` x ) ) ) + 1 ) / y ) ) ) $.
      $( Lemma for ~ smcn . $)
      smcnlem $p |- S e. ( ( K tX J ) Cn J ) $=
        ( vw vz ctx co ccn wcel cc cxp wf cv clt wbr cfv wral crp wrex cnv nvsf
        wi ax-mp wa c1 c2nd c1st cabs caddc cdiv 1rp cr cc0 xp2nd sylancr xp1st
        nvcl abscl syl readdcl syl2anc peano2re 0re a1i cle nvge0 absge0 addge0
        syl22anc ltp1 lelttrd elrp sylanbrc rpdivcl cop 1st2nd2 ad2antrr fveq2d
        wceq df-ov syl6eqr ad2antrl oveq12d cme nvscl syl3anc metcl rpre adantr
        syl13anc cmul remulcl cmin subcl eqtrd eqid eqbrtrrd wb imsdval eqeltrd
        jca mpbid ltle mpd eqbrtrd recnd breqtrd 1re sylib sylancl letrd mp2and
        cneg ax-1cn oveq2d oveq1d nvs 3eqtr2d cdm sylan rpaddcl syl5eqel imsmet
        rpreccl imsbai ad2antlr mettri cnsb abssub cnmetdval cnmetba metxpdval2
        cnmet ad2ant2r simprr nvmcl maxlt simprd lemul1a syl31anc mulcom pncan3
        cif abstri ltaddrp recgt1 syl5eqbr leadd2 simpld lemul12a le2add negcli
        cpv mulcl nvdir mulm1 negsub nvsass 3eqtr3d nvmdi addass adddir 3brtr4d
        imsdval2 wne rpcn rpne0 divrec oveq2i syl6reqr addcom ltdiv23 ralrimiva
        simplr expr breq2 imbi1d ralbidv rgen2 metxp cpr csup ltso supex dmmpt2
        rcla4ev dmeqi dmxpid eqtr2i txmet metcn mp2an mpbir2an ) GKJUGUHZJUIUHU
        JZUKMULZMGUMZAUNZUEUNZFUHZUFUNZUOUPZUXSGUQZUXTGUQZDUHZBUNZUOUPZVCZUEUXQ
        URZUFUSUTZBUSURAUXQURZIVAUJZUXRUBGIMTQVBVDUYKABUXQUSUXSUXQUJZUYGUSUJZVE
        ZHUSUJZUYAHUOUPZUYHVCZUEUXQURZUYKUYPHVFVFUXSVGUQZLUQZUXSVHUQZVIUQZVJUHZ
        VFVJUHZUYGVKUHZVJUHZVKUHZUSUDUYPVUHUSUJZVUIUSUJUYPVFUSUJVUGUSUJZVUJVLUY
        NVUFUSUJZUYOVUKUYNVUFVMUJZVNVUFUOUPVULUYNVUEVMUJZVUMUYNVUBVMUJZVUDVMUJZ
        VUNUYNUYMVUAMUJZVUOUBUXSUKMVOZVUAILMTUAVRVPZUYNVUCUKUJZVUPUXSUKMVQZVUCV
        SVTZVUBVUDWAZWBZVUEWCZVTZUYNVNVUEVUFVNVMUJUYNWDWEVVDVVFUYNVUOVUPVNVUBWF
        UPZVNVUDWFUPZVNVUEWFUPVUSVVBUYNUYMVUQVVGUBVURVUAILMTUAWGZVPUYNVUTVVHVVA
        VUCWHVTVUBVUDWIWJUYNVUNVUEVUFUOUPVVDVUEWKVTWLVUFWMWNVUFUYGWOUUAZVFVUGUU
        BVPZVUHUUEVTUUCZUYPUYSUEUXQUYPUXTUXQUJZUYRUYHUYPVVMUYRVEZVEZUYFVUCVUAGU
        HZUXTVHUQZUXTVGUQZGUHZDUHZUYGUOVVOUYDVVPUYEVVSDVVOUYDVUCVUAWPZGUQVVPVVO
        UXSVWAGUYNUXSVWAWTUYOVVNUXSUKMWQWRWSVUCVUAGXAXBVVOUYEVVQVVRWPZGUQVVSVVO
        UXTVWBGVVMUXTVWBWTUYPUYRUXTUKMWQXCWSVVQVVRGXAXBXDVVOVVTVVPVVQVUAGUHZDUH
        ZVWCVVSDUHZVJUHZUYGVVODXEUJZVVPMUJZVVSMUJZVVTVMUJVWGVVOUYMVWGUBDIOUUDVD
        ZWEZVVOUYMVUTVUQVWHUYMVVOUBWEZUYNVUTUYOVVNVVAWRZUYNVUQUYOVVNVURWRZVUCVU
        AGIMTQXFXGZVVOUYMVVQUKUJZVVRMUJZVWIVWLVVMVWPUYPUYRUXTUKMVQXCZVVMVWQUYPU
        YRUXTUKMVOXCZVVQVVRGIMTQXFXGZVVPVVSDMDIMTOUBUUFZXHXGVVOVWDVMUJZVWEVMUJZ
        VWFVMUJVVOVWGVWHVWCMUJZVXBVWKVWOVVOUYMVWPVUQVXDVWLVWRVWNVVQVUAGIMTQXFXG
        ZVVPVWCDMVXAXHXGVVOVWGVXDVWIVXCVWKVXEVWTVWCVVSDMVXAXHXGVWDVWEWAWBZUYOUY
        GVMUJZUYNVVNUYGXIUUGZVVOVWGVWHVWIVXDVVTVWFWFUPVWKVWOVWTVXEVVPVVSVWCDMVX
        AUUHXKVVOVWFVUFHXLUHZUYGVXFVVOVUMHVMUJZVXIVMUJVVOVUNVUMVVOVUOVUPVUNUYNV
        UOUYOVVNVUSWRZUYNVUPUYOVVNVVBWRZVVCWBVVEVTZVVOUYQVXJUYPUYQVVNVVLXJHXIVT
        ZVUFHXMWBVXHVVOVUCVVQXNUHZVIUQZVUBXLUHZVVQVIUQZVUAVVRIUUIUQZUHZLUQZXLUH
        ZVJUHZVUBHXLUHZVUDVFVJUHZHXLUHZVJUHZVWFVXIWFVVOVXQVYDWFUPZVYBVYFWFUPZVY
        CVYGWFUPZVVOVXQHVUBXLUHZVYDWFVVOVXPVMUJZVXJVUOVVGVEVXPHWFUPVXQVYKWFUPVV
        OVXOUKUJZVYLVVOVUTVWPVYMVWMVWRVUCVVQXOWBZVXOVSVTZVXNVVOVUOVVGVXKVVOUYMV
        UQVVGUBVWNVVIVPYBVVOVXPVVQVUCXNUHZVIUQZHWFVVOVUTVWPVXPVYQWTVWMVWRVUCVVQ
        UUJWBZVVOVYQHUOUPZVYQHWFUPZVVOVUCVVQEUHZVYQHUOVVOWUAVXPVYQVVOVUTVWPWUAV
        XPWTVWMVWRVUCVVQERUUKWBZVYRXPVVOVUAVVRDUHZHUOUPZWUAHUOUPZVVOWUCWUAWFUPW
        UAWUCUVDZHUOUPZWUDWUEVEZVVOUYAWUFHUOUYNVVMUYAWUFWTUYOUYRNCEDFUXSUXTVUCV
        UAVVQVVRUKMERUULZVXAERUUNZVWJUCVUCXQVUAXQVVQXQVVRXQUUMUUOUYPVVMUYRUUPXR
        VVOWUCVMUJWUAVMUJVXJWUGWUHXSVVOWUCVYAVMVVOUYMVUQVWQWUCVYAWTVWLVWNVWSVUA
        VVRDIVXSLMTVXSXQZUAOXTXGZVVOUYMVXTMUJZVYAVMUJZUBVVOUYMVUQVWQWUMVWLVWNVW
        SVUAVVRIVXSMTWUKUUQXGZVXTILMTUAVRVPZYAVVOWUAVXPVMWUBVYOYAVXNWUCWUAHUURX
        GYCZUUSXRVVOVYQVMUJZVXJVYSVYTVCVVOVYPUKUJZWURVVOVWPVUTWUSVWRVWMVVQVUCXO
        WBZVYPVSVTZVXNVYQHYDWBYEZYFVXPHVUBUUTUVAVVOHUKUJZVUBUKUJZVYKVYDWTVVOHVX
        NYGZVVOVUBVXKYGZHVUBUVBWBYHVVOVXRVYEWFUPZVYAHWFUPZVYIVVOVXRVUDVYQVJUHZV
        YEVVOVWPVXRVMUJZVWRVVQVSVTZVVOVUPWURWVIVMUJVXLWVAVUDVYQWAWBVVOVUPVYEVMU
        JZVXLVUDWCVTZVVOVUCVYPVJUHZVIUQZVXRWVIWFVVOWVNVVQVIVVOVUTVWPWVNVVQWTVWM
        VWRVUCVVQUVCWBWSVVOVUTWUSWVOWVIWFUPVWMWUTVUCVYPUVEWBXRVVOVYQVFWFUPZWVIV
        YEWFUPZVVOVYQHVFWVAVXNVFVMUJZVVOYIWEZWVBVVOHVFUOUPZHVFWFUPZVVOHVUIVFUOU
        DVVOVFVUHUOUPZVUIVFUOUPZVVOWVRVUKWWBYIUYPVUKVVNVVJXJZVFVUGUVFVPVVOVUHVM
        UJVNVUHUOUPVEZWWBWWCXSVVOVUJWWEUYPVUJVVNVVKXJZVUHWMYJZVUHUVGVTYCUVHVVOV
        XJWVRWVTWWAVCVXNYIHVFYDYKYEYLVVOWURWVRVUPWVPWVQXSWVAWVSVXLVYQVFVUDUVIXG
        YCYLVVOVYAHUOUPZWVHVVOWUCVYAHUOWULVVOWUDWUEWUQUVJXRVVOWUNVXJWWHWVHVCWUP
        VXNVYAHYDWBYEVVOWVJVNVXRWFUPZVEWVLWUNVNVYAWFUPZVEVXJWVGWVHVEVYIVCVVOWVJ
        WWIWVKVVOVWPWWIVWRVVQWHVTYBWVMVVOWUNWWJWUPVVOUYMWUMWWJUBWUOVXTILMTUAWGV
        PYBVXNVXRVYEVYAHUVKWJYMVVOVXQVMUJZVYBVMUJZVYDVMUJZVYFVMUJZVYHVYIVEVYJVC
        VVOVYLVUOWWKVYOVXKVXPVUBXMWBVVOWVJWUNWWLWVKWUPVXRVYAXMWBVVOVUOVXJWWMVXK
        VXNVUBHXMWBVVOWVLVXJWWNWVMVXNVYEHXMWBVXQVYBVYDVYFUVLWJYMVVOVWDVXQVWEVYB
        VJVVOVWDVVPVFYNZVWCGUHZIUVNUQZUHZLUQZVXOVUAGUHZLUQZVXQVVOUYMVWHVXDVWDWW
        SWTVWLVWOVXEVVPVWCDGIWWQLMTWWQXQZQUAOUWEXGVVOWWTWWRLVVOVUCWWOVVQXLUHZVJ
        UHZVUAGUHZVVPWXCVUAGUHZWWQUHZWWTWWRVVOUYMVUTWXCUKUJZVUQWXEWXGWTVWLVWMVV
        OWWOUKUJZVWPWXHVFYOUVMZVWRWWOVVQUVOVPVWNVUCWXCVUAGIWWQMTWXBQUVPXKVVOWXD
        VXOVUAGVVOWXDVUCVVQYNZVJUHZVXOVVOWXCWXKVUCVJVVOVWPWXCWXKWTVWRVVQUVQVTYP
        VVOVUTVWPWXLVXOWTVWMVWRVUCVVQUVRWBXPYQVVOWXFWWPVVPWWQVVOUYMWXIVWPVUQWXF
        WWPWTVWLWXIVVOWXJWEVWRVWNWWOVVQVUAGIMTQUVSXKYPUVTWSVVOUYMVYMVUQWXAVXQWT
        VWLVYNVWNVXOVUAGILMTQUAYRXGYSVVOVWEVWCVVSVXSUHZLUQZVVQVXTGUHZLUQZVYBVVO
        UYMVXDVWIVWEWXNWTVWLVXEVWTVWCVVSDIVXSLMTWUKUAOXTXGVVOWXOWXMLVVOUYMVWPVU
        QVWQWXOWXMWTVWLVWRVWNVWSVVQVUAVVRGIVXSMTWUKQUWAXKWSVVOUYMVWPWUMWXPVYBWT
        VWLVWRWUOVVQVXTGILMTQUAYRXGYSXDVVOVXIVUBVYEVJUHZHXLUHZVYGVVOVUFWXQHXLVV
        OWVDVUDUKUJVFUKUJZVUFWXQWTWVFVVOVUDVXLYGWXSVVOYOWEVUBVUDVFUWBXGYQVVOWVD
        VYEUKUJWVCWXRVYGWTWVFVVOVYEWVMYGWVEVUBVYEHUWCXGXPUWDVVOVXIVUFVUHVKUHZUY
        GUOVVOWXTVUFVUIXLUHZVXIVVOVUFUKUJVUHUKUJZVUHVNUWFZWXTWYAWTVVOVUFVXMYGVV
        OVUJWYBWWFVUHUWGVTVVOVUJWYCWWFVUHUWHVTVUFVUHUWIXGHVUIVUFXLUDUWJUWKVVOVU
        GVUHUOUPZWXTUYGUOUPZVVOVUGVUGVFVJUHZVUHUOVVOVUGVMUJZVUGWYFUOUPVVOVUKWYG
        WWDVUGXIVTZVUGWKVTVVOVUGUKUJWXSWYFVUHWTVVOVUGWYHYGYOVUGVFUWLYKYHVVOVUMV
        XGVNUYGUOUPVEZWWEWYDWYEXSVXMVVOUYOWYIUYNUYOVVNUWOUYGWMYJWWGVUFUYGVUHUWM
        XGYCYFWLWLYFUWPUWNUYJUYTUFHUSUYBHWTZUYIUYSUEUXQWYJUYCUYRUYHUYBHUYAUOUWQ
        UWRUWSUXGWBUWTFXEUJVWGUXPUXRUYLVEXSNCEDFUKMWUIVXAWUJVWJUCUXAVWJABUFUEFD
        GUXOJUXQMFYTZYTUXQUXQULZYTUXQWYKWYLNCUXQUXQNUNZVHUQCUNZVHUQEUHWYMVGUQWY
        NVGUQDUHUXBZVMUOUXCFUCVMWYOUOUXDUXEUXFUXHUXQUXIUXJKJFEDUKMNCSPWUIVXAWUJ
        VWJUCUXKVXAPUXLUXMUXN $.
        $( [5-May-2014] $)
    $}

    $( Scalar multiplication is jointly continuous in both arguments.
       (Contributed by Mario Carneiro, 5-May-2014.) $)
    smcn $p |- ( U e. NrmCVec -> S e. ( ( K tX J ) Cn J ) ) $=
      ( vt vs wcel ctx co caddc cfv cv eqid vx vy cnv ccn cmul cop cabs cif cns
      cims copn wceq fveq2 syl5eq fveq2d oveq2d oveq12d eleq12d cc cba cxp c1st
      c2nd cpr cr clt csup cmpt2 c1 cnm cdiv elimnvu smcnlem dedth ) DUCNZCFEOP
      ZEUDPZNVODQUEUFUGUFZUHZUIRZFVSUJRZUKRZOPZWBUDPZNDVRDVSULZCVTVQWDWECDUIRVT
      IDVSUIUMUNWEVPWCEWBUDWEEWBFOWEEAUKRWBHWEAWAUKWEADUJRWAGDVSUJUMUNUOUNZUPWF
      UQURUAUBLWABMLUSVSUTRZVAZWHMSZVBRLSZVBRBPWIVCRWJVCRWAPVDVEVFVGVHZVTVIVIUA
      SZVCRVSVJRZRWLVBRUGRQPVIQPUBSVKPQPVKPZVSWBFWMWGMWATWBTVTTJKWGTWMTDVLWKTWN
      TVMVN $.
      $( [5-May-2014] $) $( [16-Jun-2009] $)
  $}

  ${
    $d x y J $.  $d x y M $.  $d x y U $.
    vmcn.c $e |- C = ( IndMet ` U ) $.
    vmcn.j $e |- J = ( MetOpen ` C ) $.
    vmcn.m $e |- M = ( -v ` U ) $.
    $( Vector subtraction is jointly continuous in both arguments.
       (Contributed by Mario Carneiro, 6-May-2014.) $)
    vmcn $p |- ( U e. NrmCVec -> M e. ( ( J tX J ) Cn J ) ) $=
      ( vx vy wcel cuni cv c1 cfv co cmpt2 wceq eqid syl cc cnv cns cpv ctx ccn
      cneg cba cxp wfn wf nvmf ffn fnov2 sylib nvmval mpt2eq3dva cdm cme imsmet
      imsba uniopn2 mpt2eq12 syl2anc 3eqtrd ctop opntop cnmpt1st cabs cmin ccom
      eqtrd copn cnmet a1i cnmetba ax-1cn negcli cnmpt2c cnmpt2nd smcn cnmpt22f
      ax-mp vacn eqeltrd ) BUAJZDHICKZWFHLZMUFZILZBUBNZOZBUCNZOZPZCCUDOCUEOWEDH
      IBUGNZWOWGWIDOZPZHIWOWOWMPZWNWEDWOWOUHZUIZDWQQWEWSWODUJWTBDWOWORZGUKWSWOD
      ULSHIWOWODUMUNWEHIWOWOWPWMWGWIWJBWLDWOXAWLRZWJRZGUOUPWEWOWFQZXDWRWNQWEWOA
      UQUQZWFABWOXAEUTWEAURJZXEWFQABEUSZACXEXERFVASVKZXHHIWOWOWFWFWMVBVCVDWEHIW
      GWKWLCCCCCWFWFWFWFWFRZXIWEXFCVEJXGACFVFSZXJXJXJWEHICCWFWFXIXIXJXJVGWEHIWH
      WIWJCCVHVIVJZVLNZCCWFWFWFTXIXIXJXJXLVEJZWEXKURJZXMXKXKRZVMZXKXLXLRZVFWBVN
      ZXJWEHIWHCCXLWFWFTXIXIXJXJXRXNTXLKQXPXKXLTXKXOVOXQVAWBZWHTJWEMVPVQVNVRWEH
      ICCWFWFXIXIXJXJVSXSXIXJAXKWJBCXLEFXCXOXQVTWAXIXIXJABWLCEFXBWCWAWD $.
      $( [6-May-2014] $)
  $}


