$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Examples of normed complex vector spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    cnnv.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The set of complex numbers is a normed complex vector space.  The vector
       operation is ` + ` , the scalar product is ` x. ` , and the norm
       function is ` abs ` .  (Contributed by Steve Rodriguez, 3-Dec-2006.) $)
    cnnv $p |- U e. NrmCVec $=
      ( vx vy cmul caddc cabs cc0 cablo wcel cnaddablo ablogrpo ax-mp ax-addopr
      cc cgr cxp fdmi cv wceq grporn cnid cnvc abs00 biimpa absmul abstri isnvi
      absf cfv ) CDEAFGOHFOFIJFPJKFLMOOQOFNRUAUBUCUICSZOJUKGUJHTUKHTUKUDUEDSZUK
      UFUKULUGBUH $.
      $( [12-Jan-2008] $)
  $}

  ${
    cnnvg.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The vector addition (group) operation of the normed complex vector space
       of complex numbers. $)
    cnnvg $p |- + = ( +v ` U ) $=
      ( cpv cfv c1st caddc cmul cop eqid vafval cabs fveq2i op1st eqtri 3eqtrri
      opex addex ) ACDZAEDZEDFGHZEDFARRIJSTESTKHZEDTAUAEBLTKFGPMNLFGQMO $.
      $( [12-Jan-2008] $)
  $}

  ${
    cnnvba.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The base set of the normed complex vector space of complex numbers. $)
    cnnvba $p |- CC = ( BaseSet ` U ) $=
      ( caddc crn cpv cfv cc cba cnnvg rneqi cablo cgr cnaddablo ablogrpo ax-mp
      wcel cxp ax-addopr fdmi eqid grporn bafval 3eqtr4i ) CDAEFZDGAHFZCUDABIJC
      GCKPCLPMCNOGGQGCRSUAAUDUEUETUDTUBUC $.
      $( [24-Jul-2008] $) $( [7-Nov-2007] $)
  $}

  $( Derive the associative law for complex number addition ~ addass to
     demonstrate the use of ~ cnnv , ~ cnnvg , and ~ cnnvba . $)
  cnnvdemo $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) -> ( ( A + B ) + C ) =
      ( A + ( B + C ) ) ) $=
    ( caddc cmul cop cabs cnv wcel cc co wceq eqid cnnv cnnvba cnnvg nvass mpan
    w3a ) DEFGFZHIAJIBJICJISABDKCDKABCDKDKLTTMZNABCTDJTUAOTUAPQR $.
    $( [12-Jan-2008] $)

  ${
    cnnvs.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The scalar product operation of the normed complex vector space of
       complex numbers. $)
    cnnvs $p |- x. = ( .s ` U ) $=
      ( cns c1st c2nd caddc cmul eqid smfval cabs fveq2i opex op1st eqtri addex
      cfv cop mulex op2nd 3eqtrri ) ACPZADPZEPFGQZEPGUAAUAHIUBUCEUBUCJQZDPUCAUD
      DBKUCJFGLMNKFGORST $.
      $( [12-Jan-2008] $)
  $}

  ${
    cnnvnm.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The norm operation of the normed complex vector space of complex
       numbers. $)
    cnnvnm $p |- abs = ( norm ` U ) $=
      ( cnm cfv c2nd caddc cmul cop cabs eqid nmfval fveq2i opex cc cr cvv wcel
      wf absf cnex fex mp2an op2nd 3eqtrri ) ACDZAEDFGHZIHZEDIAUEUEJKAUGEBLUFIF
      GMNOIRNPQIPQSTNOPIUAUBUCUD $.
      $( [12-Jan-2008] $)
  $}

  ${
    $d x y U $.
    cnnvm.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The vector subtraction operation of the normed complex vector space of
       complex numbers. $)
    cnnvm $p |- - = ( -v ` U ) $=
      ( vx vy cc cv cmin co cmpt2 c1 cneg cmul caddc cnsb wcel wceq mulm1 ax-mp
      cfv wa adantl oveq2d negsub eqtr2d mpt2eq3ia cxp wfn wf subopr fnov2 mpbi
      ffn cnv cnnv cnnvba cnnvg cnnvs eqid nvmfval 3eqtr4i ) CDEECFZDFZGHZIZCDE
      EVAJKVBLHZMHZIZGANSZCDEEVCVFVAEOZVBEOZTZVFVAVBKZMHVCVKVEVLVAMVJVEVLPVIVBQ
      UAUBVAVBUCUDUEGEEUFZUGZGVDPVMEGUHVNUIVMEGULRCDEEGUJUKAUMOVHVGPABUNCDLAMVH
      EABUOABUPABUQVHURUSRUT $.
      $( [23-Dec-2013] $) $( [12-Jan-2008] $)
  $}

  ${
    elimnv.1 $e |- X = ( BaseSet ` U ) $.
    elimnv.5 $e |- Z = ( 0vec ` U ) $.
    elimnv.9 $e |- U e. NrmCVec $.
    $( Hypothesis elimination lemma for normed complex vector spaces to assist
       weak deduction theorem. $)
    elimnv $p |- if ( A e. X , A , Z ) e. X $=
      ( cnv wcel nvzcl ax-mp elimel ) ADCBHIDCIGBCDEFJKL $.
      $( [16-May-2007] $)
  $}

  $( Hypothesis elimination lemma for normed complex vector spaces to assist
     weak deduction theorem. $)
  elimnvu $p |- if ( U e. NrmCVec , U , <. <. + , x. >. , abs >. )
                 e. NrmCVec $=
    ( caddc cmul cop cabs cnv eqid cnnv elimel ) ABCDEDZFJJGHI $.
    $( [16-May-2007] $)

