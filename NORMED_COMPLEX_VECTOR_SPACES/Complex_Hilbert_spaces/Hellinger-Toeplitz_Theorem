$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Hellinger-Toeplitz Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w y F $.  $d w x y z K $.  $d w x y z N $.  $d w z P $.  $d w x y z W $.
    $d w x y z ph $.  $d u v w x y z T $.  $d u v w x y z U $.  $d w x y z X $.
    htth.1 $e |- X = ( BaseSet ` U ) $.
    htth.2 $e |- P = ( .i ` U ) $.
    htth.3 $e |- L = ( U LnOp U ) $.
    htth.4 $e |- B = ( U BLnOp U ) $.
    ${
      htthlem.5 $e |- N = ( norm ` U ) $.
      htthlem.6 $e |- U e. CHil $.
      htthlem.7 $e |- W = <. <. + , x. >. , abs >. $.
      htthlem.8 $e |- ( ph -> T e. L ) $.
      htthlem.9 $e |- ( ph -> A. x e. X A. y e. X
                        ( x P ( T ` y ) ) = ( ( T ` x ) P y ) ) $.
      htthlem.10 $e |- F = ( z e. X |-> ( w e. X |-> ( w P ( T ` z ) ) ) ) $.
      htthlem.11 $e |- K = ( F " { z e. X | ( N ` z ) <_ 1 } ) $.
      $( Lemma for ~ htth .  The collection ` K ` , which consists of functions
         ` F ( z ) ( w ) = <. w | T ( z ) >. = <. T ( w ) | z >. ` for each
         ` z ` in the unit ball, is a collection of bounded linear functions by
         ~ ipblnfi , so by the Uniform Boundedness theorem ~ ubth , there is a
         uniform bound ` y ` on ` || F ( x ) || ` for all ` x ` in the unit
         ball.  Then ` | T ( x ) | ^ 2 = <. T ( x ) | T ( x ) >. = F ( x ) ( `
         ` T ( x ) ) <_ y | T ( x ) | ` , so ` | T ( x ) | <_ y ` and ` T ` is
         bounded.  (The proof was shortened by Mario Carneiro, 12-Jan-2014.) $)
      htthlem $p |- ( ph -> T e. B ) $=
        ( wcel cnmo co cfv cpnf clt wbr cv cle wral cr wrex cabs wa cnv cbn chl
        hlbn ax-mp bnnv wf lnof mp3an12 ffvelrn sylan nvcl sylancr wceq c1 crab
        syl wfun cima cblo cmpt cphl hlph eqid ralrimiva fmpt sylib ffun adantr
        ipblnfi id syl6eleq fvelima syl2an ex fveq2 breq1d elrab oveq2d cba cvv
        mpteq2dv fvex eqeltri mptex fvmpt fveq1d oveq1 ovex sylan9eqr ad2ant2lr
        wi ra42 adantrr eqtrd fveq2d cmul cc ax-mulrcl syl2anc cc0 a1i syl31anc
        jca simprr recnd letrd syl5ibcom rexlimdva syld wss wb ad2ant2r mpd 0re
        mpbid adantl 3expia expr cxr imp anassrs simpl ipcl abscl mpan ad2antrl
        mp3an1 sii 1re nvge0 lemul2a mulid1 breqtrd eqbrtrd sylan2b fveq1 breq2
        ralrimiv ralbidv rcla4ev crn imassrn eqsstri frn syl5ss cnnv ubth simpr
        cnnvnm sylibr cdm eleq2d biimpar funfvima syldan syl6eleqr rcla4v leloe
        wo cnnvba nmblore simplr c2 cexp ipidsq resqcl sqge0 absid sqval 3eqtrd
        fdm nmblolbi eqbrtrrd lemul1a lemul1 biimprd expdimp syl21anc mpid blof
        w3a nmoge0 breq1 jaod com23 ralrimdva nmoubi ltpnf nmoxr pnfxr xrlelttr
        rexr mp3an3 mpan2d 3syld isblo mp2an sylanbrc ) AHLUGZHIIUHUIZUJZUKULUM
        ZHFUGZUCAEUNZINUHUIZUJZCUNZUOUMZEKUPZCUQURZUYCABUNZUYEUJZUSUJZDUNZUOUMZ
        EKUPZDUQURZBOUPZUYKAUYRBOAUYLOUGZUTZUYLHUJZMUJZUQUGZUYNVUCUOUMZEKUPZUYR
        VUAIVAUGZVUBOUGZVUDIVBUGZVUGIVCUGZVUIUAIVDVEZIVFVEZAOOHVGZUYTVUHAUXTVUM
        UCVUGVUGUXTVUMVULVULHILIOOPPRVHVIVQZOOUYLHVJVKZVUBIMOPTVLVMZVUAVUEEKVUA
        UYEKUGZUYHJUJZUYEVNZCUYOMUJZVOUOUMZDOVPZURZVUEVUAVUQVVCVUAJVRZUYEJVVBVS
        ZUGVVCVUQAVVDUYTAOINVTUIZJVGZVVDAEOUYEUYOHUJZGUIZWAZVVFUGZDOUPVVGAVVKDO
        AUYOOUGZUTVVHOUGZVVKAVUMVVLVVMVUNOOUYOHVJVKEVVHVVFNGIVVJOPQVUJIWBUGUAIW
        CVEZUBVVFWDZVVJWDWJVQWEDOVVFVVJJUEWFWGZOVVFJWHVQZWIVUQUYEKVVEVUQWKUFWLC
        UYEVVBJWMWNWOVUAVUSVUECVVBVUAUYHVVBUGZUTUYLVURUJZUSUJZVUCUOUMZVUSVUEVVR
        VUAUYHOUGZUYHMUJZVOUOUMZUTZVWAVVAVWDDUYHOUYOUYHVNZVUTVWCVOUOUYOUYHMWPWQ
        WRVUAVWEUTZVVTVUBUYHGUIZUSUJZVUCUOVWGVVSVWHUSVWGVVSUYLUYHHUJZGUIZVWHUYT
        VWBVVSVWKVNAVWDVWBUYTVVSUYLEOUYEVWJGUIZWAZUJVWKVWBUYLVURVWMDUYHVVJVWMOJ
        VWFEOVVIVWLVWFVVHVWJUYEGUYOUYHHWPWSXBUEEOVWLOIWTUJXAPIWTXCXDZXEXFXGEUYL
        VWLVWKOVWMUYEUYLVWJGXHVWMWDUYLVWJGXIXFXJXKVUAVWBVWKVWHVNZVWDAUYTVWBVWOA
        UYTVWBUTZVWOAVWOCOUPBOUPVWPVWOXLUDVWOBCOOXMVQUUAUUBXNXOXPVWGVWIVUCVWCXQ
        UIZVUCVWGVWHXRUGZVWIUQUGVUAVUHVWBVWRVWEVUOVWBVWDUUCZVUGVUHVWBVWRVULVUBU
        YHGIOPQUUDUUHWNVWHUUEVQVWGVUDVWCUQUGZVWQUQUGVUAVUDVWEVUPWIZVWBVWTVUAVWD
        VUGVWBVWTVULUYHIMOPTVLUUFUUGZVUCVWCXSXTVXAVUAVUHVWBVWIVWQUOUMVWEVUOVWSV
        UBUYHGIMOPTQVVNUUIWNVWGVWQVUCVOXQUIZVUCUOVWGVWTVOUQUGZVUDYAVUCUOUMZUTZV
        WDVWQVXCUOUMVXBVXDVWGUUJYBVUAVXFVWEVUAVUDVXEVUPVUAVUGVUHVXEVULVUOVUBIMO
        PTUUKZVMYDWIVUAVWBVWDYEVWCVOVUCUULYCVWGVUCXRUGZVXCVUCVNVWGVUCVXAYFVUCUU
        MVQUUNYGUUOUUPVUSVVTUYNVUCUOVUSVVSUYMUSUYLVURUYEUUQXPWQYHYIYJUUSUYQVUFD
        VUCUQUYOVUCVNUYPVUEEKUYOVUCUYNUOUURUUTUVAXTWEAKVVFYKZUYSUYKYLZAKJUVBZVV
        FKVVEVXKUFJVVBUVCUVDAVVGVXKVVFYKVVPOVVFJUVEVQUVFVUINVAUGZVXIVXJVUKNUBUV
        GZBEKIUYFUSNODCPNUBUVJZUYFWDZUVHVIVQYPAUYJUYCCUQAUYHUQUGZUTZUYJUYLMUJZV
        OUOUMZVUCUYHUOUMZXLZBOUPZUYBUYHUOUMZUYCVXQUYJVYABOVXQUYTUTVXSUYJVXTVXQU
        YTVXSUYJVXTXLVXQUYTVXSUTZUTZUYJUYLJUJZUYFUJZUYHUOUMZVXTVYEVYFKUGUYJVYHX
        LVYEVYFVVEKVYEUYLVVBUGZVYFVVEUGZVYEVYDVYIVXQVYDUVIVVAVXSDUYLOUYOUYLVNZV
        UTVXRVOUOUYOUYLMWPWQWRUVKAUYTVYIVYJXLZVXPVXSAUYTUYLJUVLZUGZVYLAVYNUYTAV
        YMOUYLAVVGVYMOVNVVPOVVFJUWLVQUVMUVNAVVDVYNVYLVVQVVBUYLJUVOVKUVPYMYNUFUV
        QUYIVYHEVYFKUYEVYFVNUYGVYGUYHUOUYEVYFUYFWPWQUVRVQVXQUYTVYHVXTXLVXSVXQUY
        TVYHVXTVXQUYTVYHUTZUTZYAVUCULUMZYAVUCVNZUVTZVXTVYPVXEVYSVYPVUGVUHVXEVUL
        AUYTVUHVXPVYHVUOYMZVXGVMZVYPYAUQUGZVUDVXEVYSYLYOAUYTVUDVXPVYHVUPYMZYAVU
        CUVSVMYPVYPVYQVXTVYRVYPVYQVUCVUCXQUIZUYHVUCXQUIZUOUMZVXTVYPWUDVYGVUCXQU
        IZWUEVYPVUDVUDWUDUQUGWUCWUCVUCVUCXSXTVYPVYGUQUGZVUDWUGUQUGAUYTWUHVXPVYH
        VUAVYFVVFUGZWUHAVVGUYTWUIVVPOVVFUYLJVJVKZVUGVXLWUIWUHVULVXMVVFVYFIUYFNO
        XRPNUBUWAZVXOVVOUWBVIVQYMZWUCVYGVUCXSXTVYPVXPVUDWUEUQUGAVXPVYOUWCZWUCUY
        HVUCXSXTVYPVUBVYFUJZUSUJZWUDWUGUOVYPWUOVUCUWDUWEUIZUSUJZWUPWUDVYPWUNWUP
        USVYPWUNVUBVUBGUIZWUPAUYTWUNWURVNVXPVYHVUAWUNVUBEOUYEVUBGUIZWAZUJZWURVU
        AVUBVYFWUTUYTVYFWUTVNADUYLVVJWUTOJVYKEOVVIWUSVYKVVHVUBUYEGUYOUYLHWPWSXB
        UEEOWUSVWNXEXFYQXGVUAVUHWVAWURVNVUOEVUBWUSWUROWUTUYEVUBVUBGXHWUTWDVUBVU
        BGXIXFVQXOYMVYPVUGVUHWURWUPVNVULVYTVUBGIMOPTQUWFVMXOXPVYPVUDWUQWUPVNZWU
        CVUDWUPUQUGYAWUPUOUMWVBVUCUWGVUCUWHWUPUWIXTVQVYPVXHWUPWUDVNVYPVUCWUCYFV
        UCUWJVQUWKVYPWUIVUHWUOWUGUOUMAUYTWUIVXPVYHWUJYMVYTVUBVVFVYFIMUSUYFNOPTV
        XNVXOVVOVULVXMUWMXTUWNVYPWUHVXPVXFVYHWUGWUEUOUMWULWUMVYPVUDVXEWUCWUAYDV
        XQUYTVYHYEZVYGUYHVUCUWOYCYGVYPVUDVXPVUDVYQWUFVXTXLZXLWUCWUMWUCVUDVXPUTV
        UDVYQWVDVUDVXPVUDVYQUTZWVDVUDVXPWVEUXBVXTWUFVUCUYHVUCUWPUWQYRUWRUWSUWTV
        YPYAUYHUOUMVYRVXTVYPYAVYGUYHWUBVYPYOYBWULWUMVYPOXRVYFVGZYAVYGUOUMZAUYTW
        VFVXPVYHVUAWUIWVFWUJVUGVXLWUIWVFVULVXMVVFVYFINOXRPWUKVVOUXAVIVQYMVUGVXL
        WVFWVGVULVXMVYFIUYFNOXRPWUKVXOUXCVIVQWVCYGYAVUCUYHUOUXDYHUXEYNYSXNYJYSU
        XFUXGAVUMUYHYTUGZVYBVYCXLVXPVUNUYHUXMZVUMWVHVYBVYCBUYHHIMMUYAIOOPPTTUYA
        WDZVULVULUXHYRWNVXQVYCUYHUKULUMZUYCVXPWVKAUYHUXIYQAUYBYTUGZWVHVYCWVKUTU
        YCXLZVXPAVUMWVLVUNVUGVUGVUMWVLVULVULHIUYAIOOPPWVJUXJVIVQWVIWVLWVHUKYTUG
        WVMUXKUYBUYHUKUXLUXNWNUXOUXPYIYNVUGVUGUYDUXTUYCUTYLVULVULFHILUYAIWVJRSU
        XQUXRUXS $.
        $( [23-Aug-2014] $) $( [11-Jan-2008] $)
    $}

    $( Hellinger-Toeplitz Theorem: any self-adjoint linear operator defined on
       all of Hilbert space is bounded.  Theorem 10.1-1 of [Kreyszig] p. 525.
       Discovered by E. Hellinger and O. Toeplitz in 1910, "it aroused both
       admiration and puzzlement since the theorem establishes a relation
       between properties of two different kinds, namely, the properties of
       being defined everywhere and being bounded."  (The proof was shortened
       by Mario Carneiro, 12-Jan-2014.) $)
    htth $p |- ( ( U e. CHil /\ T e. L /\
        A. x e. X A. y e. X ( x P ( T ` y ) ) = ( ( T ` x ) P y ) ) ->
      T e. B ) $=
      ( vw cv cfv co wceq wral fveq2 eqid vu vv vz chl wcel wa wi cmul cop cabs
      caddc cif clno cip cba cblo oveq12 anidms syl5eq eleq2d eqeq12d raleqbidv
      oveqd anbi12d imbi12d cmpt cnm cle wbr crab cima elimel simpl simpr oveq1
      c1 cnhl oveq1d oveq2d oveq2 cbvral2v sylib cbvmptv breq1d cbvrabv imaeq2i
      mpteq2dv htthlem dedth 3impib ) FUDUEZEGUEZANZBNZEOZDPZWMEOZWNDPZQZBHRZAH
      RZECUEZWKWLXAUFZXBUGEWKFUKUHUIUJUIZULZXEUMPZUEZWMWOXEUNOZPZWQWNXHPZQZBXEU
      OOZRZAXLRZUFZEXEXEUPPZUEZUGFXDFXEQZXCXOXBXQXRWLXGXAXNXRGXFEXRGFFUMPZXFKXR
      XSXFQFXEFXEUMUQURUSUTXRWTXMAHXLXRHFUOOXLIFXEUOSUSZXRWSXKBHXLXTXRWPXIWRXJX
      RDXHWMWOXRDFUNOXHJFXEUNSUSZVCXRDXHWQWNYAVCVAVBVBVDXRCXPEXRCFFUPPZXPLXRYBX
      PQFXEFXEUPUQURUSUTVEXOUAUBUCMXPXHEXEAXLBXLWNWQXHPZVFZVFZYEWMXEVGOZOZVPVHV
      IZAXLVJZVKXFYFXDXLXLTXHTXFTXPTYFTFXDUDXDXDTZVQVLYJXGXNVMXOXNUANZUBNZEOZXH
      PZYKEOZYLXHPZQZUBXLRUAXLRXGXNVNXKYQYKWOXHPZYOWNXHPZQABUAUBXLXLWMYKQZXIYRX
      JYSWMYKWOXHVOYTWQYOWNXHWMYKESVRVAWNYLQZYRYNYSYPUUAWOYMYKXHWNYLESVSWNYLYOX
      HVTVAWAWBAUCXLYDMXLMNZUCNZEOZXHPZVFZWMUUCQZYDMXLUUBWQXHPZVFUUFBMXLYCUUHWN
      UUBWQXHVOWCUUGMXLUUHUUEUUGWQUUDUUBXHWMUUCESVSWGUSWCYIUUCYFOZVPVHVIZUCXLVJ
      YEYHUUJAUCXLUUGYGUUIVPVHWMUUCYFSWDWEWFWHWIWJ $.
      $( [23-Aug-2014] $) $( [11-Jan-2008] $)
  $}

