$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Examples of pre-Hilbert spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    cnph.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The set of complex numbers is an inner product (pre-Hilbert) space.
       (Contributed by Steve Rodriguez, 28-Apr-2007; revised by nm
       24-Jul-2008.) $)
    cnph $p |- U e. CPreHil $=
      ( vx vy caddc cmul cop cabs cphl wcel cv co cfv c2 cexp wceq cc cvv recnd
      syl cnv c1 cneg wral wa wb addex mulex cr absf cnex mp2an cablo cnaddablo
      wf fex cgr ablogrpo ax-mp cxp ax-addopr fdmi grporn isphg mp3an eqid cnnv
      cmin mulm1 adantl oveq2d negsub eqtrd fveq2d oveq1d ccj sqabsadd sqabssub
      cre oveq12d abscl sqcl addcl syl2an cjcl sylan2 recl 2cn mpan 3syl ppncan
      mulcl syl3anc 2times eqcomd 3eqtrd rgen2a mpbir2an eqeltri ) AEFGHGZIBWTI
      JZWTUAJZCKZDKZELHMNOLZXCUBUCXDFLZELZHMZNOLZELZNXCHMZNOLZXDHMZNOLZELZFLZPZ
      DQUDCQUDZERJFRJHRJZXAXBXRUEUFUGUHQUIHUOQRJXSUJUKQUIRHUPULCDRRRFEHQEQEUMJE
      UQJUNEURUSQQUTQEVAVBVCVDVEWTWTVFVGXQCDQXCQJZXDQJZUEZXJXEXCXDVHLZHMZNOLZEL
      ZXOXOELZXPYBXIYEXEEYBXHYDNOYBXGYCHYBXGXCXDUCZELYCYBXFYHXCEYAXFYHPXTXDVIVJ
      VKXCXDVLVMVNVOVKYBYFXONXCXDVPMZFLZVSMZFLZELZXOYLVHLZELZYGYBXEYMYEYNEXCXDV
      QXCXDVRVTYBXOQJZYLQJZYPYOYGPXTXLQJZXNQJZYPYAXTXKQJYRXTXKXCWASXKWBTYAXMQJY
      SYAXMXDWASXMWBTXLXNWCWDZYBYJQJZYKQJZYQYAXTYIQJUUAXDWEXCYIWLWFUUAYKYJWGSNQ
      JUUBYQWHNYKWLWIWJYTXOYLXOWKWMVMYBYPYGXPPYTYPXPYGXOWNWOTWPWQWRWS $.
      $( [7-Nov-2013] $) $( [28-Apr-2007] $)
  $}

  ${
    elimph.1 $e |- X = ( BaseSet ` U ) $.
    elimph.5 $e |- Z = ( 0vec ` U ) $.
    elimph.6 $e |- U e. CPreHil $.
    $( MAY BE REVISED - use elimnvOLD ? $)
    $( Hypothesis elimination lemma for complex inner product spaces to assist
       weak deduction theorem. $)
    elimph $p |- if ( A e. X , A , Z ) e. X $=
      ( phnvi elimnv ) ABCDEFBGHI $.
      $( [27-Apr-2007] $)
  $}

  $( MAY BE REVISED - use elimnvu ? $)
  $( Hypothesis elimination lemma for complex inner product spaces to assist
     weak deduction theorem. $)
  elimphu $p |- if ( U e. CPreHil , U , <. <. + , x. >. , abs >. )
                 e. CPreHil $=
    ( caddc cmul cop cabs cphl eqid cnph elimel ) ABCDEDZFJJGHI $.
    $( [6-May-2007] $)


