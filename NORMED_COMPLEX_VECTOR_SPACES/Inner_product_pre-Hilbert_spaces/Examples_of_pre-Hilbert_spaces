$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Examples of pre-Hilbert spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    cnph.6 $e |- U = <. <. + , x. >. , abs >. $.
    $( The set of complex numbers is an inner product (pre-Hilbert) space.
       (Contributed by Steve Rodriguez, 28-Apr-2007; revised by nm
       24-Jul-2008.) $)
    cnph $p |- U e. CPreHil $=
      ( vx vy caddc cmul cop cabs cphl wcel cv co cfv c2 cexp wceq cc recnd syl
      cvv cnv c1 cneg wral eqid cnnv wa mulm1 adantl oveq2d negsub eqtrd fveq2d
      cmin oveq1d ccj cre sqabsadd sqabssub oveq12d abscl sqcl addcl cjcl mulcl
      syl2an sylan2 recl 2cn mpan ppncan syl3anc 2times eqcomd 3eqtrd rgen2a wb
      3syl addex mulex cr wf absf cnex fex mp2an cablo cnaddablo ablogrpo ax-mp
      cgr cxp ax-addopr fdmi grporn isphg mp3an mpbir2an eqeltri ) AEFGHGZIBWTI
      JZWTUAJZCKZDKZELHMNOLZXCUBUCXDFLZELZHMZNOLZELZNXCHMZNOLZXDHMZNOLZELZFLZPZ
      DQUDCQUDZWTWTUEUFXQCDQXCQJZXDQJZUGZXJXEXCXDUNLZHMZNOLZELZXOXOELZXPYAXIYDX
      EEYAXHYCNOYAXGYBHYAXGXCXDUCZELYBYAXFYGXCEXTXFYGPXSXDUHUIUJXCXDUKULUMUOUJY
      AYEXONXCXDUPMZFLZUQMZFLZELZXOYKUNLZELZYFYAXEYLYDYMEXCXDURXCXDUSUTYAXOQJZY
      KQJZYOYNYFPXSXLQJZXNQJZYOXTXSXKQJYQXSXKXCVARXKVBSXTXMQJYRXTXMXDVARXMVBSXL
      XNVCVFZYAYIQJZYJQJZYPXTXSYHQJYTXDVDXCYHVEVGYTYJYIVHRNQJUUAYPVINYJVEVJVRYS
      XOYKXOVKVLULYAYOYFXPPYSYOXPYFXOVMVNSVOVPETJFTJHTJZXAXBXRUGVQVSVTQWAHWBQTJ
      UUBWCWDQWATHWEWFCDTTTFEHQEQEWGJEWKJWHEWIWJQQWLQEWMWNWOWPWQWRWS $.
      $( [7-Nov-2013] $) $( [28-Apr-2007] $)
  $}

  ${
    elimph.1 $e |- X = ( BaseSet ` U ) $.
    elimph.5 $e |- Z = ( 0vec ` U ) $.
    elimph.6 $e |- U e. CPreHil $.
    $( MAY BE REVISED - use elimnvOLD ? $)
    $( Hypothesis elimination lemma for complex inner product spaces to assist
       weak deduction theorem. $)
    elimph $p |- if ( A e. X , A , Z ) e. X $=
      ( phnvi elimnv ) ABCDEFBGHI $.
      $( [27-Apr-2007] $)
  $}

  $( MAY BE REVISED - use elimnvu ? $)
  $( Hypothesis elimination lemma for complex inner product spaces to assist
     weak deduction theorem. $)
  elimphu $p |- if ( U e. CPreHil , U , <. <. + , x. >. , abs >. )
                 e. CPreHil $=
    ( caddc cmul cop cabs cphl eqid cnph elimel ) ABCDEDZFJJGHI $.
    $( [6-May-2007] $)


