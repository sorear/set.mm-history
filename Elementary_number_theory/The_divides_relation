$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                             The divides relation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c || $.

  $( Extend the definition of a class to include the divides relation. $)
  cdivides $a class || $.

  ${
    $d n x y $.
    $( Define the divides relation. $)
    df-divides $a |- || = { <. x , y >. | ( ( x e. ZZ /\ y e. ZZ ) /\
                                            E. n e. ZZ ( n x. x ) = y ) } $.
  $}

  ${
    $d M n x y $.  $d N n x y $.
    $( Divides relation. ` M || N ` means ` M ` divides into ` N ` with no
       remainder.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    divides $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                    ( M || N <-> E. n e. ZZ ( n x. M ) = N ) ) $=
      ( vx vy cz wcel wa cop cv cmul co wceq wrex copab cdivides wbr opreq2
      eqeq1d rexbidv eqeq2 opelopab2 df-br df-divides eleq2i bitri syl5bb ) BFG
      CFGHBCIZDJZFGEJZFGHAJZUIKLZUJMZAFNZHDEOZGZUKBKLZCMZAFNZBCPQZUNUQUJMZAFNUS
      DEBCFFUIBMZUMVAAFVBULUQUJUIBUKKRSTUJCMVAURAFUJCUQUATUBUTUHPGUPBCPUCPUOUHD
      EAUDUEUFUG $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K x $.  $d M x $.  $d N x $.
    $( A lemma to assist theorems of ` || ` with no antecedents.  (Contributed
       by Paul Chapman, 21-Mar-2011.) $)
    dvds0lem $p |- ( ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) /\ ( K x. M ) = N ) ->
                     M || N ) $=
      ( vx cz wcel w3a cmul co wceq cdivides wbr wi wa cv wrex opreq1 eqeq1d
      rcla4ev adantl wb divides adantr mpbird 3impb 3expia 3impa 3comr imp ) AE
      FZBEFZCEFZGABHIZCJZBCKLZUKULUJUNUOMZUKULUJUPUKULNZUJUNUOUQUJUNUOUQUJUNNZN
      UODOZBHIZCJZDEPZURVBUQVAUNDAEUSAJUTUMCUSABHQRSTUQUOVBUAURDBCUBUCUDUEUFUGU
      HUI $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d J x $.  $d K x $.  $d M x z $.  $d N x z $.  $d Z z $.  $d ph x $.
    dvds1lem.1 $e |- ( ph -> ( J e. ZZ /\ K e. ZZ ) ) $.
    dvds1lem.2 $e |- ( ph -> ( M e. ZZ /\ N e. ZZ ) ) $.
    dvds1lem.3 $e |- ( ( ph /\ x e. ZZ ) -> Z e. ZZ ) $.
    dvds1lem.4 $e |- ( ( ph /\ x e. ZZ ) ->
                       ( ( x x. J ) = K -> ( Z x. M ) = N ) ) $.
    $( A lemma to assist theorems of ` || ` with one antecedent.  (Contributed
       by Paul Chapman, 21-Mar-2011.) $)
    dvds1lem $p |- ( ph -> ( J || K -> M || N ) ) $=
      ( vz cdivides wbr cv cmul co wceq cz wrex wcel wa wb divides syl biimpd
      wi opreq1 eqeq1d rcla4ev sylan ex syld r19.23adv sylibrd ) ACDMNZLOZEPQZF
      RZLSTZEFMNZAUPBOZCPQDRZBSTZUTAUPVDACSUADSUAUBUPVDUCHBCDUDUEUFAVCUTBSAVBSU
      AZVCUTUGAVEUBZVCGEPQZFRZUTKVFVHUTGSUAVHUTVFUSVHLGSUQGRURVGFUQGEPUHUIUJJUK
      ULUMULUNUMAESUAFSUAUBVAUTUCILEFUDUEUO $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d I x y $.  $d J x y $.  $d K x y $.  $d L x y $.  $d M x y z $.
    $d N x y z $.  $d Z z $.  $d ph x y $.
    dvds2lem.1 $e |- ( ph -> ( I e. ZZ /\ J e. ZZ ) ) $.
    dvds2lem.2 $e |- ( ph -> ( K e. ZZ /\ L e. ZZ ) ) $.
    dvds2lem.3 $e |- ( ph -> ( M e. ZZ /\ N e. ZZ ) ) $.
    dvds2lem.4 $e |- ( ( ph /\ ( x e. ZZ /\ y e. ZZ ) ) -> Z e. ZZ ) $.
    dvds2lem.5 $e |- ( ( ph /\ ( x e. ZZ /\ y e. ZZ ) ) ->
               ( ( ( x x. I ) = J /\ ( y x. K ) = L ) -> ( Z x. M ) = N ) ) $.
    $( A lemma to assist theorems of ` || ` with two antecedents.  (Contributed
       by Paul Chapman, 21-Mar-2011.) $)
    dvds2lem $p |- ( ph -> ( ( I || J /\ K || L ) -> M || N ) ) $=
      ( vz cdivides wbr wa cv cmul co wceq cz wrex wcel wb divides bi2anan9
      syl11anc biimpd reeanv syl6ibr wi opreq1 eqeq1d rcla4ev sylan ex syld
      r19.23advv syl sylibrd ) ADEQRZFGQRZSZPTZHUAUBZIUCZPUDUEZHIQRZAVFBTZDUAUB
      EUCZCTZFUAUBGUCZSZCUDUEBUDUEZVJAVFVMBUDUEZVOCUDUEZSZVQAVFVTADUDUFEUDUFSZF
      UDUFGUDUFSZVFVTUGKLWAVDVRWBVEVSBDEUHCFGUHUIUJUKVMVOBCUDUDULUMAVPVJBCUDUDA
      VLUDUFVNUDUFSZVPVJUNAWCSZVPJHUAUBZIUCZVJOWDWFVJJUDUFWFVJWDVIWFPJUDVGJUCVH
      WEIVGJHUAUOUPUQNURUSUTUSVAUTAHUDUFIUDUFSVKVJUGMPHIUHVBVC $.
      $( [21-Mar-2011] $)
  $}

  $( An integer divides itself.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
  iddvds $p |- ( N e. ZZ -> N || N ) $=
    ( cz wcel c1 cmul co wceq cdivides wbr cc zcn mulid2 syl 1z dvds0lem
    mp3anl1 anabsan mpdan ) ABCZDAEFAGZAAHIZSAJCTAKALMSTUADBCSSTUANDAAOPQR $.
    $( [21-Mar-2011] $)

  $( 1 divides any integer.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
  1dvds $p |- ( N e. ZZ -> 1 || N ) $=
    ( cz wcel c1 cmul co wceq cdivides wbr cc zcn ax1id syl 1z dvds0lem
    mp3anl2 anabsan mpdan ) ABCZADEFAGZDAHIZSAJCTAKALMSTUASDBCSTUANADAOPQR $.
    $( [21-Mar-2011] $)

  $( Any integer divides 0.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
  dvds0 $p |- ( N e. ZZ -> N || 0 ) $=
    ( cz wcel cc0 cmul co wceq cdivides wbr cc zcn mul02 syl wi 0z w3a
    dvds0lem ex mp3an13 mpd ) ABCZDAEFDGZADHIZUAAJCUBAKALMDBCZUAUDUBUCNOOUDUAUD
    PUBUCDADQRST $.
    $( [21-Mar-2011] $)

  ${
    $d M x $.  $d N x $.
    $( An integer divides another iff its negation does.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    negdvdsb $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> -u M || N ) ) $=
      ( vx cz wcel wa cdivides wbr cneg cv id znegcl anim1i adantl cmul co
      wceq cc mul2neg zcn syl2an ancoms adantlr eqeq1d biimprd dvds1lem
      mulneg12 impbid ) ADEZBDEZFZABGHAIZBGHUKCABULBCJZIZUKKZUIULDEUJALMZUMDEZU
      NDEUKUMLNZUKUQFZUNULOPZBQUMAOPZBQUSUTVABUIUQUTVAQZUJUQUIVBUMREZAREZVBUQUI
      UMASUMTZATZUAUBUCUDUEUFUKCULBABUNUPUOURUSUNAOPZBQUMULOPZBQUSVGVHBUIUQVGVH
      QZUJUQUIVIVCVDVIUQUIUMAUGVEVFUAUBUCUDUEUFUH $.
      $( [21-Mar-2011] $)

    $( An integer divides another iff it divides its negation.  (Contributed by
       Paul Chapman, 21-Mar-2011.) $)
    dvdsnegb $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || -u N ) ) $=
      ( vx cz wcel wa cdivides wbr cneg cv id znegcl anim2i adantl cmul co
      wceq wi cc negeq eqeq2d mulneg1 syl5cbi zcn syl2an ancoms adantlr
      dvds1lem negneg sylan9eqr sylan9eq anassrs ex 3impa syl3an 3coml 3expa
      impbid ) ADEZBDEZFZABGHABIZGHVACABAVBCJZIZVAKZUTVBDEUSBLMZVCDEZVDDEVAVCLN
      ZUSVGVCAOPZBQZVDAOPZVBQZRZUTVGUSVMVCSEZASEZVMVGUSVJVKVIIZQVLVNVOFZVJVPVBV
      KVIBTUAVCAUBZUCVCUDZAUDZUEUFUGUHVACAVBABVDVFVEVHUSUTVGVIVBQZVKBQZRZVGUSUT
      WCVNVOBSEZWCVGUSUTVNVOWDWCVQWDFWAWBVQWDWAWBVQWDWAFVKVPBVRWAWDVPVBIBVIVBTB
      UIUJUKULUMUNVSVTBUDUOUPUQUHUR $.
      $( [21-Mar-2011] $)
  $}

  $( An integer divides another iff its absolute value does.  (Contributed by
     Paul Chapman, 21-Mar-2011.) $)
  absdvdsb $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                   ( M || N <-> ( abs ` M ) || N ) ) $=
    ( cz wcel wa cabs cfv wceq cneg wo cdivides wbr wb cr zre absor syl adantr
    wi breq1 bicomd a1i negdvdsb anim12i bitr ex jaod mpd ) ACDZBCDZEZAFGZAHZUL
    AIZHZJZABKLZULBKLZMZUIUPUJUIANDUPAOAPQRUKUMUSUOUMUSSUKUMURUQULABKTUAUBUKUOU
    SUKUOEUQUNBKLZMZUTURMZEUSUKVAUOVBABUCUOURUTULUNBKTUAUDUQUTURUEQUFUGUH $.
    $( [21-Mar-2011] $)

  $( An integer divides another iff it divides its absolute value.
     (Contributed by Paul Chapman, 21-Mar-2011.) $)
  dvdsabsb $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                   ( M || N <-> M || ( abs ` N ) ) ) $=
    ( cz wcel wa cabs cfv wceq cneg wo cdivides wbr wb cr zre absor syl adantl
    wi breq2 bicomd a1i dvdsnegb anim12i bitr ex jaod mpd ) ACDZBCDZEZBFGZBHZUL
    BIZHZJZABKLZAULKLZMZUJUPUIUJBNDUPBOBPQRUKUMUSUOUMUSSUKUMURUQULBAKTUAUBUKUOU
    SUKUOEUQAUNKLZMZUTURMZEUSUKVAUOVBABUCUOURUTULUNAKTUAUDUQUTURUEQUFUGUH $.
    $( [21-Mar-2011] $)

  ${
    $d N n $.
    $( Only 0 is divisible by 0 .  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    0dvds $p |- ( N e. ZZ -> ( 0 || N <-> N = 0 ) ) $=
      ( vn cz wcel cc0 cdivides wbr wceq cv cmul co wrex wb 0z divides mpan
      eqtr2 cc zcn mul01 syl sylan2 ancoms r19.23aiva syl6bi dvds0 ax-mp breq2
      mpbiri impbid1 ) ACDZEAFGZAEHZUKULBIZEJKZAHZBCLZUMECDZUKULUQMNBEAOPUPUMBC
      UPUNCDZUMUPUOEHZUMUSUOAEQUSUNRDUTUNSUNTUAUBUCUDUEUMULEEFGZURVANEUFUGAEEFU
      HUIUJ $.
      $( [21-Mar-2011] $)
  $}

  $( An integer divides a multiple of itself.  (Contributed by Paul Chapman,
     21-Mar-2011.) $)
  dvdsmul1 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> M || ( M x. N ) ) $=
    ( cz wcel wa cmul co wceq cdivides wbr cc mulcom zcn syl2an ancoms wi
    zmulcl w3a dvds0lem ex 3com12 mpd3an3 mpd ) ACDZBCDZEBAFGABFGZHZAUFIJZUEUDU
    GBKDAKDUGUEUDBALBMAMNOUDUEUFCDZUGUHPZABQUEUDUIUJUEUDUIRUGUHBAUFSTUAUBUC $.
    $( [21-Mar-2011] $)

  $( An integer divides a multiple of itself.  (Contributed by Paul Chapman,
     21-Mar-2011.) $)
  dvdsmul2 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> N || ( M x. N ) ) $=
    ( cz wcel cmul co cdivides wbr zmulcl w3a wceq eqid dvds0lem mpan2 mpd3an3
    ) ACDZBCDZABEFZCDZBRGHZABIPQSJRRKTRLABRMNO $.
    $( [21-Mar-2011] $)

  $( An integer divides a positive integer power of itself.  (Contributed by
     Paul Chapman, 26-Oct-2012.) $)
  iddvdsexp $p |- ( ( M e. ZZ /\ N e. NN ) -> M || ( M ^ N ) ) $=
    ( cz wcel cn wa cexp co cdivides wbr c1 cmin cmul cn0 zexpcl nnm1nn0
    sylan2 simpl dvdsmul2 syl11anc cc wceq expm1t zcn sylan breq2d mpbird ) ACD
    ZBEDZFZAABGHZIJAABKLHZGHZAMHZIJZUJUMCDZUHUOUHULNDUPUIAULOBPQUHUIRUMASTUJUKU
    NAIAUADUIUKUNUBUHABUCAUDUEUFUG $.
    $( [26-Oct-2012] $)

  ${
    $d K x $.  $d M x $.  $d N x $.
    $( If a product divides an integer, so does one of its factors.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    muldvds1 $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K x. M ) || N -> K || N ) ) $=
      ( vx cz wcel w3a cmul co cv wa zmulcl id anim12i 3impa 3simpb ancoms
      3ad2antl2 wceq cc mulass mul23 eqtr3d zcn syl3an 3coml 3expa 3adantl3
      eqeq1d biimpd dvds1lem ) AEFZBEFZCEFZGZDABHIZCACDJZBHIZULUMUNUPEFZUNKULUM
      KUSUNUNABLUNMNOULUMUNPUMULUQEFZUREFZUNUTUMVAUQBLQRUOUTKZUQUPHIZCSURAHIZCS
      VBVCVDCULUMUTVCVDSZUNULUMUTVEUTULUMVEUQTFZATFZBTFZVEUTULUMVFVGVHGUQAHIBHI
      VCVDUQABUAUQABUBUCUQUDAUDBUDUEUFUGUHUIUJUK $.
      $( [21-Mar-2011] $)

    $( If a product divides an integer, so does one of its factors.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    muldvds2 $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K x. M ) || N -> M || N ) ) $=
      ( vx cz wcel w3a cmul co cv wa zmulcl id anim12i 3impa 3simpc ancoms
      3ad2antl1 wceq cc mulass zcn syl3an 3coml 3expa 3adantl3 eqeq1d biimprd
      dvds1lem ) AEFZBEFZCEFZGZDABHIZCBCDJZAHIZUJUKULUNEFZULKUJUKKUQULULABLULMN
      OUJUKULPUJUKUOEFZUPEFZULURUJUSUOALQRUMURKZUPBHIZCSUOUNHIZCSUTVAVBCUJUKURV
      AVBSZULUJUKURVCURUJUKVCUOTFATFBTFVCURUJUKUOABUAUOUBAUBBUBUCUDUEUFUGUHUI
      $.
      $( [21-Mar-2011] $)

    $( Multiplication by a constant maintains the divides relation.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvdscmul $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) ->
                     ( M || N -> ( K x. M ) || ( K x. N ) ) ) $=
      ( vx cz wcel cdivides wbr cmul co wi w3a cv 3simpc zmulcl 3adant3
      3adant2 jca simpr wa wceq cc mul12 zcn syl3an 3coml 3expa 3adantl3
      opreq2 sylan9eq ex dvds1lem ) AEFZBEFZCEFZBCGHABIJZACIJZGHKUMUNUOLZDBCUPU
      QDMZUMUNUONURUPEFZUQEFZUMUNUTUOABOPUMUOVAUNACOQRURUSEFZSURVBTZUSBIJZCUAZU
      SUPIJZUQUAVCVEVFAVDIJZUQUMUNVBVFVGUAZUOUMUNVBVHVBUMUNVHUSUBFAUBFBUBFVHVBU
      MUNUSABUCUSUDAUDBUDUEUFUGUHVDCAIUIUJUKULUF $.
      $( [21-Mar-2011] $)

    $( Multiplication by a constant maintains the divides relation.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvdsmulc $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) ->
                     ( M || N -> ( M x. K ) || ( N x. K ) ) ) $=
      ( vx cz wcel cdivides wbr cmul co wi w3a cv 3simpc wa zmulcl 3adant2
      3adant1 jca 3comr simpr wceq cc mulass zcn syl3an 3com13 3expa 3adantl3
      opreq1 sylan9req ex dvds1lem 3coml ) AEFZBEFZCEFZBCGHBAIJZCAIJZGHKUOUPUQL
      ZDBCURUSDMZUOUPUQNUPUQUOUREFZUSEFZOUPUQUOLVBVCUPUOVBUQBAPQUQUOVCUPCAPRSTU
      TVAEFZUAUTVDOZVABIJZCUBZVAURIJZUSUBVEVGVHVFAIJZUSUOUPVDVIVHUBZUQUOUPVDVJV
      DUPUOVJVAUCFBUCFAUCFVJVDUPUOVABAUDVAUEBUEAUEUFUGUHUIVFCAIUJUKULUMUN $.
      $( [21-Mar-2011] $)

    $( Cancellation law for the divides relation.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    dvdscmulr $p |- ( ( M e. ZZ /\ N e. ZZ /\ ( K e. ZZ /\ K =/= 0 ) ) ->
                      ( ( K x. M ) || ( K x. N ) <-> M || N ) ) $=
      ( vx cz wcel cc0 wne wa w3a cmul co cdivides wbr cv zmulcl 3adant3
      3adant2 jca 3coml 3adant3r 3simpa simpr wceq wb wi cc mul12 3adant1r
      3expb ancoms eqeq1d mulcan mulcl syl3an1 bitr3d zcn anim12i anim1i
      syl3an 3impa 3expia 3impb imp biimpd dvds1lem dvdscmul impbid ) BEFZCEFZA
      EFZAGHZIZJZABKLZACKLZMNZBCMNZVNDVOVPBCDOZVIVJVKVOEFZVPEFZIZVLVKVIVJWBVKVI
      VJJVTWAVKVIVTVJABPQVKVJWAVIACPRSTUAVIVJVMUBVNVSEFZUCVNWCIVSVOKLZVPUDZVSBK
      LZCUDZVNWCWEWGUEZVIVJVMWCWHUFVIVJVMIZWCWHWCVIWIWHWCVIWIWHWCVIIZVJVMWHVSUG
      FZBUGFZIZCUGFZAUGFZVLIZWHWJVJVMWMWNWPJZAWFKLZVPUDZWEWGWQWRWDVPWMWPWRWDUDZ
      WNWPWMWTWPWKWLWTWOWKWLWTVLAVSBUHUIUJUKRULWFUGFWNWPWSWGUEWMWFCAUMVSBUNUOUP
      WCWKVIWLVSUQBUQURCUQVKWOVLAUQUSUTUJVATVBVCVDVEVFVIVJVKVRVQUFVLABCVGUAVH
      $.
      $( [21-Mar-2011] $)

    $( Cancellation law for the divides relation.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    dvdsmulcr $p |- ( ( M e. ZZ /\ N e. ZZ /\ ( K e. ZZ /\ K =/= 0 ) ) ->
                      ( ( M x. K ) || ( N x. K ) <-> M || N ) ) $=
      ( vx cz wcel cc0 wne wa w3a cmul co cdivides wbr cv zmulcl 3adant2
      3adant1 jca 3adant3r 3simpa simpr wceq wb wi cc mulass 3expa adantrr
      eqeq1d mulcan2 mulcl syl3an1 bitr3d zcn anim12i anim1i syl3an 3expb
      3impa 3coml 3expia 3impb imp biimpd dvds1lem dvdsmulc impbid ) BEFZCEFZAE
      FZAGHZIZJZBAKLZCAKLZMNZBCMNZVNDVOVPBCDOZVIVJVKVOEFZVPEFZIVLVIVJVKJVTWAVIV
      KVTVJBAPQVJVKWAVICAPRSTVIVJVMUAVNVSEFZUBVNWBIVSVOKLZVPUCZVSBKLZCUCZVNWBWD
      WFUDZVIVJVMWBWGUEVIVJVMIZWBWGWBVIWHWGWBVIWHWGWBVIIZVJVMWGVSUFFZBUFFZIZCUF
      FZAUFFZVLIZWGWIVJVMWLWMWOJZWEAKLZVPUCZWDWFWPWQWCVPWLWOWQWCUCZWMWLWNWSVLWJ
      WKWNWSVSBAUGUHUIQUJWEUFFWMWOWRWFUDWLWECAUKVSBULUMUNWBWJVIWKVSUOBUOUPCUOVK
      WNVLAUOUQURUSUTVAVBVCVDVEVFVIVJVKVRVQUEVLABCVGTVH $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d I x y $.  $d J x y $.  $d K x y $.  $d M x y $.  $d N x y $.
    $( If an integer divides each of two other integers, it divides any linear
       combination of them.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvds2ln $p |- ( ( ( I e. ZZ /\ J e. ZZ ) /\
                      ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ) ->
        ( ( K || M /\ K || N ) -> K || ( ( I x. M ) + ( J x. N ) ) ) ) $=
      ( vx vy cz wcel wa w3a cmul co caddc cv simpr simp1d simp2d jca simp3d
      simpl simpld zmulcl syl11anc simprd zaddcl wi anim12i an4s expcom imp
      syl wceq cc zcn adantr adddir 3expa adantl ad3antrrr mul23 syl111anc
      opreq12d mulcl mulcom 3eqtrd opreq2 opreqan12d sylan9eq ex dvds2lem ) AHI
      ZBHIZJZCHIZDHIZEHIZKZJZFGCDCECADLMZBELMZNMZFOZALMZGOZBLMZNMZVSVOVPVSVOVPV
      QVNVRPZQZVSVOVPVQWHRZSVSVOVQWIVSVOVPVQWHTZSVSVOWBHIZWIVSVTHIZWAHIZWLVSVLV
      PWMVSVLVMVNVRUAZUBZWJADUCUDVSVMVQWNVSVLVMWOUEZWKBEUCUDVTWAUFUDSVSWCHIZWEH
      IZJZJZWDHIZWFHIZJZWGHIVSWTXDVSVLVMWTXDUGWPWQWTVNXDWRVLWSVMXDWRVLJXBWSVMJX
      CWCAUCWEBUCUHUIUJUDUKZWDWFUFULXAWCCLMZDUMZWECLMZEUMZJZWGCLMZWBUMXAXJXKAXF
      LMZBXHLMZNMZWBXAXKWDCLMZWFCLMZNMZXFALMZXHBLMZNMXNXAWDUNIZWFUNIZJZCUNIZXKX
      QUMZXAXDYBXEXBXTXCYAWDUOWFUOUHULVSYCWTVSVOYCWICUOULUPZXTYAYCYDWDWFCUQURUD
      XAXOXRXPXSNXAWCUNIZAUNIZYCXOXRUMWTYFVSWRYFWSWCUOUPUSZVLYGVMVRWTAUOUTZYEWC
      ACVAVBXAWEUNIZBUNIZYCXPXSUMWTYJVSWSYJWRWEUOUSUSZVSYKWTVSVMYKWQBUOULUPZYEW
      EBCVAVBVCXAXRXLXSXMNXAXFUNIZYGXRXLUMXAYFYCYNYHYEWCCVDUDYIXFAVEUDXAXHUNIZY
      KXSXMUMXAYJYCYOYLYEWECVDUDYMXHBVEUDVCVFXGXIXLVTXMWANXFDALVGXHEBLVGVHVIVJV
      K $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K x y $.  $d M x y $.  $d N x y $.
    $( If an integer divides each of two other integers, it divides their sum.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvds2add $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K || M /\ K || N ) -> K || ( M + N ) ) ) $=
      ( vx vy cz wcel w3a caddc co cv 3simpa 3simpb wa id zaddcl anim12i 3impb
      adantl cmul wceq wi cc adddir zcn syl3an 3comr 3expb opreq12 sylan9eq ex
      3ad2antl1 dvds2lem ) AFGZBFGZCFGZHZDEABACABCIJZDKZEKZIJZUNUOUPLUNUOUPMUNU
      OUPUNURFGZNUNUNUOUPNVBUNOBCPQRUSFGZUTFGZNZVAFGUQUSUTPSUNUOVEUSATJZBUAUTAT
      JZCUANZVAATJZURUAZUBUPUNVENZVHVJVKVHVIVFVGIJZURUNVCVDVIVLUAZVCVDUNVMUSUCG
      UTUCGAUCGVMVCVDUNUSUTAUDUSUEUTUEAUEUFUGUHVFBVGCIUIUJUKULUM $.
      $( [21-Mar-2011] $)

    $( If an integer divides each of two other integers, it divides their
       difference.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvds2sub $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K || M /\ K || N ) -> K || ( M - N ) ) ) $=
      ( vx vy cz wcel w3a cmin co cv 3simpa 3simpb wa id zsubcl anim12i 3impb
      adantl cmul wceq wi cc subdir zcn syl3an 3comr 3expb opreq12 sylan9eq ex
      3ad2antl1 dvds2lem ) AFGZBFGZCFGZHZDEABACABCIJZDKZEKZIJZUNUOUPLUNUOUPMUNU
      OUPUNURFGZNUNUNUOUPNVBUNOBCPQRUSFGZUTFGZNZVAFGUQUSUTPSUNUOVEUSATJZBUAUTAT
      JZCUANZVAATJZURUAZUBUPUNVENZVHVJVKVHVIVFVGIJZURUNVCVDVIVLUAZVCVDUNVMUSUCG
      UTUCGAUCGVMVCVDUNUSUTAUDUSUEUTUEAUEUFUGUHVFBVGCIUIUJUKULUM $.
      $( [21-Mar-2011] $)

    $( The divides relation is transitive.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    dvdstr $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                   ( ( K || M /\ M || N ) -> K || N ) ) $=
      ( vx vy cz wcel w3a cv cmul co 3simpa 3simpc 3simpb wa zmulcl adantl
      wceq cc mulass mul12 eqtrd zcn syl3an 3comr 3expb 3ad2antl1 eqeq1d
      opreq2 adantr wb eqeq2 mpbid syl5bir dvds2lem ) AFGZBFGZCFGZHZDEABBCACDIZ
      EIZJKZUPUQURLUPUQURMUPUQURNUTFGZVAFGZOZVBFGUSUTVAPQUSVEOZVBAJKZCRVAUTAJKZ
      JKZCRZVHBRZVABJKZCRZOZVFVGVICUPUQVEVGVIRZURUPVCVDVOVCVDUPVOUTSGZVASGZASGZ
      VOVCVDUPVPVQVRHVGUTVAAJKJKVIUTVAATUTVAAUAUBUTUCVAUCAUCUDUEUFUGUHVNVIVLRZV
      JVKVSVMVHBVAJUIUJVMVSVJUKVKVLCVIULQUMUNUO $.
      $( [21-Mar-2011] $)
  $}

  $( If an integer divides another, it divides a multiple of it.  (Contributed
     by Paul Chapman, 17-Nov-2012.) $)
  dvdsmultr1 $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( K || M -> K || ( M x. N ) ) ) $=
    ( cz wcel w3a cdivides wbr cmul co wa dvdsmul1 3adant1 biantrud wi simp1
    simp2 zmulcl dvdstr syl111anc sylbid ) ADEZBDEZCDEZFZABGHZUFBBCIJZGHZKZAUGG
    HZUEUHUFUCUDUHUBBCLMNUEUBUCUGDEZUIUJOUBUCUDPUBUCUDQUCUDUKUBBCRMABUGSTUA $.
    $( [17-Nov-2012] $)

  $( If an integer divides another, it divides a multiple of it.  (Contributed
     by Paul Chapman, 17-Nov-2012.) $)
  dvdsmultr2 $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( K || N -> K || ( M x. N ) ) ) $=
    ( cz wcel w3a cdivides wbr cmul co wa wb dvdsmul2 biantrud 3adant1 wi
    simp1 simp3 zmulcl dvdstr syl111anc sylbid ) ADEZBDEZCDEZFZACGHZUGCBCIJZGHZ
    KZAUHGHZUDUEUGUJLUCUDUEKUIUGBCMNOUFUCUEUHDEZUJUKPUCUDUEQUCUDUERUDUEULUCBCSO
    ACUHTUAUB $.
    $( [17-Nov-2012] $)

  $( If an integer divides either of two others, it divides their product.
     (Contributed by Paul Chapman, 17-Nov-2012.) $)
  ordvdsmul $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                    ( ( K || M \/ K || N ) -> K || ( M x. N ) ) ) $=
    ( cz wcel w3a cdivides wbr cmul co wa wb dvdsmul1 biantrud 3adant1 wi
    simp1 simp2 zmulcl dvdstr syl111anc sylbid dvdsmul2 simp3 jaod ) ADEZBDEZCD
    EZFZABGHZABCIJZGHZACGHZUIUJUJBUKGHZKZULUGUHUJUOLUFUGUHKZUNUJBCMNOUIUFUGUKDE
    ZUOULPUFUGUHQZUFUGUHRUGUHUQUFBCSOZABUKTUAUBUIUMUMCUKGHZKZULUGUHUMVALUFUPUTU
    MBCUCNOUIUFUHUQVAULPURUFUGUHUDUSACUKTUAUBUE $.
    $( [17-Nov-2012] $)

  $( An integer divides another iff it divides their sum.  (Contributed by Paul
     Chapman, 31-Mar-2011.) $)
  dvdsadd $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || ( M + N ) ) ) $=
    ( cz wcel wa cdivides wbr caddc co wi iddvds adantr dvds2add 3anidm12
    expdimp mpdan cmin simpl zaddcl dvds2sub syl111anc ancomsd cc wceq pncan2
    zcn syl2an breq2d sylibd impbid ) ACDZBCDZEZABFGZAABHIZFGZUMAAFGZUNUPJUKUQU
    LAKLZUMUQUNUPUKULUQUNEUPJAABMNOPUMUPAUOAQIZFGZUNUMUQUPUTJURUMUQUPUTUMUPUQUT
    UMUKUOCDUKUPUQEUTJUKULRZABSVAAUOATUAUBOPUMUSBAFAUCDBUCDUSBUDUKULABUEAUFBUFU
    GUHUIUJ $.
    $( [21-Mar-2011] $)

  $( An integer divides another iff it divides their sum.  (Contributed by Paul
     Chapman, 31-Mar-2011.) $)
  dvdsaddr $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || ( N + M ) ) ) $=
    ( cz wcel wa cdivides wbr caddc co dvdsadd cc wceq addcom zcn syl2an
    breq2d bitrd ) ACDZBCDZEZABFGAABHIZFGABAHIZFGABJTUAUBAFAKDBKDUAUBLRSABMANBN
    OPQ $.
    $( [31-Mar-2011] $)

  $( An integer divides another iff it divides their difference.  (Contributed
     by Paul Chapman, 31-Mar-2011.) $)
  dvdssub $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || ( M - N ) ) ) $=
    ( cz wcel wa cdivides wbr cneg caddc co cmin dvdsnegb wb dvdsadd znegcl
    sylan2 cc wceq negsub zcn syl2an breq2d 3bitrd ) ACDZBCDZEZABFGABHZFGZAAUGI
    JZFGZAABKJZFGABLUDUGCDUHUJMUEAUGNBOPUFUIUKAFAQDBQDUIUKRUDUEABSATBTUAUBUC $.
    $( [21-Mar-2011] $)

  $( An integer divides another iff it divides their difference.  (Contributed
     by Paul Chapman, 31-Mar-2011.) $)
  dvdssubr $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || ( N - M ) ) ) $=
    ( cz wcel wa cmin co cdivides wbr caddc wb simpl zsubcl ancoms dvdsadd
    syl11anc cc wceq pncan3 zcn syl2an breq2d bitr2d ) ACDZBCDZEZABAFGZHIZAAUGJ
    GZHIZABHIUFUDUGCDZUHUJKUDUELUEUDUKBAMNAUGOPUFUIBAHAQDBQDUIBRUDUEABSATBTUAUB
    UC $.
    $( [31-Mar-2011] $)

  ${
    dvdslelem.1 $e |- M e. ZZ $.
    dvdslelem.2 $e |- N e. NN $.
    dvdslelem.3 $e |- K e. ZZ $.
    $( Lemma for ~ dvdsle . $)
    dvdslelem $p |- ( N < M -> ( K x. M ) =/= N ) $=
      ( clt wbr cmul co wo wne cc0 cle c1 cr wcel zrei 0re lelttric mp2an cz
      wb wi wa cn elnnz elnnz1 bitr3i pm5.32 mpbir ax-mp orbi2i mpbi cneg
      renegcli mulge0 an4s mpanl12 nngt0i nnrei lttri mpan ltlei syl sylan2
      le0neg1 sylanb expcom remulcli cc wceq recni mulneg1 breq2i bitr4i
      syl6ibr lelttr mp3an mpan2 a1i syld lemulge12 sylan ex ltletr orim12d
      mpi lttri2i sylibr ) CBGHZABIJZCGHZCWLGHZKZWLCLWKAMNHZOANHZKZWOWPMAGHZKZW
      RAPQZMPQZWTAFRZSAMTUAWSWQWPAUBQZWSWQUCZFXDXEUDXDWSUEZXDWQUEZUCXFAUFQXGAUG
      AUHUIXDWSWQUJUKULUMUNWKWPWMWQWNWKWPWLMNHZWMWKWPMAUOZBIJZNHZXHWPWKXKMXINHZ
      WKXKWPXLMBNHZXKWKXIPQZBPQZXLXMUEXKAXCUPBDRZXNXLXOXMXKXIBUQURUSWKMBGHZXMMC
      GHZWKXQCEUTZMCBSCEVAZXPVBVCMBSXPVDVEZVFXAWPXLUCXCAVGULVHVIXHMWLUOZNHZXKWL
      PQZXHYCUCABXCXPVJZWLVGULXJYBMNAVKQBVKQXJYBVLAXCVMBXPVMABVNUAVOVPVQXHWMUDW
      KXHXRWMXSYDXBCPQZXHXRUEWMUDYESXTWLMCVRVSVTWAWBWKWQBWLNHZWNWKWQYGXMWQYGWKX
      OXAXMWQUEYGXPXCBAWCUSYAWDWEWKYGWNYFXOYDWKYGUEWNUDXTXPYECBWLWFVSWEWBWGWHWL
      CYEXTWIWJ $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d M n $.  $d N n $.
    $( The divisors of a positive integer are bounded by it.  The proof does
       not use ` / ` .  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvdsle $p |- ( ( M e. ZZ /\ N e. NN ) -> ( M || N -> M <_ N ) ) $=
      ( vn cz wcel cn wa cdivides wbr clt wn cle w3a cv cmul co wceq wrex wne
      wi c1 cif breq2 opreq2 neeq1d imbi12d breq1 neeq2 opreq1 imbi2d 1z
      elimel 1nn dvdslelem dedth3h 3expia com23 3impia imp df-ne sylib nrexdv
      wb divides nnz sylan2 3adant3 mtbird con2d cr lenlt zre nnre syl2an
      sylibrd ) ADEZBFEZGZABHIZBAJIZKZABLIZVRVTVSVPVQVTVSKVPVQVTMZVSCNZAOPZBQZC
      DRZWCWFCDWCWDDEZGWEBSZWFKWCWHWIVPVQVTWHWITVRWHVTWIVPVQWHVTWITZVPVQWHWJBVP
      AUAUBZJIZWDWKOPZBSZTVQBUAUBZWKJIZWMWOSZTWPWHWDUAUBZWKOPZWOSZTABWDUAUAUAAW
      KQZVTWLWIWNAWKBJUCXAWEWMBAWKWDOUDUEUFBWOQWLWPWNWQBWOWKJUGBWOWMUHUFWDWRQZW
      QWTWPXBWMWSWOWDWRWKOUIUEUJWRWKWOAUADUKULBUAFUMULWDUADUKULUNUOUPUQURUSWEBU
      TVAVBVPVQVSWGVCZVTVPBDEXCVQCABVDBVEVFVGVHUPVIAVJEBVJEWBWAVCVPVQABVKAVLBVM
      VNVO $.
      $( [21-Mar-2011] $)
  $}

  $( The divisors of a nonzero integer are bounded by its absolute value.
     (Contributed by Paul Chapman, 21-Mar-2011.) $)
  dvdsleabs $p |- ( ( M e. ZZ /\ N e. ZZ /\ N =/= 0 ) ->
                    ( M || N -> M <_ ( abs ` N ) ) ) $=
    ( cz wcel cc0 wne w3a cdivides wbr cabs cfv cle wb wa wceq cneg wo cr zre
    absor syl adantl wi breq2 a1i dvdsnegb adantr bitr4d ex jaod mpd 3adant3
    cn dvdsle nnabscl sylan2 3impb sylbird ) ACDZBCDZBEFZGABHIZABJKZHIZAVCLIZUS
    UTVDVBMZVAUSUTNZVCBOZVCBPZOZQZVFUTVKUSUTBRDVKBSBTUAUBVGVHVFVJVHVFUCVGVCBAHU
    DUEVGVJVFVGVJNVDAVIHIZVBVJVDVLMVGVCVIAHUDUBVGVBVLMVJABUFUGUHUIUJUKULUSUTVAV
    DVEUCZUSVCUMDVMUTVANAVCUNBUOUPUQUR $.
    $( [21-Mar-2011] $)

  ${
    $d N x $.
    $( Only 0 is divisible by all integers.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    alzdvds $p |- ( N e. ZZ -> ( A. x e. ZZ x || N <-> N = 0 ) ) $=
      ( cz wcel cv cdivides wbr wral cc0 wceq wa wne wn cabs cfv cle clt wrex
      cr cn cc zcn abscl syl arch wss wi nnssz ssrexv ax-mp 3syl wb ltnle zre
      syl2an rexbidva rexnal syl6bb mpbid adantl dvdsleabs 3expb expcom
      r19.21aiv ralim com12 expdimp mtod nne sylib breq2 dvds0 syl5bir impbid1
      ) BCDZAEZBFGZACHZBIJZVRVOVSVRVOKZBILZMVSVTWAVPBNOZPGZACHZVOWDMZVRVOWBVPQG
      ZACRZWEVOWBSDZWFATRZWGVOBUADWHBUBBUCUDZWBAUETCUFWIWGUGUHWFATCUIUJUKVOWGWC
      MZACRWEVOWFWKACWHVPSDWFWKULVOVPCDZWBVPUMWJVPUNUOUPWCACUQURUSUTVRVOWAWDVOW
      AKZVRWDWMVQWCUGZACHVRWDUGWMWNACWLWMWNWLVOWAWNVPBVAVBVCVDVQWCACVEUDVFVGVHB
      IVIVJVCVSVQACVSVQVPIFGWLBIVPFVKVPVLVMVDVN $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K x y $.  $d N x $.
    $( A positive integer divides any greater factorial.  (Contributed by Paul
       Chapman, 28-Nov-2012.) $)
    dvdsfac $p |- ( ( K e. NN /\ N e. ( ZZ>= ` K ) ) -> K || ( ! ` N ) ) $=
      ( vx vy cuz cfv wcel cn cfa cdivides wbr cv wi c1 caddc co wceq fveq2
      breq2d imbi2d cz cmin cmul cn0 nnm1nn0 faccl nnz 3syl dvdsmul2 syl11anc
      facnn2 mpbird a1i wa adantl uztrn elnnuz sylan2b sylibr nnnn0 syl
      peano2z dvdsmultr1 syl111anc facp1 sylibrd ex a2d uzind4 impcom ) BAEFZGA
      HGZABIFZJKZVLACLZIFZJKZMVLAAIFZJKZMZVLADLZIFZJKZMVLAWANOPZIFZJKZMVLVNMCDA
      BVOAQZVQVSVLWGVPVRAJVOAIRSTVOWAQZVQWCVLWHVPWBAJVOWAIRSTVOWDQZVQWFVLWIVPWE
      AJVOWDIRSTVOBQZVQVNVLWJVPVMAJVOBIRSTVTAUAGZVLVSAANUBPZIFZAUCPZJKZVLWMUAGZ
      WKWOVLWLUDGWMHGWPAUEWLUFWMUGUHAUGZWMAUIUJVLVRWNAJAUKSULUMWAVKGZVLWCWFWRVL
      WCWFMWRVLUNZWCAWBWDUCPZJKZWFWSWKWBUAGZWDUAGZWCXAMVLWKWRWQUOWSWBHGZXBWSWAH
      GZWAUDGZXDWSWANEFZGZXEWRAXGGXHVLAWANUPAUQURWAUQUSZWAUTZWAUFUHWBUGVAWSXEWA
      UAGXCXIWAUGWAVBUHAWBWDVCVDWSWEWTAJWSXEXFWEWTQXIXJWAVEUHSVFVGVHVIVJ $.
      $( [28-Nov-2012] $)
  $}

