$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Fundamental theorem of arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Lemma for ~ 1arith . $)
  1arithlem1 $p |- ( F = G ->
      ( ( N = ( # ` F ) -> ( P e. ran F <-> P || ( M ` F ) ) ) <->
        ( N = ( # ` G ) -> ( P e. ran G <-> P || ( M ` G ) ) ) ) ) $=
    ( wceq chash cfv crn wcel cdivides wbr wb fveq2 eqeq2d rneq eleq2d breq2d
    bibi12d imbi12d ) BCFZEBGHZFECGHZFABIZJZABDHZKLZMACIZJZACDHZKLZMUAUBUCEBCGN
    OUAUEUIUGUKUAUDUHABCPQUAUFUJAKBCDNRST $.
    $( [21-Nov-2012] $) $( [17-Nov-2012] $)

  ${
    $d N k $.  $d j k ph $.
    1arithlem2.1 $e |- ( ph -> G = ( F |` ( 1 ... N ) ) ) $.
    $( Lemma for ~ 1arith . $)
    1arithlem2 $p |- ( ph -> ( A. j e. ( 1 ... N ) A. k e. ( 1 ... N )
                               ( j < k -> ( G ` j ) <_ ( G ` k ) ) <->
                               A. j e. ( 1 ... N ) A. k e. ( 1 ... N )
                               ( j < k -> ( F ` j ) <_ ( F ` k ) ) ) ) $=
      ( cv clt wbr cfv cle wi c1 cfz co wcel wa cres fveq1d breq12d fvres
      breqan12d sylan9bb imbi2d 2ralbidva ) ABHZCHZIJZUGEKZUHEKZLJZMUIUGDKZUHDK
      ZLJZMBCNFOPZUPAUGUPQZUHUPQZRZRULUOUIAULUGDUPSZKZUHUTKZLJUSUOAUJVAUKVBLAUG
      EUTGTAUHEUTGTUAUQURVAUMVBUNLUGUPDUBUHUPDUBUCUDUEUF $.
      $( [21-Nov-2012] $) $( [17-Nov-2012] $)
  $}

  ${
    1arithlem3.1 $e |- S = { p e. Prime | p || Z } $.
    1arithlem3.2 $e |- Q = sup ( S , RR , < ) $.

    ${
      $d Q q y $.  $d S x y $.  $d Z p q x y $.  $d p y $.
      $( Lemma for ~ 1arith . $)
      1arithlem3 $p |- ( Z e. ( ZZ>= ` 2 ) ->
                         ( Q e. Prime /\ Q || Z /\ ( K e. S -> K <_ Q ) ) ) $=
        ( vy vx vq c2 cuz cfv wcel cprime cdivides wbr cle wi wa cr clt csup
        wss c0 wne cv wral wrex w3a maxprmfct simprd syl5eqel breq1 crab
        cbvrabv eqtri elrab2 sylib simpld suprub sylan syl6breqr ex 3jca ) DKLM
        NZAONZADPQZCBNZCARQZSVFVGVHVFABNVGVHTVFABUAUBUCZBGVFBUAUDBUEUFHUGIUGRQH
        BUHIUAUIUJZVKBNZIHEBDFUKZULUMJUGZDPQZVHJAOBVOADPUNBEUGZDPQZEOUOVPJOUOFV
        RVPEJOVQVODPUNUPUQURUSZUTVFVGVHVSULVFVIVJVFVITCVKARVFVLVICVKRQVFVLVMVNU
        TIHBCVAVBGVCVDVE $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)

      $( Lemma for ~ 1arith . $)
      1arithlem4 $p |- ( Z e. ( ZZ>= ` 2 ) ->
                         ( ( Z / Q ) e. NN /\ ( Z / Q ) < Z ) ) $=
        ( cK vy c2 cuz cfv wcel cdiv co cn clt wbr cz cc0 cdivides cprime cle
        wi 1arithlem3 simp2d cv cmul wceq wrex wb simp1d prmz syl eluzelz
        divides syl11anc wa cc cr zre recnd zcn mulcom syl2an eqeq1d rexbidva
        prmnn zdiv 3bitr2d mpbid eluzelre c1 eluz2b2 simplbi nngt0 divgt0
        syl22anc elnnz sylanbrc prmuz2 eluz2b1 simprbi 3syl ltmulgt12
        syl111anc ltdivmul syl112anc mpbird jca ) CIJKZLZCAMNZOLZWLCPQZWKWLRLZS
        WLPQZWMWKACTQZWOWKAUALZWQGBLGAUBQUCZABGCDEFUDZUEWKWQHUFZAUGNZCUHZHRUIZA
        XAUGNZCUHZHRUIZWOWKARLZCRLZWQXDUJWKWRXHWKWRWQWSWTUKZAULUMZICUNZHACUOUPW
        KXFXCHRWKXARLZUQXEXBCWKAURLXAURLXEXBUHXMWKAWKXHAUSLZXKAUTUMZVAXAVBAXAVC
        VDVEVFWKAOLZXIXGWOUJWKWRXPXJAVGUMZXLHACVHUPVIVJWKCUSLZSCPQZXNSAPQZWPICV
        KZWKCOLZXSWKYBVLCPQCVMVNCVOUMZXOWKXPXTXQAVOUMZCAVPVQWLVRVSWKWNCACUGNPQZ
        WKVLAPQZYEWKWRAWJLZYFXJAVTYGXHYFAWAWBWCWKXRXNXSYFYEUJYAXOYCCAWDWEVJWKXR
        XRXNXTWNYEUJYAYAXOYDCCAWFWGWHWI $.
        $( [23-Nov-2012] $) $( [17-Nov-2012] $)
    $}
  $}

  ${
    $d G j k $.  $d N j k $.
    $( Lemma for ~ 1arith . $)
    1arithlem5 $p |- ( ( N e. NN0 /\ A. j e. ( 1 ... N ) A. k e. ( 1 ... N )
                                     ( j < k -> ( G ` j ) <_ ( G ` k ) ) /\
        A. j e. ( 1 ... N ) ( G ` j ) <_ ( G ` ( N + 1 ) ) ) ->
        A. j e. ( 1 ... ( N + 1 ) ) A. k e. ( 1 ... ( N + 1 ) )
        ( j < k -> ( G ` j ) <_ ( G ` k ) ) ) $=
      ( cn0 wcel cv clt wbr cfv cle wi c1 cfz co wral caddc wa wb elfzle2
      adantl cz elfzelz nn0z zleltp1 syl2an ancoms mpbid pm5.5 syl ralbidva
      adantr wn cr zre nn0re letrp1 syl3an 3com23 3anidm12 peano2re lenlt
      pm2.21d r19.21aiva ltnr jca csn cun wceq 1z a1i cn nn0p1nn nnge1 fzsuc
      syl111anc raleqdv raleqbidv cvv oprex breq1 fveq2 breq1d imbi12d breq2
      breq2d 2ralunsn ax-mp syl6bb biimpar 3impb 3expia mpdan expdimp sylbird
      3impia ) DEFZAGZBGZHIZWRCJZWSCJZKIZLZBMDNOZPAXEPZXADMQOZCJZKIZAXEPZXDBMXG
      NOZPZAXKPZWQXFRXJWRXGHIZXILZAXEPZXMWQXPXJSXFWQXOXIAXEWQWRXEFZRZXNXOXISXRW
      RDKIZXNXQXSWQWRMDTUAXQWQXSXNSZXQWRUBFDUBFZXTWQWRMDUCDUDZWRDUEUFUGUHXNXIUI
      UJUKULWQXFXPXMWQXGWSHIZXHXBKIZLZBXEPZXGXGHIZXHXHKIZLZRZXFXPRZXMLWQYFYIWQY
      EBXEWQWSXEFZRYCYDYLWQYCUMZYLWQRWSXGKIZYMYLWQYNYLWQYLYNYLWSUNFZWQDUNFZYLWS
      DKIYNYLWSUBFYOWSMDUCWSUOUJZDUPZWSMDTWSDUQURUSUTYLYOXGUNFZYNYMSWQYQWQYPYSY
      RDVAUJZWSXGVBUFUHUGVCVDWQYGYHWQYSYGUMYTXGVEUJVCVFWQYJYKXMWQYKYJXMWQYKYJXM
      WQXMYKYJRZWQXMXDBXEXGVGVHZPZAUUBPZUUAWQXLUUCAXKUUBWQMUBFZYAMXGKIZXKUUBVIU
      UEWQVJVKYBWQXGVLFUUFDVMXGVNUJMDVOVPZWQXDBXKUUBUUGVQVRXGVSFUUDUUASDMQVTXDX
      OYEYIABXEXGVSWRXGVIZWTYCXCYDWRXGWSHWAUUHXAXHXBKWRXGCWBZWCWDWSXGVIZWTXNXCX
      IWSXGWRHWEUUJXBXHXAKWSXGCWBWFWDUUHXNYGXIYHWRXGXGHWAUUHXAXHXHKUUIWCWDWGWHW
      IWJWKUSWLWMWNWOWP $.
      $( [21-Nov-2012] $) $( [17-Nov-2012] $)
  $}

  ${
    $d F a b g h j k n p s x y $.  $d G f j k n s z $.  $d H f j k n s $.
    $d M g h m p u v x y $.  $d M g h z $.  $d N a b n x y $.  $d R f g h z $.
    $d R g h u v x y $.  $d Z p q $.  $d f g h j k n s x y $.
    1arithlem6.1 $e |- R = { s | E. n e. NN0 ( s : ( 1 ... n ) --> Prime /\
                                 A. j e. ( 1 ... n ) A. k e. ( 1 ... n )
                                 ( j < k -> ( s ` j ) <_ ( s ` k ) ) ) } $.
    1arithlem6.2 $e |- M = { <. f , z >. | ( f e. R /\
      z = if ( f = (/) , 1 , ( ( x. seq1 f ) ` ( # ` f ) ) ) ) } $.

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem6 $p |- ( F e. R <-> E. n e. NN0 ( F : ( 1 ... n ) --> Prime /\
                                    A. x e. ( 1 ... n ) A. y e. ( 1 ... n )
                                    ( x < y -> ( F ` x ) <_ ( F ` y ) ) ) ) $=
        ( wcel cvv c1 cv cfz co cprime wf clt wbr cfv cle wi wral wa cn0 wrex
        elisset oprex fex mpan2 adantr r19.23aivw wceq feq1 fveq1 breq12d
        imbi2d 2ralbidv breq1 fveq2 breq1d imbi12d breq2 breq2d cbvral2v
        syl6bb anbi12d rexbidv elab2g pm5.21nii ) IDNIONZPHQZRSZTIUAZAQZBQZUBUC
        ZVSIUDZVTIUDZUEUCZUFZBVQUGAVQUGZUHZHUIUJZIDUKWGVOHUIVRVOWFVRVQONVOPVPRU
        LVQTOIUMUNUOUPVQTKQZUAZFQZGQZUBUCZWKWIUDZWLWIUDZUEUCZUFZGVQUGFVQUGZUHZH
        UIUJWHKIDOWIIUQZWSWGHUIWTWJVRWRWFVQTWIIURWTWRWMWKIUDZWLIUDZUEUCZUFZGVQU
        GFVQUGWFWTWQXDFGVQVQWTWPXCWMWTWNXAWOXBUEWKWIIUSWLWIIUSUTVAVBXDWEVSWLUBU
        CZWBXBUEUCZUFFGABVQVQWKVSUQZWMXEXCXFWKVSWLUBVCXGXAWBXBUEWKVSIVDVEVFWLVT
        UQZXEWAXFWDWLVTVSUBVGXHXBWCWBUEWLVTIVDVHVFVIVJVKVLLVMVN $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d M a b $.  $d R a b f $.
      $( Lemma for ~ 1arith . $)
      1arithlem7 $p |- ( ( F e. R /\ N = ( # ` F ) ) <->
        ( N e. NN0 /\ F : ( 1 ... N ) --> Prime /\ A. a e. ( 1 ... N )
          A. b e. ( 1 ... N ) ( a < b -> ( F ` a ) <_ ( F ` b ) ) ) ) $=
        ( wcel chash cfv wceq wa cn0 c1 cfz co cprime wf cv clt wbr cle wi
        wral w3a wrex 1arithlem6 simpl wfn ffn fseq1hash sylan2 eleq1d mpbird
        adantrr r19.23aiva sylbi eleq1 syl5ibrcom imp wb eqtr eqcomd ancoms
        opreq2 feq2d raleqdv raleqbidv anbi12d syl biimpd ex com23 exp3a
        pm2.43d imp3a simpld simprd 3jca rcla4ev sylibr 3impb 3adant3 jca
        impbii ) GBOZIGPQZRZSZITOZUAIUBUCZUDGUEZKUFZLUFZUGUHWTGQXAGQUIUHUJZLWRU
        KZKWRUKZULZWPWQWSXDWMWOWQWMWQWOWNTOZWMUAFUFZUBUCZUDGUEZXBLXHUKZKXHUKZSZ
        FTUMZXFKLABCDEFGHJMNUNZXLXFFTXGTOZXIXFXKXOXISZXFXOXOXIUOXPWNXGTXIXOGXHU
        PWNXGRZXHUDGUQGXGURUSZUTVAVBVCVDIWNTVEVFVGWPWSXDWMWOWSXDSZWMXMWOXSUJZXN
        XLXTFTXOXLXTXOXIXKXTXOXIXKXTUJZXOXIXIYAUJXPXIXKXTXPXQXLXTUJXRXQWOXLXSXQ
        WOXLXSUJXQWOSZXLXSYBXGIRZXLXSVHWOXQYCWOXQSIXGIWNXGVIVJVKYCXIWSXKXDYCXHW
        RUDGXGIUAUBVLZVMYCXJXCKXHWRYDYCXBLXHWRYDVNVOVPZVQVRVSVTVQWAVSWBWCVGVCVD
        VGZWDWPWSXDYFWEWFXEWMWOWQWSXDWMWQXSSXMWMXLXSFITYEWGXNWHWIWQWSWOXDWQWSSW
        NIWSWQGWRUPWNIRWRUDGUQGIURUSVJWJWKWL $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem8 $p |- ( ( F e. R /\ N = ( # ` F ) ) ->
                         ( N e. NN0 /\ F : ( 1 ... N ) --> Prime ) ) $=
        ( vx vy wcel chash cfv wceq wa cn0 c1 cfz co cprime wf cv clt wbr cle
        wi wral 1arithlem7 simp1bi simp2bi jca ) GBOIGPQRSZITOZUAIUBUCZUDGUEZUP
        UQUSMUFZNUFZUGUHUTGQVAGQUIUHUJNURUKMURUKZABCDEFGHIJMNKLULZUMUPUQUSVBVCU
        NUO $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem9 $p |- ( ( F e. R /\ N = ( # ` F ) /\ N e. NN ) ->
                         ( F ` N ) e. Prime ) $=
        ( wcel chash cfv wceq cn cprime wa c1 cfz co wf cn0 1arithlem8 simprd
        cuz elnnuz eluzfz2 sylbi ffvelrn syl2an 3impa ) GBMZIGNOPZIQMZIGORMZUNU
        OSZTIUAUBZRGUCZIUSMZUQUPURIUDMUTABCDEFGHIJKLUEUFUPITUGOMVAIUHTIUIUJUSRI
        GUKULUM $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem10 $p |- ( F e. R -> F e. Fin ) $=
        ( wcel c1 chash cfv cfz co cfn cn0 cprime wf wceq wa eqid 1arithlem8
        mpan2 simpld fz1fi syl cen wbr wb wfn simprd ffn cvv oprex fndmeng
        3syl enfi mpdan mpbid ) GBLZMGNOZPQZRLZGRLZVCVDSLZVFVCVHVETGUAZVCVDVDUB
        VHVIUCVDUDABCDEFGHVDIJKUEUFZUGVDUHUIVCVEGUJUKZVFVGULVCVIGVEUMZVKVCVHVIV
        JUNVETGUOVLVEUPLVKMVDPUQVEUPGURUFUSVEGBUTVAVB $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem11 $p |- M : R --> NN $=
        ( vx vy cv c0 wceq c1 chash cfv cmul cseq1 co cif cn wcel wral wf cfz
        cprime cn0 wrex clt wbr cle wi wa 1arithlem6 r19.40 simpld sylbi cc0
        1nn wfn wb ffn 0fz1 sylan2 biimpar iftrue syl eleq1d mpbiri wn wo
        elnn00nn biimpi orcanai wss prmnn ssriv fss mpan2 ffvelrn r19.21aiva
        mulex visset nnmulcl seq1alcl nnnn0 fseq1hash syl2an fveq2d mpbird
        sylan an32s notbid iffalse pm2.61dan r19.23aiva rgen fopab2 mpbi ) CMZN
        OZPXBQRZSXBTUAZRZUBZUCUDZCBUEBUCGUFXHCBXBBUDZPFMZUGUAZUHXBUFZFUIUJZXHXI
        XLKMZLMZUKULXNXBRXOXBRZUMULUNLXKUEKXKUEZUOFUIUJZXMKLABCDEFXBGHIJUPXRXMX
        QFUIUJXLXQFUIUQURUSXLXHFUIXJUIUDZXLUOZXJUTOZXHXTYAUOZXHPUCUDVAYBXGPUCYB
        XCXGPOXTXCYAXLXSXBXKVBZXCYAVCXKUHXBVDZXBXJVEVFZVGXCPXFVHVIVJVKXTYAVLZUO
        ZXHXFUCUDZXSYFXLYHXSYFUOXJUCUDZXLYHXSYAYIXSYAYIVMXJVNVOVPYIXLUOZYHXJXER
        ZUCUDZXLYIXKUCXBUFZYLXLUHUCVQYMFUHUCXJVRVSXKUHUCXBVTWAYMYIXPUCUDZLXKUEY
        LYMYNLXKXKUCXOXBWBWCFHLUCSXBXJWDCWEXJHMWFWGVFVFYJXFYKUCYJXDXJXEYIXSYCXD
        XJOXLXJWHYDXBXJWIWJWKVJWLWMWNYGXGXFUCYGXCVLZXGXFOXTYOYFXTXCYAYEWOVGXCPX
        FWPVIVJWLWQWRVIWSCABUCXGGJWTXA $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem12 $p |- ( (/) e. R /\ ( M ` (/) ) = 1 ) $=
        ( vx vy c0 wcel cfv c1 wceq cc0 cn0 cfz co cprime wf cv clt wbr cle wi
        wral 0nn0 f0 eqid wb fz1n ax-mp mpbir feq2i ral0 raleqi w3a chash wa
        1arithlem7 biimpri simpld mp3an cmul cseq1 cif eqeq1 opreq2 fveq2
        fveq12d ifbieq2d cz 1z elisseti fvex ifex fvopab4 iftrue syl6eq pm3.2i
        ) MBNZMGOZPQZRSNZPRTUAZUBMUCZKUDZLUDZUEUFWJMOWKMOUGUFUHLWHUIZKWHUIZWDUJ
        WIMUBMUCUBUKWHMUBMWHMQZRRQZRULWGWNWOUMUJRUNUOUPZUQUPWMWLKMUIWLKURWLKWHM
        WPUSUPWGWIWMUTZWDRMVAOZQZWDWSVBWQABCDEFMGRHKLIJVCVDVEVFZWDWFWTWDWEMMQZP
        WRVGMVHUAZOZVIZPCAMCUDZMQZPXEVAOZVGXEVHUAZOZVIXDBGXFXFXAXIXCPXEMMVJXFXG
        WRXHXBXEMVGVHVKXEMVAVLVMVNJXAPXCPVOVPVQWRXBVRVSVTXAXDPQMULXAPXCWAUOWBUO
        WC $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F f z $.
      $( Lemma for ~ 1arith . $)
      1arithlem13 $p |- ( ( F e. R /\ N = ( # ` F ) /\ N e. NN ) ->
                          ( M ` F ) = ( ( x. seq1 F ) ` N ) ) $=
        ( wcel chash cfv wceq cn w3a cmul cseq1 co wa eleq1 biimpa c0 c1 cif
        cv eqeq1 opreq2 fveq2 fveq12d ifbieq2d 1nn elisseti fvex ifex fvopab4
        adantr wn cc0 wne nnne0 cfn wb 1arithlem10 hash0 necon3abid syl sylan2
        iffalse eqtrd 3impb 3ad2ant2 eqtr4d ) GBMZIGNOZPZIQMZRGHOZVQSGTUAZOZIWA
        OZVPVRVSVTWBPZVRVSUBVPVQQMZWDVRVSWEIVQQUCUDVPWEUBZVTGUEPZUFWBUGZWBVPVTW
        HPWECAGCUHZUEPZUFWINOZSWITUAZOZUGWHBHWIGPZWJWGWMWBUFWIGUEUIWNWKVQWLWAWI
        GSTUJWIGNUKULUMLWGUFWBUFQUNUOVQWAUPUQURUSWFWGUTZWHWBPWEVPVQVAVBZWOVQVCV
        PWPWOVPGVDMZWPWOVEABCDEFGHJKLVFWQWGVQVAGVGVHVIUDVJWGUFWBVKVIVLVJVMVRVPW
        CWBPVSIVQWAUKVNVO $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d g x y z $.
      $( Lemma for ~ 1arith . $)
      1arithlem14 $p |- ( F e. R ->
                        ( ( # ` F ) e. NN <-> ( M ` F ) e. ( ZZ>= ` 2 ) ) ) $=
        ( vg vx vy wcel chash cfv cn c2 cuz cv wi wceq fveq2 eleq1d imbi12d wa
        cmul cseq1 co eqid 1arithlem13 mp3an2 c1 cfz wral cprime wf cn0
        1arithlem8 simprd mpan2 ffvelrn sylan prmuz2 syl r19.21aiva mulex
        visset uz2mulcl seq1alcl sylan2 ancoms eqeltrd ex vtoclga cc0 wo cfn
        1arithlem10 hashcl elnn00nn sylib wn c0 wb hash0 1nuz2 1arithlem12
        simpri syl6eq mtbiri syl6bi orim1d imor syl6ibr mpd impbid ) GBOZGPQZRO
        ZGHQZSTQZOZLUAZPQZROZXEHQZXCOZUBXAXDUBLGBXEGUCZXGXAXIXDXJXFWTRXEGPUDUEX
        JXHXBXCXEGHUDUEUFXEBOZXGXIXKXGUGXHXFUHXEUIUJQZXCXKXFXFUCZXGXHXLUCXFUKZA
        BCDEFXEHXFIJKULUMXGXKXLXCOZXKXGAUAZXEQZXCOZAUNXFUOUJZUPXOXKXRAXSXKXPXSO
        ZUGXQUQOZXRXKXSUQXEURZXTYAXKXMYBXNXKXMUGXFUSOYBABCDEFXEHXFIJKUTVAVBXSUQ
        XPXEVCVDXQVEVFVGMNAXCUHXEXFVHLVIMUANUAVJVKVLVMVNVOVPWSWTVQUCZXAVRZXDXAU
        BZWSWTUSOZYDWSGVSOZYFABCDEFGHIJKVTZGWAVFWTWBWCWSYDXDWDZXAVRYEWSYCYIXAWS
        YCGWEUCZYIWSYGYCYJWFYHGWGVFYJXDUNXCOWHYJXBUNXCYJXBWEHQZUNGWEHUDWEBOYKUN
        UCABCDEFHIJKWIWJWKUEWLWMWNXDXAWOWPWQWR $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem15 $p |- ( F e. R -> ( ( # ` F ) = 0 <-> ( M ` F ) = 1 ) ) $=
        ( wcel chash cfv cn c2 cuz wa wn wo cc0 wceq c1 wb 1arithlem14 dfbi3
        sylib wi 0nnn eleq1 mtbiri con2i 1nuz2 anim12i pm5.1 syl con4bid a1i
        cn0 cfz co cprime wf eqid 1arithlem8 mpan2 simpld elnn00nn 1arithlem11
        ffvelrn mpan elnn1uz2 2thd biorf orcom syl6bb bi2bian9 syl5ibrcom jaod
        mpd ) GBLZGMNZOLZGHNZPQNZLZRZWCSZWFSZRZTZWBUAUBZWDUCUBZUDZWAWCWFUDWKABC
        DEFGHIJKUEWCWFUFUGWAWGWNWJWGWNUHWAWGWLWMWGWLSZWMSZRWOWPUDWCWOWFWPWLWCWL
        WCUAOLUIWBUAOUJUKULWMWFWMWFUCWELUMWDUCWEUJUKULUNWOWPUOUPUQURWAWNWJWLWCT
        ZWMWFTZUDWAWQWRWAWBUSLZWQWAWSUCWBUTVAVBGVCZWAWBWBUBWSWTRWBVDABCDEFGHWBI
        JKVEVFVGWBVHUGWAWDOLZWRBOHVCWAXAABCDEFHIJKVIBOGHVJVKWDVLUGVMWHWLWQWIWMW
        RWHWLWCWLTWQWCWLVNWCWLVOVPWIWMWFWMTWRWFWMVNWFWMVOVPVQVRVSVT $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F x z $.  $d N x z $.  $d P z $.
      $( Lemma for ~ 1arith . $)
      1arithlem16 $p |- ( ( F e. R /\ N = ( # ` F ) /\ N e. NN ) ->
                          ( P e. ran F -> P <_ ( F ` N ) ) ) $=
        ( vx vy wcel chash cfv wceq cn w3a cv cle wbr crn wral c1 cfz co clt
        wi cn0 cprime wf simp1 simp2 wa 1arithlem7 biimpi syl11anc simp3d wo
        elfzle2 adantl wb cr cz elfzelz zre syl simp3 cuz elnnuz eluzfz2 sylbi
        3syl leloe syl2an ancoms mpbid adantr breq2 fveq2 breq2d imbi12d
        rcla4v imp simp2d ffvelrn prmz sylan eqle ex jaod mpd exp31 a2d
        ralimdv2 wfn ffn wfo dffn4 breq1 cbvfo syl5com ) HCPZJHQRSZJTPZUAZAUBZJ
        HRZUCUDZAHUEZUFZBXMPBXKUCUDZXINUBZHRZXKUCUDZNUGJUHUIZUFZXNXIXPOUBZUJUDZ
        XQYAHRZUCUDZUKZOXSUFZNXSUFZXTXIJULPZXSUMHUNZYGXIXFXGYHYIYGUAZXFXGXHUOXF
        XGXHUPXFXGUQYJACDEFGHIJKNOLMURUSUTZVAXIYFXRNXSXSXIXPXSPZYFXRXIYLYFXRXIY
        LUQZYFUQZXPJUJUDZXPJSZVBZXRYMYQYFYMXPJUCUDZYQYLYRXIXPUGJVCVDYLXIYRYQVEZ
        YLXPVFPZJVFPZYSXIYLXPVGPYTXPUGJVHXPVIVJXIJXSPZJVGPUUAXIXHUUBXFXGXHVKXHJ
        UGVLRPUUBJVMUGJVNVOVJZJUGJVHJVIVPXPJVQVRVSVTWAYNYOXRYPYMYFYOXRUKZXIYFUU
        DUKZYLXIUUBUUEUUCYEUUDOJXSYAJSZYBYOYDXRYAJXPUJWBUUFYCXKXQUCYAJHWCWDWEWF
        VJWAWGYMYPXRUKYFYMYPXRYMXQVFPZXQXKSXRYPXIYIYLUUGXIYHYIYGYKWHZYIYLUQXQUM
        PXQVGPUUGXSUMXPHWIXQWJXQVIVPWKXPJHWCXQXKWLVRWMWAWNWOWPWQWRWOXIYIHXSWSZX
        TXNVEZUUHXSUMHWTUUIXSXMHXAUUJXSHXBXRXLNAXSXMHXQXJXKUCXCXDVOVPVTXLXOABXM
        XJBXKUCXCWFXE $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem17 $p |- ( ( F e. R /\ Z e. ( ZZ>= ` 2 ) /\ Q e. NN ) ->
          ( ( Z / Q ) = ( M ` F ) <-> Z = ( ( M ` F ) x. Q ) ) ) $=
        ( wcel c2 cuz cfv cn w3a cc cc0 wne wa cdiv co wceq cmul wb cz simp2
        eluzelz zcn 3syl wf 1arithlem11 a1i simp1 ffvelrn syl11anc nncn syl
        simp3 nnne0 jca divmul3 syl111anc ) HCNZJOPQNZBRNZSZJTNZHIQZTNZBTNZBUAU
        BZUCZJBUDUEVLUFJVLBUGUEUFUHVJVHJUINVKVGVHVIUJOJUKJULUMVJVLRNZVMVJCRIUNZ
        VGVQVRVJACDEFGIKLMUOUPVGVHVIUQCRHIURUSVLUTVAVJVIVPVGVHVIVBVIVNVOBUTBVCV
        DVAJVLBVEVF $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      1arithlem18.3 $e |- G = ( F |` ( 1 ... N ) ) $.

      ${
        $d G j k n s x y $.
        $( Lemma for ~ 1arith . $)
        1arithlem18 $p |- ( ( F e. R /\ ( N + 1 ) = ( # ` F ) /\ N e. NN0 ) ->
                            ( G e. R /\ N = ( # ` G ) ) ) $=
          ( vx vy wcel c1 caddc co chash cfv wceq cn0 w3a cfz cprime wf cv clt
          wbr cle wi wral wa simp3 1arithlem7 biimpi simp2d cres wss cz 1z
          nn0z fzssp1 sylancr fssres sylan2 feq1i sylibr sylan 3impa simp3d
          ssralv ralimdv syld syl wb id 1arithlem2 ax-mp syl6ibr syl5com
          3impia 3jca ) GBQZJRSTZGUAUBUCZJUDQZUEZWIRJUFTZUGHUHZOUIZPUIZUJUKZWMH
          UBWNHUBULUKUMPWKUNOWKUNZUEHBQJHUAUBUCUOWJWIWLWPWFWHWIUPWFWHWIWLWFWHUO
          ZRWGUFTZUGGUHZWIWLWQWGUDQZWSWOWMGUBWNGUBULUKUMZPWRUNZOWRUNZWQWTWSXCUE
          ABCDEFGIWGKOPLMUQURZUSWSWIUOWKUGGWKUTZUHZWLWIWSWKWRVAZXFWIRVBQJVBQXGV
          CJVDRJVEVFZWRUGWKGVGVHWKUGHXENVIVJVKVLWFWHWIWPWQXCWIWPWQWTWSXCXDVMWIX
          CXAPWKUNZOWKUNZWPWIXGXCXJUMXHXGXCXIOWRUNXJXGXBXIOWRXAPWKWRVNVOXIOWKWR
          VNVPVQHXEUCZWPXJVRNXKOPGHJXKVSVTWAWBWCWDWEABCDEFHIJKOPLMUQVJ $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}

      ${
        $d F f z $.
        $( Lemma for ~ 1arith . $)
        1arithlem19 $p |- ( ( F e. R /\ ( N + 1 ) = ( # ` F ) /\ N e. NN0 ) ->
                            ( M ` F ) = ( ( M ` G ) x. ( F ` ( N + 1 ) ) ) ) $=
          ( wcel c1 caddc co chash cfv wceq cn0 cmul cc0 cn wo wa elnn00nn w3a
          cc 0nn0 eleq1 mpbiri nn0p1nn syl cprime cz 1arithlem9 prmz zcn 3syl
          syl3an3 mulid2 c0 1arithlem18 simprd wb eqeq1 3ad2ant3 mpbid eqcomd
          cfn simpld 1arithlem10 hash0 fveq2d 1arithlem12 simpri syl6eq
          opreq1d cseq1 1arithlem13 opreq1 ax-1cn addid2i cvv mulex seq11g
          mpan 3ad2ant1 3eqtrd eqtr4d 3eqtr4rd 3expia seq1p1g cfz cres opreq2i
          fveq1i cuz elnnuz eluzfz2 sylbi ancli seq1resval2g sylan2 3impa
          syl5eq mp3an1 3adant2 peano2nn nnnn0 simp3 syl111anc 3eqtr4d jaod
          syl5bi 3impia ) GBOZJPQRZGSTUAZJUBOZGITZHITZXTGTZUCRZUAZYBJUDUAZJUEOZ
          UFXSYAUGZYGJUHYJYHYGYIXSYAYHYGXSYAYHUIZPYEUCRZYEYFYCYKYEUJOZYLYEUAYHX
          SYAXTUEOZYMYHYBYNYHYBUDUBOUKJUDUBULUMZJUNUOZXSYAYNUIYEUPOYEUQOYMABCDE
          FGIXTKLMURYEUSYEUTVAVBYEVCUOYKYDPYEUCYKYDVDITZPYKHVDIYKHSTZUDUAZHVDUA
          ZYKUDYRYKJYRUAZUDYRUAZYKHBOZUUAYHXSYAYBUUCUUAUGZYOABCDEFGHIJKLMNVEZVB
          ZVFYHXSUUAUUBVGYAJUDYRVHVIVJVKYKUUCHVLOYSYTVGYKUUCUUAUUFVMABCDEFHIKLM
          VNHVOVAVJVPVDBOYQPUAABCDEFIKLMVQVRVSVTYKYCPGTZYEYKYCXTUCGWARZTZPUUHTZ
          UUGYHXSYAYNYCUUIUAZYPABCDEFGIXTKLMWBZVBYKXTPUUHYHXSXTPUAYAYHXTUDPQRPJ
          UDPQWCPWDWEVSVIZVPXSYAUUJUUGUAZYHUCWFOZXSUUNWGWFBUCGWHWIWJWKYKXTPGUUM
          VPWLWMWNXSYAYIYGXSYAYIUIZUUIJUCHWARZTZYEUCRZYCYFXSYIUUIUUSUAZYAUUOXSY
          IUUTWGUUOXSYIUIZUUIJUUHTZYEUCRUUSJWFBUCGWOUVAUURUVBYEUCUVAUURJUCGPJWP
          RZWQZWARZTZUVBJUUQUVEHUVDUCWANWRWSUUOXSYIUVFUVBUAZYIUUOXSUGYIJUVCOZUG
          UVGYIUVHYIJPWTTOUVHJXAPJXBXCXDWFBUCGJJXEXFXGXHVTWLXIXJYIXSYAYNUUKJXKU
          ULVBUUPYDUURYEUCUUPUUCUUAYIYDUURUAUUPUUCUUAYIXSYAYBUUDJXLUUEVBZVMUUPU
          UCUUAUVIVFXSYAYIXMABCDEFHIJKLMWBXNVTXOWNXPXQXR $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}

      ${
        $d F f j k n s $.  $d F f z $.
        $( Lemma for ~ 1arith . $)
        1arithlem20 $p |- ( ( ( F e. R /\ ( N + 1 ) = ( # ` F ) /\ N e. NN0 )
                              /\ P e. Prime ) ->
        ( P || ( M ` F ) <-> ( P || ( M ` G ) \/ P = ( F ` ( N + 1 ) ) ) ) ) $=
          ( cprime wcel c1 caddc co chash cfv wceq cn0 w3a cdivides wbr wo wb
          wa cmul 1arithlem19 breq2d adantl cz cn 1arithlem18 simpld wf
          1arithlem11 ffvelrn mpan nnz 3syl nn0p1nn 1arithlem9 syl3an3 prmz
          syl jca euclemma 3expb sylan2 c2 cuz prmuz2 dvdsprm syl2an orbi2d
          3bitrd ancoms ) BPQZHCQZKRSTZHUAUBUCZKUDQZUEZBHJUBZUFUGZBIJUBZUFUGZBW
          DHUBZUCZUHZUIWBWGUJZWIBWJWLUKTZUFUGZWKBWLUFUGZUHZWNWGWIWQUIWBWGWHWPBU
          FACDEFGHIJKLMNOULUMUNWGWBWJUOQZWLUOQZUJWQWSUIZWGWTXAWGICQZWJUPQZWTWGX
          CKIUAUBUCACDEFGHIJKLMNOUQURCUPJUSXCXDACDEFGJLMNUTCUPIJVAVBWJVCVDWGWLP
          QZXAWFWCWEWDUPQXEKVEACDEFGHJWDLMNVFVGZWLVHVIVJWBWTXAXBBWJWLVKVLVMWOWR
          WMWKWBBVNVOUBQXEWRWMUIWGBVPXFWLBVQVRVSVTWA $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}

      ${
        $( Lemma for ~ 1arith . $)
        1arithlem21 $p |- ( ( ( F e. R /\ ( N + 1 ) = ( # ` F ) /\ N e. NN0 )
                              /\ P e. Prime ) ->
        ( P e. ran F <-> ( P e. ran G \/ P = ( F ` ( N + 1 ) ) ) ) ) $=
          ( wcel c1 caddc co chash cfv wceq cn0 w3a crn csn cun cprime wo wb
          wa cfz wfn wf 1arithlem8 simprd ffn syl cima imaundi fnima cz cle
          wbr nn0z cn nn0p1nn nnge1 1z fzsuc mp3an1 syl11anc imaeq2d sylan9req
          cres rneqi df-ima eqtr4i a1i cuz elnnuz eluzfz2 sylbi fnsnfv sylan2
          uneq12d eqeq12d mpbiri eleq2d sylan 3impa elun elsncg orbi2d syl5bb
          sylan9bb ) HCPZKQRSZHTUAUBZKUCPZUDBHUEZPZBIUEZWRHUAZUFZUGZPZBUHPZBXCP
          ZBXDUBZUIZWQWSWTXBXGUJZWQWSUKZHQWRULSZUMZWTXLXMXNUHHUNZXOXMWRUCPXPACD
          EFGHJWRLMNUOUPXNUHHUQURXOWTUKZXAXFBXQXAXFUBHQKULSZWRUFZUGZUSZHXRUSZHX
          SUSZUGZUBHXRXSUTXQXAYAXFYDXOWTXAHXNUSYAXNHVAWTXNXTHWTKVBPZQWRVCVDZXNX
          TUBZKVEWTWRVFPZYFKVGZWRVHURQVBPYEYFYGVIQKVJVKVLVMVNXQXCYBXEYCXCYBUBXQ
          XCHXRVOZUEYBIYJOVPHXRVQVRVSWTXOWRXNPZXEYCUBWTYHYKYIYHWRQVTUAPYKWRWAQW
          RWBWCURXNWRHWDWEWFWGWHWIWJWKXGXIBXEPZUIXHXKBXCXEWLXHYLXJXIBXDUHWMWNWO
          WP $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}
    $}

    ${
      $d F g m $.  $d P g h m x y $.  $d R f g h m x y z $.
      1arithlem22.3 $e |- P e. Prime $.
      $( Lemma for ~ 1arith . $)
      1arithlem22 $p |- ( F e. R -> ( P e. ran F <-> P || ( M ` F ) ) ) $=
        ( vm vg vx vy vh chash cfv cn0 wcel crn cdivides wbr wb wceq eqid wa
        c1 cfz co cprime wf 1arithlem8 simpld mpan2 wi fvex cv eleq1 wral cc0
        caddc eqeq1 imbi1d ralbidv eqcom c0 noel wn nprmdvds1 ax-mp 2false cfn
        1arithlem10 hash0 syl biimpa rneqd rn0 syl6eq eleq2d fveq2d
        1arithlem12 simpri breq2d bibi12d mpbiri sylan2br ex rgen 1arithlem1
        cbvralv w3a cres 1arithlem18 rcla4v com12 imp3a syl5 wo orbi1
        1arithlem21 1arithlem20 syl5ibr sylcom 3expia com3l imp exp3a
        r19.21aiv syl5bi nn0ind com13 sylbird vtocle mpcom ) HSTZUAUBZHCUBZBHUC
        UBBHITUDUEUFZYAXSXSUGZXTXSUHYAYCUIXTUJXSUKULUMHUNACDEFGHIXSJKLUOUPUQXTY
        AYBURZURNXSHSUSNUTZXSUGZXTYEUAUBZYDYEXSUAVAYAYGYFYBYGYEOUTZSTZUGZBYHUCZ
        UBZBYHITZUDUEZUFZURZOCVBZYAYFYBURZPUTZYIUGZYOURZOCVBVCYIUGZYOURZOCVBQUT
        ZYIUGZYOURZOCVBZUUDUJVDULZYIUGZYOURZOCVBZYQPQYEYSVCUGZUUAUUCOCUULYTUUBY
        OYSVCYIVEVFVGYSUUDUGZUUAUUFOCUUMYTUUEYOYSUUDYIVEVFVGYSUUHUGZUUAUUJOCUUN
        YTUUIYOYSUUHYIVEVFVGYSYEUGZUUAYPOCUUOYTYJYOYSYEYIVEVFVGUUCOCYHCUBZUUBYO
        UUBUUPYIVCUGZYOYIVCVHUUPUUQUIZYOBVIUBZBUJUDUEZUFUUSUUTBVJBUMUBZUUTVKMBV
        LVMVNUURYLUUSYNUUTUURYKVIBUURYKVIUCVIUURYHVIUUPUUQYHVIUGZUUPYHVOUBUUQUV
        BUFACDEFGYHIJKLVPYHVQVRVSZVTWAWBWCUURYMUJBUDUURYMVIITZUJUURYHVIIUVCWDVI
        CUBUVDUJUGACDEFGIJKLWEWFWBWGWHWIWJWKWLUUGUUDRUTZSTUGBUVEUCUBBUVEITUDUEU
        FURZRCVBZUUDUAUBZUUKUUFUVFORCBYHUVEIUUDWMWNUVHUVGUUKUVHUVGUIZUUJOCUVIUU
        PUUIYOUVHUVGUUPUUIUIZYOURUVJUVHUVGYOUUPUUIUVHUVGYOURUVGUUPUUIUVHWOZYOUV
        GUVKBYHUJUUDUKULWPZUCUBZBUVLITUDUEZUFZYOUVKUVLCUBZUUDUVLSTUGZUIUVGUVOAC
        DEFGYHUVLIUUDJKLUVLUHZWQUVGUVPUVQUVOUVPUVGUVQUVOURZUVFUVSRUVLCBUVEUVLIU
        UDWMWRWSWTXAUVKUVAUVOYOURMUVOYOUVKUVAUIZUVMBUUHYHTUGZXBZUVNUWAXBZUFUVMU
        VNUWAXCUVTYLUWBYNUWCABCDEFGYHUVLIUUDJKLUVRXDABCDEFGYHUVLIUUDJKLUVRXEWHX
        FUQXGWSXHXIXJXKXLWKXMXNYPYROHCBYHHIYEWMWRXAXOXPXQXR $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( A prime number ` P ` appears in the prime factorization ` F ` of a
         positive integer ` Z ` iff it divides ` Z ` .  The proof uses Euclid's
         lemma ~ euclemma .  Lemma for ~ 1arith .  (Contributed by Paul
         Chapman, 17-Nov-2012.) $)
      1arithprmb $p |- ( ( F e. R /\ Z = ( M ` F ) /\ P e. Prime ) ->
                         ( P e. ran F <-> P || Z ) ) $=
        ( wcel cfv wceq cprime w3a crn cdivides wbr wb wi c2 cif eleq1 breq1
        bibi12d imbi2d 2prm elimel 1arithlem22 dedth impcom 3adant2 breq2
        3ad2ant2 bitr4d ) HCNZJHIOZPZBQNZRBHSZNZBUTTUAZBJTUAZUSVBVDVEUBZVAVBUSV
        GVBUSVGUCUSVBBUDUEZVCNZVHUTTUAZUBZUCBUDBVHPZVGVKUSVLVDVIVEVJBVHVCUFBVHU
        TTUGUHUIAVHCDEFGHIKLMBUDQUJUKULUMUNUOVAUSVFVEUBVBJUTBTUPUQUR $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      1arithlem23.3 $e |- S = { p e. Prime | p || Z } $.
      1arithlem23.4 $e |- Q = sup ( S , RR , < ) $.

      ${
        $d F q z $.  $d Q q z $.  $d S x y $.  $d Z x y $.
        $( Lemma for ~ 1arith . $)
        1arithlem23 $p |- ( ( F e. R /\ Z = ( M ` F ) /\ Z e. ( ZZ>= ` 2 ) ) ->
                            ( F ` ( # ` F ) ) = Q ) $=
          ( vq vy vx wcel cfv wceq c2 cuz w3a chash cle wbr wa cprime cdivides
          cn simp1 eqidd simp3 simp2 eleq1d mpbid 1arithlem14 biimpar syl11anc
          1arithlem9 syl111anc crn c1 cfz co wf cn0 1arithlem8 simprd wfn ffn
          elnnuz eluzfz2 sylbi fnfvelrn syl2an wb 1arithprmb syld3an3 cv breq1
          crab cbvrabv eqtri elrab2 sylanbrc cr clt csup wss c0 wne wral wrex
          maxprmfct syl simpld suprub sylan syl6breqr mpdan syl5eqel wi
          biimprd 3expia imp3a syl5bi 1arithlem16 syld mpd jca cz prmz zre
          3syl 1arithlem3 simp1d letri3 mpbird ) ICUAZKIJUBZUCZKUDUEUBZUAZUFZIU
          GUBZIUBZBUCZYJBUHUIZBYJUHUIZUJZYHYLYMYHYJDUAZYLYHYJUKUAZYJKULUIZYOYHY
          CYIYIUCZYIUMUAZYPYCYEYGUNZYHYIUOZYHYCYDYFUAZYSYTYHYGUUBYCYEYGUPZYHKYD
          YFYCYEYGUQZURUSYCYSUUBACEFGHIJLNOUTVAVBZACEFGHIJYILNOVCVDZYHYJIVEZUAZ
          YQYHVFYIVGVHZUKIVIZYSUUHYHYCYRUUJYTUUAYCYRUJYIVJUAUUJACEFGHIJYILNOVKV
          LVBUUEUUJIUUIVMYIUUIUAZUUHYSUUIUKIVNYSYIVFUEUBUAUUKYIVOVFYIVPVQUUIYII
          VRVSVBYCYEYGYPUUHYQVTUUFAYJCEFGHIJKLNOWAWBUSRWCZKULUIZYQRYJUKDUULYJKU
          LWDDMWCZKULUIZMUKWEUUMRUKWEPUUOUUMMRUKUUNUULKULWDWFWGZWHWIYHYOUJYJDWJ
          WKWLZBUHYHDWJWMDWNWOSWCTWCUHUISDWPTWJWQUFZYOYJUUQUHUIYHUURUUQDUAZYHYG
          UURUUSUJUUCTSMDKPWRWSZWTTSDYJXAXBQXCXDYHBDUAZYMYHBUUQDQYHUURUUSUUTVLX
          EYHUVABUUGUAZYMUVABUKUAZBKULUIZUJYHUVBUUMUVDRBUKDUULBKULWDUUPWHYHUVCU
          VDUVBYHYCYEUVCUVDUVBXFZXFYTUUDYCYEUVCUVEYCYEUVCUFUVBUVDABCEFGHIJKLNOW
          AXGXHVBXIXJYHYCYRYSUVBYMXFYTUUAUUEABCEFGHIJYILNOXKVDXLXMXNYHYJWJUAZBW
          JUAZYKYNVTYHYPYJXOUAUVFUUFYJXPYJXQXRYHUVCBXOUAUVGYHUVCUVDYOYLXFZYHYGU
          VCUVDUVHUFUUCBDYJKMPQXSWSXTBXPBXQXRYJBYAVBYB $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}

      ${
        $d G j k n s x y $.  $d N f j k n s x y $.  $d N f z $.  $d ph x y $.
        1arithlem24.5 $e |- G = ( F u. { <. ( N + 1 ) , Q >. } ) $.
        1arithlem24.6 $e |- N = ( # ` F ) $.
        1arithlem24.7 $e |- ( ph <->
          ( F e. R /\ Z e. ( ZZ>= ` 2 ) /\ ( Z / Q ) = ( M ` F ) ) ) $.
        $( Lemma for ~ 1arith . $)
        1arithlem24 $p |- ( ph -> ( G e. R /\ Z = ( M ` G ) ) ) $=
          ( vx vy wcel cfv wceq c1 caddc co chash cn0 cfz cprime wf cv clt wbr
          cle wi wral wa w3a c2 cuz cdiv simp1bi 1arithlem7 biimpi sylancl
          simp1d peano2nn0 syl cres simp2d cdivides simp2bi 1arithlem3 cop csn
          cun eqid fseq1p1m1 biimpa mp3anr3 syl12anc simp3d 1arithlem2 mpbid
          ffvelrn sylan cmul crn wfn ffn fnfvelrn wb adantr eqidd 1arithprmb
          syl111anc cz prmz cn 1arithlem11 mpan nnz 3syl dvdsmultr1 mpd prmnn
          simp3bi 1arithlem17 syl31anc breq2d mpbird breq1 elrab2 sylanbrc
          fveq1d fvres sylan9eq eqcomd breq12d r19.21aiva 1arithlem5 biimpri
          simpld fveq2d opreq12d simprd 1arithlem19 3eqtr4d jca ) AKDUFZNKLUGZU
          HAYPMUIUJUKZKULUGUHZAYRUMUFZUIYRUNUKZUOKUPZUDUQZUEUQZURUSZUUCKUGZUUDK
          UGUTUSVAZUEUUAVBUDUUAVBZYPYSVCZAMUMUFZYTAUUJUIMUNUKZUOJUPZUUEUUCJUGZU
          UDJUGUTUSVAUEUUKVBUDUUKVBZAJDUFZMJULUGUHZUUJUULUUNVDZAUUONVEVFUGUFZNC
          VGUKJLUGZUHZUCVHZUBUUOUUPVCUUQBDFGHIJLMOUDUEQRVIVJVKZVLZMVMVNAUUBYRKU
          GZCUHZJKUUKVOZUHZAUUJUULCUOUFZUUBUVEUVGVDZUVCAUUJUULUUNUVBVPZAUVHCNVQ
          USZUUMEUFZUUMCUTUSZVAZAUURUVHUVKUVNVDAUUOUURUUTUCVRZCEUUMNPSTVSVNZVLZ
          UUJUULUVHKJYRCVTWAZWBUHZUVIUAUUJUULUVHUVSVDUVIUOCJKUVRMUVRWCWDWEWFWGZ
          VLAUUJUUGUEUUKVBUDUUKVBZUUFUVDUTUSZUDUUKVBUUHUVCAUUNUWAAUUJUULUUNUVBW
          HAUDUEKJMAUUBUVEUVGUVTWHZWIWJAUWBUDUUKAUUCUUKUFZVCZUVMUWBUWEUVLUVMUWE
          UUMUOUFZUUMNVQUSZUVLAUULUWDUWFUVJUUKUOUUCJWKWLZUWEUWGUUMUUSCWMUKZVQUS
          ZUWEUUMUUSVQUSZUWJUWEUUMJWNUFZUWKAJUUKWOZUWDUWLAUULUWMUVJUUKUOJWPVNUU
          KUUCJWQWLUWEUUOUUSUUSUHUWFUWLUWKWRAUUOUWDUVAWSUWEUUSWTUWHBUUMDFGHIJLU
          USOQRXAXBWJUWEUUMXCUFZUUSXCUFZCXCUFZUWKUWJVAUWEUWFUWNUWHUUMXDVNAUWOUW
          DAUUOUUSXEUFZUWOUVADXELUPUUOUWQBDFGHILOQRXFDXEJLWKXGUUSXHXIWSAUWPUWDA
          UVHUWPUVQCXDVNWSUUMUUSCXJXBXKAUWGUWJWRUWDANUWIUUMVQAUUOUURCXEUFZUUTNU
          WIUHZUVAUVOAUVHUWRUVQCXLVNAUUOUURUUTUCXMUUOUURUWRVDUUTUWSBCDFGHIJLNOQ
          RXNWEXOZXPWSXQPUQZNVQUSUWGPUUMUOEUXAUUMNVQXRSXSXTAUVNUWDAUVHUVKUVNUVP
          WHWSXKUWEUUMUUFCUVDUTAUWDUUMUUCUVFUGUUFAUUCJUVFUWCYAUUCUUKKYBYCACUVDU
          HUWDAUVDCAUUBUVEUVGUVTVPYDZWSYEWJYFUDUEKMYGXBUUIYTUUBUUHVDBDFGHIKLYRO
          UDUEQRVIYHXBZYIZAUWIUVFLUGZUVDWMUKZNYQAUUSUXECUVDWMAJUVFLUWCYJUXBYKUW
          TAYPYSUUJYQUXFUHUXDAYPYSUXCYLUVCBDFGHIKUVFLMOQRUVFWCYMXBYNYO $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}
    $}

    ${
      $d G g $.  $d N f j k n s $.  $d N f z $.  $d Q h y $.  $d h p x $.
      1arithlem25.3 $e |- S = { p e. Prime | p || x } $.
      1arithlem25.4 $e |- Q = sup ( S , RR , < ) $.
      1arithlem25.5 $e |- G = ( h u. { <. ( N + 1 ) , Q >. } ) $.
      1arithlem25.6 $e |- N = ( # ` h ) $.
      $( Lemma for ~ 1arith . $)
      1arithlem25 $p |- ( x e. ( ZZ>= ` 2 ) -> ( A. y e. NN
      ( y < x -> E. g e. R y = ( M ` g ) ) -> E. g e. R x = ( M ` g ) ) ) $=
        ( cv c2 cuz cfv wcel clt wbr wceq wrex wi cn wral cdiv co 1arithlem4
        simprd simpld breq1 fveq2 eqeq2d cbvrexv eqeq1 rexbidv syl5bb imbi12d
        rcla4v syl mpid w3a wa biid 1arithlem24 rcla4ev 3expia ancoms
        r19.23adva syld ) AUDZUEUFUGUHZBUDZWAUIUJZWCHUDZNUGZUKZHEULZUMZBUNUOZWA
        DUPUQZIUDZNUGZUKZIEULZWAWFUKZHEULZWBWJWKWAUIUJZWOWBWKUNUHZWRDFWAQTUAURZ
        USWBWSWJWRWOUMZUMWBWSWRWTUTWIXABWKUNWCWKUKZWDWRWHWOWCWKWAUIVAWHWCWMUKZI
        EULXBWOWGXCHIEWEWLUKWFWMWCWEWLNVBVCVDXBXCWNIEWCWKWMVEVFVGVHVIVJVKWBWNWQ
        IEWLEUHZWBWNWQUMXDWBWNWQXDWBWNVLZMEUHWAMNUGZUKZVMWQXECDEFGJKLWLMNOWAPQR
        STUAUBUCXEVNVOWPXGHMEWEMUKWFXFWAWEMNVBVCVPVJVQVRVSVT $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d R f h x z $.  $d Z g x $.  $d f p x z $.
      $( Lemma for ~ 1arith . $)
      1arithlem26 $p |- ( Z e. NN -> E. g e. R Z = ( M ` g ) ) $=
        ( vx vy vp vh cv cfv wceq wrex cn eqeq1 rexbidv c1 c0 wcel wa
        1arithlem12 eqcom anbi2i mpbi fveq2 eqeq2d rcla4ev ax-mp cdivides wbr
        cprime crab cr clt csup chash caddc co cop csn cun eqid 1arithlem25
        indstr2 vtoclga ) MQZDQZHRZSZDBTZIVOSZDBTMIUAVMISVPVRDBVMIVOUBUCVQNQZVO
        SZDBTUDVOSZDBTZMNVMUDSVPWADBVMUDVOUBUCVMVSSVPVTDBVMVSVOUBUCUEBUFZUDUEHR
        ZSZUGZWBWCWDUDSZUGWFABCEFGHJKLUHWGWEWCWDUDUIUJUKWAWEDUEBVNUESVOWDUDVNUE
        HULUMUNUOMNAOQVMUPUQOURUSZUTVAVBZBWHCDPEFGPQZWJVCRZUDVDVEWIVFVGVHZHWKJO
        KLWHVIWIVIWLVIWKVIVJVKVL $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      1arithlem27.3 $e |- N = ( # ` F ) $.
      $( Lemma for ~ 1arith . $)
      1arithlem27 $p |- ( F e. R ->
          dom ( F |` ( 1 ... ( N - 1 ) ) ) = ( 1 ... ( N - 1 ) ) ) $=
        ( wcel c1 cmin co cfz cdm wss cres wceq cn0 cprime wf chash cfv wa
        1arithlem8 mpan2 simpld caddc cz nn0z peano2zm 1z fzssp1 mpan 3syl cc
        nn0cn ax-1cn npcan syl opreq2d sseqtrd simprd fdm sseqtr4d ssdmres
        sylib ) GBNZOIOPQZRQZGSZTGVNUASVNUBVLVNOIRQZVOVLIUCNZVNVPTVLVQVPUDGUEZV
        LIGUFUGUBVQVRUHMABCDEFGHIJKLUIUJZUKVQVNOVMOULQZRQZVPVQIUMNVMUMNZVNWATZI
        UNIUOOUMNWBWCUPOVMUQURUSVQVTIORVQIUTNZVTIUBZIVAWDOUTNWEVBIOVCUJVDVEVFVD
        VLVRVOVPUBVLVQVRVSVGVPUDGVHVDVIVNGVJVK $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      1arithlem28.3 $e |- S = { p e. Prime | p || Z } $.
      1arithlem28.4 $e |- Q = sup ( S , RR , < ) $.

      ${
        $d F f j k n s $.  $d F f z $.  $d Q z $.
        1arithlem28.5 $e |- N = ( ( # ` F ) - 1 ) $.
        1arithlem28.6 $e |- G = ( F |` ( 1 ... N ) ) $.
        $( Lemma for ~ 1arith . $)
        1arithlem28 $p |- ( ( F e. R /\ Z = ( M ` F ) /\ Z e. ( ZZ>= ` 2 ) ) ->
                            ( G e. R /\ ( Z / Q ) = ( M ` G ) ) ) $=
          ( cK wcel cfv wceq c2 cuz w3a cdiv co chash c1 caddc cn0 wa simp1
          cmin opreq1i cc cn simp2 simp3 eleq1 biimpa syl11anc 1arithlem14
          biimpar nncn syl ax-1cn a1i npcan syl5eq nnm1nn0 syl5eqel
          1arithlem18 syl111anc simpld cmul 1arithlem19 fveq2d 1arithlem23
          eqtrd opreq2d wb eqeq1 mpbird cprime cdivides wbr cle wi 1arithlem3
          simp1d prmnn 3syl 1arithlem17 jca ) ICUCZMIKUDZUEZMUFUGUDZUCZUHZJCUCZ
          MBUIUJJKUDZUEZXDXELJUKUDUEZXDWSLULUMUJZIUKUDZUEZLUNUCZXEXHUOWSXAXCUPZ
          XDXIXJULUQUJZULUMUJZXJLXNULUMTURXDXJUSUCZULUSUCZXOXJUEXDXJUTUCZXPXDWS
          WTXBUCZXRXMXDXAXCXSWSXAXCVAZWSXAXCVBZXAXCXSMWTXBVCVDVEWSXRXSACEFGHIKN
          PQVFVGVEZXJVHVIXQXDVJVKXJULVLVEVMZXDLXNUNTXDXRXNUNUCYBXJVNVIVOZACEFGH
          IJKLNPQUAVPVQVRZXDXGMXFBVSUJZUEZXDYGWTYFUEZXDWTXFXIIUDZVSUJZYFXDWSXKX
          LWTYJUEXMYCYDACEFGHIJKLNPQUAVTVQXDYIBXFVSXDYIXJIUDBXDXIXJIYCWAABCDEFG
          HIKMNOPQRSWBWCWDWCXDXAYGYHWEXTMWTYFWFVIWGXDXEXCBUTUCZXGYGWEYEYAXDXCBW
          HUCZYKYAXCYLBMWIWJUBDUCUBBWKWJWLBDUBMORSWMWNBWOWPABCEFGHJKMNPQWQVQWGW
          R $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}

      ${
        1arithlem29.5 $e |- G = ( g |` ( 1 ... ( ( # ` g ) - 1 ) ) ) $.
        1arithlem29.6 $e |- H = ( h |` ( 1 ... ( ( # ` h ) - 1 ) ) ) $.

        ${
          $d Q z $.
          1arithlem29.7 $e |- ( ph <-> ( Z e. ( ZZ>= ` 2 ) /\
            ( g e. R /\ h e. R ) /\ ( Z = ( M ` g ) /\ Z = ( M ` h ) ) ) ) $.
          $( Lemma for ~ 1arith . $)
          1arithlem29 $p |- ( ( ph /\ G = H ) -> g = h ) $=
            ( wceq wa cv chash cfv cop csn cun simpr c1 cmin co cfz cdm dmeq
            adantl wb wcel c2 cuz simp2bi simpld simprd cres dmeqi eqid
            1arithlem27 syl5eq eqeqan12d syl11anc adantr mpbid cn0 cn simp3bi
            simp1bi eqeltrrd 1arithlem14 biimpar nnm1nn0 syl fz1eqb cc nncn
            ax-1cn a1i subcan2 syl111anc opeq1d sneqd uneq12d cprime wf w3a
            eqidd 1arithlem8 1arithlem23 fseq1m1p1 syl13anc simp3d eqeq12d
            mpbird ) ALMUEZUFZGUGZHUGZUEZLXIUHUIZCUJZUKZULZMXJUHUIZCUJZUKZULZUE
            ZXHLMXNXRAXGUMXHXMXQXHXLXPCXHXLUNUOUPZXPUNUOUPZUEZXLXPUEZXHUNYAUQUP
            ZUNYBUQUPZUEZYCXHLURZMURZUEZYGXGYJALMUSUTAYJYGVAZXGAXIDVBZXJDVBZYKA
            YLYMAOVCVDUIZVBZYLYMUFZOXINUIZUEZOXJNUIZUEZUFZUDVEZVFZAYLYMUUBVGZYL
            YMYHYEYIYFYLYHXIYEVHZURYELUUEUBVIBDFIJKXINXLPRSXLVJVKVLYMYIXJYFVHZU
            RYFMUUFUCVIBDFIJKXJNXPPRSXPVJVKVLVMVNVOVPAYGYCVAZXGAYAVQVBZYBVQVBZU
            UGAXLVRVBZUUHAYLYQYNVBZUUJUUCAOYQYNAYRYTAYOYPUUAUDVSZVFZAYOYPUUAUDV
            TZWAYLUUJUUKBDFIJKXINPRSWBWCVNZXLWDWEAXPVRVBZUUIAYMYSYNVBZUUPUUDAOY
            SYNAYRYTUULVGZUUNWAYMUUPUUQBDFIJKXJNPRSWBWCVNZXPWDWEYAYBWFVNVOVPAYC
            YDVAZXGAXLWGVBZXPWGVBZUNWGVBZUUTAUUJUVAUUOXLWHWEAUUPUVBUUSXPWHWEUVC
            AWIWJXLXPUNWKWLVOVPWMWNWOAXKXTVAXGAXIXOXJXSAYEWPLWQZCWPVBZXIXOUEZAU
            UJUNXLUQUPWPXIWQZXLXIUICUEZLUUEUEZUVDUVEUVFWRZUUOAXLVQVBZUVGAYLXLXL
            UEUVKUVGUFUUCAXLWSBDFIJKXINXLPRSWTVNVGAYLYRYOUVHUUCUUMUUNBCDEFIJKXI
            NOPQRSTUAXAWLUVIAUBWJUUJUVJUVGUVHUVIWRWPCLXIXNXLXNVJXBWCXCXDAYFWPMW
            QZUVEXJXSUEZAUUPUNXPUQUPWPXJWQZXPXJUICUEZMUUFUEZUVLUVEUVMWRZUUSAXPV
            QVBZUVNAYMXPXPUEUVRUVNUFUUDAXPWSBDFIJKXJNXPPRSWTVNVGAYMYTYOUVOUUDUU
            RUUNBCDEFIJKXJNOPQRSTUAXAWLUVPAUCWJUUPUVQUVNUVOUVPWRWPCMXJXRXPXRVJX
            BWCXCXDXEVOXF $.
            $( [21-Nov-2012] $) $( [17-Nov-2012] $)
        $}

        ${
          $d G u v $.  $d H v z $.  $d Q g h u v y $.  $d Q g h z $.
          $d Z g h u v y $.
          $( Lemma for ~ 1arith . $)
          1arithlem30 $p |- ( Z e. ( ZZ>= ` 2 ) ->
              ( A. y e. NN ( y < Z -> A. g e. R A. h e. R
                ( ( y = ( M ` g ) /\ y = ( M ` h ) ) -> g = h ) ) ->
    A. g e. R A. h e. R ( ( Z = ( M ` g ) /\ Z = ( M ` h ) ) -> g = h ) ) ) $=
            ( vu vv cv clt wbr cfv wceq wa wi wral cn c2 cuz wcel fveq2 eqeq2d
            anbi1d eqeq1 imbi12d anbi2d eqeq2 cbvral2v imbi2i ralbii cdiv co
            1arithlem4 simprd simpld breq1 anbi12d imbi1d 2ralbidv rcla4v syl
            mpid syl6ib rcla42v com12 syl6 impexp syl6ibr w3a biid 1arithlem29
            ex 3expib wb simp2l simp3l simp1 chash c1 cmin eqid 1arithlem28
            syl111anc simp2r simp3r jca an4 sylib imim21b mpbird syld syl5bir
            r19.21advv syl5bi ) AUFZOUGUHZXLGUFZNUIZUJZXLHUFZNUIZUJZUKZXNXQUJZU
            LZHDUMGDUMZULZAUNUMZXMXLUDUFZNUIZUJZXLUEUFZNUIZUJZUKZYFYIUJZULZUEDU
            MUDDUMZULZAUNUMZOUOUPUIUQZOXOUJZOXRUJZUKZYAULZHDUMGDUMYDYPAUNYCYOXM
            YBYNYHXSUKZYFXQUJZULGHUDUEDDXNYFUJZXTUUCYAUUDUUEXPYHXSUUEXOYGXLXNYF
            NURZUSUTXNYFXQVAZVBXQYIUJZUUCYLUUDYMUUHXSYKYHUUHXRYJXLXQYINURZUSVCX
            QYIYFVDZVBVEVFVGZYRYQUUBGHDDYQYEYRXNDUQZXQDUQZUKZUUBULZUUKYRYEUUNUU
            AUKZYAULZUUOYRYELDUQZMDUQZUKZOCVHVIZLNUIZUJZUVAMNUIZUJZUKZUKZLMUJZU
            LZUUQYRYEUUTUVFUVHULZULZUVIYRYEUVAYGUJZUVAYJUJZUKZYMULZUEDUMUDDUMZU
            VKYRYEUVAXOUJZUVAXRUJZUKZYAULZHDUMGDUMZUVPYRYEUVAOUGUHZUWAYRUVAUNUQ
            ZUWBCEOQTUAVJZVKYRUWCYEUWBUWAULZULYRUWCUWBUWDVLYDUWEAUVAUNXLUVAUJZX
            MUWBYCUWAXLUVAOUGVMUWFYBUVTGHDDUWFXTUVSYAUWFXPUVQXSUVRXLUVAXOVAXLUV
            AXRVAVNVOVPVBVQVRVSUVTUVOUVLUVRUKZUUDULGHUDUEDDUUEUVSUWGYAUUDUUEUVQ
            UVLUVRUUEXOYGUVAUUFUSUTUUGVBUUHUWGUVNUUDYMUUHUVRUVMUVLUUHXRYJUVAUUI
            USVCUUJVBVEVTUUTUVPUVJUVOUVJUVCUVMUKZLYIUJZULUDUELMDDYFLUJZUVNUWHYM
            UWIUWJUVLUVCUVMUWJYGUVBUVAYFLNURUSUTYFLYIVAVBYIMUJZUWHUVFUWIUVHUWKU
            VMUVEUVCUWKYJUVDUVAYIMNURUSVCYIMLVDVBWAWBWCUUTUVFUVHWDWEYRUVIUUQULZ
            UUPUVHYAULZULZYRUUNUUAUWMYRUUNUUAWFZUVHYAUWOBCDEFGHIJKLMNOPQRSTUAUB
            UCUWOWGWHWIWJYRUUPUVGULUWLUWNWKYRUUNUUAUVGUWOUURUVCUKZUUSUVEUKZUKUV
            GUWOUWPUWQUWOUULYSYRUWPYRUULUUMUUAWLYRUUNYSYTWMYRUUNUUAWNZBCDEFIJKX
            NLNXNWOUIWPWQVIZOPQRSTUAUWSWRUBWSWTUWOUUMYTYRUWQYRUULUUMUUAXAYRUUNY
            SYTXBUWRBCDEFIJKXQMNXQWOUIWPWQVIZOPQRSTUAUWTWRUCWSWTXCUURUVCUUSUVEX
            DXEWJUVGUUPUVHYAXFVRXGXHUUNUUAYAWDVTXIXJXK $.
            $( [21-Nov-2012] $) $( [17-Nov-2012] $)
        $}
      $}
    $}

    ${
      $d G h x $.  $d p x z $.
      $( Lemma for ~ 1arith . $)
      1arithlem31 $p |- ( ( F e. R /\ G e. R ) ->
                          ( ( M ` F ) = ( M ` G ) -> F = G ) ) $=
        ( vx vg vh vy vp wcel wa cfv cn wceq wi wf 1arithlem11 ffvelrn mpan
        adantr cv wrex risset simpl eqeq1 biimpar jca wral c1 anbi12d imbi1d
        2ralbidv c0 eqcom anbi12i chash cc0 1arithlem15 bi2anan9 cfn wb
        1arithlem10 hash0 syl bitr3d syl5bbr eqtr3 syl6bi rgen2 cdivides wbr
        cprime crab cr clt csup cmin co cfz cres eqid 1arithlem30 indstr2
        fveq2 eqeq2d anbi1d imbi12d anbi2d eqeq2 rcla42v syl5 syl7 exp4a
        r19.23adv syl5bi mpd ) GBRZHBRZSZGITZUARZXHHITZUBZGHUBZUCZXEXIXFBUAIUDX
        EXIABCDEFIJKLUEBUAGIUFUGUHXIMUIZXHUBZMUAUJXGXMMXHUAUKXGXOXMMUAXGXNUARZX
        OXKXLXOXKSZXOXNXJUBZSZXGXPXLXQXOXRXOXKULXOXRXKXNXHXJUMUNUOXPXNNUIZITZUB
        ZXNOUIZITZUBZSZXTYCUBZUCZOBUPNBUPZXGXSXLUCZYIPUIZYAUBZYKYDUBZSZYGUCZOBU
        PNBUPUQYAUBZUQYDUBZSZYGUCZOBUPNBUPMPXNUQUBZYHYSNOBBYTYFYRYGYTYBYPYEYQXN
        UQYAUMXNUQYDUMURUSUTXNYKUBZYHYONOBBUUAYFYNYGUUAYBYLYEYMXNYKYAUMXNYKYDUM
        URUSUTYSNOBBXTBRZYCBRZSZYRXTVAUBZYCVAUBZSZYGYRYAUQUBZYDUQUBZSZUUDUUGUUH
        YPUUIYQYAUQVBYDUQVBVCUUDXTVDTZVEUBZYCVDTZVEUBZSUUJUUGUUBUULUUHUUCUUNUUI
        ABCDEFXTIJKLVFABCDEFYCIJKLVFVGUUBUULUUEUUCUUNUUFUUBXTVHRUULUUEVIABCDEFX
        TIJKLVJXTVKVLUUCYCVHRUUNUUFVIABCDEFYCIJKLVJYCVKVLVGVMVNXTYCVAVOVPVQPAQU
        IXNVRVSQVTWAZWBWCWDZBUUOCNODEFXTUQUUKUQWEWFWGWFWHZYCUQUUMUQWEWFWGWFWHZI
        XNJQKLUUOWIUUPWIUUQWIUURWIWJWKYHYJXOYESZGYCUBZUCNOGHBBXTGUBZYFUUSYGUUTU
        VAYBXOYEUVAYAXHXNXTGIWLWMWNXTGYCUMWOYCHUBZUUSXSUUTXLUVBYEXRXOUVBYDXJXNY
        CHIWLWMWPYCHGWQWOWRWSWTXAXBXCXD $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Fundamental theorem of arithmetic, where a prime factorization is
         represented as a finite monotonic 1-based sequence of primes.  The
         function ` M ` maps the set of prime factorizations ` R ` one-to-one
         onto the positive integers.  (Contributed by Paul Chapman,
         17-Nov-2012.) $)
      1arith $p |- M : R -1-1-onto-> NN $=
        ( vg vh cn wf1o wf1 wfo df-f1o wf cv cfv wceq wi wral dff13
        1arithlem11 1arithlem31 rgen2 mpbir2an wrex dffo3 1arithlem26 rgen ) BM
        GNBMGOZBMGPZBMGQUMBMGRZKSZGTZLSZGTUAUPURUAUBZLBUCKBUCKLBMGUDABCDEFGHIJU
        EZUSKLBBABCDEFUPURGHIJUFUGUHUNUOASZUQUAKBUIZAMUCKABMGUJUTVBAMABCKDEFGVA
        HIJUKULUHUH $.
        $( [17-Nov-2012] $)
    $}

    ${
      $( Fundamental theorem of arithmetic, where a prime factorization is
         represented as a finite monotonic 1-based sequence of primes.  Every
         positive integer has a unique prime factorization.  (Contributed by
         Paul Chapman, 17-Nov-2012.) $)
      1arith2 $p |- A. z e. NN E! g e. R ( M ` g ) = z $=
        ( cv cfv wceq wreu cn wf1o wcel 1arith f1ofveu mpan rgen ) DLHMALZNDBOZ
        APBPHQUCPRUDABCEFGHIJKSDBPUCHTUAUB $.
        $( [17-Nov-2012] $)
    $}
  $}


