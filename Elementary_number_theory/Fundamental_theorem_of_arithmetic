$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Fundamental theorem of arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Lemma for ~ 1arith . $)
  1arithlem1 $p |- ( F = G ->
      ( ( N = ( # ` F ) -> ( P e. ran F <-> P || ( M ` F ) ) ) <->
        ( N = ( # ` G ) -> ( P e. ran G <-> P || ( M ` G ) ) ) ) ) $=
    ( wceq chash cfv crn wcel cdivides wbr wb eqeq2d rneq eleq2d breq2d bibi12d
    fveq2 imbi12d ) BCFZEBGHZFECGHZFABIZJZABDHZKLZMACIZJZACDHZKLZMUAUBUCEBCGSNU
    AUEUIUGUKUAUDUHABCOPUAUFUJAKBCDSQRT $.
    $( [21-Nov-2012] $) $( [17-Nov-2012] $)

  ${
    $d N k $.  $d j k ph $.
    1arithlem2.1 $e |- ( ph -> G = ( F |` ( 1 ... N ) ) ) $.
    $( Lemma for ~ 1arith . $)
    1arithlem2 $p |- ( ph -> ( A. j e. ( 1 ... N ) A. k e. ( 1 ... N )
                               ( j < k -> ( G ` j ) <_ ( G ` k ) ) <->
                               A. j e. ( 1 ... N ) A. k e. ( 1 ... N )
                               ( j < k -> ( F ` j ) <_ ( F ` k ) ) ) ) $=
      ( cv clt wbr cfv cle wi c1 cfz co wcel wa fveq1d fvres breqan12d sylan9bb
      cres breq12d imbi2d 2ralbidva ) ABHZCHZIJZUGEKZUHEKZLJZMUIUGDKZUHDKZLJZMB
      CNFOPZUPAUGUPQZUHUPQZRZRULUOUIAULUGDUPUCZKZUHUTKZLJUSUOAUJVAUKVBLAUGEUTGS
      AUHEUTGSUDUQURVAUMVBUNLUGUPDTUHUPDTUAUBUEUF $.
      $( [21-Nov-2012] $) $( [17-Nov-2012] $)
  $}

  ${
    1arithlem3.1 $e |- S = { p e. Prime | p || Z } $.
    1arithlem3.2 $e |- Q = sup ( S , RR , < ) $.

    ${
      $d Q q y $.  $d S x y $.  $d Z p q x y $.  $d p y $.
      $( Lemma for ~ 1arith . $)
      1arithlem3 $p |- ( Z e. ( ZZ>= ` 2 ) ->
                         ( Q e. Prime /\ Q || Z /\ ( K e. S -> K <_ Q ) ) ) $=
        ( vy vx vq wcel cprime cdivides wbr cle wa cr cv simprd breq1 c2 cuz wi
        cfv clt csup wss c0 wne wral wrex maxprmfct syl5eqel crab cbvrabv eqtri
        w3a elrab2 sylib simpld suprub sylan syl6breqr ex 3jca ) DUAUBUDKZALKZA
        DMNZCBKZCAONZUCVFVGVHVFABKVGVHPVFABQUEUFZBGVFBQUGBUHUIHRIRONHBUJIQUKUQZ
        VKBKZIHEBDFULZSUMJRZDMNZVHJALBVOADMTBERZDMNZELUNVPJLUNFVRVPEJLVQVODMTUO
        UPURUSZUTVFVGVHVSSVFVIVJVFVIPCVKAOVFVLVICVKONVFVLVMVNUTIHBCVAVBGVCVDVE
        $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)

      $( Lemma for ~ 1arith . $)
      1arithlem4 $p |- ( Z e. ( ZZ>= ` 2 ) ->
                         ( ( Z / Q ) e. NN /\ ( Z / Q ) < Z ) ) $=
        ( cK vy c2 wcel co cn clt wbr cz cc0 cmul wceq wb syl cuz cdiv cdivides
        cfv cprime cle wi 1arithlem3 simp2d cv wrex simp1d prmz eluzelz divides
        syl2anc wa cc cr zre recnd zcn mulcom syl2an eqeq1d rexbidva prmnn zdiv
        3bitr2d mpbid eluzelre c1 eluz2b2 simplbi nngt0 syl22anc elnnz sylanbrc
        divgt0 prmuz2 eluz2b1 simprbi 3syl ltmulgt12 syl3anc ltdivmul syl112anc
        mpbird jca ) CIUAUDZJZCAUBKZLJZWLCMNZWKWLOJZPWLMNZWMWKACUCNZWOWKAUEJZWQ
        GBJGAUFNUGZABGCDEFUHZUIWKWQHUJZAQKZCRZHOUKZAXAQKZCRZHOUKZWOWKAOJZCOJZWQ
        XDSWKWRXHWKWRWQWSWTULZAUMTZICUNZHACUOUPWKXFXCHOWKXAOJZUQXEXBCWKAURJXAUR
        JXEXBRXMWKAWKXHAUSJZXKAUTTZVAXAVBAXAVCVDVEVFWKALJZXIXGWOSWKWRXPXJAVGTZX
        LHACVHUPVIVJWKCUSJZPCMNZXNPAMNZWPICVKZWKCLJZXSWKYBVLCMNCVMVNCVOTZXOWKXP
        XTXQAVOTZCAVSVPWLVQVRWKWNCACQKMNZWKVLAMNZYEWKWRAWJJZYFXJAVTYGXHYFAWAWBW
        CWKXRXNXSYFYESYAXOYCCAWDWEVJWKXRXRXNXTWNYESYAYAXOYDCCAWFWGWHWI $.
        $( [23-Nov-2012] $) $( [17-Nov-2012] $)
    $}
  $}

  ${
    $d G j k $.  $d N j k $.
    $( Lemma for ~ 1arith . $)
    1arithlem5 $p |- ( ( N e. NN0 /\ A. j e. ( 1 ... N ) A. k e. ( 1 ... N )
                                     ( j < k -> ( G ` j ) <_ ( G ` k ) ) /\
        A. j e. ( 1 ... N ) ( G ` j ) <_ ( G ` ( N + 1 ) ) ) ->
        A. j e. ( 1 ... ( N + 1 ) ) A. k e. ( 1 ... ( N + 1 ) )
        ( j < k -> ( G ` j ) <_ ( G ` k ) ) ) $=
      ( cn0 wcel clt wbr cfv cle wi c1 co wral wa wb cz syl cr cuz cv cfz caddc
      elfzle2 adantl elfzelz nn0z zleltp1 syl2an ancoms mpbid pm5.5 ralbidva wn
      adantr zre nn0re letrp1 syl3an 3com23 3anidm12 peano2re pm2.21d ralrimiva
      lenlt ltnr jca csn cun cmin wceq 1z cc0 nn0uz ax-1cn subidi fveq2i eqtr4i
      id syl6eleq fzsuc2 sylancr raleqdv raleqbidv cvv oprex breq1 fveq2 breq1d
      imbi12d breq2 breq2d 2ralunsn ax-mp syl6bb biimpar 3expia expdimp sylbird
      3impb mpdan 3impia ) DEFZAUAZBUAZGHZXDCIZXECIZJHZKZBLDUBMZNAXKNZXGDLUCMZC
      IZJHZAXKNZXJBLXMUBMZNZAXQNZXCXLOXPXDXMGHZXOKZAXKNZXSXCYBXPPXLXCYAXOAXKXCX
      DXKFZOZXTYAXOPYDXDDJHZXTYCYEXCXDLDUDUEYCXCYEXTPZYCXDQFDQFYFXCXDLDUFDUGXDD
      UHUIUJUKXTXOULRUMUOXCXLYBXSXCXMXEGHZXNXHJHZKZBXKNZXMXMGHZXNXNJHZKZOZXLYBO
      ZXSKXCYJYMXCYIBXKXCXEXKFZOYGYHYPXCYGUNZYPXCOXEXMJHZYQYPXCYRYPXCYPYRYPXESF
      ZXCDSFZYPXEDJHYRYPXEQFYSXELDUFXEUPRZDUQZXELDUDXEDURUSUTVAYPYSXMSFZYRYQPXC
      UUAXCYTUUCUUBDVBRZXEXMVEUIUKUJVCVDXCYKYLXCUUCYKUNUUDXMVFRVCVGXCYNYOXSXCYO
      YNXSXCYOYNXSXCXSYOYNOZXCXSXJBXKXMVHVIZNZAUUFNZUUEXCXRUUGAXQUUFXCLQFDLLVJM
      ZTIZFXQUUFVKVLXCDEUUJXCVSEVMTIUUJVNUUIVMTLVOVPVQVRVTLDWAWBZXCXJBXQUUFUUKW
      CWDXMWEFUUHUUEPDLUCWFXJYAYIYMABXKXMWEXDXMVKZXFYGXIYHXDXMXEGWGUULXGXNXHJXD
      XMCWHZWIWJXEXMVKZXFXTXIXOXEXMXDGWKUUNXHXNXGJXEXMCWHWLWJUULXTYKXOYLXDXMXMG
      WGUULXGXNXNJUUMWIWJWMWNWOWPWTUTWQXAWRWSXB $.
      $( [21-Nov-2012] $) $( [17-Nov-2012] $)
  $}

  ${
    $d F a b g h j k n p s x y $.  $d G f j k n s z $.  $d H f j k n s $.
    $d M g h m p u v x y $.  $d M g h z $.  $d N a b n x y $.  $d R f g h z $.
    $d R g h u v x y $.  $d Z p q $.  $d f g h j k n s x y $.
    1arithlem6.1 $e |- R = { s | E. n e. NN0 ( s : ( 1 ... n ) --> Prime /\
                                 A. j e. ( 1 ... n ) A. k e. ( 1 ... n )
                                 ( j < k -> ( s ` j ) <_ ( s ` k ) ) ) } $.
    1arithlem6.2 $e |- M = { <. f , z >. | ( f e. R /\
      z = if ( f = (/) , 1 , ( ( x. seq1 f ) ` ( # ` f ) ) ) ) } $.

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem6 $p |- ( F e. R <-> E. n e. NN0 ( F : ( 1 ... n ) --> Prime /\
                                    A. x e. ( 1 ... n ) A. y e. ( 1 ... n )
                                    ( x < y -> ( F ` x ) <_ ( F ` y ) ) ) ) $=
        ( cvv cv clt wbr cfv cle wral wcel c1 cfz co cprime wf wi cn0 wrex elex
        wa oprex mpan2 adantr rexlimivw wceq feq1 fveq1 breq12d imbi2d 2ralbidv
        breq1 fveq2 breq1d imbi12d breq2 breq2d cbvral2v syl6bb anbi12d rexbidv
        fex elab2g pm5.21nii ) IDUAINUAZUBHOZUCUDZUEIUFZAOZBOZPQZVSIRZVTIRZSQZU
        GZBVQTAVQTZUKZHUHUIZIDUJWGVOHUHVRVOWFVRVQNUAVOUBVPUCULVQUENIVLUMUNUOVQU
        EKOZUFZFOZGOZPQZWKWIRZWLWIRZSQZUGZGVQTFVQTZUKZHUHUIWHKIDNWIIUPZWSWGHUHW
        TWJVRWRWFVQUEWIIUQWTWRWMWKIRZWLIRZSQZUGZGVQTFVQTWFWTWQXDFGVQVQWTWPXCWMW
        TWNXAWOXBSWKWIIURWLWIIURUSUTVAXDWEVSWLPQZWBXBSQZUGFGABVQVQWKVSUPZWMXEXC
        XFWKVSWLPVBXGXAWBXBSWKVSIVCVDVEWLVTUPZXEWAXFWDWLVTVSPVFXHXBWCWBSWLVTIVC
        VGVEVHVIVJVKLVMVN $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d M a b $.  $d R a b f $.
      $( Lemma for ~ 1arith . $)
      1arithlem7 $p |- ( ( F e. R /\ N = ( # ` F ) ) <->
        ( N e. NN0 /\ F : ( 1 ... N ) --> Prime /\ A. a e. ( 1 ... N )
          A. b e. ( 1 ... N ) ( a < b -> ( F ` a ) <_ ( F ` b ) ) ) ) $=
        ( wcel wceq wa cn0 cprime wi chash cfv c1 cfz co wf cv clt wbr cle wral
        w3a wrex 1arithlem6 simpl wfn fseq1hash sylan2 eleq1d adantrr rexlimiva
        ffn mpbird sylbi eleq1 syl5ibrcom imp eqtr eqcomd ancoms opreq2 raleqdv
        wb feq2d raleqbidv anbi12d syl biimpd com23 exp3a pm2.43d simpld simprd
        ex imp3a 3jca rcla4ev sylibr 3impb 3adant3 jca impbii ) GBOZIGUAUBZPZQZ
        IROZUCIUDUEZSGUFZKUGZLUGZUHUIWTGUBXAGUBUJUITZLWRUKZKWRUKZULZWPWQWSXDWMW
        OWQWMWQWOWNROZWMUCFUGZUDUEZSGUFZXBLXHUKZKXHUKZQZFRUMZXFKLABCDEFGHJMNUNZ
        XLXFFRXGROZXIXFXKXOXIQZXFXOXOXIUOXPWNXGRXIXOGXHUPWNXGPZXHSGVBGXGUQURZUS
        VCUTVAVDIWNRVEVFVGWPWSXDWMWOWSXDQZWMXMWOXSTZXNXLXTFRXOXLXTXOXIXKXTXOXIX
        KXTTZXOXIXIYATXPXIXKXTXPXQXLXTTXRXQWOXLXSXQWOXLXSTXQWOQZXLXSYBXGIPZXLXS
        VMWOXQYCWOXQQIXGIWNXGVHVIVJYCXIWSXKXDYCXHWRSGXGIUCUDVKZVNYCXJXCKXHWRYDY
        CXBLXHWRYDVLVOVPZVQVRWDVSVQVTWDWAWEVGVAVDVGZWBWPWSXDYFWCWFXEWMWOWQWSXDW
        MWQXSQXMWMXLXSFIRYEWGXNWHWIWQWSWOXDWQWSQWNIWSWQGWRUPWNIPWRSGVBGIUQURVIW
        JWKWL $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem8 $p |- ( ( F e. R /\ N = ( # ` F ) ) ->
                         ( N e. NN0 /\ F : ( 1 ... N ) --> Prime ) ) $=
        ( vx vy wcel chash cfv cv wbr wral wceq wa cn0 c1 cfz co cprime clt cle
        wf wi 1arithlem7 simp1bi simp2bi jca ) GBOIGPQUAUBZIUCOZUDIUEUFZUGGUJZU
        PUQUSMRZNRZUHSUTGQVAGQUISUKNURTMURTZABCDEFGHIJMNKLULZUMUPUQUSVBVCUNUO
        $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem9 $p |- ( ( F e. R /\ N = ( # ` F ) /\ N e. NN ) ->
                         ( F ` N ) e. Prime ) $=
        ( wcel chash cfv wceq cn cprime wa c1 cfz cn0 1arithlem8 simprd eluzfz2
        co wf cuz elnnuz sylbi ffvelrn syl2an 3impa ) GBMZIGNOPZIQMZIGORMZUNUOS
        ZTIUAUFZRGUGZIUSMZUQUPURIUBMUTABCDEFGHIJKLUCUDUPITUHOMVAIUITIUEUJUSRIGU
        KULUM $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem10 $p |- ( F e. R -> F e. Fin ) $=
        ( wcel c1 chash cfv cfz cfn cprime mpan2 cvv co fzfi cen wbr wb wfn cn0
        wf wceq eqid 1arithlem8 simprd ffn oprex fndmeng 3syl enfi mpdan mpbii
        wa ) GBLZMGNOZPUAZQLZGQLZMVBUBVAVCGUCUDZVDVEUEVAVCRGUHZGVCUFZVFVAVBUGLZ
        VGVAVBVBUIVIVGUTVBUJABCDEFGHVBIJKUKSULVCRGUMVHVCTLVFMVBPUNVCTGUOSUPVCGB
        UQURUS $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem11 $p |- M : R --> NN $=
        ( vy cv wceq c1 cfv cn wcel cprime cn0 wa vx c0 chash cmul cseq1 co cif
        wral wf cfz wrex clt wbr cle 1arithlem6 r19.40 simpld sylbi cc0 1nn wfn
        wi wb ffn 0fz1 sylan2 biimpar iftrue syl eleq1d mpbiri wn elnn0 orcanai
        wo pm1.4 wss prmnn ssriv fss mpan2 ffvelrn ralrimiva mulex vex seq1alcl
        nnmulcl nnnn0 fseq1hash syl2an fveq2d mpbird sylan an32s notbid iffalse
        pm2.61dan rexlimiva rgen fopab2 mpbi ) CLZUBMZNXBUCOZUDXBUEUFZOZUGZPQZC
        BUHBPGUIXHCBXBBQZNFLZUJUFZRXBUIZFSUKZXHXIXLUALZKLZULUMXNXBOXOXBOZUNUMVB
        KXKUHUAXKUHZTFSUKZXMUAKABCDEFXBGHIJUOXRXMXQFSUKXLXQFSUPUQURXLXHFSXJSQZX
        LTZXJUSMZXHXTYATZXHNPQUTYBXGNPYBXCXGNMXTXCYAXLXSXBXKVAZXCYAVCXKRXBVDZXB
        XJVEVFZVGXCNXFVHVIVJVKXTYAVLZTZXHXFPQZXSYFXLYHXSYFTXJPQZXLYHXSYAYIXSYIY
        AVOYAYIVOXJVMYIYAVPURVNYIXLTZYHXJXEOZPQZXLYIXKPXBUIZYLXLRPVQYMFRPXJVRVS
        XKRPXBVTWAYMYIXPPQZKXKUHYLYMYNKXKXKPXOXBWBWCFHKPUDXBXJWDCWEXJHLWGWFVFVF
        YJXFYKPYJXDXJXEYIXSYCXDXJMXLXJWHYDXBXJWIWJWKVJWLWMWNYGXGXFPYGXCVLZXGXFM
        XTYOYFXTXCYAYEWOVGXCNXFWPVIVJWLWQWRVIWSCABPXGGJWTXA $.
        $( [12-Mar-2014] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem12 $p |- ( (/) e. R /\ ( M ` (/) ) = 1 ) $=
        ( vx vy c0 cfv c1 wceq cc0 co cprime cv wcel cn0 cfz wf clt wbr wi wral
        cle 0nn0 f0 eqid wb fz1n ax-mp mpbir feq2i ral0 raleqi chash 1arithlem7
        w3a wa biimpri simpld mp3an cmul cseq1 cif eqeq1 fveq2 fveq12d ifbieq2d
        opreq2 cz 1z elexi fvex ifex fvopab4 iftrue syl6eq pm3.2i ) MBUAZMGNZOP
        ZQUBUAZOQUCRZSMUDZKTZLTZUEUFWJMNWKMNUIUFUGLWHUHZKWHUHZWDUJWIMSMUDSUKWHM
        SMWHMPZQQPZQULWGWNWOUMUJQUNUOUPZUQUPWMWLKMUHWLKURWLKWHMWPUSUPWGWIWMVBZW
        DQMUTNZPZWDWSVCWQABCDEFMGQHKLIJVAVDVEVFZWDWFWTWDWEMMPZOWRVGMVHRZNZVIZOC
        AMCTZMPZOXEUTNZVGXEVHRZNZVIXDBGXFXFXAXIXCOXEMMVJXFXGWRXHXBXEMVGVHVNXEMU
        TVKVLVMJXAOXCOVOVPVQWRXBVRVSVTXAXDOPMULXAOXCWAUOWBUOWC $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F f z $.
      $( Lemma for ~ 1arith . $)
      1arithlem13 $p |- ( ( F e. R /\ N = ( # ` F ) /\ N e. NN ) ->
                          ( M ` F ) = ( ( x. seq1 F ) ` N ) ) $=
        ( wcel chash cfv wceq cn cmul cseq1 c1 w3a co wa eleq1 biimpa c0 cif cv
        eqeq1 opreq2 fveq2 fveq12d ifbieq2d 1nn fvex ifex fvopab4 adantr wn cc0
        elexi wne nnne0 cfn 1arithlem10 hash0 necon3abid syl sylan2 eqtrd 3impb
        wb iffalse 3ad2ant2 eqtr4d ) GBMZIGNOZPZIQMZUAGHOZVQRGSUBZOZIWAOZVPVRVS
        VTWBPZVRVSUCVPVQQMZWDVRVSWEIVQQUDUEVPWEUCZVTGUFPZTWBUGZWBVPVTWHPWECAGCU
        HZUFPZTWINOZRWISUBZOZUGWHBHWIGPZWJWGWMWBTWIGUFUIWNWKVQWLWAWIGRSUJWIGNUK
        ULUMLWGTWBTQUNVAVQWAUOUPUQURWFWGUSZWHWBPWEVPVQUTVBZWOVQVCVPWPWOVPGVDMZW
        PWOVLABCDEFGHJKLVEWQWGVQUTGVFVGVHUEVIWGTWBVMVHVJVIVKVRVPWCWBPVSIVQWAUKV
        NVO $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d g x y z $.
      $( Lemma for ~ 1arith . $)
      1arithlem14 $p |- ( F e. R ->
                        ( ( # ` F ) e. NN <-> ( M ` F ) e. ( ZZ>= ` 2 ) ) ) $=
        ( vg wcel chash cfv cn cv wceq c1 c0 vx vy c2 cuz wi fveq2 imbi12d cmul
        eleq1d wa cseq1 co eqid 1arithlem13 mp3an2 cfz cprime wf cn0 1arithlem8
        wral simprd mpan2 ffvelrn sylan prmuz2 syl ralrimiva mulex vex uz2mulcl
        seq1alcl sylan2 ancoms eqeltrd ex vtoclga wn cc0 cfn 1arithlem10 hashcl
        wo elnn0 sylib ord wb hash0 1nuz2 1arithlem12 simpri syl6eq mtbiri syld
        syl6bi con4d impbid ) GBMZGNOZPMZGHOZUCUDOZMZLQZNOZPMZXDHOZXBMZUEWTXCUE
        LGBXDGRZXFWTXHXCXIXEWSPXDGNUFUIXIXGXAXBXDGHUFUIUGXDBMZXFXHXJXFUJXGXEUHX
        DUKULOZXBXJXEXERZXFXGXKRXEUMZABCDEFXDHXEIJKUNUOXFXJXKXBMZXJXFAQZXDOZXBM
        ZASXEUPULZVAXNXJXQAXRXJXOXRMZUJXPUQMZXQXJXRUQXDURZXSXTXJXLYAXMXJXLUJXEU
        SMYAABCDEFXDHXEIJKUTVBVCXRUQXOXDVDVEXPVFVGVHUAUBAXBUHXDXEVILVJUAQUBQVKV
        LVMVNVOVPVQWRWTXCWRWTVRWSVSRZXCVRZWRWTYBWRWSUSMZWTYBWCWRGVTMZYDABCDEFGH
        IJKWAZGWBVGWSWDWEWFWRYBGTRZYCWRYEYBYGWGYFGWHVGYGXCSXBMWIYGXASXBYGXATHOZ
        SGTHUFTBMYHSRABCDEFHIJKWJWKWLUIWMWOWNWPWQ $.
        $( [12-Mar-2014] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem15 $p |- ( F e. R -> ( ( # ` F ) = 0 <-> ( M ` F ) = 1 ) ) $=
        ( wcel cfv cn wn cc0 wceq c1 eleq1 mtbiri chash c2 cuz 1arithlem14 0nnn
        notbid cn0 wo cfz co cprime wf eqid 1arithlem8 mpan2 simpld elnn0 sylib
        wa ord impbid2 1nuz2 1arithlem11 ffvelrn mpan elnn1uz2 con1d 3bitr4d )
        GBLZGUAMZNLZOZGHMZUBUCMZLZOZVJPQZVMRQZVIVKVOABCDEFGHIJKUDUFVIVQVLVQVKPN
        LUEVJPNSTVIVKVQVIVJUGLZVKVQUHVIVSRVJUIUJUKGULZVIVJVJQVSVTUSVJUMABCDEFGH
        VJIJKUNUOUPVJUQURUTVAVIVRVPVRVORVNLVBVMRVNSTVIVRVOVIVRVOVIVMNLZVRVOUHBN
        HULVIWAABCDEFHIJKVCBNGHVDVEVMVFURUTVGVAVH $.
        $( [12-Mar-2014] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F x z $.  $d N x z $.  $d P z $.
      $( Lemma for ~ 1arith . $)
      1arithlem16 $p |- ( ( F e. R /\ N = ( # ` F ) /\ N e. NN ) ->
                          ( P e. ran F -> P <_ ( F ` N ) ) ) $=
        ( vx vy wcel cfv cle wbr c1 chash wceq cn w3a cv crn wral cfz co clt wi
        cn0 cprime wf simp1 simp2 wa 1arithlem7 biimpi syl2anc simp3d wo adantl
        elfzle2 wb cr elfzelz zre syl simp3 cuz elnnuz eluzfz2 sylbi 3syl leloe
        syl2an ancoms mpbid adantr breq2 fveq2 breq2d imbi12d rcla4v imp simp2d
        cz ffvelrn prmz sylan eqle ex jaod mpd exp31 a2d ralimdv2 wfn ffn dffn4
        wfo breq1 cbvfo syl5com ) HCPZJHUAQUBZJUCPZUDZAUEZJHQZRSZAHUFZUGZBXMPBX
        KRSZXINUEZHQZXKRSZNTJUHUIZUGZXNXIXPOUEZUJSZXQYAHQZRSZUKZOXSUGZNXSUGZXTX
        IJULPZXSUMHUNZYGXIXFXGYHYIYGUDZXFXGXHUOXFXGXHUPXFXGUQYJACDEFGHIJKNOLMUR
        USUTZVAXIYFXRNXSXSXIXPXSPZYFXRXIYLYFXRXIYLUQZYFUQZXPJUJSZXPJUBZVBZXRYMY
        QYFYMXPJRSZYQYLYRXIXPTJVDVCYLXIYRYQVEZYLXPVFPZJVFPZYSXIYLXPWHPYTXPTJVGX
        PVHVIXIJXSPZJWHPUUAXIXHUUBXFXGXHVJXHJTVKQPUUBJVLTJVMVNVIZJTJVGJVHVOXPJV
        PVQVRVSVTYNYOXRYPYMYFYOXRUKZXIYFUUDUKZYLXIUUBUUEUUCYEUUDOJXSYAJUBZYBYOY
        DXRYAJXPUJWAUUFYCXKXQRYAJHWBWCWDWEVIVTWFYMYPXRUKYFYMYPXRYMXQVFPZXQXKUBX
        RYPXIYIYLUUGXIYHYIYGYKWGZYIYLUQXQUMPXQWHPUUGXSUMXPHWIXQWJXQVHVOWKXPJHWB
        XQXKWLVQWMVTWNWOWPWQWRWOXIYIHXSWSZXTXNVEZUUHXSUMHWTUUIXSXMHXBUUJXSHXAXR
        XLNAXSXMHXQXJXKRXCXDVNVOVSXLXOABXMXJBXKRXCWEXE $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith . $)
      1arithlem17 $p |- ( ( F e. R /\ Z e. ( ZZ>= ` 2 ) /\ Q e. NN ) ->
          ( ( Z / Q ) = ( M ` F ) <-> Z = ( ( M ` F ) x. Q ) ) ) $=
        ( wcel c2 cfv cn cc co wceq cuz w3a cc0 wne wa cdiv wb cz simp2 eluzelz
        cmul zcn 3syl wf 1arithlem11 a1i simp1 ffvelrn syl2anc nncn simp3 nnne0
        syl jca divmul3 syl3anc ) HCNZJOUAPNZBQNZUBZJRNZHIPZRNZBRNZBUCUDZUEZJBU
        FSVLTJVLBUKSTUGVJVHJUHNVKVGVHVIUIOJUJJULUMVJVLQNZVMVJCQIUNZVGVQVRVJACDE
        FGIKLMUOUPVGVHVIUQCQHIURUSVLUTVCVJVIVPVGVHVIVAVIVNVOBUTBVBVDVCJVLBVEVF
        $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      1arithlem18.3 $e |- G = ( F |` ( 1 ... N ) ) $.

      ${
        $d G j k n s x y $.
        $( Lemma for ~ 1arith . $)
        1arithlem18 $p |- ( ( F e. R /\ ( N + 1 ) = ( # ` F ) /\ N e. NN0 ) ->
                            ( G e. R /\ N = ( # ` G ) ) ) $=
          ( vx vy wcel cfv cprime wral c1 caddc co chash wceq cn0 w3a cfz wf cv
          clt wbr cle wi wa simp3 1arithlem7 biimpi simp2d fzssp1 fssres sylan2
          cres wss feq1i sylibr sylan simp3d ssralv ralimdv syld syl 1arithlem2
          3impa wb id ax-mp syl6ibr syl5com 3impia 3jca ) GBQZJUAUBUCZGUDRUEZJU
          FQZUGZWEUAJUHUCZSHUIZOUJZPUJZUKULZWIHRWJHRUMULUNPWGTOWGTZUGHBQJHUDRUE
          UOWFWEWHWLWBWDWEUPWBWDWEWHWBWDUOZUAWCUHUCZSGUIZWEWHWMWCUFQZWOWKWIGRWJ
          GRUMULUNZPWNTZOWNTZWMWPWOWSUGABCDEFGIWCKOPLMUQURZUSWOWEUOWGSGWGVCZUIZ
          WHWEWOWGWNVDZXBUAJUFUTZWNSWGGVAVBWGSHXANVEVFVGVNWBWDWEWLWMWSWEWLWMWPW
          OWSWTVHWEWSWQPWGTZOWGTZWLWEXCWSXFUNXDXCWSXEOWNTXFXCWRXEOWNWQPWGWNVIVJ
          XEOWGWNVIVKVLHXAUEZWLXFVONXGOPGHJXGVPVMVQVRVSVTWAABCDEFHIJKOPLMUQVF
          $.
          $( [2-Sep-2013] $) $( [17-Nov-2012] $)
      $}

      ${
        $d F f z $.
        $( Lemma for ~ 1arith . $)
        1arithlem19 $p |- ( ( F e. R /\ ( N + 1 ) = ( # ` F ) /\ N e. NN0 ) ->
                            ( M ` F ) = ( ( M ` G ) x. ( F ` ( N + 1 ) ) ) ) $=
          ( wcel c1 co cfv wceq cmul caddc chash cn0 cn cc0 wo wa elnn0 w3a cvv
          cseq1 mulex seq1p1g cfz opreq2i fveq1i cuz elnnuz eluzfz2 sylbi ancli
          cres seq1resval2g sylan2 3impa syl5eq opreq1d eqtr4d 3adant2 peano2nn
          mp3an1 1arithlem13 syl3an3 nnnn0 simpld simprd syl3anc 3eqtr4d 3expia
          1arithlem18 simp3 cc 0nn0 eleq1 mpbiri nn0p1nn cprime 1arithlem9 prmz
          syl cz zcn 3syl mulid2 c0 eqeq1 3ad2ant3 mpbid eqcomd cfn 1arithlem10
          wb fveq2d 1arithlem12 simpri syl6eq opreq1 ax-1cn addid2i seq11g mpan
          hash0 3ad2ant1 3eqtrd 3eqtr4rd jaod syl5bi 3impia ) GBOZJPUAQZGUBRSZJ
          UCOZGIRZHIRZXTGRZTQZSZYBJUDOZJUESZUFXSYAUGZYGJUHYJYHYGYIXSYAYHYGXSYAY
          HUIZXTTGUKQZRZJTHUKQZRZYETQZYCYFXSYHYMYPSZYATUJOZXSYHYQULYRXSYHUIZYMJ
          YLRZYETQYPJUJBTGUMYSYOYTYETYSYOJTGPJUNQZVBZUKQZRZYTJYNUUCHUUBTUKNUOUP
          YRXSYHUUDYTSZYHYRXSUGYHJUUAOZUGUUEYHUUFYHJPUQROUUFJURPJUSUTVAUJBTGJJV
          CVDVEVFVGVHVKVIYHXSYAXTUDOZYCYMSZJVJABCDEFGIXTKLMVLZVMYKYDYOYETYKHBOZ
          JHUBRZSZYHYDYOSYKUUJUULYHXSYAYBUUJUULUGZJVNABCDEFGHIJKLMNVTZVMZVOYKUU
          JUULUUOVPXSYAYHWAABCDEFHIJKLMVLVQVGVRVSXSYAYIYGXSYAYIUIZPYETQZYEYFYCU
          UPYEWBOZUUQYESYIXSYAUUGUURYIYBUUGYIYBUEUCOWCJUEUCWDWEZJWFWJZXSYAUUGUI
          YEWGOYEWKOUURABCDEFGIXTKLMWHYEWIYEWLWMVMYEWNWJUUPYDPYETUUPYDWOIRZPUUP
          HWOIUUPUUKUESZHWOSZUUPUEUUKUUPUULUEUUKSZUUPUUJUULYIXSYAYBUUMUUSUUNVMZ
          VPYIXSUULUVDXBYAJUEUUKWPWQWRWSUUPUUJHWTOUVBUVCXBUUPUUJUULUVEVOABCDEFH
          IKLMXAHXLWMWRXCWOBOUVAPSABCDEFIKLMXDXEXFVGUUPYCPGRZYEUUPYCYMPYLRZUVFY
          IXSYAUUGUUHUUTUUIVMUUPXTPYLYIXSXTPSYAYIXTUEPUAQPJUEPUAXGPXHXIXFWQZXCX
          SYAUVGUVFSZYIYRXSUVIULUJBTGXJXKXMXNUUPXTPGUVHXCVHXOVSXPXQXR $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}

      ${
        $d F f j k n s $.  $d F f z $.
        $( Lemma for ~ 1arith . $)
        1arithlem20 $p |- ( ( ( F e. R /\ ( N + 1 ) = ( # ` F ) /\ N e. NN0 )
                              /\ P e. Prime ) ->
        ( P || ( M ` F ) <-> ( P || ( M ` G ) \/ P = ( F ` ( N + 1 ) ) ) ) ) $=
          ( wcel cfv cdivides wbr wb cprime c1 caddc co chash wceq cn0 w3a cmul
          wo wa 1arithlem19 breq2d adantl cz cn 1arithlem18 1arithlem11 ffvelrn
          simpld mpan nnz 3syl nn0p1nn 1arithlem9 syl3an3 prmz syl jca euclemma
          wf 3expb sylan2 c2 cuz prmuz2 dvdsprm syl2an orbi2d 3bitrd ancoms ) B
          UAPZHCPZKUBUCUDZHUEQUFZKUGPZUHZBHJQZRSZBIJQZRSZBWDHQZUFZUJZTWBWGUKZWI
          BWJWLUIUDZRSZWKBWLRSZUJZWNWGWIWQTWBWGWHWPBRACDEFGHIJKLMNOULUMUNWGWBWJ
          UOPZWLUOPZUKWQWSTZWGWTXAWGICPZWJUPPZWTWGXCKIUEQUFACDEFGHIJKLMNOUQUTCU
          PJVKXCXDACDEFGJLMNURCUPIJUSVAWJVBVCWGWLUAPZXAWFWCWEWDUPPXEKVDACDEFGHJ
          WDLMNVEVFZWLVGVHVIWBWTXAXBBWJWLVJVLVMWOWRWMWKWBBVNVOQPXEWRWMTWGBVPXFW
          LBVQVRVSVTWA $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}

      ${
        $( Lemma for ~ 1arith . $)
        1arithlem21 $p |- ( ( ( F e. R /\ ( N + 1 ) = ( # ` F ) /\ N e. NN0 )
                              /\ P e. Prime ) ->
        ( P e. ran F <-> ( P e. ran G \/ P = ( F ` ( N + 1 ) ) ) ) ) $=
          ( wcel c1 co cfv wceq caddc chash cn0 w3a crn csn cun cprime wo wb wa
          cfz wfn wf 1arithlem8 simprd ffn syl cima imaundi fnima cz cmin 1z id
          cuz nn0uz ax-1cn subidi fveq2i eqtr4i syl6eleq fzsuc2 sylancr imaeq2d
          cc0 sylan9req rneqi df-ima a1i cn nn0p1nn elnnuz eluzfz2 sylbi fnsnfv
          sylan2 uneq12d eqeq12d mpbiri eleq2d sylan 3impa elsncg orbi2d syl5bb
          cres elun sylan9bb ) HCPZKQUARZHUBSTZKUCPZUDBHUEZPZBIUEZXAHSZUFZUGZPZ
          BUHPZBXFPZBXGTZUIZWTXBXCXEXJUJZWTXBUKZHQXAULRZUMZXCXOXPXQUHHUNZXRXPXA
          UCPXSACDEFGHJXALMNUOUPXQUHHUQURXRXCUKZXDXIBXTXDXITHQKULRZXAUFZUGZUSZH
          YAUSZHYBUSZUGZTHYAYBUTXTXDYDXIYGXRXCXDHXQUSYDXQHVAXCXQYCHXCQVBPKQQVCR
          ZVFSZPXQYCTVDXCKUCYIXCVEUCVPVFSYIVGYHVPVFQVHVIVJVKVLQKVMVNVOVQXTXFYEX
          HYFXFYETXTXFHYAWQZUEYEIYJOVRHYAVSVKVTXCXRXAXQPZXHYFTXCXAWAPZYKKWBYLXA
          QVFSPYKXAWCQXAWDWEURXQXAHWFWGWHWIWJWKWLWMXJXLBXHPZUIXKXNBXFXHWRXKYMXM
          XLBXGUHWNWOWPWS $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}
    $}

    ${
      $d F g m $.  $d P g h m x y $.  $d R f g h m x y z $.
      1arithlem22.3 $e |- P e. Prime $.
      $( Lemma for ~ 1arith . $)
      1arithlem22 $p |- ( F e. R -> ( P e. ran F <-> P || ( M ` F ) ) ) $=
        ( vg cfv wcel wb wceq wi c0 vm vx vy vh chash cn0 crn cdivides wbr eqid
        wa c1 cfz co cprime wf 1arithlem8 simpld mpan2 fvex cv eleq1 wral caddc
        cc0 eqeq1 imbi1d ralbidv eqcom noel wn nprmdvds1 2false cfn 1arithlem10
        ax-mp hash0 biimpa rneqd syl6eq eleq2d fveq2d 1arithlem12 simpri breq2d
        syl rn0 bibi12d mpbiri sylan2br rgen 1arithlem1 cbvralv w3a 1arithlem18
        ex cres rcla4v com12 imp3a syl5 wo orbi1 1arithlem21 1arithlem20 sylcom
        syl5ibr 3expia com3l imp exp3a syl5bi nn0ind com13 sylbird vtocle mpcom
        ralrimiv ) HUEOZUFPZHCPZBHUGPBHIOUHUIQZYAXSXSRZXTXSUJYAYCUKXTULXSUMUNUO
        HUPACDEFGHIXSJKLUQURUSXTYAYBSZSUAXSHUEUTUAVAZXSRZXTYEUFPZYDYEXSUFVBYAYG
        YFYBYGYENVAZUEOZRZBYHUGZPZBYHIOZUHUIZQZSZNCVCZYAYFYBSZUBVAZYIRZYOSZNCVC
        VEYIRZYOSZNCVCUCVAZYIRZYOSZNCVCZUUDULVDUNZYIRZYOSZNCVCZYQUBUCYEYSVERZUU
        AUUCNCUULYTUUBYOYSVEYIVFVGVHYSUUDRZUUAUUFNCUUMYTUUEYOYSUUDYIVFVGVHYSUUH
        RZUUAUUJNCUUNYTUUIYOYSUUHYIVFVGVHYSYERZUUAYPNCUUOYTYJYOYSYEYIVFVGVHUUCN
        CYHCPZUUBYOUUBUUPYIVERZYOYIVEVIUUPUUQUKZYOBTPZBULUHUIZQUUSUUTBVJBUOPZUU
        TVKMBVLVPVMUURYLUUSYNUUTUURYKTBUURYKTUGTUURYHTUUPUUQYHTRZUUPYHVNPUUQUVB
        QACDEFGYHIJKLVOYHVQWFVRZVSWGVTWAUURYMULBUHUURYMTIOZULUURYHTIUVCWBTCPUVD
        ULRACDEFGIJKLWCWDVTWEWHWIWJWPWKUUGUUDUDVAZUEORBUVEUGPBUVEIOUHUIQSZUDCVC
        ZUUDUFPZUUKUUFUVFNUDCBYHUVEIUUDWLWMUVHUVGUUKUVHUVGUKZUUJNCUVIUUPUUIYOUV
        HUVGUUPUUIUKZYOSUVJUVHUVGYOUUPUUIUVHUVGYOSUVGUUPUUIUVHWNZYOUVGUVKBYHULU
        UDUMUNWQZUGPZBUVLIOUHUIZQZYOUVKUVLCPZUUDUVLUEORZUKUVGUVOACDEFGYHUVLIUUD
        JKLUVLUJZWOUVGUVPUVQUVOUVPUVGUVQUVOSZUVFUVSUDUVLCBUVEUVLIUUDWLWRWSWTXAU
        VKUVAUVOYOSMUVOYOUVKUVAUKZUVMBUUHYHORZXBZUVNUWAXBZQUVMUVNUWAXCUVTYLUWBY
        NUWCABCDEFGYHUVLIUUDJKLUVRXDABCDEFGYHUVLIUUDJKLUVRXEWHXGUSXFWSXHXIXJXKX
        RWPXLXMYPYRNHCBYHHIYEWLWRXAXNXOXPXQ $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Lemma for ~ 1arith .  A prime number ` P ` appears in the prime
         factorization ` F ` of a positive integer ` Z ` iff it divides
         ` Z ` .  The proof uses Euclid's lemma ~ euclemma . $)
      1arithprmb $p |- ( ( F e. R /\ Z = ( M ` F ) /\ P e. Prime ) ->
                         ( P e. ran F <-> P || Z ) ) $=
        ( wcel wceq cprime cdivides wbr wb c2 cfv w3a crn wi eleq1 breq1 imbi2d
        cif bibi12d 2prm elimel 1arithlem22 dedth impcom 3adant2 breq2 3ad2ant2
        bitr4d ) HCNZJHIUAZOZBPNZUBBHUCZNZBUTQRZBJQRZUSVBVDVESZVAVBUSVGVBUSVGUD
        USVBBTUHZVCNZVHUTQRZSZUDBTBVHOZVGVKUSVLVDVIVEVJBVHVCUEBVHUTQUFUIUGAVHCD
        EFGHIKLMBTPUJUKULUMUNUOVAUSVFVESVBJUTBQUPUQUR $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      1arithlem23.3 $e |- S = { p e. Prime | p || Z } $.
      1arithlem23.4 $e |- Q = sup ( S , RR , < ) $.

      ${
        $d F q z $.  $d Q q z $.  $d S x y $.  $d Z x y $.
        $( Lemma for ~ 1arith . $)
        1arithlem23 $p |- ( ( F e. R /\ Z = ( M ` F ) /\ Z e. ( ZZ>= ` 2 ) ) ->
                            ( F ` ( # ` F ) ) = Q ) $=
          ( wcel wbr cprime vq vy vx cfv c2 cuz w3a chash cle wa cdivides simp1
          wceq cn eqidd simp3 simp2 eleq1d mpbid 1arithlem14 biimpar 1arithlem9
          syl2anc syl3anc crn c1 cfz co wf cn0 1arithlem8 simprd wfn ffn elnnuz
          eluzfz2 sylbi fnfvelrn syl2an wb 1arithprmb syld3an3 cv breq1 cbvrabv
          crab eqtri elrab2 sylanbrc cr clt csup wss c0 wne wral wrex maxprmfct
          syl simpld suprub sylan syl6breqr mpdan syl5eqel biimprd 3expia imp3a
          wi syl5bi 1arithlem16 syld mpd jca cz prmz zre 3syl 1arithlem3 simp1d
          letri3 mpbird ) ICRZKIJUDZUMZKUEUFUDZRZUGZIUHUDZIUDZBUMZYJBUISZBYJUIS
          ZUJZYHYLYMYHYJDRZYLYHYJTRZYJKUKSZYOYHYCYIYIUMZYIUNRZYPYCYEYGULZYHYIUO
          ZYHYCYDYFRZYSYTYHYGUUBYCYEYGUPZYHKYDYFYCYEYGUQZURUSYCYSUUBACEFGHIJLNO
          UTVAVCZACEFGHIJYILNOVBVDZYHYJIVEZRZYQYHVFYIVGVHZTIVIZYSUUHYHYCYRUUJYT
          UUAYCYRUJYIVJRUUJACEFGHIJYILNOVKVLVCUUEUUJIUUIVMYIUUIRZUUHYSUUITIVNYS
          YIVFUFUDRUUKYIVOVFYIVPVQUUIYIIVRVSVCYCYEYGYPUUHYQVTUUFAYJCEFGHIJKLNOW
          AWBUSUAWCZKUKSZYQUAYJTDUULYJKUKWDDMWCZKUKSZMTWFUUMUATWFPUUOUUMMUATUUN
          UULKUKWDWEWGZWHWIYHYOUJYJDWJWKWLZBUIYHDWJWMDWNWOUBWCUCWCUISUBDWPUCWJW
          QUGZYOYJUUQUISYHUURUUQDRZYHYGUURUUSUJUUCUCUBMDKPWRWSZWTUCUBDYJXAXBQXC
          XDYHBDRZYMYHBUUQDQYHUURUUSUUTVLXEYHUVABUUGRZYMUVABTRZBKUKSZUJYHUVBUUM
          UVDUABTDUULBKUKWDUUPWHYHUVCUVDUVBYHYCYEUVCUVDUVBXIZXIYTUUDYCYEUVCUVEY
          CYEUVCUGUVBUVDABCEFGHIJKLNOWAXFXGVCXHXJYHYCYRYSUVBYMXIYTUUAUUEABCEFGH
          IJYILNOXKVDXLXMXNYHYJWJRZBWJRZYKYNVTYHYPYJXORUVFUUFYJXPYJXQXRYHUVCBXO
          RUVGYHUVCUVDYOYLXIZYHYGUVCUVDUVHUGUUCBDYJKMPQXSWSXTBXPBXQXRYJBYAVCYB
          $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}

      ${
        $d G j k n s x y $.  $d N f j k n s x y $.  $d N f z $.  $d ph x y $.
        1arithlem24.5 $e |- G = ( F u. { <. ( N + 1 ) , Q >. } ) $.
        1arithlem24.6 $e |- N = ( # ` F ) $.
        1arithlem24.7 $e |- ( ph <->
          ( F e. R /\ Z e. ( ZZ>= ` 2 ) /\ ( Z / Q ) = ( M ` F ) ) ) $.
        $( Lemma for ~ 1arith . $)
        1arithlem24 $p |- ( ph -> ( G e. R /\ Z = ( M ` G ) ) ) $=
          ( vx vy wcel cfv wceq c1 caddc co chash cn0 cfz cprime wf clt wbr cle
          cv wi wral wa w3a c2 cdiv simp1bi 1arithlem7 biimpi sylancl peano2nn0
          cuz simp1d syl cres cdivides simp2bi 1arithlem3 cop csn cun fseq1p1m1
          simp2d eqid biimpa mp3anr3 simp3d 1arithlem2 mpbid ffvelrn sylan cmul
          syl12anc crn wfn ffn fnfvelrn wb adantr eqidd 1arithprmb syl3anc prmz
          cz 1arithlem11 mpan nnz 3syl dvdsmultr1 mpd prmnn simp3bi 1arithlem17
          cn syl31anc breq2d mpbird breq1 elrab2 sylanbrc fveq1d fvres sylan9eq
          eqcomd breq12d ralrimiva 1arithlem5 biimpri simpld fveq2d 1arithlem19
          opreq12d simprd 3eqtr4d jca ) AKDUFZNKLUGZUHAYPMUIUJUKZKULUGUHZAYRUMU
          FZUIYRUNUKZUOKUPZUDUTZUEUTZUQURZUUCKUGZUUDKUGUSURVAZUEUUAVBUDUUAVBZYP
          YSVCZAMUMUFZYTAUUJUIMUNUKZUOJUPZUUEUUCJUGZUUDJUGUSURVAUEUUKVBUDUUKVBZ
          AJDUFZMJULUGUHZUUJUULUUNVDZAUUONVEVLUGUFZNCVFUKJLUGZUHZUCVGZUBUUOUUPV
          CUUQBDFGHIJLMOUDUEQRVHVIVJZVMZMVKVNAUUBYRKUGZCUHZJKUUKVOZUHZAUUJUULCU
          OUFZUUBUVEUVGVDZUVCAUUJUULUUNUVBWCZAUVHCNVPURZUUMEUFZUUMCUSURZVAZAUUR
          UVHUVKUVNVDAUUOUURUUTUCVQZCEUUMNPSTVRVNZVMZUUJUULUVHKJYRCVSVTZWAUHZUV
          IUAUUJUULUVHUVSVDUVIUOCJKUVRMUVRWDWBWEWFWMZVMAUUJUUGUEUUKVBUDUUKVBZUU
          FUVDUSURZUDUUKVBUUHUVCAUUNUWAAUUJUULUUNUVBWGAUDUEKJMAUUBUVEUVGUVTWGZW
          HWIAUWBUDUUKAUUCUUKUFZVCZUVMUWBUWEUVLUVMUWEUUMUOUFZUUMNVPURZUVLAUULUW
          DUWFUVJUUKUOUUCJWJWKZUWEUWGUUMUUSCWLUKZVPURZUWEUUMUUSVPURZUWJUWEUUMJW
          NUFZUWKAJUUKWOZUWDUWLAUULUWMUVJUUKUOJWPVNUUKUUCJWQWKUWEUUOUUSUUSUHUWF
          UWLUWKWRAUUOUWDUVAWSUWEUUSWTUWHBUUMDFGHIJLUUSOQRXAXBWIUWEUUMXDUFZUUSX
          DUFZCXDUFZUWKUWJVAUWEUWFUWNUWHUUMXCVNAUWOUWDAUUOUUSXNUFZUWOUVADXNLUPU
          UOUWQBDFGHILOQRXEDXNJLWJXFUUSXGXHWSAUWPUWDAUVHUWPUVQCXCVNWSUUMUUSCXIX
          BXJAUWGUWJWRUWDANUWIUUMVPAUUOUURCXNUFZUUTNUWIUHZUVAUVOAUVHUWRUVQCXKVN
          AUUOUURUUTUCXLUUOUURUWRVDUUTUWSBCDFGHIJLNOQRXMWEXOZXPWSXQPUTZNVPURUWG
          PUUMUOEUXAUUMNVPXRSXSXTAUVNUWDAUVHUVKUVNUVPWGWSXJUWEUUMUUFCUVDUSAUWDU
          UMUUCUVFUGUUFAUUCJUVFUWCYAUUCUUKKYBYCACUVDUHUWDAUVDCAUUBUVEUVGUVTWCYD
          ZWSYEWIYFUDUEKMYGXBUUIYTUUBUUHVDBDFGHIKLYROUDUEQRVHYHXBZYIZAUWIUVFLUG
          ZUVDWLUKZNYQAUUSUXECUVDWLAJUVFLUWCYJUXBYLUWTAYPYSUUJYQUXFUHUXDAYPYSUX
          CYMUVCBDFGHIKUVFLMOQRUVFWDYKXBYNYO $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}
    $}

    ${
      $d G g $.  $d N f j k n s $.  $d N f z $.  $d Q h y $.  $d h p x $.
      1arithlem25.3 $e |- S = { p e. Prime | p || x } $.
      1arithlem25.4 $e |- Q = sup ( S , RR , < ) $.
      1arithlem25.5 $e |- G = ( h u. { <. ( N + 1 ) , Q >. } ) $.
      1arithlem25.6 $e |- N = ( # ` h ) $.
      $( Lemma for ~ 1arith . $)
      1arithlem25 $p |- ( x e. ( ZZ>= ` 2 ) -> ( A. y e. NN
      ( y < x -> E. g e. R y = ( M ` g ) ) -> E. g e. R x = ( M ` g ) ) ) $=
        ( cv c2 cuz cfv wcel clt wceq wrex wi cn wral cdiv co 1arithlem4 simprd
        wbr simpld breq1 fveq2 eqeq2d cbvrexv rexbidv syl5bb imbi12d rcla4v syl
        eqeq1 mpid w3a wa biid 1arithlem24 rcla4ev 3expia ancoms rexlimdva syld
        ) AUDZUEUFUGUHZBUDZWAUIUSZWCHUDZNUGZUJZHEUKZULZBUMUNZWADUOUPZIUDZNUGZUJ
        ZIEUKZWAWFUJZHEUKZWBWJWKWAUIUSZWOWBWKUMUHZWRDFWAQTUAUQZURWBWSWJWRWOULZU
        LWBWSWRWTUTWIXABWKUMWCWKUJZWDWRWHWOWCWKWAUIVAWHWCWMUJZIEUKXBWOWGXCHIEWE
        WLUJWFWMWCWEWLNVBVCVDXBXCWNIEWCWKWMVJVEVFVGVHVIVKWBWNWQIEWLEUHZWBWNWQUL
        XDWBWNWQXDWBWNVLZMEUHWAMNUGZUJZVMWQXECDEFGJKLWLMNOWAPQRSTUAUBUCXEVNVOWP
        XGHMEWEMUJWFXFWAWEMNVBVCVPVIVQVRVSVT $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d R f h x z $.  $d Z g x $.  $d f p x z $.
      $( Lemma for ~ 1arith . $)
      1arithlem26 $p |- ( Z e. NN -> E. g e. R Z = ( M ` g ) ) $=
        ( vx vy cv wceq wrex c1 c0 eqid vp vh cn eqeq1 rexbidv wcel 1arithlem12
        cfv wa eqcom anbi2i mpbi fveq2 eqeq2d rcla4ev ax-mp cdivides wbr cprime
        crab cr clt csup chash caddc co cop csn cun 1arithlem25 indstr2 vtoclga
        ) MOZDOZHUHZPZDBQZIVOPZDBQMIUCVMIPVPVRDBVMIVOUDUEVQNOZVOPZDBQRVOPZDBQZM
        NVMRPVPWADBVMRVOUDUEVMVSPVPVTDBVMVSVOUDUESBUFZRSHUHZPZUIZWBWCWDRPZUIWFA
        BCEFGHJKLUGWGWEWCWDRUJUKULWAWEDSBVNSPVOWDRVNSHUMUNUOUPMNAUAOVMUQURUAUSU
        TZVAVBVCZBWHCDUBEFGUBOZWJVDUHZRVEVFWIVGVHVIZHWKJUAKLWHTWITWLTWKTVJVKVL
        $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      1arithlem27.3 $e |- N = ( # ` F ) $.
      $( Lemma for ~ 1arith . $)
      1arithlem27 $p |- ( F e. R ->
          dom ( F |` ( 1 ... ( N - 1 ) ) ) = ( 1 ... ( N - 1 ) ) ) $=
        ( wcel c1 co cfz wss wceq syl cmin cdm cres cn0 cprime chash 1arithlem8
        wf cfv wa mpan2 simpld caddc oprex fzssp1 ax-mp cc nn0cn ax-1cn opreq2d
        cvv npcan syl5sseq simprd fdm sseqtr4d ssdmres sylib ) GBNZOIOUAPZQPZGU
        BZRGVKUCUBVKSVIVKOIQPZVLVIIUDNZVKVMRVIVNVMUEGUHZVIIGUFUISVNVOUJMABCDEFG
        HIJKLUGUKZULVNOVJOUMPZQPZVKVMVJVANVKVRRIOUAUNOVJVAUOUPVNVQIOQVNIUQNZVQI
        SZIURVSOUQNVTUSIOVBUKTUTVCTVIVOVLVMSVIVNVOVPVDVMUEGVETVFVKGVGVH $.
        $( [2-Sep-2013] $) $( [17-Nov-2012] $)
    $}

    ${
      1arithlem28.3 $e |- S = { p e. Prime | p || Z } $.
      1arithlem28.4 $e |- Q = sup ( S , RR , < ) $.

      ${
        $d F f j k n s $.  $d F f z $.  $d Q z $.
        1arithlem28.5 $e |- N = ( ( # ` F ) - 1 ) $.
        1arithlem28.6 $e |- G = ( F |` ( 1 ... N ) ) $.
        $( Lemma for ~ 1arith . $)
        1arithlem28 $p |- ( ( F e. R /\ Z = ( M ` F ) /\ Z e. ( ZZ>= ` 2 ) ) ->
                            ( G e. R /\ ( Z / Q ) = ( M ` G ) ) ) $=
          ( cK wcel cfv wceq c2 cuz w3a cdiv co chash c1 caddc wa simp1 opreq1i
          cn0 cmin cc simp2 simp3 eleq1 biimpa syl2anc 1arithlem14 biimpar nncn
          syl ax-1cn a1i npcan syl5eq nnm1nn0 syl5eqel 1arithlem18 syl3anc cmul
          cn simpld 1arithlem19 fveq2d 1arithlem23 eqtrd wb eqeq1 mpbird cprime
          opreq2d cdivides wbr cle 1arithlem3 simp1d prmnn 3syl 1arithlem17 jca
          wi ) ICUCZMIKUDZUEZMUFUGUDZUCZUHZJCUCZMBUIUJJKUDZUEZXDXELJUKUDUEZXDWS
          LULUMUJZIUKUDZUEZLUQUCZXEXHUNWSXAXCUOZXDXIXJULURUJZULUMUJZXJLXNULUMTU
          PXDXJUSUCZULUSUCZXOXJUEXDXJVRUCZXPXDWSWTXBUCZXRXMXDXAXCXSWSXAXCUTZWSX
          AXCVAZXAXCXSMWTXBVBVCVDWSXRXSACEFGHIKNPQVEVFVDZXJVGVHXQXDVIVJXJULVKVD
          VLZXDLXNUQTXDXRXNUQUCYBXJVMVHVNZACEFGHIJKLNPQUAVOVPVSZXDXGMXFBVQUJZUE
          ZXDYGWTYFUEZXDWTXFXIIUDZVQUJZYFXDWSXKXLWTYJUEXMYCYDACEFGHIJKLNPQUAVTV
          PXDYIBXFVQXDYIXJIUDBXDXIXJIYCWAABCDEFGHIKMNOPQRSWBWCWHWCXDXAYGYHWDXTM
          WTYFWEVHWFXDXEXCBVRUCZXGYGWDYEYAXDXCBWGUCZYKYAXCYLBMWIWJUBDUCUBBWKWJW
          RBDUBMORSWLWMBWNWOABCEFGHJKMNPQWPVPWFWQ $.
          $( [21-Nov-2012] $) $( [17-Nov-2012] $)
      $}

      ${
        1arithlem29.5 $e |- G = ( g |` ( 1 ... ( ( # ` g ) - 1 ) ) ) $.
        1arithlem29.6 $e |- H = ( h |` ( 1 ... ( ( # ` h ) - 1 ) ) ) $.

        ${
          $d Q z $.
          1arithlem29.7 $e |- ( ph <-> ( Z e. ( ZZ>= ` 2 ) /\
            ( g e. R /\ h e. R ) /\ ( Z = ( M ` g ) /\ Z = ( M ` h ) ) ) ) $.
          $( Lemma for ~ 1arith . $)
          1arithlem29 $p |- ( ( ph /\ G = H ) -> g = h ) $=
            ( wceq wa cv chash cfv cop csn cun simpr c1 cmin co cfz dmeq adantl
            cdm wb wcel cuz simp2bi simpld simprd cres dmeqi 1arithlem27 syl5eq
            c2 eqid eqeqan12d syl2anc adantr mpbid cn0 simp3bi simp1bi eqeltrrd
            cn 1arithlem14 biimpar nnm1nn0 syl fz1eqb cc ax-1cn subcan2 syl3anc
            a1i opeq1d sneqd uneq12d cprime wf w3a eqidd 1arithlem8 1arithlem23
            nncn fseq1m1p1 syl13anc simp3d eqeq12d mpbird ) ALMUEZUFZGUGZHUGZUE
            ZLXIUHUIZCUJZUKZULZMXJUHUIZCUJZUKZULZUEZXHLMXNXRAXGUMXHXMXQXHXLXPCX
            HXLUNUOUPZXPUNUOUPZUEZXLXPUEZXHUNYAUQUPZUNYBUQUPZUEZYCXHLUTZMUTZUEZ
            YGXGYJALMURUSAYJYGVAZXGAXIDVBZXJDVBZYKAYLYMAOVKVCUIZVBZYLYMUFZOXINU
            IZUEZOXJNUIZUEZUFZUDVDZVEZAYLYMUUBVFZYLYMYHYEYIYFYLYHXIYEVGZUTYELUU
            EUBVHBDFIJKXINXLPRSXLVLVIVJYMYIXJYFVGZUTYFMUUFUCVHBDFIJKXJNXPPRSXPV
            LVIVJVMVNVOVPAYGYCVAZXGAYAVQVBZYBVQVBZUUGAXLWAVBZUUHAYLYQYNVBZUUJUU
            CAOYQYNAYRYTAYOYPUUAUDVRZVEZAYOYPUUAUDVSZVTYLUUJUUKBDFIJKXINPRSWBWC
            VNZXLWDWEAXPWAVBZUUIAYMYSYNVBZUUPUUDAOYSYNAYRYTUULVFZUUNVTYMUUPUUQB
            DFIJKXJNPRSWBWCVNZXPWDWEYAYBWFVNVOVPAYCYDVAZXGAXLWGVBZXPWGVBZUNWGVB
            ZUUTAUUJUVAUUOXLXAWEAUUPUVBUUSXPXAWEUVCAWHWKXLXPUNWIWJVOVPWLWMWNAXK
            XTVAXGAXIXOXJXSAYEWOLWPZCWOVBZXIXOUEZAUUJUNXLUQUPWOXIWPZXLXIUICUEZL
            UUEUEZUVDUVEUVFWQZUUOAXLVQVBZUVGAYLXLXLUEUVKUVGUFUUCAXLWRBDFIJKXINX
            LPRSWSVNVFAYLYRYOUVHUUCUUMUUNBCDEFIJKXINOPQRSTUAWTWJUVIAUBWKUUJUVJU
            VGUVHUVIWQWOCLXIXNXLXNVLXBWCXCXDAYFWOMWPZUVEXJXSUEZAUUPUNXPUQUPWOXJ
            WPZXPXJUICUEZMUUFUEZUVLUVEUVMWQZUUSAXPVQVBZUVNAYMXPXPUEUVRUVNUFUUDA
            XPWRBDFIJKXJNXPPRSWSVNVFAYMYTYOUVOUUDUURUUNBCDEFIJKXJNOPQRSTUAWTWJU
            VPAUCWKUUPUVQUVNUVOUVPWQWOCMXJXRXPXRVLXBWCXCXDXEVOXF $.
            $( [21-Nov-2012] $) $( [17-Nov-2012] $)
        $}

        ${
          $d G u v $.  $d H v z $.  $d Q g h u v y $.  $d Q g h z $.
          $d Z g h u v y $.
          $( Lemma for ~ 1arith . $)
          1arithlem30 $p |- ( Z e. ( ZZ>= ` 2 ) ->
              ( A. y e. NN ( y < Z -> A. g e. R A. h e. R
                ( ( y = ( M ` g ) /\ y = ( M ` h ) ) -> g = h ) ) ->
    A. g e. R A. h e. R ( ( Z = ( M ` g ) /\ Z = ( M ` h ) ) -> g = h ) ) ) $=
            ( vu vv cv clt wbr cfv wceq wa wi wral cn c2 cuz wcel eqeq2d anbi1d
            fveq2 eqeq1 imbi12d anbi2d eqeq2 cbvral2v imbi2i cdiv co 1arithlem4
            ralbii simprd simpld anbi12d imbi1d 2ralbidv rcla4v syl mpid syl6ib
            breq1 rcla42v com12 syl6 impexp syl6ibr w3a biid 1arithlem29 3expib
            ex wb simp2l simp3l simp1 chash cmin eqid 1arithlem28 simp2r simp3r
            syl3anc jca an4 sylib imim21b mpbird syld syl5bir ralrimdvv syl5bi
            c1 ) AUFZOUGUHZXLGUFZNUIZUJZXLHUFZNUIZUJZUKZXNXQUJZULZHDUMGDUMZULZA
            UNUMZXMXLUDUFZNUIZUJZXLUEUFZNUIZUJZUKZYFYIUJZULZUEDUMUDDUMZULZAUNUM
            ZOUOUPUIUQZOXOUJZOXRUJZUKZYAULZHDUMGDUMYDYPAUNYCYOXMYBYNYHXSUKZYFXQ
            UJZULGHUDUEDDXNYFUJZXTUUCYAUUDUUEXPYHXSUUEXOYGXLXNYFNUTZURUSXNYFXQV
            AZVBXQYIUJZUUCYLUUDYMUUHXSYKYHUUHXRYJXLXQYINUTZURVCXQYIYFVDZVBVEVFV
            JZYRYQUUBGHDDYQYEYRXNDUQZXQDUQZUKZUUBULZUUKYRYEUUNUUAUKZYAULZUUOYRY
            ELDUQZMDUQZUKZOCVGVHZLNUIZUJZUVAMNUIZUJZUKZUKZLMUJZULZUUQYRYEUUTUVF
            UVHULZULZUVIYRYEUVAYGUJZUVAYJUJZUKZYMULZUEDUMUDDUMZUVKYRYEUVAXOUJZU
            VAXRUJZUKZYAULZHDUMGDUMZUVPYRYEUVAOUGUHZUWAYRUVAUNUQZUWBCEOQTUAVIZV
            KYRUWCYEUWBUWAULZULYRUWCUWBUWDVLYDUWEAUVAUNXLUVAUJZXMUWBYCUWAXLUVAO
            UGVTUWFYBUVTGHDDUWFXTUVSYAUWFXPUVQXSUVRXLUVAXOVAXLUVAXRVAVMVNVOVBVP
            VQVRUVTUVOUVLUVRUKZUUDULGHUDUEDDUUEUVSUWGYAUUDUUEUVQUVLUVRUUEXOYGUV
            AUUFURUSUUGVBUUHUWGUVNUUDYMUUHUVRUVMUVLUUHXRYJUVAUUIURVCUUJVBVEVSUU
            TUVPUVJUVOUVJUVCUVMUKZLYIUJZULUDUELMDDYFLUJZUVNUWHYMUWIUWJUVLUVCUVM
            UWJYGUVBUVAYFLNUTURUSYFLYIVAVBYIMUJZUWHUVFUWIUVHUWKUVMUVEUVCUWKYJUV
            DUVAYIMNUTURVCYIMLVDVBWAWBWCUUTUVFUVHWDWEYRUVIUUQULZUUPUVHYAULZULZY
            RUUNUUAUWMYRUUNUUAWFZUVHYAUWOBCDEFGHIJKLMNOPQRSTUAUBUCUWOWGWHWJWIYR
            UUPUVGULUWLUWNWKYRUUNUUAUVGUWOUURUVCUKZUUSUVEUKZUKUVGUWOUWPUWQUWOUU
            LYSYRUWPYRUULUUMUUAWLYRUUNYSYTWMYRUUNUUAWNZBCDEFIJKXNLNXNWOUIXKWPVH
            ZOPQRSTUAUWSWQUBWRXAUWOUUMYTYRUWQYRUULUUMUUAWSYRUUNYSYTWTUWRBCDEFIJ
            KXQMNXQWOUIXKWPVHZOPQRSTUAUWTWQUCWRXAXBUURUVCUUSUVEXCXDWIUVGUUPUVHY
            AXEVQXFXGUUNUUAYAWDVSXHXIXJ $.
            $( [21-Nov-2012] $) $( [17-Nov-2012] $)
        $}
      $}
    $}

    ${
      $d G h x $.  $d p x z $.
      $( Lemma for ~ 1arith . $)
      1arithlem31 $p |- ( ( F e. R /\ G e. R ) ->
                          ( ( M ` F ) = ( M ` G ) -> F = G ) ) $=
        ( vg vh wcel wa cfv cn wceq c1 vx vy vp 1arithlem11 ffvelrn mpan adantr
        wi wf cv wrex risset simpl eqeq1 biimpar jca anbi12d imbi1d 2ralbidv c0
        wral eqcom anbi12i chash cc0 1arithlem15 bi2anan9 cfn 1arithlem10 hash0
        syl bitr3d syl5bbr eqtr3 syl6bi rgen2 cdivides wbr cprime crab clt csup
        wb cr cmin co cres eqid 1arithlem30 indstr2 fveq2 eqeq2d anbi1d imbi12d
        cfz anbi2d eqeq2 rcla42v syl5 syl7 exp4a rexlimdv syl5bi mpd ) GBOZHBOZ
        PZGIQZROZXHHIQZSZGHSZUHZXEXIXFBRIUIXEXIABCDEFIJKLUDBRGIUEUFUGXIUAUJZXHS
        ZUARUKXGXMUAXHRULXGXOXMUARXGXNROZXOXKXLXOXKPZXOXNXJSZPZXGXPXLXQXOXRXOXK
        UMXOXRXKXNXHXJUNUOUPXPXNMUJZIQZSZXNNUJZIQZSZPZXTYCSZUHZNBVAMBVAZXGXSXLU
        HZYIUBUJZYASZYKYDSZPZYGUHZNBVAMBVATYASZTYDSZPZYGUHZNBVAMBVAUAUBXNTSZYHY
        SMNBBYTYFYRYGYTYBYPYEYQXNTYAUNXNTYDUNUQURUSXNYKSZYHYOMNBBUUAYFYNYGUUAYB
        YLYEYMXNYKYAUNXNYKYDUNUQURUSYSMNBBXTBOZYCBOZPZYRXTUTSZYCUTSZPZYGYRYATSZ
        YDTSZPZUUDUUGUUHYPUUIYQYATVBYDTVBVCUUDXTVDQZVESZYCVDQZVESZPUUJUUGUUBUUL
        UUHUUCUUNUUIABCDEFXTIJKLVFABCDEFYCIJKLVFVGUUBUULUUEUUCUUNUUFUUBXTVHOUUL
        UUEWCABCDEFXTIJKLVIXTVJVKUUCYCVHOUUNUUFWCABCDEFYCIJKLVIYCVJVKVGVLVMXTYC
        UTVNVOVPUBAUCUJXNVQVRUCVSVTZWDWAWBZBUUOCMNDEFXTTUUKTWEWFWOWFWGZYCTUUMTW
        EWFWOWFWGZIXNJUCKLUUOWHUUPWHUUQWHUURWHWIWJYHYJXOYEPZGYCSZUHMNGHBBXTGSZY
        FUUSYGUUTUVAYBXOYEUVAYAXHXNXTGIWKWLWMXTGYCUNWNYCHSZUUSXSUUTXLUVBYEXRXOU
        VBYDXJXNYCHIWKWLWPYCHGWQWNWRWSWTXAXBXCXD $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $( Fundamental theorem of arithmetic, where a prime factorization is
         represented as a finite monotonic 1-based sequence of primes.  The
         function ` M ` maps the set of prime factorizations ` R ` one-to-one
         onto the positive integers.  (Contributed by Paul Chapman,
         17-Nov-2012.) $)
      1arith $p |- M : R -1-1-onto-> NN $=
        ( vg vh cn wf1o wf1 cv cfv wceq wral mpbir2an wfo df-f1o wf 1arithlem11
        wi dff13 1arithlem31 rgen2 wrex dffo3 1arithlem26 rgen ) BMGNBMGOZBMGUA
        ZBMGUBUMBMGUCZKPZGQZLPZGQRUPURRUEZLBSKBSKLBMGUFABCDEFGHIJUDZUSKLBBABCDE
        FUPURGHIJUGUHTUNUOAPZUQRKBUIZAMSKABMGUJUTVBAMABCKDEFGVAHIJUKULTT $.
        $( [17-Nov-2012] $)
    $}

    ${
      $( Fundamental theorem of arithmetic, where a prime factorization is
         represented as a finite monotonic 1-based sequence of primes.  Every
         positive integer has a unique prime factorization.  (Contributed by
         Paul Chapman, 17-Nov-2012.) $)
      1arith2 $p |- A. z e. NN E! g e. R ( M ` g ) = z $=
        ( cv cfv wceq wreu cn wf1o wcel 1arith f1ofveu mpan rgen ) DLHMALZNDBOZ
        APBPHQUCPRUDABCEFGHIJKSDBPUCHTUAUB $.
        $( [17-Nov-2012] $)
    $}
  $}


