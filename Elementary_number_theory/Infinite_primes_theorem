$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          Infinite primes theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d m n y A $.  $d m n y G $.
    unbenlem.1 $e |- G = ( rec ( ( x e. _V |-> ( x + 1 ) ) , 1 ) |` om ) $.
    $( Lemma for ~ unben . $)
    unbenlem $p |-
        ( ( A C_ NN /\ A. m e. NN E. n e. A m < n ) -> A ~~ om ) $=
      ( vy cn clt wbr wrex wa com wcel wf1o wceq wb ax-mp wi syl wral ccnv cima
      wss cv cen cvv cres nnex ssex wf1 c1 cuz cfv om2uzf1oi nnuz f1oeq3 f1ocnv
      1z mpbir f1of1 f1ores mpan f1oeng syl2anc adantr crn imassrn cdm dfdm4 wf
      f1of fdmi eqtr3i sseqtri om2uzuzi syl6eleqr breq1 rexbidv wfun funcnvres2
      rcla4v f1ofun f1oeq1 sylib wfo f1ofo forn eleq2d wfn f1ofn fvelrnb bitr3d
      biimpa fvres eqeq1d sseli om2uzlt2i sylan2 breq2 sylan9bb syldan biimparc
      adantll exp44 imp31 reximdva syl5 exp4b com4l imp rexlimdv com3l ralrimiv
      syld ex unbnn3 sylancr entr ) BHUDZCUEZDUEZIJZDBKZCHUAZLZBEUBZBUCZUFJZYHM
      UFJZBMUFJXTYIYEXTBUGNBYHYGBUHZOZYIBHUIUJHMYGUKZXTYLHMYGOZYMMHEOZYNYOMULUM
      UNZEOZAULEUSFUOHYPPYOYQQUPHYPMEUQRUTZMHEURRHMYGVARHMBYGVBVCZBYHUGYKVDVEVF
      YFYHMUDGUEZYANZCYHKZGMUAYJYHYGVGZMYGBVHEVIUUCMEVJMHEYOMHEVKYRMHEVLRVMVNVO
      ZYFUUBGMXTYEYTMNZUUBSUUEXTYEUUBUUEXTYEUUBSUUEXTLZYEYTEUNZYBIJZDBKZUUBUUEY
      EUUISZXTUUEUUGHNUUJUUEUUGYPHAYTULEUSFVPUPVQYDUUICUUGHYAUUGPYCUUHDBYAUUGYB
      IVRVSWBTVFUUFUUHUUBDBUUEXTYBBNZUUHUUBSSUUHUUEXTUUKUUBUUHUUEXTUUKUUBXTUUKL
      YAEYHUHZUNZYBPZCYHKZUUHUUELZUUBXTUUKUUOXTYHBUULOZUUKUUOQXTYHBYKUBZOZUUQXT
      YLUUSYSBYHYKURTUURUULPZUUSUUQQEVTZUUTYOUVAYRMHEWCRBEWARYHBUURUULWDRWEUUQY
      BUULVGZNZUUKUUOUUQUVBBYBUUQYHBUULWFUVBBPYHBUULWGYHBUULWHTWIUUQUULYHWJUVCU
      UOQYHBUULWKCYHYBUULWLTWMTWNUUPUUNUUACYHUUHUUEYAYHNZUUNUUASUUHUUEUVDUUNUUA
      UUEUVDLZUUNLUUAUUHUVEUUNYAEUNZYBPZUUAUUHQUVDUUNUVGUUEUVDUUNUVGUVDUUMUVFYB
      YAYHEWOWPWNXDUVEUUAUUGUVFIJZUVGUUHUVDUUEYAMNUUAUVHQYHMYAUUDWQAYTYAULEUSFW
      RWSUVFYBUUGIWTXAXBXCXEXFXGXHXIXJXKXLXOXPXMXKXNGCYHXQXRBYHMXSVE $.
      $( [15-Sep-2013] $) $( [5-May-2005] $)
  $}

  ${
    $d m n x y A $.
    $( An unbounded set of natural numbers is infinite. $)
    unben $p |- ( ( A C_ NN /\ A. m e. NN E. n e. A m < n ) -> A ~~ NN ) $=
      ( vx cn wss cv clt wbr wrex wral wa com cen cvv c1 caddc co cmpt crdg
      cres eqid unbenlem omex nnenom ensymi entr sylancl ) AEFBGCGHICAJBEKLAMNI
      MENIAENIDABCDODGPQRSPTMUAZUIUBUCEMUDUEUFAMEUGUH $.
      $( [15-Sep-2013] $) $( [5-May-2005] $)
  $}

  ${
    $d j k N $.  $d j M $.  $d j k K $.
    infpnlem.1 $e |- K = ( ( ! ` N ) + 1 ) $.
    $( Lemma for ~ infpn .  The smallest divisor (greater than 1) ` M ` of
       ` N ! + 1 ` is a prime greater than ` N ` . $)
    infpnlem1 $p |- ( ( N e. NN /\ M e. NN ) ->
                        ( ( ( 1 < M /\ ( K / M ) e. NN ) /\
                  A. j e. NN ( ( 1 < j /\ ( K / j ) e. NN ) -> M <_ j ) ) ->
       ( N < M /\ A. j e. NN ( ( M / j ) e. NN -> ( j = 1 \/ j = M ) ) ) ) ) $=
      ( cn wcel wa c1 clt wbr cdiv co cle wi wral wb cr nnre syl cv wo wn lenlt
      syl2an ancoms adantr cn0 nnnn0 cfa cfv caddc cz facndiv opreq1i syl5eqelr
      wceq nnz nsyl sylanl1 sylbird con4d expimpd adantrd w3a cc faccl peano2nn
      expr syl5eqel nncn nndivtr 3com13 3expa adantrl biimprd exp4b com3l imp32
      ex letri3 adantll imim2d com23 sylan2d exp4d com24 exp32 imp31 com14 3imp
      ralimdva adantld imp3a prime adantl sylibrd jcad ) DFGZCFGZHZICJKZBCLMZFG
      ZHZIAUAZJKZBXFLMFGZHZCXFNKZOZAFPZHZDCJKZCXFLMFGZXFIUQXFCUQZUBOAFPZXAXEXNX
      LXAXBXDXNXAXBHZXNXDXRXNUCZCDNKZXDUCZXAXTXSQZXBWTWSYBWTCRGZDRGYBWSCSZDSCDU
      DUEUFUGXAXBXTYAWSDUHGZWTXBXTHZYADUIZYEWTHYFHDUJUKZIULMZCLMZUMGXDDCUNXDYJX
      CUMBYICLEUOXCURUPUSUTVIVAVBVCVDXAXMXGXFCNKZXOVEZXPOZAFPZXQXAXEXLYNXAXDXLY
      NOZXBXAXDYOXAXDHZXKYMAFYLYPXFFGZHZXKXPXGYKXOYRXKXPOZOYRYKXOXGYSXAXDYQYKXO
      XGYSOOZOXAYKYQXDYTXAYKYQXDYTOXAYKYQHZHZXGXOXDYSUUBXGXOXDYSUUBXOXDHZXHXGYS
      XAYQUUCXHOZYKWSBVFGZWTYQUUDWSBFGUUEWSBYIFEWSYHFGZYIFGWSYEUUFYGDVGTYHVHTVJ
      BVKTUUEWTYQUUDYQWTUUEUUDYQWTUUEVEUUCXHXFCBVLVTVMVNUTVOUUBXKXIXPUUBXJXPXIW
      TUUAXJXPOZWSWTYKYQUUGYQWTYKUUGYQWTYKXJXPYQWTHXPYKXJHZYQXFRGYCXPUUHQWTXFSY
      DXFCWAUEVPVQVRVSWBWCWDWEWFWGWHWGWIWJWKVRWLVTWMWNWTXQYNQWSACWOWPWQWR $.
      $( [5-May-2005] $)

    $( Lemma for ~ infpn .  For any natural number ` N ` , there exists a prime
       number ` j ` greater than ` N ` . $)
    infpnlem2 $p |- ( N e. NN -> E. j e. NN ( N < j /\
                    A. k e. NN ( ( j / k ) e. NN -> ( k = 1 \/ k = j ) ) ) ) $=
      ( cn wcel c1 cv clt wbr cdiv co wa cle wi wral wrex wceq syl wo cfa caddc
      cfv cn0 nnnn0 faccl peano2nn syl5eqel nnge1 wb 1nn nnleltp1 sylancr mpbid
      syl6breqr cc cc0 wne nnne0 jca divid syl6eqel breq2 opreq2 eleq1d anbi12d
      nncn rcla4ev syl12anc nnwos infpnlem1 reximdva mpd ) DFGZHAIZJKZCVPLMZFGZ
      NZHBIZJKZCWALMZFGZNZVPWAOKPBFQNZAFRZDVPJKVPWALMFGWAHSWAVPSUAPBFQNZAFRVOVT
      AFRZWGVOCFGZHCJKZCCLMZFGZWIVOCDUBUDZHUCMZFEVOWNFGZWOFGVODUEGWPDUFDUGTZWNU
      HTUIZVOHWOCJVOHWNOKZHWOJKZVOWPWSWQWNUJTVOHFGWPWSWTUKULWQHWNUMUNUOEUPVOWLH
      FVOCUQGZCURUSZNZWLHSVOWJXCWRWJXAXBCVHCUTVATCVBTULVCVTWKWMNACFVPCSZVQWKVSW
      MVPCHJVDXDVRWLFVPCCLVEVFVGVIVJVTWEABVPWASZVQWBVSWDVPWAHJVDXEVRWCFVPWACLVE
      VFVGVKTVOWFWHAFBCVPDEVLVMVN $.
      $( [5-May-2005] $)
  $}

  ${
    $d j k N $.
    $( There exist infinitely many prime numbers: for any natural number
       ` N ` , there exists a prime number ` j ` greater than ` N ` .  (See
       ~ infpn2 for the equinumerosity version.) $)
    infpn $p |- ( N e. NN -> E. j e. NN ( N < j /\
                    A. k e. NN ( ( j / k ) e. NN -> ( k = 1 \/ k = j ) ) ) ) $=
      ( cfa cfv c1 caddc co eqid infpnlem2 ) ABCDEFGHZCKIJ $.
      $( [1-Jun-2006] $)
  $}

  ${
    $d j k n m A $.  $d j k S $.
    infpn2.1 $e |- S = { n e. NN | ( 1 < n /\
                A. m e. NN ( ( n / m ) e. NN -> ( m = 1 \/ m = n ) ) ) } $.
    $( There exist infinitely many prime numbers: the set of all primes ` S `
       is unbounded by ~ infpn , so by ~ unben it is infinite. $)
    infpn2 $p |- S ~~ NN $=
      ( vj vk cn cv clt wbr wrex wral c1 cdiv co wcel wceq wi wa cr wss wo crab
      cen ssrab2 eqsstri infpn cle nnge1 adantr nnre lelttr mp3an1 syl2an mpand
      1re ancld anim1d anass syl6ib reximdva breq2 opreq1 eleq1d orbi2d imbi12d
      mpd eqeq2 ralbidv anbi12d elrab2 anbi1i anbi2i 3bitri rexbii2 sylibr rgen
      ancom unben mp2an ) AGUAEHZFHZIJZFAKZEGLAGUDJAMCHZIJZWEBHZNOZGPZWGMQZWGWE
      QZUBZRZBGLZSZCGUCGDWOCGUEUFWDEGWAGPZWCMWBIJZWBWGNOZGPZWJWGWBQZUBZRZBGLZSZ
      SZFGKZWDWPWCXCSZFGKXFFBWAUGWPXGXEFGWPWBGPZSZXGWCWQSZXCSXEXIWCXJXCXIWCWQXI
      MWAUHJZWCWQWPXKXHWAUIUJWPWATPZWBTPZXKWCSWQRZXHWAUKWBUKMTPXLXMXNUPMWAWBULU
      MUNUOUQURWCWQXCUSUTVAVGWCXEFAGWBAPZWCSXHXDSZWCSXHXDWCSZSXHXESXOXPWCWOXDCW
      BGAWEWBQZWFWQWNXCWEWBMIVBXRWMXBBGXRWIWSWLXAXRWHWRGWEWBWGNVCVDXRWKWTWJWEWB
      WGVHVEVFVIVJDVKVLXHXDWCUSXQXEXHXDWCVRVMVNVOVPVQAEFVSVT $.
      $( [5-May-2005] $)
  $}

  ${
    $d N p $.
    $( The primes are unbounded.  (Contributed by Paul Chapman,
       28-Nov-2012.) $)
    prmunb $p |- ( N e. NN -> E. p e. Prime N < p ) $=
      ( cn wcel cn0 clt wbr cprime wrex nnnn0 cfv c1 caddc co cdivides c2 wa cz
      cuz syl cv cfa faccl elnnuz eluzp1p1 df-2 fveq2i syl6eleqr sylbi exprmfct
      3syl cle wn wi wb prmz nn0z eluz syl2an simpl prmuz2 eluz2b2 sylib simpld
      simpr eluznn0 syl2anc nnz simprd dvdsfac w3a ndvdsp1 imp syl31anc sylbird
      ex adantr con2d ancoms cr nn0re zre ltnle sylibrd reximdva mpd ) ACDAEDZA
      BUAZFGZBHIZAJWGWHAUBKZLMNZOGZBHIZWJWGWKCDZWLPSKZDZWNAUCZWOWKLSKDZWQWKUDWS
      WLLLMNZSKWPLWKUEPWTSUFUGUHUIWLBUJUKWGWMWIBHWGWHHDZQWMWHAULGZUMZWIXAWGWMXC
      UNXAWGQZXBWMXDXBAWHSKDZWMUMZXAWHRDZARDXEXBUOWGWHUPZAUQWHAURUSXAXEXFUNWGXA
      XEXFXAXEQZWKRDZWHCDZLWHFGZWHWKOGZXFXIWGWOXJXIWHEDZXEWGXIXKXNXIXKXLXIXAXKX
      LQZXAXEUTXAWHWPDXOWHVAWHVBVCTZVDZWHJTXAXEVEZAWHVFVGWRWKVHUKXQXIXKXLXPVIXI
      XKXEXMXQXRWHAVJVGXJXKXLVKXMXFWHWKVLVMVNVPVQVOVRVSWGAVTDWHVTDZWIXCUOXAAWAX
      AXGXSXHWHWBTAWHWCUSWDWEWFT $.
      $( [28-Nov-2012] $)
  $}

  ${
    $d n p $.
    $( There are an infinite number of primes.  (Contributed by Paul Chapman,
       28-Nov-2012.) $)
    prminf $p |- Prime ~~ NN $=
      ( vn vp cprime cn wss clt wbr wrex wral cen prmnn ssriv prmunb rgen unben
      cv mp2an ) CDEAPZBPZFGBCHZADICDJGBCDSKLTADRBMNCABOQ $.
      $( [28-Nov-2012] $)
  $}

