$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                            Euclid's Algorithm
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    eucalgval.1 $e |- E = { <. x , y >. | ( x e. ( NN0 X. NN0 ) /\
      y = if ( ( 2nd ` x ) = 0 , x , <. ( 2nd ` x ) , ( mod ` x ) >. ) ) } $.

    ${
      $d X m n $.  $d m n x y $.
      $( Euclid's Algorithm computes the greatest common divisor of two
         nonnegative integers by repeatedly replacing the larger of them with
         its remainder modulo the smaller until the remainder is 0.

         The value of the step function ` E ` for Euclid's Algorithm.
         (Contributed by Paul Chapman, 31-Mar-2011.) $)
      eucalgval $p |- ( X e. ( NN0 X. NN0 ) -> ( E ` X ) =
          if ( ( 2nd ` X ) = 0 , X , <. ( 2nd ` X ) , ( mod ` X ) >. ) ) $=
        ( vm vn cn0 wcel c2nd cfv cc0 wceq cmo cop cif cvv cv fveq2 wa cxp opex
        ifexg mpan2 eqeq1d id opeq12d ifbieq12d copab wb adantr eqeq12d anbi12d
        eleq1 simpr cbvopabv eqtri fvopab4g mpdan ) DHHUAZIZDJKZLMZDVBDNKZOZPZQ
        IZDCKVFMVAVEQIVGVBVDUBVCDVEUTQUCUDFGDFRZJKZLMZVHVIVHNKZOZPZVFUTQCVHDMZV
        JVCVHVLDVEVNVIVBLVHDJSZUEVNUFVNVIVBVKVDVOVHDNSUGUHCARZUTIZBRZVPJKZLMZVP
        VSVPNKZOZPZMZTZABUIVHUTIZGRZVMMZTZFGUIEWEWIABFGVPVHMZVRWGMZTZVQWFWDWHWJ
        VQWFUJWKVPVHUTUNUKWLVRWGWCVMWJWKUOWJWCVMMWKWJVTVJVPWBVHVLWJVSVILVPVHJSZ
        UEWJUFWJVSVIWAVKWMVPVHNSUGUHUKULUMUPUQURUS $.
        $( [22-Jun-2011] $) $( [31-Mar-2011] $)
    $}

    ${
      $d x y $.
      $( The value of the step function ` E ` for Euclid's Algorithm on an
         ordered pair.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
      eucalgval2 $p |- ( ( M e. NN0 /\ N e. NN0 ) -> ( E ` <. M , N >. ) =
                       if ( N = 0 , <. M , N >. , <. N , ( M mod N ) >. ) ) $=
        ( cn0 wcel wa cop cfv c2nd cc0 wceq cmo cif co cxp opelxpi eqcomd eqtrd
        eucalgval syl op2ndg eqeq1d df-opr a1i opeq12d ifbieq2d ) DGHEGHIZDEJZC
        KZUKLKZMNZUKUMUKOKZJZPZEMNZUKEDEOQZJZPUJUKGGRHULUQNDEGGSABCUKFUBUCUJUNU
        RUPUTUKUJUMEMDEGGUDZUEUJUTUPUJEUMUSUOUJUMEVATUSUONUJDEOUFUGUHTUIUA $.
        $( [22-Jun-2011] $) $( [31-Mar-2011] $)
    $}

    ${
      $d E m n $.  $d X m n $.  $d m n x y $.

      $( Domain and codomain of the step function ` E ` for Euclid's
         Algorithm.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
      eucalgf $p |- E : ( NN0 X. NN0 ) --> ( NN0 X. NN0 ) $=
        ( vm vn cn0 cv co wcel wral cfv cc0 wceq cmo cop cif wa opelxpi adantr
        cxp wfn ffnoprv c2nd cvv visset opex ifex a1i rgen fnopab2g mpbi df-opr
        wf eucalgval2 iftrue sylan9eq eleq1d mpbird wn simpr cn wo elnn0 anbi1i
        iffalse pm5.61 bitri nn0z zmodcl sylan adantrr sylan2b anassrs syl11anc
        cz eqeltrd pm2.61dan syl5eqel rgen2 mpbir2an ) GGUAZWBCUNCWBUBZEHZFHZCI
        ZWBJZFGKEGKEFGGWBCUCAHZUDLZMNZWHWIWHOLZPZQZUEJZAWBKWCWNAWBWNWHWBJWJWHWL
        AUFWIWKUGUHUIUJABWBWMCDUKULWGEFGGWDGJZWEGJZRZWFWDWEPZCLZWBWDWECUMWQWEMN
        ZWSWBJZWQWTRZXAWRWBJZWQXCWTWDWEGGSTXBWSWRWBWQWTWSWTWRWEWDWEOIZPZQZWRABC
        WDWEDUOZWTWRXEUPUQURUSWQWTUTZRZWSXEWBWQXHWSXFXEXGWTWRXEVFUQXIWPXDGJZXEW
        BJWQWPXHWOWPVATWOWPXHXJWPXHRZWOWEVBJZXHRZXJXKXLWTVCZXHRXMWPXNXHWEVDVEXL
        WTVGVHWOXLXJXHWOWDVPJXLXJWDVIWDWEVJVKVLVMVNWEXDGGSVOVQVRVSVTWA $.
        $( [31-Mar-2011] $)

      $( The invariant of the step function ` E ` for Euclid's Algorithm is the
         ` gcd ` operator applied to the state.  (Contributed by Paul Chapman,
         31-Mar-2011.) $)
      eucalginv $p |- ( X e. ( NN0 X. NN0 ) ->
                        ( gcd ` ( E ` X ) ) = ( gcd ` X ) ) $=
        ( vm vn cn0 wcel cv cop wceq wrex cfv cgcd wa co sylan9eq fveq2d cz cxp
        elxp2 cc0 cmo cif eucalgval2 iftrue wn iffalse nn0z cn wo anbi1i pm5.61
        elnn0 bitri zmodcl syl simpr nnz gcdcom syl11anc modgcd adantrr sylan2b
        eqtr3d anassrs sylanl1 eqeq12i sylib eqtrd pm2.61dan eqeq12d syl5ibrcom
        df-opr fveq2 rexlimivv sylbi ) DHHUAIDFJZGJZKZLZGHMFHMDCNZONZDONZLZFGDH
        HUBWBWFFGHHVSHIZVTHIZPZWFWBWACNZONZWAONZLZWIVTUCLZWMWIWNPWJWAOWIWNWJWNW
        AVTVSVTUDQZKZUEZWAABCVSVTEUFZWNWAWPUGRSWIWNUHZPZWKWPONZWLWTWJWPOWIWSWJW
        QWPWRWNWAWPUIRSWTVTWOOQZVSVTOQZLZXAWLLWGVSTIZWHWSXDVSUJXEWHWSXDWHWSPZXE
        VTUKIZWSPZXDXFXGWNULZWSPXHWHXIWSVTUOUMXGWNUNUPXEXGXDWSXEXGPZWOVTOQZXBXC
        XJWOTIZVTTIZXKXBLXJWOHIXLVSVTUQWOUJURXJXGXMXEXGUSVTUTURWOVTVAVBVSVTVCVF
        VDVEVGVHXBXAXCWLVTWOOVOVSVTOVOVIVJVKVLWBWDWKWEWLWBWCWJODWACVPSDWAOVPVMV
        NVQVR $.
        $( [31-Mar-2011] $)

      $( The second member of the state decreases with each iteration of the
         step function ` E ` for Euclid's Algorithm.  (Contributed by Paul
         Chapman, 31-Mar-2011.) $)
      eucalglt $p |- ( X e. ( NN0 X. NN0 ) ->
        ( ( 2nd ` ( E ` X ) ) =/= 0 -> ( 2nd ` ( E ` X ) ) < ( 2nd ` X ) ) ) $=
        ( vm vn cn0 wcel wceq cfv c2nd cc0 wne clt wbr wi wa fveq2d breq12d cxp
        cv cop elxp2 wn df-ne pm2.24 syl5bi adantl cmo co cif eucalgval2 iftrue
        wrex sylan9eq visset op2nd syl6eq neeq1d a1i imbi12d mpbird cn wo elnn0
        anbi1i pm5.61 bitri crp nn0re nnrp modlt syl2an adantrr sylan2b anassrs
        cr iffalse oprex a1d pm2.61dan fveq2 syl5ibrcom rexlimivv sylbi ) DHHUA
        IDFUBZGUBZUCZJZGHUOFHUODCKZLKZMNZWLDLKZOPZQZFGDHHUDWJWPFGHHWGHIZWHHIZRZ
        WPWJWICKZLKZMNZXAWILKZOPZQZWSWHMJZXEWSXFRZXEWHMNZWHWHOPZQZXFXJWSXHXFUEZ
        XFXIWHMUFXFXIUGUHUIXGXBXHXDXIXGXAWHMXGXAXCWHXGWTWILWSXFWTXFWIWHWGWHUJUK
        ZUCZULZWIABCWGWHEUMZXFWIXMUNUPSWGWHFUQGUQZURZUSZUTXGXAWHXCWHOXRXCWHJZXG
        XQVATVBVCWSXKRZXDXBXTXDXLWHOPZWQWRXKYAWRXKRZWQWHVDIZXKRZYAYBYCXFVEZXKRY
        DWRYEXKWHVFVGYCXFVHVIWQYCYAXKWQWGVRIWHVJIYAYCWGVKWHVLWGWHVMVNVOVPVQXTXA
        XLXCWHOXTXAXMLKXLXTWTXMLWSXKWTXNXMXOXFWIXMVSUPSWHXLXPWGWHUJVTURUSXSXTXQ
        VATVCWAWBWJWMXBWOXDWJWLXAMWJWKWTLDWICWCSZUTWJWLXAWNXCOYFDWILWCTVBWDWEWF
        $.
        $( [31-Mar-2011] $)
    $}
  $}

  ${
    $d E z $.  $d R z $.  $d x y $.
    eucalg.1 $e |- S = ( NN0 X. NN0 ) $.
    eucalg.2 $e |- E = { <. x , y >. | ( x e. ( NN0 X. NN0 ) /\
       y = if ( ( 2nd ` x ) = 0 , x , <. ( 2nd ` x ) , ( mod ` x ) >. ) ) } $.
    eucalg.3 $e |-
                R = ( ( E o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { A } ) ) $.

    ${
      eucalgcvga.4 $e |- N = ( 2nd ` A ) $.
      $( Once Euclid's Algorithm halts after ` N ` steps, the second element of
         the state remains 0 .  (Contributed by Paul Chapman, 22-Jun-2011.) $)
      eucalgcvga $p |- ( A e. S -> ( K e. ( ZZ>= ` N ) ->
                                     ( 2nd ` ( R ` K ) ) = 0 ) ) $=
        ( wcel cfv c2nd cc0 wceq cn0 cxp fvres vz eleq2i wa cres simpl syl5eqel
        cuz xp2nd eluznn0 sylan wf nn0ex xpex eucalgf c1st csn cseq0 co xpeq12i
        ccom reseq2i coeq2i opreq1i eqtri algrf ffvelrn syl11anc syl6eqr fveq2d
        syl eleq2d f2ndres wne clt wbr eucalglt ffvelrni neeq1d breq12d imbi12d
        cv wi mpbird eqid algcvga sylbird imp eqtr3d sylanb ex ) CEMZGHUGNZMZGD
        NZONZPQZWKCRRSZMZWMWPEWQCIUBWRWMUCZWNOWQUDZNZWOPWSWRGRMZXAWOQZWRWMUEWRH
        RMWMXBWRHCONZRLCRRUHUFGHUIUJWRXBUCWNWQMZXCWRRWQDUKXBXECDWQFRRULULUMZABF
        JUNZDFUOEESZUDZUTZRCUPSZUQURFUOWQWQSZUDZUTZXKUQURKXJXNXKUQXIXMFXHXLUOEW
        QEWQIIUSVAVBVCVDZVERWQGDVFUJWNWQOTVJVGWRWMXAPQZWRWMGCWTNZUGNZMXPWRXRWLG
        WRXQHUGWRXQXDHCWQOTLVHVIVKUACWTDWQFGXQXFXGXORRVLUAWAZWQMZXSFNZWTNZPVMZY
        BXSWTNZVNVOZWBYAONZPVMZYFXSONZVNVOZWBABFXSJVPXTYCYGYEYIXTYBYFPXTYAWQMYB
        YFQWQWQXSFXGVQYAWQOTVJZVRXTYBYFYDYHVNYJXSWQOTVSVTWCXQWDWEWFWGWHWIWJ $.
        $( [22-Jun-2011] $)
    $}

    $d S z $.
    eucalg.4 $e |- A = <. M , N >. $.
    $( Euclid's Algorithm.  Upon halting, the 1st member of the final state
       ` ( R `` N ) ` is equal to the gcd of the values comprising the input
       state ` <. M , N >. ` .  (Contributed by Paul Chapman, 31-Mar-2011.) $)
    eucalg $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                   ( 1st ` ( R ` N ) ) = ( M gcd N ) ) $=
      ( cn0 wcel cfv cgcd c2nd fveq2d wceq cz vz wa co cop fveq2i op2ndg syl5eq
      c1st cxp opelxpi eleq12i cc0 wf cvv nn0ex xpex eqeltri feq23i mpbir algrf
      eucalgf eleq2i xp2nd sylbi ffvelrn syl11anc syl6eleq elxp6 simplbi df-opr
      syl cuz nn0z uzid 3syl eucalgcvga opreq2d syl5eqr xp1st nn0gcdid0 3eqtrrd
      eqid mpd cres wfn wss ax-mp nn0ssz xpss12 mp2an eqsstri fnssres eucalginv
      gcdf ffn cv ffvelrni fvres eqeq12d mpbird alginv mpdan algr0 eqtrd sylbir
      3eqtr3d eqtr3d eqtr4i syl6eq ) GMNHMNUBZHDOZUHOZCPOZGHPUCZXJCQOZDOZUHOZXL
      XMXJXPXKUHXJXOHDXJXOGHUDZQOHCXRQLUEGHMMUFUGRRXJXRMMUIZNZXQXMSZGHMMUJXTCEN
      ZYACXREXSLIUKYBXQXPPOZXMYBYCXQXPQOZUDZPOZXQULPUCZXQYBXPYEPYBXPXSNZXPYESZY
      BXPEXSYBMEDUMXOMNZXPENZCDEFEXSUNIMMUOUOUPUQZEEFUMXSXSFUMABFJVAEEXSXSFIIUR
      USZKUTYBCXSNYJEXSCIVBCMMVCVDZMEXODVEVFZIVGZYHYIXQMNZYDMNUBXPMMVHVIVKRYBYF
      XQYDPUCYGXQYDPVJYBYDULXQPYBXOXOVLONZYDULSYBYJXOTNYRYNXOVMXOVNVOABCDEFXOXO
      IJKXOWBVPWCVQVRYBYHYQYGXQSYPXPMMVSXQVTVOWAYBXPPEWDZOZULDOZYSOZYCXMYBYJYTU
      UBSYNUACDEFYSXOYLYMKPTTUIZWEZEUUCWFYSEWEUUCMPUMUUDWNUUCMPWOWGEXSUUCIMTWFZ
      UUEXSUUCWFWHWHMTMTWIWJWKUUCEPWLWJUAWPZENZUUFFOZYSOZUUFYSOZSUUHPOZUUFPOZSZ
      UUGUUFXSNUUMEXSUUFIVBABFUUFJWMVDUUGUUIUUKUUJUULUUGUUHENUUIUUKSEEUUFFYMWQU
      UHEPWRVKUUFEPWRWSWTXAXBYBYKYTYCSYOXPEPWRVKYBUUBCYSOXMYBUUACYSCDEFYLYMKXCR
      CEPWRXDXFXDXEVKXGXMXRPOXNCXRPLUEGHPVJXHXI $.
      $( [22-Jun-2011] $) $( [31-Mar-2011] $)
  $}

