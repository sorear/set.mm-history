$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                            Euclid's Algorithm
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    eucalgval.1 $e |- E = { <. x , y >. | ( x e. ( NN0 X. NN0 ) /\
      y = if ( ( 2nd ` x ) = 0 , x , <. ( 2nd ` x ) , ( mod ` x ) >. ) ) } $.

    ${
      $d X m n $.  $d m n x y $.
      $( Euclid's Algorithm computes the greatest common divisor of two
         nonnegative integers by repeatedly replacing the larger of them with
         its remainder modulo the smaller until the remainder is 0.

         The value of the step function ` E ` for Euclid's Algorithm.
         (Contributed by Paul Chapman, 31-Mar-2011.) $)
      eucalgval $p |- ( X e. ( NN0 X. NN0 ) -> ( E ` X ) =
          if ( ( 2nd ` X ) = 0 , X , <. ( 2nd ` X ) , ( mod ` X ) >. ) ) $=
        ( vm vn cn0 cxp wcel c2nd cfv cc0 wceq cmo cop cif cvv opex ifexg
        mpan2 cv fveq2 eqeq1d id opeq12d ifbieq12d wa copab wb eleq1 adantr
        simpr eqeq12d anbi12d cbvopabv eqtri fvopab4g mpdan ) DHHIZJZDKLZMNZDVB
        DOLZPZQZRJZDCLVFNVAVERJVGVBVDSVCDVEUTRTUAFGDFUBZKLZMNZVHVIVHOLZPZQZVFUT
        RCVHDNZVJVCVHVLDVEVNVIVBMVHDKUCZUDVNUEVNVIVBVKVDVOVHDOUCUFUGCAUBZUTJZBU
        BZVPKLZMNZVPVSVPOLZPZQZNZUHZABUIVHUTJZGUBZVMNZUHZFGUIEWEWIABFGVPVHNZVRW
        GNZUHZVQWFWDWHWJVQWFUJWKVPVHUTUKULWLVRWGWCVMWJWKUMWJWCVMNWKWJVTVJVPWBVH
        VLWJVSVIMVPVHKUCZUDWJUEWJVSVIWAVKWMVPVHOUCUFUGULUNUOUPUQURUS $.
        $( [22-Jun-2011] $) $( [31-Mar-2011] $)
    $}

    ${
      $d x y $.
      $( The value of the step function ` E ` for Euclid's Algorithm on an
         ordered pair.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
      eucalgval2 $p |- ( ( M e. NN0 /\ N e. NN0 ) -> ( E ` <. M , N >. ) =
                       if ( N = 0 , <. M , N >. , <. N , ( M mod N ) >. ) ) $=
        ( cn0 wcel wa cop cfv c2nd cc0 wceq cmo cif co cxp opelxpi eucalgval
        syl op2ndg eqeq1d eqcomd df-opr a1i opeq12d ifbieq2d eqtrd ) DGHEGHIZDE
        JZCKZUKLKZMNZUKUMUKOKZJZPZEMNZUKEDEOQZJZPUJUKGGRHULUQNDEGGSABCUKFTUAUJU
        NURUPUTUKUJUMEMDEGGUBZUCUJUTUPUJEUMUSUOUJUMEVAUDUSUONUJDEOUEUFUGUDUHUI
        $.
        $( [22-Jun-2011] $) $( [31-Mar-2011] $)
    $}

    ${
      $d E m n $.  $d X m n $.  $d m n x y $.

      $( Domain and codomain of the step function ` E ` for Euclid's
         Algorithm.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
      eucalgf $p |- E : ( NN0 X. NN0 ) --> ( NN0 X. NN0 ) $=
        ( vm vn cn0 cxp wf wfn cv co wcel wral ffnoprv c2nd cfv cc0 wceq cmo
        cop cif cvv visset opex ifex a1i rgen fnopab2g mpbi wa opelxpi adantr
        eucalgval2 iftrue sylan9eq eleq1d mpbird wn iffalse simpr cn cz zmodcl
        nn0z sylan adantrr wo elnn0 anbi1i pm5.61 bitri sylan2b anassrs
        syl11anc eqeltrd pm2.61dan df-opr syl5eqel rgen2 mpbir2an ) GGHZWBCICWB
        JZEKZFKZCLZWBMZFGNEGNEFGGWBCOAKZPQZRSZWHWIWHTQZUAZUBZUCMZAWBNWCWNAWBWNW
        HWBMWJWHWLAUDWIWKUEUFUGUHABWBWMCDUIUJWGEFGGWDGMZWEGMZUKZWDWEUAZCQZWBWFW
        QWERSZWSWBMZWQWTUKZXAWRWBMZWQXCWTWDWEGGULUMXBWSWRWBWQWTWSWTWRWEWDWETLZU
        AZUBZWRABCWDWEDUNZWTWRXEUOUPUQURWQWTUSZUKZWSXEWBWQXHWSXFXEXGWTWRXEUTUPX
        IWPXDGMZXEWBMWQWPXHWOWPVAUMWOWPXHXJWOWEVBMZXHUKZXJWPXHUKZWOXKXJXHWDVCMX
        KXJWOWDWEVDWDVEVFVGXMXKWTVHZXHUKXLWPXNXHWEVIVJXKWTVKVLVMVNWEXDGGULVOVPV
        QWDWECVRVSVTWA $.
        $( [31-Mar-2011] $)

      $( The invariant of the step function ` E ` for Euclid's Algorithm is the
         ` gcd ` operator applied to the state.  (Contributed by Paul Chapman,
         31-Mar-2011.) $)
      eucalginv $p |- ( X e. ( NN0 X. NN0 ) ->
                        ( gcd ` ( E ` X ) ) = ( gcd ` X ) ) $=
        ( vm vn cn0 cxp wcel cv cop wceq wrex cfv cgcd elxp2 wa fveq2 fveq2d
        eqeq12d cc0 cmo co cif eucalgval2 iftrue sylan9eq wn iffalse cz cn
        zmodcl nn0z syl simpr nnz gcdcom syl11anc modgcd eqtr3d adantrr wo
        elnn0 anbi1i pm5.61 bitri sylan2b anassrs sylanl1 df-opr eqeq12i sylib
        eqtrd pm2.61dan syl5cbir r19.23aivv sylbi ) DHHIJDFKZGKZLZMZGHNFHNDCOZP
        OZDPOZMZFGDHHQWBWFFGHHWBWFWACOZPOZWAPOZMZVSHJZVTHJZRZWBWDWHWEWIWBWCWGPD
        WACSTDWAPSUAWMVTUBMZWJWMWNRWGWAPWMWNWGWNWAVTVSVTUCUDZLZUEZWAABCVSVTEUFZ
        WNWAWPUGUHTWMWNUIZRZWHWPPOZWIWTWGWPPWMWSWGWQWPWRWNWAWPUJUHTWTVTWOPUDZVS
        VTPUDZMZXAWIMVSUKJZWLWSXDWKXEWLWSXDXEVTULJZWSRZXDWLWSRZXEXFXDWSXEXFRZWO
        VTPUDZXBXCXIWOUKJZVTUKJZXJXBMXIWOHJXKVSVTUMWOUNUOXIXFXLXEXFUPVTUQUOWOVT
        URUSVSVTUTVAVBXHXFWNVCZWSRXGWLXMWSVTVDVEXFWNVFVGVHVIVSUNVJXBXAXCWIVTWOP
        VKVSVTPVKVLVMVNVOVPVQVR $.
        $( [31-Mar-2011] $)

      $( The second member of the state decreases with each iteration of the
         step function ` E ` for Euclid's Algorithm.  (Contributed by Paul
         Chapman, 31-Mar-2011.) $)
      eucalglt $p |- ( X e. ( NN0 X. NN0 ) ->
        ( ( 2nd ` ( E ` X ) ) =/= 0 -> ( 2nd ` ( E ` X ) ) < ( 2nd ` X ) ) ) $=
        ( vm vn cn0 cxp wcel cv cop wceq wrex cfv c2nd cc0 wne clt wbr wi
        elxp2 wa fveq2 fveq2d neeq1d breq12d imbi12d wn pm2.24 df-ne syl5ib
        adantl cmo co cif eucalgval2 iftrue sylan9eq visset op2nd syl6eq a1i
        mpbird cn cr crp modlt nn0re nnrp syl2an adantrr wo elnn0 anbi1i
        pm5.61 bitri sylan2b anassrs iffalse oprex a1d pm2.61dan syl5cbir
        r19.23aivv sylbi ) DHHIJDFKZGKZLZMZGHNFHNDCOZPOZQRZWLDPOZSTZUAZFGDHHUBW
        JWPFGHHWJWPWICOZPOZQRZWRWIPOZSTZUAZWGHJZWHHJZUCZWJWMWSWOXAWJWLWRQWJWKWQ
        PDWICUDUEZUFWJWLWRWNWTSXFDWIPUDUGUHXEWHQMZXBXEXGUCZXBWHQRZWHWHSTZUAZXGX
        KXEXGXGUIZXJXIXGXJUJWHQUKULUMXHWSXIXAXJXHWRWHQXHWRWTWHXHWQWIPXEXGWQXGWI
        WHWGWHUNUOZLZUPZWIABCWGWHEUQZXGWIXNURUSUEWGWHFUTGUTZVAZVBZUFXHWRWHWTWHS
        XSWTWHMZXHXRVCUGUHVDXEXLUCZXAWSYAXAXMWHSTZXCXDXLYBXCWHVEJZXLUCZYBXDXLUC
        ZXCYCYBXLWGVFJWHVGJYBXCYCWGWHVHWGVIWHVJVKVLYEYCXGVMZXLUCYDXDYFXLWHVNVOY
        CXGVPVQVRVSYAWRXMWTWHSYAWRXNPOXMYAWQXNPXEXLWQXOXNXPXGWIXNVTUSUEWHXMXQWG
        WHUNWAVAVBXTYAXRVCUGVDWBWCWDWEWF $.
        $( [31-Mar-2011] $)
    $}
  $}

  ${
    $d E z $.  $d R z $.  $d x y $.
    eucalg.1 $e |- S = ( NN0 X. NN0 ) $.
    eucalg.2 $e |- E = { <. x , y >. | ( x e. ( NN0 X. NN0 ) /\
       y = if ( ( 2nd ` x ) = 0 , x , <. ( 2nd ` x ) , ( mod ` x ) >. ) ) } $.
    eucalg.3 $e |-
                R = ( ( E o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { A } ) ) $.

    ${
      eucalgcvga.4 $e |- N = ( 2nd ` A ) $.
      $( Once Euclid's Algorithm halts after ` N ` steps, the second element of
         the state remains 0 .  (Contributed by Paul Chapman, 22-Jun-2011.) $)
      eucalgcvga $p |- ( A e. S -> ( K e. ( ZZ>= ` N ) ->
                                     ( 2nd ` ( R ` K ) ) = 0 ) ) $=
        ( vz wcel cuz cfv c2nd cc0 wceq cn0 cxp wa cres simpl eluznn0 xp2nd
        syl5eqel sylan wf ffvelrn nn0ex xpex eucalgf c1st ccom csn cseq0 co
        xpeq12i reseq2i coeq2i opreq1i eqtri algrf fvres syl syl11anc syl6eqr
        fveq2d eleq2d f2ndres cv wne clt wbr wi eucalglt ffvelrni neeq1d
        breq12d imbi12d mpbird eqid algcvga sylbird imp eqtr3d eleq2i sylanb
        ex ) CENZGHOPZNZGDPZQPZRSZCTTUAZNZWMWPWKWRWMUBZWNQWQUCZPZWORWSWRGTNZXAW
        OSZWRWMUDHTNWMXBWRGHUEWRCQPZTHCTTUFLUGUHWRXBUBWNWQNZXCTWQDUIXBXEWRTWQGD
        UJCDWQFTTUKUKULZABFJUMZDFUNEEUAZUCZUOZTCUPUAZUQURFUNWQWQUAZUCZUOZXKUQUR
        KXJXNXKUQXIXMFXHXLUNEWQEWQIIUSUTVAVBVCZVDUHWNWQQVEVFVGWRWMXARSZWRWMGCWT
        PZOPZNXPWRXRWLGWRXQHOWRXQXDHCWQQVELVHVIVJMCWTDWQFGXQXFXGXOTTVKMVLZWQNZX
        SFPZWTPZRVMZYBXSWTPZVNVOZVPYAQPZRVMZYFXSQPZVNVOZVPABFXSJVQXTYCYGYEYIXTY
        BYFRXTYAWQNYBYFSWQWQXSFXGVRYAWQQVEVFZVSXTYBYFYDYHVNYJXSWQQVEVTWAWBXQWCW
        DWEWFWGEWQCIWHWIWJ $.
        $( [22-Jun-2011] $)
    $}

    $d S z $.
    eucalg.4 $e |- A = <. M , N >. $.
    $( Euclid's Algorithm.  Upon halting, the 1st member of the final state
       ` ( R `` N ) ` is equal to the gcd of the values comprising the input
       state ` <. M , N >. ` .  (Contributed by Paul Chapman, 31-Mar-2011.) $)
    eucalg $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                   ( 1st ` ( R ` N ) ) = ( M gcd N ) ) $=
      ( vz cn0 wcel wa cfv c1st cgcd co c2nd cop op2ndg fveq2i syl5eq fveq2d
      cxp wceq opelxpi eleq12i cc0 wf cvv nn0ex xpex eqeltri eucalgf feq23i
      mpbir algrf eleq2i xp2nd sylbi ffvelrn syl11anc syl6eleq elxp6 simplbi
      syl cuz cz nn0z uzid 3syl eqid eucalgcvga mpd opreq2d df-opr syl5eqr
      xp1st nn0gcdid0 3eqtrrd cres wfn wss gcdf ffn ax-mp nn0ssz xpss12 mp2an
      eqsstri fnssres cv eucalginv ffvelrni fvres eqeq12d mpbird alginv mpdan
      algr0 eqtrd 3eqtr3d sylbir eqtr3d eqtr4i syl6eq ) GNOHNOPZHDQZRQZCSQZGHST
      ZXJCUAQZDQZRQZXLXMXJXPXKRXJXOHDXJGHUBZUAQHXOGHNNUCCXRUALUDUEUFUFXJXRNNUGZ
      OZXQXMUHZGHNNUIXTCEOZYACXREXSLIUJYBXQXPSQZXMYBYCXQXPUAQZUBZSQZXQUKSTZXQYB
      XPYESYBXPXSOZXPYEUHZYBXPEXSYBNEDULXONOZXPEOZCDEFEXSUMINNUNUNUOUPZEEFULXSX
      SFULABFJUQEEXSXSFIIURUSZKUTYBCXSOYJEXSCIVACNNVBVCZNEXODVDVEZIVFZYHYIXQNOZ
      YDNOPXPNNVGVHVIUFYBXQYDSTYGYFYBYDUKXQSYBXOXOVJQOZYDUKUHYBYJXOVKOYRYNXOVLX
      OVMVNABCDEFXOXOIJKXOVOVPVQVRXQYDSVSVTYBYHYQYGXQUHYPXPNNWAXQWBVNWCYBXPSEWD
      ZQZUKDQZYSQZYCXMYBYJYTUUBUHYNMCDEFYSXOYLYMKSVKVKUGZWEZEUUCWFYSEWEUUCNSULU
      UDWGUUCNSWHWIEXSUUCINVKWFZUUEXSUUCWFWJWJNVKNVKWKWLWMUUCESWNWLMWOZEOZUUFFQ
      ZYSQZUUFYSQZUHUUHSQZUUFSQZUHZUUGUUFXSOUUMEXSUUFIVAABFUUFJWPVCUUGUUIUUKUUJ
      UULUUGUUHEOUUIUUKUHEEUUFFYMWQUUHESWRVIUUFESWRWSWTXAXBYBYKYTYCUHYOXPESWRVI
      YBUUBCYSQXMYBUUACYSCDEFYLYMKXCUFCESWRXDXEXDXFVIXGXMXRSQXNCXRSLUDGHSVSXHXI
      $.
      $( [22-Jun-2011] $) $( [31-Mar-2011] $)
  $}

