$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                            Euclid's Algorithm
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    eucalgval.1 $e |- E = { <. x , y >. | ( x e. ( NN0 X. NN0 ) /\
      y = if ( ( 2nd ` x ) = 0 , x , <. ( 2nd ` x ) , ( mod ` x ) >. ) ) } $.

    ${
      $d X m n $.  $d m n x y $.
      $( Euclid's Algorithm computes the greatest common divisor of two
         nonnegative integers by repeatedly replacing the larger of them with
         its remainder modulo the smaller until the remainder is 0.

         The value of the step function ` E ` for Euclid's Algorithm.
         (Contributed by Paul Chapman, 31-Mar-2011.) $)
      eucalgval $p |- ( X e. ( NN0 X. NN0 ) -> ( E ` X ) =
          if ( ( 2nd ` X ) = 0 , X , <. ( 2nd ` X ) , ( mod ` X ) >. ) ) $=
        ( vm vn cn0 wcel c2nd cfv cc0 wceq cmo cop cif cvv cv fveq2 wa cxp opex
        ifexg mpan2 eqeq1d id opeq12d ifbieq12d copab wb adantr eqeq12d anbi12d
        eleq1 simpr cbvopabv eqtri fvopab4g mpdan ) DHHUAZIZDJKZLMZDVBDNKZOZPZQ
        IZDCKVFMVAVEQIVGVBVDUBVCDVEUTQUCUDFGDFRZJKZLMZVHVIVHNKZOZPZVFUTQCVHDMZV
        JVCVHVLDVEVNVIVBLVHDJSZUEVNUFVNVIVBVKVDVOVHDNSUGUHCARZUTIZBRZVPJKZLMZVP
        VSVPNKZOZPZMZTZABUIVHUTIZGRZVMMZTZFGUIEWEWIABFGVPVHMZVRWGMZTZVQWFWDWHWJ
        VQWFUJWKVPVHUTUNUKWLVRWGWCVMWJWKUOWJWCVMMWKWJVTVJVPWBVHVLWJVSVILVPVHJSZ
        UEWJUFWJVSVIWAVKWMVPVHNSUGUHUKULUMUPUQURUS $.
        $( [22-Jun-2011] $) $( [31-Mar-2011] $)
    $}

    ${
      $d x y $.
      $( The value of the step function ` E ` for Euclid's Algorithm on an
         ordered pair.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
      eucalgval2 $p |- ( ( M e. NN0 /\ N e. NN0 ) -> ( E ` <. M , N >. ) =
                       if ( N = 0 , <. M , N >. , <. N , ( M mod N ) >. ) ) $=
        ( cn0 wcel wa cop cfv c2nd cc0 wceq cmo cif co cxp opelxpi eqcomd eqtrd
        eucalgval syl op2ndg eqeq1d df-opr a1i opeq12d ifbieq2d ) DGHEGHIZDEJZC
        KZUKLKZMNZUKUMUKOKZJZPZEMNZUKEDEOQZJZPUJUKGGRHULUQNDEGGSABCUKFUBUCUJUNU
        RUPUTUKUJUMEMDEGGUDZUEUJUTUPUJEUMUSUOUJUMEVATUSUONUJDEOUFUGUHTUIUA $.
        $( [22-Jun-2011] $) $( [31-Mar-2011] $)
    $}

    ${
      $d E m n $.  $d X m n $.  $d m n x y $.

      $( Domain and codomain of the step function ` E ` for Euclid's
         Algorithm.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
      eucalgf $p |- E : ( NN0 X. NN0 ) --> ( NN0 X. NN0 ) $=
        ( vm vn cn0 cv co wcel wral cfv cc0 wceq cmo cop cif wa opelxpi adantr
        cxp wf wfn ffnoprv c2nd cvv vex opex ifex a1i rgen fnopab2g mpbi df-opr
        eucalgval2 iftrue sylan9eq eleq1d mpbird wn iffalse simpr anbi1i pm5.61
        cn wo elnn0 bitri cz nn0z sylan adantrr sylan2b anassrs syl2anc eqeltrd
        zmodcl pm2.61dan syl5eqel rgen2 mpbir2an ) GGUAZWBCUBCWBUCZEHZFHZCIZWBJ
        ZFGKEGKEFGGWBCUDAHZUELZMNZWHWIWHOLZPZQZUFJZAWBKWCWNAWBWNWHWBJWJWHWLAUGW
        IWKUHUIUJUKABWBWMCDULUMWGEFGGWDGJZWEGJZRZWFWDWEPZCLZWBWDWECUNWQWEMNZWSW
        BJZWQWTRZXAWRWBJZWQXCWTWDWEGGSTXBWSWRWBWQWTWSWTWRWEWDWEOIZPZQZWRABCWDWE
        DUOZWTWRXEUPUQURUSWQWTUTZRZWSXEWBWQXHWSXFXEXGWTWRXEVAUQXIWPXDGJZXEWBJWQ
        WPXHWOWPVBTWOWPXHXJWPXHRZWOWEVEJZXHRZXJXKXLWTVFZXHRXMWPXNXHWEVGVCXLWTVD
        VHWOXLXJXHWOWDVIJXLXJWDVJWDWEVQVKVLVMVNWEXDGGSVOVPVRVSVTWA $.
        $( [31-Mar-2011] $)

      $( The invariant of the step function ` E ` for Euclid's Algorithm is the
         ` gcd ` operator applied to the state.  (Contributed by Paul Chapman,
         31-Mar-2011.) $)
      eucalginv $p |- ( X e. ( NN0 X. NN0 ) ->
                        ( gcd ` ( E ` X ) ) = ( gcd ` X ) ) $=
        ( vm vn cn0 wcel cv cop wceq wrex cfv cgcd wa co sylan9eq fveq2d cz cxp
        elxp2 cc0 cmo cif eucalgval2 iftrue wn iffalse nn0z cn wo anbi1i pm5.61
        elnn0 bitri zmodcl syl nnz gcdcom syl2anc modgcd eqtr3d adantrr sylan2b
        simpr anassrs sylanl1 df-opr eqeq12i sylib pm2.61dan eqeq12d syl5ibrcom
        eqtrd fveq2 rexlimivv sylbi ) DHHUAIDFJZGJZKZLZGHMFHMDCNZONZDONZLZFGDHH
        UBWBWFFGHHVSHIZVTHIZPZWFWBWACNZONZWAONZLZWIVTUCLZWMWIWNPWJWAOWIWNWJWNWA
        VTVSVTUDQZKZUEZWAABCVSVTEUFZWNWAWPUGRSWIWNUHZPZWKWPONZWLWTWJWPOWIWSWJWQ
        WPWRWNWAWPUIRSWTVTWOOQZVSVTOQZLZXAWLLWGVSTIZWHWSXDVSUJXEWHWSXDWHWSPZXEV
        TUKIZWSPZXDXFXGWNULZWSPXHWHXIWSVTUOUMXGWNUNUPXEXGXDWSXEXGPZWOVTOQZXBXCX
        JWOTIZVTTIZXKXBLXJWOHIXLVSVTUQWOUJURXJXGXMXEXGVFVTUSURWOVTUTVAVSVTVBVCV
        DVEVGVHXBXAXCWLVTWOOVIVSVTOVIVJVKVOVLWBWDWKWEWLWBWCWJODWACVPSDWAOVPVMVN
        VQVR $.
        $( [31-Mar-2011] $)

      $( The second member of the state decreases with each iteration of the
         step function ` E ` for Euclid's Algorithm.  (Contributed by Paul
         Chapman, 31-Mar-2011.) $)
      eucalglt $p |- ( X e. ( NN0 X. NN0 ) ->
        ( ( 2nd ` ( E ` X ) ) =/= 0 -> ( 2nd ` ( E ` X ) ) < ( 2nd ` X ) ) ) $=
        ( vm vn cn0 wcel wceq cfv c2nd cc0 wne clt wbr wi wa fveq2d breq12d cxp
        cv cop elxp2 wn df-ne pm2.24 syl5bi adantl cmo co cif eucalgval2 iftrue
        wrex sylan9eq vex op2nd syl6eq neeq1d imbi12d mpbird cn wo elnn0 anbi1i
        a1i pm5.61 bitri cr crp nn0re nnrp modlt syl2an adantrr sylan2b anassrs
        iffalse oprex a1d pm2.61dan fveq2 syl5ibrcom rexlimivv sylbi ) DHHUAIDF
        UBZGUBZUCZJZGHUOFHUODCKZLKZMNZWLDLKZOPZQZFGDHHUDWJWPFGHHWGHIZWHHIZRZWPW
        JWICKZLKZMNZXAWILKZOPZQZWSWHMJZXEWSXFRZXEWHMNZWHWHOPZQZXFXJWSXHXFUEZXFX
        IWHMUFXFXIUGUHUIXGXBXHXDXIXGXAWHMXGXAXCWHXGWTWILWSXFWTXFWIWHWGWHUJUKZUC
        ZULZWIABCWGWHEUMZXFWIXMUNUPSWGWHFUQGUQZURZUSZUTXGXAWHXCWHOXRXCWHJZXGXQV
        GTVAVBWSXKRZXDXBXTXDXLWHOPZWQWRXKYAWRXKRZWQWHVCIZXKRZYAYBYCXFVDZXKRYDWR
        YEXKWHVEVFYCXFVHVIWQYCYAXKWQWGVJIWHVKIYAYCWGVLWHVMWGWHVNVOVPVQVRXTXAXLX
        CWHOXTXAXMLKXLXTWTXMLWSXKWTXNXMXOXFWIXMVSUPSWHXLXPWGWHUJVTURUSXSXTXQVGT
        VBWAWBWJWMXBWOXDWJWLXAMWJWKWTLDWICWCSZUTWJWLXAWNXCOYFDWILWCTVAWDWEWF $.
        $( [31-Mar-2011] $)
    $}
  $}

  ${
    $d E z $.  $d R z $.  $d x y $.
    eucalg.1 $e |- S = ( NN0 X. NN0 ) $.
    eucalg.2 $e |- E = { <. x , y >. | ( x e. ( NN0 X. NN0 ) /\
       y = if ( ( 2nd ` x ) = 0 , x , <. ( 2nd ` x ) , ( mod ` x ) >. ) ) } $.
    eucalg.3 $e |-
                R = ( ( E o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { A } ) ) $.

    ${
      eucalgcvga.4 $e |- N = ( 2nd ` A ) $.
      $( Once Euclid's Algorithm halts after ` N ` steps, the second element of
         the state remains 0 .  (Contributed by Paul Chapman, 22-Jun-2011.) $)
      eucalgcvga $p |- ( A e. S -> ( K e. ( ZZ>= ` N ) ->
                                     ( 2nd ` ( R ` K ) ) = 0 ) ) $=
        ( wcel cfv c2nd cc0 wceq cn0 cxp fvres vz eleq2i wa cres simpl syl5eqel
        cuz xp2nd eluznn0 sylan wf nn0ex xpex eucalgf c1st csn cseq0 co xpeq12i
        ccom reseq2i coeq2i opreq1i eqtri ffvelrn syl2anc syl6eqr fveq2d eleq2d
        algrf syl f2ndres cv wne clt wi eucalglt ffvelrni neeq1d breq12d mpbird
        wbr imbi12d eqid algcvga sylbird imp eqtr3d sylanb ex ) CEMZGHUGNZMZGDN
        ZONZPQZWKCRRSZMZWMWPEWQCIUBWRWMUCZWNOWQUDZNZWOPWSWRGRMZXAWOQZWRWMUEWRHR
        MWMXBWRHCONZRLCRRUHUFGHUIUJWRXBUCWNWQMZXCWRRWQDUKXBXECDWQFRRULULUMZABFJ
        UNZDFUOEESZUDZUTZRCUPSZUQURFUOWQWQSZUDZUTZXKUQURKXJXNXKUQXIXMFXHXLUOEWQ
        EWQIIUSVAVBVCVDZVJRWQGDVEUJWNWQOTVKVFWRWMXAPQZWRWMGCWTNZUGNZMXPWRXRWLGW
        RXQHUGWRXQXDHCWQOTLVGVHVIUACWTDWQFGXQXFXGXORRVLUAVMZWQMZXSFNZWTNZPVNZYB
        XSWTNZVOWBZVPYAONZPVNZYFXSONZVOWBZVPABFXSJVQXTYCYGYEYIXTYBYFPXTYAWQMYBY
        FQWQWQXSFXGVRYAWQOTVKZVSXTYBYFYDYHVOYJXSWQOTVTWCWAXQWDWEWFWGWHWIWJ $.
        $( [22-Jun-2011] $)
    $}

    $d S z $.
    eucalg.4 $e |- A = <. M , N >. $.
    $( Euclid's Algorithm.  Upon halting, the 1st member of the final state
       ` ( R `` N ) ` is equal to the gcd of the values comprising the input
       state ` <. M , N >. ` .  (Contributed by Paul Chapman, 31-Mar-2011.) $)
    eucalg $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                   ( 1st ` ( R ` N ) ) = ( M gcd N ) ) $=
      ( cn0 wcel cfv cgcd c2nd fveq2d wceq cz vz wa co cop fveq2i op2ndg syl5eq
      c1st cxp opelxpi eleq12i cc0 wf cvv nn0ex xpex eqeltri feq23i mpbir algrf
      eucalgf eleq2i xp2nd sylbi ffvelrn syl2anc syl6eleq elxp6 simplbi syl cuz
      df-opr nn0z uzid 3syl eqid eucalgcvga mpd opreq2d syl5eqr xp1st nn0gcdid0
      3eqtrrd cres wfn wss gcdf ffn ax-mp nn0ssz xpss12 mp2an eqsstri eucalginv
      fnssres cv ffvelrni fvres eqeq12d mpbird alginv mpdan algr0 eqtrd 3eqtr3d
      sylbir eqtr3d eqtr4i syl6eq ) GMNHMNUBZHDOZUHOZCPOZGHPUCZXJCQOZDOZUHOZXLX
      MXJXPXKUHXJXOHDXJXOGHUDZQOHCXRQLUEGHMMUFUGRRXJXRMMUIZNZXQXMSZGHMMUJXTCENZ
      YACXREXSLIUKYBXQXPPOZXMYBYCXQXPQOZUDZPOZXQULPUCZXQYBXPYEPYBXPXSNZXPYESZYB
      XPEXSYBMEDUMXOMNZXPENZCDEFEXSUNIMMUOUOUPUQZEEFUMXSXSFUMABFJVAEEXSXSFIIURU
      SZKUTYBCXSNYJEXSCIVBCMMVCVDZMEXODVEVFZIVGZYHYIXQMNZYDMNUBXPMMVHVIVJRYBYFX
      QYDPUCYGXQYDPVLYBYDULXQPYBXOXOVKONZYDULSYBYJXOTNYRYNXOVMXOVNVOABCDEFXOXOI
      JKXOVPVQVRVSVTYBYHYQYGXQSYPXPMMWAXQWBVOWCYBXPPEWDZOZULDOZYSOZYCXMYBYJYTUU
      BSYNUACDEFYSXOYLYMKPTTUIZWEZEUUCWFYSEWEUUCMPUMUUDWGUUCMPWHWIEXSUUCIMTWFZU
      UEXSUUCWFWJWJMTMTWKWLWMUUCEPWOWLUAWPZENZUUFFOZYSOZUUFYSOZSUUHPOZUUFPOZSZU
      UGUUFXSNUUMEXSUUFIVBABFUUFJWNVDUUGUUIUUKUUJUULUUGUUHENUUIUUKSEEUUFFYMWQUU
      HEPWRVJUUFEPWRWSWTXAXBYBYKYTYCSYOXPEPWRVJYBUUBCYSOXMYBUUACYSCDEFYLYMKXCRC
      EPWRXDXEXDXFVJXGXMXRPOXNCXRPLUEGHPVLXHXI $.
      $( [22-Jun-2011] $) $( [31-Mar-2011] $)
  $}

