$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                                 Algorithms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d F k m $.  $d N k m $.  $d k m ph $.
    nn0seqcvgd.1 $e |- ( ph -> F : NN0 --> NN0 ) $.
    nn0seqcvgd.2 $e |- ( ph -> N = ( F ` 0 ) ) $.
    nn0seqcvgd.3 $e |- ( ( ph /\ k e. NN0 ) ->
      ( ( F ` ( k + 1 ) ) =/= 0 -> ( F ` ( k + 1 ) ) < ( F ` k ) ) ) $.
    $( A strictly-decreasing nonnegative integer sequence with initial term
       ` N ` reaches zero by the ` N ` th term.  Deduction version.
       (Contributed by Paul Chapman, 31-Mar-2011.) $)
    nn0seqcvgd $p |- ( ph -> ( F ` N ) = 0 ) $=
      ( cfv cc0 wceq cle wbr cn0 wcel cr wa cmin co wi syl 0nn0 ffvelrn sylancl
      vm wb wf eqeltrd syl11anc nn0re 0re letri3 mpan2 3syl leid cv caddc fveq2
      c1 opreq2 imbi2d eqbrtrrd cc recnd subid1 breq2d mpbird a1i posdif syl2an
      breq12d ancoms adantr breq1 adantl cz peano2nn0 nn0z zsubcl zltlem1 nn0cn
      clt ax-1cn subsub4 mp3an3 bitrd 3bitr2d biimpa an32s a1d wn wne df-ne zre
      sylan ltletr syl111anc sylibd syland sylan2br pm2.61dan anasss expcom a2d
      expdimp 3adant1 fnn0ind pm2.43i subid breqtrd nn0ge0 mpbir2and ) ADCHZIJZ
      XLIKLZIXLKLZAXLMNZXLONZXMXNXOPUEZAMMCUFZDMNZXPEADICHZMFAXSIMNYAMNEUAMMICU
      BUCUGZMMDCUBUHZXLUIXQIONXRUJXLIUKULUMAXLDDQRZIKAXLYDKLZAXTXTDDKLZAYESZYBY
      BADONZYFAXTYHYBDUITZDUNTZAUDUOZCHZDYKQRZKLZSAYADIQRZKLZSZABUOZCHZDYRQRZKL
      ZSZAYRURUPRZCHZDUUCQRZKLZSZYGUDBDDYKIJZYNYPAUUHYLYAYMYOKYKICUQYKIDQUSVJUT
      YKYRJZYNUUAAUUIYLYSYMYTKYKYRCUQYKYRDQUSVJUTYKUUCJZYNUUFAUUJYLUUDYMUUEKYKU
      UCCUQYKUUCDQUSVJUTYKDJZYNYEAUUKYLXLYMYDKYKDCUQYKDDQUSVJUTYQXTAYPYADKLADYA
      DKFYJVAAYODYAKADVBNZYODJADYIVCZDVDTVEVFVGYRMNZYRDWALZUUBUUGSXTUUNUUOPZAUU
      AUUFAUUPUUAUUFSZAUUNUUOUUQAUUNPZUUOPZUUDIJZUUQUUSUUTPUUFUUAUURUUTUUOUUFUU
      RUUTPZUUOUUFUVAUUOIYTWALZUUDYTWALZUUFUURUUOUVBUEZUUTUUNAUVDUUNYRONYHUVDAY
      RUIYIYRDVHVIVKVLUUTUVCUVBUEUURUUDIYTWAVMVNUURUVCUUFUEUUTUURUVCUUDYTURQRZK
      LZUUFUURUUDVONZYTVONZUVCUVFUEUURUUDMNZUVGAXSUUCMNUVIUUNEYRVPMMUUCCUBVIZUU
      DVQTADVONZYRVONUVHUUNAXTUVKYBDVQTYRVQDYRVRVIZUUDYTVSUHUURUVEUUEUUDKAUULYR
      VBNZUVEUUEJZUUNUUMYRVTUULUVMURVBNUVNWBDYRURWCWDVIVEWEZVLWFWGWHWIUUTWJUUSU
      UDIWKZUUQUUDIWLUUSUVPUUAUUFUURUVPUUAPUUFSUUOUURUVPUUDYSWALZUUAUUFGUURUVQU
      UAPZUVCUUFUURUUDONZYSONZYTONZUVRUVCSUURUVIUVSUVJUUDUITUURYSMNZUVTAXSUUNUW
      BEMMYRCUBWNYSUITUURUVHUWAUVLYTWMTUUDYSYTWOWPUVOWQWRVLXDWSWTXAXBXCXEXFWPXG
      AUULYDIJUUMDXHTXIAXPXOYCXLXJTXK $.
      $( [31-Mar-2011] $)
  $}

  ${
    $d A z $.  $d R z $.  $d S z $.
    algrf.1 $e |- S e. _V $.
    algrf.2 $e |- F : S --> S $.
    algrf.3 $e |-
               R = ( ( F o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { A } ) ) $.
    $( An algorithm is step a function ` F : S --> S ` on a state space
       ` S ` .  An algorithm acts on an initial state ` A e. S ` by iteratively
       applying ` F ` to give ` A ` , ` ( F `` A ) ` , ` ( F `` ( F `` A ) ) `
       and so on.  An algorithm is said to halt if a fixed point of ` F ` is
       reached after a finite number of iterations.

       The algorithm iterator ` R : NN0 --> S ` "runs" the algorithm ` F ` so
       that ` ( R `` k ) ` is the state after ` k ` iterations of ` F ` on the
       initial state ` A ` .

       Domain and codomain of the algorithm iterator ` R ` .  (Contributed by
       Paul Chapman, 31-Mar-2011.) $)
    algrf $p |- ( A e. S -> R : NN0 --> S ) $=
      ( vz wcel cv cfv cn0 wral wf wa cxp mp2an cvv xpex wfn c1st cres ccom csn
      cseq0 co fveq1i wss fconstg snssi fss syl11anc f1stres fco fex nn0ex snex
      seq0cl mp3an3 sylan2 syl5eqel ancoms r19.21aiva ffnfv seq0fn fneq1i mpbir
      mpbiran sylibr ) ACIZHJZBKZCIZHLMZLCBNZVJVMHLVKLIZVJVMVPVJOVLVKDUACCPZUBZ
      UCZLAUDZPZUEUFZKZCVKBWBGUGVJVPLCWANZWCCIZVJLVTWANVTCUHWDLACUIACUJLVTCWAUK
      ULVPWDVQCVSNZWECCDNVQCVRNWFFCCUMVQCCDVRUNQZCVSWAVKWFVQRIVSRIWGCCEESVQCRVS
      UOQZLVTUPAUQSZURUSUTVAVBVCVOBLTZVNHLCBVDWJWBLTVSWAWHWIVELBWBGVFVGVHVI $.
      $( [22-Jun-2011] $) $( [31-Mar-2011] $)

    $( The value of the algorithm iterator ` R ` at ` 0 ` is the initial state
       ` A ` .  (Contributed by Paul Chapman, 31-Mar-2011.) $)
    algr0 $p |- ( A e. S -> ( R ` 0 ) = A ) $=
      ( wcel cc0 cfv cn0 csn cxp c1st cres ccom wf cvv mp2an xpex cseq0 f1stres
      co fveq1i fco fex nn0ex snex seq00 eqtri wceq 0nn0 fvconst2g mpan2 syl5eq
      ) ACHZIBJZIKALZMZJZAUQIDNCCMZOZPZUSUAUCZJUTIBVDGUDVCUSVACVCQZVARHVCRHCCDQ
      VACVBQVEFCCUBVACCDVBUESCCEETVACRVCUFSKURUGAUHTUIUJUPIKHUTAUKULKAICUMUNUO
      $.
      $( [31-Mar-2011] $)
  $}

  ${
    algrp1lem.1 $e |- Fun F $.
    $( Lemma for ~ algrp1 . $)
    algrp1lem $p |- ( ( A e. S /\ B e. S /\ K e. NN0 ) ->
 ( B ( F o. ( 1st |` ( S X. S ) ) ) ( ( NN0 X. { A } ) ` K ) ) = ( F ` B ) ) $=
      ( wcel cn0 w3a csn cxp cfv c1st cres ccom co cop wceq syl eqtrd df-opr wa
      fvconst2g eleq1d biimprd adantrd pm2.43i opelxpi sylan2 3impb 3com12 wfun
      wf f1stres fvco3 mp3an12 fvres op1stg 3ad2ant2 fveq2d syl5eq ) ACGZBCGZEH
      GZIZBEHAJKLZDMCCKZNZOZPBVFQZVILZBDLZBVFVIUAVEVKVJVHLZDLZVLVEVJVGGZVKVNRZV
      CVBVDVOVCVBVDVOVBVDUBZVCVFCGZVOVQVRVQVBVRVDVQVRVBVQVFACHAECUCUDUEUFUGBVFC
      CUHUIUJUKZDULVGCVHUMVOVPFCCUNVGCVJDVHUOUPSVEVMBDVEVMVJMLZBVEVOVMVTRVSVJVG
      MUQSVCVBVTBRVDBVFCURUSTUTTVA $.
      $( [31-Mar-2011] $)
  $}

  ${
    algrp1.1 $e |- S e. _V $.
    algrp1.2 $e |- F : S --> S $.
    algrp1.3 $e |-
                R = ( ( F o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { A } ) ) $.
    $( The value of the algorithm iterator ` R ` at ` ( K + 1 ) ` .
       (Contributed by Paul Chapman, 31-Mar-2011.) $)
    algrp1 $p |- ( ( A e. S /\ K e. NN0 ) ->
                   ( R ` ( K + 1 ) ) = ( F ` ( R ` K ) ) ) $=
      ( wcel cn0 co cfv cxp wceq wf cvv mp2an xpex fveq1i adantl wa c1 csn c1st
      caddc cres ccom cseq0 f1stres fco fex nn0ex seq0p1 opreq1i eqeq12i sylibr
      snex simpl algrf ffvelrn sylan peano2nn0 wfun ffun ax-mp algrp1lem eqtrd
      syl111anc ) ACIZEJIZUAZEUBUEKZBLZEBLZVLJAUCZMZLZDUDCCMZUFZUGZKZVNDLZVJVMW
      ANZVIVJVLVTVPUHKZLZEWDLZVQVTKZNWCVTVPEVRCVTOZVRPIVTPICCDOZVRCVSOWHGCCUIVR
      CCDVSUJQCCFFRVRCPVTUKQJVOULAUQRUMVMWEWAWGVLBWDHSVNWFVQVTEBWDHSUNUOUPTVKVI
      VNCIZVLJIZWAWBNVIVJURVIJCBOVJWJABCDFGHUSJCEBUTVAVJWKVIEVBTAVNCDVLWIDVCGCC
      DVDVEVFVHVG $.
      $( [31-Mar-2011] $)

    $d A k m $.  $d F x $.  $d I k m x $.  $d K m $.  $d R k m x $.
    $d S k m x $.
    alginv.4 $e |- I Fn S $.
    alginv.5 $e |- ( x e. S -> ( I ` ( F ` x ) ) = ( I ` x ) ) $.
    $( If ` I ` is an invariant of ` F ` , its value is unchanged after any
       number of iterations of ` F ` .  (Contributed by Paul Chapman,
       31-Mar-2011.) $)
    alginv $p |- ( ( A e. S /\ K e. NN0 ) ->
                   ( I ` ( R ` K ) ) = ( I ` ( R ` 0 ) ) ) $=
      ( cn0 wcel cfv wceq wi fveq2 fveq2d eqeq1d vm vk cc0 cv c1 caddc co eqidd
      imbi2d wa algrp1 wf algrf ffvelrn sylan eqeq12d vtoclri syl eqtrd biimprd
      rgen expcom a2d nn0ind impcom ) GMNBDNZGCOZFOZUCCOZFOZPZVFUAUDZCOZFOZVJPZ
      QVFVJVJPZQVFUBUDZCOZFOZVJPZQVFVQUEUFUGZCOZFOZVJPZQVFVKQUAUBGVLUCPZVOVPVFW
      EVNVJVJWEVMVIFVLUCCRSTUIVLVQPZVOVTVFWFVNVSVJWFVMVRFVLVQCRSTUIVLWAPZVOWDVF
      WGVNWCVJWGVMWBFVLWACRSTUIVLGPZVOVKVFWHVNVHVJWHVMVGFVLGCRSTUIVFVJUHVQMNZVF
      VTWDVFWIVTWDQVFWIUJZWDVTWJWCVSVJWJWCVREOZFOZVSWJWBWKFBCDEVQHIJUKSWJVRDNZW
      LVSPZVFMDCULWIWMBCDEHIJUMMDVQCUNUOAUDZEOZFOZWOFOZPZWNAVRDWOVRPZWQWLWRVSWT
      WPWKFWOVRERSWOVRFRUPWSADLVAUQURUSTUTVBVCVDVE $.
      $( [22-Jun-2011] $) $( [31-Mar-2011] $)
  $}

  ${
    $d A k $.  $d C k z $.  $d F z $.  $d N k $.  $d R k z $.  $d S k z $.
    algcvg.1 $e |- S e. _V $.
    algcvg.2 $e |- F : S --> S $.
    algcvg.3 $e |-
             R = ( ( F o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { A } ) ) $.
    algcvg.4 $e |- C : S --> NN0 $.
    algcvg.5 $e |- ( z e. S -> ( ( C ` ( F ` z ) ) =/= 0 ->
                                 ( C ` ( F ` z ) ) < ( C ` z ) ) ) $.
    algcvg.6 $e |- N = ( C ` A ) $.
    $( One way to prove that an algorithm halts is to construct a countdown
       function ` C : S --> NN0 ` whose value is guaranteed to decrease for
       each iteration of ` F ` until it reaches ` 0 ` .  That is, if ` X e. S `
       is not a fixed point of ` F ` , then
       ` ( C `` ( F `` X ) ) < ( C `` X ) ` .

       If ` C ` is a countdown function for algorithm ` F ` , the sequence
       ` ( C `` ( R `` k ) ) ` reaches ` 0 ` after at most ` N ` steps, where
       ` N ` is the value of ` C ` for the initial state ` A ` .  (Contributed
       by Paul Chapman, 22-Jun-2011.) $)
    algcvg $p |- ( A e. S -> ( C ` ( R ` N ) ) = 0 ) $=
      ( wcel cfv cc0 cn0 wceq fvco3 clt vk ccom wf algrf ffvelrni syl5eqel wfun
      ffun ax-mp mp3an1 syl11anc fco sylancr 0nn0 mp3an13 algr0 fveq2d syl6reqr
      syl eqtrd cv wa c1 caddc co wne wbr wi ffvelrn sylan fveq2 neeq1d breq12d
      imbi12d rgen vtoclri peano2nn0 syl2an algrp1 mpbird nn0seqcvgd eqtr3d )
      BENZGCDUBZOZGDOCOZPWCQEDUCZGQNZWEWFRZBDEFHIJUDZWCGBCOZQMEQBCKUEUFCUGZWGWH
      WIEQCUCZWLKEQCUHUIZQEGCDSUJUKWCUAWDGWCWMWGQQWDUCKWJQEQCDULUMWCPWDOZWKGWCW
      OPDOZCOZWKWCWGWOWQRZWJWLWGPQNWRWNUNQEPCDSUOUSWCWPBCBDEFHIJUPUQUTMURWCUAVA
      ZQNZVBZWSVCVDVEZWDOZPVFZXCWSWDOZTVGZVHWSDOZFOZCOZPVFZXIXGCOZTVGZVHZXAXGEN
      ZXMWCWGWTXNWJQEWSDVIVJAVAZFOZCOZPVFZXQXOCOZTVGZVHZXMAXGEXOXGRZXRXJXTXLYBX
      QXIPYBXPXHCXOXGFVKUQZVLYBXQXIXSXKTYCXOXGCVKVMVNYAAELVOVPUSXAXDXJXFXLXAXCX
      IPXAXCXBDOZCOZXIWCWGXBQNZXCYERZWTWJWSVQWLWGYFYGWNQEXBCDSUJVRXAYDXHCBDEFWS
      HIJVSUQUTZVLXAXCXIXEXKTYHWCWGWTXEXKRZWJWLWGWTYIWNQEWSCDSUJVJVMVNVTWAWB $.
      $( [22-Jun-2011] $)
  $}

  $( Lemma for ~ algcvgb . $)
  algcvgblem $p |- ( ( M e. NN0 /\ N e. NN0 ) -> ( ( N =/= 0 -> N < M ) <->
                      ( ( M =/= 0 -> N < M ) /\ ( M = 0 -> N = 0 ) ) ) ) $=
    ( cn0 wcel wa cc0 wne clt wbr wi wceq wn cle cr wb nn0re syl 0re df-ne a1i
    wo imor simpl ltnle mpan nn0le0eq0 notbid adantr bitrd syl6bbr anbi2d breq1
    nne sylbi biimpar sylbird exp3a ax-1 jctir jaob sylibr syl5bi nn0ge0 adantl
    simpr lelttr mp3an1 syl11anc sylibd pm3.33 mpan2d jcad ancoms impbid con34b
    mpand imbi12i bitr4i anbi2i ) ACDZBCDZEZBFGZBAHIZJZAFGZWDJZWCWFJZEZWGAFKZBF
    KZJZEWBWEWIWBWEWGWHWEWCLZWDUAZWBWGWCWDUBWBWMWGJZWDWGJZEWNWGJWBWOWPWBWMWFWDW
    BWMWFEWMFAHIZEZWDWBWQWFWMWBWQWJLZWFWBWQAFMIZLZWSWBANDZWQXAOZWBVTXBVTWAUCAPQ
    ZFNDZXBXCRFAUDUEQVTXAWSOWAVTWTWJAUFUGUHUIAFSZUJZUKWRWDJWBWMWDWQWMWKWDWQOBFU
    MBFAHULUNUOTUPUQWDWFURUSWMWGWDUTVAVBWBWEWDWFJZWHWBWDWQWFWBFBMIZWDWQWAXIVTBV
    CVDWBBNDZXBXIWDEWQJZWBWAXJVTWAVEBPQXDXEXJXBXKRFBAVFVGVHVPXGVIWEXHEWHJWBWCWD
    WFVJTVKVLWIWEJWBWHWGWEWCWFWDVJVMTVNWLWHWGWLWKLZWSJWHWJWKVOWCXLWFWSBFSXFVQVR
    VSUJ $.
    $( [31-Mar-2011] $)

  ${
    algcvgb.1 $e |- F : S --> S $.
    algcvgb.2 $e |- C : S --> NN0 $.
    $( Two ways of expressing that ` C ` is a countdown function for algorithm
       ` F ` .  The first is used in these theorems.  The second states the
       condition more intuitively as a conjunction: if the countdown function's
       value is currently non-zero, it must decrease at the next step; if it
       has reached zero, it must remain zero at the next step.  (Contributed by
       Paul Chapman, 31-Mar-2011.) $)
    algcvgb $p |- ( X e. S ->
        ( ( ( C ` ( F ` X ) ) =/= 0 -> ( C ` ( F ` X ) ) < ( C ` X ) ) <->
          ( ( ( C ` X ) =/= 0 -> ( C ` ( F ` X ) ) < ( C ` X ) ) /\
            ( ( C ` X ) = 0 -> ( C ` ( F ` X ) ) = 0 ) ) ) ) $=
      ( wcel cfv cn0 cc0 wne clt wbr wi wceq wa wb ffvelrni syl algcvgblem
      syl11anc ) DBGZDAHZIGDCHZAHZIGZUEJKUEUCLMZNUCJKUGNUCJOUEJONPQBIDAFRUBUDBG
      UFBBDCERBIUDAFRSUCUETUA $.
      $( [31-Mar-2011] $)
  $}

  ${
    $d A k m $.  $d C k m z $.  $d F z $.  $d K m $.  $d N k m $.
    $d R k m z $.  $d S k m z $.
    algcvga.1 $e |- S e. _V $.
    algcvga.2 $e |- F : S --> S $.
    algcvga.3 $e |-
              R = ( ( F o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { A } ) ) $.
    algcvga.4 $e |- C : S --> NN0 $.
    algcvga.5 $e |- ( z e. S -> ( ( C ` ( F ` z ) ) =/= 0 ->
                                  ( C ` ( F ` z ) ) < ( C ` z ) ) ) $.
    algcvga.6 $e |- N = ( C ` A ) $.
    $( The countdown function ` C ` remains ` 0 ` after ` N ` steps.
       (Contributed by Paul Chapman, 22-Jun-2011.) $)
    algcvga $p |- ( A e. S -> ( K e. ( ZZ>= ` N ) ->
                                ( C ` ( R ` K ) ) = 0 ) ) $=
      ( wcel cn0 cfv cc0 wceq wi vm vk cuz ffvelrni syl5eqel cz nn0z cle wbr wa
      eluz1 cv c1 caddc fveq2 fveq2d eqeq1d imbi2d algcvg a1i w3a nn0ge0 adantr
      co cr nn0re zre 0re letr mp3an1 syl2an mpand elnn0z simplbi2 adantl sylan
      3impia 3expb expcom 3adant1 ancld wf algrf ffvelrn wne clt neeq1d breq12d
      syld imbi12d rgen vtoclri algcvgb simpr syl6bi mpd syl algrp1 sylibrd a2d
      syl6 uzind 3expib sylbid com3r ) BEOZHPOZGHUCQOZGDQZCQZRSZTXFHBCQPNEPBCLU
      DUEZXGXHXFXKXGHUFOZXHXFXKTZTHUGXMXHGUFOZHGUHUIZUJXNHGUKXMXOXPXNXFUAULZDQZ
      CQZRSZTXFHDQZCQZRSZTZXFUBULZDQZCQZRSZTXFYEUMUNVDZDQZCQZRSZTXNUAUBHGXQHSZX
      TYCXFYMXSYBRYMXRYACXQHDUOUPUQURXQYESZXTYHXFYNXSYGRYNXRYFCXQYEDUOUPUQURXQY
      ISZXTYLXFYOXSYKRYOXRYJCXQYIDUOUPUQURXQGSZXTXKXFYPXSXJRYPXRXICXQGDUOUPUQUR
      YDXMABCDEFHIJKLMNUSUTXMYEUFOZHYEUHUIZVAZXFYHYLYSXFXFYEPOZUJZYHYLTYSXFYTYQ
      YRXFYTTXMXFYQYRUJYTXFYQYRYTXFYQYRYTXFXGYQYRYTTXLXGYQUJZYRRYEUHUIZYTUUBRHU
      HUIZYRUUCXGUUDYQHVBVCXGHVEOZYEVEOZUUDYRUJUUCTZYQHVFYEVGRVEOUUEUUFUUGVHRHY
      EVIVJVKVLYQUUCYTTXGYTYQUUCYEVMVNVOWIVPVQVRVSVTWAUUAYHYFFQZCQZRSZYLUUAYFEO
      ZYHUUJTZXFPEDWBYTUUKBDEFIJKWCPEYEDWDVPUUKUUIRWEZUUIYGWFUIZTZUULAULZFQZCQZ
      RWEZUURUUPCQZWFUIZTZUUOAYFEUUPYFSZUUSUUMUVAUUNUVCUURUUIRUVCUUQUUHCUUPYFFU
      OUPZWGUVCUURUUIUUTYGWFUVDUUPYFCUOWHWJUVBAEMWKWLUUKUUOYGRWEUUNTZUULUJUULCE
      FYFJLWMUVEUULWNWOWPWQUUAYKUUIRUUAYJUUHCBDEFYEIJKWRUPUQWSXAWTXBXCXDWQXEWP
      $.
      $( [22-Jun-2011] $)

    $d K z $.  $d N z $.
    algfx.7 $e |- ( z e. S -> ( ( C ` z ) = 0 -> ( F ` z ) = z ) ) $.
    $( If ` F ` reaches a fixed point when the countdown function ` C ` reaches
       ` 0 ` , ` F ` remains fixed after ` N ` steps.  (Contributed by Paul
       Chapman, 22-Jun-2011.) $)
    algfx $p |- ( A e. S -> ( K e. ( ZZ>= ` N ) -> ( R ` K ) = ( R ` N ) ) ) $=
      ( wcel cfv wceq wi cn0 vm vk cz cuz ffvelrni syl5eqel nn0z syl wa cle wbr
      cv crab uzval eleq2d pm5.32i c1 caddc fveq2 eqeq1d imbi2d eqidd a1i simpl
      co eluznn0 sylan algrp1 syl11anc cc0 algcvga imp wf algrf ffvelrn eqeq12d
      jca id imbi12d rgen vtoclri mpd eqtrd biimprd expcom adantl sylbir uzind3
      3syl a2d sylbi ex com3r ) BEPZHUCPZGHUDQZPZGDQZHDQZRZSWNHTPZWOWNHBCQTNETB
      CLUEUFZHUGUHWOWQWNWTWOWQWNWTSZWOWQUIWOGHAULZUJUKAUCUMZPZUIXCWOWQXFWOWPXEG
      AHUNZUOUPWNUAULZDQZWSRZSWNWSWSRZSZWNUBULZDQZWSRZSWNXMUQURVEZDQZWSRZSXCUAA
      UBHGXHHRZXJXKWNXSXIWSWSXHHDUSUTVAXHXMRZXJXOWNXTXIXNWSXHXMDUSUTVAXHXPRZXJX
      RWNYAXIXQWSXHXPDUSUTVAXHGRZXJWTWNYBXIWRWSXHGDUSUTVAXLWOWNWSVBVCWOXMXEPZUI
      ZWNXOXRYDWOXMWPPZUIWNXOXRSZSZWOYEYCWOWPXEXMXGUOUPYEYGWOWNYEYFWNYEUIZXRXOY
      HXQXNWSYHXQXNFQZXNYHWNXMTPZXQYIRWNYEVDZWNXAYEYJXBXMHVFVGZBDEFXMIJKVHVIYHX
      NCQZVJRZYIXNRZWNYEYNABCDEFXMHIJKLMNVKVLYHWNYJUIXNEPZYNYOSZYHWNYJYKYLVQWNT
      EDVMYJYPBDEFIJKVNTEXMDVOVGXDCQZVJRZXDFQZXDRZSZYQAXNEXDXNRZYSYNUUAYOUUCYRY
      MVJXDXNCUSUTUUCYTYIXDXNXDXNFUSUUCVRVPVSUUBAEOVTWAWIWBWCUTWDWEWFWGWJWHWKWL
      WMWB $.
      $( [22-Jun-2011] $)
  $}

