$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      The greatest common divisor operator
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c gcd $.

  $( Extend the definition of a class to include the greatest common divisor
     operator. $)
  cgcd $a class gcd $.

  ${
    $d n x y z $.
    $( Define the ` gcd ` operator. $)
    df-gcd $a |- gcd = ( x e. ZZ , y e. ZZ |-> if ( ( x = 0 /\ y = 0 ) , 0 ,
             sup ( { n e. ZZ | ( n || x /\ n || y ) } , RR , < ) ) ) $.
  $}

  ${
    $d M n x y $.  $d N n y $.
    $( The value of the ` gcd ` operator. ` ( M gcd N ) ` is the greatest
       common divisor of ` M ` and ` N ` .  If ` M ` and ` N ` are both ` 0 ` ,
       the result is defined conventionally as ` 0 ` .  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    gcdval $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M gcd N ) =
              if ( ( M = 0 /\ N = 0 ) , 0 ,
                   sup ( { n e. ZZ | ( n || M /\ n || N ) } , RR , < ) ) ) $=
      ( vx vy cz cv cc0 wceq wa cdivides wbr crab cr clt cif eqeq1 anbi1d eqidd
      csup cgcd breq2 rabbidv supeq1d ifbieq12d anbi2d df-gcd 0cn elisseti ltso
      cc supex ifex ovmpt2 ) DEBCFFDGZHIZEGZHIZJZHAGZUOKLZUTUQKLZJZAFMZNOTZPBHI
      ZCHIZJZHUTBKLZUTCKLZJZAFMZNOTZPUAVFURJZHVIVBJZAFMZNOTZPUOBIZUSVNHVEHVQVRU
      PVFURUOBHQRVRHSVRNVDVPOVRVCVOAFVRVAVIVBUOBUTKUBRUCUDUEUQCIZVNVHHVQHVMVSUR
      VGVFUQCHQUFVSHSVSNVPVLOVSVOVKAFVSVBVJVIUQCUTKUBUFUCUDUEDEAUGVHHVMHUKUHUIN
      VLOUJULUMUN $.
      $( [10-Nov-2013] $) $( [21-Mar-2011] $)
  $}

  $( The value, by convention, of the ` gcd ` operator when both operands are
     0.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
  gcd0val $p |- ( 0 gcd 0 ) = 0 $=
    ( vn cc0 cgcd co wceq wa cv cdivides wbr cz crab cr clt csup wcel 0z gcdval
    cif mp2an eqid iftrue eqtri ) BBCDZBBEZUDFZBAGBHIZUFFAJKLMNZRZBBJOZUIUCUHEP
    PABBQSUDUDUHBEBTZUJUEBUGUASUB $.
    $( [21-Mar-2011] $)

  ${
    $d M n $.  $d N n $.
    $( The value of the ` gcd ` operator when at least one operand is nonzero.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    gcdn0val $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ -. ( M = 0 /\ N = 0 ) ) ->
        ( M gcd N ) = sup ( { n e. ZZ | ( n || M /\ n || N ) } , RR , < ) ) $=
      ( cz wcel wa cc0 wceq wn cgcd co cv cdivides wbr crab clt csup cif gcdval
      cr iffalse sylan9eq ) BDECDEFBGHCGHFZIBCJKUCGALZBMNUDCMNFADOTPQZRUEABCSUC
      GUEUAUB $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d A n w x y z $.  $d S w x $.
    gcdcllem1.1 $e |- S = { z e. ZZ | A. n e. A z || n } $.
    $( Lemma for ~ gcdn0cl , ~ gcddvds and ~ dvdslegcd . $)
    gcdcllem1 $p |- ( ( A C_ ZZ /\ E. n e. A n =/= 0 ) ->
                      ( S =/= (/) /\ E. x e. RR A. y e. S y <_ x ) ) $=
      ( vw cz cv wa cle wbr wral cr c1 wcel cdivides wceq wi wss cc0 wrex c0 1z
      wne ssel 1dvds syl6 r19.21aiv breq1 ralbidv elrab2 biimpri sylancr adantr
      ne0i syl neeq1 cbvrexv cfv simprbi adantl simplbi dvdsleabs 3expia sylan2
      cabs imp anassrs com23 r19.21aiva ancoms r19.26 pm3.35 ralim ax-mp sylbir
      a1i rgen syl11anc fveq2 breq2d imbi12d cbvralv ralbii ralcom 3bitri sylib
      r19.21v cc zssre sstr mpan2 ssel2 sylan recn abscl 3syl wb breq2 rcla4edv
      syldan imim2d mpd r19.23v sylan2b jca ) DIUAZFJZUBUFZFDUCZKEUDUFZBJZAJZLM
      ZBENZAOUCZXIXMXLXIPEQZXMXIPIQZPXJRMZFDNZXSUEXIYAFDXIXJDQZXJIQZYADIXJUGZXJ
      UHUIUJXSXTYBKCJZXJRMZFDNZYBCPIEYFPSYGYAFDYFPXJRUKULGUMUNUOEPUQURUPXLXIHJZ
      UBUFZHDUCZXRXKYJFHDXJYIUBUSZUTXIYKXRXIYJXRTZHDNZYKXRTXIYJXNYIVHVAZLMZBENZ
      TZHDNZYNXIXKXNXJVHVAZLMZTZFDNZBENZYSXIUUCBEXIXNEQZKXNXJRMZFDNZUUFUUBTZFDN
      ZUUCUUEUUGXIUUEXNIQZUUGYHUUGCXNIEYFXNSYGUUFFDYFXNXJRUKULGUMZVBVCUUEXIUUJU
      UIUUEUUJUUGUUKVDUUJXIUUIUUJXIKZUUHFDUULYCKXKUUFUUAUUJXIYCXKUUFUUATZTZXIYC
      KUUJYDUUNXIYCYDYEVIUUJYDXKUUMXNXJVEVFVGVJVKVLVMVGUUGUUIKUUFUUHKZFDNZUUCUU
      FUUHFDVNUUOUUBTZFDNUUPUUCTUUQFDUUQYCUUFUUBVOVSVTUUOUUBFDVPVQVRWAVLUUDYJYP
      TZHDNZBENUURBENZHDNYSUUCUUSBEUUBUURFHDXJYISZXKYJUUAYPYLUVAYTYOXNLXJYIVHWB
      WCWDWEWFUURBHEDWGUUTYRHDYJYPBEWJWFWHWIXIYRYMTZHDNYSYNTXIUVBHDXIYIDQZKZYQX
      RYJXIUVCYOOQZYQXRTUVDYIOQZYIWKQUVEXIDOUAZUVCUVFXIIOUAUVGWLDIOWMWNDOYIWOWP
      YIWQYIWRWSXIXQYQAYOOXOYOSZXQYQWTXIUVHXPYPBEXOYOXNLXAULVCXBXCXDVLYRYMHDVPU
      RXEYJXRHDXFWIVIXGXH $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K z $.  $d M n x y z $.  $d N n x y z $.  $d R x y $.  $d S x y $.
    gcdcllem2.1 $e |- S = { z e. ZZ | A. n e. { M , N } z || n } $.
    gcdcllem2.2 $e |- R = { z e. ZZ | ( z || M /\ z || N ) } $.
    $( Lemma for ~ gcdn0cl , ~ gcddvds and ~ dvdslegcd . $)
    gcdcllem2 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> R = S ) $=
      ( vx cz wcel wa cv cdivides wbr wral wceq breq1 elrab2 wi prid1g anim12ii
      ralbidv prid2g breq2 rcla4v syl2an wo visset elpr biimprcd jaao r19.21aiv
      cpr syl5bi a1i impbid anbi2d syl5bb anbi12d syl6rbbr eqrdv ) EJKZFJKZLZIB
      CVEIMZCKZVFJKZVFENOZVFFNOZLZLZVFBKVGVHVFDMZNOZDEFUNZPZLVEVLAMZVMNOZDVOPVP
      AVFJCVQVFQZVRVNDVOVQVFVMNRUCGSVEVPVKVHVEVPVKVCEVOKZFVOKZVPVKTVDEFJUAEFJUD
      VTVPVIWAVJVNVIDEVOVMEVFNUEZUFVNVJDFVOVMFVFNUEZUFUBUGVKVPTVEVKVNDVOVMVOKVM
      EQZVMFQZUHVKVNVMEFDUIUJVIWDVNVJWEWDVNVIWBUKWEVNVJWCUKULUOUMUPUQURUSVQENOZ
      VQFNOZLVKAVFJBVSWFVIWGVJVQVFENRVQVFFNRUTHSVAVB $.
      $( [21-Mar-2011] $)

    $( Lemma for ~ gcdn0cl , ~ gcddvds and ~ dvdslegcd . $)
    gcdcllem3 $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ -. ( M = 0 /\ N = 0 ) ) ->
        ( sup ( R , RR , < ) e. NN /\
          ( sup ( R , RR , < ) || M /\ sup ( R , RR , < ) || N ) /\
          ( ( K e. ZZ /\ K || M /\ K || N ) -> K <_ sup ( R , RR , < ) ) ) ) $=
      ( vy vx cz wcel wa cc0 wceq cdivides wbr c1 breq1 wn cr clt cn w3a cle wi
      csup c0 wne cv wral wrex cpr wss wal wo visset elpr eleq1 biimprcd syl5bi
      19.21aiv dfss2 sylibr adantr neorian prid1g neeq1 rcla4ev adantlr adantll
      jaao sylan prid2g jaodan sylan2br gcdcllem1 syl11anc wb gcdcllem2 rexbidv
      raleq anbi12d syl mpbird crab ssrab2 eqsstri suprzcl mp3an1 cbvrabv eqtri
      elrab2 sylib simpld 1dvds anim12i 1z mpbiran zssre sstri suprub ex elnnz1
      mpd sylanbrc simprd biimpri 3impb syl2im 3jca ) FLMZGLMZNZFOPGOPNUAZNZBUB
      UCUHZUDMZXRFQRZXRGQRZNZELMZEFQRZEGQRZUEZEXRUFRZUGXQXRLMZSXRUFRZXSXQYHYBXQ
      XRBMZYHYBNXQBUIUJZJUKKUKZUFRZJBULZKUBUMZNZYJXQYPCUIUJZYMJCULZKUBUMZNZXQFG
      UNZLUOZDUKZOUJZDUUAUMZYTXOUUBXPXOYLUUAMZYLLMZUGZKUPUUBXOUUHKUUFYLFPZYLGPZ
      UQXOUUGYLFGKURUSXMUUIUUGXNUUJUUIUUGXMYLFLUTVAUUJUUGXNYLGLUTVAVMVBVCKUUALV
      DVEVFXPXOFOUJZGOUJZUQUUEFOGOVGXOUUKUUEUULXMUUKUUEXNXMFUUAMUUKUUEFGLVHUUDU
      UKDFUUAUUCFOVIVJVNVKXNUULUUEXMXNGUUAMUULUUEFGLVOUUDUULDGUUAUUCGOVIVJVNVLV
      PVQKJAUUACDHVRVSXOYPYTVTZXPXOBCPZUUMABCDFGHIWAUUNYKYQYOYSBCUIVIUUNYNYRKUB
      YMJBCWCWBWDWEVFWFZBLUOYKYOYJBAUKZFQRZUUPGQRZNZALWGZLIUUSALWHWIZKJBWJWKWEY
      LFQRZYLGQRZNZYBKXRLBYLXRPUVBXTUVCYAYLXRFQTYLXRGQTWDBUUTUVDKLWGIUUSUVDAKLU
      UPYLPUUQUVBUURUVCUUPYLFQTUUPYLGQTWDWLWMWNWOZWPXQSBMZYIXOUVFXPXOSFQRZSGQRZ
      NZUVFXMUVGXNUVHFWQGWQWRUVFSLMUVIUUSUVIASLBUUPSPUUQUVGUURUVHUUPSFQTUUPSGQT
      WDIWNWSWTVEVFXQYPUVFYIUGZUUOBUBUOZYKYOUVJBLUBUVAXAXBZUVKYKYOUEZUVFYIKJBSX
      CXDWKWEXFXRXEXGXQYHYBUVEXHXQYPYFEBMZYGUUOYCYDYEUVNUVNYCYDYENZNUUSUVOAELBU
      UPEPUUQYDUURYEUUPEFQTUUPEGQTWDIWNXIXJUVKYKYOUVNYGUGUVLUVMUVNYGKJBEXCXDWKX
      KXL $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K n $.  $d M n z $.  $d N n z $.
    $( Closure of the ` gcd ` operator.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    gcdn0cl $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ -. ( M = 0 /\ N = 0 ) ) ->
                    ( M gcd N ) e. NN ) $=
      ( vn cK vz cz wcel wa cc0 wceq wn cgcd co cv cdivides wbr crab cr cn eqid
      clt csup gcdn0val w3a cle wi cpr wral gcdcllem3 simp1d eqeltrd ) AFGBFGHA
      IJBIJHKHZABLMCNZAOPUMBOPHCFQZRUAUBZSCABUCULUOSGUOAOPUOBOPHDFGDAOPDBOPUDDU
      OUEPUFCUNUMENOPEABUGUHCFQZEDABUPTUNTUIUJUK $.
      $( [21-Mar-2011] $)

    $( The gcd of two integers divides each of them.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    gcddvds $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                    ( ( M gcd N ) || M /\ ( M gcd N ) || N ) ) $=
      ( vn cK vz cz wcel wa cc0 wceq cgcd co cdivides wbr breq1d anbi12d mpbird
      breq2 cv crab 0z dvds0 ax-mp bi2anan9 syl6bb mpbiri opreq2 opreq1 gcd0val
      anidm sylan9eqr syl6eq adantl wn cr clt csup cn w3a cle wi wral gcdcllem3
      cpr eqid simp2d gcdn0val pm2.61dan ) AFGBFGHZAIJZBIJZHZABKLZAMNZVMBMNZHZV
      LVPVIVLVPIAMNZIBMNZHZVLVSIIMNZIFGVTUAIUBUCVLVSVTVTHVTVJVQVTVKVRVTAIIMRBII
      MRUDVTUJUEUFVLVNVQVOVRVLVMIAMVLVMIIKLZIVKVJVMAIKLWABIAKUGAIIKUHUKUIULZOVL
      VMIBMWBOPQUMVIVLUNHZVPCSZAMNWDBMNHCFTZUOUPUQZAMNZWFBMNZHZWCWFURGWIDFGDAMN
      DBMNUSDWFUTNVACWEWDESMNEABVDVBCFTZEDABWJVEWEVEVCVFWCVNWGVOWHWCVMWFAMCABVG
      ZOWCVMWFBMWKOPQVH $.
      $( [21-Mar-2011] $)

    $( An integer which divides both operands of the ` gcd ` operator is
       bounded by it.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvdslegcd $p |- ( ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) /\
                        -. ( M = 0 /\ N = 0 ) ) ->
                      ( ( K || M /\ K || N ) -> K <_ ( M gcd N ) ) ) $=
      ( vn vz cz wcel w3a cc0 wceq wa wn cdivides wbr cle wi cv crab eqid com12
      cgcd co cr clt csup cn cpr gcdcllem3 simp3d gcdn0val breq2d sylibrd 3expb
      wral exp3a ex com23 impcom 3impb imp ) AFGZBFGZCFGZHBIJCIJKLZABMNZACMNZKZ
      ABCUAUBZONZPZVAVBVCVDVJPZVBVCKZVAVKVLVDVAVJVLVDVAVJPVLVDKZVAVGVIVAVGKVMVI
      VAVEVFVMVIPVMVAVEVFHZVIVMVNADQZBMNVOCMNKDFRZUCUDUEZONZVIVMVQUFGVQBMNVQCMN
      KVNVRPDVPVOEQMNEBCUGUNDFRZEABCVSSVPSUHUIVMVHVQAODBCUJUKULTUMTUOUPUQURUSUT
      $.
      $( [21-Mar-2011] $)
  $}

  $( Closure of the ` gcd ` operator.  (Contributed by Paul Chapman,
     21-Mar-2011.) $)
  gcdcl $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M gcd N ) e. NN0 ) $=
    ( cz wcel wa cc0 wceq cgcd co opreq2 opreq1 sylan9eqr gcd0val syl6eq eleq1d
    cn0 0nn0 mpbiri adantl wn cn gcdn0cl nnnn0 syl pm2.61dan ) ACDBCDEZAFGZBFGZ
    EZABHIZPDZUIUKUFUIUKFPDQUIUJFPUIUJFFHIZFUHUGUJAFHIULBFAHJAFFHKLMNORSUFUITEU
    JUADUKABUBUJUCUDUE $.
    $( [21-Mar-2011] $)

  ${
    $d n x y $.
    $( Domain and codomain of the ` gcd ` operator.  (Contributed by Paul
       Chapman, 31-Mar-2011.) $)
    gcdf $p |- gcd : ( ZZ X. ZZ ) --> NN0 $=
      ( vx vy vn cv cc0 wceq wa cdivides wbr cz crab clt csup cif cn0 wcel wral
      cr cxp cgcd wf co gcdval gcdcl eqeltrrd rgen2a df-gcd foprab2a mpbi ) ADZ
      EFBDZEFGECDZUJHIULUKHIGCJKRLMNZOPZBJQAJQJJSOTUAUNABJUJJPUKJPGUJUKTUBUMOCU
      JUKUCUJUKUDUEUFABJJUMOTABCUGUHUI $.
      $( [16-Nov-2013] $) $( [31-Mar-2011] $)
  $}

  ${
    $d M n $.  $d N n $.
    $( The ` gcd ` operator is commutative.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    gcdcom $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M gcd N ) = ( N gcd M ) ) $=
      ( vn cz wcel wa cc0 wceq cdivides wbr crab cr clt csup cif cgcd ancom a1i
      co gcdval cv wb rabbiia supeq1i ifbieq2i ancoms 3eqtr4d ) ADEZBDEZFZAGHZB
      GHZFZGCUAZAIJZUNBIJZFZCDKZLMNZOZULUKFZGUPUOFZCDKZLMNZOZABPSBAPSZUTVEHUJUM
      VAUSVDGUKULQLURVCMUQVBCDUQVBUBUNDEUOUPQRUCUDUERCABTUIUHVFVEHCBATUFUG $.
      $( [22-Jun-2011] $) $( [21-Mar-2011] $)
  $}

  $( The gcd of two integers is zero iff they are both zero.  (Contributed by
     Paul Chapman, 21-Mar-2011.) $)
  gcdeq0 $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                 ( ( M gcd N ) = 0 <-> ( M = 0 /\ N = 0 ) ) ) $=
    ( cz wcel wa cgcd co cc0 wceq wne wn nne cn gcdn0cl nnne0 syl con1d syl5bir
    ex opreq12 gcd0val syl6eq impbid1 ) ACDBCDEZABFGZHIZAHIBHIEZUFUEHJZKUDUGUEH
    LUDUGUHUDUGKZUHUDUIEUEMDUHABNUEOPSQRUGUEHHFGHAHBHFTUAUBUC $.
    $( [21-Mar-2011] $)

  $( The gcd of two integers is positive (nonzero) iff they are not both zero.
     (Contributed by Paul Chapman, 22-Jun-2011.) $)
  gcdn0gt0 $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                   ( -. ( M = 0 /\ N = 0 ) <-> 0 < ( M gcd N ) ) ) $=
    ( cz wcel wa cc0 cgcd co clt wbr wne wceq wn cn0 wb gcdcl cle nn0re nn0ge0
    cr 0re leltne mp3an1 syl11anc syl df-ne gcdeq0 notbid syl5bb bitr2d ) ACDBC
    DEZFABGHZIJZULFKZAFLBFLEZMZUKULNDZUMUNOZABPUQULTDZFULQJZURULRULSFTDUSUTURUA
    FULUBUCUDUEUNULFLZMUKUPULFUFUKVAUOABUGUHUIUJ $.
    $( [22-Jun-2011] $)

  $( The gcd of 0 and an integer is the integer's absolute value.  (Contributed
     by Paul Chapman, 21-Mar-2011.) $)
  gcd0id $p |- ( N e. ZZ -> ( 0 gcd N ) = ( abs ` N ) ) $=
    ( cz wcel cc0 wceq cgcd co cabs cfv syl6eq wn wa cle wbr wb 0z syl cdivides
    adantr wi opreq2 gcd0val fveq2 abs0 eqtr4d adantl wne df-ne gcdcl mpan nn0z
    cr cn0 zre nn0abscl letri3 syl11anc gcddvds simprd dvdsleabs 3anidm12 dvds0
    syl3an1 mpd iddvds absdvdsb anidms mpbid jca eqid biantrur notbii bitri w3a
    dvdslegcd ex mp3an2 mpancom syl5bi imp mpbir2and sylan2br pm2.61dan ) ABCZA
    DEZDAFGZAHIZEZWEWHWDWEWFDWGWEWFDDFGDADDFUAUBJWEWGDHIDADHUCUDJUEUFWEKZWDADUG
    ZWHADUHZWDWJLZWHWFWGMNZWGWFMNZWDWHWMWNLOZWJWDWFULCZWGULCZWOWDWFBCZWPWDWFUMC
    ZWRDBCZWDWSPDAUIUJWFUKQZWFUNQWDWGBCZWQWDWGUMCXBAUOWGUKQZWGUNQWFWGUPUQSWLWFA
    RNZWMWDXDWJWDWFDRNZXDWTWDXEXDLPDAURUJUSSWDWJXDWMTZWDWRWDWJXFXAWFAUTVCVAVDWL
    WGDRNZWGARNZLZWNWDXIWJWDXGXHWDXBXGXCWGVBQWDAARNZXHAVEWDXJXHOAAVFVGVHVISWDWJ
    XIWNTZWJDDEZWELZKZWDXKWJWIXNWKWEXMXLWEDVJVKVLVMXBWDXNXKTZXCXBWTWDXOPXBWTWDV
    NXNXKWGDAVOVPVQVRVSVTVDWAWBWC $.
    $( [21-Mar-2011] $)

  $( The gcd of an integer and 0 is the integer's absolute value.  (Contributed
     by Paul Chapman, 31-Mar-2011.) $)
  gcdid0 $p |- ( N e. ZZ -> ( N gcd 0 ) = ( abs ` N ) ) $=
    ( cz wcel cc0 cgcd co cabs cfv wceq 0z gcdcom mpan gcd0id eqtr3d ) ABCZDAEF
    ZADEFZAGHDBCOPQIJDAKLAMN $.
    $( [31-Mar-2011] $)

  $( The gcd of a nonnegative integer with 0 is itself.  (Contributed by Paul
     Chapman, 31-Mar-2011.) $)
  nn0gcdid0 $p |- ( N e. NN0 -> ( N gcd 0 ) = N ) $=
    ( cn0 wcel cc0 cgcd co cabs cfv cz wceq nn0z 0z gcdcom gcd0id eqtr3d syl cr
    mpan cle wbr nn0re nn0ge0 absid syl11anc eqtrd ) ABCZADEFZAGHZAUFAICZUGUHJA
    KUIDAEFZUGUHDICUIUJUGJLDAMRANOPUFAQCDASTUHAJAUAAUBAUCUDUE $.
    $( [31-Mar-2011] $)

  $( Negating one operand of the ` gcd ` operator does not alter the result.
     (Contributed by Paul Chapman, 21-Mar-2011.) $)
  gcdneg $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M gcd -u N ) = ( M gcd N ) ) $=
    ( cz wcel wa cgcd co cc0 wceq opreq12 adantl wb syl anbi2d cle wbr syl11anc
    wn cdivides wi cneg cc zcn negeq0 syl6bi imp eqtr4d cr cn0 gcdcl zre znegcl
    nn0z sylan2 letri3 adantr gcddvds simpr dvdsnegb mpbid notbid w3a dvdslegcd
    simpl syl111anc sylbid com12 mpdi impcom mpbird mpbir2and pm2.61dan eqcomd
    ex ) ACDZBCDZEZABFGZABUAZFGZVQAHIZBHIZEZVRVTIZVQWCEVRHHFGZVTWCVRWEIVQAHBHFJ
    KVQWCVTWEIZVQWCWAVSHIZEZWFVPWCWHLVOVPWBWGWAVPBUBDWBWGLBUCBUDMNKZAHVSHFJUEUF
    UGVQWCRZEWDVRVTOPZVTVROPZVQWDWKWLELZWJVQVRUHDZVTUHDZWMVQVRCDZWNVQVRUIDWPABU
    JVRUMMZVRUKMVQVTCDZWOVPVOVSCDZWRBULZVOWSEVTUIDWRAVSUJVTUMMUNZVTUKMVRVTUOQUP
    WJVQWKWJVQVRASPZVRVSSPZEZWKVQXBVRBSPZEXDABUQVQXEXCXBVQWPVPXEXCLWQVOVPURZVRB
    USQNUTVQWJXDWKTZVQWJWHRZXGVQWCWHWIVAVQWPVOWSXHXGTWQVOVPVDZVPWSVOWTKWPVOWSVB
    XHXGVRAVSVCVNVEVFVGVHVIWJVQWLWJVQVTASPZVTBSPZEZWLVQXLXJVTVSSPZEZVPVOWSXNWTA
    VSUQUNVQXKXMXJVQWRVPXKXMLXAXFVTBUSQNVJVQWJXLWLTZVQWRVOVPWJXOTXAXIXFWRVOVPVB
    WJXOVTABVCVNVEVGVHVIVKVLVM $.
    $( [21-Mar-2011] $)

  $( Negating one operand of the ` gcd ` operator does not alter the result.
     (Contributed by Paul Chapman, 22-Jun-2011.) $)
  neggcd $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( -u M gcd N ) = ( M gcd N ) ) $=
    ( cz wcel wa cneg cgcd co wceq gcdneg ancoms znegcl gcdcom sylan 3eqtr4d )
    ACDZBCDZEBAFZGHZBAGHZRBGHZABGHQPSTIBAJKPRCDQUASIALRBMNABMO $.
    $( [22-Jun-2011] $)

  ${
    gcdaddmlem.1 $e |- K e. ZZ $.
    gcdaddmlem.2 $e |- M e. ZZ $.
    gcdaddmlem.3 $e |- N e. ZZ $.
    $( Lemma for ~ gcdaddm . $)
    gcdaddmlem $p |- ( M gcd N ) = ( M gcd ( ( K x. M ) + N ) ) $=
      ( cc0 wceq cmul co caddc wa cgcd wbr cdivides cz wcel mp2an wi ax-mp nn0z
      wn cle gcddvds simpli c1 cn0 gcdcl w3a 1z dvds2ln mpanl12 mulid2i opreq2i
      mp3an zcn breqtri pm3.2i zmulcl zaddcl dvdslegcd mpi cneg znegcl mulneg1i
      cc ex opreq12i mulcli negcli addcomi negidi eqtri opreq1i addassi addid2i
      3eqtr3i anim12i cr zre letri3i sylibr pm4.57 opreq2 mul01i syl6eq opreq1d
      wo eqeq1d pm5.32i opreq12 sylbir eqtr4d sylbi jaoi pm2.61i ) BGHZABIJZCKJ
      ZGHZLZUBZWQCGHZLZUBZLZBCMJZBWSMJZHZXFXGXHUCNZXHXGUCNZLXIXBXJXEXKXBXGBONZX
      GWSONZLZXJXLXMXLXGCONZBPQZCPQZXLXOLZEFBCUDRZUEXGWRUFCIJZKJZWSOXRXGYAONZXS
      XGPQZXPXQXRYBSZXGUGQZYCXPXQYEEFBCUHRXGUATZEFAPQZUFPQZYCXPXQUIYDDUJAUFXGBC
      UKULUOTXTCWRKCXQCVFQFCUPTZUMUNUQURYCXPWSPQZXBXNXJSZSYFEWRPQZXQYJYGXPYLDEA
      BUSRFWRCUTRZYCXPYJUIXBYKXGBWSVAVGUOVBXEXHBONZXHCONZLZXKYNYOYNXHWSONZXPYJY
      NYQLZEYMBWSUDRZUEXHAVCZBIJZUFWSIJZKJZCOYRXHUUCONZYSXHPQZXPYJYRUUDSZXHUGQZ
      UUEXPYJUUGEYMBWSUHRXHUATZEYMYTPQZYHUUEXPYJUIUUFYGUUIDAVDTUJYTUFXHBWSUKULU
      OTUUCWRVCZWSKJZCUUAUUJUUBWSKABYGAVFQDAUPTZXPBVFQEBUPTZVEWSYJWSVFQYMWSUPTU
      MVHUUJWRKJZCKJGCKJZUUKCUUNGCKUUNWRUUJKJGUUJWRWRABUULUUMVIZVJZUUPVKWRUUPVL
      VMVNUUJWRCUUQUUPYIVOCYIVPZVQVMUQURUUEXPXQXEYPXKSZSUUHEFUUEXPXQUIXEUUSXHBC
      VAVGUOVBVRXGXHYCXGVSQYFXGVTTUUEXHVSQUUHXHVTTWAWBXFUBXAXDWHXIXAXDWCXAXIXDX
      AXDXIWQWTXCWQWSCGWQWSUUOCWQWRGCKWQWRAGIJGBGAIWDAUULWEWFWGUURWFWIWJZXDXGGG
      MJZXHBGCGMWKXDXAXHUVAHUUTBGWSGMWKWLWMZWNUVBWOWNWP $.
      $( [31-Mar-2011] $)
  $}

  $( Adding a multiple of one operand of the ` gcd ` operator to the other does
     not alter the result.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
  gcdaddm $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                  ( M gcd N ) = ( M gcd ( N + ( K x. M ) ) ) ) $=
    ( cz wcel cgcd cmul caddc wceq cc0 cif opreq1 opreq1d opreq2d opreq2 elimel
    co 0z cc zcn w3a eqeq2d id opreq12d eqeq12d gcdaddmlem dedth3h mulcl syl2an
    wa addcom 3impa eqtrd ) ADEZBDEZCDEZUAZBCFQZBABGQZCHQZFQZBCUSHQZFQUNUOUPURV
    AIURBUNAJKZBGQZCHQZFQZIUOBJKZCFQZVGVCVGGQZCHQZFQZIVGUPCJKZFQZVGVIVLHQZFQZIA
    BCJJJAVCIZVAVFURVPUTVEBFVPUSVDCHAVCBGLMNUBBVGIZURVHVFVKBVGCFLVQBVGVEVJFVQUC
    VQVDVICHBVGVCGOMUDUECVLIZVHVMVKVOCVLVGFOVRVJVNVGFCVLVIHONUEVCVGVLAJDRPBJDRP
    CJDRPUFUGUQUTVBBFUNUOUPUTVBIZUNUOUJUSSEZCSEVSUPUNASEBSEVTUOATBTABUHUICTUSCU
    KUIULNUM $.
    $( [31-Mar-2011] $)

  $( The gcd of a number and itself is its absolute value.  (Contributed by
     Paul Chapman, 31-Mar-2011.) $)
  gcdid $p |- ( N e. ZZ -> ( N gcd N ) = ( abs ` N ) ) $=
    ( cz wcel cc0 cgcd co c1 cmul caddc cabs cfv wceq 1z gcdaddm mp3an13 gcdid0
    0z cc zcn opreq2d mulid2 addid2 eqtrd syl 3eqtr3rd ) ABCZADEFZADGAHFZIFZEFZ
    AJKAAEFGBCUFDBCUGUJLMQGADNOAPUFUIAAEUFARCZUIALASUKUIDAIFAUKUHADIAUATAUBUCUD
    TUE $.
    $( [31-Mar-2011] $)

  $( The gcd of two integers is the same as that of their absolute values.
     (Contributed by Paul Chapman, 31-Mar-2011.) $)
  gcdabs $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                 ( ( abs ` M ) gcd ( abs ` N ) ) = ( M gcd N ) ) $=
    ( cz wcel wa cabs cfv wceq cneg wo co cr zre absor opreq12 gcdneg sylan9eqr
    cgcd gcdcom ex anim12i syl2an wi a1i ancoms znegcl sylan 3eqtr4d ccased mpd
    eqtrd ) ACDZBCDZEZAFGZAHZUOAIZHZJZBFGZBHZUTBIZHZJZEZUOUTRKZABRKZHZULALDZBLD
    ZVEUMAMBMVIUSVJVDANBNUAUBUNUPVAURVCVHUPVAEVHUCUNUOAUTBROUDUNURVAEZVHVKUNVFU
    QBRKZVGUOUQUTBROUNBUQRKZBARKZVLVGUMULVMVNHBAPUEULUQCDZUMVLVMHAUFZUQBSUGABSU
    HZQTUNUPVCEZVHVRUNVFAVBRKVGUOAUTVBROABPQTUNURVCEZVHVSUNVFUQVBRKZVGUOUQUTVBR
    OUNVTVLVGULVOUMVTVLHVPUQBPUGVQUKQTUIUJ $.
    $( [31-Mar-2011] $)

  $( The gcd remains unchanged if one operand is replaced with its remainder
     modulo the other.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
  modgcd $p |- ( ( M e. ZZ /\ N e. NN ) ->
                 ( ( M mod N ) gcd N ) = ( M gcd N ) ) $=
    ( cz wcel wa co cgcd cneg cmul caddc cr wceq recnd adantl syl eqtrd opreq2d
    cc syl111anc eqtr4d cmo cdiv cfl cfv cmin crp zre nnrp modval syl2an adantr
    cn nnre cc0 wne nnne0 redivcl syl3an 3anidm23 zcn w3a mulneg1 mulcom negeqd
    flcl ancoms 3adant1 mulcl negsub sylan2 3impb znegcl nnz gcdaddm cn0 zmodcl
    simpl nn0z gcdcom syl11anc 3eqtr3d ) ACDZBULDZEZBABUAFZGFZBAGFZWEBGFZABGFZW
    DWFBAABUBFZUCUDZHZBIFZJFZGFZWGWDWEWNBGWDWEABWKIFZUEFZWNWBAKDZBUFDWEWQLWCAUG
    ZBUHABUIUJWDARDZBRDZWKRDZWNWQLWBWTWCWBAWSMUKWCXAWBWCBBUMZMNWDWKCDZXBWDWJKDZ
    XDWBWCXEWBWRWCBKDWCBUNUOXEWSXCBUPABUQURUSWJVEOZWKUTOWTXAXBVAZWNAWPHZJFZWQXG
    WMXHAJXAXBWMXHLZWTXBXAXJXBXAEZWMWKBIFZHXHWKBVBXKXLWPWKBVCVDPVFVGQWTXAXBXIWQ
    LZXAXBEWTWPRDXMBWKVHAWPVIVJVKPSTQWDWLCDZBCDZWBWGWOLWDXDXNXFWKVLOWCXOWBBVMNZ
    WBWCVQZWLBAVNSTWDXOWECDZWFWHLXPWDWEVODXRABVPWEVROBWEVSVTWDXOWBWGWILXPXQBAVS
    VTWA $.
    $( [31-Mar-2011] $)

