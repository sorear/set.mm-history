$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                                Prime numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Prime $.

  $( Extend the definition of a class to include the set of prime numbers. $)
  cprime $a class Prime $.

  ${
    $d n p $.
    $( Define the set of prime numbers. $)
    df-prime $a |- Prime = { p e. NN | { n e. NN | n || p } ~~ 2o } $.
  $}

  ${
    $d P n p z $.
    $( The predicate "is a prime number".  A prime number is a positive integer
       with exactly two positive divisors.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    isprm $p |- ( P e. Prime <->
                  ( P e. NN /\ { n e. NN | n || P } ~~ 2o ) ) $=
      ( vp cv cdivides wbr cn crab c2o cen cprime wceq wb wcel breq2 adantr
      rabbidva breq1d df-prime elrab2 ) BDZCDZEFZBGHZIJFUAAEFZBGHZIJFCAGKUBALZU
      DUFIJUGUCUEBGUGUCUEMUAGNUBAUAEOPQRBCST $.
      $( [22-Jun-2011] $)


    $( A prime number is an integer.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    prmz $p |- ( P e. Prime -> P e. ZZ ) $=
      ( vn cprime wcel cn cz cv cdivides wbr crab c2o cen isprm simplbi nnz
      syl ) ACDZAEDZAFDQRBGAHIBEJKLIABMNAOP $.
      $( [23-Nov-2012] $) $( [22-Jun-2011] $)

    $( A prime number is a positive integer.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    prmnn $p |- ( P e. Prime -> P e. NN ) $=
      ( vz cprime wcel cn cv cdivides wbr crab c2o cen wa isprm biimpi simpld
      ) ACDZAEDZBFAGHBEIJKHZPQRLABMNO $.
      $( [22-Jun-2011] $) $( [26-Jun-2011] $)
  $}

  ${
    $d n z $.
    $( 1 is not a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    1nprm $p |- 1 e/ Prime $=
      ( vn vz c1 cprime wnel wcel wn cv cdivides wbr cn crab c2o cen csdm c1o
      csn wa wceq cle cz wi nnz 1nn dvdsle mpan2 syl nnge1 a1d jcad cr wb nnre
      1re letri3 sylibrd imp 1z iddvds ax-mp pm3.2i eleq1 breq1 anbi12d mpbiri
      impbii elrab elsn 3bitr4i eqriv elisseti ensn1 eqbrtri 1sdom2 ensdomtr
      mp2an sdomnen isprm mpbiran mtbir df-nel mpbir ) CDECDFZGWCAHZCIJZAKLZMNJ
      ZWFMOJZWGGWFPNJPMOJWHWFCQZPNBWFWIBHZKFZWJCIJZRZWJCSZWJWFFWJWIFWMWNWKWLWNW
      KWLWJCTJZCWJTJZRZWNWKWLWOWPWKWJUAFZWLWOUBZWJUCWRCKFZWSUDWJCUEUFUGWKWPWLWJ
      UHUIUJWKWJUKFZWNWQULZWJUMXACUKFXBUNWJCUOUFUGUPUQWNWMWTCCIJZRWTXCUDCUAFXCU
      RCUSUTVAWNWKWTWLXCWJCKVBWJCCIVCVDVEVFWEWLAWJKWDWJCIVCVGBCVHVIVJCCKUDVKVLV
      MVNWFPMVOVPWFMVQUTWCWTWGCAVRUDVSVTCDWAWB $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d N n $.
    $( The positive divisors of a positive integer include 1 and itself.
       (Contributed by Paul Chapman, 22-Jun-2011.) $)
    1idssfct $p |- ( N e. NN -> { 1 , N } C_ { n e. NN | n || N } ) $=
      ( cn wcel c1 cv cdivides wbr crab cpr wss cz nnz 1dvds 1nn wa breq1
      elrab biimpri mpan 3syl iddvds syl mpdan prssg ibi syl11anc ) BCDZEAFZBGH
      ZACIZDZBUKDZEBJUKKZUHBLDZEBGHZULBMZBNECDZUPULOULURUPPUJUPAECUIEBGQRSTUAUH
      BBGHZUMUHUOUSUQBUBUCUMUHUSPUJUSABCUIBBGQRSUDULUMPUNEBUKUKUKUEUFUG $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d P n p $.
    $( Lemma for ~ isprm2 . $)
    isprm2lem $p |- ( ( P e. NN /\ P =/= 1 ) ->
      ( { n e. NN | n || P } ~~ 2o <-> { n e. NN | n || P } = { 1 , P } ) ) $=
      ( vp cn wcel c1 wne cv cdivides wbr crab c2o cen cpr wceq wb wi neeq1
      breq2 rabbidv breq1d preq2 eqeq12d bibi12d imbi12d wa wss 1idssfct wn
      csn cun cin c0 disjsn c1o 1nn elisseti ensn1 visset pm54.43 mp2an elsnc
      notbii 3bitr3ri df-ne df-pr breq1i 3bitr4i com 2onn ensym entr sylan2
      sylanb cfn prfi cvv nnex ssrab2 ssexi enfi mpan mpbii adantl wpss dfpss2
      pssinf sylanbr an32s ex con4d mpd syl2an eqcomd anassrs breq1 syl6bbr
      biimprcd impbid rgen vtoclri imp ) ADEAFGZBHZAIJZBDKZLMJZXFFANZOZPZCHZFGZ
      XDXKIJZBDKZLMJZXNFXKNZOZPZQZXCXJQCADXKAOZXLXCXRXJXKAFRXTXOXGXQXIXTXNXFLMX
      TXMXEBDXKAXDISTZUAXTXNXFXPXHYAXKAFUBUCUDUEXSCDXKDEZXLXRYBXLUFZXOXQYCXOXQY
      BXLXOXQYBXLXOUFZUFXPXNYBXPXNUGZXPXNMJZXPXNOZYDBXKUHXLXPLMJZXOYFXKFOZUIZFU
      JZXKUJZUKZLMJZXLYHYKYLULUMOZXKYKEZUIYNYJYKXKUNYKUOMJYLUOMJYOYNPFFDUPUQURX
      KCUSZURYKYLUTVAYPYIXKFYQVBVCVDXKFVEXPYMLMFXKVFVGVHZXOYHLXNMJYFXNLLVIVJUQV
      KXPLXNVLVMVNYEYFUFZXNVOEZYGYFYTYEYFXPVOEZYTFXKVPXNVQEYFUUAYTPXNDVRXMBDVSV
      TXPXNVQWAWBWCWDYSYGYTYSYGUIZYTUIZYEUUBYFUUCYEUUBUFXPXNWEYFUUCXPXNWFXPXNWG
      WHWIWJWKWLWMWNWOWJXLXQXOQYBXQXOXLXQXOYHXLXNXPLMWPYRWQWRWDWSWJWTXAXB $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d P n z $.
    $( The predicate "is a prime number".  A prime number is an integer greater
       than or equal to 2 whose only positive divisors are 1 and itself.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    isprm2 $p |- ( P e. Prime <-> ( P e. ( ZZ>= ` 2 ) /\
                      A. z e. NN ( z || P -> ( z = 1 \/ z = P ) ) ) ) $=
      ( vn cprime wcel cn cv cdivides wbr crab c1 cpr wss wa wne c2 cuz cfv
      wceq wo wi wral wn wnel 1nprm df-nel mpbi eleq1 biimpcd mtoi df-ne
      sylibr pm4.71i c2o cen isprm wb isprm2lem jcab eqss imbi2i 1idssfct
      biantru 3bitr4i pm5.74ri adantr bitrd expcom pm5.32d syl5bb pm5.32ri
      bitri ancom anass eluz2b3 bitr4i anbi1i 3bitr2i wal breq1 elrab visset
      elpr imbi12i impexp albii dfss2 df-ral anbi2i 3bitri ) BDEZBFEZCGZBHIZCFJ
      ZKBLZMZNZBKOZNZBPQREZWQNZXAAGZBHIZXCKSXCBSTZUAZAFUBZNWKWKWSNWTWKWSWKBKSZU
      CWSWKXHKDEZKDUDXIUCUEKDUFUGXHWKXIBKDUHUIUJBKUKULUMWSWKWRWKWLWOUNUOIZNWSWR
      BCUPWSWLXJWQWLWSXJWQUQWLWSNZXJWOWPSZWQBCURWLXLWQUQWSWLXLWQWLWQWPWOMZNZUAW
      LWQUAZWLXMUAZNWLXLUAXOWLWQXMUSXLXNWLWOWPUTVAXPXOCBVBVCVDVEVFVGVHVIVJVKVLW
      TWSWRNWSWLNZWQNXBWRWSVMWSWLWQVNXQXAWQXQXKXAWSWLVMBVOVPVQVRWQXGXAXCWOEZXCW
      PEZUAZAVSXCFEZXFUAZAVSWQXGXTYBAXTYAXDNZXEUAYBXRYCXSXEWNXDCXCFWMXCBHVTWAXC
      KBAWBWCWDYAXDXEWEVLWFAWOWPWGXFAFWHVDWIWJ $.
      $( [23-Nov-2012] $) $( [22-Jun-2011] $)

    $( The predicate "is a prime number".  A prime number is an integer greater
       than or equal to 2 with no divisors strictly between 1 and itself.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    isprm3 $p |- ( P e. Prime <-> ( P e. ( ZZ>= ` 2 ) /\
                      A. z e. ( 2 ... ( P - 1 ) ) -. z || P ) ) $=
      ( cprime wcel c2 cuz cfv cv cdivides wbr c1 wceq wo wi cn wral wa wn
      cmin co cfz isprm2 bi2.04 wb iman clt cle 2nn elnnuz mpbi uztrn mpan2
      sylibr nnge1 adantr a1d cz nnz dvdsle sylan jcad sylan2 wne cr zre nnre
      1re leltne mp3an1 3adant2 3expia anim12d syl2an pm4.38 df-ne necom bitri
      anbi12i ioran bitr4i bibi2i sylib syl6 syld imp eluzelz caddc 1z zltp1le
      mpan df-2 breq1i syl6bbr zltlem1 anbi12d peano2zm 2z elfz mp3an2 bitr4d
      bitr3d anasss expcom pm5.32d fzssuz wss 2re 1lt2 ltleii eluz mp2an mpbir
      uzss ax-mp sstri nnuz sseqtr4i sseli pm4.71ri notbid syl5bb ex pm5.74d
      syl5bbr con2b syl6bb ralbidv2 pm5.32i ) BCDBEFGZDZAHZBIJZYKKLZYKBLZMZNZAO
      PZQYJYLRZAEBKSTZUATZPZQABUBYJYQUUAYJYPYRAOYTYJYKODZYPNZYLYKYTDZRZNZUUDYRN
      UUCYLUUBYONZNYJUUFYLUUBYOUCYJYLUUGUUEYJYLUUGUUEUDUUGUUBYORZQZRYJYLQZUUEUU
      BYOUEUUJUUIUUDUUJUUIUUBUUDQUUDUUJUUBUUHUUDUUBUUJUUHUUDUDZUUBYJYLUUKUUBYJQ
      ZYLQKYKUFJZYKBUFJZQZUUHUUDUULYLUUOUUHUDZUULYLKYKUGJZYKBUGJZQZUUPYJUUBBODZ
      YLUUSNYJBKFGZDZUUTYJEUVADZUVBEODUVCUHEUIUJEBKUKULBUIUMZUUBUUTQZYLUUQUURUV
      EUUQYLUUBUUQUUTYKUNUOUPUUBYKUQDZUUTYLUURNYKURZYKBUSUTVAVBUUBUVFUUTUUSUUPN
      YJUVGUVDUVFUUTQUUSUUMYKKVCZUDZUUNBYKVCZUDZQZUUPUVFYKVDDZBVDDZUUSUVLNUUTYK
      VEBVFUVMUVNQUUQUVIUURUVKUVMUVNUUQUVIUVMUUQUVIUVNKVDDUVMUUQUVIVGKYKVHVIVJV
      KUVMUVNUURUVKYKBVHVKVLVMUVLUUOUVHUVJQZUDUUPUUMUUNUVHUVJVNUVOUUHUUOUVOYMRZ
      YNRZQUUHUVHUVPUVJUVQYKKVOUVJYKBVCUVQBYKVPYKBVOVQVRYMYNVSVTWAWBWCVMWDWEUUL
      UUOUUDUDZYLUUBUVFBUQDZUVRYJUVGEBWFUVFUVSQZUUOEYKUGJZYKYSUGJZQZUUDUVTUUMUW
      AUUNUWBUVFUUMUWAUDUVSUVFUUMKKWGTZYKUGJZUWAKUQDZUVFUUMUWEUDWHKYKWIWJEUWDYK
      UGWKWLWMUOYKBWNWOUVSUVFYSUQDZUUDUWCUDZBWPUVFEUQDZUWGUWHWQYKEYSWRWSVBWTVMU
      OXAXBXCXDUUDUUBYTOYKYTUVAOYTYIUVAEYSXEUVCYIUVAXFUVCKEUGJZKEVGXGXHXIUWFUWI
      UVCUWJUDWHWQKEXJXKXLKEXMXNXOXPXQXRXSWMXTYAYBYCYDYLUUDYEYFYGYHVQ $.
      $( [27-Nov-2012] $) $( [22-Jun-2011] $)
  $}

  ${
    $d P z $.
    $( The predicate "is a prime number".  A prime number is an integer greater
       than or equal to 2 whose only divisor greater than or equal to 2 is
       itself.  (Contributed by Paul Chapman, 26-Oct-2012.) $)
    isprm4 $p |- ( P e. Prime <-> ( P e. ( ZZ>= ` 2 ) /\
                      A. z e. ( ZZ>= ` 2 ) ( z || P -> z = P ) ) ) $=
      ( cprime wcel c2 cuz cfv cv cdivides wbr c1 wceq wo wi cn wral wa isprm2
      wne eluz2b3 imbi1i impexp bi2.04 wn df-ne df-or bitr4i imbi2i bitri
      3bitri ralbii2 anbi2i ) BCDBEFGZDZAHZBIJZUOKLZUOBLZMZNZAOPZQUNUPURNZAUMPZ
      QABRVCVAUNVBUTAUMOUOUMDZVBNUOODZUOKSZQZVBNVEVFVBNZNVEUTNVDVGVBUOTUAVEVFVB
      UBVHUTVEVHUPVFURNZNUTVFUPURUCVIUSUPVIUQUDZURNUSVFVJURUOKUEUAUQURUFUGUHUIU
      HUJUKULUG $.
      $( [23-Nov-2012] $) $( [26-Oct-2012] $)
  $}

  ${
    $d P x $.
    $( A prime number is an integer greater than or equal to 2.  (Contributed
       by Paul Chapman, 17-Nov-2012.) $)
    prmuz2 $p |- ( P e. Prime -> P e. ( ZZ>= ` 2 ) ) $=
      ( vx cprime wcel c2 cuz cfv cv cdivides wbr wceq wi wral isprm4 simplbi
      ) ACDAEFGZDBHZAIJQAKLBPMBANO $.
      $( [17-Nov-2012] $)
  $}

  ${
    $d N z $.  $d P z $.
    $( An integer greater than or equal to 2 divides a prime number iff it is
       equal to it.  (Contributed by Paul Chapman, 26-Oct-2012.) $)
    dvdsprm $p |- ( ( N e. ( ZZ>= ` 2 ) /\ P e. Prime ) ->
                    ( N || P <-> N = P ) ) $=
      ( vz c2 cuz cfv wcel cprime wa cdivides wbr wceq wi cv wral isprm4
      simprbi breq1 eqeq1 imbi12d rcla4v mpan9 ancoms cz eluzelz iddvds breq2
      syl5ibcom syl adantr impbid ) BDEFZGZAHGZIBAJKZBALZUNUMUOUPMZUNCNZAJKZURA
      LZMZCULOZUMUQUNAULGVBCAPQVAUQCBULURBLUSUOUTUPURBAJRURBASTUAUBUCUMUPUOMZUN
      UMBUDGZVCDBUEVDBBJKUPUOBUFBABJUGUHUIUJUK $.
      $( [26-Oct-2012] $)
  $}

  $( 2 is a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  2prm $p |- 2 e. Prime $=
    ( vz c2 cprime wcel cuz cfv cv cdivides wbr wn c1 cmin co cfz wral isprm3
    cz clt eluz2b1 2z 1lt2 mpbir2an c0 ral0 wceq caddc df-2 eqcomi 2cn ax-1cn
    subaddi mpbir eqbrtri wb 1z eqeltri fzn mp2an mpbi raleqi ) BCDBBEFDZAGBHIJ
    ZABBKLMZNMZOZABPVABQDZKBRIBSTUAUBVEVBAUCOVBAUDVBAVDUCVCBRIZVDUCUEZVCKBRVCKU
    EKKUFMZBUEBVIUGUHBKKUIUJUJUKULZUAUMVFVCQDVGVHUNTVCKQVJUOUPBVCUQURUSUTULUB
    $.
    $( [27-Nov-2012] $) $( [22-Jun-2011] $)

  $( 3 is a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  3prm $p |- 3 e. Prime $=
    ( vz c3 cprime wcel c2 cuz cfv cv cdivides wbr wn c1 cmin co cfz wral
    isprm3 cz clt eluz2b1 cn 3nn nnz ax-mp 1lt3 mpbir2an wceq caddc df-3
    eqcomi cc nncn ax-1cn 2cn subadd2i mpbir opreq2i eleq2i wi 2z iddvds 2nn
    1lt2 ndvdsp1 mp3an breq2i mtbir breq1 mtbiri elfz1eq elfz3 eleq1 mpbiri
    impbii imbi1i sylbi rgen ) BCDBEFGDZAHZBIJZKZAEBLMNZONZPABQVRBRDZLBSJBTBUAD
    ZWDUBBUCUDUEUFWAAWCVSWCDVSEEONZDZWAWCWFVSWBEEOWBEUGELUHNZBUGBWHUIUJBLEWEBUK
    DUBBULUDUMUNUOUPUQURWGWAUSVSEUGZWAUSWIVTEBIJZWJEWHIJZEEIJZWKKZERDZWLUTEVAUD
    WNEUADLESJWLWMUSUTVBVCEEVDVEUDBWHEIUIVFVGVSEBIVHVIWGWIWAWGWIVSEVJWIWGEWFDZW
    NWOUTEVKUDVSEWFVLVMVNVOUPVPVQUF $.
    $( [23-Nov-2012] $) $( [22-Jun-2011] $)

  $( 4 is not a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  4nprm $p |- 4 e/ Prime $=
    ( vz c4 cprime wnel wcel wn c2 cdivides wbr wceq wi wa cmul co cz 2z
    dvdsmul1 mp2an 2t2e4 breqtri wne cc0 2ne0 caddc 2p2e4 2cn addid1i eqeq12i
    0cn addcani bitr3i necon3bii mpbir necom df-ne bitri mpbi pm3.2i annim cuz
    cfv cv wral uzid ax-mp isprm4 simprbi breq1 eqeq1 imbi12d rcla4v mpsyl mto
    df-nel ) BCDBCEZFVOGBHIZGBJZKZVPVQFZLVRFVPVSGGGMNZBHGOEZWAGVTHIPPGGQRSTBGUA
    ZVSWBGUBUAUCBGGUBBGJGGUDNZGUBUDNZJGUBJWCBWDGUEGUFUGUHGGUBUFUFUIUJUKULUMWBGB
    UAVSBGUNGBUOUPUQURVPVQUSUQGGUTVAZEZVOAVBZBHIZWGBJZKZAWEVCZVRWAWFPGVDVEVOBWE
    EWKABVFVGWJVRAGWEWGGJWHVPWIVQWGGBHVHWGGBVIVJVKVLVMBCVNUM $.
    $( [23-Nov-2012] $) $( [22-Jun-2011] $)

  ${
    $d N z $.  $d P z $.
    $( A prime number either divides an integer or is coprime to it, but not
       both.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    coprm $p |- ( ( P e. Prime /\ N e. ZZ ) ->
                  ( -. P || N <-> ( P gcd N ) = 1 ) ) $=
      ( vz cprime wcel cz wa cdivides wbr wn cgcd co c1 wceq clt c2 cuz cfv cv
      wo wi cn wral isprm2 simplbi eluz2b1 sylib simpld gcddvds sylan simprd
      breq1 syl5ibcom con3d cc0 0nnn prmnn eleq1 mtoi intnanrd adantr gcdn0cl
      ex mpd simprbi eqeq1 orbi12d imbi12d rcla4v syl5com mp2d biorf orcom
      syl6bb syl5ibrcom syld wne cle iddvds syl w3a dvdslegcd 3anidm12 mpand
      cr zre nnre 1re ltletr mp3an1 syl11anc ltne 3expia sylancr 3syld df-ne
      syl6ib con2d impbid ) ADEZBFEZGZABHIZJZABKLZMNZXBXDXEANZJZXFXBXGXCXBXEBHI
      ZXGXCXBXEAHIZXIWTAFEZXAXJXIGWTXKMAOIZWTAPQREZXKXLGWTXMCSZAHIZXNMNZXNANZTZ
      UAZCUBUCZCAUDZUEAUFUGZUHZABUIUJZUKXEABHULUMUNXBXFXHXFXGTZXBXEUBEZXJYEXBAU
      ONZBUONZGJZYFWTYIXAWTYGYHWTYGUOUBEZUPWTAUBEYGYJAUQAUOUBURUMUSUTVAZWTXKXAY
      IYFUAYCXKXAGYIYFABVBVCUJVDZXBXJXIYDUHWTYFXJYEUAZUAXAWTXTYFYMWTXMXTYAVEXSY
      MCXEUBXNXENZXOXJXRYEXNXEAHULYNXPXFXQXGXNXEMVFXNXEAVFVGVHVIVJVAVKXHXFXGXFT
      YEXGXFVLXGXFVMVNVOVPXBXCXFXBXCXEMVQZXFJXBXCAXEVRIZMXEOIZYOXBAAHIZXCYPWTYR
      XAWTXKYRYCAVSVTVAXBYIYRXCGYPUAZYKWTXKXAYIYSUAZYCXKXAYTXKXKXAWAYIYSAABWBVC
      WCUJVDWDXBXLYPYQWTXLXAWTXKXLYBUKVAXBAWEEZXEWEEZXLYPGYQUAZWTUUAXAWTXKUUAYC
      AWFVTVAXBYFUUBYLXEWGVTZMWEEZUUAUUBUUCWHMAXEWIWJWKWDXBUUEUUBYQYOUAWHUUDUUE
      UUBYQYOMXEWLWMWNWOXEMWPWQWRWS $.
      $( [23-Nov-2012] $) $( [22-Jun-2011] $)
  $}

  $( If an integer divides the product of two integers and is coprime to one of
     them, then it divides the other.  (Contributed by Paul Chapman,
     22-Jun-2011.) $)
  coprmdvds $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                    ( ( K || ( M x. N ) /\ ( K gcd M ) = 1 ) -> K || N ) ) $=
    ( cz wcel w3a cmul co cdivides wbr cgcd c1 wceq wi wa cc zcn mulcom syl2an
    3adant1 breq2d dvdsmul2 ancoms 3adant2 simp1 zmulcl dvdsgcd syl111anc
    mpand sylbid adantr cabs cfv absmulgcd 3coml opreq2 mulid1 syl sylan9eqr
    fveq2d 3ad2antl3 eqtrd wb dvdsabsb bitr4d sylibd ex com23 imp3a ) ADEZBDEZC
    DEZFZABCGHZIJZABKHZLMZACIJZVMVQVOVRVMVQVOVRNVMVQOZVOACAGHZCBGHZKHZIJZVRVMVO
    WCNVQVMVOAWAIJZWCVMVNWAAIVKVLVNWAMZVJVKBPECPEZWEVLBQCQZBCRSTUAVMAVTIJZWDWCV
    JVLWHVKVLVJWHCAUBUCUDVMVJVTDEZWADEZWHWDOWCNVJVKVLUEVJVLWIVKVLVJWICAUFUCUDVK
    VLWJVJVLVKWJCBUFUCTAVTWAUGUHUIUJUKVSWCACULUMZIJZVRVSWBWKAIVSWBCVPGHZULUMZWK
    VMWBWNMZVQVLVJVKWOCABUNUOUKVLVJVQWNWKMVKVLVQOWMCULVQVLWMCLGHZCVPLCGUPVLWFWP
    CMWGCUQURUSUTVAVBUAVMVRWLVCZVQVJVLWQVKACVDUDUKVEVFVGVHVI $.
    $( [22-Jun-2011] $)

  $( Euclid's lemma.  A prime number divides the product of two integers iff it
     divides at least one of them.  (Contributed by Paul Chapman,
     17-Nov-2012.) $)
  euclemma $p |- ( ( P e. Prime /\ M e. ZZ /\ N e. ZZ ) ->
                        ( P || ( M x. N ) <-> ( P || M \/ P || N ) ) ) $=
    ( cprime wcel cz w3a cmul co cdivides wbr wo wn wi wa cgcd c1 wceq wb
    coprm 3adant3 anbi2d prmz coprmdvds syl3an1 sylbid exp3a df-or syl6ibr
    ordvdsmul impbid ) ADEZBFEZCFEZGZABCHIJKZABJKZACJKZLZUOUPUQMZURNUSUOUPUTURU
    OUPUTOUPABPIQRZOZURUOUTVAUPULUMUTVASUNABTUAUBULAFEZUMUNVBURNAUCZABCUDUEUFUG
    UQURUHUIULVCUMUNUSUPNVDABCUJUEUK $.
    $( [17-Nov-2012] $)

  ${
    $d N m $.  $d S x y $.  $d m x y $.
    exprmfctlem.1 $e |- S = { m e. ( ZZ>= ` 2 ) | m || N } $.
    exprmfctlem.2 $e |- N e. ( ZZ>= ` 2 ) $.
    $( Lemma for ~ exprmfct . $)
    exprmfctlem $p |- E. x e. Prime x || N $=
      ( vy cv cle wbr wral wrex cdivides cprime c2 cuz cfv wss c0 wne crab
      ssrab2 eqsstri wcel cz eluzelz ax-mp iddvds wa breq1 elrab2 ne0i sylbir
      mp2an uzwo wceq wi w3a df-3an simpl a1i simprbi adantl sseli syl dvdstr
      mp3an3 syl2an mpan2d expcom imp3a jcad syl6ibr 3impib cn c1 eluz2b3
      simplbi dvdsle cr wb zre nnre letri3 biimprd exp3a syld imim1 syl5 mpdi
      syl5bir com12 ralimdv2 isprm4 baib imp adantr jca reximi2 ) AHZGHZIJZGBKZ
      ABLZWTDMJZANLBOPQZRBSTZXDBCHZDMJZCXFUAXFEXICXFUBUCZDXFUDZDDMJZXGFDUEUDZXL
      XKXMFODUFUGZDUHUGXKXLUIDBUDXGXIXLCDXFBXHDDMUJEUKBDULUMUNBAGOUOUNXCXEABNWT
      BUDZXCUIWTNUDZXEXOXCXPXOXCXAWTMJZXAWTUPZUQZGXFKZXPXOXBXSGBXFXABUDZXBUQZXO
      XAXFUDZXSUQYBXOYCXSYBXOYCUIZXQXRYDXQUIXOYCXQURZYBXRXOYCXQUSYBYEYAXRXOYCXQ
      YAXOYCXQUIZYCXADMJZUIYAXOYFYCYGYFYCUQXOYCXQUTVAXOYCXQYGYCXOXQYGUQYCXOUIZX
      QXEYGXOXEYCXOWTXFUDZXEXIXECWTXFBXHWTDMUJEUKVBZVCYCXAUEUDZWTUEUDZXQXEUIYGU
      QZXOOXAUFZXOYIYLBXFWTXJVDZOWTUFVEYKYLXMYMXNXAWTDVFVGVHVIVJVKVLXIYGCXAXFBX
      HXADMUJEUKVMVNYEXBXRUQZYBYAXRUQXOYCXQYPXOYCXQYPYCXOXQYPUQYHXQXAWTIJZYPYCY
      KWTVOUDZXQYQUQXOYNXOYIYRYOYIYRWTVPTWTVQVRVEZXAWTVSVHYHYQXBXRYHXRYQXBUIZYC
      XAVTUDZWTVTUDZXRYTWAXOYCYKUUAYNXAWBVEXOYRUUBYSWTWCVEXAWTWDVHWEWFWGVJVKVNY
      AXBXRWHWIWJWKWFWFWLWMXOXPXTXOYIXPXTWAYOXPYIXTGWTWNWOVEWEWGWPXOXEXCYJWQWRW
      SUG $.
      $( [23-Nov-2012] $) $( [26-Oct-2012] $)
  $}

  ${
    $d N m p $.
    $( Every integer greater than or equal to 2 has a prime factor.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    exprmfct $p |- ( N e. ( ZZ>= ` 2 ) -> E. p e. Prime p || N ) $=
      ( vm c2 cuz cfv wcel cv cdivides wbr cprime wrex cif wceq breq2 rexbidv
      crab eqid cz 2z uzid ax-mp elimel exprmfctlem dedth ) ADEFZGZBHZAIJZBKLUH
      UGADMZIJZBKLADAUJNUIUKBKAUJUHIOPBCHUJIJCUFQZCUJULRADUFDSGDUFGTDUAUBUCUDUE
      $.
      $( [26-Oct-2012] $)
  $}

  $( No prime number divides 1.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
  nprmdvds1 $p |- ( P e. Prime -> -. P || 1 ) $=
    ( cprime wcel c1 cdivides wbr clt cz c2 cuz cfv wa prmuz2 eluz2b1 sylib
    simprd cle wn wi prmz cn 1nn dvdsle mpan2 syl cr wb zre 1re lenlt 3syl
    sylibd mt2d ) ABCZADEFZDAGFZUNAHCZUPUNAIJKCUQUPLAMANOPUNUOADQFZUPRZUNUQUOUR
    SZATZUQDUACUTUBADUCUDUEUNUQAUFCZURUSUGZVAAUHVBDUFCVCUIADUJUDUKULUM $.
    $( [23-Nov-2012] $) $( [17-Nov-2012] $)

  ${
    $d N x y $.  $d N y z $.  $d S x y $.
    maxprmfct.1 $e |- S = { z e. Prime | z || N } $.
    $( The set of prime factors of an integer greater than or equal to 2
       satisfies the conditions to have a supremum, and that supremum is a
       member of the set.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
    maxprmfct $p |- ( N e. ( ZZ>= ` 2 ) ->
         ( ( S C_ RR /\ S =/= (/) /\ E. x e. RR A. y e. S y <_ x ) /\
         sup ( S , RR , < ) e. S ) ) $=
      ( c2 cuz cfv wcel cr wss c0 wne cv cle wbr wral wrex w3a clt csup cz
      cprime cdivides crab ssrab2 eqsstri prmz ssriv sstri zssre a1i exprmfct
      wex wa breq1 elrab2 exbii n0 df-rex 3bitr4ri sylib cn c1 eluz2b3 simplbi
      anim1i sylbi wi dvdsle expcom imp3a syl5 r19.21aiv syl nnre wceq breq2
      ralbidv rcla4ev sylan mpdan 3jca suprzcl mp3an1 syl11anc jca ) EGHIJZDKLZ
      DMNZBOZAOZPQZBDRZAKSZTDKUAUBDJZWIWJWKWPWJWIDUCKDUDUCDCOZEUEQZCUDUFUDFWSCU
      DUGUHBUDUCWLUIZUJUKZULUKUMWIWLEUEQZBUDSZWKEBUNWLDJZBUOWLUDJZXBUPZBUOWKXCX
      DXFBWSXBCWLUDDWRWLEUEUQFURZUSBDUTXBBUDVAVBVCZWIWLEPQZBDRZWPWIEVDJZXJWIXKE
      VENEVFVGZXKXIBDXDWLUCJZXBUPZXKXIXDXFXNXGXEXMXBWTVHVIXKXMXBXIXMXKXBXIVJWLE
      VKVLVMVNVOVPWIEKJZXJWPWIXKXOXLEVQVPWOXJAEKWMEVRWNXIBDWMEWLPVSVTWAWBWCZWDW
      IWKWPWQXHXPDUCLWKWPWQXAABDWEWFWGWH $.
      $( [23-Nov-2012] $) $( [17-Nov-2012] $)
  $}

  ${
    $d N m $.  $d P k m $.  $d Q k m $.
    $( A prime divides a positive power of another iff they are equal.
       (Contributed by Paul Chapman, 30-Nov-2012.) $)
    prmdvdsexpb $p |- ( ( P e. Prime /\ Q e. Prime /\ N e. NN ) ->
                        ( P || ( Q ^ N ) <-> P = Q ) ) $=
      ( vm vk cprime wcel cn w3a cexp co cdivides wbr wceq wi wa cv c1 caddc
      opreq2 breq2d imbi1d imbi2d wb cc cz prmz zcn syl exp1 adantl c2 cuz cfv
      prmuz2 dvdsprm sylan biimpd sylbid wo cmul cn0 nnnn0 expp1 syl2an
      3adant1 simp1 zexpcl 3ad2ant2 euclemma syl111anc bitrd biimpa 3adant3
      orim2d adantr mpd ex imimorb sylibr 3expa expcom a2d nnind impcom 3impa
      iddvdsexp opreq1 syl5ibcom 3adant2 impbid ) AFGZBFGZCHGZIABCJKZLMZABNZWLW
      MWNWPWQOZWNWLWMPZWRWSABDQZJKZLMZWQOZOWSABRJKZLMZWQOZOWSABEQZJKZLMZWQOZOWS
      ABXGRSKZJKZLMZWQOZOWSWRODECWTRNZXCXFWSXOXBXEWQXOXAXDALWTRBJTUAUBUCWTXGNZX
      CXJWSXPXBXIWQXPXAXHALWTXGBJTUAUBUCWTXKNZXCXNWSXQXBXMWQXQXAXLALWTXKBJTUAUB
      UCWTCNZXCWRWSXRXBWPWQXRXAWOALWTCBJTUAUBUCWSXEABLMZWQWMXEXSUDWLWMXDBALWMBU
      EGZXDBNWMBUFGZXTBUGZBUHUIZBUJUIUAUKWSXSWQWLAULUMUNGWMXSWQUDAUOBAUPUQURZUS
      XGHGZWSXJXNWSYEXJXNOZWLWMYEYFWLWMYEIZXMXIWQUTZOYFYGXMYHYGXMPXIXSUTZYHYGXM
      YIYGXMAXHBVAKZLMZYIYGXLYJALWMYEXLYJNZWLWMXTXGVBGZYLYEYCXGVCZBXGVDVEVFUAYG
      WLXHUFGZYAYKYIUDWLWMYEVGWMYEYOWLWMYAYMYOYEYBYNBXGVHVEVFWMWLYAYEYBVIAXHBVJ
      VKVLVMYGYIYHOXMYGXSWQXIWLWMXSWQOYEYDVNVOVPVQVRXMXIWQVSVTWAWBWCWDWEWFWLWNW
      QWPOWMWLWNPAACJKZLMZWQWPWLAUFGWNYQAUGACWGUQWQYPWOALABCJWHUAWIWJWK $.
      $( [30-Nov-2012] $)
  $}

  ${
    $d P z $.
    $( Two positive prime powers are equal iff the primes and the powers are
       equal.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
    prmexpb $p |- ( ( ( P e. Prime /\ Q e. Prime ) /\ ( M e. NN /\ N e. NN ) )
                    -> ( ( P ^ M ) = ( Q ^ N ) <-> ( P = Q /\ M = N ) ) ) $=
      ( vz cprime wcel wa cn cexp co wceq w3a cdivides wbr cz prmz adantr
      3ad2ant1 simp2l iddvdsexp syl11anc wb breq2 3ad2ant3 simp1l simp1r
      simp2r prmdvdsexpb syl111anc bitrd mpbid simp3 opreq1d eqtr4d cr cn0 c1
      clt zre syl nnnn0 3ad2ant2 adantl c2 cuz cfv cv wi wral isprm4 simplbi
      eluz2b1 simprbi ad2antrr 3adant3 expcan syl31anc jca 3expia opreq12
      impbid1 ) AFGZBFGZHZCIGZDIGZHZHACJKZBDJKZLZABLZCDLZHZWEWHWKWNWEWHWKMZWLWM
      WOAWINOZWLWOAPGZWFWPWEWHWQWKWCWQWDAQRSZWEWFWGWKTACUAUBWOWPAWJNOZWLWKWEWPW
      SUCWHWIWJANUDUEWOWCWDWGWSWLUCWCWDWHWKUFWCWDWHWKUGWEWFWGWKUHABDUIUJUKULZWO
      WIADJKZLZWMWOWIWJXAWEWHWKUMWOABDJWTUNUOWOAUPGZCUQGZDUQGZURAUSOZXBWMUCWOWQ
      XCWRAUTVAWHWEXDWKWFXDWGCVBRVCWHWEXEWKWGXEWFDVBVDVCWEWHXFWKWCXFWDWHWCAVEVF
      VGZGZXFWCXHEVHZANOXIALVIEXGVJEAVKVLXHWQXFAVMVNVAVOVPACDVQVRULVSVTABCDJWAW
      B $.
      $( [30-Nov-2012] $)
  $}

