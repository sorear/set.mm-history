$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                           The division algorithm
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    divalglem0.1 $e |- N e. ZZ $.
    divalglem0.2 $e |- D e. ZZ $.
    $( Lemma for ~ divalg . $)
    divalglem0 $p |- ( ( R e. ZZ /\ K e. ZZ ) -> ( D || ( N - R ) ->
                       D || ( N - ( R - ( K x. ( abs ` D ) ) ) ) ) ) $=
      ( cz wcel wa cmin co cdivides wbr cabs cfv cmul caddc cn0 nn0abscl ax-mp
      nn0z dvdsmul2 mpan2 iddvds wb dvdsabsb anidms mpbid jctil wi zmulcl
      dvdstr mp3an12 syl mpd adantl zsubcl mpan dvds2add mp3an1 syl2an mpan2d
      cc wceq zcn subsub breq2d sylibrd ) BGHZCGHZIZADBJKZLMZAVLCANOZPKZQKZLMZA
      DBVOJKJKZLMVKVMAVOLMZVQVJVSVIVJAVNLMZVNVOLMZIZVSVJWAVTVJVNGHZWAVNRHZWCAGH
      ZWDFASTVNUATZCVNUBUCWEVTFWEAALMZVTAUDWEWGVTUEAAUFUGUHTUIVJVOGHZWBVSUJZVJW
      CWHWFCVNUKUCZWEWCWHWIFWFAVNVOULUMUNUOUPVIVLGHZWHVMVSIVQUJZVJDGHZVIWKEDBUQ
      URWJWEWKWHWLFAVLVOUSUTVAVBVKVRVPALVIBVCHZVOVCHZVRVPVDZVJBVEVJWHWOWJVOVEUN
      DVCHZWNWOWPWMWQEDVETDBVOVFUTVAVGVH $.
      $( [21-Mar-2011] $)

    divalglem1.3 $e |- D =/= 0 $.
    $( Lemma for ~ divalg . $)
    divalglem1 $p |- 0 <_ ( N + ( abs ` ( N x. D ) ) ) $=
      ( cneg cmul co cabs cfv cle wbr cc0 caddc wo zrei 0re letrii c1 cn wcel
      cz wne nnabscl mp2an nnge1 ax-mp cr wb le0neg1 wa renegcli recni abscli
      lemulge11 mpanl12 sylanb mpan2 absmuli absnidi opreq1d syl5eq breqtrrd
      le0neg2 cc remulcli absge0 letri sylbi jaoi cmin df-neg breq1i
      lesubadd2i bitri mpbi ) BFZBAGHZIJZKLZMBVSNHKLZBMKLZMBKLZOVTBMBCPZQRWBVTW
      CWBVQVQAIJZGHZVSKWBSWEKLZVQWFKLZWETUAZWGAUBUAAMUCWIDEAUDUEWEUFUGWBMVQKLZW
      GWHBUHUAZWBWJUIWDBUJUGVQUHUAWEUHUAWJWGUKWHBWDULZAAADPZUMZUNVQWEUOUPUQURWB
      VSBIJZWEGHWFBABWDUMWNUSWBWOVQWEGBWDUTVAVBVCWCVQMKLZVTWKWCWPUIWDBVDUGWPMVS
      KLZVTVRVEUAWQVRBAWDWMVFUMZVRVGUGVQMVSWLQVRWRUNZVHURVIVJUGVTMBVKHZVSKLWAVQ
      WTVSKBVLVMMBVSQWDWSVNVOVP $.
      $( [21-Mar-2011] $)

    $d D r $.  $d N r $.
    divalglem2.4 $e |- S = { r e. NN0 | D || ( N - r ) } $.
    $( Lemma for ~ divalg . $)
    divalglem2 $p |- sup ( S , RR , `' < ) e. S $=
      ( c0 wne cr clt ccnv csup wcel cmul co cabs cfv caddc cn0 cc0 cle wbr
      divalglem1 cz elnn0z zmulcl mp2an nn0abscl ax-mp nn0z zaddcl mpbiran
      mpbir cmin cdivides cv wceq opreq2 breq2d elrab2 cneg iddvds wb dvdsabsb
      anidms mpbid znegcl w3a eqid dvds0lem mpan2 mp3an wa wi dvdstr cc zcn
      absmuli negeqi df-neg subidi opreq1i subsub4 3eqtr2ri abscli recni
      mulneg1i 3eqtr4i breqtrri mpbiran2 ne0i cuz wss crab ssrab2 eqsstri
      nn0uz sseqtri infmssuzcl mpan ) BIJZBKLMNBOZCCAPQZRSZTQZBOZXCXHXGUAOZXIUB
      XGUCUDZACEFGUEXIXGUFOZXJXGUGCUFOZXFUFOZXKEXFUAOZXMXEUFOZXNXLAUFOZXOEFCAUH
      UIXEUJUKXFULUKZCXFUMUIUNUOXHXIACXGUPQZUQUDZACDURZUPQZUQUDZXSDXGUABXTXGUSY
      AXRAUQXTXGCUPUTVAHVBACRSZVCZARSZPQZXRUQAYEUQUDZYEYFUQUDZAYFUQUDZXPYGFXPAA
      UQUDZYGAVDXPYJYGVEAAVFVGVHUKYDUFOZYEUFOZYFUFOZYHYCUFOZYKYCUAOZYNXLYOECUJU
      KYCULUKYCVIUKZYEUAOZYLXPYQFAUJUKYEULUKZYKYLYMYPYRYDYEUHUIZYKYLYMVJYFYFUSY
      HYFVKYDYEYFVLVMVNXPYLYMYGYHVOYIVPFYRYSAYEYFVQVNUIXFVCZYCYEPQZVCXRYFXFUUAC
      AXLCVROZECVSUKZXPAVROFAVSUKZVTWAYTUBXFUPQCCUPQZXFUPQZXRXFWBUUEUBXFUPCUUCW
      CWDUUBUUBXFVROZUUFXRUSUUCUUCXMUUGXQXFVSUKCCXFWEVNWFYCYEYCCUUCWGWHYEAUUDWG
      WHWIWJWKWLUOBXGWMUKBUBWNSZWOXCXDBUAUUHBYBDUAWPUAHYBDUAWQWRWSWTBUBXAXBUK
      $.
      $( [21-Mar-2011] $)

    $d D q r x z $.  $d N q r x z $.  $d R x $.  $d S z $.
    $( Lemma for ~ divalg . $)
    divalglem4 $p |- S = { r e. NN0 | E. q e. ZZ N = ( ( q x. D ) + r ) } $=
      ( vz cv cmul co caddc wceq cz wrex cn0 crab wcel cmin cdivides wbr wa wb
      nn0z zsubcl mpan divides 3syl eqcom cc nn0cn zmulcl mpan2 zcn syl ax-mp
      subadd mp3an1 addcom eqeq1d bitrd syl2an syl6bb syl5bbr rexbidva pm5.32i
      opreq2 breq2d elrab2 eqeq2d rexbidv elrab 3bitr4i eqriv ) JBCEKZALMZDKZNM
      ZOZEPQZDRSZJKZRTZACWDUAMZUBUCZUDWECVRWDNMZOZEPQZUDWDBTWDWCTWEWGWJWEWGVRWF
      OZEPQZWJWEWDPTZWFPTZWGWLUEZWDUFCPTZWMWNFCWDUGUHAPTZWNWOGEAWFUIUHUJWEWKWIE
      PWKWFVROZWEVQPTZUDZWIWFVRUKWTWRWHCOZWIWEWDULTZVRULTZWRXAUEWSWDUMWSVRPTZXC
      WSWQXDGVQAUNUOVRUPUQXBXCUDZWRWDVRNMZCOZXACULTZXBXCWRXGUEWPXHFCUPURCWDVRUS
      UTXEXFWHCWDVRVAVBVCVDWHCUKVEVFVGVCVHACVSUAMZUBUCWGDWDRBVSWDOZXIWFAUBVSWDC
      UAVIVJIVKWBWJDWDRXJWAWIEPXJVTWHCVSWDVRNVIVLVMVNVOVP $.
      $( [21-Mar-2011] $)

    divalglem5.5 $e |- R = sup ( S , RR , `' < ) $.
    $( Lemma for ~ divalg . $)
    divalglem5 $p |- ( 0 <_ R /\ R < ( abs ` D ) ) $=
      ( vx cc0 cle wbr cabs cfv clt cn0 wcel cmin co cdivides wa cr ccnv csup
      divalglem2 eqeltri cv wceq opreq2 breq2d crab cbvrabv eqtri elrab2 mpbi
      simpli nn0ge0 ax-mp wn cn cz wne nnabscl mp2an nngt0 wb 0re cc zcn
      abscli ltnle cuz wss ssrab2 eqsstri nn0uz sseqtri nn0abscl nn0sub2
      mp3an1 mpan a1i wi nn0z c1 cmul 1z divalglem0 mpan2 recni mulid2i
      opreq2i breq2i syl6ib syl imp sylanbrc infmssuzle sylancr syl5eqbr
      simpld nn0re lesub mp3an3 syl11anc recnd subid bitrd mpbid mto mpbir
      pm3.2i ) LBMNZBAOPZQNZBRSZXOXRADBTUAZUBNZBCSXRXTUCZBCUDQUEUFZCJACDEFGHIUG
      UHADKUIZTUAZUBNZXTKBRCYCBUJYDXSAUBYCBDTUKULCADEUIZTUAZUBNZERUMZYEKRUMIYHY
      EEKRYFYCUJYGYDAUBYFYCDTUKULUNUOZUPUQZURZBUSUTXQXPBMNZVAZYMXPLMNZLXPQNZYOV
      AZXPVBSZYPAVCSZALVDYRGHAVEVFXPVGUTLUDSXPUDSZYPYQVHVIAYSAVJSGAVKUTVLZLXPVM
      VFUQYMBBXPTUAZMNZYOYMBYBUUBMJYMCLVNPZVOUUBCSZYBUUBMNCRUUDCYIRIYHERVPVQVRV
      SYMUUBRSZADUUBTUAZUBNZUUEXRYMUUFYLXPRSZXRYMUUFYSUUIGAVTUTXPBWAWBWCYMYAUUH
      YAYMYKWDZXRXTUUHXRBVCSZXTUUHWEBWFUUKXTADBWGXPWHUAZTUAZTUAZUBNZUUHUUKWGVCS
      XTUUOWEWIABWGDFGWJWKUUNUUGAUBUUMUUBDTUULXPBTXPXPUUAWLWMWNWNWOWPWQWRWQYEUU
      HKUUBRCYCUUBUJYDUUGAUBYCUUBDTUKULYJUPWSUUBCLWTXAXBYMUUCXPBBTUAZMNZYOYMBUD
      SZUURUUCUUQVHZYMXRUURYMXRXTUUJXCBXDZWQZUVAUURUURYTUUSUUABBXPXEXFXGYMUUPLX
      PMYMBVJSUUPLUJYMBUVAXHBXIWQULXJXKXLUURXQYNVHZXRUURYLUUTUTUURYTUVBUUABXPVM
      WKUTXMXN $.
      $( [21-Mar-2011] $)
  $}

  ${
    divalglem6.1 $e |- A e. NN $.
    divalglem6.2 $e |- X e. ( 0 ... ( A - 1 ) ) $.
    divalglem6.3 $e |- K e. ZZ $.
    $( Lemma for ~ divalg . $)
    divalglem6 $p |- ( K =/= 0 ->
                       -. ( X + ( K x. A ) ) e. ( 0 ... ( A - 1 ) ) ) $=
      ( cc0 wne clt wbr wo cmul co caddc c1 cmin cfz wcel wn zrei 0re lttri2i
      cle cneg cr wb lt0neg1 ax-mp cz 0z znegcl zltp1le mp2an ax1cn addid2i
      breq1i bitri renegcli wa nnrei nnnn0i nn0ge0i lemulge12 an4s mpanl12
      mpan sylbi recni mulneg1i opreq2i remulcli subnegi eqtri suble0 bitr3i
      sylibr w3a cn nnz elfzm11 mpbi simp3i simp1i ltadd1i readdcli ltletri
      syl ltnle sylib simp2bi nsyl simp2i addge02 letri mpan2 lenlt simp3bi
      jaoi ) BGHBGIJZGBIJZKCBALMZNMZGAOPMQMZRZSZBGBFTZUAUBWSXEWTWSGXBUCJZXDWSXB
      GIJZXGSZWSAXANMZGUCJZXHWSABUDZALMZUCJZXKWSOXLUCJZXNWSGXLIJZXOBUERZWSXPUFX
      FBUGUHXPGONMZXLUCJZXOGUIRZXLUIRZXPXSUFUJBUIRZYAFBUKUHGXLULUMXROXLUCOUNUOZ
      UPUQUQXLUERZXOXNBXFURZAUERZGAUCJZYDXOUSXNADUTZAADVAVBZYFYDYGXOXNAXLVCVDVE
      VFVGXKAXMPMZGUCJZXNYJXJGUCYJAXAUDZPMXJXMYLAPBABXFVHAYHVHZVIVJAXAYMXABAXFY
      HVKZVHVLVMUPYFXMUERYKXNUFYHXLAYEYHVKAXMVNUMVOVPXBXJIJZXKXHCAIJZYOCUIRZGCU
      CJZYPCXCRZYQYRYPVQZEXTAUIRZYSYTUFUJAVRRUUADAVSUHZCGAVTUMWAZWBCAXACYQYRYPU
      UCWCTZYHYNWDWAXBXJGCXAUUDYNWEZAXAYHYNWEUAWFVFWGXBUERZGUERXHXIUFUUEUAXBGWH
      UMWIXDXBUIRZXGXBAIJZXTUUAXDUUGXGUUHVQUFUJUUBXBGAVTUMZWJWKWTUUHXDWTAXBUCJZ
      UUHSZWTAXAUCJZUUJWTOBUCJZUULWTXRBUCJZUUMXTYBWTUUNUFUJFGBULUMXROBUCYCUPUQY
      GUUMUULYIYFXQYGUUMUSUULYHXFABVCVEVFVGUULXAXBUCJZUUJYRUUOYQYRYPUUCWLXAUERC
      UERYRUUOUFYNUUDXACWMUMWAAXAXBYHYNUUEWNWOWGYFUUFUUJUUKUFYHUUEAXBWPUMWIXDUU
      GXGUUHUUIWQWKWRVG $.
      $( [21-Mar-2011] $)
  $}

  ${
    divalglem7.1 $e |- D e. ZZ $.
    divalglem7.2 $e |- D =/= 0 $.
    $( Lemma for ~ divalg . $)
    divalglem7 $p |- ( ( X e. ( 0 ... ( ( abs ` D ) - 1 ) ) /\ K e. ZZ ) ->
                       ( K =/= 0 -> -. ( X + ( K x. ( abs ` D ) ) ) e.
                                       ( 0 ... ( ( abs ` D ) - 1 ) ) ) ) $=
      ( cc0 cabs cfv c1 cmin co cfz wcel cz wne cmul caddc wn wi cif wceq
      opreq1 eleq1d notbid imbi2d neeq1 opreq2d imbi12d wa cn pm3.2i nnabscl
      ax-mp cle wbr clt 0z 0re leidi nngt0i w3a wb nnz elfzm11 mp2an biimpri
      mp3an elimel divalglem6 dedth2h ) CFAGHZIJKLKZMZBNMZBFOZCBVKPKZQKZVLMZRZS
      VOVMCFTZVPQKZVLMZRZSVNBFTZFOZVTWDVKPKZQKZVLMZRZSCBFFCVTUAZVSWCVOWJVRWBWJV
      QWAVLCVTVPQUBUCUDUEBWDUAZVOWEWCWIBWDFUFWKWBWHWKWAWGVLWKVPWFVTQBWDVKPUBUGU
      CUDUHVKWDVTANMZAFOZUIVKUJMZWLWMDEUKAULUMZCFVLFNMZFFUNUOZFVKUPUOZFVLMZUQFU
      RUSVKWOUTWSWPWQWRVAZWPVKNMZWSWTVBUQWNXAWOVKVCUMFFVKVDVEVFVGVHBFNUQVHVIVJ
      $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d D q r x z $.  $d N q r x z $.  $d S x z $.  $d X z $.  $d Y z $.
    divalglem8.1 $e |- N e. ZZ $.
    divalglem8.2 $e |- D e. ZZ $.
    divalglem8.3 $e |- D =/= 0 $.
    divalglem8.4 $e |- S = { r e. NN0 | D || ( N - r ) } $.
    $( Lemma for ~ divalg . $)
    divalglem8 $p |-
        ( ( ( X e. S /\ Y e. S ) /\ ( X < ( abs ` D ) /\ Y < ( abs ` D ) ) ) ->
          ( K e. ZZ -> ( ( K x. ( abs ` D ) ) = ( Y - X ) -> X = Y ) ) ) $=
      ( vz wcel cabs cfv clt wbr cz cmul co cmin wceq wi wa w3a cc0 caddc wb
      cc cn0 cv cdivides crab ssrab2 eqsstri nn0sscn sstri sseli cn wne
      nnabscl mp2an nnz ax-mp zmulcl mpan2 zcn syl subadd syl3an 3com12 eqcom
      bibi12i sylib 3adant1r 3adant2r wn c1 cfz breq1 eleq1 imbi12d cle elnn0z
      anim1i df-3an sylibr 0z elfzm11 ex rgen vtoclri biimpd sylan9 com23 imp
      3ad2ant2 divalglem7 sylan 3adant2 con2d syld df-ne con2bii syl6ibr
      sylbid opreq1 mul02i syl6eq eqeq1d biimpac subeq0 syl2an ancoms syl5ib
      adantr an4s 3adant3 exp3a mpdd 3expia ) EBMZEANOZPQZFBMZFYCPQZCRMZCYCSTZF
      EUATZUBZEFUBZUCZUCYBYDUDZYEYFUDZYGYLYMYNYGUEZYJCUFUBZYKYOYJFEYHUGTZUBZYPY
      MYEYGYJYRUHZYFYBYEYGYSYDYBYEYGUEYIYHUBZYQFUBZUHZYSYEYBYGUUBYEFUIMZYBEUIMZ
      YGYHUIMZUUBBUIFBUJUIBADGUKUATULQZGUJUMUJKUUFGUJUNUOZUPUQZURZBUIEUUHURZYGY
      HRMZUUEYGYCRMZUUKYCUSMZUULARMAUFUTUUMIJAVAVBYCVCVDZCYCVEVFYHVGVHFEYHVIVJV
      KYTYJUUAYRYIYHVLYQFVLVMVNVOVPYOYRCUFUTZVQZYPYOYRYQUFYCVRUATVSTZMZUUPYNYMY
      RUURUCZYGYEYFUUSYEYRYFUURYEYRYFUURUCYEYFFUUQMZYRUURLUKZYCPQZUVAUUQMZUCZYF
      UUTUCLFBUVAFUBUVBYFUVCUUTUVAFYCPVTUVAFUUQWAWBUVDLBUVABMZUVBUVCUVEUVBUDZUV
      ARMZUFUVAWCQZUVBUEZUVCUVFUVGUVHUDZUVBUDUVIUVEUVJUVBUVEUVAUJMUVJBUJUVAUUGU
      RUVAWDVNWEUVGUVHUVBWFWGUFRMUULUVCUVIUHWHUUNUVAUFYCWIVBWGWJWKZWLYRUUTUURFY
      QUUQWAWMWNWJWOWPWQYOUUOUURYMYGUUOUURVQUCZYNYMEUUQMZYGUVLYBYDUVMUVDYDUVMUC
      LEBUVAEUBUVBYDUVCUVMUVAEYCPVTUVAEUUQWAWBUVKWLWPACEIJWRWSWTXAXBUUOYPCUFXCX
      DXEXFYOYJYPYKYMYNYJYPUDZYKUCZYGYBYEYDYFUVOYBYEUDZUVOYDYFUDUVNUFYIUBZUVPYK
      YPYJUVQYPYHUFYIYPYHUFYCSTUFCUFYCSXGYCUULYCUIMUUNYCVGVDXHXIXJXKUVPYIUFUBZF
      EUBZUHZUVQYKUHYEYBUVTYEUUCUUDUVTYBUUIUUJFEXLXMXNUVRUVQUVSYKYIUFVLFEVLVMVN
      XOXPXQXRXSXTYAXQ $.
      $( [21-Mar-2011] $)

    ${
      $d D k r x y $.  $d R x $.  $d S k x y $.
      divalglem9.5 $e |- R = sup ( S , RR , `' < ) $.
      $( Lemma for ~ divalg . $)
      divalglem9 $p |- E! x e. S x < ( abs ` D ) $=
        ( vy vk cv cabs cfv clt wbr wreu wrex wa wceq wi wral breq1 reu4 wcel
        cr ccnv csup divalglem2 eqeltri cc0 cle divalglem5 simpri rcla4ev
        mp2an cmul co cmin cz cdivides cn0 opreq2 breq2d elrab2 simprbi
        anim12i simplbi nn0z syl zsubcl mpan syl2an dvds2sub mp3an1 mpd cc zcn
        caddc zrei recni subid ax-mp opreq1i 0cn subsub2 syl5eq sub4 mpanl12
        subcl ancoms addid2 3eqtr3d mpbid wb absdvdsb sylancr cn wne nnabscl
        nnz divides adantr divalglem8 r19.23adv ex rgen2 mpbir2an ) ANZBOPZQRZA
        DSXMADTZXMLNZXLQRZUAZXKXOUBZUCZLDUDADUDXMXPALDXKXOXLQUEUFCDUGCXLQRZXNCD
        UHQUIUJDKBDEFGHIJUKULUMCUNRXTBCDEFGHIJKUOUPXMXTACDXKCXLQUEUQURXSALDDXKD
        UGZXODUGZUAZXQXRYCXQUAZMNZXLUSUTXOXKVAUTZUBZMVBTZXRYCYHXQYCXLYFVCRZYHYC
        BYFVCRZYIYCBEXKVAUTZEXOVAUTZVAUTZVCRZYJYCBYKVCRZBYLVCRZUAZYNYAYOYBYPYAX
        KVDUGZYOBEFNZVAUTZVCRZYOFXKVDDYSXKUBYTYKBVCYSXKEVAVEVFJVGZVHYBXOVDUGZYP
        UUAYPFXOVDDYSXOUBYTYLBVCYSXOEVAVEVFJVGZVHVIYCYKVBUGZYLVBUGZUAZYQYNUCZYA
        XKVBUGZXOVBUGZUUGYBYAYRUUIYAYRYOUUBVJXKVKVLZYBUUCUUJYBUUCYPUUDVJXOVKVLZ
        UUIUUEUUJUUFEVBUGZUUIUUEGEXKVMVNUUMUUJUUFGEXOVMVNVIVOBVBUGZUUEUUFUUHHBY
        KYLVPVQVLVRYCYMYFBVCYAUUIUUJYMYFUBZYBUUKUULUUIXKVSUGZXOVSUGZUUOUUJXKVTX
        OVTUUPUUQUAZEEVAUTZXKXOVAUTZVAUTZUMYFWAUTZYMYFUURUVAUMUUTVAUTZUVBUUSUMU
        UTVAEVSUGZUUSUMUBEEGWBWCZEWDWEWFUMVSUGUUPUUQUVCUVBUBWGUMXKXOWHVQWIUVDUV
        DUURUVAYMUBUVEUVEEEXKXOWJWKUURYFVSUGZUVBYFUBUUQUUPUVFXOXKWLWMYFWNVLWOVO
        VOVFWPYAUUIUUJYJYIWQZYBUUKUULUUIUUJUAZUUNYFVBUGZUVGHUUJUUIUVIXOXKVMWMZB
        YFWRWSVOWPYAUUIUUJYIYHWQZYBUUKUULUVHXLVBUGZUVIUVKXLWTUGZUVLUUNBUMXAUVMH
        IBXBURXLXCWEUVJMXLYFXDWSVOWPXEYDYGXRMVBBDYEEXKXOFGHIJXFXGVRXHXIXJ $.
        $( [21-Mar-2011] $)
    $}

    $( Lemma for ~ divalg . $)
    divalglem10 $p |- E! r e. ZZ E. q e. ZZ
                   ( 0 <_ r /\ r < ( abs ` D ) /\ N = ( ( q x. D ) + r ) ) $=
      ( vx cc0 cv cle wbr cabs cfv clt cmul co caddc wceq w3a cz wrex wreu cr
      ccnv csup eqid divalglem9 wcel wa weu cn0 opreq2 eqeq2d rexbidv
      divalglem4 elrab2 anbi2i ancom anass 3bitr4i elnn0z an12 3bitri anbi1i
      df-3an rexbii r19.42v bitri bitr4i eubii df-reu mpbi breq2 breq1
      3anbi123d cbvreuv ) KJLZMNZVTAOPZQNZCELARSZVTTSZUAZUBZEUCUDZJUCUEZKDLZMNZ
      WJWBQNZCWDWJTSZUAZUBZEUCUDZDUCUEWCJBUEZWIJABUFQUGUHZBCDFGHIWRUIUJVTBUKZWC
      ULZJUMVTUCUKZWHULZJUMWQWIWTXBJWTXAWAWCULZWFEUCUDZULZULZXBWTWCVTUNUKZULZXD
      ULZXAXCULZXDULXFWCWSULWCXGXDULZULWTXIWSXKWCWNEUCUDXDDVTUNBWJVTUAZWNWFEUCX
      LWMWECWJVTWDTUOUPUQABCDEFGHIURUSUTWSWCVAWCXGXDVBVCXHXJXDXHWCXAWAULZULXAWC
      WAULZULXJXGXMWCVTVDUTWCXAWAVEXNXCXAWCWAVAUTVFVGXAXCXDVBVFWHXEXAWHXCWFULZE
      UCUDXEWGXOEUCWAWCWFVHVIXCWFEUCVJVKUTVLVMWCJBVNWHJUCVNVCVOWHWPJDUCVTWJUAZW
      GWOEUCXPWAWKWCWLWFWNVTWJKMVPVTWJWBQVQXPWEWMCVTWJWDTUOUPVRUQVSVO $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d D q r $.  $d N q r $.
    $( The division algorithm (theorem).  Dividing an integer ` N ` by a
       nonzero integer ` D ` produces a (unique) quotient ` q ` and a unique
       remainder ` 0 <_ r < ( abs `` D ) ` .  The proof does not use ` / ` ,
       ` |_ ` or ` mod ` .  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    divalg $p |- ( ( N e. ZZ /\ D e. ZZ /\ D =/= 0 ) -> E! r e. ZZ E. q e. ZZ
                   ( 0 <_ r /\ r < ( abs ` D ) /\ N = ( ( q x. D ) + r ) ) ) $=
      ( cz wcel cc0 wne cv cle wbr cabs cfv clt cmul co caddc wceq w3a wrex
      wreu wa c1 cif eqeq1 3anbi3d rexbidv reubidv fveq2 breq2d opreq2 opreq1d
      eqeq2d 3anbi23d cmin cdivides cn0 crab 1z elimel simpl eleq1 elimdhyp
      simpr neeq1 ax1ne0 eqid divalglem10 dedth2h 3impb ) BEFZAEFZAGHZGCIZJKZVN
      ALMZNKZBDIZAOPZVNQPZRZSZDETZCEUAZVKVLVMUBZWDVOVQVKBUCUDZVTRZSZDETZCEUAVOV
      NWEAUCUDZLMZNKZWFVRWJOPZVNQPZRZSZDETZCEUABAUCUCBWFRZWCWICEWRWBWHDEWRWAWGV
      OVQBWFVTUEUFUGUHAWJRZWIWQCEWSWHWPDEWSVQWLWGWOVOWSVPWKVNNAWJLUIUJWSVTWNWFW
      SVSWMVNQAWJVROUKULUMUNUGUHWJWJWFVNUOPUPKCUQURZWFCDBUCEUSUTWEVLWJEFUCEFAUC
      VLVMVAAWJEVBUCWJEVBUSVCWEVMWJGHUCGHAUCVLVMVDAWJGVEUCWJGVEVFVCWTVGVHVIVJ
      $.
      $( [21-Mar-2011] $)

    $( Express the division algorithm as stated in ~ divalg in terms of
       ` || ` .  (Contributed by Paul Chapman, 31-Mar-2011.) $)
    divalgb $p |- ( ( N e. ZZ /\ D e. ZZ /\ D =/= 0 ) ->
                    ( E! r e. ZZ E. q e. ZZ ( 0 <_ r /\ r < ( abs ` D ) /\
                                              N = ( ( q x. D ) + r ) ) <->
                      E! r e. NN0 ( r < ( abs ` D ) /\ D || ( N - r ) ) ) ) $=
      ( cz wcel cc0 cv cle wbr cabs cfv clt cmul co caddc wceq w3a wrex wreu
      cmin cdivides wa cn0 wb wne zsubcl divides sylan2 3impb 3com12 ax-17 wi
      cc zcn zmulcl syl subadd syl3an addcom syl2an 3adant1 eqeq1d bitrd eqcom
      bibi12i sylib 3expia exp3a com23 3impia imp rexbida 3com23 anbi2d df-3an
      rexbii r19.42v bitri syl6rbbr anass syl6bb 3expa reubidva weu elnn0z
      anbi1i eubii df-reu 3bitr4ri 3adant3 ) BEFZAEFZGCHZIJZWNAKLMJZBDHZANOZWNP
      OZQZRZDESZCETZWPABWNUAOZUBJZUCZCUDTZUEAGUFWLWMUCZXCWOXFUCZCETZXGXHXBXICEW
      LWMWNEFZXBXIUEWLWMXKRZXBWOWPUCZXEUCZXIXLXNXMWTDESZUCZXBXLXEXOXMXLXEWRXDQZ
      DESZXOWMWLXKXEXRUEZWMWLXKXSWLXKUCZWMXDEFXSBWNUGDAXDUHUIUJUKWLXKWMXRXOUEWL
      XKWMRZXQWTDEYADULYAWQEFZXQWTUEZWLXKWMYBYCUMXTYBWMYCXTYBWMYCWLXKYBWMUCZYCW
      LXKYDRZXDWRQZWSBQZUEYCYEYFWNWRPOZBQZYGWLBUNFXKWNUNFZYDWRUNFZYFYIUEBUOWNUO
      ZYDWREFYKWQAUPWRUOUQZBWNWRURUSYEYHWSBXKYDYHWSQZWLXKYJYKYNYDYLYMWNWRUTVAVB
      VCVDYFXQYGWTXDWRVEWSBVEVFVGVHVIVJVKVLVMVNVDVOXBXMWTUCZDESXPXAYODEWOWPWTVP
      VQXMWTDEVRVSVTWOWPXEWAWBWCWDWNUDFZXFUCZCWEXKXIUCZCWEXGXJYQYRCYQXKWOUCZXFU
      CYRYPYSXFWNWFWGXKWOXFWAVSWHXFCUDWIXICEWIWJWBWK $.
      $( [31-Mar-2011] $)

    $( The division algorithm (theorem) for a positive divisor.  (Contributed
       by Paul Chapman, 21-Mar-2011.) $)
    divalg2 $p |- ( ( N e. ZZ /\ D e. NN ) ->
                    E! r e. NN0 ( r < D /\ D || ( N - r ) ) ) $=
      ( vq cz wcel cn wa cv cabs cfv clt wbr cmin co cdivides cn0 wreu cc0 wne
      nnz nnne0 jca w3a cle cmul caddc wceq wrex divalg divalgb mpbid 3expb
      sylan2 wb cr nnre nnnn0 nn0ge0 syl absid syl11anc breq2d anbi1d reubidv
      adantl ) BEFZAGFZHCIZAJKZLMZABVINOPMZHZCQRZVIALMZVLHZCQRZVHVGAEFZASTZHVNV
      HVRVSAUAAUBUCVGVRVSVNVGVRVSUDSVIUEMVKBDIAUFOVIUGOUHUDDEUICERVNABCDUJABCDU
      KULUMUNVHVNVQUOVGVHVMVPCQVHVKVOVLVHVJAVILVHAUPFSAUEMZVJAUHAUQVHAQFVTAURAU
      SUTAVAVBVCVDVEVFUL $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d D r z $.  $d N r z $.
    $( The result of the ` mod ` operator satisfies the requirements for the
       remainder ` r ` in the division algorithm for a positive divisor
       (compare ~ divalg2 and ~ divalgb ).  This demonstration theorem
       justifies the use of ` mod ` to yield an explicit remainder from this
       point forward.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
    divalgmod $p |- ( ( N e. ZZ /\ D e. NN ) -> ( r = ( N mod D ) <->
                      ( r e. NN0 /\ ( r < D /\ D || ( N - r ) ) ) ) ) $=
      ( vz cv cmo co wceq csn wcel cz cn wa cn0 clt wbr cmin cdivides elsn
      crab cuni wreu wb zmodcl divalg2 breq1 opreq2 breq2d anbi12d reuuni2
      syl11anc cr crp zre nnrp modlt syl2an cdiv cfl cfv cmul cc0 wne nnre
      nnne0 redivcl syl3an 3anidm23 flcl syl nnz adantl simpl nn0z zsubcl cc
      recnd zcn mulcom modval adantr zmulcl an12s anabsan2 nn0cn subsub23
      syl111anc eqcom bibi12i sylib mpbid eqtr3d dvds0lem syl31anc mpbi2and
      eqcomd sneqd reuunisn eqtr4d eleq2d elrab syl6bb syl5bbr ) CEZBAFGZHXDXEI
      ZJZBKJZALJZMZXDNJXDAOPZABXDQGZRPZMZMZCXESXJXGXDDEZAOPZABXPQGZRPZMZDNTZJXO
      XJXFYAXDXJXFYAUAZIZYAXJXEYBXJYBXEXJXEAOPZABXEQGZRPZYBXEHZXJXENJZXTDNUBZYD
      YFMZYGUCBAUDZABDUEZXTYJDNXEXPXEHZXQYDXSYFXPXEAOUFYMXRYEARXPXEBQUGUHUIUJUK
      XHBULJZAUMJZYDXIBUNZAUOZBAUPUQXJBAURGZUSUTZKJZAKJZYEKJZYSAVAGZYEHYFXJYRUL
      JZYTXHXIUUDXHYNXIAULJXIAVBVCUUDYPAVDZAVEBAVFVGVHYRVIVJZXIUUAXHAVKZVLXJXHX
      EKJZUUBXHXIVMXJYHUUHYKXEVNVJBXEVOUKXJAYSVAGZUUCYEXJAVPJZYSVPJZUUIUUCHXIUU
      JXHXIAUUEVQVLXJYTUUKUUFYSVRVJAYSVSUKXJXEBUUIQGZHZUUIYEHZXHYNYOUUMXIYPYQBA
      VTUQXJUULXEHZYEUUIHZUCZUUMUUNUCXJBVPJZUUIVPJZXEVPJZUUQXHUURXIXHBYPVQWAXJU
      UIKJZUUSXHXIUVAXIXHXIUVAXIUUAYTUVAXJUUGUUFAYSWBUQWCWDUUIVRVJXJYHUUTYKXEWE
      VJBUUIXEWFWGUUOUUMUUPUUNUULXEWHYEUUIWHWIWJWKWLYSAYEWMWNWOWPWQXJYIYAYCHYLX
      TDNWRVJWSWTXTXNDXDNXPXDHZXQXKXSXMXPXDAOUFUVBXRXLARXPXDBQUGUHUIXAXBXC $.
      $( [31-Mar-2011] $)
  $}

  ${
    $d D r x $.  $d K r $.  $d N r x $.
    $( Corollary of the division algorithm.  If an integer ` D ` greater than
       ` 1 ` divides ` N ` , then it does not divide any of ` N - 1 ` ,
       ` N - 2 ` ... ` N - ( D - 1 ) ` .  (Contributed by Paul Chapman,
       31-Mar-2011.) $)
    ndvdssub $p |- ( ( N e. ZZ /\ D e. NN /\ ( K e. NN /\ K < D ) ) ->
                     ( D || N -> -. D || ( N - K ) ) ) $=
      ( vr vx cz wcel cn clt wbr wa cdivides cmin co wn wi w3a cn0 cc0 wne
      nnnn0 nnne0 jca wceq df-ne anbi2i cv wral wrex wreu divalg2 breq1 opreq2
      breq2d anbi12d reu4 sylib simprd nngt0 3ad2ant2 cc zcn subid1 syl
      biimpar 3adant2 3expa anim2i ancoms 0nn0 anbi2d eqeq2 imbi12d rcla4v
      ax-mp syl5 exp3a ralimi r19.21v pm2.43i 3impia eqeq1 syl5com pm4.14
      syl6ib syl7bi exp4a com23 imp4a syl7 imp3a 3expia ) CFGZAHGZBHGZBAIJZKZAC
      LJZACBMNZLJZOZPWMWNKZWRWQXAWMWNWRWQXAPWMWNWRQZWOWPXAXCWPWOXAWOBRGZBSTZKXC
      WPXAWOXDXEBUABUBUCXCWPXDXEXAXCXDWPXEXAPXCXDWPXEXAWPXEKWPBSUDZOZKZXCXDXAXE
      XGWPBSUEUFXCXDWPWTKZXFPZXHXAPXCDUGZAIJZACXKMNZLJZKZXKSUDZPZDRUHZXDXJWMWNW
      RXRXBWRXRPXBXBWRXRXBXBWRKZXQPZDRUHZXSXRPXBXOEUGZAIJZACYBMNZLJZKZKZXKYBUDZ
      PZERUHZDRUHZYAXBXODRUIZYKXBXODRUJYLYKKACDUKXOYFDERYHXLYCXNYEXKYBAIULYHXMY
      DALXKYBCMUMUNUOUPUQURYJXTDRYJXSXOXPXSXOKXOSAIJZACSMNZLJZKZKZYJXPXOXSYQXSY
      PXOWMWNWRYPXCYMYOWNWMYMWRAUSUTWMWRYOWNWMYOWRWMYNCALWMCVAGYNCUDCVBCVCVDUNV
      EVFUCVGVHVISRGYJYQXPPZPVJYIYRESRYBSUDZYGYQYHXPYSYFYPXOYSYCYMYEYOYBSAIULYS
      YDYNALYBSCMUMUNUOVKYBSXKVLVMVNVOVPVQVRVDXSXQDRVSUQVQVTWAXQXJDBRXKBUDZXOXI
      XPXFYTXLWPXNWTXKBAIULYTXMWSALXKBCMUMUNUOXKBSWBVMVNWCWPWTXFWDWEWFWGWHWIWJW
      HWKWLWHWA $.
      $( [31-Mar-2011] $)
  $}

  $( Corollary of the division algorithm.  If an integer ` D ` greater than
     ` 1 ` divides ` N ` , then it does not divide any of ` N + 1 ` ,
     ` N + 2 ` ... ` N + ( D - 1 ) ` .  (Contributed by Paul Chapman,
     31-Mar-2011.) $)
  ndvdsadd $p |- ( ( N e. ZZ /\ D e. NN /\ ( K e. NN /\ K < D ) ) ->
                   ( D || N -> -. D || ( N + K ) ) ) $=
    ( cz wcel cn clt wbr wa w3a cdivides cmin co wn caddc wi cc0 wb cr nnre
    posdif syl2an ancoms pm5.32i nnz zsubcl elnnz biimpri sylan sylbi anasss
    nngt0 ancli ltsubpos biimpd expcom imp3a syl5 imp adantrr jca 3adant1
    ndvdssub syld3an3 zaddcl sylan2 dvdssubr an12s 3impb cc wceq zcn nncn
    subsub3 syl3an breq2d bitr4d notbid 3adant3r sylibrd ) CDEZAFEZBFEZBAGHZIZJ
    ACKHZACABLMZLMZKHZNZACBOMZKHZNZWAWBWEWGFEZWGAGHZIZWFWJPWBWEWPWAWBWEIWNWOWBW
    CWDWNWBWCIZWDIWQQWGGHZIWNWQWDWRWCWBWDWRRZWCBSEZASEZWSWBBTZATZBAUAUBUCUDWQWG
    DEZWRWNWBADEZBDEZXDWCAUEZBUEZABUFUBWNXDWRIWGUGUHUIUJUKWBWCWOWDWBWCWOWCWCQBG
    HZIWBWOWCXIBULUMWBWCXIWOWCWBXIWOPWCWBIXIWOWCWTXAXIWORWBXBXCBAUNUBUOUPUQURUS
    UTVAVBAWGCVCVDWAWBWCWMWJRWDWAWBWCJZWLWIXJWLAWKALMZKHZWIWAWBWCWLXLRZWBWAWCXM
    WBXEWKDEZXMWAWCIXGWCWAXFXNXHCBVEVFAWKVGUBVHVIXJWHXKAKWACVJEWBAVJEWCBVJEWHXK
    VKCVLAVMBVMCABVNVOVPVQVRVSVT $.
    $( [31-Mar-2011] $)

  $( Special case of ~ ndvdsadd .  If an integer ` D ` greater than ` 1 `
     divides ` N ` , it does not divide ` N + 1 ` .  (Contributed by Paul
     Chapman, 31-Mar-2011.) $)
  ndvdsp1 $p |- ( ( N e. ZZ /\ D e. NN /\ 1 < D ) ->
                  ( D || N -> -. D || ( N + 1 ) ) ) $=
    ( c1 clt wbr cz wcel cn wa cdivides caddc co wn wi 1nn jctl ndvdsadd
    syl3an3 ) CADEZBFGAHGCHGZSIABJEABCKLJEMNSTOPACBQR $.
    $( [31-Mar-2011] $)

