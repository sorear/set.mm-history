$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Minimizing Vector Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    minvec1.u $e |- U e. CPreHil $.
    minvec1.w1 $e |- W e. ( SubSp ` U ) $.
    $( Lemma for ~ minvecex . $)
    minveclem1 $p |- W e. NrmCVec $=
      ( cnv wcel css cfv phnvi eqid sspnv mp2an ) AEFBAGHZFBEFACIDAMBMJKL $.
      $( [4-Mar-2008] $)

    minvec2.y $e |- Y = ( BaseSet ` W ) $.
    $( Lemma for ~ minvecex . $)
    minveclem2 $p |- ( 0v ` W ) e. Y $=
      ( cnv wcel cn0v cfv minveclem1 eqid nvzcl ax-mp ) BGHBIJZCHABDEKBCOFOLMN
      $.
      $( [4-Mar-2008] $)

    minvec3.x $e |- X = ( BaseSet ` U ) $.
    $( Lemma for ~ minvecex . $)
    minveclem3 $p |- Y C_ X $=
      ( cnv wcel css cfv wss phnvi eqid sspba mp2an ) AIJBAKLZJDCMAENFARBCDHGRO
      PQ $.
      $( [29-Feb-2008] $)

    $( Lemma for ~ minvecex . $)
    minveclem4 $p |- ( ( f : NN --> Y /\ n e. NN ) -> ( f ` n ) e. X ) $=
      ( cn cv wf wcel wa cfv ffvelrn minveclem3 sseli syl ) KFBLZMCLZKNOUBUAPZF
      NUCENKFUBUAQFEUCADEFGHIJRST $.
      $( [5-Mar-2008] $)

    minvec5.m $e |- M = ( -v ` U ) $.
    minvec5.n $e |- N = ( norm ` U ) $.
    minvec5.a $e |- A e. X $.
    $( Lemma for ~ minvecex . $)
    minveclem5 $p |- ( y e. Y -> ( N ` ( A M y ) ) e. RR ) $=
      ( cv wcel co cfv cr minveclem3 sseli cnv phnvi nvmcl mp3an12 syl nvcl
      mpan ) APZHQZBUJDRZGQZULESTQZUKUJGQZUMHGUJCFGHIJKLUAUBCUCQZBGQUOUMCIUDZOB
      UJCDGLMUEUFUGUPUMUNUQULCEGLNUHUIUG $.
      $( [4-Mar-2008] $)
  $}

  ${
    $d j A $.  $d j H $.  $d j M $.  $d f j m $.
    minvec6.h $e |- ( j e. NN -> ( H ` j ) = ( A M ( f ` j ) ) ) $.
    $( Lemma for ~ minvecex . $)
    minveclem6 $p |- ( m e. NN -> ( H ` m ) = ( A M ( f ` m ) ) ) $=
      ( cv cfv co wceq cn fveq2 opreq2d eqeq12d vtoclga ) CHZEIZAQBHZIZFJZKDHZE
      IZAUBSIZFJZKCUBLQUBKZRUCUAUEQUBEMUFTUDAFQUBSMNOGP $.
      $( [5-Mar-2008] $)
  $}

  ${
    $d j F $.  $d j H $.  $d j N $.  $d j n $.
    minvec7.f $e |- ( j e. NN -> ( F ` j ) = ( N ` ( H ` j ) ) ) $.
    $( Lemma for ~ minvecex . $)
    minveclem7 $p |- ( n e. NN -> ( F ` n ) = ( N ` ( H ` n ) ) ) $=
      ( cv cfv wceq cn fveq2 fveq2d eqeq12d vtoclga ) AGZCHZODHZEHZIBGZCHZSDHZE
      HZIASJOSIZPTRUBOSCKUCQUAEOSDKLMFN $.
      $( [6-Mar-2008] $)
  $}

  ${
    $d j A $.  $d j F $.  $d j M $.  $d j N $.  $d f j n $.
    minvec8.hf $e |- ( j e. NN -> ( F ` j ) = ( N ` ( A M ( f ` j ) ) ) ) $.
    $( Lemma for ~ minvecex . $)
    minveclem8 $p |- ( n e. NN -> ( F ` n ) = ( N ` ( A M ( f ` n ) ) ) ) $=
      ( cv cfv co wceq cn fveq2 opreq2d fveq2d eqeq12d vtoclga ) CIZEJZASBIZJZF
      KZGJZLDIZEJZAUEUAJZFKZGJZLCUEMSUELZTUFUDUISUEENUJUCUHGUJUBUGAFSUEUANOPQHR
      $.
      $( [16-Feb-2008] $)
  $}

  ${
    $d k m u D $.  $d k m u J $.  $d k u M $.  $d k u N $.  $d k m u Y $.
    $d a f k m u $.
    minvec9.u $e |- U e. CPreHil $.
    minvec9.m $e |- M = ( -v ` U ) $.
    minvec9.n $e |- N = ( norm ` U ) $.
    minvec9.x $e |- X = ( BaseSet ` U ) $.
    minvec9.w1 $e |- W e. ( SubSp ` U ) $.
    minvec9.y $e |- Y = ( BaseSet ` W ) $.
    minvec9.d $e |- D = ( IndMet ` W ) $.
    minvec9.j $e |- J e. _V $.
    minvec9.k $e |- ( m e. NN -> ( J ` m ) = ( N ` ( ( f ` m ) M a ) ) ) $.
    $( Lemma for ~ minvecex . $)
    minveclem9 $p |- ( ( ( f : NN --> Y /\ f ( ~~>m ` D ) a ) /\ a e. Y ) ->
                J ~~> 0 ) $=
      ( vk vu cn cv wf clm cfv wbr wa wcel cc0 cli cle co cabs clt wi wral
      wrex crp cme cnv css phnvi eqid sspnv mp2an imsmet ax-mp imsbai eqidd
      lmcvgnns mpanl1 an1s anassrs adantlll wb wceq cims sspimsval mpanl12
      imsdval mp3an1 cr nvmcl nvcl mpan syl nvge0 absid syl11anc eqtr4d
      minveclem3 sseli syl2an eqtrd ffvelrn sylan an1rs breq1d imbi2d ralbidva
      rexbidv adantlr adantr mpbid r19.21aiva cc recnd minveclem4 clm0nnsi
      mpbird ) UCJCUDZUEZXMKUDZAUFUGUHZUIXOJUJZUIZEUKULUHZUAUDDUDZUMUHZXTXMUGZX
      OFUNZGUGZUOUGZUBUDZUPUHZUQZDUCURZUAUCUSZUBUTURZXRYJUBUTXRYFUTUJZUIYAYBXOA
      UNZYFUPUHZUQZDUCURZUAUCUSZYJXPXQYLYQXNXPXQYLYQXQXPYLYQAVAUJZXQXPYLUIYQHVB
      UJZYRBVBUJZHBVCUGZUJZYSBLVDZPBUUAHUUAVEZVFVGZAHRVHVIYBJAXOYFUADXMJAHJQRUU
      EVJXTUCUJZYBVKVLVMVNVOVPXRYQYJVQZYLXNXQUUGXPXNXQUIZYPYIUAUCUUHYOYHDUCUUHU
      UFUIZYNYGYAUUIYMYEYFUPXNUUFXQYMYEVRZYBJUJZXQUUJXNUUFUIZUUKXQUIZYMYBXOBVSU
      GZUNZYEYTUUBUUMYMUUOVRUUCPYBXOAUUNBUUAHJQUUNVEZRUUDVTWAYBIUJZXOIUJZUUOYEV
      RUUKXQUUQUURUIZUUOYDYEYTUUQUURUUOYDVRUUCYBXOUUNBFGIOMNUUPWBWCUUSYDWDUJZUK
      YDUMUHZYEYDVRUUSYCIUJZUUTYTUUQUURUVBUUCYBXOBFIOMWEWCZYTUVBUUTUUCYCBGIONWF
      WGWHZUUSUVBUVAUVCYTUVBUVAUUCYCBGIONWIWGWHYDWJWKWLJIYBBHIJLPQOWMZWNJIXOUVE
      WNZWOWPUCJXTXMWQWRWSWTXAXBXCXDXEXFXGXNXQXSYKVQZXPUUHYDXHUJZDUCURUVGUUHUVH
      DUCXNUUFXQUVHUUQUURUVHUULXQUUSYDUVDXIBCDHIJLPQOXJUVFWOWSXGUBYDUADESTXKWHX
      DXL $.
      $( [24-Feb-2008] $)
  $}

  ${
    $d f j k m t u x y A $.  $d k m n x y B $.  $d a k m D $.
    $d a j k m n u F $.  $d x y G $.  $d j H $.  $d f j k m n t u x y M $.
    $d f j k m n u t x y N $.  $d a f j k m n t u P $.  $d a t u R $.
    $d x y S $.  $d t u x y U $.  $d t u x y W $.  $d a f j k m n t u x y Y $.
    minvec10.1 $e |- R = { x | E. y e. Y x = -u ( N ` ( A M y ) ) } $.
    minvec10.u $e |- U e. CPreHil $.
    minvec10.m $e |- M = ( -v ` U ) $.
    minvec10.n $e |- N = ( norm ` U ) $.
    minvec10.x $e |- X = ( BaseSet ` U ) $.
    minvec10.w1 $e |- W e. ( SubSp ` U ) $.
    minvec10.y $e |- Y = ( BaseSet ` W ) $.
    minvec10.a $e |- A e. X $.
    $( Lemma for ~ minvecex .  The set of reals ` R ` is bounded above. $)
    minveclem10 $p |- E. u e. RR A. t e. R t <_ u $=
      ( cc0 cr wcel cv cle wbr wral wrex 0re co cfv cneg wceq visset eqeq1
      rexbidv elab2 breq1 minveclem3 sseli cnv phnvi nvmcl mp3an12 syl nvge0
      mpan wb nvcl le0neg2 mpbid syl5cbir r19.23aiv sylbi rgen breq2 ralbidv
      rcla4ev mp2an ) UAUBUCDUDZUAUEUFZDFUGZVTCUDZUEUFZDFUGZCUBUHUIWADFVTFUCVTE
      BUDZHUJZIUKZULZUMZBLUHZWAAUDZWIUMZBLUHWKAVTFDUNWLVTUMWMWJBLWLVTWIUOUPMUQW
      JWABLWJWAWIUAUEUFZWFLUCZVTWIUAUEURWOUAWHUEUFZWNWOWGKUCZWPWOWFKUCZWQLKWFGJ
      KLNRSQUSUTGVAUCZEKUCWRWQGNVBZTEWFGHKQOVCVDVEZWSWQWPWTWGGIKQPVFVGVEWOWHUBU
      CZWPWNVHWOWQXBXAWSWQXBWTWGGIKQPVIVGVEWHVJVEVKVLVMVNVOWEWBCUAUBWCUAUMWDWAD
      FWCUAVTUEVPVQVRVS $.
      $( [10-Feb-2008] $)

    $( Lemma for ~ minvecex . $)
    minveclem11 $p |- sup ( R , RR , < ) e. RR $=
      ( vu vt cv co cfv cn0v minveclem5 minveclem2 minveclem10 infcvgaux1i
      suprclii ) STDABSTCBUAFUBGUCDJHUDUCKBCEFGHIJLPQOMNRUEEHJLPQUFABSTCDEFGHIJ
      KLMNOPQRUGUHUI $.
      $( [6-Mar-2008] $)

    ${
      minvec12.2 $e |- P = -u sup ( R , RR , < ) $.
      $( Lemma for ~ minvecex . $)
      minveclem12 $p |- P e. RR $=
        ( cr clt csup cneg minveclem11 renegcli eqeltri ) DEUAUBUCZUDUATUHABCEF
        GHIJKLMNOPQRSUEUFUG $.
        $( [3-Mar-2008] $)

      $( Lemma for ~ minvecex . $)
      minveclem13 $p |- ( B e. Y -> P <_ ( N ` ( A M B ) ) ) $=
        ( vu vt cv co cfv cn0v minveclem5 minveclem2 minveclem10 wceq opreq2
        fveq2d infcvgaux2i ) ABUBUCCBUDZHUEZIUFCDHUEZIUFDFELJUGUFMBCGHIJKLNRSQO
        PTUHGJLNRSUIABUBUCCFGHIJKLMNOPQRSTUJUAUODUKUPUQIUODCHULUMUN $.
        $( [9-Feb-2008] $)

      $( Lemma for ~ minvecex . $)
      minveclem14 $p |- 0 <_ P $=
        ( va vu vt cc0 cr clt csup cneg cle wbr wcel cv wral 0re co cfv wceq
        wrex visset eqeq1 rexbidv elab2 wa simpr minveclem3 sseli cnv phnvi
        nvmcl mp3an12 nvge0 mpan wb nvcl le0neg2 syl mpbid adantr eqbrtrd
        r19.23aiva sylbi rgen cn0v minveclem5 minveclem2 minveclem10
        infcvgaux1i suprleubii mp2an minveclem11 le0neg1 ax-mp mpbi breqtrri )
        UDEUEUFUGZUHZDUIWOUDUIUJZUDWPUIUJZUDUEUKUAULZUDUIUJZUAEUMWQUNWTUAEWSEUK
        WSCBULZGUOZHUPZUHZUQZBKURZWTAULZXDUQZBKURXFAWSEUAUSXGWSUQXHXEBKXGWSXDUT
        VALVBXEWTBKXAKUKZXEVCWSXDUDUIXIXEVDXIXDUDUIUJZXEXIXAJUKZXJKJXAFIJKMQRPV
        EVFXKXBJUKZXJFVGUKZCJUKXKXLFMVHZSCXAFGJPNVIVJXLUDXCUIUJZXJXMXLXOXNXBFHJ
        POVKVLXLXCUEUKZXOXJVMXMXLXPXNXBFHJPOVNVLXCVOVPVQVPVPVRVSVTWAWBUBUCUAEUD
        ABUBUCXCEKIWCUPLBCFGHIJKMQRPNOSWDFIKMQRWEABUBUCCEFGHIJKLMNOPQRSWFWGWHWI
        WOUEUKWQWRVMABCEFGHIJKLMNOPQRSWJWOWKWLWMTWN $.
        $( [17-Feb-2008] $)

      $( Lemma for ~ minvecex . $)
      minveclem15 $p |- ( ( F ~~> P /\ B e. RR+ ) ->
       E. k e. NN A. n e. NN ( k <_ n -> ( abs ` ( ( F ` n ) - P ) ) < B ) ) $=
        ( cr wcel cli wbr crp cv cle cfv cmin co cabs clt wi cn wral wrex
        minveclem12 c1 1z cuz nnuz eqimss2i nnssz clmi2rpi mp3an1 ) EUEUFJEUGUH
        DUIUFHUJIUJZUKUHVJJULEUMUNUOULDUPUHUQIURUSHURUTABCEFGKLMNOPQRSTUAUBUCUD
        VAEDUEHIJVBURURVCURVBVDULVEVFVGVHVI $.
        $( [21-Feb-2008] $)

      minvec16.g $e |- G = ( +v ` U ) $.
      minvec16.s $e |- S = ( .s ` U ) $.
      $( ??? reuse f'nGf'm antecedent computation in mnvc3lem3 $)
      $( Lemma for ~ minvecex . $)
      minveclem16 $p |- ( ( ( f : NN --> Y /\ n e. NN ) /\ ( f : NN --> Y /\
   m e. NN ) ) -> ( 2 x. P ) <_
         ( N ` ( 2 S ( A M ( ( 1 / 2 ) S ( ( f ` n ) G ( f ` m ) ) ) ) ) ) ) $=
        ( cv cfv wcel c2 cmul co c1 cdiv cle wbr cn wf wa cpv cnv css wceq
        phnvi eqid sspgval mpanl12 minveclem1 nvgcl mp3an1 eqeltrrd cns cc 2cn
        2ne0 reccli sspsval mpan nvscl mp3an12 cr minveclem3 sseli nvmcl syl
        nvcl minveclem13 minveclem12 cc0 2re 0re 2pos ltleii pm3.2i lemul2a
        mp3anl3 mpanl1 syl11anc nvsge0 breqtrrd ffvelrn syl2an ) JUHZHUHZUIZPUJ
        ZIUHZXEUIZPUJZUKDULUMZUKCUNUKUOUMZXFXIKUMZFUMZLUMZFUMMUIZUPUQZURPXEUSZX
        DURUJUTXRXHURUJUTXGXJUTZXMPUJZXQXSXFXINVAUIZUMZXMPGVBUJZNGVCUIZUJZXSYBX
        MVDGRVEZUBXFXIGYAKYDNPUCUFYAVFZYDVFZVGVHNVBUJZXGXJYBPUJGNRUBVIZXFXINYAP
        UCYGVJVKVLXTXKUKXOMUIZULUMZXPUPXTXNPUJZXKYLUPUQZXTXLXMNVMUIZUMZXNPXLVNU
        JZXTYPXNVDZUKVOVPVQZYCYEYQXTUTYRYFUBXLXMYOFGYDNPUCUGYOVFZYHVRVHVSYIYQXT
        YPPUJYJYSXLXMYONPUCYTVTWAVLYMYKWBUJZDYKUPUQZYNYMXOOUJZUUAYMXNOUJZUUCPOX
        NGNOPRUBUCUAWCZWDYCCOUJUUDUUCYFUDCXNGLOUASWEWAZWFYCUUCUUAYFXOGMOUATWGVS
        WFABCXNDEGLMNOPQRSTUAUBUCUDUEWHDWBUJZUUAUUBYNABCDEGLMNOPQRSTUAUBUCUDUEW
        IUUGUUAUKWBUJZWJUKUPUQZUTZUUBYNUUHUUIWKWJUKWLWKWMWNZWODYKUKWPWQWRWSWFXT
        XMOUJZXPYLVDZPOXMUUEWDUULUUCUUMUULUUDUUCYCYQUULUUDYFYSXLXMFGOUAUGVTWAUU
        FWFUUHUUIUUCUUMWKUUKYCUUJUUCUUMYFUKXOFGMOUAUGTWTVKVHWFWFXAWFURPXDXEXBUR
        PXHXEXBXC $.
        $( [6-Mar-2008] $)
    $}

    ${
      minvec17.h $e |- ( j e. NN -> ( H ` j ) = ( A M ( f ` j ) ) ) $.
      $( Lemma for ~ minvecex . $)
      minveclem17 $p |- ( ( f : NN --> Y /\ n e. NN ) -> ( H ` n ) e. X ) $=
        ( cn cv wf wcel wa cfv co wceq minveclem6 adantl ffvelrn minveclem3
        sseli cnv phnvi nvmcl mp3an12 syl eqeltrd ) UDNFUEZUFZHUEZUDUGZUHZVEIUI
        ZCVEVCUIZJUJZMVFVHVJUKVDCFGHIJUCULUMVGVINUGZVJMUGZUDNVEVCUNVKVIMUGZVLNM
        VIELMNPTUASUOUPEUQUGCMUGVMVLEPURUBCVIEJMSQUSUTVAVAVB $.
        $( [4-Mar-2008] $)

      minvec18.g $e |- G = ( +v ` U ) $.
      $( Lemma for ~ minvecex . $)
      minveclem18 $p |- ( ( ( f : NN --> Y /\ n e. NN ) /\ ( f : NN --> Y /\
   m e. NN ) ) -> ( ( 2 x. ( ( ( N ` ( H ` n ) ) ^ 2 ) + ( ( N ` ( H ` m ) ) ^
   2 ) ) ) - ( ( N ` ( ( H ` n ) G ( H ` m ) ) ) ^ 2 ) ) =
      ( ( N ` ( ( f ` n ) M ( f ` m ) ) ) ^ 2 ) ) $=
        ( cn cv wf wcel wa c2 cfv cexp co caddc cmul cmin wceq cphl phpar2
        mp3an1 cc wb addcl cnv cr phnvi nvcl mpan recnd sqcl syl syl2an 2cn
        mulcl nvgcl nvmcl subadd syl111anc mpbird minveclem17 minveclem6
        opreqan12d ad2ant2l w3a nvnnncan1 minveclem4 eqtrd fveq2d nvsub eqtr4d
        opreq1d ) UGPFUHZUIZIUHZUGUJZUKZWOHUHZUGUJZUKZUKZULWPKUMZMUMZULUNUOZWSK
        UMZMUMZULUNUOZUPUOZUQUOZXCXFJUOZMUMZULUNUOZURUOZXCXFLUOZMUMZULUNUOZWPWN
        UMZWSWNUMZLUOMUMZULUNUOXCOUJZXFOUJZXNXQUSZWRXAYAYBUKZYCXMXQUPUOXJUSZEUT
        UJYAYBYERXCXFEJLMOUAUFSTVAVBYDXJVCUJZXMVCUJZXQVCUJZYCYEVDYDXIVCUJZYFXEV
        CUJZXHVCUJZYIYAYBXEXHVEYAXDVCUJYJYAXDEVFUJZYAXDVGUJERVHZXCEMOUATVIVJVKX
        DVLVMYBXGVCUJYKYBXGYLYBXGVGUJYMXFEMOUATVIVJVKXGVLVMVNULVCUJYIYFVOULXIVP
        VJVMYDXLVCUJYGYDXLYDXKOUJZXLVGUJZYLYAYBYNYMXCXFEJOUAUFVQVBYLYNYOYMXKEMO
        UATVIVJVMVKXLVLVMYDXPVCUJYHYDXPYDXOOUJZXPVGUJZYLYAYBYPYMXCXFELOUASVRVBY
        LYPYQYMXOEMOUATVIVJVMVKXPVLVMXJXMXQVSVTWAABCDEFGIKLMNOPQRSTUAUBUCUDUEWB
        ABCDEFGHKLMNOPQRSTUAUBUCUDUEWBVNXBXPXTULUNXBXPXSXRLUOZMUMZXTXBXOYRMXBXO
        CXRLUOZCXSLUOZLUOZYRWQWTXOUUBUSWOWOWQWTXCYTXFUUALCFGIKLUEWCCFGHKLUEWCWD
        WEXROUJZXSOUJZUUBYRUSZWRXACOUJZUUCUUDUUEUDYLUUFUUCUUDWFUUEYMCXRXSELOUAS
        WGVJVBEFINOPRUBUCUAWHZEFHNOPRUBUCUAWHZVNWIWJUUCUUDXTYSUSZWRXAYLUUCUUDUU
        IYMXRXSELMOUASTWKVBUUGUUHVNWLWMWI $.
        $( [9-Mar-2008] $)

      minvec19.s $e |- S = ( .s ` U ) $.
      minvec19.2 $e |- P = -u sup ( R , RR , < ) $.
      $( Lemma for ~ minvecex . $)
      minveclem19 $p |- ( ( ( f : NN --> Y /\ n e. NN ) /\ ( f : NN --> Y /\
            m e. NN ) ) -> ( 2 x. P ) <_
          ( N ` ( ( H ` n ) G ( H ` m ) ) ) ) $=
        ( cn cv wf wcel wa c2 cmul co c1 cdiv cfv cle minveclem16 wceq
        minveclem6 opreqan12d ad2ant2l cnv phnvi pm3.2i nvaddsub4 mp3an12
        minveclem4 syl2an eqtr4d nv2 mp2an opreq1i syl6eq nvgcl mp3an1 cc 2cn
        2ne0 reccli nvscl w3a nvmdi mpan syl nvsass nvsid recidi syl5eq eqtr3d
        opreq2d eqtr2d fveq2d breqtrd ) UKRHULZUMZKULZUKUNZUOZXAJULZUKUNZUOZUOZ
        UPDUQURUPCUSUPUTURZXBWTVAZXEWTVAZLURZFURZNURFURZOVAXBMVAZXEMVAZLURZOVAV
        BABCDEFGHJKLNOPQRSTUAUBUCUDUEUFUJUHUIVCXHXNXQOXHXQUPCFURZXLNURZXNXHXQCC
        LURZXLNURZXSXHXQCXJNURZCXKNURZLURZYAXCXFXQYDVDXAXAXCXFXOYBXPYCLCHIKMNUG
        VECHIJMNUGVEVFVGXJQUNZXKQUNZYAYDVDZXDXGGVHUNZCQUNZYIUOYEYFUOZYGGTVIZYIY
        IUFUFVJCCXJXKGLNQUCUHUAVKVLGHKPQRTUDUEUCVMZGHJPQRTUDUEUCVMZVNVOXTXRXLNY
        HYIXTXRVDYKUFCFGLQUCUHUIVPVQVRVSYEYFXSXNVDZXDXGYJXLQUNZYNYHYEYFYOYKXJXK
        GLQUCUHVTWAYOXNXRUPXMFURZNURZXSYOXMQUNZXNYQVDZYHXIWBUNZYOYRYKUPWCWDWEZX
        IXLFGQUCUIWFVLUPWBUNZYIYRYSWCUFYHUUBYIYRWGYSYKUPCXMFGNQUCUAUIWHWIVLWJYO
        YPXLXRNYOUPXIUQURZXLFURZYPXLUUBYTYOUUDYPVDZWCUUAYHUUBYTYOWGUUEYKUPXIXLF
        GQUCUIWKWIVLYOUSXLFURZXLUUDYHYOUUFXLVDYKXLFGQUCUIWLWIUUCUSXLFUPWCWDWMVR
        WNWOWPWQWJYLYMVNWQWRWS $.
        $( [17-Feb-2008] $)
    $}

    ${
      minvec20.h $e |- ( j e. NN -> ( H ` j ) = ( A M ( f ` j ) ) ) $.
      minvec20.f $e |- ( j e. NN -> ( F ` j ) = ( N ` ( H ` j ) ) ) $.
      $( Lemma for ~ minvecex . $)
      minveclem20 $p |- ( ( f : NN --> Y /\ n e. NN ) -> ( F ` n ) e. RR ) $=
        ( cn cv wf wcel wa cfv co cr wceq minveclem7 minveclem6 fveq2d eqtrd
        adantl minveclem4 cnv phnvi nvmcl mp3an12 nvcl mpan syl eqeltrd ) UFOFU
        GZUHZHUGZUFUIZUJZVKIUKZCVKVIUKZKULZLUKZUMVLVNVQUNVJVLVNVKJUKZLUKVQGHIJL
        UEUOVLVRVPLCFGHJKUDUPUQURUSVMVONUIZVQUMUIZEFHMNOQUAUBTUTVSVPNUIZVTEVAUI
        ZCNUIVSWAEQVBZUCCVOEKNTRVCVDWBWAVTWCVPELNTSVEVFVGVGVH $.
        $( [17-Feb-2008] $)

      minvec21.2 $e |- P = -u sup ( R , RR , < ) $.
      $( Lemma for ~ minvecex . $)
      minveclem21 $p |- ( ( ( f : NN --> Y /\ n e. NN ) /\ ( f : NN --> Y /\
        m e. NN ) ) -> ( ( N ` ( ( f ` n ) M ( f ` m ) ) ) ^ 2 ) <_ ( ( 2 x.
        ( ( ( F ` n ) ^ 2 ) + ( ( F ` m ) ^ 2 ) ) ) - ( ( 2 x. P ) ^ 2 ) ) ) $=
        ( cn cv wf wcel wa cfv co c2 cexp caddc cmul cpv cmin cle wceq
        minveclem7 opreq1d opreqan12d opreq2d ad2ant2l eqid minveclem18 eqtr2d
        wbr cr cnv phnvi nvgcl mp3an1 nvcl mpan syl minveclem17 syl2an cns
        minveclem19 cc0 2re minveclem12 remulcli 0re 2pos ltleii minveclem14
        mulge0i mp2an le2sq2 mpanl12 syl11anc wb resqcl readdcl remulcl
        minveclem20 resqcli lesub2 mpbid eqbrtrd ) UIQGUJZUKZJUJZUIULZUMZXHIUJZ
        UIULZUMZUMZXIXGUNXLXGUNMUONUNUPUQUOZUPXIKUNZUPUQUOZXLKUNZUPUQUOZURUOZUS
        UOZXILUNZXLLUNZFUTUNZUOZNUNZUPUQUOZVAUOZYBUPDUSUOZUPUQUOZVAUOZVBXOYIUPY
        CNUNZUPUQUOZYDNUNZUPUQUOZURUOZUSUOZYHVAUOZXPXJXMYIYSVCXHXHXJXMUMZYBYRYH
        VAYTYAYQUPUSXJXMXRYNXTYPURXJXQYMUPUQHJKLNUGVDVEXMXSYOUPUQHIKLNUGVDVEVFV
        GVEVHABCEFGHIJYELMNOPQRSTUAUBUCUDUEUFYEVIZVJVKXOYKYHVBVLZYIYLVBVLZXOYGV
        MULZYJYGVBVLZUUBYCPULZYDPULZUUDXKXNUUFUUGUMZYFPULZUUDFVNULZUUFUUGUUIFSV
        OZYCYDFYEPUBUUAVPVQUUJUUIUUDUUKYFFNPUBUAVRVSVTZABCEFGHJLMNOPQRSTUAUBUCU
        DUEUFWAZABCEFGHILMNOPQRSTUAUBUCUDUEUFWAZWBABCDEFWCUNZFGHIJYELMNOPQRSTUA
        UBUCUDUEUFUUAUUOVIUHWDYJVMULWEYJVBVLZUUDUUEUMUUBUPDWFABCDEFMNOPQRSTUAUB
        UCUDUEUHWGZWHZWEUPVBVLWEDVBVLUUPWEUPWIWFWJWKABCDEFMNOPQRSTUAUBUCUDUEUHW
        LUPDWFUUQWMWNYJYGWOWPWQXOYHVMULZYBVMULZUUBUUCWRZUUFUUGUUSXKXNUUHUUDUUSU
        ULYGWSVTUUMUUNWBXQVMULZXSVMULZUUTXKXNUVBUVCUMYAVMULZUUTXRVMULXTVMULUVDU
        VBUVCXRXTWTXQWSXSWSWBUPVMULUVDUUTWFUPYAXAVSVTABCEFGHJKLMNOPQRSTUAUBUCUD
        UEUFUGXBABCEFGHIKLMNOPQRSTUAUBUCUDUEUFUGXBWBYKVMULUUSUUTUVAYJUURXCYKYHY
        BXDVQWQXEXF $.
        $( [7-Mar-2008] $)
    $}

    ${
      minvec22.hf $e |- ( j e. NN -> ( F ` j ) = ( N ` ( A M ( f ` j ) ) ) ) $.
      $( Lemma for ~ minvecex . $)
      minveclem22 $p |- ( ( f : NN --> Y /\ n e. NN ) -> ( F ` n ) e. RR ) $=
        ( cn cv wf wcel wa cfv co cr wceq minveclem8 adantl minveclem4 cnv
        phnvi nvmcl mp3an12 nvcl mpan syl eqeltrd ) UDNFUEZUFZHUEZUDUGZUHZVFIUI
        ZCVFVDUIZJUJZKUIZUKVGVIVLULVECFGHIJKUCUMUNVHVJMUGZVLUKUGZEFHLMNPTUASUOV
        MVKMUGZVNEUPUGZCMUGVMVOEPUQZUBCVJEJMSQURUSVPVOVNVQVKEKMSRUTVAVBVBVC $.
        $( [17-Feb-2008] $)

      minvec23.2 $e |- P = -u sup ( R , RR , < ) $.
      $( Lemma for ~ minvecex .  Eliminate ` H ` . $)
      minveclem23 $p |- ( ( ( f : NN --> Y /\ n e. NN ) /\ ( f : NN --> Y /\
        m e. NN ) ) -> ( ( N ` ( ( f ` n ) M ( f ` m ) ) ) ^ 2 ) <_ ( ( 2 x.
        ( ( ( F ` n ) ^ 2 ) + ( ( F ` m ) ^ 2 ) ) ) - ( ( 2 x. P ) ^ 2 ) ) ) $=
        ( vk vu cv cn wcel cfv co wceq wa copab fveq2 opreq2d eqid oprex
        fvopab4 fveq2d eqcomd eqtrd minveclem21 ) ABCDEFGHIJKUGUIZUJUKUHUICVFGU
        IZULZLUMZUNUOUGUHUPZLMNOPQRSTUAUBUCUDUGUHHUIZVICVKVGULZLUMZUJVJVFVKUNVH
        VLCLVFVKVGUQURVJUSCVLLUTVAZVKUJUKZVKKULVMMULZVKVJULZMULZUEVOVRVPVOVQVMM
        VNVBVCVDUFVE $.
        $( [8-Mar-2008] $)

      $( Use F - P g.e. 0 instead of abs for lt2sqt?  check hyp chaining $)
      $( Lemma for ~ minvecex . $)
      minveclem24 $p |- ( ( f : NN --> Y /\ F ~~> P /\ B e. RR+ ) ->
         E. k e. NN A. n e. NN ( k <_ n -> ( ( ( F ` n ) - P ) ^ 2 ) < B ) ) $=
        ( cn cv wf cli wbr crp wcel w3a cle cfv cmin co cabs csqr clt wi wral
        wrex c2 cexp minveclem15 rpsqrcl sylan2 3adant1 wa wb cr cc0 lt2sq cc
        recn abscl syl absge0 jca adantr sqrcl sqrge0 syl2an absresq breq1d
        ad2antrr wceq sqsqr adantl breq2d 3bitrd minveclem22 minveclem12
        resubcl mpan2 ancri rpre rpge0 an1rs 3adantl2 imbi2d ralbidva rexbidv
        mpbid ) UHQHUIUJZLEUKULZDUMUNZUOZJUIKUIZUPULZXLLUQZEURUSZUTUQZDVAUQZVBU
        LZVCZKUHVDZJUHVEZXMXOVFVGUSZDVBULZVCZKUHVDZJUHVEXIXJYAXHXIXQUMUNYAXJABC
        XQEFGJKLMNOPQRSTUAUBUCUDUEUGVHDVIVJVKXKXTYEJUHXKXSYDKUHXKXLUHUNZVLXRYCX
        MXHXJYFXRYCVMZXIXHYFXJYGXOVNUNZXHYFVLZVLZDVNUNZVODUPULZVLZYGYIXJYJYMVLZ
        XRXPVFVGUSZXQVFVGUSZVBULZYBYPVBULZYCXPVNUNZVOXPUPULZVLZXQVNUNZVOXQUPULZ
        VLXRYQVMYJYMXPXQVPYHUUAYIYHYSYTYHXOVQUNZYSXOVRZXOVSVTYHUUDYTUUEXOWAVTWB
        WCYMUUBUUCDWDDWEWBWFYHYQYRVMYIYMYHYOYBYPVBXOWGWHWIYNYPDYBVBYMYPDWJYJDWK
        WLWMWNYIYHYIXNVNUNZYHABCFGHIKLMNOPQRSTUAUBUCUDUEUFWOUUFEVNUNYHABCEFGMNO
        PQRSTUAUBUCUDUEUGWPXNEWQWRVTWSXJYKYLDWTDXAWBWFXBXCXDXEXFXG $.
        $( [21-Feb-2008] $)

      $d x y P $.
      $( Lemma for ~ minvecex . $)
      minveclem25 $p |- ( ( f : NN --> Y /\ F ~~> P /\ B e. RR+ ) ->
          E. k e. NN A. n e. NN
                ( k <_ n -> ( ( 2 x. P ) x. ( ( F ` n ) - P ) ) < B ) ) $=
        ( cn cv wf cli wbr crp wcel w3a cle cfv cmin co cabs c2 cmul c1 caddc
        cdiv clt wi wral wrex minveclem15 2re minveclem12 remulcli 1re
        readdcli cc0 0re 2pos ltleii minveclem14 mulge0i mp2an lt01 addgegt0i
        elrpii rpdivcl mpan2 sylan2 3adant1 wa cr wb ltmuldiv2OLD mp3an1 cc
        resubcl recnd abscl syl rpre syl2an leabs ltp1i absge0 pm3.2i lemul12b
        mpanl12 syl12anc mpani mpd adantr remulcl mpan adantl lelttr syl111anc
        mpand sylbird minveclem22 sylan an1rs 3adantl2 imim2d ralimdva
        reximdv ) UHQHUIUJZLEUKULZDUMUNZUOZJUIKUIZUPULZYJLUQZEURUSZUTUQZDVAEVBU
        SZVCVDUSZVEUSZVFULZVGZKUHVHZJUHVIZYKYOYMVBUSZDVFULZVGZKUHVHZJUHVIYGYHUU
        AYFYGYQUMUNZUUAYHABCYQEFGJKLMNOPQRSTUAUBUCUDUEUGVJYHYPUMUNUUFYPYOVCVAEV
        KABCEFGMNOPQRSTUAUBUCUDUEUGVLZVMZVNVOZVPYOUPULZVPVCVFULVPYPVFULZVPVAUPU
        LVPEUPULUUJVPVAVQVKVRVSABCEFGMNOPQRSTUAUBUCUDUEUGVTVAEVKUUGWAWBZWCYOVCU
        UHVNWDWBZWEDYPWFWGWHWIYIYTUUEJUHYIYSUUDKUHYIYJUHUNZWJYRUUCYKYFYHUUNYRUU
        CVGZYGYFUUNYHUUOYLWKUNZYHUUOYFUUNWJUUPYHWJZYRYPYNVBUSZDVFULZUUCYNWKUNZD
        WKUNZUUSYRWLZUUPYHYPWKUNZUUTUVAUVBUUIUVCUUTUVAUOUUKUVBUUMYPYNDWMWGWNUUP
        YMWOUNZUUTUUPYMUUPEWKUNYMWKUNZUUGYLEWPWGZWQZYMWRWSZDWTZXAUUQUUBUURUPULZ
        UUSUUCUUPUVJYHUUPYMYNUPULZUVJUUPUVEUVKUVFYMXBWSUUPYOYPUPULZUVKUVJYOYPUU
        HUUIYOUUHXCVSUUPUVEUUTVPYNUPULZUVLUVKWJUVJVGZUVFUVHUUPUVDUVMUVGYMXDWSYO
        WKUNZUUJWJUVCUVEUUTUVMWJWJUVNUVOUUJUUHUULXEUUIYOYPYMYNXFXGXHXIXJXKUUQUU
        BWKUNZUURWKUNZUVAUVJUUSWJUUCVGUUPUVPYHUUPUVEUVPUVFUVOUVEUVPUUHYOYMXLXMW
        SXKUUPUVQYHUUPUUTUVQUVHUVCUUTUVQUUIYPYNXLXMWSXKYHUVAUUPUVIXNUUBUURDXOXP
        XQXRABCFGHIKLMNOPQRSTUAUBUCUDUEUFXSXTYAYBYCYDYEXJ $.
        $( [21-Feb-2008] $)

      $( ?? r19.22dv/r19.20dva could be combined w mnvc3lemm3OLD ? $)
      $( Lemma for ~ minvecex . $)
      minveclem26 $p |- ( ( f : NN --> Y /\ F ~~> P /\ B e. RR+ ) -> E. k e. NN
            A. n e. NN ( k <_ n -> ( ( ( F ` n ) ^ 2 ) - ( P ^ 2 ) ) < B ) ) $=
        ( cn cv wf cli wbr crp wcel w3a cle cfv cmin co c2 cexp cdiv clt cmul
        wa wi wral wrex minveclem24 minveclem25 jca c1 1z nnuz cvganuzi sylib
        2re 2pos elrpii rpdivcl mpan2 syl3an3 cr caddc lt2add minveclem12
        resubcl resqcl syl remulcli remulcl mpan rehalfcl syl2an cc wceq recn
        recni subsq2 2halves eqcomd breqan12d sylibrd minveclem22 rpre an1rs
        3adantl2 imim2d ralimdva reximdv mpd ) UHQHUIUJZLEUKULZDUMUNZUOZJUIKUI
        ZUPULZXPLUQZEURUSZUTVAUSZDUTVBUSZVCULZUTEVDUSZXSVDUSZYAVCULZVEZVFZKUHVG
        ZJUHVHZXQXRUTVAUSEUTVAUSURUSZDVCULZVFZKUHVGZJUHVHXLXMYAUMUNZYIXNXLXMYNU
        OZXQYBVFKUHVGJUHVHZXQYEVFKUHVGJUHVHZVEYIYOYPYQABCYAEFGHIJKLMNOPQRSTUAUB
        UCUDUEUFUGVIABCYAEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGVJVKYBYEJKVLUHVMVNVOVPXN
        UTUMUNYNUTVQVRVSDUTVTWAWBXOYHYMJUHXOYGYLKUHXOXPUHUNZVEYFYKXQXLXNYRYFYKV
        FZXMXLYRXNYSXRWCUNZDWCUNZYSXLYRVEXNYTUUAVEYFXTYDWDUSZYAYAWDUSZVCULZYKXT
        WCUNZYDWCUNZVEYAWCUNZUUGVEYFUUDVFYTUUAXTYDYAYAWEYTUUEUUFYTXSWCUNZUUEYTE
        WCUNUUHABCEFGMNOPQRSTUAUBUCUDUEUGWFZXREWGWAZXSWHWIYTUUHUUFUUJYCWCUNUUHU
        UFUTEVQUUIWJYCXSWKWLWIVKUUAUUGUUGDWMZUUKVKWNYTUUAYJUUBDUUCVCYTXRWOUNZYJ
        UUBWPZXRWQUULEWOUNUUMEUUIWRXREWSWAWIUUAUUCDUUADWOUNUUCDWPDWQDWTWIXAXBXC
        ABCFGHIKLMNOPQRSTUAUBUCUDUEUFXDDXEWNXFXGXHXIXJXK $.
        $( [23-Feb-2008] $)

      $( Lemma for ~ minvecex . $)
      minveclem27 $p |- ( ( ( f : NN --> Y /\ F ~~> P ) /\ B e. RR+ ) ->
         E. k e. NN A. m e. NN ( k <_ m ->
               ( ( N ` ( ( f ` k ) M ( f ` m ) ) ) ^ 2 ) < B ) ) $=
        ( cn cv wf cli wbr wa crp wcel cle cfv c2 cexp co cmin cdiv clt wi
        wral wrex minveclem26 2re 2pos elrpii rpdivcl mpan2 syl syl3an3 3expa
        cr nnre leid wceq breq2 fveq2 opreq1d breq1d imbi12d rcla4v mpid
        adantl ancrd r19.28av syl6 pm3.43 ax-1 sylan caddc cmul lt2add resqcl
        minveclem12 resqcli resubcl anim12i rpre rehalfcl jca syl2an cc recni
        addsub4 mpanr12 recnd 2timesi opreq2i syl5req rpcn halfcl 2halves
        breqan12d wb w3a cc0 ltmul2OLD mp3an3 readdcl remulcli addcl 2cn subdi
        mp3an13 sqmuli c4 sq2 2t2e4 eqtr4i opreq1i mulassi 3eqtrri syl6eq wne
        2ne0 divcan2 mp3an23 3bitrd sylibd minveclem22 anandis an1rs
        minveclem23 adantlr cnv phnvi nvmcl mp3an1 minveclem4 nvcl mpan
        remulcl ad2antlr lelttr syl111anc mpand syld anassrs syl9r ralimdva
        adantllr reximdva mpd ) UHQHUIZUJZLEUKULZUMDUNUOZUMZJUIZKUIZUPULZUVNLUQ
        ZURUSUTZEURUSUTZVAUTZDURVBUTZURVBUTZVCULZVDZKUHVEZJUHVFZUVOUVMUVHUQZUVN
        UVHUQZMUTZNUQZURUSUTZDVCULZVDZKUHVEZJUHVFUVIUVJUVKUWEUVIUVJUWAUNUOZUWEU
        VKABCUWAEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGVGUVKUVTUNUOZUWNUVKURUNUOZUWOURVH
        VIVJZDURVKVLUWOUWPUWNUWQUVTURVKVLVMVNVOUVLUWDUWMJUHUVLUVMUHUOZUMZUWDUVM
        LUQZURUSUTZUVRVAUTZUWAVCULZUWCUMZKUHVEZUWMUWSUWDUXCUWDUMUXEUWSUWDUXCUWR
        UWDUXCVDUVLUWRUWDUVMUVMUPULZUXCUWRUVMVPUOUXFUVMVQUVMVRVMUWCUXFUXCVDKUVM
        UHUVNUVMVSZUVOUXFUWBUXCUVNUVMUVMUPVTUXGUVSUXBUWAVCUXGUVQUXAUVRVAUXGUVPU
        WTURUSUVNUVMLWAWBWBWCWDWEWFWGWHUXCUWCKUHWIWJUVIUVKUWRUXEUWMVDUVJUVIUVKU
        MZUWRUMZUXDUWLKUHUXDUVOUXCUWBUMZUXIUVNUHUOZUMUWKUVOUXCVDUWCUVOUXJVDUXCU
        VOUXCUWBWKUXCUVOWLWMUXHUWRUXKUXJUWKVDUXHUWRUXKUMZUMZUXJURUXAUVQWNUTZWOU
        TZUREWOUTZURUSUTZVAUTZDVCULZUWKUVIUXLUVKUXJUXSVDZUWTVPUOZUVPVPUOZUMZUVK
        UXTUVIUXLUMZUYCUVKUMZUXJUXBUVSWNUTZUWAUWAWNUTZVCULZUXSUXBVPUOZUVSVPUOZU
        MUWAVPUOZUYKUMZUXJUYHVDUYCUVKUXBUVSUWAUWAWPUYAUYIUYBUYJUYAUXAVPUOZUYIUW
        TWQZUYMUVRVPUOZUYIEABCEFGMNOPQRSTUAUBUCUDUEUGWRZWSZUXAUVRWTVLVMUYBUVQVP
        UOZUYJUVPWQZUYRUYOUYJUYQUVQUVRWTVLVMXAUVKDVPUOZUYLDXBZUYTUVTVPUOZUYLDXC
        ZVUBUYKUYKUVTXCZVUDXDVMVMXEUYEUYHUXNURUVRWOUTZVAUTZUVTVCULZURVUFWOUTZUR
        UVTWOUTZVCULZUXSUYCUVKUYFVUFUYGUVTVCUYCUXNUVRUVRWNUTZVAUTZUYFVUFUXAXFUO
        ZUVQXFUOZVULUYFVSZUYAUYBVUMVUNUMUVRXFUOZVUPVUOUVRUYQXGZVUQUXAUVQUVRUVRX
        HXIUYAUXAUYNXJZUYBUVQUYSXJZXEVUEVUKUXNVAUVRVUQXKXLXMUVKUVTXFUOZUYGUVTVS
        UVKDXFUOZVUTDXNZDXOVMUVTXPVMXQVUFVPUOZVUBVUGVUJXRZUYCUVKVVCVUBURVPUOZVV
        DVHVVCVUBVVEXSXTURVCULVVDVIVUFUVTURYAVLYBUYCUXNVPUOZVVCUYMUYRVVFUYAUYBU
        XAUVQYCZUYNUYSXEVVFVUEVPUOVVCURUVRVHUYQYDZUXNVUEWTVLVMUVKUYTVUBVUAVUCVM
        XEUYCUVKVUHUXRVUIDVCUYCVUHUXOURVUEWOUTZVAUTZUXRUYCUXNXFUOZVUHVVJVSZVUMV
        UNVVKUYAUYBUXAUVQYEVURVUSXEURXFUOZVVKVUEXFUOVVLYFVUEVVHXGURUXNVUEYGYHVM
        VVIUXQUXOVAUXQURURUSUTZUVRWOUTURURWOUTZUVRWOUTVVIUREYFEUYPXGYIVVNVVOUVR
        WOVVNYJVVOYKYLYMYNURURUVRYFYFVUQYOYPXLYQUVKVVAVUIDVSZVVBVVAVVMURXTYRVVP
        YFYSDURYTUUAVMXQUUBUUCUVIUWRUXKUYCUVIUWRUMZUYAUVIUXKUMZUYBABCFGHIJLMNOP
        QRSTUAUBUCUDUEUFUUDZABCFGHIKLMNOPQRSTUAUBUCUDUEUFUUDZXAUUEWMUUFUXMUWJUX
        RUPULZUXSUWKUVIUXLVWAUVKUVIUWRUXKVWAABCEFGHIKJLMNOPQRSTUAUBUCUDUEUFUGUU
        GUUEUUHUXMUWJVPUOZUXRVPUOZUYTVWAUXSUMUWKVDUVIUXLVWBUVKUYDUWIVPUOZVWBUYD
        UWHPUOZVWDUVIUWRUXKVWEUWFPUOZUWGPUOZVWEVVQVVRGUUIUOZVWFVWGVWEGSUUJZUWFU
        WGGMPUBTUUKUULGHJOPQSUCUDUBUUMGHKOPQSUCUDUBUUMXEUUEVWHVWEVWDVWIUWHGNPUB
        UAUUNUUOVMUWIWQVMUUHUVIUXLVWCUVKUYDUXOVPUOZVWCUYDVVFVWJUVIUWRUXKVVFUYMU
        YRVVFVVQVVRVVGVVQUYAUYMVVSUYNVMVVRUYBUYRVVTUYSVMXEUUEVVEVVFVWJVHURUXNUU
        PUUOVMVWJUXQVPUOVWCUXPUREVHUYPYDWSUXOUXQWTVLVMUUHUVKUYTUVIUXLVUAUUQUWJU
        XRDUURUUSUUTUVAUVBUVCUVDUVEUVAUVFUVG $.
        $( [23-Feb-2008] $)

      minvec28.d $e |- D = ( IndMet ` W ) $.
      $( Lemma for ~ minvecex . $)
      minveclem28 $p |- ( ( ( f : NN --> Y /\ F ~~> P ) /\ B e. RR+ ) ->
         E. k e. NN A. m e. NN ( k <_ m -> ( ( f ` k ) D ( f ` m ) ) < B ) ) $=
        ( cn cv wf cli wbr wa crp wcel cle cfv co c2 cexp clt wi wral wrex
        minveclem27 cn0 2nn0 rpexpcl mpan2 sylan2 wb wceq cnsb cnm cnv css
        phnvi eqid sspnv mp2an imsdval mp3an1 nvmcl sspnval mp3an12 syl
        sspmval mpanl12 fveq2d 3eqtrd adantr breq1d cr cc0 lt2sq nvcl mpan
        nvge0 jca wss sspba sseli syl2an rpre rpge0 bitr2d ffvelrn anim12i
        anandis sylan an1rs adantllr anassrs imbi2d ralbidva rexbidva mpbid )
        UJRIUKZULZMFUMUNZUOZDUPUQZUOZKUKZLUKZURUNZYFXTUSZYGXTUSZNUTZOUSZVAVBUTD
        VAVBUTZVCUNZVDZLUJVEZKUJVFZYHYIYJEUTZDVCUNZVDZLUJVEZKUJVFYCYMUPUQZYQYDA
        BCYMFGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHVGYDVAVHUQUUBVIDVAVJVKVLYEYPUUAKUJYE
        YFUJUQZUOZYOYTLUJUUDYGUJUQZUOYNYSYHYEUUCUUEYNYSVMZYAYDUUCUUEUOZUUFYBYAU
        UGYDUUFYIRUQZYJRUQZUOZYDUUFYAUUGUOUUJYDUOZYSYLDVCUNZYNUUKYRYLDVCUUJYRYL
        VNYDUUJYRYIYJPVOUSZUTZPVPUSZUSZUUNOUSZYLPVQUQZUUHUUIYRUUPVNHVQUQZPHVRUS
        ZUQZUURHTVSZUDHUUTPUUTVTZWAWBZYIYJEPUUMUUORUEUUMVTZUUOVTZUIWCWDUUJUUNRU
        QZUUPUUQVNZUURUUHUUIUVGUVDYIYJPUUMRUEUVEWEWDUUSUVAUVGUVHUVBUDUUNHUUTUUO
        OPRUEUBUVFUVCWFWGWHUUJUUNYKOUUSUVAUUJUUNYKVNUVBUDYIYJHUUTUUMNPRUEUAUVEU
        VCWIWJWKWLWMWNYLWOUQZWPYLURUNZUOZDWOUQZWPDURUNZUOUULYNVMUUJYDYLDWQYIQUQ
        ZYJQUQZUVKUUHUUIUVNUVOUOYKQUQZUVKUUSUVNUVOUVPUVBYIYJHNQUCUAWEWDUVPUVIUV
        JUUSUVPUVIUVBYKHOQUCUBWRWSUUSUVPUVJUVBYKHOQUCUBWTWSXAWHRQYIUUSUVARQXBUV
        BUDHUUTPQRUCUEUVCXCWBZXDRQYJUVQXDXEYDUVLUVMDXFDXGXAXEXHYAUUCUUEUUJYAUUC
        UOUUHYAUUEUOUUIUJRYFXTXIUJRYGXTXIXJXKXLXMXNXOXPXQXRXS $.
        $( [23-Feb-2008] $)

      minvec29.w2 $e |- W e. CBan $.
      $( Lemma for ~ minvecex .  Sequence ` f ` is Cauchy, and since vector
         subspace ` W ` is complete, ` f ` therefore converges to a vector in
         ` W ` . $)
      minveclem29 $p |- ( ( f : NN --> Y /\ F ~~> P ) ->
                E. a e. Y f ( ~~>m ` D ) a ) $=
        ( vk vm cn cv wf cli wbr wa cca cfv wcel clm wrex cle co clt wi wral
        crp minveclem28 r19.21aiva wb cme cnv css phnvi eqid sspnv mp2an
        imsmet ax-mp imsbai iscau5 mpan adantr mpbird cms cbn bncms bnnv
        cmscvg syl ) UKOHULZUMZJEUNUOZUPZWKDUQURUSZWKPULZDUTURUOPOVAZWNWOUIULZU
        JULZVBUOWRWKURWSWKURDVCWPVDUOVEUJUKVFUIUKVAZPVGVFZWNWTPVGABCWPDEFGHIUIU
        JJKLMNOQRSTUAUBUCUDUEUFUGVHVIWLWOXAVJZWMDVKUSZWLXBMVLUSZXCGVLUSMGVMURZU
        SXDGRVNUBGXEMXEVOVPVQZDMUGVRVSPDUIUJWKODMOUCUGXFVTWAWBWCWDDWEUSZWOWQMWF
        USZXGUHDMUGWGVSPDWKODMOUCUGXHXDUHMWHVSVTWIWBWJ $.
        $( [24-Feb-2008] $)
    $}

    ${
      $d m A $.  $d u D $.  $d k m u J $.  $d k m u M $.  $d k m u N $.
      $d m T $.  $d u Y $.  $d f u $.
      minvec30.2 $e |- P = -u sup ( R , RR , < ) $.
      minvec30.hf $e |- ( j e. NN -> ( F ` j ) = ( N ` ( A M ( f ` j ) ) ) ) $.
      minvec30.d $e |- D = ( IndMet ` W ) $.
      minvec30.jv $e |- J e. _V $.
      minvec30.j $e |- ( m e. NN -> ( J ` m ) = ( N ` ( ( f ` m ) M a ) ) ) $.
      minvec30.fv $e |- F e. _V $.
      minvec30.tv $e |- T e. _V $.
      minvec30.t $e |- ( m e. NN -> ( T ` m ) =
                         ( ( F ` m ) + ( N ` ( ( f ` m ) M a ) ) ) ) $.
      $( Lemma for ~ minvecex . $)
      minveclem30 $p |- ( ( ( f : NN --> Y /\ f ( ~~>m ` D ) a ) /\
          ( F ~~> P /\ a e. Y ) ) -> T ~~> P ) $=
        ( cn cv wf clm cfv wbr wa cli wcel cc0 caddc co cc wceq w3a wral
        simprl minveclem9 adantrl minveclem22 recnd adantlr cr adantl cnv
        phnvi nvmcl mp3an1 nvcl mpan syl minveclem4 minveclem3 sseli syl2an
        an1rs eqeltrd adantllr adantlrl opreq2d eqtr4d 3jca r19.21aiva c1 cuz
        cz 1z minveclem12 elisseti 0cn climadd mpanr1 nnuz raleqi sylan2b
        syl21anc recni addid1i syl6breq ) UPRIUQZURZXOSUQZDUSUTVAZVBZLEVCVAZXQR
        VDZVBZVBZGEVEVFVGZEVCYCXTMVEVCVAZKUQZLUTZVHVDZYFMUTZVHVDZYFGUTZYGYIVFVG
        ZVIZVJZKUPVKZGYDVCVAZXSXTYAVLXSYAYEXTDHIKMNOPQRSUAUBUCUDUEUFUJUKULVMVNY
        CYNKUPYCYFUPVDZVBYHYJYMXSYQYHYBXPYQYHXRXPYQVBZYGABCFHIJKLNOPQRTUAUBUCUD
        UEUFUGUIVOVPVQVQXSYAYQYJXTXPYAYQYJXRXPYAVBZYQVBZYIYTYIYFXOUTZXQNVGZOUTZ
        VRYQYIUUCVIYSULVSXPYQYAUUCVRVDZUUAQVDZXQQVDZUUDYRYAUUEUUFVBUUBQVDZUUDHV
        TVDZUUEUUFUUGHUAWAZUUAXQHNQUDUBWBWCUUHUUGUUDUUIUUBHOQUDUCWDWEWFHIKPQRUA
        UEUFUDWGRQXQHPQRUAUEUFUDWHWIWJWKWLVPWMWNYQYMYCYQYKYGUUCVFVGYLUOYQYIUUCY
        GVFULWOWPVSWQWRXTYEVBZYNKWSWTUTZVKZYPYOUUJWSXAVDUULYPXBEVEKLMGWSUMUKUNE
        VRABCEFHNOPQRTUAUBUCUDUEUFUGUHXCZXDVEVHXEXDXFXGYNKUPUUKXHXIXJXKEEUUMXLX
        MXN $.
        $( [28-Feb-2008] $)

      $( Lemma for ~ minvecex . $)
      minveclem31 $p |- ( ( ( f : NN --> Y /\ f ( ~~>m ` D ) a ) /\
          ( F ~~> P /\ a e. Y ) ) -> ( N ` ( A M a ) ) <_ P ) $=
        ( cn cv wf clm cfv wbr wa cli wcel co cr c1 cz cle cuz wral minveclem5
        ad2antll minveclem30 caddc wceq adantl minveclem22 adantlr cnv phnvi
        nvmcl mp3an1 nvcl mpan syl minveclem4 minveclem3 sseli syl2an an1rs
        adantlrl adantllr readdcl syl11anc eqeltrd w3a nvmtri2 minveclem8
        ad2antlr opreq1d breqtrrd jca elnnuz sylan2br r19.21aiva 1z jctil fvex
        minveclem12 elisseti climcmpc1 syl21anc ) UPRIUQZURZXNSUQZDUSUTVAZVBZLE
        VCVAZXPRVDZVBZVBZCXPNVEZOUTZVFVDZGEVCVAVGVHVDZKUQZGUTZVFVDZYDYHVIVAZVBZ
        KVGVJUTZVKZVBYDEVIVAXTYEXRXSSCHNOPQRUAUEUFUDUBUCUGVLVMABCDEFGHIJKLMNOPQ
        RSTUAUBUCUDUEUFUGUHUIUJUKULUMUNUOVNYBYMYFYBYKKYLYBYGUPVDZYKYGYLVDYBYNVB
        ZYIYJYOYHYGLUTZYGXNUTZXPNVEZOUTZVOVEZVFYNYHYTVPYBUOVQZYOYPVFVDZYSVFVDZY
        TVFVDXRYNUUBYAXOYNUUBXQABCFHIJKLNOPQRTUAUBUCUDUEUFUGUIVRVSVSXOYAYNUUCXQ
        XOXTYNUUCXSXOYNXTUUCYQQVDZXPQVDZUUCXOYNVBZXTUUDUUEVBYRQVDZUUCHVTVDZUUDU
        UEUUGHUAWAZYQXPHNQUDUBWBWCUUHUUGUUCUUIYRHOQUDUCWDWEWFHIKPQRUAUEUFUDWGZR
        QXPHPQRUAUEUFUDWHWIZWJWKWLWMYPYSWNWOWPYOYDYTYHVIXOYAYNYDYTVIVAZXQXOXTYN
        UULXSXOYNXTUULUUFXTVBZYDCYQNVEOUTZYSVOVEZYTVIUUDUUEYDUUOVIVAZUUFXTCQVDZ
        UUDUUEUUPUGUUHUUQUUDUUEWQUUPUUICYQXPHNOQUDUBUCWRWEWCUUJUUKWJUUMYPUUNYSV
        OYNYPUUNVPXOXTCIJKLNOUIWSWTXAXBWKWLWMUUAXBXCYGXDXEXFXGXHYDEKGVGUNYCOXIE
        VFABCEFHNOPQRTUAUBUCUDUEUFUGUHXJXKXLXM $.
        $( [28-Feb-2008] $)

      $( Lemma for ~ minvecex . $)
      minveclem32 $p |- ( ( ( f : NN --> Y /\ f ( ~~>m ` D ) a ) /\
          ( F ~~> P /\ a e. Y ) ) -> ( N ` ( A M a ) ) = P ) $=
        ( cn cv wf clm cfv wbr wa cli wcel co wceq cle wb cr minveclem5
        minveclem12 letri3 mpan2 syl ad2antll minveclem31 minveclem13
        mpbir2and ) UPRIUQZURVSSUQZDUSUTVAVBZLEVCVAZVTRVDZVBVBCVTNVEOUTZEVFZWDE
        VGVAZEWDVGVAZWCWEWFWGVBVHZWAWBWCWDVIVDZWHSCHNOPQRUAUEUFUDUBUCUGVJWIEVIV
        DWHABCEFHNOPQRTUAUBUCUDUEUFUGUHVKWDEVLVMVNVOABCDEFGHIJKLMNOPQRSTUAUBUCU
        DUEUFUGUHUIUJUKULUMUNUOVPWCWGWAWBABCVTEFHNOPQRTUAUBUCUDUEUFUGUHVQVOVR
        $.
        $( [28-Feb-2008] $)
    $}

    ${
      minvec33.2 $e |- P = -u sup ( R , RR , < ) $.
      minvec33.hf $e |- ( j e. NN -> ( F ` j ) = ( N ` ( A M ( f ` j ) ) ) ) $.
      minvec33.d $e |- D = ( IndMet ` W ) $.
      minvec33.t1 $e |- F e. _V $.
      $( Lemma for ~ minvecex . $)
      minveclem33 $p |- ( ( ( f : NN --> Y /\ f ( ~~>m ` D ) a ) /\
          ( F ~~> P /\ a e. Y ) ) -> ( N ` ( A M a ) ) = P ) $=
        ( vn vu vm cv cn wcel cfv co caddc wceq wa copab nnex eqid fopabex2
        fveq2 opreq1d fveq2d fvex fvopab4 opreq12d oprex minveclem32 ) ABCDEFUI
        ULZUMUNZUJULZVLJUOZVLHULZUOZPULZKUPZLUOZUQUPZURUSUIUJUTZGHIUKJVMVNVTURU
        SUIUJUTZKLMNOPQRSTUAUBUCUDUEUFUGUIUJUMVTWCVAWCVBZVCUIUJUKULZVTWEVPUOZVR
        KUPZLUOZUMWCVLWEURZVSWGLWIVQWFVRKVLWEVPVDVEVFZWDWGLVGVHUHUIUJUMWAWBVAWB
        VBZVCUIUJWEWAWEJUOZWHUQUPUMWBWIVOWLVTWHUQVLWEJVDWJVIWKWLWHUQVJVHVK $.
        $( [28-Feb-2008] $)

      minvec34.w2 $e |- W e. CBan $.
      $( Minimizing vector theorem (existence part).  There is exactly one
         vector in a complete subspace ` W ` that minimizes the distance to an
         arbitrary vector ` A ` in a parent inner product space.  Part of
         Theorem 3.3-1 of [Kreyszig] p. 144, specialized to subspaces instead
         of convex subsets.  Note that we work with the negative of supremum
         instead of infimum in order to use theorems we already have
         available. $)
      minvecex $p |- E. a e. Y ( N ` ( A M a ) ) = P $=
        ( vu vt cn cv wf cli wbr wa wex co cfv wceq wrex cn0v cneg cab eqeq1
        rexbidv cbvabv opreq2 fveq2d negeqd eqeq2d cbvrexv abbii eqtri
        minveclem5 cnv wcel css phnvi eqid sspnv mp2an nvzcl ax-mp minveclem10
        infcvgi clm minveclem29 wi minveclem33 exp43 com23 com34 imp31
        reximdva mpd 19.23aiv ) ULOHUMZUNZJEUOUPZUQZHURCPUMZKUSLUTEVAZPOVBZUJUK
        UJUKCUKUMZKUSZLUTZCIUMWSUTZKUSZLUTFEHIJOMVCUTZFAUMZCBUMZKUSZLUTZVDZVAZB
        OVBZAVEZUJUMZXHVDZVAZUKOVBZUJVEZQXSXTXPVAZBOVBZUJVEYDXRYFAUJXLXTVAXQYEB
        OXLXTXPVFVGVHYFYCUJYEYBBUKOXMXFVAZXPYAXTYGXOXHYGXNXGLXMXFCKVIVJVKVLVMVN
        VOVOZUKCGKLMNORUBUCUASTUDVPMVQVRZXKOVRGVQVRMGVSUTZVRYIGRVTUBGYJMYJWAWBW
        CMOXKUCXKWAWDWEABUJUKCFGKLMNOQRSTUAUBUCUDWFUEUHXFXIVAXGXJLXFXICKVIVJUFW
        GXBXEHXBWSXCDWHUTUPZPOVBXEUJUKCDEFGHIJKLMNOPYHRSTUAUBUCUDUFUEUGUIWIXBYK
        XDPOWTXAXCOVRZYKXDWJWTXAYKYLXDWTYKXAYLXDWJWTYKXAYLXDABCDEFGHIJKLMNOPQRS
        TUAUBUCUDUEUFUGUHWKWLWMWNWOWPWQWRWE $.
        $( [28-Feb-2008] $)
    $}
  $}

  ${
    $d x y A $.  $d x y G $.  $d x y M $.  $d x y N $.  $d x y S $.
    $d x y U $.  $d x y W $.  $d x y Y $.  $d a b x y $.
    minvec35.x $e |- X = ( BaseSet ` U ) $.
    minvec35.g $e |- G = ( +v ` U ) $.
    minvec35.m $e |- M = ( -v ` U ) $.
    minvec35.s $e |- S = ( .s ` U ) $.
    minvec35.n $e |- N = ( norm ` U ) $.
    minvec35.y $e |- Y = ( BaseSet ` W ) $.
    minvec35.u $e |- U e. CPreHil $.
    minvec35.a $e |- A e. X $.
    $( Lemma for ~ minveceu . $)
    minveclem35 $p |- ( ( a e. X /\ b e. X ) -> ( N ` ( ( A M a ) G ( A M b )
         ) ) = ( 2 x. ( N ` ( A M ( ( 1 / 2 ) S ( a G b ) ) ) ) ) ) $=
      ( cv wcel wa co cfv c2 c1 cdiv cmul wceq cnv phnvi nvaddsub4 mp3an1
      mpanl12 nvgcl nv2 mp2an a1i 2cn 2ne0 recidi opreq1i cc reccli w3a nvsass
      mpan mp3an12 nvsid 3eqtr3rd opreq12d nvscl nvmdi syl eqtr4d eqtr3d
      fveq2d nvmcl cr cc0 cle wbr 2re 0re 2pos ltleii nvsge0 eqtrd ) JTZHUAZKTZ
      HUAZUBZAWIEUCAWKEUCDUCZFUDUEAUFUEUGUCZWIWKDUCZBUCZEUCZBUCZFUDZUEWRFUDUHUC
      ZWMWNWSFWMAADUCZWPEUCZWNWSAHUAZXDWMXCWNUIZSSCUJUAZXDXDUBWMXECRUKZAAWIWKCD
      EHLMNULUMUNWMWPHUAZXCWSUIXFWJWLXHXGWIWKCDHLMUOUMZXHXCUEABUCZUEWQBUCZEUCZW
      SXHXBXJWPXKEXBXJUIZXHXFXDXMXGSABCDHLMOUPUQURXHUEWOUHUCZWPBUCZUFWPBUCZXKWP
      XOXPUIXHXNUFWPBUEUSUTVAVBURUEVCUAZWOVCUAZXHXOXKUIZUSUEUSUTVDZXFXQXRXHVEXS
      XGUEWOWPBCHLOVFVGVHXFXHXPWPUIXGWPBCHLOVIVGVJVKXHWQHUAZWSXLUIZXFXRXHYAXGXT
      WOWPBCHLOVLVHZXQXDYAYBUSSXFXQXDYAVEYBXGUEAWQBCEHLNOVMVGVHVNVOVNVPVQWMWRHU
      AZWTXAUIZWMYAYDWMXHYAXIYCVNXFXDYAYDXGSAWQCEHLNVRVHVNUEVSUAZVTUEWAWBZYDYEW
      CVTUEWDWCWEWFXFYFYGUBYDYEXGUEWRBCFHLOPWGUMUNVNWH $.
      $( [19-Mar-2008] $)

    minvec36.w $e |- W e. ( SubSp ` U ) $.
    minvec36.2 $e |- P = -u sup ( R , RR , < ) $.
    minvec36.1 $e |- R = { x | E. y e. Y x = -u ( N ` ( A M y ) ) } $.
    $( Lemma for ~ minveceu . $)
    minveclem36 $p |- ( ( ( a e. X /\ b e. X ) /\ ( ( N ` ( A M a ) ) = P /\
    ( N ` ( A M b ) ) = P ) ) -> ( ( N ` ( a M b ) ) ^ 2 ) = ( ( 2 x. ( ( P
       ^ 2 ) + ( P ^ 2 ) ) ) - ( ( N ` ( ( A M a ) G ( A M b ) ) ) ^ 2 ) ) ) $=
      ( cv wcel wa co cfv wceq c2 cexp caddc cmul cmin cnv w3a phnvi nvnnncan1
      mpan mp3an1 fveq2d nvsub eqtr4d opreq1d opreq2d cphl phpar2 nvmcl
      mp3an12 syl2an eqtr3d opreq1 opreqan12d sylan9eq wb nvgcl cc 2cn
      minveclem12 resqcli readdcli recni mulcli subadd sqcl cr nvcl recnd
      syl11anc adantr mpbird eqcomd ) NUGZLUHZOUGZLUHZUIZCWPIUJZJUKZDULZCWRIUJZ
      JUKZDULZUIZUIZUMDUMUNUJZXIUOUJZUPUJZXAXDHUJZJUKZUMUNUJZUQUJZWPWRIUJZJUKZU
      MUNUJZXHXOXRULZXNXRUOUJZXKULZWTXGXTUMXBUMUNUJZXEUMUNUJZUOUJZUPUJZXKWTXNXA
      XDIUJZJUKZUMUNUJZUOUJZXTYEWTYHXRXNUOWTYGXQUMUNWTYGWRWPIUJZJUKZXQWTYFYJJCL
      UHZWQWSYFYJULZUCGURUHZYLWQWSUSYMGUBUTZCWPWRGILPRVAVBVCVDYNWQWSXQYKULYOWPW
      RGIJLPRTVEVCVFVGVHXALUHZXDLUHZYIYEULZWQWSGVIUHYPYQYRUBXAXDGHIJLPQRTVJVCYN
      YLWQYPYOUCCWPGILPRVKVLZYNYLWSYQYOUCCWRGILPRVKVLZVMVNXGYDXJUMUPXCXFYBXIYCX
      IUOXBDUMUNVOXEDUMUNVOVPVHVQWTXSYAVRZXGWTXLLUHZXPLUHZUUAYPYQUUBWQWSYNYPYQU
      UBYOXAXDGHLPQVSVCYSYTVMYNWQWSUUCYOWPWRGILPRVKVCXMVTUHZXQVTUHZUUAUUBUUCXNV
      TUHZXRVTUHZUUAUUDUUEXKVTUHUUFUUGUUAUMXJWAXJXIXIDABCDEGIJKLMUFUBRTPUDUAUCU
      EWBWCZUUHWDWEWFXKXNXRWGVCXMWHXQWHVMUUBXMYNUUBXMWIUHYOXLGJLPTWJVBWKUUCXQYN
      UUCXQWIUHYOXPGJLPTWJVBWKVMWLWMWNWO $.
      $( [19-Mar-2008] $)

    $( Lemma for ~ minveceu . $)
    minveclem37 $p |- ( ( a e. Y /\ b e. Y ) ->
              P <_ ( N ` ( A M ( ( 1 / 2 ) S ( a G b ) ) ) ) ) $=
      ( cv wcel wa c1 c2 cdiv co cfv cle wbr cpv cnv css wceq phnvi eqid
      sspgval mpanl12 minveclem1 nvgcl mp3an1 eqeltrrd cns cc 2cn 2ne0 reccli
      sspsval mpan nvscl mp3an12 syl minveclem13 ) NUGZMUHZOUGZMUHZUIZUJUKULUMZ
      VTWBHUMZFUMZMUHZDCWGIUMJUNUOUPWDWFMUHZWHWDVTWBKUQUNZUMZWFMGURUHZKGUSUNZUH
      ZWDWKWFUTGUBVAZUDVTWBGWJHWMKMUAQWJVBZWMVBZVCVDKURUHZWAWCWKMUHGKUBUDVEZVTW
      BKWJMUAWPVFVGVHWIWEWFKVIUNZUMZWGMWEVJUHZWIXAWGUTZUKVKVLVMZWLWNXBWIUIXCWOU
      DWEWFWTFGWMKMUASWTVBZWQVNVDVOWRXBWIXAMUHWSXDWEWFWTKMUAXEVPVQVHVRABCWGDEGI
      JKLMUFUBRTPUDUAUCUEVSVR $.
      $( [19-Mar-2008] $)

    $( Lemma for ~ minveceu . $)
    minveclem38 $p |- ( ( ( a e. Y /\ b e. Y ) /\ ( ( N ` ( A M a ) ) = P /\
    ( N ` ( A M b ) ) = P ) ) -> ( N ` ( a M b ) ) <_ 0 ) $=
      ( cv wcel wa co cfv wceq cc0 cle wbr c2 cexp caddc cmul cmin minveclem36
      minveclem3 sseli anim12i sylan c4 c1 cdiv cr cnv phnvi nvgcl mp3an1
      syl2an cc 2cn 2ne0 reccli nvscl mp3an12 syl nvmcl nvcl mpan resqcl
      minveclem37 minveclem12 minveclem14 le2sq2 mpanl12 syl11anc resqcli 4re
      0re 4pos ltleii pm3.2i lemul2a mp3anl3 mpanl1 recni mulassi 2t2e4
      opreq1i 2timesi opreq2i 3eqtr3ri syl5eqbr recnd sqmul sq2 syl6req
      minveclem35 opreq1d eqtr4d breqtrd wb 2re readdcli remulcli suble0
      mpbird adantr eqbrtrd sq0 syl6breqr nvge0 leidi le2sq mpanr12 ) NUGZMUHZO
      UGZMUHZUIZCYKIUJZJUKDULCYMIUJZJUKDULUIZUIZYKYMIUJZJUKZUMUNUOZUUAUPUQUJZUM
      UPUQUJZUNUOZYSUUCUMUUDUNYSUUCUPDUPUQUJZUUFURUJZUSUJZYPYQHUJZJUKZUPUQUJZUT
      UJZUMUNYKLUHZYMLUHZUIZYRUUCUULULYOABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFVAYLUUM
      YNUUNMLYKGKLMUBUDUAPVBZVCZMLYMUUPVCZVDVEYOUULUMUNUOZYRYOUUSUUHUUKUNUOZYOU
      UHVFCVGUPVHUJZYKYMHUJZFUJZIUJZJUKZUPUQUJZUSUJZUUKUNYOVFUUFUSUJZUVGUUHUNYO
      UVFVIUHZUUFUVFUNUOZUVHUVGUNUOZYOUVEVIUHZUVIYOUVDLUHZUVLYOUVCLUHZUVMYOUVBL
      UHZUVNUUMUUNUVOYLYNGVJUHZUUMUUNUVOGUBVKZYKYMGHLPQVLVMUUQUURVNUVPUVAVOUHUV
      OUVNUVQUPVPVQVRUVAUVBFGLPSVSVTWAUVPCLUHZUVNUVMUVQUCCUVCGILPRWBVTWAUVPUVMU
      VLUVQUVDGJLPTWCWDWAZUVEWEWAYOUVLDUVEUNUOZUVJUVSABCDEFGHIJKLMNOPQRSTUAUBUC
      UDUEUFWFDVIUHUMDUNUOUVLUVTUIUVJABCDEGIJKLMUFUBRTPUDUAUCUEWGZABCDEGIJKLMUF
      UBRTPUDUAUCUEWHDUVEWIWJWKUUFVIUHZUVIUVJUVKDUWAWLZUWBUVIVFVIUHZUMVFUNUOZUI
      UVJUVKUWDUWEWMUMVFWNWMWOWPWQUUFUVFVFWRWSWTWKUPUPUSUJZUUFUSUJUPUPUUFUSUJZU
      SUJUVHUUHUPUPUUFVPVPUUFUWCXAZXBUWFVFUUFUSXCXDUWGUUGUPUSUUFUWHXEXFXGXHYOUV
      GUPUVEUSUJZUPUQUJZUUKYOUWJUPUPUQUJZUVFUSUJZUVGYOUVEVOUHZUWJUWLULZYOUVEUVS
      XIUPVOUHUWMUWNVPUPUVEXJWDWAUWKVFUVFUSXKXDXLYOUUJUWIUPUQUUMUUNUUJUWIULYLYN
      CFGHIJKLMNOPQRSTUAUBUCXMUUQUURVNXNXOXPUUMUUNUUSUUTXQZYLYNUUOUUKVIUHZUWOUU
      OUUJVIUHZUWPUUOUUILUHZUWQYPLUHZYQLUHZUWRUUMUUNUVPUWSUWTUWRUVQYPYQGHLPQVLV
      MUVPUVRUUMUWSUVQUCCYKGILPRWBVTUVPUVRUUNUWTUVQUCCYMGILPRWBVTVNUVPUWRUWQUVQ
      UUIGJLPTWCWDWAUUJWEWAUUHVIUHUWPUWOUPUUGXRUUFUUFUWCUWCXSXTUUHUUKYAWDWAUUQU
      URVNYBYCYDYEYFYOUUBUUEXQZYRUUMUUNUXAYLYNUUOYTLUHZUXAUVPUUMUUNUXBUVQYKYMGI
      LPRWBVMUXBUUAVIUHZUMUUAUNUOZUXAUVPUXBUXCUVQYTGJLPTWCWDUVPUXBUXDUVQYTGJLPT
      YGWDUXCUXDUIUMVIUHUMUMUNUOUXAWNUMWNYHUUAUMYIYJWKWAUUQUURVNYCYB $.
      $( [23-Mar-2008] $)
  $}

  ${
    $d a b f j n v x y A $.  $d x y B $.  $d a b f j n v x y M $.
    $d a b f j n v x y N $.  $d a b f n P $.  $d a f Q $.  $d a b R $.
    $d x y U $.  $d a b x y W $.  $d a b f n x y Y $.
    minvec.x $e |- X = ( BaseSet ` U ) $.
    minvec.m $e |- M = ( -v ` U ) $.
    minvec.n $e |- N = ( norm ` U ) $.
    minvec.y $e |- Y = ( BaseSet ` W ) $.
    minvec.1 $e |- R = { x | E. y e. Y x = -u ( N ` ( A M y ) ) } $.
    minvec.2 $e |- P = -u sup ( R , RR , < ) $.
    minvec.u $e |- U e. CPreHil $.
    minvec.w $e |- W e. ( ( SubSp ` U ) i^i CBan ) $.
    minvec.a $e |- A e. X $.
    $( Minimizing vector theorem.  There is exactly one vector in a complete
       subspace ` W ` that minimizes the distance to an arbitrary vector ` A `
       in a parent inner product space.  Theorem 3.3-1 of [Kreyszig] p. 144,
       specialized to subspaces instead of convex subsets.  Note that we work
       with the negative of the supremum of negatives instead of infimum in
       order to use theorems we already have available. $)
    minveceu $p |- E! a e. Y ( N ` ( A M a ) ) = P $=
      ( vb vf vn vj vv cv co cfv wceq wreu wrex wa wi wral opreq2 fveq2d
      eqeq1d reu4 cims cn wcel copab css cbn cin inss1 sseli ax-mp fveq2
      opreq2d eqid fvex fvopab4 nnex opabex2 inss2 minvecex cc0 cle wbr cns
      cpv minveclem38 cnv phnvi nvmcl mp3an1 nvge0 mpan syl idd mpan2d cn0v wb
      nvz cr nvcl 0re letri3 mpan2 nvmeq0 3bitr3d sylibd minveclem3 syl2an
      adantr mpd ex rgen2a mpbir2an ) CLUGZGUHZHUIZDUJZLKUKXOLKULXOCUBUGZGUHZHU
      IZDUJZUMZXLXPUJZUNZUBKUOLKUOXOXSLUBKYAXNXRDYAXMXQHXLXPCGUPUQURUSABCIUTUIZ
      DEFUCUDUEUGZVAVBUFUGCYDUCUGZUIZGUHZHUIZUJUMUEUFVCZGHIJKLQSNOMIFVDUIZVEVFZ
      VBZIYJVBTYKYJIYJVEVGVHVIZPUARUEUFUDUGZYHCYNYEUIZGUHZHUIVAYIYDYNUJZYGYPHYQ
      YFYOCGYDYNYEVJVKUQYIVLYPHVMVNYCVLUEUFVAYHVOVPYLIVEVBTYKVEIYJVEVQVHVIVRYBL
      UBKXLKVBZXPKVBZUMZXTYAYTXTUMXLXPGUHZHUIZVSVTWAZYAABCDEFWBUIZFFWCUIZGHIJKL
      UBMUUEVLNUUDVLOPSUAYMRQWDYTUUCYAUNZXTXLJVBZXPJVBZUUFYRYSUUGUUHUMZUUCUUCVS
      UUBVTWAZUMZYAUUIUUCUUJUUKUUIUUAJVBZUUJFWEVBZUUGUUHUULFSWFZXLXPFGJMNWGWHZU
      UMUULUUJUUNUUAFHJMOWIWJWKUUIUUKWLWMUUIUUBVSUJZUUAFWNUIZUJZUUKYAUUIUULUUPU
      URWOZUUOUUMUULUUSUUNUUAFHJUUQMUUQVLZOWPWJWKUUIUUBWQVBZUUPUUKWOZUUIUULUVAU
      UOUUMUULUVAUUNUUAFHJMOWRWJWKUVAVSWQVBUVBWSUUBVSWTXAWKUUMUUGUUHUURYAWOUUNX
      LXPFGJUUQMNUUTXBWHXCXDKJXLFIJKSYMPMXEZVHKJXPUVCVHXFXGXHXIXJXK $.
      $( [8-Feb-2008] $)

    minveccl.q $e |- Q = U. { b e. Y | ( N ` ( A M b ) ) = P } $.
    $( The minimizing vector of ~ minveceu belongs to the subspace ` Y ` . $)
    minveccl $p |- Q e. Y $=
      ( cv co cfv wceq crab cuni wreu wcel minveceu reucl ax-mp eqeltri ) ECMUD
      HUEIUFDUGZMLUHUIZLUCUPMLUJUQLUKABCDFGHIJKLMNOPQRSTUAUBULUPMLUMUNUO $.
      $( [24-Mar-2008] $)

    $( Distance of the minimizing vector of ~ minveceu . $)
    minvecdist $p |- ( N ` ( A M Q ) ) = P $=
      ( vf co cfv wceq cv crab cuni eqcomi wcel wreu wb minveccl minveceu
      hbrab1 hbuni hbxfr wal wi ax-17 hbopr hbfv hbeq a1i opreq2 fveq2d eqeq1d
      reuuni2f mp2an mpbir ) CEHUEZIUFZDUGZCMUHZHUEZIUFZDUGZMLUIZUJZEUGZEWAUCUK
      ELULZVSMLUMVOWBUNABCDEFGHIJKLMNOPQRSTUAUBUCUOABCDFGHIJKLMNOPQRSTUAUBUPVSV
      OMUDLEMUDEWAUCMUDVTVSMUDLUQURUSZVOVOMUTVAWCMUDUDVNDMUDVMIUDUHZIULMVBMUDCE
      HWECULMVBWEHULMVBWDVCVDWEDULMVBVEVFVPEUGZVRVNDWFVQVMIVPECHVGVHVIVJVKVL $.
      $( [24-Mar-2008] $)

    $( The minimizing vector from ~ minveceu has the smallest distance. $)
    minvecle $p |- ( B e. Y -> ( N ` ( A M Q ) ) <_ ( N ` ( A M B ) ) ) $=
      ( wcel co cfv cle css cbn cin inss1 sselii minveclem13 minvecdist
      syl5eqbr ) DMUEECDIUFJUGCFIUFJUGUHABCDEGHIJKLMSUAPQOHUIUGZUJUKUQKUQUJULUB
      UMRUCTUNABCEFGHIJKLMNOPQRSTUAUBUCUDUOUP $.
      $( [24-Mar-2008] $)

    $( Lemma for ~ minvecex2 . $)
    minveclem39 $p |- E. a e. Y A. f e. Y
           ( N ` ( A M a ) ) <_ ( N ` ( A M f ) ) $=
      ( wcel co cfv cv cle wbr wral wrex minveccl minvecle rgen wceq opreq2
      fveq2d breq1d ralbidv rcla4ev mp2an ) EMUFCEIUGZJUHZCHUIZIUGJUHZUJUKZHMUL
      ZCNUIZIUGZJUHZVGUJUKZHMULZNMUMABCDEFGIJKLMOPQRSTUAUBUCUDUEUNVHHMABCVFDEFG
      IJKLMOPQRSTUAUBUCUDUEUOUPVNVINEMVJEUQZVMVHHMVOVLVEVGUJVOVKVDJVJECIURUSUTV
      AVBVC $.
      $( [11-Apr-2008] $)
  $}

  ${
    $d v w x y z A $.  $d v w x y z M $.  $d v w x y z N $.  $d w z U $.
    $d v w x z W $.  $d v w x y z Y $.
    minvec2.x $e |- X = ( BaseSet ` U ) $.
    minvec2.m $e |- M = ( -v ` U ) $.
    minvec2.n $e |- N = ( norm ` U ) $.
    minvecx2.y $e |- Y = ( BaseSet ` W ) $.
    minvec2.u $e |- U e. CPreHil $.
    minvec2.w $e |- W e. ( ( SubSp ` U ) i^i CBan ) $.
    minvec2.a $e |- A e. X $.
    $( Existence version of ~ minvecle . $)
    minvecex2 $p |- E. x e. Y A. y e. Y
           ( N ` ( A M x ) ) <_ ( N ` ( A M y ) ) $=
      ( vz vw vv cv co cfv cneg wceq wrex cab cr clt csup crab cuni eqid
      minveclem39 ) QRCQTCRTEUAFUBUCUDRIUEQUFZUGUHUIUCZCSTEUAFUBUOUDSIUJUKZUNDB
      EFGHIASJKLMUNULUOULNOPUPULUM $.
      $( [11-Apr-2008] $)
  $}

