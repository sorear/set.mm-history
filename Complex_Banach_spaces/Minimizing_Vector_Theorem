$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Minimizing Vector Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    minvec1.u $e |- U e. CPreHil $.
    minvec1.w1 $e |- W e. ( SubSp ` U ) $.
    $( Lemma for ~ mnvc3 . $)
    minveclem1 $p |- W e. NrmCVec $=
      ( cnv wcel css cfv phnvi eqid sspnv mp2an ) AEFBAGHZFBEFACIDAMBMJKL $.
      $( [?] $) $( [4-Mar-2008] $)

    minvec2.y $e |- Y = ( Base ` W ) $.
    $( Lemma for ~ mnvc3 . $)
    minveclem2 $p |- ( 0v ` W ) e. Y $=
      ( cnv wcel cn0v cfv minveclem1 eqid nvzcl ax-mp ) BGHBIJZCHABDEKBCOFOLMN
      $.
      $( [?] $) $( [4-Mar-2008] $)

    minvec3.x $e |- X = ( Base ` U ) $.
    $( Lemma for ~ mnvc3 . $)
    minveclem3 $p |- Y (_ X $=
      ( cnv wcel css cfv wss phnvi eqid sspba mp2an ) AIJBAKLZJDCMAENFARBCDHGRO
      PQ $.
      $( [?] $) $( [29-Feb-2008] $)

    minveclem4 $p |- ( ( f : NN --> Y /\ n e. NN ) -> ( f ` n ) e. X ) $=
      ( cn cv wf wcel wa cfv ffvelrn minveclem3 sseli syl ) KFBLZMCLZKNOUBUAPZF
      NUCENKFUBUAQFEUCADEFGHIJRST $.
      $( [?] $) $( [5-Mar-2008] $)

    minvec5.m $e |- M = ( -v ` U ) $.
    minvec5.n $e |- N = ( norm ` U ) $.
    minvec5.a $e |- A e. X $.
    $( Lemma for ~ mnvc3 . $)
    minveclem5 $p |- ( y e. Y -> ( N ` ( A M y ) ) e. RR ) $=
      ( cv wcel co cfv cr minveclem3 sseli cnv phnvi nvmcl mp3an12 syl nvcl
      mpan ) APZHQZBUJDRZGQZULESTQZUKUJGQZUMHGUJCFGHIJKLUAUBCUCQZBGQUOUMCIUDZOB
      UJCDGLMUEUFUGUPUMUNUQULCEGLNUHUIUG $.
      $( [?] $) $( [4-Mar-2008] $)
  $}

  ${
    $d j A $.  $d j H $.  $d j M $.  $d f j m $.
    minvec6.h $e |- ( j e. NN -> ( H ` j ) = ( A M ( f ` j ) ) ) $.
    $( Lemma for ~ mnvc3 . $)
    minveclem6 $p |- ( m e. NN -> ( H ` m ) = ( A M ( f ` m ) ) ) $=
      ( cv cfv co wceq cn fveq2 opreq2d eqeq12d vtoclga ) CHZEIZAQBHZIZFJZKDHZE
      IZAUBSIZFJZKCUBLQUBKZRUCUAUEQUBEMUFTUDAFQUBSMNOGP $.
      $( [?] $) $( [5-Mar-2008] $)
  $}

  ${
    $d j F $.  $d j H $.  $d j N $.  $d j n $.
    minvec7.f $e |- ( j e. NN -> ( F ` j ) = ( N ` ( H ` j ) ) ) $.
    $( Lemma for ~ mnvc3 . $)
    minveclem7 $p |- ( n e. NN -> ( F ` n ) = ( N ` ( H ` n ) ) ) $=
      ( cv cfv wceq cn fveq2 fveq2d eqeq12d vtoclga ) AGZCHZODHZEHZIBGZCHZSDHZE
      HZIASJOSIZPTRUBOSCKUCQUAEOSDKLMFN $.
      $( [?] $) $( [6-Mar-2008] $)
  $}

  ${
    $d j A $.  $d j F $.  $d j M $.  $d j N $.  $d f j n $.
    minvec8.hf $e |- ( j e. NN -> ( F ` j ) = ( N ` ( A M ( f ` j ) ) ) ) $.
    $( Lemma for ~ mnvc3 . $)
    minveclem8 $p |- ( n e. NN -> ( F ` n ) = ( N ` ( A M ( f ` n ) ) ) ) $=
      ( cv cfv co wceq cn fveq2 opreq2d fveq2d eqeq12d vtoclga ) CIZEJZASBIZJZF
      KZGJZLDIZEJZAUEUAJZFKZGJZLCUEMSUELZTUFUDUISUEENUJUCUHGUJUBUGAFSUEUANOPQHR
      $.
      $( [?] $) $( [16-Feb-2008] $)
  $}

  ${
    $d k m u D $.  $d k m u J $.  $d k u M $.  $d k u N $.  $d k m u Y $.
    $d a f k m u $.
    minvec9.u $e |- U e. CPreHil $.
    minvec9.m $e |- M = ( -v ` U ) $.
    minvec9.n $e |- N = ( norm ` U ) $.
    minvec9.x $e |- X = ( Base ` U ) $.
    minvec9.w1 $e |- W e. ( SubSp ` U ) $.
    minvec9.y $e |- Y = ( Base ` W ) $.
    minvec9.d $e |- D = ( IndMet ` W ) $.
    minvec9.j $e |- J e. V $.
    minvec9.k $e |- ( m e. NN -> ( J ` m ) = ( N ` ( ( f ` m ) M a ) ) ) $.
    $( Lemma for ~ mnvc3 . $)
    minveclem9 $p |- ( ( ( f : NN --> Y /\ f ( ~~>m ` D ) a ) /\ a e. Y ) ->
                J ~~> 0 ) $=
      ( vk vu cn cv wf clm cfv wbr wa wcel cc0 cli cle co cabs clt wi wral
      wrex crp cme cnv css phnvi eqid sspnv mp2an imsmet ax-mp imsbai wceq a1i
      lmcvgnns mpanl1 an1s anassrs adantlll wb cims sspimsval mpanl12 imsdval
      mp3an1 cr absidt nvmcl nvcl mpan syl nvge0 sylanc eqtr4d minveclem3
      sseli syl2an eqtrd ffvelrn sylan an1rs breq1d imbi2d ralbidva rexbidv
      adantlr adantr mpbid r19.21aiva cc recnd minveclem4 clm0nns mpbird ) UCJC
      UDZUEZXMKUDZAUFUGUHZUIXOJUJZUIZEUKULUHZUAUDDUDZUMUHZXTXMUGZXOFUNZGUGZUOUG
      ZUBUDZUPUHZUQZDUCURZUAUCUSZUBUTURZXRYJUBUTXRYFUTUJZUIYAYBXOAUNZYFUPUHZUQZ
      DUCURZUAUCUSZYJXPXQYLYQXNXPXQYLYQXQXPYLYQAVAUJZXQXPYLUIYQHVBUJZYRBVBUJZHB
      VCUGZUJZYSBLVDZPBUUAHUUAVEZVFVGZAHRVHVIYBJAXOYFUADXMJAHJQRUUEVJYBYBVKXTUC
      UJZYBVEVLVMVNVOVPVQXRYQYJVRZYLXNXQUUGXPXNXQUIZYPYIUAUCUUHYOYHDUCUUHUUFUIZ
      YNYGYAUUIYMYEYFUPXNUUFXQYMYEVKZYBJUJZXQUUJXNUUFUIZUUKXQUIZYMYBXOBVSUGZUNZ
      YEYTUUBUUMYMUUOVKUUCPYBXOAUUNBUUAHJQUUNVEZRUUDVTWAYBIUJZXOIUJZUUOYEVKUUKX
      QUUQUURUIZUUOYDYEYTUUQUURUUOYDVKUUCYBXOUUNBFGIOMNUUPWBWCYDWDUJZUKYDUMUHZY
      EYDVKUUSYDWEUUSYCIUJZUUTYTUUQUURUVBUUCYBXOBFIOMWFWCZYTUVBUUTUUCYCBGIONWGW
      HWIZUUSUVBUVAUVCYTUVBUVAUUCYCBGIONWJWHWIWKWLJIYBBHIJLPQOWMZWNJIXOUVEWNZWO
      WPUCJXTXMWQWRWSWTXAXBXCXDXEXFXGXNXQXSYKVRZXPUUHYDXHUJZDUCURUVGUUHUVHDUCXN
      UUFXQUVHUUQUURUVHUULXQUUSYDUVDXIBCDHIJLPQOXJUVFWOWSXGUBYDUADESTXKWIXDXL
      $.
      $( [?] $) $( [24-Feb-2008] $)
  $}

  ${
    $d f j k m u x y A $.  $d k m n x y B $.  $d a k m D $.
    $d a j k m n u F $.  $d x y G $.  $d j H $.  $d f j k m n u x y M $.
    $d f j k m n u x y N $.  $d a f j k m n x y P $.  $d a t u R $.
    $d x y S $.  $d x y U $.  $d x y W $.  $d a f j k m n x y Y $.  $d t x y $.
    minvec10.1 $e |- R = { x | E. y e. Y x = -u ( N ` ( A M y ) ) } $.
    minvec10.u $e |- U e. CPreHil $.
    minvec10.m $e |- M = ( -v ` U ) $.
    minvec10.n $e |- N = ( norm ` U ) $.
    minvec10.x $e |- X = ( Base ` U ) $.
    minvec10.w1 $e |- W e. ( SubSp ` U ) $.
    minvec10.y $e |- Y = ( Base ` W ) $.
    minvec10.a $e |- A e. X $.
    $( Lemma for ~ mnvc3 .  The set of reals ` R ` is bounded above. $)
    minveclem10 $p |- E. u e. RR A. t e. R t <_ u $=
      ( cc0 cr wcel cv cle wbr wral wrex 0re co cfv cneg wceq visset eqeq1
      rexbidv elab2 breq1 minveclem3 sseli cnv phnvi nvmcl mp3an12 syl nvge0
      mpan wb nvcl le0neg2t mpbid syl5cbir r19.23aiv sylbi rgen breq2 ralbidv
      rcla4ev mp2an ) UAUBUCDUDZUAUEUFZDFUGZVTCUDZUEUFZDFUGZCUBUHUIWADFVTFUCVTE
      BUDZHUJZIUKZULZUMZBLUHZWAAUDZWIUMZBLUHWKAVTFDUNWLVTUMWMWJBLWLVTWIUOUPMUQW
      JWABLWJWAWIUAUEUFZWFLUCZVTWIUAUEURWOUAWHUEUFZWNWOWGKUCZWPWOWFKUCZWQLKWFGJ
      KLNRSQUSUTGVAUCZEKUCWRWQGNVBZTEWFGHKQOVCVDVEZWSWQWPWTWGGIKQPVFVGVEWOWHUBU
      CZWPWNVHWOWQXBXAWSWQXBWTWGGIKQPVIVGVEWHVJVEVKVLVMVNVOWEWBCUAUBWCUAUMWDWAD
      FWCUAVTUEVPVQVRVS $.
      $( [?] $) $( [10-Feb-2008] $)

    $( Lemma for ~ mnvc3 . $)
    minveclem11 $p |- sup ( R , RR , < ) e. RR $=
      ( vu vt cv co cfv cn0v minveclem5 minveclem2 minveclem10 infcvgaux1
      suprcli ) STDABSTCBUAFUBGUCDJHUDUCKBCEFGHIJLPQOMNRUEEHJLPQUFABSTCDEFGHIJK
      LMNOPQRUGUHUI $.
      $( [?] $) $( [6-Mar-2008] $)

    ${
      minvec12.2 $e |- P = -u sup ( R , RR , < ) $.
      $( Lemma for ~ mnvc3 . $)
      minveclem12 $p |- P e. RR $=
        ( cr clt csup cneg minveclem11 renegcl eqeltr ) DEUAUBUCZUDUATUHABCEFGH
        IJKLMNOPQRSUEUFUG $.
        $( [?] $) $( [3-Mar-2008] $)

      $( Lemma for ~ mnvec . $)
      minveclem13 $p |- ( B e. Y -> P <_ ( N ` ( A M B ) ) ) $=
        ( vu vt cv co cfv cn0v minveclem5 minveclem2 minveclem10 wceq opreq2
        fveq2d infcvgaux2 ) ABUBUCCBUDZHUEZIUFCDHUEZIUFDFELJUGUFMBCGHIJKLNRSQOP
        TUHGJLNRSUIABUBUCCFGHIJKLMNOPQRSTUJUAUODUKUPUQIUODCHULUMUN $.
        $( [?] $) $( [9-Feb-2008] $)

      $( Lemma for ~ mnvc3 . $)
      minveclem14 $p |- 0 <_ P $=
        ( va vu vt cc0 cr clt csup cneg cle wbr wcel cv wral 0re co cfv wceq
        wrex visset eqeq1 rexbidv elab2 wa pm3.27 minveclem3 sseli cnv phnvi
        nvmcl mp3an12 nvge0 mpan wb nvcl le0neg2t syl mpbid adantr eqbrtrd
        r19.23aiva sylbi rgen cn0v minveclem5 minveclem2 minveclem10
        infcvgaux1 suprleubi mp2an minveclem11 le0neg1t ax-mp mpbi breqtrr ) UD
        EUEUFUGZUHZDUIWOUDUIUJZUDWPUIUJZUDUEUKUAULZUDUIUJZUAEUMWQUNWTUAEWSEUKWS
        CBULZGUOZHUPZUHZUQZBKURZWTAULZXDUQZBKURXFAWSEUAUSXGWSUQXHXEBKXGWSXDUTVA
        LVBXEWTBKXAKUKZXEVCWSXDUDUIXIXEVDXIXDUDUIUJZXEXIXAJUKZXJKJXAFIJKMQRPVEV
        FXKXBJUKZXJFVGUKZCJUKXKXLFMVHZSCXAFGJPNVIVJXLUDXCUIUJZXJXMXLXOXNXBFHJPO
        VKVLXLXCUEUKZXOXJVMXMXLXPXNXBFHJPOVNVLXCVOVPVQVPVPVRVSVTWAWBUBUCUAEUDAB
        UBUCXCEKIWCUPLBCFGHIJKMQRPNOSWDFIKMQRWEABUBUCCEFGHIJKLMNOPQRSWFWGWHWIWO
        UEUKWQWRVMABCEFGHIJKLMNOPQRSWJWOWKWLWMTWN $.
        $( [?] $) $( [17-Feb-2008] $)

      $( Lemma for ~ mnvc3 . $)
      minveclem15 $p |- ( ( F ~~> P /\ B e. RR+ ) ->
       E. k e. NN A. n e. NN ( k <_ n -> ( abs ` ( ( F ` n ) - P ) ) < B ) ) $=
        ( cr wcel cli wbr crp cv cle cfv cmin co cabs clt wi cn wral wrex
        minveclem12 c1 1z cuz nnuz eqimss2i nnssz clmi2rp mp3an1 ) EUEUFJEUGUHD
        UIUFHUJIUJZUKUHVJJULEUMUNUOULDUPUHUQIURUSHURUTABCEFGKLMNOPQRSTUAUBUCUDV
        AEDUEHIJVBURURVCURVBVDULVEVFVGVHVI $.
        $( [?] $) $( [21-Feb-2008] $)

      minvec16.g $e |- G = ( +v ` U ) $.
      minvec16.s $e |- S = ( .s ` U ) $.
      $( ??? reuse f'nGf'm antecedent computation in mnvc3lem3 $)
      $( Lemma for ~ mnvc3 . $)
      minveclem16 $p |- ( ( ( f : NN --> Y /\ n e. NN ) /\ ( f : NN --> Y /\
   m e. NN ) ) -> ( 2 x. P ) <_
         ( N ` ( 2 S ( A M ( ( 1 / 2 ) S ( ( f ` n ) G ( f ` m ) ) ) ) ) ) ) $=
        ( cv cfv wcel c2 cmul co c1 cdiv cle wbr cn wf wa cpv cnv css wceq
        phnvi eqid sspgval mpanl12 minveclem1 nvgcl mp3an1 eqeltrrd cns cc 2cn
        2ne0 reccl sspsval mpan nvscl mp3an12 cr minveclem12 cc0 2re 0re 2pos
        ltlei pm3.2i lemul2it mp3anl3 mpanl1 minveclem3 sseli nvmcl syl nvcl
        minveclem13 sylanc nvsge0 breqtrrd ffvelrn syl2an ) JUHZHUHZUIZPUJZIUHZ
        XEUIZPUJZUKDULUMZUKCUNUKUOUMZXFXIKUMZFUMZLUMZFUMMUIZUPUQZURPXEUSZXDURUJ
        UTXRXHURUJUTXGXJUTZXMPUJZXQXSXFXINVAUIZUMZXMPGVBUJZNGVCUIZUJZXSYBXMVDGR
        VEZUBXFXIGYAKYDNPUCUFYAVFZYDVFZVGVHNVBUJZXGXJYBPUJGNRUBVIZXFXINYAPUCYGV
        JVKVLXTXKUKXOMUIZULUMZXPUPXTXNPUJZXKYLUPUQZXTXLXMNVMUIZUMZXNPXLVNUJZXTY
        PXNVDZUKVOVPVQZYCYEYQXTUTYRYFUBXLXMYOFGYDNPUCUGYOVFZYHVRVHVSYIYQXTYPPUJ
        YJYSXLXMYONPUCYTVTWAVLYKWBUJZDYKUPUQZYNYMDWBUJZUUAUUBYNABCDEGLMNOPQRSTU
        AUBUCUDUEWCUUCUUAUKWBUJZWDUKUPUQZUTZUUBYNUUDUUEWEWDUKWFWEWGWHZWIDYKUKWJ
        WKWLYMXOOUJZUUAYMXNOUJZUUHPOXNGNOPRUBUCUAWMZWNYCCOUJUUIUUHYFUDCXNGLOUAS
        WOWAZWPYCUUHUUAYFXOGMOUATWQVSWPABCXNDEGLMNOPQRSTUAUBUCUDUEWRWSWPXTXMOUJ
        ZXPYLVDZPOXMUUJWNUULUUHUUMUULUUIUUHYCYQUULUUIYFYSXLXMFGOUAUGVTWAUUKWPUU
        DUUEUUHUUMWEUUGYCUUFUUHUUMYFUKXOFGMOUAUGTWTVKVHWPWPXAWPURPXDXEXBURPXHXE
        XBXC $.
        $( [?] $) $( [6-Mar-2008] $)
    $}

    ${
      minvec17.h $e |- ( j e. NN -> ( H ` j ) = ( A M ( f ` j ) ) ) $.
      $( Lemma for ~ mnvec . $)
      minveclem17 $p |- ( ( f : NN --> Y /\ n e. NN ) -> ( H ` n ) e. X ) $=
        ( cn cv wf wcel wa cfv co wceq minveclem6 adantl ffvelrn minveclem3
        sseli cnv phnvi nvmcl mp3an12 syl eqeltrd ) UDNFUEZUFZHUEZUDUGZUHZVEIUI
        ZCVEVCUIZJUJZMVFVHVJUKVDCFGHIJUCULUMVGVINUGZVJMUGZUDNVEVCUNVKVIMUGZVLNM
        VIELMNPTUASUOUPEUQUGCMUGVMVLEPURUBCVIEJMSQUSUTVAVAVB $.
        $( [?] $) $( [4-Mar-2008] $)

      minvec18.g $e |- G = ( +v ` U ) $.
      $( Lemma for ~ mnvc3 . $)
      minveclem18 $p |- ( ( ( f : NN --> Y /\ n e. NN ) /\ ( f : NN --> Y /\
   m e. NN ) ) -> ( ( 2 x. ( ( ( N ` ( H ` n ) ) ^ 2 ) + ( ( N ` ( H ` m ) ) ^
   2 ) ) ) - ( ( N ` ( ( H ` n ) G ( H ` m ) ) ) ^ 2 ) ) =
      ( ( N ` ( ( f ` n ) M ( f ` m ) ) ) ^ 2 ) ) $=
        ( cn cv wf wcel wa c2 cfv cexp co caddc cmul cmin wceq cphl phpar
        mp3an1 cc wb subaddt axaddcl cnv cr phnvi nvcl mpan recnd sqclt syl
        syl2an 2cn axmulcl nvgcl nvmcl syl3anc mpbird minveclem17 minveclem6
        opreqan12d ad2ant2l w3a nvnnncan1 minveclem4 eqtrd fveq2d nvsub eqtr4d
        opreq1d ) UGPFUHZUIZIUHZUGUJZUKZWOHUHZUGUJZUKZUKZULWPKUMZMUMZULUNUOZWSK
        UMZMUMZULUNUOZUPUOZUQUOZXCXFJUOZMUMZULUNUOZURUOZXCXFLUOZMUMZULUNUOZWPWN
        UMZWSWNUMZLUOMUMZULUNUOXCOUJZXFOUJZXNXQUSZWRXAYAYBUKZYCXMXQUPUOXJUSZEUT
        UJYAYBYERXCXFEJLMOUAUFSTVAVBXJVCUJZXMVCUJZXQVCUJZYCYEVDYDXJXMXQVEYDXIVC
        UJZYFXEVCUJZXHVCUJZYIYAYBXEXHVFYAXDVCUJYJYAXDEVGUJZYAXDVHUJERVIZXCEMOUA
        TVJVKVLXDVMVNYBXGVCUJYKYBXGYLYBXGVHUJYMXFEMOUATVJVKVLXGVMVNVOULVCUJYIYF
        VPULXIVQVKVNYDXLVCUJYGYDXLYDXKOUJZXLVHUJZYLYAYBYNYMXCXFEJOUAUFVRVBYLYNY
        OYMXKEMOUATVJVKVNVLXLVMVNYDXPVCUJYHYDXPYDXOOUJZXPVHUJZYLYAYBYPYMXCXFELO
        UASVSVBYLYPYQYMXOEMOUATVJVKVNVLXPVMVNVTWAABCDEFGIKLMNOPQRSTUAUBUCUDUEWB
        ABCDEFGHKLMNOPQRSTUAUBUCUDUEWBVOXBXPXTULUNXBXPXSXRLUOZMUMZXTXBXOYRMXBXO
        CXRLUOZCXSLUOZLUOZYRWQWTXOUUBUSWOWOWQWTXCYTXFUUALCFGIKLUEWCCFGHKLUEWCWD
        WEXROUJZXSOUJZUUBYRUSZWRXACOUJZUUCUUDUUEUDYLUUFUUCUUDWFUUEYMCXRXSELOUAS
        WGVKVBEFINOPRUBUCUAWHZEFHNOPRUBUCUAWHZVOWIWJUUCUUDXTYSUSZWRXAYLUUCUUDUU
        IYMXRXSELMOUASTWKVBUUGUUHVOWLWMWI $.
        $( [?] $) $( [9-Mar-2008] $)

      minvec19.s $e |- S = ( .s ` U ) $.
      minvec19.2 $e |- P = -u sup ( R , RR , < ) $.
      $( Lemma for ~ mnvc3 . $)
      minveclem19 $p |- ( ( ( f : NN --> Y /\ n e. NN ) /\ ( f : NN --> Y /\
            m e. NN ) ) -> ( 2 x. P ) <_
          ( N ` ( ( H ` n ) G ( H ` m ) ) ) ) $=
        ( cn cv wf wcel wa c2 cmul co c1 cdiv cfv cle minveclem16 wceq
        minveclem6 opreqan12d ad2ant2l cnv phnvi pm3.2i nvaddsub4 mp3an12
        minveclem4 syl2an eqtr4d nv2 mp2an opreq1i syl6eq nvgcl mp3an1 cc 2cn
        2ne0 reccl nvscl w3a nvmdi mpan syl nvsass nvsid recid syl5eq eqtr3d
        opreq2d eqtr2d fveq2d breqtrd ) UKRHULZUMZKULZUKUNZUOZXAJULZUKUNZUOZUOZ
        UPDUQURUPCUSUPUTURZXBWTVAZXEWTVAZLURZFURZNURFURZOVAXBMVAZXEMVAZLURZOVAV
        BABCDEFGHJKLNOPQRSTUAUBUCUDUEUFUJUHUIVCXHXNXQOXHXQUPCFURZXLNURZXNXHXQCC
        LURZXLNURZXSXHXQCXJNURZCXKNURZLURZYAXCXFXQYDVDXAXAXCXFXOYBXPYCLCHIKMNUG
        VECHIJMNUGVEVFVGXJQUNZXKQUNZYAYDVDZXDXGGVHUNZCQUNZYIUOYEYFUOZYGGTVIZYIY
        IUFUFVJCCXJXKGLNQUCUHUAVKVLGHKPQRTUDUEUCVMZGHJPQRTUDUEUCVMZVNVOXTXRXLNY
        HYIXTXRVDYKUFCFGLQUCUHUIVPVQVRVSYEYFXSXNVDZXDXGYJXLQUNZYNYHYEYFYOYKXJXK
        GLQUCUHVTWAYOXNXRUPXMFURZNURZXSYOXMQUNZXNYQVDZYHXIWBUNZYOYRYKUPWCWDWEZX
        IXLFGQUCUIWFVLUPWBUNZYIYRYSWCUFYHUUBYIYRWGYSYKUPCXMFGNQUCUAUIWHWIVLWJYO
        YPXLXRNYOUPXIUQURZXLFURZYPXLUUBYTYOUUDYPVDZWCUUAYHUUBYTYOWGUUEYKUPXIXLF
        GQUCUIWKWIVLYOUSXLFURZXLUUDYHYOUUFXLVDYKXLFGQUCUIWLWIUUCUSXLFUPWCWDWMVR
        WNWOWPWQWJYLYMVNWQWRWS $.
        $( [?] $) $( [17-Feb-2008] $)
    $}

    ${
      minvec20.h $e |- ( j e. NN -> ( H ` j ) = ( A M ( f ` j ) ) ) $.
      minvec20.f $e |- ( j e. NN -> ( F ` j ) = ( N ` ( H ` j ) ) ) $.
      $( Lemma for ~ mnvc3 . $)
      minveclem20 $p |- ( ( f : NN --> Y /\ n e. NN ) -> ( F ` n ) e. RR ) $=
        ( cn cv wf wcel wa cfv co cr wceq minveclem7 minveclem6 fveq2d eqtrd
        adantl minveclem4 cnv phnvi nvmcl mp3an12 nvcl mpan syl eqeltrd ) UFOFU
        GZUHZHUGZUFUIZUJZVKIUKZCVKVIUKZKULZLUKZUMVLVNVQUNVJVLVNVKJUKZLUKVQGHIJL
        UEUOVLVRVPLCFGHJKUDUPUQURUSVMVONUIZVQUMUIZEFHMNOQUAUBTUTVSVPNUIZVTEVAUI
        ZCNUIVSWAEQVBZUCCVOEKNTRVCVDWBWAVTWCVPELNTSVEVFVGVGVH $.
        $( [?] $) $( [17-Feb-2008] $)

      minvec21.2 $e |- P = -u sup ( R , RR , < ) $.
      $( Lemma for ~ mnvc3 . $)
      minveclem21 $p |- ( ( ( f : NN --> Y /\ n e. NN ) /\ ( f : NN --> Y /\
        m e. NN ) ) -> ( ( N ` ( ( f ` n ) M ( f ` m ) ) ) ^ 2 ) <_ ( ( 2 x.
        ( ( ( F ` n ) ^ 2 ) + ( ( F ` m ) ^ 2 ) ) ) - ( ( 2 x. P ) ^ 2 ) ) ) $=
        ( cn cv wf wcel wa cfv co c2 cexp caddc cmul cpv cmin cle wceq
        minveclem7 opreq1d opreqan12d opreq2d ad2ant2l eqid minveclem18 eqtr2d
        wbr cr cc0 2re minveclem12 remulcl 0re 2pos ltlei minveclem14 mulge0
        mp2an le2sqit mpanl12 cnv phnvi nvgcl mp3an1 nvcl mpan syl minveclem17
        syl2an cns minveclem19 sylanc wb resqcl lesub2t resqclt axaddrcl
        axmulrcl minveclem20 mpbid eqbrtrd ) UIQGUJZUKZJUJZUIULZUMZXHIUJZUIULZU
        MZUMZXIXGUNXLXGUNMUONUNUPUQUOZUPXIKUNZUPUQUOZXLKUNZUPUQUOZURUOZUSUOZXIL
        UNZXLLUNZFUTUNZUOZNUNZUPUQUOZVAUOZYBUPDUSUOZUPUQUOZVAUOZVBXOYIUPYCNUNZU
        PUQUOZYDNUNZUPUQUOZURUOZUSUOZYHVAUOZXPXJXMYIYSVCXHXHXJXMUMZYBYRYHVAYTYA
        YQUPUSXJXMXRYNXTYPURXJXQYMUPUQHJKLNUGVDVEXMXSYOUPUQHIKLNUGVDVEVFVGVEVHA
        BCEFGHIJYELMNOPQRSTUAUBUCUDUEUFYEVIZVJVKXOYKYHVBVLZYIYLVBVLZYGVMULZYJYG
        VBVLZUUBXOYJVMULVNYJVBVLZUUDUUEUMUUBUPDVOABCDEFMNOPQRSTUAUBUCUDUEUHVPZV
        QZVNUPVBVLVNDVBVLUUFVNUPVRVOVSVTABCDEFMNOPQRSTUAUBUCUDUEUHWAUPDVOUUGWBW
        CYJYGWDWEYCPULZYDPULZUUDXKXNUUIUUJUMZYFPULZUUDFWFULZUUIUUJUULFSWGZYCYDF
        YEPUBUUAWHWIUUMUULUUDUUNYFFNPUBUAWJWKWLZABCEFGHJLMNOPQRSTUAUBUCUDUEUFWM
        ZABCEFGHILMNOPQRSTUAUBUCUDUEUFWMZWNABCDEFWOUNZFGHIJYELMNOPQRSTUAUBUCUDU
        EUFUUAUURVIUHWPWQYHVMULZYBVMULZUUBUUCWRZXOYKVMULUUSUUTUVAYJUUHWSYKYHYBW
        TWIUUIUUJUUSXKXNUUKUUDUUSUUOYGXAWLUUPUUQWNXQVMULZXSVMULZUUTXKXNUVBUVCUM
        YAVMULZUUTXRVMULXTVMULUVDUVBUVCXRXTXBXQXAXSXAWNUPVMULUVDUUTVOUPYAXCWKWL
        ABCEFGHJKLMNOPQRSTUAUBUCUDUEUFUGXDABCEFGHIKLMNOPQRSTUAUBUCUDUEUFUGXDWNW
        QXEXF $.
        $( [?] $) $( [7-Mar-2008] $)
    $}

    ${
      minvec22.hf $e |- ( j e. NN -> ( F ` j ) = ( N ` ( A M ( f ` j ) ) ) ) $.
      $( Lemma for ~ mnvc3 . $)
      minveclem22 $p |- ( ( f : NN --> Y /\ n e. NN ) -> ( F ` n ) e. RR ) $=
        ( cn cv wf wcel wa cfv co cr wceq minveclem8 adantl minveclem4 cnv
        phnvi nvmcl mp3an12 nvcl mpan syl eqeltrd ) UDNFUEZUFZHUEZUDUGZUHZVFIUI
        ZCVFVDUIZJUJZKUIZUKVGVIVLULVECFGHIJKUCUMUNVHVJMUGZVLUKUGZEFHLMNPTUASUOV
        MVKMUGZVNEUPUGZCMUGVMVOEPUQZUBCVJEJMSQURUSVPVOVNVQVKEKMSRUTVAVBVBVC $.
        $( [?] $) $( [17-Feb-2008] $)

      minvec23.2 $e |- P = -u sup ( R , RR , < ) $.
      $( Lemma for ~ mnvc3 .  Eliminate ` H ` . $)
      minveclem23 $p |- ( ( ( f : NN --> Y /\ n e. NN ) /\ ( f : NN --> Y /\
        m e. NN ) ) -> ( ( N ` ( ( f ` n ) M ( f ` m ) ) ) ^ 2 ) <_ ( ( 2 x.
        ( ( ( F ` n ) ^ 2 ) + ( ( F ` m ) ^ 2 ) ) ) - ( ( 2 x. P ) ^ 2 ) ) ) $=
        ( vk vu cv cn wcel cfv co wceq wa copab fveq2 opreq2d eqid oprex
        fvopab4 fveq2d eqcomd eqtrd minveclem21 ) ABCDEFGHIJKUGUIZUJUKUHUICVFGU
        IZULZLUMZUNUOUGUHUPZLMNOPQRSTUAUBUCUDUGUHHUIZVICVKVGULZLUMZUJVJVFVKUNVH
        VLCLVFVKVGUQURVJUSCVLLUTVAZVKUJUKZVKKULVMMULZVKVJULZMULZUEVOVRVPVOVQVMM
        VNVBVCVDUFVE $.
        $( [?] $) $( [8-Mar-2008] $)

      $( Use F - P g.e. 0 instead of abs for lt2sqt?  check hyp chaining $)
      $( Lemma for ~ mnvc3 . $)
      minveclem24 $p |- ( ( f : NN --> Y /\ F ~~> P /\ B e. RR+ ) ->
         E. k e. NN A. n e. NN ( k <_ n -> ( ( ( F ` n ) - P ) ^ 2 ) < B ) ) $=
        ( cn cv wf cli wbr crp wcel w3a cle cfv cmin co cabs csqr clt wi wral
        wrex c2 cexp minveclem15 rpsqrclt sylan2 3adant1 wa wb cr cc0 lt2sqt
        cc recnt absclt syl absge0t jca adantr sqrclt sqrge0t syl2an absresqt
        breq1d ad2antrr wceq sqsqrt adantl breq2d 3bitrd minveclem22
        minveclem12 resubclt mpan2 ancri rpret rpge0t an1rs 3adantl2 imbi2d
        ralbidva rexbidv mpbid ) UHQHUIUJZLEUKULZDUMUNZUOZJUIKUIZUPULZXLLUQZEUR
        USZUTUQZDVAUQZVBULZVCZKUHVDZJUHVEZXMXOVFVGUSZDVBULZVCZKUHVDZJUHVEXIXJYA
        XHXIXQUMUNYAXJABCXQEFGJKLMNOPQRSTUAUBUCUDUEUGVHDVIVJVKXKXTYEJUHXKXSYDKU
        HXKXLUHUNZVLXRYCXMXHXJYFXRYCVMZXIXHYFXJYGXOVNUNZXHYFVLZVLZDVNUNZVODUPUL
        ZVLZYGYIXJYJYMVLZXRXPVFVGUSZXQVFVGUSZVBULZYBYPVBULZYCXPVNUNZVOXPUPULZVL
        ZXQVNUNZVOXQUPULZVLXRYQVMYJYMXPXQVPYHUUAYIYHYSYTYHXOVQUNZYSXOVRZXOVSVTY
        HUUDYTUUEXOWAVTWBWCYMUUBUUCDWDDWEWBWFYHYQYRVMYIYMYHYOYBYPVBXOWGWHWIYNYP
        DYBVBYMYPDWJYJDWKWLWMWNYIYHYIXNVNUNZYHABCFGHIKLMNOPQRSTUAUBUCUDUEUFWOUU
        FEVNUNYHABCEFGMNOPQRSTUAUBUCUDUEUGWPXNEWQWRVTWSXJYKYLDWTDXAWBWFXBXCXDXE
        XFXG $.
        $( [?] $) $( [21-Feb-2008] $)

      $( Lemma for ~ mnvc3 . $)
      minveclem25 $p |- ( ( f : NN --> Y /\ F ~~> P /\ B e. RR+ ) ->
          E. k e. NN A. n e. NN
                ( k <_ n -> ( ( 2 x. P ) x. ( ( F ` n ) - P ) ) < B ) ) $=
        ( cn cv wf cli wbr crp wcel w3a cle cfv cmin co cabs c2 cmul c1 caddc
        cdiv clt wi wral wrex minveclem15 2re minveclem12 remulcl 1re readdcl
        cc0 0re 2pos ltlei minveclem14 mulge0 mp2an lt01 addgegt0 elrpi
        rpdivclt mpan2 sylan2 3adant1 wa cr wb ltmuldiv2t mp3an1 cc resubclt
        recnd absclt syl rpret syl2an leabst ltp1 pm3.2i lemul12ait mpanl12
        absge0t jca sylanc mpani mpd adantr lelttrt axmulrcl mpan adantl
        syl3anc mpand sylbird minveclem22 sylan an1rs 3adantl2 imim2d
        r19.20dva r19.22sdv ) UHQHUIUJZLEUKULZDUMUNZUOZJUIKUIZUPULZYKLUQZEURUSZ
        UTUQZDVAEVBUSZVCVDUSZVEUSZVFULZVGZKUHVHZJUHVIZYLYPYNVBUSZDVFULZVGZKUHVH
        ZJUHVIYHYIUUBYGYHYRUMUNZUUBYIABCYREFGJKLMNOPQRSTUAUBUCUDUEUGVJYIYQUMUNU
        UGYQYPVCVAEVKABCEFGMNOPQRSTUAUBUCUDUEUGVLZVMZVNVOZVPYPUPULZVPVCVFULVPYQ
        VFULZVPVAUPULVPEUPULUUKVPVAVQVKVRVSABCEFGMNOPQRSTUAUBUCUDUEUGVTVAEVKUUH
        WAWBZWCYPVCUUIVNWDWBZWEDYQWFWGWHWIYJUUAUUFJUHYJYTUUEKUHYJYKUHUNZWJYSUUD
        YLYGYIUUOYSUUDVGZYHYGUUOYIUUPYMWKUNZYIUUPYGUUOWJUUQYIWJZYSYQYOVBUSZDVFU
        LZUUDYOWKUNZDWKUNZUUTYSWLZUUQYIYQWKUNZUVAUVBUVCUUJUVDUVAUVBUOUULUVCUUNY
        QYODWMWGWNUUQYNWOUNZUVAUUQYNUUQEWKUNYNWKUNZUUHYMEWPWGZWQZYNWRWSZDWTZXAU
        URUUCUUSUPULZUUTUUDUUQUVKYIUUQYNYOUPULZUVKUUQUVFUVLUVGYNXBWSUUQYPYQUPUL
        ZUVLUVKYPYQUUIUUJYPUUIXCVSUVFUVAVPYOUPULZWJZUVMUVLWJUVKVGZUUQYPWKUNZUUK
        WJUVDUVFUVOWJUVPUVQUUKUUIUUMXDUUJYPYQYNYOXEXFUVGUUQUVAUVNUVIUUQUVEUVNUV
        HYNXGWSXHXIXJXKXLUUCWKUNZUUSWKUNZUVBUVKUUTWJUUDVGUURUUCUUSDXMUUQUVRYIUU
        QUVFUVRUVGUVQUVFUVRUUIYPYNXNXOWSXLUUQUVSYIUUQUVAUVSUVIUVDUVAUVSUUJYQYOX
        NXOWSXLYIUVBUUQUVJXPXQXRXSABCFGHIKLMNOPQRSTUAUBUCUDUEUFXTYAYBYCYDYEYFXK
        $.
        $( [?] $) $( [21-Feb-2008] $)

      $( ?? r19.22dv/r19.20dva could be combined w mnvc3lemm3OLD ? $)
      $( Lemma for ~ mnvc3 . $)
      minveclem26 $p |- ( ( f : NN --> Y /\ F ~~> P /\ B e. RR+ ) -> E. k e. NN
            A. n e. NN ( k <_ n -> ( ( ( F ` n ) ^ 2 ) - ( P ^ 2 ) ) < B ) ) $=
        ( cn cv wf cli wbr crp wcel w3a cle cfv cmin co c2 cexp cdiv clt cmul
        wa wi wral wrex minveclem24 minveclem25 jca c1 1z nnuz cvganuz sylib
        2re 2pos elrpi rpdivclt mpan2 syl3an3 cr caddc lt2addt minveclem12
        resubclt resqclt syl remulcl axmulrcl mpan rehalfclt syl2an cc wceq
        recnt recn subsq2t 2halvest eqcomd breqan12d sylibrd minveclem22 rpret
        an1rs 3adantl2 imim2d r19.20dva r19.22sdv mpd ) UHQHUIUJZLEUKULZDUMUNZU
        OZJUIKUIZUPULZXPLUQZEURUSZUTVAUSZDUTVBUSZVCULZUTEVDUSZXSVDUSZYAVCULZVEZ
        VFZKUHVGZJUHVHZXQXRUTVAUSEUTVAUSURUSZDVCULZVFZKUHVGZJUHVHXLXMYAUMUNZYIX
        NXLXMYNUOZXQYBVFKUHVGJUHVHZXQYEVFKUHVGJUHVHZVEYIYOYPYQABCYAEFGHIJKLMNOP
        QRSTUAUBUCUDUEUFUGVIABCYAEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGVJVKYBYEJKVLUHVM
        VNVOVPXNUTUMUNYNUTVQVRVSDUTVTWAWBXOYHYMJUHXOYGYLKUHXOXPUHUNZVEYFYKXQXLX
        NYRYFYKVFZXMXLYRXNYSXRWCUNZDWCUNZYSXLYRVEXNYTUUAVEYFXTYDWDUSZYAYAWDUSZV
        CULZYKXTWCUNZYDWCUNZVEYAWCUNZUUGVEYFUUDVFYTUUAXTYDYAYAWEYTUUEUUFYTXSWCU
        NZUUEYTEWCUNUUHABCEFGMNOPQRSTUAUBUCUDUEUGWFZXREWGWAZXSWHWIYTUUHUUFUUJYC
        WCUNUUHUUFUTEVQUUIWJYCXSWKWLWIVKUUAUUGUUGDWMZUUKVKWNYTUUAYJUUBDUUCVCYTX
        RWOUNZYJUUBWPZXRWQUULEWOUNUUMEUUIWRXREWSWAWIUUAUUCDUUADWOUNUUCDWPDWQDWT
        WIXAXBXCABCFGHIKLMNOPQRSTUAUBUCUDUEUFXDDXEWNXFXGXHXIXJXK $.
        $( [?] $) $( [23-Feb-2008] $)

      $( Lemma for ~ mnvc3 . $)
      minveclem27 $p |- ( ( ( f : NN --> Y /\ F ~~> P ) /\ B e. RR+ ) ->
         E. k e. NN A. m e. NN ( k <_ m ->
               ( ( N ` ( ( f ` k ) M ( f ` m ) ) ) ^ 2 ) < B ) ) $=
        ( cn cv wf cli wbr wa crp wcel cle cfv c2 cexp co cmin cdiv clt wi
        wral wrex minveclem26 2re 2pos elrpi rpdivclt mpan2 syl syl3an3 3expa
        cr nnret leidt wceq breq2 fveq2 opreq1d breq1d imbi12d rcla4v mpid
        adantl ancrd r19.28av syl6 pm3.43 ax-1 sylan caddc cmul lt2addt
        resqclt minveclem12 resqcl resubclt anim12i rpret rehalfclt jca syl2an
        cc recn pm3.2i addsub4t recnd 2times opreq2i syl5req halfclt 2halvest
        breqan12d wb w3a cc0 ltmul2t mp3an3 axaddrcl remulcl axaddcl 2cn
        subdit mp3an13 sqmul c4 sq2 2t2e4 eqtr4 opreq1i mulass 3eqtrr syl6eq
        wne 2ne0 divcan2t 3bitrd sylibd minveclem22 anandis an1rs minveclem23
        adantlr lelttrt cnv phnvi nvmcl mp3an1 minveclem4 nvcl mpan axmulrcl
        ad2antlr syl3anc mpand syld anassrs syl9r r19.20dva adantllr r19.22dva
        mpd ) UHQHUIZUJZLEUKULZUMDUNUOZUMZJUIZKUIZUPULZUVLLUQZURUSUTZEURUSUTZVA
        UTZDURVBUTZURVBUTZVCULZVDZKUHVEZJUHVFZUVMUVKUVFUQZUVLUVFUQZMUTZNUQZURUS
        UTZDVCULZVDZKUHVEZJUHVFUVGUVHUVIUWCUVGUVHUVSUNUOZUWCUVIABCUVSEFGHIJKLMN
        OPQRSTUAUBUCUDUEUFUGVGUVIUVRUNUOZUWLUVIURUNUOZUWMURVHVIVJZDURVKVLUWMUWN
        UWLUWOUVRURVKVLVMVNVOUVJUWBUWKJUHUVJUVKUHUOZUMZUWBUVKLUQZURUSUTZUVPVAUT
        ZUVSVCULZUWAUMZKUHVEZUWKUWQUWBUXAUWBUMUXCUWQUWBUXAUWPUWBUXAVDUVJUWPUWBU
        VKUVKUPULZUXAUWPUVKVPUOUXDUVKVQUVKVRVMUWAUXDUXAVDKUVKUHUVLUVKVSZUVMUXDU
        VTUXAUVLUVKUVKUPVTUXEUVQUWTUVSVCUXEUVOUWSUVPVAUXEUVNUWRURUSUVLUVKLWAWBW
        BWCWDWEWFWGWHUXAUWAKUHWIWJUVGUVIUWPUXCUWKVDUVHUVGUVIUMZUWPUMZUXBUWJKUHU
        XBUVMUXAUVTUMZUXGUVLUHUOZUMUWIUVMUXAVDUWAUVMUXHVDUXAUVMUXAUVTWKUXAUVMWL
        WMUXFUWPUXIUXHUWIVDUXFUWPUXIUMZUMZUXHURUWSUVOWNUTZWOUTZUREWOUTZURUSUTZV
        AUTZDVCULZUWIUVGUXJUVIUXHUXQVDZUWRVPUOZUVNVPUOZUMZUVIUXRUVGUXJUMZUYAUVI
        UMZUXHUWTUVQWNUTZUVSUVSWNUTZVCULZUXQUWTVPUOZUVQVPUOZUMUVSVPUOZUYIUMZUXH
        UYFVDUYAUVIUWTUVQUVSUVSWPUXSUYGUXTUYHUXSUWSVPUOZUYGUWRWQZUYKUVPVPUOZUYG
        EABCEFGMNOPQRSTUAUBUCUDUEUGWRZWSZUWSUVPWTVLVMUXTUVOVPUOZUYHUVNWQZUYPUYM
        UYHUYOUVOUVPWTVLVMXAUVIDVPUOZUYJDXBZUYRUVRVPUOZUYJDXCZUYTUYIUYIUVRXCZVU
        BXDVMVMXEUYCUYFUXLURUVPWOUTZVAUTZUVRVCULZURVUDWOUTZURUVRWOUTZVCULZUXQUY
        AUVIUYDVUDUYEUVRVCUYAUXLUVPUVPWNUTZVAUTZUYDVUDUWSXFUOZUVOXFUOZVUJUYDVSZ
        UXSUXTVUKVULUMUVPXFUOZVUNUMVUMVUNVUNUVPUYOXGZVUOXHUWSUVOUVPUVPXIVLUXSUW
        SUYLXJZUXTUVOUYQXJZXEVUCVUIUXLVAUVPVUOXKXLXMUVIUVRXFUOZUYEUVRVSUVIDXFUO
        ZVURUVIDUYSXJZDXNVMUVRXOVMXPVUDVPUOZUYTVUEVUHXQZUYAUVIVVAUYTURVPUOZVVBV
        HVVAUYTVVCXRXSURVCULVVBVIVUDUVRURXTVLYAUYAUXLVPUOZVVAUYKUYPVVDUXSUXTUWS
        UVOYBZUYLUYQXEVVDVUCVPUOVVAURUVPVHUYOYCZUXLVUCWTVLVMUVIUYRUYTUYSVUAVMXE
        UYAUVIVUFUXPVUGDVCUYAVUFUXMURVUCWOUTZVAUTZUXPUYAUXLXFUOZVUFVVHVSZVUKVUL
        VVIUXSUXTUWSUVOYDVUPVUQXEURXFUOZVVIVUCXFUOVVJYEVUCVVFXGURUXLVUCYFYGVMVV
        GUXOUXMVAUXOURURUSUTZUVPWOUTURURWOUTZUVPWOUTVVGUREYEEUYNXGYHVVLVVMUVPWO
        VVLYIVVMYJYKYLYMURURUVPYEYEVUOYNYOXLYPUVIVUSVUGDVSZVUTVVKVUSURXSYQVVNYE
        YRURDYSYGVMXPYTUUAUVGUWPUXIUYAUVGUWPUMZUXSUVGUXIUMZUXTABCFGHIJLMNOPQRST
        UAUBUCUDUEUFUUBZABCFGHIKLMNOPQRSTUAUBUCUDUEUFUUBZXAUUCWMUUDUXKUWHUXPUPU
        LZUXQUWIUVGUXJVVSUVIUVGUWPUXIVVSABCEFGHIKJLMNOPQRSTUAUBUCUDUEUFUGUUEUUC
        UUFUWHVPUOZUXPVPUOZUYRVVSUXQUMUWIVDUXKUWHUXPDUUGUVGUXJVVTUVIUYBUWGVPUOZ
        VVTUYBUWFPUOZVWBUVGUWPUXIVWCUWDPUOZUWEPUOZVWCVVOVVPGUUHUOZVWDVWEVWCGSUU
        IZUWDUWEGMPUBTUUJUUKGHJOPQSUCUDUBUULGHKOPQSUCUDUBUULXEUUCVWFVWCVWBVWGUW
        FGNPUBUAUUMUUNVMUWGWQVMUUFUVGUXJVWAUVIUYBUXMVPUOZVWAUYBVVDVWHUVGUWPUXIV
        VDUYKUYPVVDVVOVVPVVEVVOUXSUYKVVQUYLVMVVPUXTUYPVVRUYQVMXEUUCVVCVVDVWHVHU
        RUXLUUOUUNVMVWHUXOVPUOVWAUXNUREVHUYNYCWSUXMUXOWTVLVMUUFUVIUYRUVGUXJUYSU
        UPUUQUURUUSUUTUVAUVBUVCUUSUVDUVE $.
        $( [?] $) $( [23-Feb-2008] $)

      minvec28.d $e |- D = ( IndMet ` W ) $.
      $( Lemma for ~ mnvc3 . $)
      minveclem28 $p |- ( ( ( f : NN --> Y /\ F ~~> P ) /\ B e. RR+ ) ->
         E. k e. NN A. m e. NN ( k <_ m -> ( ( f ` k ) D ( f ` m ) ) < B ) ) $=
        ( cn cv wf cli wbr wa crp wcel cle cfv co c2 cexp clt wi wral wrex
        minveclem27 cn0 2nn0 rpexpclt mpan2 sylan2 wb wceq cnsb cnm cnv css
        phnvi eqid sspnv mp2an imsdval mp3an1 nvmcl sspnval mp3an12 syl
        sspmval mpanl12 fveq2d 3eqtrd adantr breq1d cr cc0 lt2sqt nvcl mpan
        nvge0 jca wss sspba sseli syl2an rpret rpge0t bitr2d ffvelrn anim12i
        anandis sylan an1rs adantllr anassrs imbi2d ralbidva rexbidva mpbid )
        UJRIUKZULZMFUMUNZUOZDUPUQZUOZKUKZLUKZURUNZYFXTUSZYGXTUSZNUTZOUSZVAVBUTD
        VAVBUTZVCUNZVDZLUJVEZKUJVFZYHYIYJEUTZDVCUNZVDZLUJVEZKUJVFYCYMUPUQZYQYDA
        BCYMFGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHVGYDVAVHUQUUBVIDVAVJVKVLYEYPUUAKUJYE
        YFUJUQZUOZYOYTLUJUUDYGUJUQZUOYNYSYHYEUUCUUEYNYSVMZYAYDUUCUUEUOZUUFYBYAU
        UGYDUUFYIRUQZYJRUQZUOZYDUUFYAUUGUOUUJYDUOZYSYLDVCUNZYNUUKYRYLDVCUUJYRYL
        VNYDUUJYRYIYJPVOUSZUTZPVPUSZUSZUUNOUSZYLPVQUQZUUHUUIYRUUPVNHVQUQZPHVRUS
        ZUQZUURHTVSZUDHUUTPUUTVTZWAWBZYIYJEPUUMUUORUEUUMVTZUUOVTZUIWCWDUUJUUNRU
        QZUUPUUQVNZUURUUHUUIUVGUVDYIYJPUUMRUEUVEWEWDUUSUVAUVGUVHUVBUDUUNHUUTUUO
        OPRUEUBUVFUVCWFWGWHUUJUUNYKOUUSUVAUUJUUNYKVNUVBUDYIYJHUUTUUMNPRUEUAUVEU
        VCWIWJWKWLWMWNYLWOUQZWPYLURUNZUOZDWOUQZWPDURUNZUOUULYNVMUUJYDYLDWQYIQUQ
        ZYJQUQZUVKUUHUUIUVNUVOUOYKQUQZUVKUUSUVNUVOUVPUVBYIYJHNQUCUAWEWDUVPUVIUV
        JUUSUVPUVIUVBYKHOQUCUBWRWSUUSUVPUVJUVBYKHOQUCUBWTWSXAWHRQYIUUSUVARQXBUV
        BUDHUUTPQRUCUEUVCXCWBZXDRQYJUVQXDXEYDUVLUVMDXFDXGXAXEXHYAUUCUUEUUJYAUUC
        UOUUHYAUUEUOUUIUJRYFXTXIUJRYGXTXIXJXKXLXMXNXOXPXQXRXS $.
        $( [?] $) $( [23-Feb-2008] $)

      minvec29.w2 $e |- W e. CBan $.
      $( Lemma for ~ mnvc3 .  Sequence ` f ` is Cauchy, and since vector
         subspace ` W ` is complete, ` f ` therefore converges to a vector in
         ` W ` . $)
      minveclem29 $p |- ( ( f : NN --> Y /\ F ~~> P ) ->
                E. a e. Y f ( ~~>m ` D ) a ) $=
        ( vk vm cn cv wf cli wbr wa cca cfv wcel clm wrex cle co clt wi wral
        crp minveclem28 r19.21aiva wb cme cnv css phnvi eqid sspnv mp2an
        imsmet ax-mp imsbai iscau5 mpan adantr mpbird cms cbn bncms bnnv
        cmscvg syl ) UKOHULZUMZJEUNUOZUPZWKDUQURUSZWKPULZDUTURUOPOVAZWNWOUIULZU
        JULZVBUOWRWKURWSWKURDVCWPVDUOVEUJUKVFUIUKVAZPVGVFZWNWTPVGABCWPDEFGHIUIU
        JJKLMNOQRSTUAUBUCUDUEUFUGVHVIWLWOXAVJZWMDVKUSZWLXBMVLUSZXCGVLUSMGVMURZU
        SXDGRVNUBGXEMXEVOVPVQZDMUGVRVSPDUIUJWKODMOUCUGXFVTWAWBWCWDDWEUSZWOWQMWF
        USZXGUHDMUGWGVSPDWKODMOUCUGXHXDUHMWHVSVTWIWBWJ $.
        $( [?] $) $( [24-Feb-2008] $)
    $}

    ${
      $d m A $.  $d u D $.  $d k m u J $.  $d k m u M $.  $d k m u N $.
      $d m T $.  $d u Y $.  $d f u $.
      minvec30.2 $e |- P = -u sup ( R , RR , < ) $.
      minvec30.hf $e |- ( j e. NN -> ( F ` j ) = ( N ` ( A M ( f ` j ) ) ) ) $.
      minvec30.d $e |- D = ( IndMet ` W ) $.
      minvec30.jv $e |- J e. V $.
      minvec30.j $e |- ( m e. NN -> ( J ` m ) = ( N ` ( ( f ` m ) M a ) ) ) $.
      minvec30.fv $e |- F e. V $.
      minvec30.tv $e |- T e. V $.
      minvec30.t $e |- ( m e. NN -> ( T ` m ) =
                         ( ( F ` m ) + ( N ` ( ( f ` m ) M a ) ) ) ) $.
      $( Lemma for ~ mnvc3 . $)
      minveclem30 $p |- ( ( ( f : NN --> Y /\ f ( ~~>m ` D ) a ) /\
          ( F ~~> P /\ a e. Y ) ) -> T ~~> P ) $=
        ( cn cv wf clm cfv wbr wa cli wcel cc0 caddc co cc wceq w3a wral c1
        cuz cz 1z cr minveclem12 elisseti 0cn climadd mpanr1 wb nnuz raleq1
        ax-mp sylan2b simprl minveclem9 adantrl jca minveclem22 recnd adantlr
        adantl cnv phnvi nvmcl mp3an1 nvcl mpan syl minveclem4 minveclem3
        sseli syl2an an1rs eqeltrd adantllr adantlrl opreq2d eqtr4d 3jca
        r19.21aiva sylanc recn addid1 syl6breq ) UPRIUQZURZXRSUQZDUSUTVAZVBZLEV
        CVAZXTRVDZVBZVBZGEVEVFVGZEVCYCMVEVCVAZVBZKUQZLUTZVHVDZYJMUTZVHVDZYJGUTZ
        YKYMVFVGZVIZVJZKUPVKZGYGVCVAZYFYIYRKVLVMUTZVKZYTYSYIVLVNVDUUBYTVOEVEKLM
        GVLUMUKUNEVPABCEFHNOPQRTUAUBUCUDUEUFUGUHVQZVRVEVHVSVRVTWAUPUUAVIYSUUBWB
        WCYRKUPUUAWDWEWFYFYCYHYBYCYDWGYBYDYHYCDHIKMNOPQRSUAUBUCUDUEUFUJUKULWHWI
        WJYFYRKUPYFYJUPVDZVBYLYNYQYBUUDYLYEXSUUDYLYAXSUUDVBZYKABCFHIJKLNOPQRTUA
        UBUCUDUEUFUGUIWKWLWMWMYBYDUUDYNYCXSYDUUDYNYAXSYDVBZUUDVBZYMUUGYMYJXRUTZ
        XTNVGZOUTZVPUUDYMUUJVIUUFULWNXSUUDYDUUJVPVDZUUHQVDZXTQVDZUUKUUEYDUULUUM
        VBUUIQVDZUUKHWOVDZUULUUMUUNHUAWPZUUHXTHNQUDUBWQWRUUOUUNUUKUUPUUIHOQUDUC
        WSWTXAHIKPQRUAUEUFUDXBRQXTHPQRUAUEUFUDXCXDXEXFXGWLXHXIUUDYQYFUUDYOYKUUJ
        VFVGYPUOUUDYMUUJYKVFULXJXKWNXLXMXNEEUUCXOXPXQ $.
        $( [?] $) $( [28-Feb-2008] $)

      $( Lemma for ~ mnvc3 . $)
      minveclem31 $p |- ( ( ( f : NN --> Y /\ f ( ~~>m ` D ) a ) /\
          ( F ~~> P /\ a e. Y ) ) -> ( N ` ( A M a ) ) <_ P ) $=
        ( cv co cfv cr wcel cli wbr wa c1 cz cle cuz wral cn wf clm fvex
        minveclem12 elisseti climcmpc1 minveclem5 ad2antll minveclem30 jca
        caddc wceq adantl axaddrcl minveclem22 adantlr cnv phnvi nvmcl mp3an1
        nvcl mpan syl minveclem4 minveclem3 sseli syl2an an1rs adantlrl
        adantllr sylanc eqeltrd w3a nvmtri2 minveclem8 ad2antlr opreq1d
        breqtrrd elnnuz sylan2br r19.21aiva 1z jctil ) CSUPZNUQZOURZUSUTZGEVAVB
        ZVCVDVEUTZKUPZGURZUSUTZXOXTVFVBZVCZKVDVGURZVHZVCXOEVFVBVIRIUPZVJZYFXMDV
        KURVBZVCZLEVAVBZXMRUTZVCZVCZXOEKGVDUNXNOVLEUSABCEFHNOPQRTUAUBUCUDUEUFUG
        UHVMVNVOYMXPXQYKXPYIYJSCHNOPQRUAUEUFUDUBUCUGVPVQABCDEFGHIJKLMNOPQRSTUAU
        BUCUDUEUFUGUHUIUJUKULUMUNUOVRVSYMYEXRYMYCKYDYMXSVIUTZYCXSYDUTYMYNVCZYAY
        BYOXTXSLURZXSYFURZXMNUQZOURZVTUQZUSYNXTYTWAYMUOWBZYPUSUTZYSUSUTZYTUSUTY
        OYPYSWCYIYNUUBYLYGYNUUBYHABCFHIJKLNOPQRTUAUBUCUDUEUFUGUIWDWEWEYGYLYNUUC
        YHYGYKYNUUCYJYGYNYKUUCYQQUTZXMQUTZUUCYGYNVCZYKUUDUUEVCYRQUTZUUCHWFUTZUU
        DUUEUUGHUAWGZYQXMHNQUDUBWHWIUUHUUGUUCUUIYRHOQUDUCWJWKWLHIKPQRUAUEUFUDWM
        ZRQXMHPQRUAUEUFUDWNWOZWPWQWRWSWTXAYOXOYTXTVFYGYLYNXOYTVFVBZYHYGYKYNUULY
        JYGYNYKUULUUFYKVCZXOCYQNUQOURZYSVTUQZYTVFUUDUUEXOUUOVFVBZUUFYKCQUTZUUDU
        UEUUPUGUUHUUQUUDUUEXBUUPUUICYQXMHNOQUDUBUCXCWKWIUUJUUKWPUUMYPUUNYSVTYNY
        PUUNWAYGYKCIJKLNOUIXDXEXFXGWQWRWSUUAXGVSXSXHXIXJXKXLWT $.
        $( [?] $) $( [28-Feb-2008] $)

      $( Lemma for ~ mnvc3 . $)
      minveclem32 $p |- ( ( ( f : NN --> Y /\ f ( ~~>m ` D ) a ) /\
          ( F ~~> P /\ a e. Y ) ) -> ( N ` ( A M a ) ) = P ) $=
        ( cn cv wf clm cfv wbr wa cli wcel co wceq cle minveclem31 minveclem13
        ad2antll jca wb cr minveclem5 minveclem12 letri3t mpan2 syl mpbird ) UP
        RIUQZURVTSUQZDUSUTVAVBZLEVCVAZWARVDZVBVBZCWANVEOUTZEVFZWFEVGVAZEWFVGVAZ
        VBZWEWHWIABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKULUMUNUOVHWDWIWBWCAB
        CWAEFHNOPQRTUAUBUCUDUEUFUGUHVIVJVKWDWGWJVLZWBWCWDWFVMVDZWKSCHNOPQRUAUEU
        FUDUBUCUGVNWLEVMVDWKABCEFHNOPQRTUAUBUCUDUEUFUGUHVOWFEVPVQVRVJVS $.
        $( [?] $) $( [28-Feb-2008] $)
    $}

    ${
      minvec33.2 $e |- P = -u sup ( R , RR , < ) $.
      minvec33.hf $e |- ( j e. NN -> ( F ` j ) = ( N ` ( A M ( f ` j ) ) ) ) $.
      minvec33.d $e |- D = ( IndMet ` W ) $.
      minvec33.t1 $e |- F e. V $.
        $( Lemma for ~ mnvc3 . $)
      minveclem33 $p |- ( ( ( f : NN --> Y /\ f ( ~~>m ` D ) a ) /\
          ( F ~~> P /\ a e. Y ) ) -> ( N ` ( A M a ) ) = P ) $=
        ( vn vu vm cv cn wcel cfv co caddc wceq wa copab nnex eqid fopabex2
        fveq2 opreq1d fveq2d fvex fvopab4 opreq12d oprex minveclem32 ) ABCDEFUI
        ULZUMUNZUJULZVLJUOZVLHULZUOZPULZKUPZLUOZUQUPZURUSUIUJUTZGHIUKJVMVNVTURU
        SUIUJUTZKLMNOPQRSTUAUBUCUDUEUFUGUIUJUMVTWCVAWCVBZVCUIUJUKULZVTWEVPUOZVR
        KUPZLUOZUMWCVLWEURZVSWGLWIVQWFVRKVLWEVPVDVEVFZWDWGLVGVHUHUIUJUMWAWBVAWB
        VBZVCUIUJWEWAWEJUOZWHUQUPUMWBWIVOWLVTWHUQVLWEJVDWJVIWKWLWHUQVJVHVK $.
        $( [?] $) $( [28-Feb-2008] $)

      minvec34.w2 $e |- W e. CBan $.
      $( Lemma for ~ mnvc3 . $)
      minveclem34 $p |- E. a e. Y ( N ` ( A M a ) ) = P $=
        ( vu vt cn cv wf cli wbr wa wex co cfv wceq wrex cn0v minveclem5 cnv
        wcel css phnvi eqid sspnv mp2an nvzcl ax-mp minveclem10 opreq2 fveq2d
        infcvg clm minveclem29 wi minveclem33 exp43 com23 com34 imp31
        r19.22dva mpd 19.23aiv ) ULOHUMZUNZJEUOUPZUQZHURCPUMZKUSLUTEVAZPOVBZABU
        JUKCBUMZKUSZLUTCIUMWIUTZKUSZLUTFEHIJOMVCUTZQBCGKLMNORUBUCUASTUDVDMVEVFZ
        WTOVFGVEVFMGVGUTZVFXAGRVHUBGXBMXBVIVJVKMOWTUCWTVIVLVMABUJUKCFGKLMNOQRST
        UAUBUCUDVNUEUHWPWRVAWQWSLWPWRCKVOVPUFVQWLWOHWLWIWMDVRUTUPZPOVBWOABCDEFG
        HIJKLMNOPQRSTUAUBUCUDUFUEUGUIVSWLXCWNPOWJWKWMOVFZXCWNVTWJWKXCXDWNWJXCWK
        XDWNVTWJXCWKXDWNABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHWAWBWCWDWEWFWGWHVM
        $.
        $( [?] $) $( [28-Feb-2008] $)
    $}
  $}

  ${
    $d x y A $.  $d x y G $.  $d x y M $.  $d x y N $.  $d x y P $.
    $d x y S $.  $d x y U $.  $d x y W $.  $d x y Y $.  $d a b x y $.
    minvc15.x $e |- X = ( Base ` U ) $.
    minvc15.g $e |- G = ( +v ` U ) $.
    minvc15.m $e |- M = ( -v ` U ) $.
    minvc15.s $e |- S = ( .s ` U ) $.
    minvc15.n $e |- N = ( norm ` U ) $.
    minvc15.y $e |- Y = ( Base ` W ) $.
    minvc15.u $e |- U e. CPreHil $.
    minvc15.a $e |- A e. X $.
    $( Lemma for ~ mnvc3 . $)
    minvc1lem1 $p |- ( ( a e. X /\ b e. X ) -> ( N ` ( ( A M a ) G ( A M b )
         ) ) = ( 2 x. ( N ` ( A M ( ( 1 / 2 ) S ( a G b ) ) ) ) ) ) $=
      ( cv wcel wa co cfv c2 c1 cdiv cmul wceq cnv phnvi nvaddsub4 mp3an1
      mpanl12 nvgcl nv2 mp2an a1i 2cn 2ne0 recid opreq1i cc reccl w3a nvsass
      mpan mp3an12 nvsid 3eqtr3rd opreq12d nvscl nvmdi syl eqtr4d eqtr3d
      fveq2d nvmcl cr cc0 cle wbr 2re 0re 2pos ltlei nvsge0 eqtrd ) JTZHUAZKTZH
      UAZUBZAWIEUCAWKEUCDUCZFUDUEAUFUEUGUCZWIWKDUCZBUCZEUCZBUCZFUDZUEWRFUDUHUCZ
      WMWNWSFWMAADUCZWPEUCZWNWSAHUAZXDWMXCWNUIZSSCUJUAZXDXDUBWMXECRUKZAAWIWKCDE
      HLMNULUMUNWMWPHUAZXCWSUIXFWJWLXHXGWIWKCDHLMUOUMZXHXCUEABUCZUEWQBUCZEUCZWS
      XHXBXJWPXKEXBXJUIZXHXFXDXMXGSABCDHLMOUPUQURXHUEWOUHUCZWPBUCZUFWPBUCZXKWPX
      OXPUIXHXNUFWPBUEUSUTVAVBURUEVCUAZWOVCUAZXHXOXKUIZUSUEUSUTVDZXFXQXRXHVEXSX
      GUEWOWPBCHLOVFVGVHXFXHXPWPUIXGWPBCHLOVIVGVJVKXHWQHUAZWSXLUIZXFXRXHYAXGXTW
      OWPBCHLOVLVHZXQXDYAYBUSSXFXQXDYAVEYBXGUEAWQBCEHLNOVMVGVHVNVOVNVPVQWMWRHUA
      ZWTXAUIZWMYAYDWMXHYAXIYCVNXFXDYAYDXGSAWQCEHLNVRVHVNUEVSUAZVTUEWAWBZYDYEWC
      VTUEWDWCWEWFXFYFYGUBYDYEXGUEWRBCFHLOPWGUMUNVNWH $.
      $( [?] $) $( [19-Mar-2008] $)

    minvc13.w $e |- W e. ( SubSp ` U ) $.
    minvc13.2 $e |- P = -u sup ( R , RR , < ) $.
    minvc13.1 $e |- R = { x | E. y e. Y x = -u ( N ` ( A M y ) ) } $.
    $( Lemma for ~ mnvc3 . $)
    minvc1lem2 $p |- ( ( ( a e. X /\ b e. X ) /\ ( ( N ` ( A M a ) ) = P /\
    ( N ` ( A M b ) ) = P ) ) -> ( ( N ` ( a M b ) ) ^ 2 ) = ( ( 2 x. ( ( P
       ^ 2 ) + ( P ^ 2 ) ) ) - ( ( N ` ( ( A M a ) G ( A M b ) ) ) ^ 2 ) ) ) $=
      ( cv wcel wa co cfv wceq c2 cexp caddc cmul cmin cnv w3a phnvi nvnnncan1
      mpan mp3an1 fveq2d nvsub eqtr4d opreq1d opreq2d cphl phpar nvmcl mp3an12
      syl2an eqtr3d opreq1 opreqan12d sylan9eq wb cc 2cn minveclem12 resqcl
      readdcl recn mulcl subaddt sqclt cr nvcl recnd nvgcl sylanc adantr
      mpbird eqcomd ) NUGZLUHZOUGZLUHZUIZCWPIUJZJUKZDULZCWRIUJZJUKZDULZUIZUIZUM
      DUMUNUJZXIUOUJZUPUJZXAXDHUJZJUKZUMUNUJZUQUJZWPWRIUJZJUKZUMUNUJZXHXOXRULZX
      NXRUOUJZXKULZWTXGXTUMXBUMUNUJZXEUMUNUJZUOUJZUPUJZXKWTXNXAXDIUJZJUKZUMUNUJ
      ZUOUJZXTYEWTYHXRXNUOWTYGXQUMUNWTYGWRWPIUJZJUKZXQWTYFYJJCLUHZWQWSYFYJULZUC
      GURUHZYLWQWSUSYMGUBUTZCWPWRGILPRVAVBVCVDYNWQWSXQYKULYOWPWRGIJLPRTVEVCVFVG
      VHXALUHZXDLUHZYIYEULZWQWSGVIUHYPYQYRUBXAXDGHIJLPQRTVJVCYNYLWQYPYOUCCWPGIL
      PRVKVLZYNYLWSYQYOUCCWRGILPRVKVLZVMVNXGYDXJUMUPXCXFYBXIYCXIUOXBDUMUNVOXEDU
      MUNVOVPVHVQWTXSYAVRZXGXLLUHZXPLUHZUUAWTXMVSUHZXQVSUHZUUAUUBUUCXNVSUHZXRVS
      UHZUUAUUDUUEXKVSUHUUFUUGUUAUMXJVTXJXIXIDABCDEGIJKLMUFUBRTPUDUAUCUEWAWBZUU
      HWCWDWEXKXNXRWFVCXMWGXQWGVMUUBXMYNUUBXMWHUHYOXLGJLPTWIVBWJUUCXQYNUUCXQWHU
      HYOXPGJLPTWIVBWJVMYPYQUUBWQWSYNYPYQUUBYOXAXDGHLPQWKVCYSYTVMYNWQWSUUCYOWPW
      RGILPRVKVCWLWMWNWO $.
      $( [?] $) $( [19-Mar-2008] $)

    $( Lemma for ~ mnvc3 . $)
    minvc1lem3 $p |- ( ( a e. Y /\ b e. Y ) ->
              P <_ ( N ` ( A M ( ( 1 / 2 ) S ( a G b ) ) ) ) ) $=
      ( cv wcel wa c1 c2 cdiv co cfv cle wbr cpv cnv css wceq phnvi eqid
      sspgval mpanl12 minveclem1 nvgcl mp3an1 eqeltrrd cns cc 2cn 2ne0 reccl
      sspsval mpan nvscl mp3an12 syl minveclem13 ) NUGZMUHZOUGZMUHZUIZUJUKULUMZ
      VTWBHUMZFUMZMUHZDCWGIUMJUNUOUPWDWFMUHZWHWDVTWBKUQUNZUMZWFMGURUHZKGUSUNZUH
      ZWDWKWFUTGUBVAZUDVTWBGWJHWMKMUAQWJVBZWMVBZVCVDKURUHZWAWCWKMUHGKUBUDVEZVTW
      BKWJMUAWPVFVGVHWIWEWFKVIUNZUMZWGMWEVJUHZWIXAWGUTZUKVKVLVMZWLWNXBWIUIXCWOU
      DWEWFWTFGWMKMUASWTVBZWQVNVDVOWRXBWIXAMUHWSXDWEWFWTKMUAXEVPVQVHVRABCWGDEGI
      JKLMUFUBRTPUDUAUCUEVSVR $.
      $( [?] $) $( [19-Mar-2008] $)

    $( Lemma for ~ mnvc3 . $)
    minvc1lem4 $p |- ( ( ( a e. Y /\ b e. Y ) /\ ( ( N ` ( A M a ) ) = P /\
    ( N ` ( A M b ) ) = P ) ) -> ( N ` ( a M b ) ) <_ 0 ) $=
      ( cv wcel wa co cfv wceq cc0 cle wbr c2 cexp caddc cmul cmin minvc1lem2
      minveclem3 sseli anim12i sylan c4 c1 cdiv cr minveclem12 resqcl 4re 0re
      4pos ltlei pm3.2i lemul2it mp3anl3 mpanl1 cnv phnvi nvgcl mp3an1 syl2an
      cc 2cn 2ne0 reccl nvscl mp3an12 syl nvmcl nvcl mpan resqclt minveclem14
      le2sqit mpanl12 minvc1lem3 sylanc recn mulass 2t2e4 opreq1i 2times
      opreq2i 3eqtr3r syl5eqbr recnd sqmult sq2 syl6req minvc1lem1 opreq1d
      eqtr4d breqtrd wb 2re readdcl remulcl suble0t mpbird adantr eqbrtrd sq0
      syl6breqr leid le2sqt mpan2 nvge0 ) NUGZMUHZOUGZMUHZUIZCYKIUJZJUKDULCYMIU
      JZJUKDULUIZUIZYKYMIUJZJUKZUMUNUOZUUAUPUQUJZUMUPUQUJZUNUOZYSUUCUMUUDUNYSUU
      CUPDUPUQUJZUUFURUJZUSUJZYPYQHUJZJUKZUPUQUJZUTUJZUMUNYKLUHZYMLUHZUIZYRUUCU
      ULULYOABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFVAYLUUMYNUUNMLYKGKLMUBUDUAPVBZVCZML
      YMUUPVCZVDVEYOUULUMUNUOZYRYOUUSUUHUUKUNUOZYOUUHVFCVGUPVHUJZYKYMHUJZFUJZIU
      JZJUKZUPUQUJZUSUJZUUKUNYOVFUUFUSUJZUVGUUHUNUVFVIUHZUUFUVFUNUOZUVHUVGUNUOZ
      YOUUFVIUHZUVIUVJUVKDABCDEGIJKLMUFUBRTPUDUAUCUEVJZVKZUVLUVIVFVIUHZUMVFUNUO
      ZUIUVJUVKUVOUVPVLUMVFVMVLVNVOVPUUFUVFVFVQVRVSYOUVEVIUHZUVIYOUVDLUHZUVQYOU
      VCLUHZUVRYOUVBLUHZUVSUUMUUNUVTYLYNGVTUHZUUMUUNUVTGUBWAZYKYMGHLPQWBWCUUQUU
      RWDUWAUVAWEUHUVTUVSUWBUPWFWGWHUVAUVBFGLPSWIWJWKUWACLUHZUVSUVRUWBUCCUVCGIL
      PRWLWJWKUWAUVRUVQUWBUVDGJLPTWMWNWKZUVEWOWKUVQDUVEUNUOZUVJYODVIUHUMDUNUOUV
      QUWEUIUVJUVMABCDEGIJKLMUFUBRTPUDUAUCUEWPDUVEWQWRUWDABCDEFGHIJKLMNOPQRSTUA
      UBUCUDUEUFWSWTWTUPUPUSUJZUUFUSUJUPUPUUFUSUJZUSUJUVHUUHUPUPUUFWFWFUUFUVNXA
      ZXBUWFVFUUFUSXCXDUWGUUGUPUSUUFUWHXEXFXGXHYOUVGUPUVEUSUJZUPUQUJZUUKYOUWJUP
      UPUQUJZUVFUSUJZUVGYOUVEWEUHZUWJUWLULZYOUVEUWDXIUPWEUHUWMUWNWFUPUVEXJWNWKU
      WKVFUVFUSXKXDXLYOUUJUWIUPUQUUMUUNUUJUWIULYLYNCFGHIJKLMNOPQRSTUAUBUCXMUUQU
      URWDXNXOXPUUMUUNUUSUUTXQZYLYNUUOUUKVIUHZUWOUUOUUJVIUHZUWPUUOUUILUHZUWQYPL
      UHZYQLUHZUWRUUMUUNUWAUWSUWTUWRUWBYPYQGHLPQWBWCUWAUWCUUMUWSUWBUCCYKGILPRWL
      WJUWAUWCUUNUWTUWBUCCYMGILPRWLWJWDUWAUWRUWQUWBUUIGJLPTWMWNWKUUJWOWKUUHVIUH
      UWPUWOUPUUGXRUUFUUFUVNUVNXSXTUUHUUKYAWNWKUUQUURWDYBYCYDYEYFYOUUBUUEXQZYRU
      UMUUNUXAYLYNUUOYTLUHZUXAUWAUUMUUNUXBUWBYKYMGILPRWLWCUUAVIUHZUMUUAUNUOZUXA
      UXBUXCUXDUIUMVIUHZUMUMUNUOZUIUXAUXEUXFVMUMVMYGVPUUAUMYHYIUWAUXBUXCUWBYTGJ
      LPTWMWNUWAUXBUXDUWBYTGJLPTYJWNWTWKUUQUURWDYCYB $.
      $( [?] $) $( [23-Mar-2008] $)
  $}

  ${
    $d a b f j n v x y A $.  $d x y B $.  $d a b f j n v x y M $.
    $d a b f j n v x y N $.  $d a b f n x y P $.  $d a Q $.  $d a R $.
    $d x y U $.  $d a x y W $.  $d a b f n x y Y $.
    minvc1a.x $e |- X = ( Base ` U ) $.
    minvc1a.m $e |- M = ( -v ` U ) $.
    minvc1a.n $e |- N = ( norm ` U ) $.
    minvc1a.y $e |- Y = ( Base ` W ) $.
    minvc1a.1 $e |- R = { x | E. y e. Y x = -u ( N ` ( A M y ) ) } $.
    minvc1a.2 $e |- P = -u sup ( R , RR , < ) $.
    minvc1a.u $e |- U e. CPreHil $.
    minvc1a.w $e |- W e. ( ( SubSp ` U ) i^i CBan ) $.
    minvc1a.a $e |- A e. X $.
    $( Minimizing vector theorem.  There is exactly one vector in a complete
       subspace ` W ` that minimizes the distance to an arbitrary vector ` A `
       in a parent inner product space.  Theorem 3.3-1 of [Kreyszig] p. 144,
       specialized to subspaces instead of convex subsets.  Note that we work
       with the negative of supremum instead of infimum in order to use
       theorems we already have available. $)
    minvc1i $p |- E! a e. Y ( N ` ( A M a ) ) = P $=
      ( vb vf vn vj vv cv co cfv wceq wreu wrex wa wi wral opreq2 fveq2d
      eqeq1d reu4 cims cn wcel copab css cbn cin inss1 sseli ax-mp fveq2
      opreq2d eqid fvex fvopab4 nnex opabex2 inss2 minveclem34 cc0 cle wbr cns
      cpv minvc1lem4 cnv phnvi nvmcl mp3an1 nvge0 mpan syl idd mpan2d cn0v wb
      nvz cr nvcl 0re letri3t mpan2 nvmeq0 3bitr3d sylibd minveclem3 syl2an
      adantr mpd ex rgen2a mpbir2an ) CLUGZGUHZHUIZDUJZLKUKXOLKULXOCUBUGZGUHZHU
      IZDUJZUMZXLXPUJZUNZUBKUOLKUOXOXSLUBKYAXNXRDYAXMXQHXLXPCGUPUQURUSABCIUTUIZ
      DEFUCUDUEUGZVAVBUFUGCYDUCUGZUIZGUHZHUIZUJUMUEUFVCZGHIJKLQSNOMIFVDUIZVEVFZ
      VBZIYJVBTYKYJIYJVEVGVHVIZPUARUEUFUDUGZYHCYNYEUIZGUHZHUIVAYIYDYNUJZYGYPHYQ
      YFYOCGYDYNYEVJVKUQYIVLYPHVMVNYCVLUEUFVAYHVOVPYLIVEVBTYKVEIYJVEVQVHVIVRYBL
      UBKXLKVBZXPKVBZUMZXTYAYTXTUMXLXPGUHZHUIZVSVTWAZYAABCDEFWBUIZFFWCUIZGHIJKL
      UBMUUEVLNUUDVLOPSUAYMRQWDYTUUCYAUNZXTXLJVBZXPJVBZUUFYRYSUUGUUHUMZUUCUUCVS
      UUBVTWAZUMZYAUUIUUCUUJUUKUUIUUAJVBZUUJFWEVBZUUGUUHUULFSWFZXLXPFGJMNWGWHZU
      UMUULUUJUUNUUAFHJMOWIWJWKUUIUUKWLWMUUIUUBVSUJZUUAFWNUIZUJZUUKYAUUIUULUUPU
      URWOZUUOUUMUULUUSUUNUUAFHJUUQMUUQVLZOWPWJWKUUIUUBWQVBZUUPUUKWOZUUIUULUVAU
      UOUUMUULUVAUUNUUAFHJMOWRWJWKUVAVSWQVBUVBWSUUBVSWTXAWKUUMUUGUUHUURYAWOUUNX
      LXPFGJUUQMNUUTXBWHXCXDKJXLFIJKSYMPMXEZVHKJXPUVCVHXFXGXHXIXJXK $.
      $( [?] $) $( [8-Feb-2008] $)

    minvc1a.q $e |- Q = U. { a e. Y | ( N ` ( A M a ) ) = P } $.
    $( The minimizing vector of ~ minvc1i belongs to the subspace ` Y ` . $)
    minvccl $p |- Q e. Y $=
      ( cv co cfv wceq crab cuni wreu wcel minvc1i reucl ax-mp eqeltr ) ECMUDHU
      EIUFDUGZMLUHUIZLUCUPMLUJUQLUKABCDFGHIJKLMNOPQRSTUAUBULUPMLUMUNUO $.
      $( [?] $) $( [24-Mar-2008] $)

    $( Distance of the minimizing vector of ~ minvc1i . $)
    minvcdist $p |- ( N ` ( A M Q ) ) = P $=
      ( co cfv wceq cv crab cuni eqcomi wcel wreu wb minvccl minvc1i opreq2
      fveq2d eqeq1d reuuni2 mp2an mpbir ) CEHUDZIUEZDUFZCMUGZHUDZIUEZDUFZMLUHUI
      ZEUFZEVIUCUJELUKVHMLULVDVJUMABCDEFGHIJKLMNOPQRSTUAUBUCUNABCDFGHIJKLMNOPQR
      STUAUBUOVHVDMLEVEEUFZVGVCDVKVFVBIVEECHUPUQURUSUTVA $.
      $( [?] $) $( [24-Mar-2008] $)

    $( The minimizing vector from ~ minvc1i has the smallest distance. $)
    minvcle $p |- ( B e. Y -> ( N ` ( A M Q ) ) <_ ( N ` ( A M B ) ) ) $=
      ( wcel co cfv cle css cbn cin inss1 sselii minveclem13 minvcdist
      syl5eqbr ) DMUEECDIUFJUGCFIUFJUGUHABCDEGHIJKLMSUAPQOHUIUGZUJUKUQKUQUJULUB
      UMRUCTUNABCEFGHIJKLMNOPQRSTUAUBUCUDUOUP $.
      $( [?] $) $( [24-Mar-2008] $)
  $}



