$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CBan $.

  $( Extend class notation with the class of all complex Banach spaces. $)
  cbn $a class CBan $.

  $( Define the class of all complex Banach spaces. $)
  df-bn $a |- CBan = { u e. NrmCVec | ( IndMet ` u ) e. CMet } $.

  ${
    $d u U $.
    $( A complex Banach space is a normed complex vector space with a complete
       induced metric. $)
    isbn $p |- ( U e. CBan <-> ( U e. NrmCVec /\ ( IndMet ` U ) e. CMet ) ) $=
      ( vu cbn wcel cv cims cfv cms cncv crab wa df-bn eleq2i wceq fveq2
      eleq1d elrab bitr ) ACDABEZFGZHDZBIJZDAIDAFGZHDZKCUBABLMUAUDBAISANTUCHSAF
      OPQR $.
      $( [8-Dec-2006] $) $( [5-Dec-2006] $)
  $}

  $( Every complex Banach space is a normed complex vector space. $)
  bnncv $p |- ( U e. CBan -> U e. NrmCVec ) $=
    ( cbn wcel cncv cims cfv cms isbn pm3.26bd ) ABCADCAEFGCAHI $.
    $( [19-Mar-2007] $) $( [17-Mar-2007] $)

  $( The class of all complex Banach spaces is a relation. $)
  bnrel $p |- Rel CBan $=
    ( vx cbn cncv wss wrel cv bnncv ssriv ncvrel relss mp2 ) BCDCEBEABCAFGHIBCJ
    K $.
    $( [2-Apr-2007] $) $( [17-Mar-2007] $)

