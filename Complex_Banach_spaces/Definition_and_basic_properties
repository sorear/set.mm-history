$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CBan $.

  $( Extend class notation with the class of all complex Banach spaces. $)
  cbn $a class CBan $.

  $( Define the class of all complex Banach spaces. $)
  df-bn $a |- CBan = { u e. NrmCVec | ( IndMet ` u ) e. CMet } $.

  ${
    $d u U $.  $d u D $.
    isbn.8 $e |- D = ( IndMet ` U ) $.
    $( A complex Banach space is a normed complex vector space with a complete
       induced metric. $)
    isbn $p |- ( U e. CBan <-> ( U e. NrmCVec /\ D e. CMet ) ) $=
      ( vu cv cims cfv cms wcel cnv cbn wceq fveq2 syl6eqr eleq1d df-bn elrab2
      ) DEZFGZHIAHIDBJKRBLZSAHTSBFGARBFMCNODPQ $.
      $( [8-Sep-2007] $) $( [5-Dec-2006] $)

    $( The induced metric on complex Banach space is complete. $)
    bncms $p |- ( U e. CBan -> D e. CMet ) $=
      ( cbn wcel cnv cms isbn simprbi ) BDEBFEAGEABCHI $.
      $( [8-Sep-2007] $)
  $}

  $( Every complex Banach space is a normed complex vector space. $)
  bnnv $p |- ( U e. CBan -> U e. NrmCVec ) $=
    ( cbn wcel cnv cims cfv cms eqid isbn simplbi ) ABCADCAEFZGCKAKHIJ $.
    $( [17-Mar-2007] $)

  $( The class of all complex Banach spaces is a relation. $)
  bnrel $p |- Rel CBan $=
    ( vx cbn cnv wss wrel cv bnnv ssriv nvrel relss mp2 ) BCDCEBEABCAFGHIBCJK
    $.
    $( [12-Feb-2008] $) $( [17-Mar-2007] $)

  ${
    bnsscmcl.x $e |- X = ( BaseSet ` U ) $.
    bnsscmcl.d $e |- D = ( IndMet ` U ) $.
    bnsscmcl.j $e |- J = ( MetOpen ` D ) $.
    bnsscmcl.h $e |- H = ( SubSp ` U ) $.
    bnsscmcl.y $e |- Y = ( BaseSet ` W ) $.
    $( A subspace of a Banach space is a Banach space iff it is closed in the
       norm-induced metric of the parent space. $)
    bnsscmcl $p |- ( ( U e. CBan /\ W e. H ) -> ( W e. CBan <->
                Y e. ( Clsd ` J ) ) ) $=
      ( cbn wcel wa cims cfv cms cxp cres ccld cnv wb bnnv sspnv sylan eqid
      isbn baib syl wceq sspims eleq1d cdm wss bncms adantr sspba imsba
      sseqtrd cmsss syl11anc 3bitrd ) BMNZECNZOZEMNZEPQZRNZAGGSTZRNZGDUAQNZVFEU
      BNZVGVIUCVDBUBNZVEVMBUDZBCEKUEUFVGVMVIVHEVHUGZUHUIUJVFVHVJRVDVNVEVHVJUKVO
      VHABCEGLIVPKULUFUMVFARNZGAUNUNZUOZVKVLUCVDVQVEABIUPUQVDVNVEVSVOVNVEOGFVRB
      CEFGHLKURVNFVRUKVEABFHIUSUQUTUFADVRGVRUGJVAVBVC $.
      $( [20-May-2009] $) $( [1-Feb-2008] $)
  $}

