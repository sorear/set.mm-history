$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          Uniform Boundedness Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d p A $.  $d h j m n y z N $.  $d h n z P $.  $d h j m n y z T $.
    $d j p y z X $.  $d h j k m n p y z $.
    ubthlem1.1 $e |- X = ( BaseSet ` U ) $.
    ubthlem1.11 $e |- A = { <. j , y >. | ( j e. NN /\ y = { z e. X |
       A. h e. NN ( N ` ( ( T ` h ) ` z ) ) <_ j } ) } $.
    $( Lemma for ~ ubthi .  Membership in ` A `` k ` , the set of all vectors
       ` ( T `` n ) `` z ` whose norm is less than ` k ` . $)
    ubthlem1 $p |- ( k e. NN -> ( P e. ( A ` k ) <-> ( P e. X /\
       A. n e. NN ( N ` ( ( T ` n ) ` P ) ) <_ k ) ) ) $=
      ( cv cn wcel cfv cle wbr wral wa crab wceq breq2 ralbidv rabbidv cba fvex
      cvv eqeltri rabex fvopab4 eleq2d fveq2 fveq2d breq1d elrab syl6bb cbvralv
      fveq1d anbi2i ) IOZPQZDVCCRZQZDLQZDGOZERZRZKRZVCSTZGPUAZUBZVGDJOZERZRZKRZ
      VCSTZJPUAZUBVDVFDBOZVIRZKRZVCSTZGPUAZBLUCZQVNVDVEWFDHAVCWCHOZSTZGPUAZBLUC
      WFPCWGVCUDZWIWEBLWJWHWDGPWGVCWCSUEUFUGNWEBLLFUHRUJMFUHUIUKULUMUNWEVMBDLWA
      DUDZWDVLGPWKWCVKVCSWKWBVJKWADVIUOUPUQUFURUSVMVTVGVLVSGJPVHVOUDZVKVRVCSWLV
      JVQKWLDVIVPVHVOEUOVAUPUQUTVBUS $.
      $( [19-Dec-2007] $)

    $( Lemma for ~ ubthi . ` A `` k ` is a set of vectors. $)
    ubthlem2 $p |- ( k e. NN -> ( A ` k ) C_ X ) $=
      ( vp vm cv cn wcel cfv cle wbr wral ubthlem1 simprbda ex ssrdv ) HOZPQZMU
      FCRZJUGMOZUHQZUIJQZUGUJUKUINODRRIRUFSTNPUAABCUIDEFGHNIJKLUBUCUDUE $.
      $( [7-Nov-2007] $)
  $}

  ${
    $d g m A $.  $d m C $.  $d g m x D $.  $d g G $.  $d n j h z y N $.
    $d g n j m h z y x T $.  $d g W $.  $d g j z y X $.  $d g m x Y $.
    $d f g n j k m h w z y x $.
    ubthlem3.1 $e |- X = ( BaseSet ` U ) $.
    ubthlem3.2 $e |- Y = ( BaseSet ` W ) $.
    ubthlem3.3 $e |- N = ( norm ` W ) $.
    ubthlem3.5 $e |- B = ( U BLnOp W ) $.
    ubthlem3.6 $e |- T : NN --> B $.
    ubthlem3.u $e |- U e. NrmCVec $.
    ubthlem3.w $e |- W e. NrmCVec $.
    ubthlem3.9c $e |- C = ( IndMet ` U ) $.
    ubthlem3.9d $e |- D = ( IndMet ` W ) $.
    ubthlem3.11 $e |- A = { <. j , y >. | ( j e. NN /\ y = { z e. X |
       A. h e. NN ( N ` ( ( T ` h ) ` z ) ) <_ j } ) } $.
    ubthlem3.12 $e |- G = { <. m , x >. | ( m e. NN /\ x =
                 ( ( T ` n ) ` ( f ` m ) ) ) } $.
    $( Lemma for ~ ubthi .  The limit of any sequence in ` A `` k ` has
       operator value norm less than ` k ` , for any bounded linear operator
       ` T `` n ` , using ~ metcn4i (with continuity shown by ~ blocn2 ) and
       the properties of ` A `` k ` . $)
    ubthlem3 $p |- ( ( ( f : NN --> ( A ` k ) /\ f ( ~~>m ` C ) w ) /\
         ( k e. NN /\ n e. NN ) ) -> ( N ` ( ( T ` n ) ` w ) ) <_ k ) $=
      ( vg cn cv cfv wf clm wbr wa wcel cr cle wral ffvelrni cnv mp3an12 adantl
      blof syl ad2antlr wss ubthlem2 fss sylan2 ffvelrn sylan adantlrr syl11anc
      adantllr r19.21aiva fopab2 sylib copn ccn co eqid blocn2 ad2antll wceq wb
      cdm imsbai feq3 ax-mp adantrr anim1i imsmet visset metcn4i mp3anl1 mpanl1
      an32s cme nnre ad2antrl fveq2 fveq2d fvex fvopab4 adantlr simplrl simplrr
      ubthlem1 simplbda ancoms r19.21bi syl21anc eqbrtrd nvlmle syl22anc ) UNNU
      OZEUPZKUOZUQZYDDUOZGURUPUSZUTZYBUNVAZPUOZUNVAZUTZUTZUNUAQUQZQYFYJIUPZUPZH
      URUPUSZYBVBVAZUMUOZQUPZRUPZYBVCUSZUMUNVDZYPRUPYBVCUSZYMOUOZYDUPZYOUPZUAVA
      ZOUNVDYNYMUUHOUNYEYLUUEUNVAZUUHYGYEYLUTZUUIUTTUAYOUQZUUFTVAZUUHYLUUKYEUUI
      YKUUKYIYKYOFVAZUUKUNFYJIUFVEZJVFVAZSVFVAZUUMUUKUGUHFYOJSTUAUBUCUEVIVGVJVH
      VKYEYIUUIUULYKYEYIUTZUNTYDUQZUUIUULYIYEYCTVLUURBCEIJLMNRTUBUKVMUNYCTYDVNV
      OZUNTUUEYDVPVQVRTUAUUFYOVPVSVTWAOAUNUAUUGQULWBWCYMYOGWDUPZHWDUPZWEWFVAZUN
      GWLWLZYDUQZYGUTZYQYKUVBYHYIYKUUMUVBUUNFGHYOJUUTUVASUIUJUUTWGZUVAWGZUEUGUH
      WHVJWIYEYLYGUVEUUJUVDYGYEYIUVDYKUUQUURUVDUUSTUVCWJUURUVDWKGJTUBUIUGWMTUVC
      UNYDWNWOWCWPWQXCHXDVAZUVBUVEYQUUPUVHUHHSUJWRWOGXDVAZUVHUVBUVEYQUUOUVIUGGJ
      UIWRWOAGHYFOYOYDQUUTUVAUVCUVCWGUVFUVGULDWSWTXAXBVSYIYRYHYKYBXEXFYEYLUUCYG
      UUJUUBUMUNUUJYSUNVAZUTZUUAYSYDUPZYOUPZRUPZYBVCUVJUUAUVNWJUUJUVJYTUVMROAYS
      UUGUVMUNQUUEYSWJUUFUVLYOUUEYSYDXGXHULUVLYOXIXJXHVHUVKUVLYCVAZYIYKUVNYBVCU
      SZYEUVJUVOYLUNYCYSYDVPXKYEYIYKUVJXLYEYIYKUVJXMUVOYIUTUVPPUNYIUVOUVPPUNVDZ
      YIUVOUVLTVAUVQBCEUVLIJLMNPRTUBUKXNXOXPXQXRXSWAXKUUPYNYQYRUUCUTUUDUHHYPYBS
      UMQRUAUCUDUJYFYOXIXTXAYA $.
      $( [26-Dec-2007] $)
  $}

  ${
    $d c f k m n p r x A $.  $d f k m n r x D $.  $d f k p r x J $.
    $d h j k n y z N $.  $d h j k m n w y z T $.  $d m w W $.
    $d c f j k n x y z X $.  $d m w Y $.  $d f h w x $.
    ubthlem4.1 $e |- X = ( BaseSet ` U ) $.
    ubthlem4.2 $e |- Y = ( BaseSet ` W ) $.
    ubthlem4.3 $e |- N = ( norm ` W ) $.
    ubthlem4.5 $e |- B = ( U BLnOp W ) $.
    ubthlem4.6 $e |- T : NN --> B $.
    ubthlem4.7 $e |- U e. CBan $.
    ubthlem4.8 $e |- W e. NrmCVec $.
    ubthlem4.9 $e |- D = ( IndMet ` U ) $.
    ubthlem4.10 $e |- J = ( MetOpen ` D ) $.
    ubthlem4.11 $e |- A = { <. j , y >. | ( j e. NN /\ y = { z e. X |
       A. h e. NN ( N ` ( ( T ` h ) ` z ) ) <_ j } ) } $.
    $( Lemma for ~ ubthi .  The set ` A `` k ` is therefore closed by
       ~ metcld .  (The hypothesis ` U e. CBan ` could be weakened to
       ` U e. NrmCVec ` .) $)
    ubthlem4 $p |- ( k e. NN -> ( A ` k ) e. ( Clsd ` J ) ) $=
      ( vf vx vn vw vm cv cn wcel cfv ccld wf clm wbr wal cle wral cnv cbn bnnv
      wa ax-mp visset nvlmcl mpan ad2antll cims wceq copab eqid ubthlem3 ancoms
      wi an32s r19.21aiva jca ex ubthlem1 sylibrd 19.21aivv wss wb ubthlem2 cme
      imsmet imsbai metcld syl mpbird ) JUKZULUMZWNCUNZKUOUNUMZULWPUFUKZUPZWRUG
      UKZEUQUNURZVEZWTWPUMZVQZUFUSUGUSZWOXDUGUFWOXBWTNUMZWTUHUKZFUNZUNLUNWNUTUR
      ZUHULVAZVEZXCWOXBXKWOXBVEZXFXJXAXFWOWSGVBUMZXAXFGVCUMXMUAGVDVFZEWTGWRNPUC
      UGVGVHVIVJXLXIUHULWOXGULUMZXBXIXBWOXOVEXIUIABUGCDEMVKUNZFGUFHIJUJUHUJUKZU
      LUMUIUKXQWRUNXHUNVLVEUJUIVMZLMNOPQRSTXNUBUCXPVNUEXRVNVOVPVRVSVTWAABCWTFGH
      IJUHLNPUEWBWCWDWOWPNWEZWQXEWFZABCFGHIJLNPUEWGEWHUMZXSXTXMYAXNEGUCWIVFUGEU
      FKWPNEGNPUCXNWJUDWKVIWLWM $.
      $( [7-Nov-2007] $)

    $( Lemma for ~ ubthi .  The union of all ` A `` k ` equals the base set,
       provided the values of the operators ` T `` n ` are bounded for each
       vector ` x ` . $)
    ubthlem5 $p |- ( A. x e. X E. c e. RR A. n e. NN
   ( N ` ( ( T ` n ) ` x ) ) <_ c -> U_ k e. NN ( A ` k ) = X ) $=
      ( cv cfv cle wbr cn wral cr wrex ciun wss iunss ubthlem2 mprgbir a1i wcel
      wa clt arch ad2antlr wi nnre ltle sylan2 adantll adantlr cnv ffvelrni cbn
      wf bnnv ax-mp blof mp3an12 ffvelrn sylan ancoms nvcl sylancr simpllr letr
      syl syl111anc exp4b com34 com23 imp31 ralimdva ex simplll jctild ubthlem1
      imp wb sylibrd an32s syld reximdva r19.23adva eliun syl6ibr ralimia dfss3
      mpd sylibr eqssd ) AUIZLUIZGUJZUJZNUJZRUIZUKULZLUMUNZRUOUPZAPUNZKUMKUIZDU
      JZUQZPYFPURZYCYGYEPURKUMKUMYEPUSBCDGHIJKNPSUHUTVAVBYCXNYFVCZAPUNPYFURYBYH
      APXNPVCZYBXNYEVCZKUMUPZYHYIYAYKRUOYIXSUOVCZVDZYAYKYMYAVDZXSYDVEULZKUMUPZY
      KYLYPYIYAXSKVFVGYNYOYJKUMYNYDUMVCZVDYOXSYDUKULZYJYMYQYOYRVHZYAYLYQYSYIYQY
      LYDUOVCZYSYDVIZXSYDVJVKVLVMYMYQYAYRYJVHYMYQVDZYAVDZYRYIXRYDUKULZLUMUNZVDZ
      YJUUCYRUUEYIUUBYAYRUUEVHUUBYRYAUUEUUBYRYAUUEVHUUBYRVDXTUUDLUMUUBYRXOUMVCZ
      XTUUDVHZUUBUUGYRUUHUUBUUGXTYRUUDUUBUUGXTYRUUDUUBUUGVDXRUOVCZYLYTXTYRVDUUD
      VHYMUUGUUIYQYIUUGUUIYLYIUUGVDOVNVCZXQQVCZUUIUEUUGYIUUKUUGPQXPVQZYIUUKUUGX
      PEVCZUULUMEXOGUCVOHVNVCZUUJUUMUULHVPVCUUNUDHVRVSUEEXPHOPQSTUBVTWAWIPQXNXP
      WBWCWDXQONQTUAWEWFVMVMYIYLYQUUGWGYQYTYMUUGUUAVGXRXSYDWHWJWKWLWMWNWOWPWMWT
      YIYLYQYAWQWRYQYJUUFXAYMYABCDXNGHIJKLNPSUHWSVGXBXCXDXEXKWPXFKXNUMYEXGXHXIA
      PYFXJXLXM $.
      $( [26-Dec-2007] $)

    $( Lemma for ~ ubthi .  Using ~ bcth (via ~ bcthlem33 ), at least one set
       ` A `` k ` contains a ball. $)
    ubthlem6 $p |- ( A. x e. X E. c e. RR A. n e. NN ( N ` ( ( T ` n ) ` x
        ) ) <_ c -> E. k e. NN E. p e. X E. r e. RR ( 0 < r /\
                 ( p ( ball ` D ) r ) C_ ( A ` k ) ) ) $=
      ( cv cfv cle wbr cn wral cr wrex cnt c0 wne cc0 clt cbl wss crn cuni wceq
      co wa ccld ciun wfn crab cba cvv fvex eqeltri rabex fnopab2 fniunfv ax-mp
      ubthlem5 syl5eqr wcel wb fvelrnb simpr ubthlem4 eqeltrrd r19.23aiva sylbi
      adantr ssriv cbn cms bncms cn0v cnv bnnv eqid nvzcl ne0i imsbai cpw ffnfv
      wf ubthlem2 elpw sylibr rgen mpbir2an bcthlem33 sylancl wex imsmet opntop
      ctop cme uniopn2 eqcomi ntrss2 sylancr sstrd sseld ntropn opni2 mp3an1 ex
      sylan sstr sylan2 expcom anim2d reximdv syld jcad eximdv n0 df-rex syl
      3imtr4g reximia ) AUKZLUKGULULNULTUKUMUNLUOUPTUQURAPUPZKUKZDULZMUSULULZUT
      VAZKUOURZVBRUKZVCUNZSUKZUUKFVDULVIZUUGVEZVJZRUQURZSPURZKUOURUUEDVFZVGZPVH
      UUSMVKULZVEUUJUUEUUTKUOUUGVLZPDUOVMZUVBUUTVHJBUOCUKIUKGULULNULJUKUMUNIUOU
      PZCPVNDUVDCPPHVOULVPUAHVOVQVRVSUJVTZKUODWAWBABCDEFGHIJKLMNOPQTUAUBUCUDUEU
      FUGUHUIUJWCWDAUUSUVAUUDUUSWEZUUGUUDVHZKUOURZUUDUVAWEZUVCUVFUVHWFUVEKUOUUD
      DWGWBUVGUVIKUOUUFUOWEZUVGVJUUGUUDUVAUVJUVGWHUVJUUGUVAWEUVGBCDEFGHIJKMNOPQ
      UAUBUCUDUEUFUGUHUIUJWIWMWJWKWLWNFKMDPHWOWEZFWPWEUFFHUHWQWBHWRULZPWEZPUTVA
      HWSWEZUVMUVKUVNUFHWTWBZHPUVLUAUVLXAXBWBPUVLXCWBFHPUAUHUVOXDZUIUOPXEZDXGUV
      CUUGUVQWEZKUOUPKUOUVQDXFUVEUVRKUOUVJUUGPVEZUVRBCDGHIJKNPUAUJXHZUUGPUUFDVQ
      XIXJXKXLXMXNUUIUURKUOUVJUUMUUHWEZSXOUUMPWEZUUQVJZSXOUUIUURUVJUWAUWCSUVJUW
      AUWBUUQUVJUUHPUUMUVJUUHUUGPUVJMXRWEZUVSUUHUUGVEZFXSWEZUWDUVNUWFUVOFHUHXPW
      BZFMUIXQWBZUVTUUGMPMVGZPUWFUWIPVHUWGFMPUVPUIXTWBYAZYBYCZUVTYDYEUVJUWAUULU
      UNUUHVEZVJZRUQURZUUQUVJUWAUWNUVJUUHMWEZUWAUWNUVJUWDUVSUWOUWHUVTUUGMPUWJYF
      YCUWFUWOUWAUWNUWGRUUHFUUMMUIYGYHYJYIUVJUWMUUPRUQUVJUWLUUOUULUWLUVJUUOUVJU
      WLUWEUUOUWKUUNUUHUUGYKYLYMYNYOYPYQYRSUUHYSUUQSPYTUUBUUCUUA $.
      $( [27-Dec-2007] $)
  $}

  ${
    $d h j n y z N $.  $d h n z Q $.  $d h j n y z T $.  $d j y z X $.
    $d h j k n p y z $.
    ubthlem7.1 $e |- X = ( BaseSet ` U ) $.
    ubthlem7.7 $e |- U e. NrmCVec $.
    ubthlem7.n $e |- L = ( norm ` U ) $.
    ubthlem7.g $e |- G = ( +v ` U ) $.
    ubthlem7.m $e |- M = ( -v ` U ) $.
    ubthlem7.r $e |- R = ( .s ` U ) $.
    ubthlem7.z $e |- Z = ( 0vec ` U ) $.
    ubthlem7.q $e |- Q =
            ( p G ( ( ( r / 2 ) x. ( 1 / ( L ` x ) ) ) R x ) ) $.
    $( Lemma for ~ ubthi .  Auxiliary class ` Q ` is a vector. $)
    ubthlem7 $p |- ( ( p e. X /\ ( r e. RR /\ ( x e. X /\ x =/= Z ) ) ) ->
            Q e. X ) $=
      ( wcel cv cr wne wa c2 cdiv co c1 cfv cmul cc rehalfcl recnd cc0 cnv nvcl
      mpan adantr wceq nvz necon3bid biimpar reccl syl11anc mulcl syl2an simprl
      wb nvscl mp3an1 nvgcl sylan2 syl5eqel ) KUAZHTZJUAZUBTZAUAZHTZVRIUCZUDZUD
      ZUDBVNVPUEUFUGZUHVRFUIZUFUGZUJUGZVRCUGZEUGZHSWBVOWGHTZWHHTZWBWFUKTZVSWIVQ
      WCUKTWEUKTZWKWAVQWCVPULUMWAWDUKTZWDUNUCZWLVSWMVTVSWDDUOTZVSWDUBTMVRDFHLNU
      PUQUMURVSWNVTVSWDUNVRIWOVSWDUNUSVRIUSVHMVRDFHILRNUTUQVAVBWDVCVDWCWEVEVFVQ
      VSVTVGWOWKVSWIMWFVRCDHLQVIVJVDWOVOWIWJMVNWGDEHLOVKVJVLVM $.
      $( [28-Dec-2007] $)

    $( Lemma for ~ ubthi .  Compute ` x ` in terms of auxiliary vector
       ` Q ` . $)
    ubthlem8 $p |- ( ( p e. X /\ ( ( r e. RR /\ 0 < r ) /\
               ( x e. X /\ x =/= Z ) ) ) ->
         x = ( ( ( 2 / r ) x. ( L ` x ) ) R ( Q M p ) ) ) $=
      ( co cv wcel cr cc0 clt wbr wa wne c1 c2 cdiv cfv cmul cnv nvsid ad2antrl
      wceq mpan adantl eqcomi wb ubthlem7 adantrlr simpl cc rehalfcl recnd nvcl
      adantr necon3bid biimpar reccl syl11anc mulcl syl2an simprl nvscl adantlr
      nvz mp3an1 w3a nvsubadd syl111anc mpbiri opreq2d gt0ne0 2cn divcl anim12i
      nvsass mul4 2ne0 divcan6 mpanl12 opreqan12d ax-1cn mulid1i syl6eq opreq1d
      recid eqtrd 3eqtr2rd eqtr3d ) KUAZHUBZJUAZUCUBZUDXFUEUFZUGZAUAZHUBZXJIUHZ
      UGZUGZUGZUIXJCTZXJUJXFUKTZXJFULZUMTZBXDGTZCTZXNXPXJUQZXEXKYBXIXLDUNUBZXKY
      BMXJCDHLQUOURUPUSXOYAXSXFUJUKTZUIXRUKTZUMTZXJCTZCTZXSYFUMTZXJCTZXPXOXTYGX
      SCXOXTYGUQZXDYGETZBUQZBYLSUTXOBHUBZXEYGHUBZYKYMVAZXEXGXMYNXHABCDEFGHIJKLM
      NOPQRSVBVCXEXNVDXNYOXEXGXMYOXHXGXMUGYFVEUBZXKYOXGYDVEUBZYEVEUBZYQXMXGYDXF
      VFVGZXMXRVEUBZXRUDUHZYSXKUUAXLXKXRYCXKXRUCUBMXJDFHLNVHURVGVIZXKUUBXLXKXRU
      DXJIYCXKXRUDUQXJIUQVAMXJDFHILRNVSURVJVKZXRVLVMZYDYEVNZVOXGXKXLVPYCYQXKYOM
      YFXJCDHLQVQVTVMVRUSYCYNXEYOWAYPMBXDYGDEGHLOPWBURWCWDWEXNYJYHUQZXEXNXSVEUB
      ZYQXKUUGXIXQVEUBZUUAUUHXMXIXFVEUBZXFUDUHZUUIXIXFXGXHVDVGZXFWFZUJVEUBZUUJU
      UKUUIWGUJXFWHVTVMZUUCXQXRVNVOXIYRYSYQXMXGYRXHYTVIZUUEUUFVOXIXKXLVPYCUUHYQ
      XKWAUUGMXSYFXJCDHLQWJURWCUSXOYIUIXJCXNYIUIUQXEXNYIXQYDUMTZXRYEUMTZUMTZUIX
      NUUIUUAUGYRYSUGYIUUSUQXIUUIXMUUAUUOUUCWIXIYRXMYSUUPUUEWIXQXRYDYEWKVMXNUUS
      UIUIUMTUIXIXMUUQUIUURUIUMXIUUJUUKUUQUIUQZUULUUMUUNUJUDUHUUJUUKUGUUTWGWLUJ
      XFWMWNVMXMUUAUUBUURUIUQUUCUUDXRWTVMWOUIWPWQWRXAUSWSXBXC $.
      $( [28-Dec-2007] $)

    ${
      ubthlem9.5 $e |- B = ( U BLnOp W ) $.
      ubthlem9.6 $e |- T : NN --> B $.
      ubthlem9.8 $e |- W e. NrmCVec $.
      ubthlem9.s $e |- S = ( .s ` W ) $.
      $( Lemma for ~ ubthi .  Evaluate the operator value at ` x ` in terms of
         the operator value at ` Q - p ` . $)
      ubthlem9 $p |- ( ( n e. NN /\ ( p e. X /\ ( ( r e. RR /\ 0 < r ) /\
       ( x e. X /\ x =/= Z ) ) ) ) -> ( ( T ` n ) ` x ) =
           ( ( ( 2 / r ) x. ( L ` x ) ) S ( ( T ` n ) ` ( Q M p ) ) ) ) $=
        ( cv cn wcel cr cc0 clt wbr wa wne c2 cdiv co cmul wceq ubthlem8 fveq2d
        cfv adantl clno cc ffvelrni cnv eqid bloln mp3an12 syl gt0ne0 2cn divcl
        recn mp3an1 sylan syldan nvcl mpan recnd mulcl syl2an ubthlem7 adantrlr
        adantrr simpl nvmcl syl11anc jca lnomul mp3anl1 mpanl1 eqtrd ) HUIZUJUK
        ZPUIZMUKZOUIZULUKZUMXBUNUOZUPZAUIZMUKZXFNUQZUPZUPZUPZUPXFWRFVEZVEZURXBU
        SUTZXFJVEZVAUTZCWTKUTZDUTZXLVEZXPXQXLVEEUTZXKXMXSVBWSXKXFXRXLACDGIJKMNO
        PQRSTUAUBUCUDVCVDVFWSXLGLVGUTZUKZXPVHUKZXQMUKZUPZXSXTVBZXKWSXLBUKZYBUJB
        WRFUFVIGVJUKZLVJUKZYGYBRUGBXLGYALYAVKZUEVLVMVNXKYCYDXJYCXAXEXGYCXHXEXNV
        HUKZXOVHUKYCXGXCXDXBUMUQZYKXBVOXCXBVHUKZYLYKXBVRURVHUKYMYLYKVPURXBVQVSV
        TWAXGXOYHXGXOULUKRXFGJMQSWBWCWDXNXOWEWFWIVFXKCMUKZXAYDXAXCXIYNXDACDGIJK
        MNOPQRSTUAUBUCUDWGWHXAXJWJYHYNXAYDRCWTGKMQUAWKVSWLWMYIYBYEYFUGYHYIYBYEY
        FRXPXQDEXLGYALMQUBUHYJWNWOWPWFWQ $.
        $( [28-Dec-2007] $)
    $}
  $}

  ${
    $d c k n p r x A $.  $d k n p r x D $.  $d x L $.  $d h j k n x y z N $.
    $d d k p r O $.  $d h n z Q $.  $d d h j k n p r x y z T $.  $d x U $.
    $d x W $.  $d c j k n r x y z X $.
    ubthlem10.1 $e |- X = ( BaseSet ` U ) $.
    ubthlem10.2 $e |- Y = ( BaseSet ` W ) $.
    ubthlem10.3 $e |- N = ( norm ` W ) $.
    ubthlem10.4 $e |- O = ( U normOp W ) $.
    ubthlem10.5 $e |- B = ( U BLnOp W ) $.
    ubthlem10.6 $e |- T : NN --> B $.
    ubthlem10.7 $e |- U e. NrmCVec $.
    ubthlem10.8 $e |- W e. NrmCVec $.
    ubthlem10.9 $e |- D = ( IndMet ` U ) $.
    ubthlem10.n $e |- L = ( norm ` U ) $.
    ubthlem10.g $e |- G = ( +v ` U ) $.
    ubthlem10.m $e |- M = ( -v ` U ) $.
    ubthlem10.r $e |- R = ( .s ` U ) $.
    ubthlem10.z $e |- Z = ( 0vec ` U ) $.
    ubthlem10.11 $e |- A = { <. j , y >. | ( j e. NN /\ y = { z e. X |
       A. h e. NN ( N ` ( ( T ` h ) ` z ) ) <_ j } ) } $.
    ubthlem10.q $e |- Q =
            ( p G ( ( ( r / 2 ) x. ( 1 / ( L ` x ) ) ) R x ) ) $.
    $( Lemma for ~ ubthi .  Upper limit for the norm of an operator value at
       auxiliary vector ` Q ` . $)
    ubthlem10 $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\ ( ( r e. RR /\
   0 < r ) /\ ( x e. X /\ x =/= Z ) ) ) /\ ( p ( ball ` D ) r ) C_ ( A ` k ) )
        ) -> ( N ` ( ( T ` n ) ` Q ) ) <_ k ) $=
      ( cv wcel cr cc0 clt wbr wa wne cbl cfv co wss cn c2 cdiv c1 cmul wceq cc
      cle rehalfcl recnd adantr cnv nvcl mpan wb nvz necon3bid biimpar syl11anc
      rereccl adantl simprl w3a nvsass syl111anc adantlr fveq2d halfpos2 biimpa
      wi 0re sylancr mpd jca simpl nvscl mp3an1 nvsge0 syl2an eqtrd nv1 opreq2d
      ltle mulid1 syl 3eqtrd halfpos eqbrtrd reccl mulcl anim2i nvelbl2 mp3anl1
      ad2antrr mpbird syl5eqel ssel impcom sylan wral ubthlem1 simplbda syl6com
      ex ra4 com3l imp31 sylan2 ) UEVBZUAVCZUDVBZVDVCZVEUUDVFVGZVHZAVBZUAVCZUUH
      UCVIZVHZVHZVHZUUBUUDFVJVKVLZMVBZDVKZVMZVHUUOVNVCZNVBZVNVCZVHGUUPVCZGUUSIV
      KVKRVKUUOWAVGZUUMGUUNVCZUUQUVAUUMGUUBUUDVOVPVLZVQUUHPVKZVPVLZVRVLZUUHHVLZ
      OVLZUUNVAUUMUVIUUNVCZUVHPVKZUUDVFVGZUULUVLUUCUULUVKUVDUUDVFUULUVKUVDUVFUU
      HHVLZPVKZVRVLZUVDVQVRVLZUVDUULUVKUVDUVMHVLZPVKZUVOUULUVHUVQPUUEUUKUVHUVQV
      SZUUFUUEUUKVHZUVDVTVCZUVFVTVCZUUIUVSUUEUWAUUKUUEUVDUUDWBZWCZWDUUKUWBUUEUU
      KUVFUUKUVEVDVCZUVEVEVIZUVFVDVCUUIUWEUUJJWEVCZUUIUWEULUUHJPUAUFUOWFWGZWDUU
      IUWFUUJUUIUVEVEUUHUCUWGUUIUVEVEVSUUHUCVSWHULUUHJPUAUCUFUSUOWIWGWJWKZUVEWM
      WLWCZWNUUEUUIUUJWOZUWGUWAUWBUUIWPUVSULUVDUVFUUHHJUAUFURWQWGWRWSWTUUGUVDVD
      VCZVEUVDWAVGZVHZUVMUAVCZUVRUVOVSZUUKUUGUWLUWMUUEUWLUUFUWCWDUUGVEUVDVFVGZU
      WMUUEUUFUWQUUDXAXBUUEUWQUWMXCZUUFUUEVEVDVCUWLUWRXDUWCVEUVDXPXEWDXFXGUUKUW
      BUUIUWOUWJUUIUUJXHUWGUWBUUIUWOULUVFUUHHJUAUFURXIXJWLUWGUWNUWOUWPULUVDUVMH
      JPUAUFURUOXKXJXLXMUUKUVOUVPVSUUGUUKUVNVQUVDVRUWGUUIUUJUVNVQVSULUUHHJPUAUC
      UFURUSUOXNXJXOWNUUEUVPUVDVSZUUFUUKUUEUWAUWSUWDUVDXQXRYGXSUUGUVDUUDVFVGZUU
      KUUEUUFUWTUUDXTXBWDYAWNUUMUUCUVHUAVCZVHUUGUVJUVLWHZUULUXAUUCUUEUUKUXAUUFU
      VTUVGVTVCZUUIUXAUUEUWAUWBUXCUUKUWDUUKUVEVTVCZUWFUWBUUIUXDUUJUUIUVEUWHWCWD
      UWIUVEYBWLUVDUVFYCXLUWKUWGUXCUUIUXAULUVGUUHHJUAUFURXIXJWLWSYDUUCUUGUUKWOU
      WGUUCUXAUUGUXBULUVHFUUBUUDJOPUAUFUPUOUNYEYFWLYHYIUUQUVCUVAUUNUUPGYJYKYLUU
      RUUTUVAUVBUVAUURUUTUVBUURUVAUVBNVNYMZUUTUVBXCUURUVAUXEUURUVAGUAVCUXEBCDGI
      JKLMNRUAUFUTYNYOYQUVBNVNYRYPYSYTUUA $.
      $( [27-Dec-2007] $)

    $( Lemma for ~ ubthi .  Upper limit for the norm of an operator value at
       ` Q - p ` . $)
    ubthlem11 $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\ ( ( r e. RR
  /\ 0 < r ) /\ ( x e. X /\ x =/= Z ) ) ) /\ ( p ( ball ` D ) r ) C_ ( A ` k )
        ) ) -> ( N ` ( ( T ` n ) ` ( Q M p ) ) ) <_ ( 2 x. k ) ) $=
      ( cv cn wcel wa cr cc0 clt wbr wne cbl cfv co wss cnsb caddc cmul cle cnv
      c2 wf ffvelrni mp3an12 syl ubthlem7 adantrlr ffvelrn syl2an sylan adantrr
      blof eqid nvmcl mp3an1 syl11anc sylancr ad2ant2lr readdcl ad2antrr nvmtri
      nvcl nnre ubthlem10 cme imsmet ax-mp imsbai blcntr mpanl1 ancoms adantlrr
      ssel2 wral wi ubthlem1 simplbda ra4 syl6com com3l imp31 sylan2 jca le2add
      ex mp2and letrd wceq clno bloln lnosub mp3anl1 fveq2d nncn 2times 3brtr4d
      simpl cc ) MVBZVCVDZNVBZVCVDZVEZUEVBZUAVDZUDVBZVFVDZVGUUEVHVIZVEZAVBZUAVD
      UUIUCVJVEZVEZVEZUUCUUEFVKVLVMZYRDVLZVNZVEZVEZGYTIVLZVLZUUCUURVLZTVOVLZVMZ
      RVLZYRYRVPVMZGUUCQVMUURVLZRVLZVTYRVQVMZVRUUQUVCUUSRVLZUUTRVLZVPVMZUVDUUAU
      ULUVCVFVDZYSUUOUUAUULVEZTVSVDZUVBUBVDZUVKUMUVLUUSUBVDZUUTUBVDZUVNUUAUAUBU
      URWAZGUAVDZUVOUULUUAUUREVDZUVQVCEYTIUKWBZJVSVDZUVMUVSUVQULUMEUURJTUAUBUFU
      GUJWKWCWDZUUDUUFUUJUVRUUGAGHJOPQUAUCUDUEUFULUOUPUQURUSVAWEZWFUAUBGUURWGWH
      ZUUAUUDUVPUUKUUAUVQUUDUVPUWBUAUBUUCUURWGWIZWJZUVMUVOUVPUVNUMUUSUUTTUVAUBU
      GUVAWLZWMWNWOUVBTRUBUGUHXAWPWQUUAUULUVJVFVDZYSUUOUVLUVHVFVDZUVIVFVDZUWHUV
      LUVMUVOUWIUMUWDUUSTRUBUGUHXAWPZUUAUUDUWJUUKUUAUUDVEUVMUVPUWJUMUWEUUTTRUBU
      GUHXAWPWJZUVHUVIWRWOWQYSUVDVFVDZUUAUUPYSYRVFVDZUWNUWMYRXBZUWOYRYRWRWOWSUU
      AUULUVCUVJVRVIZYSUUOUVLUVOUVPUWPUWDUWFUVMUVOUVPUWPUMUUSUUTTUVARUBUGUWGUHW
      TWNWOWQUUQUVHYRVRVIZUVIYRVRVIZUVJUVDVRVIZABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUF
      UGUHUIUJUKULUMUNUOUPUQURUSUTVAXCUUPUUBUUCUUNVDZUWRUUDUUHUUOUWTUUJUUDUUHVE
      UUCUUMVDZUUOUWTFXDVDZUUDUUHUXAUWAUXBULFJUNXEXFFUUCUUEUAFJUAUFUNULXGXHXIUU
      OUXAUWTUUMUUNUUCXLXJWIXKYSUUAUWTUWRUWTYSUUAUWRYSUWTUWRNVCXMZUUAUWRXNYSUWT
      UXCYSUWTUUDUXCBCDUUCIJKLMNRUAUFUTXOXPYDUWRNVCXQXRXSXTYAUUQUWIUWJVEZUWNUWN
      VEZUWQUWRVEUWSXNUUAUULUXDYSUUOUVLUWIUWJUWKUWLYBWQYSUXEUUAUUPYSUWNUWNUWOUW
      OYBWSUVHUVIYRYRYCWOYEYFUUAUULUVFUVCYGYSUUOUVLUVEUVBRUUAUURJTYHVMZVDZUVRUU
      DVEZUVEUVBYGZUULUUAUVSUXGUVTUWAUVMUVSUXGULUMEUURJUXFTUXFWLZUJYIWCWDUUDUUF
      UUJUXHUUGUUDUUFUUJVEZVEUVRUUDUWCUUDUXKYPYBWFUVMUXGUXHUXIUMUWAUVMUXGUXHUXI
      ULGUUCUURJUXFQUVATUAUFUQUWGUXJYJYKXIWHYLWQYSUVGUVDYGZUUAUUPYSYRYQVDUXLYRY
      MYRYNWDWSYO $.
      $( [31-Dec-2007] $)

    $( Lemma for ~ ubthi .  Upper limit for the norm of an operator value at
       ` x ` . $)
    ubthlem12 $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\ (
  ( r e. RR /\ 0 < r ) /\ ( x e. X /\ x =/= Z ) ) ) /\ ( p ( ball ` D ) r ) C_
  ( A ` k ) ) ) -> ( N ` ( ( T ` n ) ` x ) ) <_ ( ( ( 2 / r ) x. ( 2 x. k ) )
         x. ( L ` x ) ) ) $=
      ( cv cn wcel wa cr cc0 clt wbr wne cbl cfv co wss c2 cdiv cmul cle w3a wf
      cnv ffvelrni blof mp3an12 syl ubthlem7 nvmcl mp3an1 ancoms syldan ffvelrn
      adantrlr syl2an nvcl sylancr adantll nnre remulcl ad2antrr gt0ne0 redivcl
      2re mpan 0re 2pos ltleii divge0 mpanl12 jca nvge0 mulge0 adantrr ad2antll
      3jca ubthlem11 lemul2a syl11anc wceq cns ubthlem9 fveq2d nvsge0 ad2ant2lr
      eqid eqtr2d cc recnd mul32 syl3an 3comr adantrrr adantrl ad2ant2r 3brtr3d
      3expb ) MVBZVCVDZNVBZVCVDZVEZUEVBZUAVDZUDVBZVFVDZVGUUCVHVIZVEZAVBZUAVDZUU
      GUCVJZVEZVEZVEZUUAUUCFVKVLVMYPDVLVNZVEVEZVOUUCVPVMZUUGPVLZVQVMZGUUAQVMZYR
      IVLZVLZRVLZVQVMZUUQVOYPVQVMZVQVMZUUGUUSVLZRVLZUUOUVCVQVMUUPVQVMZVRUUNUVAV
      FVDZUVCVFVDZUUQVFVDZVGUUQVRVIZVEZVSZUVAUVCVRVIUVBUVDVRVIYTUULUVMUUMYTUULV
      EUVHUVIUVLYSUULUVHYQYSUULVEZTWAVDZUUTUBVDZUVHUMYSUAUBUUSVTZUURUAVDZUVPUUL
      YSUUSEVDZUVQVCEYRIUKWBJWAVDZUVOUVSUVQULUMEUUSJTUAUBUFUGUJWCWDWEUUBUUKGUAV
      DZUVRUUBUUDUUJUWAUUEAGHJOPQUAUCUDUEUFULUOUPUQURUSVAWFWLUWAUUBUVRUVTUWAUUB
      UVRULGUUAJQUAUFUQWGWHWIWJUAUBUURUUSWKWMZUUTTRUBUGUHWNWOWPYQUVIYSUULYQVOVF
      VDZYPVFVDUVIXBYPWQVOYPWRWOZWSUUKUVLYTUUBUUFUUHUVLUUIUUFUUHVEUVJUVKUUFUUOV
      FVDZUUPVFVDZUVJUUHUUDUUEUUCVGVJZUWEUUCWTUWCUUDUWGUWEXBVOUUCXAWHWJZUVTUUHU
      WFULUUGJPUAUFUOWNXCZUUOUUPWRWMUUFUWEVGUUOVRVIZVEUWFVGUUPVRVIZVEUVKUUHUUFU
      WEUWJUWHUWCVGVOVRVIUUFUWJXBVGVOXDXBXEXFVOUUCXGXHXIUUHUWFUWKUWIUVTUUHUWKUL
      UUGJPUAUFUOXJXCXIUUOUUPXKWMXIXLZXMXNXLABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGU
      HUIUJUKULUMUNUOUPUQURUSUTVAXOUVAUVCUUQXPXQYSUULUVBUVFXRYQUUMUVNUVFUUQUUTT
      XSVLZVMZRVLZUVBUVNUVEUWNRAEGHUWMIJNOPQTUAUCUDUEUFULUOUPUQURUSVAUJUKUMUWMY
      DZXTYAUVNUVLUVPUWOUVBXRZUUKUVLYSUUBUWLXMUWBUVOUVLUVPUWQUMUUQUUTUWMTRUBUGU
      WPUHYBWHXQYEYCYQUULUVDUVGXRZYSUUMYQUUKUWRUUBYQUUFUUHUWRUUIYQUUFUUHUWRUUFU
      UHYQUWRUUFUUOYFVDUUHUUPYFVDYQUVCYFVDUWRUUFUUOUWHYGUUHUUPUWIYGYQUVCUWDYGUU
      OUUPUVCYHYIYJYOYKYLYMYN $.
      $( [1-Jan-2008] $)

    $( Lemma for ~ ubthi .  Upper limit for the norm of an operator value at
       ` x ` . $)
    ubthlem12OLD $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\ (
  ( r e. RR /\ 0 < r ) /\ ( x e. X /\ x =/= Z ) ) ) /\ ( p ( ball ` D ) r ) C_
  ( A ` k ) ) ) -> ( N ` ( ( T ` n ) ` x ) ) <_ ( ( ( 2 / r ) x. ( 2 x. k ) )
         x. ( L ` x ) ) ) $=
      ( cv cn wcel wa cr cc0 clt wbr wne cbl cfv co wss c2 cdiv cmul cle w3a wf
      cnv ffvelrni blof mp3an12 syl ubthlem7 nvmcl mp3an1 ancoms syldan ffvelrn
      adantrlr syl2an nvcl sylancr adantll nnre remulcl ad2antrr gt0ne0 redivcl
      2re mpan 0re 2pos ltleii divge0 mpanl12 jca nvge0 mulge0OLD an4s ad2antll
      adantrr 3jca ubthlem11 lemul2a syl11anc cns ubthlem9 fveq2d nvsge0 eqtr2d
      wceq eqid ad2ant2lr cc recnd mul32 syl3an 3comr adantrrr adantrl ad2ant2r
      3expb 3brtr3d ) MVBZVCVDZNVBZVCVDZVEZUEVBZUAVDZUDVBZVFVDZVGUUDVHVIZVEZAVB
      ZUAVDZUUHUCVJZVEZVEZVEZUUBUUDFVKVLVMYQDVLVNZVEVEZVOUUDVPVMZUUHPVLZVQVMZGU
      UBQVMZYSIVLZVLZRVLZVQVMZUURVOYQVQVMZVQVMZUUHUUTVLZRVLZUUPUVDVQVMUUQVQVMZV
      RUUOUVBVFVDZUVDVFVDZUURVFVDZVGUURVRVIZVEZVSZUVBUVDVRVIUVCUVEVRVIUUAUUMUVN
      UUNUUAUUMVEUVIUVJUVMYTUUMUVIYRYTUUMVEZTWAVDZUVAUBVDZUVIUMYTUAUBUUTVTZUUSU
      AVDZUVQUUMYTUUTEVDZUVRVCEYSIUKWBJWAVDZUVPUVTUVRULUMEUUTJTUAUBUFUGUJWCWDWE
      UUCUULGUAVDZUVSUUCUUEUUKUWBUUFAGHJOPQUAUCUDUEUFULUOUPUQURUSVAWFWLUWBUUCUV
      SUWAUWBUUCUVSULGUUBJQUAUFUQWGWHWIWJUAUBUUSUUTWKWMZUVATRUBUGUHWNWOWPYRUVJY
      TUUMYRVOVFVDZYQVFVDUVJXBYQWQVOYQWRWOZWSUULUVMUUAUUCUUGUUIUVMUUJUUGUUIVEUV
      KUVLUUGUUPVFVDZUUQVFVDZUVKUUIUUEUUFUUDVGVJZUWFUUDWTUWDUUEUWHUWFXBVOUUDXAW
      HWJZUWAUUIUWGULUUHJPUAUFUOWNXCZUUPUUQWRWMUUGUWFVGUUPVRVIZVEUWGVGUUQVRVIZV
      EUVLUUIUUGUWFUWKUWIUWDVGVOVRVIUUGUWKXBVGVOXDXBXEXFVOUUDXGXHXIUUIUWGUWLUWJ
      UWAUUIUWLULUUHJPUAUFUOXJXCXIUWFUWGUWKUWLUVLUUPUUQXKXLWMXIXNZXMXOXNABCDEFG
      HIJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKULUMUNUOUPUQURUSUTVAXPUVBUVDUURXQXRYTU
      UMUVCUVGYDYRUUNUVOUVGUURUVATXSVLZVMZRVLZUVCUVOUVFUWORAEGHUWNIJNOPQTUAUCUD
      UEUFULUOUPUQURUSVAUJUKUMUWNYEZXTYAUVOUVMUVQUWPUVCYDZUULUVMYTUUCUWMXMUWCUV
      PUVMUVQUWRUMUURUVAUWNTRUBUGUWQUHYBWHXRYCYFYRUUMUVEUVHYDZYTUUNYRUULUWSUUCY
      RUUGUUIUWSUUJYRUUGUUIUWSUUGUUIYRUWSUUGUUPYGVDUUIUUQYGVDYRUVDYGVDUWSUUGUUP
      UWIYHUUIUUQUWJYHYRUVDUWEYHUUPUUQUVDYIYJYKYOYLYMYNYP $.
      $( [1-Jan-2008] $)

    $( Lemma for ~ ubthi .  Upper bound for the operator norm of any operator
       ` T `` n ` . $)
    ubthlem13 $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\
  ( r e. RR /\ 0 < r ) ) /\ ( p ( ball ` D ) r ) C_ ( A ` k ) ) ) ->
         ( O ` ( T ` n ) ) <_ ( ( 2 / r ) x. ( 2 x. k ) ) ) $=
      ( cv cn wcel wa cr cc0 clt wbr cbl cfv co wss clno cdiv cmul cle wne wral
      c2 ffvelrni cnv eqid bloln mp3an12 syl ad2antlr gt0ne0 2re redivcl mp3an1
      wi syldan nnre remulcl sylancr syl2an 0re ltleii divge0 mpanl12 jca nngt0
      2pos ltle mpan sylc mulge0 syl11anc ancoms ad2ant2r ubthlem12 exp32 exp4d
      adantrl imp3a com34 imp32 exp3a r19.21aiv nmlnoubi syl111anc ) MVBZVCVDZN
      VBZVCVDZVEZUEVBZUAVDZUDVBZVFVDZVGYJVHVIZVEZVEZYHYJFVJVKVLYCDVKVMZVEZVEZYE
      IVKZJTVNVLZVDZVTYJVOVLZVTYCVPVLZVPVLZVFVDZVGUUCVQVIZVEZAVBZUCVRZUUGYRVKRV
      KUUCUUGPVKVPVLVQVIZWLZAUAVSYRSVKUUCVQVIYFYTYDYPYFYREVDZYTVCEYEIUKWAJWBVDT
      WBVDUUKYTULUMEYRJYSTYSWCZUJWDWEWFWGYDYNUUFYFYOYDYMUUFYIYMYDUUFYMYDVEUUDUU
      EYMUUAVFVDZUUBVFVDZUUDYDYKYLYJVGVRZUUMYJWHVTVFVDZYKUUOUUMWIVTYJWJWKWMZYDU
      UPYCVFVDZUUNWIYCWNZVTYCWOWPZUUAUUBWOWQYMUUMVGUUAVQVIZVEUUNVGUUBVQVIZVEUUE
      YDYMUUMUVAUUQUUPVGVTVQVIZYMUVAWIVGVTWRWIXDWSZVTYJWTXAXBYDUUNUVBUUTYDUURVG
      YCVQVIZUVBUUSYDUURVGYCVHVIZUVEUUSYCXCVGVFVDUURUVFUVEWLWRVGYCXEXFXGUUPUVCU
      URUVEVEUVBWIUVDVTYCXHXAXIXBUUAUUBXHWQXBXJXOXKYQUUJAUAYQUUGUAVDZUUHUUIYGYN
      YOUVGUUHVEZUUIWLYGYNUVHYOUUIYGYIYMUVHYOUUIWLZWLYGYIYMUVHUVIYGYIYMUVHVEVEY
      OUUIABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKULUMUNUOUPUQURUSUTVAXLXMXNX
      PXQXRXSXTAUUCYRJPYSRSTUAUCUFUSUOUHUIUULULUMYAYB $.
      $( [25-Jul-2009] $) $( [1-Jan-2008] $)

    $( Lemma for ~ ubthi .  Upper bound for the operator norm of any operator
       ` T `` n ` . $)
    ubthlem13OLD $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\
  ( r e. RR /\ 0 < r ) ) /\ ( p ( ball ` D ) r ) C_ ( A ` k ) ) ) ->
         ( O ` ( T ` n ) ) <_ ( ( 2 / r ) x. ( 2 x. k ) ) ) $=
      ( cv cn wcel wa cr cc0 clt wbr cbl cfv co wss clno cdiv cmul cle wne wral
      c2 ffvelrni cnv eqid bloln mp3an12 syl ad2antlr gt0ne0 2re redivcl mp3an1
      wi syldan nnre remulcl sylancr syl2an 0re ltleii divge0 mpanl12 jca nngt0
      2pos ltle mpan sylc mulge0OLD an4s syl11anc ancoms adantrl ad2ant2r exp32
      ubthlem12 exp4d imp3a com34 imp32 exp3a r19.21aiv nmlnoubi syl111anc ) MV
      BZVCVDZNVBZVCVDZVEZUEVBZUAVDZUDVBZVFVDZVGYKVHVIZVEZVEZYIYKFVJVKVLYDDVKVMZ
      VEZVEZYFIVKZJTVNVLZVDZVTYKVOVLZVTYDVPVLZVPVLZVFVDZVGUUDVQVIZVEZAVBZUCVRZU
      UHYSVKRVKUUDUUHPVKVPVLVQVIZWLZAUAVSYSSVKUUDVQVIYGUUAYEYQYGYSEVDZUUAVCEYFI
      UKWAJWBVDTWBVDUULUUAULUMEYSJYTTYTWCZUJWDWEWFWGYEYOUUGYGYPYEYNUUGYJYNYEUUG
      YNYEVEUUEUUFYNUUBVFVDZUUCVFVDZUUEYEYLYMYKVGVRZUUNYKWHVTVFVDZYLUUPUUNWIVTY
      KWJWKWMZYEUUQYDVFVDZUUOWIYDWNZVTYDWOWPZUUBUUCWOWQYNUUNVGUUBVQVIZVEUUOVGUU
      CVQVIZVEUUFYEYNUUNUVBUURUUQVGVTVQVIZYNUVBWIVGVTWRWIXDWSZVTYKWTXAXBYEUUOUV
      CUVAYEUUSVGYDVQVIZUVCUUTYEUUSVGYDVHVIZUVFUUTYDXCVGVFVDUUSUVGUVFWLWRVGYDXE
      XFXGUUQUVDUUSUVFVEUVCWIUVEUUQUUSUVDUVFUVCVTYDXHXIXAXJXBUUNUUOUVBUVCUUFUUB
      UUCXHXIWQXBXKXLXMYRUUKAUAYRUUHUAVDZUUIUUJYHYOYPUVHUUIVEZUUJWLYHYOUVIYPUUJ
      YHYJYNUVIYPUUJWLZWLYHYJYNUVIUVJYHYJYNUVIVEVEYPUUJABCDEFGHIJKLMNOPQRSTUAUB
      UCUDUEUFUGUHUIUJUKULUMUNUOUPUQURUSUTVAXOXNXPXQXRXSXTYAAUUDYSJPYTRSTUAUCUF
      USUOUHUIUUMULUMYBYC $.
      $( [1-Jan-2008] $)

    ${
      ubthlem14.7 $e |- U e. CBan $.
      $( Lemma for ~ ubthi .  The operator norms of the operators ` T `` n `
         have an upper bound. $)
      ubthlem14 $p |- ( A. x e. X E. c e. RR A. n e. NN ( N ` ( ( T ` n ) ` x
           ) ) <_ c -> E. d e. RR A. n e. NN ( O ` ( T ` n ) ) <_ d ) $=
        ( vk cv cfv cle wbr cn wral cr wrex cc0 clt cbl co wss wa copn ubthlem6
        eqid wcel wi c2 cdiv cmul wne gt0ne0 redivcl mp3an1 syldan nnre remulcl
        2re sylancr syl2an ancoms adantrl adantrr ubthlem13 exp31 imp r19.21aiv
        com23 wceq breq2 ralbidv rcla4ev exp44 exp3a imp4a r19.23adv r19.23aivv
        syl11anc syl ) AVEMVEZIVFZVFQVFUEVEVGVHMVIVJUEVKVLATVJVMUCVEZVNVHZUDVEZ
        XRFVOVFVPVDVEZDVFVQZVRZUCVKVLZUDTVLVDVIVLXQRVFZUFVEZVGVHZMVIVJZUFVKVLZA
        BCDEFIJKLVDMFVSVFZQSTUAUCUDUEUGUHUIUKULVCUNUOYJWAVAVTYDYIVDUDVITYAVIWBZ
        XTTWBZVRZYCYIUCVKYMXRVKWBZXSYBYIYMYNXSYBYIWCZYKYLYNXSVRZYOWCYKYLYPYBYIY
        KYLYPVRZYBVRZVRZWDXRWEVPZWDYAWFVPZWFVPZVKWBZYEUUBVGVHZMVIVJZYIYKYQUUCYB
        YKYPUUCYLYPYKUUCYPYTVKWBZUUAVKWBZUUCYKYNXSXRVMWGZUUFXRWHWDVKWBZYNUUHUUF
        WNWDXRWIWJWKYKUUIYAVKWBUUGWNYAWLWDYAWMWOYTUUAWMWPWQWRWSYSUUDMVIYKYRXPVI
        WBZUUDWCYKUUJYRUUDYKUUJYRUUDABCDEFGHIJKLVDMNOPQRSTUAUBUCUDUGUHUIUJUKULU
        MUNUOUPUQURUSUTVAVBWTXAXDXBXCYHUUEUFUUBVKYFUUBXEYGUUDMVIYFUUBYEVGXFXGXH
        XNXIXBXJXKXLXMXO $.
        $( [1-Jan-2008] $)
    $}
  $}

  ${
    $d c d n x B $.  $d c h j n p r x y z M $.  $d d p r N $.
    $d c d h j n p r x y z T $.  $d c d h n p r x z U $.  $d c d n x W $.
    $d c j n p r x y z X $.
    ubthi.1 $e |- X = ( BaseSet ` U ) $.
    ubthi.3 $e |- M = ( norm ` W ) $.
    ubthi.4 $e |- N = ( U normOp W ) $.
    ubthi.5 $e |- B = ( U BLnOp W ) $.
    ubthi.7 $e |- U e. CBan $.
    ubthi.8 $e |- W e. NrmCVec $.
    ${
      ubthii.6 $e |- T : NN --> B $.
      $( Inference from ~ ubthi . $)
      ubthii $p |- ( A. x e. X E. c e. RR A. n e. NN ( M ` ( ( T ` n ) ` x ) )
        <_ c -> E. d e. RR A. n e. NN ( N ` ( T ` n ) ) <_ d ) $=
        ( cfv eqid vy vz vj vh vp vr cv cn wcel cle wbr wral crab wceq wa copab
        cims c2 cdiv co cnm cmul cns cpv cnsb cba cn0v cbn bnnv ax-mp ubthlem14
        c1 cnv ) AUAUBUCUGZUHUIUAUGUBUGUDUGCSSFSVNUJUKUDUHULUBIUMUNUOUCUAUPZBDU
        QSZUEUGUFUGURUSUTVLAUGZDVASZSUSUTVBUTVQDVCSZUTDVDSZUTZVSCDUDUCEVTVRDVES
        ZFGHIHVFSZDVGSZUFUEJKLWCTMNORDVHUIDVMUIPDVIVJQVPTVRTVTTWBTVSTWDTVOTWATP
        VK $.
        $( [7-Nov-2007] $)
    $}

    $( Uniform Boundedness Theorem, also called the Banach-Steinhaus Theorem.
       Let ` T ` be a sequence of bounded linear operators on a Banach space.
       If, for every vector ` x ` , the norms of the operators' values are
       bounded, then the operators' norms are also bounded.  Theorem 4.7-3 of
       [Kreyszig] p. 249.  See also
       ~ http://en.wikipedia.org/wiki/Uniform_boundedness_principle . $)
    ubthi $p |- ( ( T : NN --> B /\ A. x e. X E. c e. RR A. n e. NN ( M ` ( (
    T ` n ) ` x ) ) <_ c ) -> E. d e. RR A. n e. NN ( N ` ( T ` n ) ) <_ d ) $=
      ( cn cfv cle wf cv wbr wral cr wrex wi c0o csn cxp cif wceq fveq1d fveq2d
      co fveq1 breq1d rexralbidv ralbidv imbi12d wss oprex fconst wcel cnv bnnv
      cbn ax-mp eqid 0blo mp2an snssi fss elimf ubthii dedth imp ) RBCUAZAUBZEU
      BZCSZSZFSZJUBZTUCZERUDJUEUFZAIUDZWAGSZKUBZTUCZERUDKUEUFZVRWGWKUGVSVTVRCRD
      HUHUOZUIZUJZUKZSZSZFSZWDTUCZERUDJUEUFZAIUDZWPGSZWITUCZERUDKUEUFZUGCWNCWOU
      LZWGXAWKXDXEWFWTAIXEWEWSJEUERXEWCWRWDTXEWBWQFXEVSWAWPVTCWOUPZUMUNUQURUSXE
      WJXCKEUERXEWHXBWITXEWAWPGXFUNUQURUTABWODEFGHIJKLMNOPQRBCWNRWMWNUAWMBVAZRB
      WNUARWLDHUHVBVCWLBVDZXGDVEVDZHVEVDXHDVGVDXIPDVFVHQBDHWLWLVIOVJVKWLBVLVHRW
      MBWNVMVKVNVOVPVQ $.
      $( [7-Nov-2007] $)
  $}


