$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          Uniform Boundedness Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d p A $.  $d h j m n y z N $.  $d h n z P $.  $d h j m n y z T $.
    $d j p y z X $.  $d h j k m n p y z $.
    ubthlem1.1 $e |- X = ( BaseSet ` U ) $.
    ubthlem1.11 $e |- A = { <. j , y >. | ( j e. NN /\ y = { z e. X |
       A. h e. NN ( N ` ( ( T ` h ) ` z ) ) <_ j } ) } $.
    $( Lemma for ~ ubthi .  Membership in ` A `` k ` , the set of all vectors
       ` ( T `` n ) `` z ` whose norm is less than ` k ` . $)
    ubthlem1 $p |- ( k e. NN -> ( P e. ( A ` k ) <-> ( P e. X /\
       A. n e. NN ( N ` ( ( T ` n ) ` P ) ) <_ k ) ) ) $=
      ( cv cn wcel cfv cle wbr wral wa crab wceq breq2 ralbidv rabbidv cba cvv
      fvex eqeltri rabex fvopab4 eleq2d fveq2 fveq2d breq1d elrab syl6bb
      fveq1d cbvralv anbi2i ) IOZPQZDVCCRZQZDLQZDGOZERZRZKRZVCSTZGPUAZUBZVGDJOZ
      ERZRZKRZVCSTZJPUAZUBVDVFDBOZVIRZKRZVCSTZGPUAZBLUCZQVNVDVEWFDHAVCWCHOZSTZG
      PUAZBLUCWFPCWGVCUDZWIWEBLWJWHWDGPWGVCWCSUEUFUGNWEBLLFUHRUIMFUHUJUKULUMUNW
      EVMBDLWADUDZWDVLGPWKWCVKVCSWKWBVJKWADVIUOUPUQUFURUSVMVTVGVLVSGJPVHVOUDZVK
      VRVCSWLVJVQKWLDVIVPVHVOEUOUTUPUQVAVBUS $.
      $( [19-Dec-2007] $)

    $( Lemma for ~ ubthi . ` A `` k ` is a set of vectors. $)
    ubthlem2 $p |- ( k e. NN -> ( A ` k ) C_ X ) $=
      ( vp vm cv cn wcel cfv cle wbr wral ubthlem1 simprbda ex ssrdv ) HOZPQZMU
      FCRZJUGMOZUHQZUIJQZUGUJUKUINODRRIRUFSTNPUAABCUIDEFGHNIJKLUBUCUDUE $.
      $( [7-Nov-2007] $)
  $}

  ${
    $d g m A $.  $d m C $.  $d g m x D $.  $d g G $.  $d n j h z y N $.
    $d g n j m h z y x T $.  $d g W $.  $d g j z y X $.  $d g m x Y $.
    $d f g n j k m h w z y x $.
    ubthlem3.1 $e |- X = ( BaseSet ` U ) $.
    ubthlem3.2 $e |- Y = ( BaseSet ` W ) $.
    ubthlem3.3 $e |- N = ( norm ` W ) $.
    ubthlem3.5 $e |- B = ( U BLnOp W ) $.
    ubthlem3.6 $e |- T : NN --> B $.
    ubthlem3.u $e |- U e. NrmCVec $.
    ubthlem3.w $e |- W e. NrmCVec $.
    ubthlem3.9c $e |- C = ( IndMet ` U ) $.
    ubthlem3.9d $e |- D = ( IndMet ` W ) $.
    ubthlem3.11 $e |- A = { <. j , y >. | ( j e. NN /\ y = { z e. X |
       A. h e. NN ( N ` ( ( T ` h ) ` z ) ) <_ j } ) } $.
    ubthlem3.12 $e |- G = { <. m , x >. | ( m e. NN /\ x =
                 ( ( T ` n ) ` ( f ` m ) ) ) } $.
    $( Lemma for ~ ubthi .  The limit of any sequence in ` A `` k ` has
       operator value norm less than ` k ` , for any bounded linear operator
       ` T `` n ` , using ~ metcn4i (with continuity shown by ~ blocn2 ) and
       the properties of ` A `` k ` . $)
    ubthlem3 $p |- ( ( ( f : NN --> ( A ` k ) /\ f ( ~~>m ` C ) w ) /\
         ( k e. NN /\ n e. NN ) ) -> ( N ` ( ( T ` n ) ` w ) ) <_ k ) $=
      ( vg cn cv cfv wf clm wbr wa wcel cr cle wral ffvelrni cnv blof mp3an12
      syl adantl ad2antlr wss ubthlem2 fss sylan2 ffvelrn sylan adantlrr
      syl11anc adantllr r19.21aiva fopab2 sylib copn ccn co cdm eqid blocn2
      ad2antll wceq wb imsbai feq3 ax-mp adantrr anim1i an32s cme imsmet
      visset metcn4i mp3anl1 mpanl1 nnre ad2antrl fveq2 fveq2d fvex fvopab4
      adantlr simplrl simplrr ubthlem1 simplbda ancoms r19.21bi syl21anc
      eqbrtrd nvlmle syl22anc ) UNNUOZEUPZKUOZUQZYDDUOZGURUPUSZUTZYBUNVAZPUOZUN
      VAZUTZUTZUNUAQUQZQYFYJIUPZUPZHURUPUSZYBVBVAZUMUOZQUPZRUPZYBVCUSZUMUNVDZYP
      RUPYBVCUSZYMOUOZYDUPZYOUPZUAVAZOUNVDYNYMUUHOUNYEYLUUEUNVAZUUHYGYEYLUTZUUI
      UTTUAYOUQZUUFTVAZUUHYLUUKYEUUIYKUUKYIYKYOFVAZUUKUNFYJIUFVEZJVFVAZSVFVAZUU
      MUUKUGUHFYOJSTUAUBUCUEVGVHVIVJVKYEYIUUIUULYKYEYIUTZUNTYDUQZUUIUULYIYEYCTV
      LUURBCEIJLMNRTUBUKVMUNYCTYDVNVOZUNTUUEYDVPVQVRTUAUUFYOVPVSVTWAOAUNUAUUGQU
      LWBWCYMYOGWDUPZHWDUPZWEWFVAZUNGWGWGZYDUQZYGUTZYQYKUVBYHYIYKUUMUVBUUNFGHYO
      JUUTUVASUIUJUUTWHZUVAWHZUEUGUHWIVIWJYEYLYGUVEUUJUVDYGYEYIUVDYKUUQUURUVDUU
      STUVCWKUURUVDWLGJTUBUIUGWMTUVCUNYDWNWOWCWPWQWRHWSVAZUVBUVEYQUUPUVHUHHSUJW
      TWOGWSVAZUVHUVBUVEYQUUOUVIUGGJUIWTWOAGHYFOYOYDQUUTUVAUVCUVCWHUVFUVGULDXAX
      BXCXDVSYIYRYHYKYBXEXFYEYLUUCYGUUJUUBUMUNUUJYSUNVAZUTZUUAYSYDUPZYOUPZRUPZY
      BVCUVJUUAUVNWKUUJUVJYTUVMROAYSUUGUVMUNQUUEYSWKUUFUVLYOUUEYSYDXGXHULUVLYOX
      IXJXHVJUVKUVLYCVAZYIYKUVNYBVCUSZYEUVJUVOYLUNYCYSYDVPXKYEYIYKUVJXLYEYIYKUV
      JXMUVOYIUTUVPPUNYIUVOUVPPUNVDZYIUVOUVLTVAUVQBCEUVLIJLMNPRTUBUKXNXOXPXQXRX
      SWAXKUUPYNYQYRUUCUTUUDUHHYPYBSUMQRUAUCUDUJYFYOXIXTXCYA $.
      $( [26-Dec-2007] $)
  $}

  ${
    $d c f k m n p r x A $.  $d f k m n r x D $.  $d f k p r x J $.
    $d h j k n y z N $.  $d h j k m n w y z T $.  $d m w W $.
    $d c f j k n x y z X $.  $d m w Y $.  $d f h w x $.
    ubthlem4.1 $e |- X = ( BaseSet ` U ) $.
    ubthlem4.2 $e |- Y = ( BaseSet ` W ) $.
    ubthlem4.3 $e |- N = ( norm ` W ) $.
    ubthlem4.5 $e |- B = ( U BLnOp W ) $.
    ubthlem4.6 $e |- T : NN --> B $.
    ubthlem4.7 $e |- U e. CBan $.
    ubthlem4.8 $e |- W e. NrmCVec $.
    ubthlem4.9 $e |- D = ( IndMet ` U ) $.
    ubthlem4.10 $e |- J = ( MetOpen ` D ) $.
    ubthlem4.11 $e |- A = { <. j , y >. | ( j e. NN /\ y = { z e. X |
       A. h e. NN ( N ` ( ( T ` h ) ` z ) ) <_ j } ) } $.
    $( Lemma for ~ ubthi .  The set ` A `` k ` is therefore closed by
       ~ metcld .  (The hypothesis ` U e. CBan ` could be weakened to
       ` U e. NrmCVec ` .) $)
    ubthlem4 $p |- ( k e. NN -> ( A ` k ) e. ( Clsd ` J ) ) $=
      ( vf vx vn vw vm cv cn wcel cfv ccld wf clm wbr wa wi wal cle wral cnv
      cbn bnnv ax-mp visset nvlmcl mpan ad2antll cims wceq copab eqid ubthlem3
      ancoms an32s r19.21aiva jca ex ubthlem1 sylibrd 19.21aivv wss wb
      ubthlem2 cme imsmet imsbai metcld syl mpbird ) JUKZULUMZWNCUNZKUOUNUMZULW
      PUFUKZUPZWRUGUKZEUQUNURZUSZWTWPUMZUTZUFVAUGVAZWOXDUGUFWOXBWTNUMZWTUHUKZFU
      NZUNLUNWNVBURZUHULVCZUSZXCWOXBXKWOXBUSZXFXJXAXFWOWSGVDUMZXAXFGVEUMXMUAGVF
      VGZEWTGWRNPUCUGVHVIVJVKXLXIUHULWOXGULUMZXBXIXBWOXOUSXIUIABUGCDEMVLUNZFGUF
      HIJUJUHUJUKZULUMUIUKXQWRUNXHUNVMUSUJUIVNZLMNOPQRSTXNUBUCXPVOUEXRVOVPVQVRV
      SVTWAABCWTFGHIJUHLNPUEWBWCWDWOWPNWEZWQXEWFZABCFGHIJLNPUEWGEWHUMZXSXTXMYAX
      NEGUCWIVGUGEUFKWPNEGNPUCXNWJUDWKVJWLWM $.
      $( [7-Nov-2007] $)

    $( Lemma for ~ ubthi .  The union of all ` A `` k ` equals the base set,
       provided the values of the operators ` T `` n ` are bounded for each
       vector ` x ` . $)
    ubthlem5 $p |- ( A. x e. X E. c e. RR A. n e. NN
   ( N ` ( ( T ` n ) ` x ) ) <_ c -> U_ k e. NN ( A ` k ) = X ) $=
      ( cv cfv cle wbr cn wral cr wrex ciun wss iunss ubthlem2 mprgbir a1i
      wcel wa clt arch ad2antlr wi nnre ltle sylan2 adantll adantlr cnv wf
      ffvelrni cbn bnnv ax-mp blof mp3an12 syl ffvelrn sylan ancoms nvcl
      sylancr simpllr letr syl111anc exp4b com34 com23 imp31 ralimdva ex imp
      simplll jctild wb ubthlem1 sylibrd an32s syld reximdva mpd r19.23adva
      eliun syl6ibr ralimia dfss3 sylibr eqssd ) AUIZLUIZGUJZUJZNUJZRUIZUKULZLU
      MUNZRUOUPZAPUNZKUMKUIZDUJZUQZPYFPURZYCYGYEPURKUMKUMYEPUSBCDGHIJKNPSUHUTVA
      VBYCXNYFVCZAPUNPYFURYBYHAPXNPVCZYBXNYEVCZKUMUPZYHYIYAYKRUOYIXSUOVCZVDZYAY
      KYMYAVDZXSYDVEULZKUMUPZYKYLYPYIYAXSKVFVGYNYOYJKUMYNYDUMVCZVDYOXSYDUKULZYJ
      YMYQYOYRVHZYAYLYQYSYIYQYLYDUOVCZYSYDVIZXSYDVJVKVLVMYMYQYAYRYJVHYMYQVDZYAV
      DZYRYIXRYDUKULZLUMUNZVDZYJUUCYRUUEYIUUBYAYRUUEVHUUBYRYAUUEUUBYRYAUUEVHUUB
      YRVDXTUUDLUMUUBYRXOUMVCZXTUUDVHZUUBUUGYRUUHUUBUUGXTYRUUDUUBUUGXTYRUUDUUBU
      UGVDXRUOVCZYLYTXTYRVDUUDVHYMUUGUUIYQYIUUGUUIYLYIUUGVDOVNVCZXQQVCZUUIUEUUG
      YIUUKUUGPQXPVOZYIUUKUUGXPEVCZUULUMEXOGUCVPHVNVCZUUJUUMUULHVQVCUUNUDHVRVSU
      EEXPHOPQSTUBVTWAWBPQXNXPWCWDWEXQONQTUAWFWGVMVMYIYLYQUUGWHYQYTYMUUGUUAVGXR
      XSYDWIWJWKWLWMWNWOWPWMWQYIYLYQYAWRWSYQYJUUFWTYMYABCDXNGHIJKLNPSUHXAVGXBXC
      XDXEXFWPXGKXNUMYEXHXIXJAPYFXKXLXM $.
      $( [26-Dec-2007] $)

    $( Lemma for ~ ubthi .  Using ~ bcth (via ~ bcthlem33 ), at least one set
       ` A `` k ` contains a ball. $)
    ubthlem6 $p |- ( A. x e. X E. c e. RR A. n e. NN ( N ` ( ( T ` n ) ` x
        ) ) <_ c -> E. k e. NN E. p e. X E. r e. RR ( 0 < r /\
                 ( p ( ball ` D ) r ) C_ ( A ` k ) ) ) $=
      ( cv cfv cle wbr cn wral cr wrex cnt c0 wne cc0 clt cbl co wss wa crn
      cuni wceq ccld ciun wfn crab cba cvv fvex eqeltri rabex fnopab2 fniunfv
      ax-mp ubthlem5 syl5eqr wcel wb fvelrnb simpr ubthlem4 adantr eqeltrrd
      r19.23aiva sylbi ssriv cbn cms bncms cn0v cnv bnnv eqid nvzcl ne0i
      imsbai cpw wf ffnfv ubthlem2 elpw sylibr rgen mpbir2an bcthlem33 sylancl
      wex ctop cme imsmet opntop uniopn2 eqcomi ntrss2 sylancr sstrd sseld
      ntropn opni2 mp3an1 sylan ex sstr sylan2 expcom anim2d reximdv syld jcad
      eximdv n0 df-rex 3imtr4g reximia syl ) AUKZLUKGULULNULTUKUMUNLUOUPTUQURAP
      UPZKUKZDULZMUSULULZUTVAZKUOURZVBRUKZVCUNZSUKZUUKFVDULVEZUUGVFZVGZRUQURZSP
      URZKUOURUUEDVHZVIZPVJUUSMVKULZVFUUJUUEUUTKUOUUGVLZPDUOVMZUVBUUTVJJBUOCUKI
      UKGULULNULJUKUMUNIUOUPZCPVNDUVDCPPHVOULVPUAHVOVQVRVSUJVTZKUODWAWBABCDEFGH
      IJKLMNOPQTUAUBUCUDUEUFUGUHUIUJWCWDAUUSUVAUUDUUSWEZUUGUUDVJZKUOURZUUDUVAWE
      ZUVCUVFUVHWFUVEKUOUUDDWGWBUVGUVIKUOUUFUOWEZUVGVGUUGUUDUVAUVJUVGWHUVJUUGUV
      AWEUVGBCDEFGHIJKMNOPQUAUBUCUDUEUFUGUHUIUJWIWJWKWLWMWNFKMDPHWOWEZFWPWEUFFH
      UHWQWBHWRULZPWEZPUTVAHWSWEZUVMUVKUVNUFHWTWBZHPUVLUAUVLXAXBWBPUVLXCWBFHPUA
      UHUVOXDZUIUOPXEZDXFUVCUUGUVQWEZKUOUPKUOUVQDXGUVEUVRKUOUVJUUGPVFZUVRBCDGHI
      JKNPUAUJXHZUUGPUUFDVQXIXJXKXLXMXNUUIUURKUOUVJUUMUUHWEZSXOUUMPWEZUUQVGZSXO
      UUIUURUVJUWAUWCSUVJUWAUWBUUQUVJUUHPUUMUVJUUHUUGPUVJMXPWEZUVSUUHUUGVFZFXQW
      EZUWDUVNUWFUVOFHUHXRWBZFMUIXSWBZUVTUUGMPMVIZPUWFUWIPVJUWGFMPUVPUIXTWBYAZY
      BYCZUVTYDYEUVJUWAUULUUNUUHVFZVGZRUQURZUUQUVJUWAUWNUVJUUHMWEZUWAUWNUVJUWDU
      VSUWOUWHUVTUUGMPUWJYFYCUWFUWOUWAUWNUWGRUUHFUUMMUIYGYHYIYJUVJUWMUUPRUQUVJU
      WLUUOUULUWLUVJUUOUVJUWLUWEUUOUWKUUNUUHUUGYKYLYMYNYOYPYQYRSUUHYSUUQSPYTUUA
      UUBUUC $.
      $( [27-Dec-2007] $)
  $}

  ${
    $d h j n y z N $.  $d h n z Q $.  $d h j n y z T $.  $d j y z X $.
    $d h j k n p y z $.
    ubthlem7.1 $e |- X = ( BaseSet ` U ) $.
    ubthlem7.7 $e |- U e. NrmCVec $.
    ubthlem7.n $e |- L = ( norm ` U ) $.
    ubthlem7.g $e |- G = ( +v ` U ) $.
    ubthlem7.m $e |- M = ( -v ` U ) $.
    ubthlem7.r $e |- R = ( .s ` U ) $.
    ubthlem7.z $e |- Z = ( 0vec ` U ) $.
    ubthlem7.q $e |- Q =
            ( p G ( ( ( r / 2 ) x. ( 1 / ( L ` x ) ) ) R x ) ) $.
    $( Lemma for ~ ubthi .  Auxiliary class ` Q ` is a vector. $)
    ubthlem7 $p |- ( ( p e. X /\ ( r e. RR /\ ( x e. X /\ x =/= Z ) ) ) ->
            Q e. X ) $=
      ( cv wcel cr wne wa c2 cdiv co c1 cfv cmul cc rehalfcl recnd cc0 cnv
      nvcl mpan adantr wceq wb nvz necon3bid biimpar reccl syl11anc mulcl
      syl2an simprl nvscl mp3an1 nvgcl sylan2 syl5eqel ) KTZHUAZJTZUBUAZATZHUAZ
      VRIUCZUDZUDZUDBVNVPUEUFUGZUHVRFUIZUFUGZUJUGZVRCUGZEUGZHSWBVOWGHUAZWHHUAZW
      BWFUKUAZVSWIVQWCUKUAWEUKUAZWKWAVQWCVPULUMWAWDUKUAZWDUNUCZWLVSWMVTVSWDDUOU
      AZVSWDUBUAMVRDFHLNUPUQUMURVSWNVTVSWDUNVRIWOVSWDUNUSVRIUSUTMVRDFHILRNVAUQV
      BVCWDVDVEWCWEVFVGVQVSVTVHWOWKVSWIMWFVRCDHLQVIVJVEWOVOWIWJMVNWGDEHLOVKVJVL
      VM $.
      $( [28-Dec-2007] $)

    $( Lemma for ~ ubthi .  Compute ` x ` in terms of auxiliary vector
       ` Q ` . $)
    ubthlem8 $p |- ( ( p e. X /\ ( ( r e. RR /\ 0 < r ) /\
               ( x e. X /\ x =/= Z ) ) ) ->
         x = ( ( ( 2 / r ) x. ( L ` x ) ) R ( Q M p ) ) ) $=
      ( cv wcel cr cc0 clt wbr wa wne c1 co c2 cdiv cfv cmul wceq cnv nvsid
      mpan ad2antrl adantl eqcomi wb ubthlem7 adantrlr simpl cc rehalfcl recnd
      nvcl adantr nvz necon3bid biimpar reccl syl11anc mulcl syl2an simprl
      nvscl mp3an1 adantlr w3a nvsubadd syl111anc mpbiri opreq2d gt0ne0 2cn
      divcl nvsass anim12i mul4 2ne0 divcan6 mpanl12 recid opreqan12d ax1cn
      mulid1i syl6eq eqtrd opreq1d 3eqtr2rd eqtr3d ) KTZHUAZJTZUBUAZUCXFUDUEZUF
      ZATZHUAZXJIUGZUFZUFZUFZUHXJCUIZXJUJXFUKUIZXJFULZUMUIZBXDGUIZCUIZXNXPXJUNZ
      XEXKYBXIXLDUOUAZXKYBMXJCDHLQUPUQURUSXOYAXSXFUJUKUIZUHXRUKUIZUMUIZXJCUIZCU
      IZXSYFUMUIZXJCUIZXPXOXTYGXSCXOXTYGUNZXDYGEUIZBUNZBYLSUTXOBHUAZXEYGHUAZYKY
      MVAZXEXGXMYNXHABCDEFGHIJKLMNOPQRSVBVCXEXNVDXNYOXEXGXMYOXHXGXMUFYFVEUAZXKY
      OXGYDVEUAZYEVEUAZYQXMXGYDXFVFVGZXMXRVEUAZXRUCUGZYSXKUUAXLXKXRYCXKXRUBUAMX
      JDFHLNVHUQVGVIZXKUUBXLXKXRUCXJIYCXKXRUCUNXJIUNVAMXJDFHILRNVJUQVKVLZXRVMVN
      ZYDYEVOZVPXGXKXLVQYCYQXKYOMYFXJCDHLQVRVSVNVTUSYCYNXEYOWAYPMBXDYGDEGHLOPWB
      UQWCWDWEXNYJYHUNZXEXNXSVEUAZYQXKUUGXIXQVEUAZUUAUUHXMXIXFVEUAZXFUCUGZUUIXI
      XFXGXHVDVGZXFWFZUJVEUAZUUJUUKUUIWGUJXFWHVSVNZUUCXQXRVOVPXIYRYSYQXMXGYRXHY
      TVIZUUEUUFVPXIXKXLVQYCUUHYQXKWAUUGMXSYFXJCDHLQWIUQWCUSXOYIUHXJCXNYIUHUNXE
      XNYIXQYDUMUIZXRYEUMUIZUMUIZUHXNUUIUUAUFYRYSUFYIUUSUNXIUUIXMUUAUUOUUCWJXIY
      RXMYSUUPUUEWJXQXRYDYEWKVNXNUUSUHUHUMUIUHXIXMUUQUHUURUHUMXIUUJUUKUUQUHUNZU
      ULUUMUUNUJUCUGUUJUUKUFUUTWGWLUJXFWMWNVNXMUUAUUBUURUHUNUUCUUDXRWOVNWPUHWQW
      RWSWTUSXAXBXC $.
      $( [28-Dec-2007] $)

    ${
      ubthlem9.5 $e |- B = ( U BLnOp W ) $.
      ubthlem9.6 $e |- T : NN --> B $.
      ubthlem9.8 $e |- W e. NrmCVec $.
      ubthlem9.s $e |- S = ( .s ` W ) $.
      $( Lemma for ~ ubthi .  Evaluate the operator value at ` x ` in terms of
         the operator value at ` Q - p ` . $)
      ubthlem9 $p |- ( ( n e. NN /\ ( p e. X /\ ( ( r e. RR /\ 0 < r ) /\
       ( x e. X /\ x =/= Z ) ) ) ) -> ( ( T ` n ) ` x ) =
           ( ( ( 2 / r ) x. ( L ` x ) ) S ( ( T ` n ) ` ( Q M p ) ) ) ) $=
        ( cv cn wcel cr cc0 clt wbr wa wne cfv c2 cdiv co cmul wceq ubthlem8
        fveq2d adantl clno cc ffvelrni cnv eqid bloln mp3an12 syl gt0ne0 recn
        2cn divcl mp3an1 sylan syldan nvcl mpan recnd mulcl syl2an adantrr
        ubthlem7 adantrlr simpl nvmcl syl11anc jca lnomul mp3anl1 mpanl1 eqtrd
        ) HUIZUJUKZPUIZMUKZOUIZULUKZUMXBUNUOZUPZAUIZMUKZXFNUQZUPZUPZUPZUPXFWRFU
        RZURZUSXBUTVAZXFJURZVBVAZCWTKVAZDVAZXLURZXPXQXLUREVAZXKXMXSVCWSXKXFXRXL
        ACDGIJKMNOPQRSTUAUBUCUDVDVEVFWSXLGLVGVAZUKZXPVHUKZXQMUKZUPZXSXTVCZXKWSX
        LBUKZYBUJBWRFUFVIGVJUKZLVJUKZYGYBRUGBXLGYALYAVKZUEVLVMVNXKYCYDXJYCXAXEX
        GYCXHXEXNVHUKZXOVHUKYCXGXCXDXBUMUQZYKXBVOXCXBVHUKZYLYKXBVPUSVHUKYMYLYKV
        QUSXBVRVSVTWAXGXOYHXGXOULUKRXFGJMQSWBWCWDXNXOWEWFWGVFXKCMUKZXAYDXAXCXIY
        NXDACDGIJKMNOPQRSTUAUBUCUDWHWIXAXJWJYHYNXAYDRCWTGKMQUAWKVSWLWMYIYBYEYFU
        GYHYIYBYEYFRXPXQDEXLGYALMQUBUHYJWNWOWPWFWQ $.
        $( [28-Dec-2007] $)
    $}
  $}

  ${
    $d c k n p r x A $.  $d k n p r x D $.  $d x L $.  $d h j k n x y z N $.
    $d d k p r O $.  $d h n z Q $.  $d d h j k n p r x y z T $.  $d x U $.
    $d x W $.  $d c j k n r x y z X $.
    ubthlem10.1 $e |- X = ( BaseSet ` U ) $.
    ubthlem10.2 $e |- Y = ( BaseSet ` W ) $.
    ubthlem10.3 $e |- N = ( norm ` W ) $.
    ubthlem10.4 $e |- O = ( U normOp W ) $.
    ubthlem10.5 $e |- B = ( U BLnOp W ) $.
    ubthlem10.6 $e |- T : NN --> B $.
    ubthlem10.7 $e |- U e. NrmCVec $.
    ubthlem10.8 $e |- W e. NrmCVec $.
    ubthlem10.9 $e |- D = ( IndMet ` U ) $.
    ubthlem10.n $e |- L = ( norm ` U ) $.
    ubthlem10.g $e |- G = ( +v ` U ) $.
    ubthlem10.m $e |- M = ( -v ` U ) $.
    ubthlem10.r $e |- R = ( .s ` U ) $.
    ubthlem10.z $e |- Z = ( 0vec ` U ) $.
    ubthlem10.11 $e |- A = { <. j , y >. | ( j e. NN /\ y = { z e. X |
       A. h e. NN ( N ` ( ( T ` h ) ` z ) ) <_ j } ) } $.
    ubthlem10.q $e |- Q =
            ( p G ( ( ( r / 2 ) x. ( 1 / ( L ` x ) ) ) R x ) ) $.
    $( Lemma for ~ ubthi .  Upper limit for the norm of an operator value at
       auxiliary vector ` Q ` . $)
    ubthlem10 $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\ ( ( r e. RR /\
   0 < r ) /\ ( x e. X /\ x =/= Z ) ) ) /\ ( p ( ball ` D ) r ) C_ ( A ` k ) )
        ) -> ( N ` ( ( T ` n ) ` Q ) ) <_ k ) $=
      ( cv wcel cr cc0 clt wbr wa wne cbl cfv co wss cn cle c2 cdiv c1 cmul
      wceq cc rehalfcl recnd adantr cnv nvcl mpan wb nvz necon3bid biimpar
      rereccl syl11anc adantl simprl w3a nvsass syl111anc adantlr fveq2d
      halfpos2 biimpa wi 0re ltle sylancr mpd jca simpl nvscl mp3an1 nvsge0
      syl2an eqtrd nv1 opreq2d mulid1 syl ad2antrr 3eqtrd halfpos eqbrtrd
      reccl mulcl anim2i nvelbl2 mp3anl1 mpbird syl5eqel ssel impcom sylan
      wral ubthlem1 simplbda ex ra4 syl6com com3l imp31 sylan2 ) UEVBZUAVCZUDVB
      ZVDVCZVEUUDVFVGZVHZAVBZUAVCZUUHUCVIZVHZVHZVHZUUBUUDFVJVKVLZMVBZDVKZVMZVHU
      UOVNVCZNVBZVNVCZVHGUUPVCZGUUSIVKVKRVKUUOVOVGZUUMGUUNVCZUUQUVAUUMGUUBUUDVP
      VQVLZVRUUHPVKZVQVLZVSVLZUUHHVLZOVLZUUNVAUUMUVIUUNVCZUVHPVKZUUDVFVGZUULUVL
      UUCUULUVKUVDUUDVFUULUVKUVDUVFUUHHVLZPVKZVSVLZUVDVRVSVLZUVDUULUVKUVDUVMHVL
      ZPVKZUVOUULUVHUVQPUUEUUKUVHUVQVTZUUFUUEUUKVHZUVDWAVCZUVFWAVCZUUIUVSUUEUWA
      UUKUUEUVDUUDWBZWCZWDUUKUWBUUEUUKUVFUUKUVEVDVCZUVEVEVIZUVFVDVCUUIUWEUUJJWE
      VCZUUIUWEULUUHJPUAUFUOWFWGZWDUUIUWFUUJUUIUVEVEUUHUCUWGUUIUVEVEVTUUHUCVTWH
      ULUUHJPUAUCUFUSUOWIWGWJWKZUVEWLWMWCZWNUUEUUIUUJWOZUWGUWAUWBUUIWPUVSULUVDU
      VFUUHHJUAUFURWQWGWRWSWTUUGUVDVDVCZVEUVDVOVGZVHZUVMUAVCZUVRUVOVTZUUKUUGUWL
      UWMUUEUWLUUFUWCWDUUGVEUVDVFVGZUWMUUEUUFUWQUUDXAXBUUEUWQUWMXCZUUFUUEVEVDVC
      UWLUWRXDUWCVEUVDXEXFWDXGXHUUKUWBUUIUWOUWJUUIUUJXIUWGUWBUUIUWOULUVFUUHHJUA
      UFURXJXKWMUWGUWNUWOUWPULUVDUVMHJPUAUFURUOXLXKXMXNUUKUVOUVPVTUUGUUKUVNVRUV
      DVSUWGUUIUUJUVNVRVTULUUHHJPUAUCUFURUSUOXOXKXPWNUUEUVPUVDVTZUUFUUKUUEUWAUW
      SUWDUVDXQXRXSXTUUGUVDUUDVFVGZUUKUUEUUFUWTUUDYAXBWDYBWNUUMUUCUVHUAVCZVHUUG
      UVJUVLWHZUULUXAUUCUUEUUKUXAUUFUVTUVGWAVCZUUIUXAUUEUWAUWBUXCUUKUWDUUKUVEWA
      VCZUWFUWBUUIUXDUUJUUIUVEUWHWCWDUWIUVEYCWMUVDUVFYDXMUWKUWGUXCUUIUXAULUVGUU
      HHJUAUFURXJXKWMWSYEUUCUUGUUKWOUWGUUCUXAUUGUXBULUVHFUUBUUDJOPUAUFUPUOUNYFY
      GWMYHYIUUQUVCUVAUUNUUPGYJYKYLUURUUTUVAUVBUVAUURUUTUVBUURUVAUVBNVNYMZUUTUV
      BXCUURUVAUXEUURUVAGUAVCUXEBCDGIJKLMNRUAUFUTYNYOYPUVBNVNYQYRYSYTUUA $.
      $( [27-Dec-2007] $)

    $( Lemma for ~ ubthi .  Upper limit for the norm of an operator value at
       ` Q - p ` . $)
    ubthlem11 $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\ ( ( r e. RR
  /\ 0 < r ) /\ ( x e. X /\ x =/= Z ) ) ) /\ ( p ( ball ` D ) r ) C_ ( A ` k )
        ) ) -> ( N ` ( ( T ` n ) ` ( Q M p ) ) ) <_ ( 2 x. k ) ) $=
      ( cv cn wcel wa cr cc0 clt wbr wne cbl cfv co wss cnsb caddc c2 cmul cle
      cnv wf ffvelrni blof mp3an12 syl ubthlem7 adantrlr ffvelrn syl2an sylan
      adantrr eqid nvmcl mp3an1 syl11anc nvcl sylancr ad2ant2lr readdcl nnre
      ad2antrr nvmtri ubthlem10 cme imsmet ax-mp imsbai blcntr mpanl1 ssel2
      ancoms adantlrr wral wi ubthlem1 simplbda ex ra4 syl6com com3l imp31
      sylan2 jca le2add mp2and letrd wceq clno bloln simpl lnosub mp3anl1
      fveq2d cc nncn 2times 3brtr4d ) MVBZVCVDZNVBZVCVDZVEZUEVBZUAVDZUDVBZVFVDZ
      VGUUEVHVIZVEZAVBZUAVDUUIUCVJVEZVEZVEZUUCUUEFVKVLVMZYRDVLZVNZVEZVEZGYTIVLZ
      VLZUUCUURVLZTVOVLZVMZRVLZYRYRVPVMZGUUCQVMUURVLZRVLZVQYRVRVMZVSUUQUVCUUSRV
      LZUUTRVLZVPVMZUVDUUAUULUVCVFVDZYSUUOUUAUULVEZTVTVDZUVBUBVDZUVKUMUVLUUSUBV
      DZUUTUBVDZUVNUUAUAUBUURWAZGUAVDZUVOUULUUAUUREVDZUVQVCEYTIUKWBZJVTVDZUVMUV
      SUVQULUMEUURJTUAUBUFUGUJWCWDWEZUUDUUFUUJUVRUUGAGHJOPQUAUCUDUEUFULUOUPUQUR
      USVAWFZWGUAUBGUURWHWIZUUAUUDUVPUUKUUAUVQUUDUVPUWBUAUBUUCUURWHWJZWKZUVMUVO
      UVPUVNUMUUSUUTTUVAUBUGUVAWLZWMWNWOUVBTRUBUGUHWPWQWRUUAUULUVJVFVDZYSUUOUVL
      UVHVFVDZUVIVFVDZUWHUVLUVMUVOUWIUMUWDUUSTRUBUGUHWPWQZUUAUUDUWJUUKUUAUUDVEU
      VMUVPUWJUMUWEUUTTRUBUGUHWPWQWKZUVHUVIWSWOWRYSUVDVFVDZUUAUUPYSYRVFVDZUWNUW
      MYRWTZUWOYRYRWSWOXAUUAUULUVCUVJVSVIZYSUUOUVLUVOUVPUWPUWDUWFUVMUVOUVPUWPUM
      UUSUUTTUVARUBUGUWGUHXBWNWOWRUUQUVHYRVSVIZUVIYRVSVIZUVJUVDVSVIZABCDEFGHIJK
      LMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKULUMUNUOUPUQURUSUTVAXCUUPUUBUUCUUNVDZUWRUU
      DUUHUUOUWTUUJUUDUUHVEUUCUUMVDZUUOUWTFXDVDZUUDUUHUXAUWAUXBULFJUNXEXFFUUCUU
      EUAFJUAUFUNULXGXHXIUUOUXAUWTUUMUUNUUCXJXKWJXLYSUUAUWTUWRUWTYSUUAUWRYSUWTU
      WRNVCXMZUUAUWRXNYSUWTUXCYSUWTUUDUXCBCDUUCIJKLMNRUAUFUTXOXPXQUWRNVCXRXSXTY
      AYBUUQUWIUWJVEZUWNUWNVEZUWQUWRVEUWSXNUUAUULUXDYSUUOUVLUWIUWJUWKUWLYCWRYSU
      XEUUAUUPYSUWNUWNUWOUWOYCXAUVHUVIYRYRYDWOYEYFUUAUULUVFUVCYGYSUUOUVLUVEUVBR
      UUAUURJTYHVMZVDZUVRUUDVEZUVEUVBYGZUULUUAUVSUXGUVTUWAUVMUVSUXGULUMEUURJUXF
      TUXFWLZUJYIWDWEUUDUUFUUJUXHUUGUUDUUFUUJVEZVEUVRUUDUWCUUDUXKYJYCWGUVMUXGUX
      HUXIUMUWAUVMUXGUXHUXIULGUUCUURJUXFQUVATUAUFUQUWGUXJYKYLXIWIYMWRYSUVGUVDYG
      ZUUAUUPYSYRYNVDUXLYRYOYRYPWEXAYQ $.
      $( [31-Dec-2007] $)

    $( Lemma for ~ ubthi .  Upper limit for the norm of an operator value at
       ` x ` . $)
    ubthlem12 $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\ (
  ( r e. RR /\ 0 < r ) /\ ( x e. X /\ x =/= Z ) ) ) /\ ( p ( ball ` D ) r ) C_
  ( A ` k ) ) ) -> ( N ` ( ( T ` n ) ` x ) ) <_ ( ( ( 2 / r ) x. ( 2 x. k ) )
         x. ( L ` x ) ) ) $=
      ( cv cn wcel wa cr cc0 clt wbr wne cbl cfv co wss c2 cdiv cmul cle w3a
      cnv wf ffvelrni blof mp3an12 syl ubthlem7 adantrlr nvmcl mp3an1 ancoms
      syldan ffvelrn syl2an nvcl sylancr adantll 2re nnre remulcl ad2antrr
      gt0ne0 redivcl mpan 0re 2pos ltleii divge0 mpanl12 jca nvge0 mulge0
      adantrr ad2antll 3jca ubthlem11 lemul2a syl11anc wceq cns eqid ubthlem9
      fveq2d nvsge0 eqtr2d ad2ant2lr cc recnd mul32 syl3an 3comr 3expb
      adantrrr adantrl ad2ant2r 3brtr3d ) MVBZVCVDZNVBZVCVDZVEZUEVBZUAVDZUDVBZV
      FVDZVGUUCVHVIZVEZAVBZUAVDZUUGUCVJZVEZVEZVEZUUAUUCFVKVLVMYPDVLVNZVEVEZVOUU
      CVPVMZUUGPVLZVQVMZGUUAQVMZYRIVLZVLZRVLZVQVMZUUQVOYPVQVMZVQVMZUUGUUSVLZRVL
      ZUUOUVCVQVMUUPVQVMZVRUUNUVAVFVDZUVCVFVDZUUQVFVDZVGUUQVRVIZVEZVSZUVAUVCVRV
      IUVBUVDVRVIYTUULUVMUUMYTUULVEUVHUVIUVLYSUULUVHYQYSUULVEZTVTVDZUUTUBVDZUVH
      UMYSUAUBUUSWAZUURUAVDZUVPUULYSUUSEVDZUVQVCEYRIUKWBJVTVDZUVOUVSUVQULUMEUUS
      JTUAUBUFUGUJWCWDWEUUBUUKGUAVDZUVRUUBUUDUUJUWAUUEAGHJOPQUAUCUDUEUFULUOUPUQ
      URUSVAWFWGUWAUUBUVRUVTUWAUUBUVRULGUUAJQUAUFUQWHWIWJWKUAUBUURUUSWLWMZUUTTR
      UBUGUHWNWOWPYQUVIYSUULYQVOVFVDZYPVFVDUVIWQYPWRVOYPWSWOZWTUUKUVLYTUUBUUFUU
      HUVLUUIUUFUUHVEUVJUVKUUFUUOVFVDZUUPVFVDZUVJUUHUUDUUEUUCVGVJZUWEUUCXAUWCUU
      DUWGUWEWQVOUUCXBWIWKZUVTUUHUWFULUUGJPUAUFUOWNXCZUUOUUPWSWMUUFUWEVGUUOVRVI
      ZVEUWFVGUUPVRVIZVEUVKUUHUUFUWEUWJUWHUWCVGVOVRVIUUFUWJWQVGVOXDWQXEXFVOUUCX
      GXHXIUUHUWFUWKUWIUVTUUHUWKULUUGJPUAUFUOXJXCXIUUOUUPXKWMXIXLZXMXNXLABCDEFG
      HIJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKULUMUNUOUPUQURUSUTVAXOUVAUVCUUQXPXQYSU
      ULUVBUVFXRYQUUMUVNUVFUUQUUTTXSVLZVMZRVLZUVBUVNUVEUWNRAEGHUWMIJNOPQTUAUCUD
      UEUFULUOUPUQURUSVAUJUKUMUWMXTZYAYBUVNUVLUVPUWOUVBXRZUUKUVLYSUUBUWLXMUWBUV
      OUVLUVPUWQUMUUQUUTUWMTRUBUGUWPUHYCWIXQYDYEYQUULUVDUVGXRZYSUUMYQUUKUWRUUBY
      QUUFUUHUWRUUIYQUUFUUHUWRUUFUUHYQUWRUUFUUOYFVDUUHUUPYFVDYQUVCYFVDUWRUUFUUO
      UWHYGUUHUUPUWIYGYQUVCUWDYGUUOUUPUVCYHYIYJYKYLYMYNYO $.
      $( [1-Jan-2008] $)

    $( Lemma for ~ ubthi .  Upper limit for the norm of an operator value at
       ` x ` . $)
    ubthlem12OLD $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\ (
  ( r e. RR /\ 0 < r ) /\ ( x e. X /\ x =/= Z ) ) ) /\ ( p ( ball ` D ) r ) C_
  ( A ` k ) ) ) -> ( N ` ( ( T ` n ) ` x ) ) <_ ( ( ( 2 / r ) x. ( 2 x. k ) )
         x. ( L ` x ) ) ) $=
      ( cv cn wcel wa cr cc0 clt wbr wne cbl cfv co wss c2 cdiv cmul cle w3a
      cnv wf ffvelrni blof mp3an12 syl ubthlem7 adantrlr nvmcl mp3an1 ancoms
      syldan ffvelrn syl2an nvcl sylancr adantll 2re nnre remulcl ad2antrr
      gt0ne0 redivcl mpan 0re 2pos ltleii divge0 mpanl12 jca nvge0 mulge0OLD
      an4s adantrr ad2antll 3jca ubthlem11 lemul2a syl11anc wceq cns eqid
      ubthlem9 fveq2d nvsge0 eqtr2d ad2ant2lr cc recnd mul32 syl3an 3comr
      3expb adantrrr adantrl ad2ant2r 3brtr3d ) MVBZVCVDZNVBZVCVDZVEZUEVBZUAVDZ
      UDVBZVFVDZVGUUDVHVIZVEZAVBZUAVDZUUHUCVJZVEZVEZVEZUUBUUDFVKVLVMYQDVLVNZVEV
      EZVOUUDVPVMZUUHPVLZVQVMZGUUBQVMZYSIVLZVLZRVLZVQVMZUURVOYQVQVMZVQVMZUUHUUT
      VLZRVLZUUPUVDVQVMUUQVQVMZVRUUOUVBVFVDZUVDVFVDZUURVFVDZVGUURVRVIZVEZVSZUVB
      UVDVRVIUVCUVEVRVIUUAUUMUVNUUNUUAUUMVEUVIUVJUVMYTUUMUVIYRYTUUMVEZTVTVDZUVA
      UBVDZUVIUMYTUAUBUUTWAZUUSUAVDZUVQUUMYTUUTEVDZUVRVCEYSIUKWBJVTVDZUVPUVTUVR
      ULUMEUUTJTUAUBUFUGUJWCWDWEUUCUULGUAVDZUVSUUCUUEUUKUWBUUFAGHJOPQUAUCUDUEUF
      ULUOUPUQURUSVAWFWGUWBUUCUVSUWAUWBUUCUVSULGUUBJQUAUFUQWHWIWJWKUAUBUUSUUTWL
      WMZUVATRUBUGUHWNWOWPYRUVJYTUUMYRVOVFVDZYQVFVDUVJWQYQWRVOYQWSWOZWTUULUVMUU
      AUUCUUGUUIUVMUUJUUGUUIVEUVKUVLUUGUUPVFVDZUUQVFVDZUVKUUIUUEUUFUUDVGVJZUWFU
      UDXAUWDUUEUWHUWFWQVOUUDXBWIWKZUWAUUIUWGULUUHJPUAUFUOWNXCZUUPUUQWSWMUUGUWF
      VGUUPVRVIZVEUWGVGUUQVRVIZVEUVLUUIUUGUWFUWKUWIUWDVGVOVRVIUUGUWKWQVGVOXDWQX
      EXFVOUUDXGXHXIUUIUWGUWLUWJUWAUUIUWLULUUHJPUAUFUOXJXCXIUWFUWGUWKUWLUVLUUPU
      UQXKXLWMXIXMZXNXOXMABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKULUMUNUOUPUQ
      URUSUTVAXPUVBUVDUURXQXRYTUUMUVCUVGXSYRUUNUVOUVGUURUVATXTVLZVMZRVLZUVCUVOU
      VFUWORAEGHUWNIJNOPQTUAUCUDUEUFULUOUPUQURUSVAUJUKUMUWNYAZYBYCUVOUVMUVQUWPU
      VCXSZUULUVMYTUUCUWMXNUWCUVPUVMUVQUWRUMUURUVAUWNTRUBUGUWQUHYDWIXRYEYFYRUUM
      UVEUVHXSZYTUUNYRUULUWSUUCYRUUGUUIUWSUUJYRUUGUUIUWSUUGUUIYRUWSUUGUUPYGVDUU
      IUUQYGVDYRUVDYGVDUWSUUGUUPUWIYHUUIUUQUWJYHYRUVDUWEYHUUPUUQUVDYIYJYKYLYMYN
      YOYP $.
      $( [1-Jan-2008] $)

    $( Lemma for ~ ubthi .  Upper bound for the operator norm of any operator
       ` T `` n ` . $)
    ubthlem13 $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\
  ( r e. RR /\ 0 < r ) ) /\ ( p ( ball ` D ) r ) C_ ( A ` k ) ) ) ->
         ( O ` ( T ` n ) ) <_ ( ( 2 / r ) x. ( 2 x. k ) ) ) $=
      ( cv cn wcel wa cr cc0 clt wbr cbl cfv co wss clno c2 cdiv cmul cle wne
      wi wral ffvelrni cnv eqid bloln mp3an12 syl ad2antlr gt0ne0 2re redivcl
      mp3an1 syldan nnre remulcl sylancr syl2an 0re 2pos ltleii divge0 mpanl12
      jca nngt0 ltle mpan sylc mulge0 syl11anc ancoms adantrl ad2ant2r
      ubthlem12 exp32 exp4d imp3a com34 imp32 exp3a r19.21aiv nmlnoubi
      syl111anc ) MVBZVCVDZNVBZVCVDZVEZUEVBZUAVDZUDVBZVFVDZVGYJVHVIZVEZVEZYHYJF
      VJVKVLYCDVKVMZVEZVEZYEIVKZJTVNVLZVDZVOYJVPVLZVOYCVQVLZVQVLZVFVDZVGUUCVRVI
      ZVEZAVBZUCVSZUUGYRVKRVKUUCUUGPVKVQVLVRVIZVTZAUAWAYRSVKUUCVRVIYFYTYDYPYFYR
      EVDZYTVCEYEIUKWBJWCVDTWCVDUUKYTULUMEYRJYSTYSWDZUJWEWFWGWHYDYNUUFYFYOYDYMU
      UFYIYMYDUUFYMYDVEUUDUUEYMUUAVFVDZUUBVFVDZUUDYDYKYLYJVGVSZUUMYJWIVOVFVDZYK
      UUOUUMWJVOYJWKWLWMZYDUUPYCVFVDZUUNWJYCWNZVOYCWOWPZUUAUUBWOWQYMUUMVGUUAVRV
      IZVEUUNVGUUBVRVIZVEUUEYDYMUUMUVAUUQUUPVGVOVRVIZYMUVAWJVGVOWRWJWSWTZVOYJXA
      XBXCYDUUNUVBUUTYDUURVGYCVRVIZUVBUUSYDUURVGYCVHVIZUVEUUSYCXDVGVFVDUURUVFUV
      EVTWRVGYCXEXFXGUUPUVCUURUVEVEUVBWJUVDVOYCXHXBXIXCUUAUUBXHWQXCXJXKXLYQUUJA
      UAYQUUGUAVDZUUHUUIYGYNYOUVGUUHVEZUUIVTYGYNUVHYOUUIYGYIYMUVHYOUUIVTZVTYGYI
      YMUVHUVIYGYIYMUVHVEVEYOUUIABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKULUMU
      NUOUPUQURUSUTVAXMXNXOXPXQXRXSXTAUUCYRJPYSRSTUAUCUFUSUOUHUIUULULUMYAYB $.
      $( [25-Jul-2009] $) $( [1-Jan-2008] $)

    $( Lemma for ~ ubthi .  Upper bound for the operator norm of any operator
       ` T `` n ` . $)
    ubthlem13OLD $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\
  ( r e. RR /\ 0 < r ) ) /\ ( p ( ball ` D ) r ) C_ ( A ` k ) ) ) ->
         ( O ` ( T ` n ) ) <_ ( ( 2 / r ) x. ( 2 x. k ) ) ) $=
      ( cv cn wcel wa cr cc0 clt wbr cbl cfv co wss clno c2 cdiv cmul cle wne
      wi wral ffvelrni cnv eqid bloln mp3an12 syl ad2antlr gt0ne0 2re redivcl
      mp3an1 syldan nnre remulcl sylancr syl2an 0re 2pos ltleii divge0 mpanl12
      jca nngt0 ltle mpan sylc mulge0OLD an4s syl11anc ancoms adantrl ad2ant2r
      ubthlem12 exp32 exp4d imp3a com34 imp32 exp3a r19.21aiv nmlnoubi
      syl111anc ) MVBZVCVDZNVBZVCVDZVEZUEVBZUAVDZUDVBZVFVDZVGYKVHVIZVEZVEZYIYKF
      VJVKVLYDDVKVMZVEZVEZYFIVKZJTVNVLZVDZVOYKVPVLZVOYDVQVLZVQVLZVFVDZVGUUDVRVI
      ZVEZAVBZUCVSZUUHYSVKRVKUUDUUHPVKVQVLVRVIZVTZAUAWAYSSVKUUDVRVIYGUUAYEYQYGY
      SEVDZUUAVCEYFIUKWBJWCVDTWCVDUULUUAULUMEYSJYTTYTWDZUJWEWFWGWHYEYOUUGYGYPYE
      YNUUGYJYNYEUUGYNYEVEUUEUUFYNUUBVFVDZUUCVFVDZUUEYEYLYMYKVGVSZUUNYKWIVOVFVD
      ZYLUUPUUNWJVOYKWKWLWMZYEUUQYDVFVDZUUOWJYDWNZVOYDWOWPZUUBUUCWOWQYNUUNVGUUB
      VRVIZVEUUOVGUUCVRVIZVEUUFYEYNUUNUVBUURUUQVGVOVRVIZYNUVBWJVGVOWRWJWSWTZVOY
      KXAXBXCYEUUOUVCUVAYEUUSVGYDVRVIZUVCUUTYEUUSVGYDVHVIZUVFUUTYDXDVGVFVDUUSUV
      GUVFVTWRVGYDXEXFXGUUQUVDUUSUVFVEUVCWJUVEUUQUUSUVDUVFUVCVOYDXHXIXBXJXCUUNU
      UOUVBUVCUUFUUBUUCXHXIWQXCXKXLXMYRUUKAUAYRUUHUAVDZUUIUUJYHYOYPUVHUUIVEZUUJ
      VTYHYOUVIYPUUJYHYJYNUVIYPUUJVTZVTYHYJYNUVIUVJYHYJYNUVIVEVEYPUUJABCDEFGHIJ
      KLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKULUMUNUOUPUQURUSUTVAXNXOXPXQXRXSXTYAAUUDY
      SJPYTRSTUAUCUFUSUOUHUIUUMULUMYBYC $.
      $( [1-Jan-2008] $)

    ${
      ubthlem14.7 $e |- U e. CBan $.
      $( Lemma for ~ ubthi .  The operator norms of the operators ` T `` n `
         have an upper bound. $)
      ubthlem14 $p |- ( A. x e. X E. c e. RR A. n e. NN ( N ` ( ( T ` n ) ` x
           ) ) <_ c -> E. d e. RR A. n e. NN ( O ` ( T ` n ) ) <_ d ) $=
        ( vk cv cfv cle wbr cn wral cr wrex cc0 clt cbl co wss wa copn eqid
        ubthlem6 wcel wi c2 cdiv cmul wne gt0ne0 2re redivcl mp3an1 syldan
        nnre remulcl sylancr syl2an ancoms adantrl adantrr ubthlem13 exp31
        com23 imp r19.21aiv wceq breq2 ralbidv rcla4ev syl11anc exp44 exp3a
        imp4a r19.23adv r19.23aivv syl ) AVEMVEZIVFZVFQVFUEVEVGVHMVIVJUEVKVLATV
        JVMUCVEZVNVHZUDVEZXRFVOVFVPVDVEZDVFVQZVRZUCVKVLZUDTVLVDVIVLXQRVFZUFVEZV
        GVHZMVIVJZUFVKVLZABCDEFIJKLVDMFVSVFZQSTUAUCUDUEUGUHUIUKULVCUNUOYJVTVAWA
        YDYIVDUDVITYAVIWBZXTTWBZVRZYCYIUCVKYMXRVKWBZXSYBYIYMYNXSYBYIWCZYKYLYNXS
        VRZYOWCYKYLYPYBYIYKYLYPVRZYBVRZVRZWDXRWEVPZWDYAWFVPZWFVPZVKWBZYEUUBVGVH
        ZMVIVJZYIYKYQUUCYBYKYPUUCYLYPYKUUCYPYTVKWBZUUAVKWBZUUCYKYNXSXRVMWGZUUFX
        RWHWDVKWBZYNUUHUUFWIWDXRWJWKWLYKUUIYAVKWBUUGWIYAWMWDYAWNWOYTUUAWNWPWQWR
        WSYSUUDMVIYKYRXPVIWBZUUDWCYKUUJYRUUDYKUUJYRUUDABCDEFGHIJKLVDMNOPQRSTUAU
        BUCUDUGUHUIUJUKULUMUNUOUPUQURUSUTVAVBWTXAXBXCXDYHUUEUFUUBVKYFUUBXEYGUUD
        MVIYFUUBYEVGXFXGXHXIXJXCXKXLXMXNXO $.
        $( [1-Jan-2008] $)
    $}
  $}

  ${
    $d c d n x B $.  $d c h j n p r x y z M $.  $d d p r N $.
    $d c d h j n p r x y z T $.  $d c d h n p r x z U $.  $d c d n x W $.
    $d c j n p r x y z X $.
    ubthi.1 $e |- X = ( BaseSet ` U ) $.
    ubthi.3 $e |- M = ( norm ` W ) $.
    ubthi.4 $e |- N = ( U normOp W ) $.
    ubthi.5 $e |- B = ( U BLnOp W ) $.
    ubthi.7 $e |- U e. CBan $.
    ubthi.8 $e |- W e. NrmCVec $.
    ${
      ubthii.6 $e |- T : NN --> B $.
      $( Inference from ~ ubthi . $)
      ubthii $p |- ( A. x e. X E. c e. RR A. n e. NN ( M ` ( ( T ` n ) ` x ) )
        <_ c -> E. d e. RR A. n e. NN ( N ` ( T ` n ) ) <_ d ) $=
        ( vy vz vj vh vp vr cv cn wcel cfv cle wbr wral crab wceq wa copab
        cims c2 cdiv co c1 cnm cmul cns cpv cnsb cba cn0v eqid cbn cnv bnnv
        ax-mp ubthlem14 ) ASTUAUEZUFUGSUETUEUBUECUHUHFUHVNUIUJUBUFUKTIULUMUNUAS
        UOZBDUPUHZUCUEUDUEUQURUSUTAUEZDVAUHZUHURUSVBUSVQDVCUHZUSDVDUHZUSZVSCDUB
        UAEVTVRDVEUHZFGHIHVFUHZDVGUHZUDUCJKLWCVHMNORDVIUGDVJUGPDVKVLQVPVHVRVHVT
        VHWBVHVSVHWDVHVOVHWAVHPVM $.
        $( [7-Nov-2007] $)
    $}

    $( Uniform Boundedness Theorem.  Let ` T ` be a sequence of bounded linear
       operators on a Banach space.  If, for every vector ` x ` , the norms of
       the operators' values are bounded, then the operators' norms are also
       bounded.  Theorem 4.7-3 of [Kreyszig] p. 249.  See also
       ~ http://en.wikipedia.org/wiki/Uniform_boundedness_principle . $)
    ubthi $p |- ( ( T : NN --> B /\ A. x e. X E. c e. RR A. n e. NN ( M ` ( (
    T ` n ) ` x ) ) <_ c ) -> E. d e. RR A. n e. NN ( N ` ( T ` n ) ) <_ d ) $=
      ( cn wf cv cfv cle wbr wral cr wrex wi c0o co csn cxp cif wceq fveq1
      fveq1d fveq2d breq1d rexralbidv ralbidv imbi12d wss oprex fconst wcel
      cnv cbn bnnv ax-mp eqid 0blo mp2an snssi fss elimf ubthii dedth imp ) RBC
      SZATZETZCUAZUAZFUAZJTZUBUCZERUDJUEUFZAIUDZWAGUAZKTZUBUCZERUDKUEUFZVRWGWKU
      GVSVTVRCRDHUHUIZUJZUKZULZUAZUAZFUAZWDUBUCZERUDJUEUFZAIUDZWPGUAZWIUBUCZERU
      DKUEUFZUGCWNCWOUMZWGXAWKXDXEWFWTAIXEWEWSJEUERXEWCWRWDUBXEWBWQFXEVSWAWPVTC
      WOUNZUOUPUQURUSXEWJXCKEUERXEWHXBWIUBXEWAWPGXFUPUQURUTABWODEFGHIJKLMNOPQRB
      CWNRWMWNSWMBVAZRBWNSRWLDHUHVBVCWLBVDZXGDVEVDZHVEVDXHDVFVDXIPDVGVHQBDHWLWL
      VIOVJVKWLBVLVHRWMBWNVMVKVNVOVPVQ $.
      $( [7-Nov-2007] $)
  $}


