$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          Uniform Boundedness Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d p A $.  $d h j m n y z N $.  $d h n z P $.  $d h j m n y z T $.
    $d j p y z X $.  $d h j k m n p y z $.
    ubthlem1.1 $e |- X = ( Base ` U ) $.
    ubthlem1.11 $e |- A = { <. j , y >. | ( j e. NN /\ y = { z e. X |
       A. h e. NN ( N ` ( ( T ` h ) ` z ) ) <_ j } ) } $.
    $( Lemma for ~ ubthi .  Membership in ` A `` k ` , the set of all vectors
        ` ( T `` n ) `` z ` whose norm is less than ` k ` . $)
    ubthlem1 $p |- ( k e. NN -> ( P e. ( A ` k ) <-> ( P e. X /\
       A. n e. NN ( N ` ( ( T ` n ) ` P ) ) <_ k ) ) ) $=
      ( cv cn wcel cfv cle wbr wral wa crab wceq breq2 ralbidv rabbisdv cba
      cvv fvex eqeltr rabex fvopab4 eleq2d fveq2 fveq2d breq1d elrab syl6bb
      fveq1d cbvralv anbi2i ) IOZPQZDVCCRZQZDLQZDGOZERZRZKRZVCSTZGPUAZUBZVGDJOZ
      ERZRZKRZVCSTZJPUAZUBVDVFDBOZVIRZKRZVCSTZGPUAZBLUCZQVNVDVEWFDHAVCWCHOZSTZG
      PUAZBLUCWFPCWGVCUDZWIWEBLWJWHWDGPWGVCWCSUEUFUGNWEBLLFUHRUIMFUHUJUKULUMUNW
      EVMBDLWADUDZWDVLGPWKWCVKVCSWKWBVJKWADVIUOUPUQUFURUSVMVTVGVLVSGJPVHVOUDZVK
      VRVCSWLVJVQKWLDVIVPVHVOEUOUTUPUQVAVBUS $.
      $( [8-Jan-2008] $) $( [19-Dec-2007] $)

    $( Lemma for ~ ubthi .  ` A `` k ` is a set of vectors. $)
    ubthlem2 $p |- ( k e. NN -> ( A ` k ) (_ X ) $=
      ( vp vm cv cn wcel cfv cle wbr wral ubthlem1 pm3.26bda ex ssrdv ) HOZPQZM
      UFCRZJUGMOZUHQZUIJQZUGUJUKUINODRRIRUFSTNPUAABCUIDEFGHNIJKLUBUCUDUE $.
      $( [10-Jan-2008] $) $( [7-Nov-2007] $)
  $}

  ${
    $d g m A $.  $d m C $.  $d g m x D $.  $d g G $.  $d n j h z y N $.
    $d g n j m h z y x T $.  $d g W $.  $d g j z y X $.  $d g m x Y $.
    $d f g n j k m h w z y x $.
    ubthlem3.1 $e |- X = ( Base ` U ) $.
    ubthlem3.2 $e |- Y = ( Base ` W ) $.
    ubthlem3.3 $e |- N = ( norm ` W ) $.
    ubthlem3.5 $e |- B = ( U BLnOp W ) $.
    ubthlem3.6 $e |- T : NN --> B $.
    ubthlem3.u $e |- U e. NrmCVec $.
    ubthlem3.w $e |- W e. NrmCVec $.
    ubthlem3.9c $e |- C = ( IndMet ` U ) $.
    ubthlem3.9d $e |- D = ( IndMet ` W ) $.
    ubthlem3.11 $e |- A = { <. j , y >. | ( j e. NN /\ y = { z e. X |
       A. h e. NN ( N ` ( ( T ` h ) ` z ) ) <_ j } ) } $.
    ubthlem3.12 $e |- G = { <. m , x >. | ( m e. NN /\ x =
                 ( ( T ` n ) ` ( f ` m ) ) ) } $.
    $( Lemma for ~ ubthi .  The limit of any sequence in ` A `` k ` has
       operator value norm less than ` k ` , for any bounded linear operator
       ` T `` n ` , using ~ metcn4i (with continuity shown by ~ blocn2 ) and
       the properties of ` A `` k ` . $)
    ubthlem3 $p |- ( ( ( f : NN --> ( A ` k ) /\ f ( ~~>m ` C ) w ) /\
         ( k e. NN /\ n e. NN ) ) -> ( N ` ( ( T ` n ) ` w ) ) <_ k ) $=
      ( vg cn wf cv cfv clm wbr cr wcel cle wral wa cnv fvex nvlmle mp3anl1
      ffvelrn ffvelrni blof mp3an12 syl adantl ad2antlr wss fss ubthlem2
      sylan2 sylan adantlrr sylanc adantllr r19.21aiva fopab2 sylib copn ccn
      co cdm cme imsmet ax-mp eqid visset metcn4i mpanl1 blocn2 ad2antll wceq
      wb imsbai feq3 adantrr anim1i an1rs nnret ad2antrl fveq2 fveq2d fvopab4
      ubthlem1 pm3.27bda ancoms r19.21bi adantlr simprl adantr jca simprr
      eqbrtrd syl2anc ) UNUAQUOZQDUPZPUPZIUQZUQZHURUQUSZNUPZUTVAZUMUPZQUQZRUQZY
      IVBUSZUMUNVCZYGRUQYIVBUSZUNYIEUQZKUPZUOZYRYDGURUQUSZVDZYIUNVAZYEUNVAZVDZV
      DZSVEVAZYCYHYJYOVDYPUHHYGYISUMQRUAUCUDUJYDYFVFVGVHUUEOUPZYRUQZYFUQZUAVAZO
      UNVCYCUUEUUJOUNYSUUDUUGUNVAZUUJYTTUAYFUOZUUHTVAZUUJYSUUDVDZUUKVDTUAUUHYFV
      IUUDUULYSUUKUUCUULUUBUUCYFFVAZUULUNFYEIUFVJZJVEVAZUUFUUOUULUGUHFYFJSTUAUB
      UCUEVKVLVMVNVOYSUUBUUKUUMUUCUNTYRUOZUUKUUMYSUUBVDZUNTUUGYRVIYSYQTVPUURUUB
      UNYQTYRVQBCEIJLMNRTUBUKVRVSZVTWAWBWCWDOAUNUAUUIQULWEWFYFGWGUQZHWGUQZWHWIV
      AZUNGWJWJZYRUOZYTVDZYHUUEHWKVAZUVCUVFYHUUFUVGUHHSUJWLWMGWKVAZUVGUVCUVFYHU
      UQUVHUGGJUIWLWMAGHYDOYFYRQUVAUVBUVDUVDWNUVAWNZUVBWNZULDWOWPVHWQUUCUVCUUAU
      UBUUCUUOUVCUUPFGHYFJUVAUVBSUIUJUVIUVJUEUGUHWRVMWSYSUUDYTUVFUUNUVEYTYSUUBU
      VEUUCUUSUURUVEUUTTUVDWTUURUVEXAGJTUBUIUGXBTUVDUNYRXCWMWFXDXEXFWBUUBYJUUAU
      UCYIXGXHYSUUDYOYTUUNYNUMUNUUNYKUNVAZVDZYMYKYRUQZYFUQZRUQZYIVBUVKYMUVOWTUU
      NUVKYLUVNROAYKUUIUVNUNQUUGYKWTUUHUVMYFUUGYKYRXIXJULUVMYFVFXKXJVNUVMYQVAZU
      UBVDZUUCUVOYIVBUSZUVLUVQUVRPUNUUBUVPUVRPUNVCZUUBUVPUVMTVAUVSBCEUVMIJLMNPR
      TUBUKXLXMXNXOUVLUVPUUBYSUVKUVPUUDUNYQYKYRVIXPUUNUUBUVKYSUUBUUCXQXRXSUUNUU
      CUVKYSUUBUUCXTXRWBYAWDXPYB $.
      $( [10-Jan-2008] $) $( [26-Dec-2007] $)
  $}

  ${
    $d c f k m n p r x A $.  $d f k m n r x D $.  $d f k p r x J $.
    $d h j k n y z N $.  $d h j k m n w y z T $.  $d m w W $.
    $d c f j k n x y z X $.  $d m w Y $.  $d f h w x $.
    ubthlem4.1 $e |- X = ( Base ` U ) $.
    ubthlem4.2 $e |- Y = ( Base ` W ) $.
    ubthlem4.3 $e |- N = ( norm ` W ) $.
    ubthlem4.5 $e |- B = ( U BLnOp W ) $.
    ubthlem4.6 $e |- T : NN --> B $.
    ubthlem4.7 $e |- U e. CBan $.
    ubthlem4.8 $e |- W e. NrmCVec $.
    ubthlem4.9 $e |- D = ( IndMet ` U ) $.
    ubthlem4.10 $e |- J = ( Open ` D ) $.
    ubthlem4.11 $e |- A = { <. j , y >. | ( j e. NN /\ y = { z e. X |
       A. h e. NN ( N ` ( ( T ` h ) ` z ) ) <_ j } ) } $.
    $( Lemma for ~ ubthi .  The set ` A `` k ` is therefore closed by
       ~ metcld .  (The hypothesis ` U e. CBan ` could be weakened to
       ` U e. NrmCVec ` .) $)
    ubthlem4 $p |- ( k e. NN -> ( A ` k ) e. ( Clsd ` J ) ) $=
      ( vf vx vn vw vm cv cn wcel cfv ccld wf clm wbr wa wi wal cle wral cnv
      cbn bnnv ax-mp visset nvlmcl mpan ad2antll cims wceq copab eqid ubthlem3
      ancoms an1rs r19.21aiva jca ex ubthlem1 sylibrd 19.21aivv wss wb
      ubthlem2 cme imsmet imsbai metcld syl mpbird ) JUKZULUMZWNCUNZKUOUNUMZULW
      PUFUKZUPZWRUGUKZEUQUNURZUSZWTWPUMZUTZUFVAUGVAZWOXDUGUFWOXBWTNUMZWTUHUKZFU
      NZUNLUNWNVBURZUHULVCZUSZXCWOXBXKWOXBUSZXFXJXAXFWOWSGVDUMZXAXFGVEUMXMUAGVF
      VGZEWTGWRNPUCUGVHVIVJVKXLXIUHULWOXGULUMZXBXIXBWOXOUSXIUIABUGCDEMVLUNZFGUF
      HIJUJUHUJUKZULUMUIUKXQWRUNXHUNVMUSUJUIVNZLMNOPQRSTXNUBUCXPVOUEXRVOVPVQVRV
      SVTWAABCWTFGHIJUHLNPUEWBWCWDWOWPNWEZWQXEWFZABCFGHIJLNPUEWGEWHUMZXSXTXMYAX
      NEGUCWIVGUGEUFKWPNEGNPUCXNWJUDWKVJWLWM $.
      $( [14-Jan-2008] $) $( [7-Nov-2007] $)

    $( Lemma for ~ ubthi .  The union of all ` A `` k ` equals the base set,
       provided the values of the operators ` T `` n ` are bounded for each
       vector ` x ` . $)
    ubthlem5 $p |- ( A. x e. X E. c e. RR A. n e. NN
   ( N ` ( ( T ` n ) ` x ) ) <_ c -> U_ k e. NN ( A ` k ) = X ) $=
      ( cv cfv cle wbr cn wral cr wrex ciun wss iunss ubthlem2 mprgbir a1i
      wcel wa clt arch ad2antlr wi ltlet nnret sylan2 adantll adantlr letrt wf
      ffvelrn ffvelrni cnv cbn bnnv ax-mp blof mp3an12 syl sylan ancoms nvcl
      mpan simplr adantr syl3anc exp4b com34 com23 imp31 r19.20dva ex imp
      simpll jctild wb ubthlem1 sylibrd an1rs syld r19.22dva mpd r19.23adva
      eliun syl6ibr r19.20i dfss3 sylibr eqssd ) AUIZLUIZGUJZUJZNUJZRUIZUKULZLU
      MUNZRUOUPZAPUNZKUMKUIZDUJZUQZPYGPURZYDYHYFPURKUMKUMYFPUSBCDGHIJKNPSUHUTVA
      VBYDXOYGVCZAPUNPYGURYCYIAPXOPVCZYCXOYFVCZKUMUPZYIYJYCYLYJYCYLYJYBYLRUOYJX
      TUOVCZVDZYBYLYNYBVDZXTYEVEULZKUMUPZYLYMYQYJYBXTKVFVGYOYPYKKUMYOYEUMVCZVDY
      PXTYEUKULZYKYNYRYPYSVHZYBYMYRYTYJYMYEUOVCZYTYRXTYEVIYEVJZVKVLVMYNYRYBYSYK
      VHYNYRVDZYBVDZYSYJXSYEUKULZLUMUNZVDZYKUUDYSUUFYJUUCYBYSUUFVHUUCYSYBUUFUUC
      YSYBUUFVHUUCYSVDYAUUELUMUUCYSXPUMVCZYAUUEVHZUUCUUHYSUUIUUCUUHYAYSUUEUUCUU
      HYAYSUUEXSUOVCZYMUUAYAYSVDUUEVHUUCUUHVDXSXTYEVNYNUUHUUJYRYJUUHUUJYMYJUUHV
      DXRQVCZUUJUUHYJUUKPQXQVOZYJUUKUUHPQXOXQVPUUHXQEVCZUULUMEXPGUCVQHVRVCZOVRV
      CZUUMUULHVSVCUUNUDHVTWAUEEXQHOPQSTUBWBWCWDWEWFUUOUUKUUJUEXRONQTUAWGWHWDVM
      VMUUCYMUUHYJYMYRWIWJYRUUAYNUUHUUBVGWKWLWMWNWOWPWQWNWRUUCYJYBYJYMYRWSWJWTY
      RYKUUGXAYNYBBCDXOGHIJKLNPSUHXBVGXCXDXEXFXGWQXHWRWQKXOUMYFXIXJXKAPYGXLXMXN
      $.
      $( [10-Jan-2008] $) $( [26-Dec-2007] $)

    $( Lemma for ~ ubthi .  Using ~ bcth (via ~ bcthlem33 ), at least one
       set ` A `` k ` contains a ball. $)
    ubthlem6 $p |- ( A. x e. X E. c e. RR A. n e. NN ( N ` ( ( T ` n ) ` x
        ) ) <_ c -> E. k e. NN E. p e. X E. r e. RR ( 0 < r /\
                 ( p ( ball ` D ) r ) (_ ( A ` k ) ) ) $=
      ( cv cfv cle wbr cn wral cr wrex cnt c0 wne cc0 clt cbl co wss wa crn
      cuni wceq ciun ubthlem5 wfn crab cba cvv fvex eqeltr rabex fnopab2
      fniunfv ax-mp syl5eqr ccld wcel wb fvelrnb pm3.27 ubthlem4 adantr
      eqeltrrd r19.23aiva sylbi ssriv cbn cms bncms cn0v cnv bnnv eqid nvzcl
      ne0i imsbai cpw wf ffnfv ubthlem2 elpw sylibr rgen mpbir2an bcthlem33
      mpan2 syl wex ctop cme imsmet opntop uniopn eqcomi ntrss2 mpan sstrd
      sseld opni2 mp3an1 ntropn sylan ex sstr sylan2 expcom anim2d r19.22sdv
      syld jcad 19.22dv ne0 df-rex 3imtr4g r19.22i ) AUKZLUKGULULNULTUKUMUNLUOU
      PTUQURAPUPZKUKZDULZMUSULULZUTVAZKUOURZVBRUKZVCUNZSUKZUUKFVDULVEZUUGVFZVGZ
      RUQURZSPURZKUOURUUEDVHZVIZPVJZUUJUUEKUOUUGVKZPUUTABCDEFGHIJKLMNOPQTUAUBUC
      UDUEUFUGUHUIUJVLDUOVMZUVBUUTVJJBUOCUKIUKGULULNULJUKUMUNIUOUPZCPVNDUVDCPPH
      VOULVPUAHVOVQVRVSUJVTZKUODWAWBWCUVAUUSMWDULZVFUUJAUUSUVFUUDUUSWEZUUGUUDVJ
      ZKUOURZUUDUVFWEZUVCUVGUVIWFUVEKUOUUDDWGWBUVHUVJKUOUUFUOWEZUVHVGUUGUUDUVFU
      VKUVHWHUVKUUGUVFWEUVHBCDEFGHIJKMNOPQUAUBUCUDUEUFUGUHUIUJWIWJWKWLWMWNFKMDP
      HWOWEZFWPWEUFFHUHWQWBHWRULZPWEZPUTVAHWSWEZUVNUVLUVOUFHWTWBZHPUVMUAUVMXAXB
      WBPUVMXCWBFHPUAUHUVPXDZUIUOPXEZDXFUVCUUGUVRWEZKUOUPKUOUVRDXGUVEUVSKUOUVKU
      UGPVFZUVSBCDGHIJKNPUAUJXHZUUGPUUFDVQXIXJXKXLXMXNXOUUIUURKUOUVKUUMUUHWEZSX
      PUUMPWEZUUQVGZSXPUUIUURUVKUWBUWDSUVKUWBUWCUUQUVKUUHPUUMUVKUUHUUGPUVKUVTUU
      HUUGVFZUWAMXQWEZUVTUWEFXRWEZUWFUVOUWGUVPFHUHXSWBZFMUIXTWBZUUGMPMVIZPUWGUW
      JPVJUWHFMPUVQUIYAWBYBZYCYDXOZUWAYEYFUVKUWBUULUUNUUHVFZVGZRUQURZUUQUVKUWBU
      WOUUHMWEZUWBUWOUVKUWGUWPUWBUWOUWHRUUHFUUMMUIYGYHUVKUVTUWPUWAUWFUVTUWPUWIU
      UGMPUWKYIYDXOYJYKUVKUWNUUPRUQUVKUWMUUOUULUWMUVKUUOUWMUWEUUOUVKUUNUUHUUGYL
      UWLYMYNYOYPYQYRYSSUUHYTUUQSPUUAUUBUUCXO $.
      $( [18-Jan-2008] $) $( [27-Dec-2007] $)
  $}

  ${
    $d h j n y z N $.  $d h n z Q $.  $d h j n y z T $.  $d j y z X $.
    $d h j k n p y z $.
    ubthlem7.1 $e |- X = ( Base ` U ) $.
    ubthlem7.7 $e |- U e. NrmCVec $.
    ubthlem7.n $e |- L = ( norm ` U ) $.
    ubthlem7.g $e |- G = ( +v ` U ) $.
    ubthlem7.m $e |- M = ( -v ` U ) $.
    ubthlem7.r $e |- R = ( .s ` U ) $.
    ubthlem7.z $e |- Z = ( 0v ` U ) $.
    ubthlem7.q $e |- Q =
            ( p G ( ( ( r / 2 ) x. ( 1 / ( L ` x ) ) ) R x ) ) $.
    $( Lemma for ~ ubthi .  Auxiliary class ` Q ` is a vector. $)
    ubthlem7 $p |- ( ( p e. X /\ ( r e. RR /\ ( x e. X /\ x =/= Z ) ) ) ->
            Q e. X ) $=
      ( cv wcel cr wne wa c2 cdiv co c1 cfv cmul cnv nvgcl mp3an1 cc nvscl
      axmulcl rehalfclt recnd cc0 recclt nvcl mpan adantr wceq wb nvz
      necon3bid biimpar sylanc syl2an simprl sylan2 syl5eqel ) KTZHUAZJTZUBUAZA
      TZHUAZVRIUCZUDZUDZUDVNVPUEUFUGZUHVRFUIZUFUGZUJUGZVRCUGZEUGZHBVOWGHUAZWHHU
      AZWBDUKUAZVOWIWJMVNWGDEHLOULUMWFUNUAZVSWIWBWKWLVSWIMWFVRCDHLQUOUMWCUNUAWE
      UNUAZWLVQWAWCWEUPVQWCVPUQURWDUNUAZWDUSUCZWMWAWDUTVSWNVTVSWDWKVSWDUBUAMVRD
      FHLNVAVBURVCVSWOVTVSWDUSVRIWKVSWDUSVDVRIVDVEMVRDFHILRNVFVBVGVHVIVJVQVSVTV
      KVIVLSVM $.
      $( [10-Jan-2008] $) $( [28-Dec-2007] $)

    $( Lemma for ~ ubthi .  Compute ` x ` in terms of auxiliary vector
        ` Q ` . $)
    ubthlem8 $p |- ( ( p e. X /\ ( ( r e. RR /\ 0 < r ) /\
               ( x e. X /\ x =/= Z ) ) ) ->
         x = ( ( ( 2 / r ) x. ( L ` x ) ) R ( Q M p ) ) ) $=
      ( cv wcel cr cc0 clt wbr wa wne c1 co c2 cdiv cfv cmul wceq cnv nvsid
      mpan ad2antrl adantl eqcomi wb w3a nvsubadd ubthlem7 adantrlr pm3.26 cc
      nvscl mp3an1 axmulcl rehalfclt recnd recclt nvcl adantr nvz necon3bid
      biimpar sylanc syl2an simprl adantlr syl3anc mpbiri opreq2d nvsass 2cn
      divclt gt0ne0t mul4t anim12i 2ne0 divcan6t mpanl12 recidt opreqan12d
      ax1cn mulid1 syl6eq eqtrd opreq1d 3eqtr2rd eqtr3d ) KTZHUAZJTZUBUAZUCXFUD
      UEZUFZATZHUAZXJIUGZUFZUFZUFZUHXJCUIZXJUJXFUKUIZXJFULZUMUIZBXDGUIZCUIZXNXP
      XJUNZXEXKYBXIXLDUOUAZXKYBMXJCDHLQUPUQURUSXOYAXSXFUJUKUIZUHXRUKUIZUMUIZXJC
      UIZCUIZXSYFUMUIZXJCUIZXPXOXTYGXSCXOXTYGUNZXDYGEUIZBUNZBYLSUTBHUAZXEYGHUAZ
      YKYMVAZXOYCYNXEYOVBYPMBXDYGDEGHLOPVCUQXEXGXMYNXHABCDEFGHIJKLMNOPQRSVDVEXE
      XNVFXNYOXEXGXMYOXHYFVGUAZXKYOXGXMUFYCYQXKYOMYFXJCDHLQVHVIYDVGUAZYEVGUAZYQ
      XGXMYDYEVJZXGYDXFVKVLZXRVGUAZXRUCUGZYSXMXRVMXKUUBXLXKXRYCXKXRUBUAMXJDFHLN
      VNUQVLVOZXKUUCXLXKXRUCXJIYCXKXRUCUNXJIUNVAMXJDFHILRNVPUQVQVRZVSZVTXGXKXLW
      AVSWBUSWCWDWEXNYJYHUNZXEXSVGUAZYQXKUUGXNYCUUHYQXKVBUUGMXSYFXJCDHLQWFUQXQV
      GUAZUUBUUHXIXMXQXRVJXFVGUAZXFUCUGZUUIXIUJVGUAZUUJUUKUUIWGUJXFWHVIXIXFXGXH
      VFVLZXFWIZVSZUUDVTYRYSYQXIXMYTXGYRXHUUAVOZUUFVTXIXKXLWAWCUSXOYIUHXJCXNYIU
      HUNXEXNYIXQYDUMUIZXRYEUMUIZUMUIZUHUUIUUBUFYRYSUFYIUUSUNXNXQXRYDYEWJXIUUIX
      MUUBUUOUUDWKXIYRXMYSUUPUUFWKVSXNUUSUHUHUMUIUHXIXMUUQUHUURUHUMUUJUUKUUQUHU
      NZXIUULUJUCUGUUJUUKUFUUTWGWLUJXFWMWNUUMUUNVSUUBUUCUURUHUNXMXRWOUUDUUEVSWP
      UHWQWRWSWTUSXAXBXC $.
      $( [17-Jan-2008] $) $( [28-Dec-2007] $)

    ${
      ubthlem9.5 $e |- B = ( U BLnOp W ) $.
      ubthlem9.6 $e |- T : NN --> B $.
      ubthlem9.8 $e |- W e. NrmCVec $.
      ubthlem9.s $e |- S = ( .s ` W ) $.
      $( Lemma for ~ ubthi .  Evaluate the operator value at ` x ` in terms of
         the operator value at ` Q - p ` . $)
      ubthlem9 $p |- ( ( n e. NN /\ ( p e. X /\ ( ( r e. RR /\ 0 < r ) /\
       ( x e. X /\ x =/= Z ) ) ) ) -> ( ( T ` n ) ` x ) =
           ( ( ( 2 / r ) x. ( L ` x ) ) S ( ( T ` n ) ` ( Q M p ) ) ) ) $=
        ( cv cn wcel cr cc0 clt wbr wa wne cfv c2 cdiv co cmul wceq ubthlem8
        fveq2d adantl clno cc cnv eqid lnomul mp3anl1 mpanl1 ffvelrni bloln
        mp3an12 syl axmulcl gt0ne0t 2cn divclt mp3an1 recnt sylan syldan nvcl
        mpan recnd syl2an adantrr nvmcl ubthlem7 adantrlr pm3.26 sylanc jca
        eqtrd ) HUIZUJUKZPUIZMUKZOUIZULUKZUMXBUNUOZUPZAUIZMUKZXFNUQZUPZUPZUPZUP
        XFWRFURZURZUSXBUTVAZXFJURZVBVAZCWTKVAZDVAZXLURZXPXQXLUREVAZXKXMXSVCWSXK
        XFXRXLACDGIJKMNOPQRSTUAUBUCUDVDVEVFXLGLVGVAZUKZXPVHUKZXQMUKZUPZXSXTVCZW
        SXKLVIUKZYBYEYFUGGVIUKZYGYBYEYFRXPXQDEXLGYALMQUBUHYAVJZVKVLVMWSXLBUKZYB
        UJBWRFUFVNYHYGYJYBRUGBXLGYALYIUEVOVPVQXKYCYDXJYCXAXEXGYCXHXNVHUKZXOVHUK
        YCXEXGXNXOVRXCXDXBUMUQZYKXBVSXBVHUKZYLYKXCUSVHUKYMYLYKVTUSXBWAWBXBWCWDW
        EXGXOYHXGXOULUKRXFGJMQSWFWGWHWIWJVFCMUKZXAYDXKYHYNXAYDRCWTGKMQUAWKWBXAX
        CXIYNXDACDGIJKMNOPQRSTUAUBUCUDWLWMXAXJWNWOWPWIWQ $.
        $( [17-Jan-2008] $) $( [28-Dec-2007] $)
    $}
  $}

  ${
    $d c k n p r x A $.  $d k n p r x D $.  $d x L $.  $d h j k n x y z N $.
    $d d k p r O $.  $d h n z Q $.  $d d h j k n p r x y z T $.
    $d x U $.  $d x W $.  $d c j k n r x y z X $.
    ubthlem10.1 $e |- X = ( Base ` U ) $.
    ubthlem10.2 $e |- Y = ( Base ` W ) $.
    ubthlem10.3 $e |- N = ( norm ` W ) $.
    ubthlem10.4 $e |- O = ( U normOp W ) $.
    ubthlem10.5 $e |- B = ( U BLnOp W ) $.
    ubthlem10.6 $e |- T : NN --> B $.
    ubthlem10.7 $e |- U e. NrmCVec $.
    ubthlem10.8 $e |- W e. NrmCVec $.
    ubthlem10.9 $e |- D = ( IndMet ` U ) $.
    ubthlem10.n $e |- L = ( norm ` U ) $.
    ubthlem10.g $e |- G = ( +v ` U ) $.
    ubthlem10.m $e |- M = ( -v ` U ) $.
    ubthlem10.r $e |- R = ( .s ` U ) $.
    ubthlem10.z $e |- Z = ( 0v ` U ) $.
    ubthlem10.11 $e |- A = { <. j , y >. | ( j e. NN /\ y = { z e. X |
       A. h e. NN ( N ` ( ( T ` h ) ` z ) ) <_ j } ) } $.
    ubthlem10.q $e |- Q =
            ( p G ( ( ( r / 2 ) x. ( 1 / ( L ` x ) ) ) R x ) ) $.
    $( Lemma for ~ ubthi .  Upper limit for the norm of an operator value at
       auxiliary vector ` Q ` . $)
    ubthlem10 $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\ ( ( r e. RR /\
   0 < r ) /\ ( x e. X /\ x =/= Z ) ) ) /\ ( p ( ball ` D ) r ) (_ ( A ` k ) )
        ) -> ( N ` ( ( T ` n ) ` Q ) ) <_ k ) $=
      ( cv cn wcel wa cfv cle wbr cr cc0 clt wne cbl co wss wral wi ubthlem1
      pm3.27bda ex ra4 syl6com com3l imp31 ssel impcom c2 cdiv c1 cmul wceq cc
      cnv w3a nvsass mpan rehalfclt recnd adantr rerecclt nvcl wb nvz
      necon3bid biimpar sylanc adantl simprl syl3anc adantlr fveq2d nvsge0
      mp3an1 halfpos2t biimpa 0re ltlet syl mpd jca nvscl pm3.26 syl2an eqtrd
      nv1 opreq2d ax1id ad2antrr 3eqtrd halfpost eqbrtrd nvelbl2 mp3anl1
      axmulcl recclt anim2i mpbird syl5eqel sylan sylan2 ) MVBZVCVDZNVBZVCVDZVE
      GUUADVFZVDZGUUCIVFVFRVFUUAVGVHZUEVBZUAVDZUDVBZVIVDZVJUUJVKVHZVEZAVBZUAVDZ
      UUNUCVLZVEZVEZVEZUUHUUJFVMVFVNZUUEVOZVEUUBUUDUUFUUGUUFUUBUUDUUGUUBUUFUUGN
      VCVPZUUDUUGVQUUBUUFUVBUUBUUFGUAVDUVBBCDGIJKLMNRUAUFUTVRVSVTUUGNVCWAWBWCWD
      GUUTVDZUVAUUFUUSUVAUVCUUFUUTUUEGWEWFUUSUUHUUJWGWHVNZWIUUNPVFZWHVNZWJVNZUU
      NHVNZOVNZUUTGUUSUVIUUTVDZUVHPVFZUUJVKVHZUURUVLUUIUURUVKUVDUUJVKUURUVKUVDU
      VFUUNHVNZPVFZWJVNZUVDWIWJVNZUVDUURUVKUVDUVMHVNZPVFZUVOUURUVHUVQPUUKUUQUVH
      UVQWKZUULUVDWLVDZUVFWLVDZUUOUVSUUKUUQVEZJWMVDZUVTUWAUUOWNUVSULUVDUVFUUNHJ
      UAUFURWOWPUUKUVTUUQUUKUVDUUJWQZWRZWSUUQUWAUUKUUQUVFUVEVIVDZUVEVJVLZUVFVIV
      DUUQUVEWTUUOUWFUUPUWCUUOUWFULUUNJPUAUFUOXAWPZWSUUOUWGUUPUUOUVEVJUUNUCUWCU
      UOUVEVJWKUUNUCWKXBULUUNJPUAUCUFUSUOXCWPXDXEZXFWRZXGUUKUUOUUPXHZXIXJXKUVDV
      IVDZVJUVDVGVHZVEZUVMUAVDZUVRUVOWKZUUMUUQUWCUWNUWOUWPULUVDUVMHJPUAUFURUOXL
      XMUUMUWLUWMUUKUWLUULUWDWSUUMVJUVDVKVHZUWMUUKUULUWQUUJXNXOUUKUWQUWMVQZUULU
      UKUWLUWRUWDVJVIVDUWLUWRXPVJUVDXQWPXRWSXSXTUWAUUOUWOUUQUWCUWAUUOUWOULUVFUU
      NHJUAUFURYAXMUWJUUOUUPYBXFYCYDUUQUVOUVPWKUUMUUQUVNWIUVDWJUWCUUOUUPUVNWIWK
      ULUUNHJPUAUCUFURUSUOYEXMYFXGUUKUVPUVDWKZUULUUQUUKUVTUWSUWEUVDYGXRYHYIUUMU
      VDUUJVKVHZUUQUUKUULUWTUUJYJXOWSYKXGUUIUVHUAVDZVEUUMUVJUVLXBZUUSUWCUUIUXAU
      UMUXBULUVHFUUHUUJJOPUAUFUPUOUNYLYMUURUXAUUIUUKUUQUXAUULUVGWLVDZUUOUXAUWBU
      WCUXCUUOUXAULUVGUUNHJUAUFURYAXMUVTUWAUXCUUKUUQUVDUVFYNUWEUVEWLVDZUWGUWAUU
      QUVEYOUUOUXDUUPUUOUVEUWHWRWSUWIXFYCUWKXFXJYPUUIUUMUUQXHXFYQVAYRYSYT $.
      $( [11-Jan-2008] $) $( [27-Dec-2007] $)

    $( Lemma for ~ ubthi .  Upper limit for the norm of an operator value at
       ` Q - p ` . $)
    ubthlem11 $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\ ( ( r e. RR
  /\ 0 < r ) /\ ( x e. X /\ x =/= Z ) ) ) /\ ( p ( ball ` D ) r ) (_ ( A ` k )
        ) ) -> ( N ` ( ( T ` n ) ` ( Q M p ) ) ) <_ ( 2 x. k ) ) $=
      ( cv cn wcel wa cr cc0 clt wbr wne cbl cfv co wss cnsb caddc c2 cmul cle
      cnv eqid nvmcl mp3an1 wf ffvelrn ffvelrni blof mp3an12 syl ubthlem7
      adantrlr syl2an sylan adantrr sylanc nvcl mpan ad2ant2lr axaddrcl nnret
      ad2antrr nvmtri ubthlem10 wral wi ubthlem1 pm3.27bda ex ra4 syl6com
      com3l imp31 ssel2 ancoms cme imsmet ax-mp imsbai blcntr mpanl1 adantlrr
      sylan2 le2addt jca mp2and letrd wceq clno lnosub mp3anl1 bloln pm3.26
      fveq2d cc nncnt 2timest 3brtr4d ) MVBZVCVDZNVBZVCVDZVEZUEVBZUAVDZUDVBZVFV
      DZVGUUEVHVIZVEZAVBZUAVDUUIUCVJVEZVEZVEZUUCUUEFVKVLVMZYRDVLZVNZVEZVEZGYTIV
      LZVLZUUCUURVLZTVOVLZVMZRVLZYRYRVPVMZGUUCQVMUURVLZRVLZVQYRVRVMZVSUUQUVCUUS
      RVLZUUTRVLZVPVMZUVDUUAUULUVCVFVDZYSUUOUUAUULVEZUVBUBVDZUVKUUSUBVDZUUTUBVD
      ZUVMUVLTVTVDZUVNUVOUVMUMUUSUUTTUVAUBUGUVAWAZWBWCUAUBUURWDZGUAVDZUVNUUAUUL
      UAUBGUURWEUUAUUREVDZUVRVCEYTIUKWFZJVTVDZUVPUVTUVRULUMEUURJTUAUBUFUGUJWGWH
      WIZUUDUUFUUJUVSUUGAGHJOPQUAUCUDUEUFULUOUPUQURUSVAWJZWKWLZUUAUUDUVOUUKUVRU
      UDUVOUUAUAUBUUCUURWEUWCWMZWNZWOUVPUVMUVKUMUVBTRUBUGUHWPWQWIWRUUAUULUVJVFV
      DZYSUUOUVHVFVDZUVIVFVDZUWHUVLUVHUVIWSUVLUVNUWIUWEUVPUVNUWIUMUUSTRUBUGUHWP
      WQWIZUUAUUDUWJUUKUUAUUDVEUVOUWJUWFUVPUVOUWJUMUUTTRUBUGUHWPWQWIWNZWOWRYSUV
      DVFVDZUUAUUPYRVFVDZUWNUWMYSYRYRWSYRWTZUWOWOXAUUAUULUVCUVJVSVIZYSUUOUVNUVO
      UWPUVLUVPUVNUVOUWPUMUUSUUTTUVARUBUGUVQUHXBWCUWEUWGWOWRUUQUVHYRVSVIZUVIYRV
      SVIZUVJUVDVSVIZABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKULUMUNUOUPUQURUS
      UTVAXCUUBUUCUUNVDZUWRUUPYSUUAUWTUWRUWTYSUUAUWRYSUWTUWRNVCXDZUUAUWRXEYSUWT
      UXAYSUWTUUDUXABCDUUCIJKLMNRUAUFUTXFXGXHUWRNVCXIXJXKXLUUDUUHUUOUWTUUJUUCUU
      MVDZUUOUWTUUDUUHVEUUOUXBUWTUUMUUNUUCXMXNFXOVDZUUDUUHUXBUWBUXCULFJUNXPXQFU
      UCUUEUAFJUAUFUNULXRXSXTWMYAYBUWIUWJVEZUWNUWNVEZUWQUWRVEUWSXEUUQUVHUVIYRYR
      YCUUAUULUXDYSUUOUVLUWIUWJUWKUWLYDWRYSUXEUUAUUPYSUWNUWNUWOUWOYDXAWOYEYFUUA
      UULUVFUVCYGYSUUOUVLUVEUVBRUURJTYHVMZVDZUVSUUDVEZUVEUVBYGZUUAUULUVPUXGUXHU
      XIUMUWBUVPUXGUXHUXIULGUUCUURJUXFQUVATUAUFUQUVQUXFWAZYIYJXTUUAUVTUXGUWAUWB
      UVPUVTUXGULUMEUURJUXFTUXJUJYKWHWIUUDUUFUUJUXHUUGUUDUUFUUJVEZVEUVSUUDUWDUU
      DUXKYLYDWKWLYMWRYSUVGUVDYGZUUAUUPYSYRYNVDUXLYRYOYRYPWIXAYQ $.
      $( [13-Jan-2008] $) $( [31-Dec-2007] $)

    $( Lemma for ~ ubthi .  Upper limit for the norm of an operator value at
       ` x ` . $)
    ubthlem12 $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\ (
  ( r e. RR /\ 0 < r ) /\ ( x e. X /\ x =/= Z ) ) ) /\ ( p ( ball ` D ) r ) (_
  ( A ` k ) ) ) -> ( N ` ( ( T ` n ) ` x ) ) <_ ( ( ( 2 / r ) x. ( 2 x. k ) )
         x. ( L ` x ) ) ) $=
      ( cv cn wcel wa cr cc0 clt wbr wne cbl cfv co wss c2 cdiv cmul cle w3a
      lemul2it wf ffvelrn ffvelrni cnv blof mp3an12 syl ubthlem7 adantrlr
      nvmcl mp3an1 ancoms syldan syl2an nvcl mpan adantll nnret 2re axmulrcl
      ad2antrr gt0ne0t redivclt mulge0t an4s 0re 2pos ltlei divge0t mpanl12
      jca nvge0 adantrr ad2antll 3jca ubthlem11 sylanc wceq cns eqid ubthlem9
      fveq2d nvsge0 eqtr2d ad2ant2lr cc mul23t recnd syl3an 3comr 3expb
      adantrrr adantrl ad2ant2r 3brtr3d ) MVBZVCVDZNVBZVCVDZVEZUEVBZUAVDZUDVBZV
      FVDZVGUUCVHVIZVEZAVBZUAVDZUUGUCVJZVEZVEZVEZUUAUUCFVKVLVMYPDVLVNZVEVEZVOUU
      CVPVMZUUGPVLZVQVMZGUUAQVMZYRIVLZVLZRVLZVQVMZUUQVOYPVQVMZVQVMZUUGUUSVLZRVL
      ZUUOUVCVQVMUUPVQVMZVRUVAVFVDZUVCVFVDZUUQVFVDZVGUUQVRVIZVEZVSZUVAUVCVRVIUV
      BUVDVRVIUUNUVAUVCUUQVTYTUULUVMUUMYTUULVEUVHUVIUVLYSUULUVHYQYSUULVEZUUTUBV
      DZUVHUAUBUUSWAZUURUAVDZUVOYSUULUAUBUURUUSWBYSUUSEVDZUVPVCEYRIUKWCJWDVDZTW
      DVDZUVRUVPULUMEUUSJTUAUBUFUGUJWEWFWGUUBUUKGUAVDZUVQUUBUUDUUJUWAUUEAGHJOPQ
      UAUCUDUEUFULUOUPUQURUSVAWHWIUWAUUBUVQUVSUWAUUBUVQULGUUAJQUAUFUQWJWKWLWMWN
      ZUVTUVOUVHUMUUTTRUBUGUHWOWPWGWQYQUVIYSUULYQYPVFVDZUVIYPWRVOVFVDZUWCUVIWSV
      OYPWTWPWGZXAUUKUVLYTUUBUUFUUHUVLUUIUUFUUHVEUVJUVKUUOVFVDZUUPVFVDZUVJUUFUU
      HUUOUUPWTUUDUUEUUCVGVJZUWFUUCXBUWDUUDUWHUWFWSVOUUCXCWKWMZUVSUUHUWGULUUGJP
      UAUFUOWOWPZWNUWFVGUUOVRVIZVEUWGVGUUPVRVIZVEUVKUUFUUHUWFUWGUWKUWLUVKUUOUUP
      XDXEUUFUWFUWKUWIUWDVGVOVRVIUUFUWKWSVGVOXFWSXGXHVOUUCXIXJXKUUHUWGUWLUWJUVS
      UUHUWLULUUGJPUAUFUOXLWPXKWNXKXMZXNXOXMABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGU
      HUIUJUKULUMUNUOUPUQURUSUTVAXPXQYSUULUVBUVFXRYQUUMUVNUVFUUQUUTTXSVLZVMZRVL
      ZUVBUVNUVEUWORAEGHUWNIJNOPQTUAUCUDUEUFULUOUPUQURUSVAUJUKUMUWNXTZYAYBUVLUV
      OUWPUVBXRZUVNUVTUVLUVOUWRUMUUQUUTUWNTRUBUGUWQUHYCWKUUKUVLYSUUBUWMXNUWBXQY
      DYEYQUULUVDUVGXRZYSUUMYQUUKUWSUUBYQUUFUUHUWSUUIYQUUFUUHUWSUUFUUHYQUWSUUOY
      FVDUUPYFVDUVCYFVDUWSUUFUUHYQUUOUUPUVCYGUUFUUOUWIYHUUHUUPUWJYHYQUVCUWEYHYI
      YJYKYLYMYNYO $.
      $( [18-Jan-2008] $) $( [1-Jan-2008] $)

    $( Lemma for ~ ubthi .  Upper bound for the operator norm of any
       operator ` T `` n ` . $)
    ubthlem13 $p |- ( ( ( k e. NN /\ n e. NN ) /\ ( ( p e. X /\
  ( r e. RR /\ 0 < r ) ) /\ ( p ( ball ` D ) r ) (_ ( A ` k ) ) ) ->
         ( O ` ( T ` n ) ) <_ ( ( 2 / r ) x. ( 2 x. k ) ) ) $=
      ( cv cfv clno co wcel c2 cdiv cmul cr cc0 cle wbr wa wne wi wral cn clt
      cbl wss eqid nmlnoubi ffvelrni cnv bloln mp3an12 syl ad2antlr axmulrcl
      gt0ne0t 2re redivclt mp3an1 syldan nnret mpan syl2an mulge0t an4s 0re
      2pos ltlei divge0t mpanl12 jca ltlet nngt0t sylc sylanc ancoms adantrl
      ad2ant2r ubthlem12 exp32 exp4d imp3a com34 imp32 exp3a r19.21aiv syl3anc
      ) NVBZIVCZJTVDVEZVFZVGUDVBZVHVEZVGMVBZVIVEZVIVEZVJVFZVKYKVLVMZVNZAVBZUCVO
      ZYOYDVCRVCYKYOPVCVIVEVLVMZVPZAUAVQYDSVCYKVLVMYIVRVFZYCVRVFZVNZUEVBZUAVFZY
      GVJVFZVKYGVSVMZVNZVNZUUBYGFVTVCVEYIDVCWAZVNZVNZAYKYDJPYERSTUAUCUFUSUOUHUI
      YEWBZULUMWCYTYFYSUUIYTYDEVFZYFVREYCIUKWDJWEVFTWEVFUULYFULUMEYDJYETUUKUJWF
      WGWHWIYSUUGYNYTUUHYSUUFYNUUCUUFYSYNUUFYSVNYLYMYHVJVFZYJVJVFZYLUUFYSYHYJWJ
      UUDUUEYGVKVOZUUMYGWKVGVJVFZUUDUUOUUMWLVGYGWMWNWOZYSYIVJVFZUUNYIWPZUUPUURU
      UNWLVGYIWJWQWHZWRUUMVKYHVLVMZVNUUNVKYJVLVMZVNYMUUFYSUUMUUNUVAUVBYMYHYJWSW
      TUUFUUMUVAUUQUUPVKVGVLVMZUUFUVAWLVKVGXAWLXBXCZVGYGXDXEXFYSUUNUVBUUTUURVKY
      IVLVMZUVBYSUUPUVCUURUVEVNUVBWLUVDUUPUURUVCUVEUVBVGYIWSWTXEUUSUURVKYIVSVMZ
      UVEYSVKVJVFUURUVFUVEVPXAVKYIXGWQUUSYIXHXIXJXFWRXFXKXLXMUUJYRAUAUUJYOUAVFZ
      YPYQUUAUUGUUHUVGYPVNZYQVPUUAUUGUVHUUHYQUUAUUCUUFUVHUUHYQVPZVPUUAUUCUUFUVH
      UVIUUAUUCUUFUVHVNVNUUHYQABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFUGUHUIUJUKULUMUNU
      OUPUQURUSUTVAXNXOXPXQXRXSXTYAYB $.
      $( [19-Jan-2008] $) $( [1-Jan-2008] $)

    ${
      ubthlem14.7 $e |- U e. CBan $.
      $( Lemma for ~ ubthi .  The operator norms of the operators ` T `` n `
         have an upper bound. $)
      ubthlem14 $p |- ( A. x e. X E. c e. RR A. n e. NN ( N ` ( ( T ` n ) ` x
           ) ) <_ c -> E. d e. RR A. n e. NN ( O ` ( T ` n ) ) <_ d ) $=
        ( vk cv cfv cle wbr cn wral cr wrex cc0 clt cbl co wss wa copn eqid
        ubthlem6 wcel wi c2 cdiv cmul wceq breq2 ralbidv rcla4ev axmulrcl wne
        gt0ne0t 2re redivclt mp3an1 syldan nnret mpan syl syl2an ancoms
        adantrl adantrr ubthlem13 exp31 com23 imp r19.21aiv sylanc exp44 exp3a
        imp4a r19.23adv r19.23aivv ) AVEMVEZIVFZVFQVFUEVEVGVHMVIVJUEVKVLATVJVMU
        CVEZVNVHZUDVEZXRFVOVFVPVDVEZDVFVQZVRZUCVKVLZUDTVLVDVIVLXQRVFZUFVEZVGVHZ
        MVIVJZUFVKVLZABCDEFIJKLVDMFVSVFZQSTUAUCUDUEUGUHUIUKULVCUNUOYJVTVAWAYDYI
        VDUDVITYAVIWBZXTTWBZVRZYCYIUCVKYMXRVKWBZXSYBYIYMYNXSYBYIWCZYKYLYNXSVRZY
        OWCYKYLYPYBYIWDXRWEVPZWDYAWFVPZWFVPZVKWBZYEYSVGVHZMVIVJZYIYKYLYPVRZYBVR
        ZVRZYHUUBUFYSVKYFYSWGYGUUAMVIYFYSYEVGWHWIWJYKUUCYTYBYKYPYTYLYPYKYTYQVKW
        BZYRVKWBZYTYPYKYQYRWKYNXSXRVMWLZUUFXRWMWDVKWBZYNUUHUUFWNWDXRWOWPWQYKYAV
        KWBZUUGYAWRUUIUUJUUGWNWDYAWKWSWTXAXBXCXDUUEUUAMVIYKUUDXPVIWBZUUAWCYKUUK
        UUDUUAYKUUKUUDUUAABCDEFGHIJKLVDMNOPQRSTUAUBUCUDUGUHUIUJUKULUMUNUOUPUQUR
        USUTVAVBXEXFXGXHXIXJXKXHXLXMXNXOWT $.
        $( [20-Jan-2008] $) $( [1-Jan-2008] $)
    $}
  $}

  ${
    $d c d n x B $.  $d c h j n p r x y z M $.  $d d p r N $.
    $d c d h j n p r x y z T $.  $d c d h n p r x z U $.  $d c d n x W $.
    $d c j n p r x y z X $.
    ubthi.1 $e |- X = ( Base ` U ) $.
    ubthi.3 $e |- M = ( norm ` W ) $.
    ubthi.4 $e |- N = ( U normOp W ) $.
    ubthi.5 $e |- B = ( U BLnOp W ) $.
    ubthi.7 $e |- U e. CBan $.
    ubthi.8 $e |- W e. NrmCVec $.
    ${
      ubthii.6 $e |- T : NN --> B $.
      $( Inference from ~ ubthi . $)
      ubthii $p |- ( A. x e. X E. c e. RR A. n e. NN ( M ` ( ( T ` n ) ` x ) )
        <_ c -> E. d e. RR A. n e. NN ( N ` ( T ` n ) ) <_ d ) $=
        ( vy vz vj vh vp vr cv cn wcel cfv cle wbr wral crab wceq wa copab
        cims c2 cdiv co c1 cnm cmul cns cpv cnsb cba cn0v eqid cbn cnv bnnv
        ax-mp ubthlem14 ) ASTUAUEZUFUGSUETUEUBUECUHUHFUHVNUIUJUBUFUKTIULUMUNUAS
        UOZBDUPUHZUCUEUDUEUQURUSUTAUEZDVAUHZUHURUSVBUSVQDVCUHZUSDVDUHZUSZVSCDUB
        UAEVTVRDVEUHZFGHIHVFUHZDVGUHZUDUCJKLWCVHMNORDVIUGDVJUGPDVKVLQVPVHVRVHVT
        VHWBVHVSVHWDVHVOVHWAVHPVM $.
        $( [21-Jan-2008] $) $( [7-Nov-2007] $)
    $}

    $( Uniform Boundedness Theorem.  Let ` T ` be a sequence of bounded linear
       operators on a Banach space.  If, for every vector ` x ` , the norms of
       the operators' values are bounded, then the operators' norms are also
       bounded.  Theorem 4.7-3 of [Kreyszig] p. 249.  See also
       ~ http://en.wikipedia.org/wiki/Uniform_boundedness_principle . $)
    ubthi $p |- ( ( T : NN --> B /\ A. x e. X E. c e. RR A. n e. NN ( M ` ( (
    T ` n ) ` x ) ) <_ c ) -> E. d e. RR A. n e. NN ( N ` ( T ` n ) ) <_ d ) $=
      ( cn wf cv cfv cle wbr wral cr wrex wi c0o co csn cxp cif wceq fveq1
      fveq1d fveq2d breq1d rexralbidv ralbidv imbi12d wss oprex fconst wcel
      cnv cbn bnnv ax-mp eqid 0blo mp2an snssi fss elimf ubthii dedth imp ) RBC
      SZATZETZCUAZUAZFUAZJTZUBUCZERUDJUEUFZAIUDZWAGUAZKTZUBUCZERUDKUEUFZVRWGWKU
      GVSVTVRCRDHUHUIZUJZUKZULZUAZUAZFUAZWDUBUCZERUDJUEUFZAIUDZWPGUAZWIUBUCZERU
      DKUEUFZUGCWNCWOUMZWGXAWKXDXEWFWTAIXEWEWSJEUERXEWCWRWDUBXEWBWQFXEVSWAWPVTC
      WOUNZUOUPUQURUSXEWJXCKEUERXEWHXBWIUBXEWAWPGXFUPUQURUTABWODEFGHIJKLMNOPQRB
      CWNRWMWNSWMBVAZRBWNSRWLDHUHVBVCWLBVDZXGDVEVDZHVEVDXHDVFVDXIPDVGVHQBDHWLWL
      VIOVJVKWLBVLVHRWMBWNVMVKVNVOVPVQ $.
      $( [21-Jan-2008] $) $( [7-Nov-2007] $)
  $}


