$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          Uniform Boundedness Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d c k n r x y z A $.  $d c k n r t x z D $.  $d k n t x y J $.
    $d d k t x z K $.  $d c d k m n r t u x y z N $.  $d t z P $.
    $d c k n r t x y ph $.  $d d t x z R $.  $d c d k m n r t u x y z T $.
    $d c d n r t x y z U $.  $d c d n r t x y W $.  $d c d k m n r t x y z X $.
    ubth.1 $e |- X = ( BaseSet ` U ) $.
    ubth.2 $e |- N = ( norm ` W ) $.
    ${
      ubthlem.3 $e |- D = ( IndMet ` U ) $.
      ubthlem.4 $e |- J = ( MetOpen ` D ) $.
      ubthlem.5 $e |- U e. CBan $.
      ubthlem.6 $e |- W e. NrmCVec $.
      ubthlem.7 $e |- ( ph -> T C_ ( U BLnOp W ) ) $.
      ${
        ubthlem.8 $e |- ( ph -> A. x e. X E. c e. RR A. t e. T
                                ( N ` ( t ` x ) ) <_ c ) $.
        ubthlem.9 $e |- A = ( k e. NN |-> { z e. X |
                              A. t e. T ( N ` ( t ` z ) ) <_ k } ) $.
        $( Lemma for ~ ubth .  The function ` A ` exhibits a countable
           collection of sets that are closed, being the inverse image under
           ` t ` of the closed ball of radius ` k ` , and by assumption they
           cover ` X ` .  Thus by the Baire Category theorem ~ bcth2 , for some
           ` n ` the set ` A `` n ` has an interior, meaning that there is a
           closed ball ` { z e. X | ( y D z ) <_ r } ` in the set.
           (Contributed by Mario Carneiro, 11-Jan-2014.) $)
        ubthlem1 $p |- ( ph -> E. n e. NN E. y e. X E. r e. RR+
                        { z e. X | ( y D z ) <_ r } C_ ( A ` n ) ) $=
          ( cv cfv cnt c0 wne cn wrex co cle wbr crab wss crp ccld wf cuni wceq
          crn wral wcel wa rzal a1d ralrimiv rabid2 sylibr eqcomd eleq1d iinrab
          ciin adantl ccnv cba cima cims copn cblo ssel2 sylan ccn eqid cbn cnv
          id bnnv ax-mp blocn2 ctop cme cms bncms cmsmeti opntop imsmet uniopn2
          wb imsbai iscncl sylib syl adantlr ffvelrn biantrurd fveq2 breq1d a1i
          elrab wfn ffn 3syl cr cn0v nvzcl mpan mp3an12 ralrimiva mp3an1 clt wi
          simpr syl2an adantr cvv mpan2 elssuni sseld rexlimdva mpd wex sylancr
          ralimdva c2 sstr2 ex mp2an simpld pm5.32da fveq2d elpreima eqrdv nnre
          syl6bbr 3bitr4d nvnd metsym eqtr4d rabbiia blcld simprd imaeq2 rcla4v
          ad2antlr sylc eqeltrd iincld syl2anc eqeltrrd topcld pm2.61ne cpw frn
          fmpt cldss2 syl6ss sspwuni arch ltle impr nvcl simpllr letr syl111anc
          an32s simplrl mpan2d expr eqeltri fvmpt2 eleq2d ralbidv syl6bb bicomd
          fvex rabex sylan9bbr fnfvelrn sylbird syl6d dfss3 eqssd bcth2 mpanl12
          ne0i mp2b syl11anc sseli elpwi ntrss3 cbl ntropn opni2 syl6sseqr cdiv
          ntrss2 syl5com ad2antrr w3a jctil 2re 2pos elrpii rpdivcl cc0 halfpos
          elrp biimpa sylbi 3jca breq2 rabbidv sseq1d rcla4ev 3syld syldan jcad
          blsscls2 eximdv n0 df-rex 3imtr4g reximdva ) AKUGZFUHZLUIUHUHZUJUKZKU
          LUMZCUGZDUGZGUNZPUGZUOUPZDOUQZUYSURZPUSUMZCOUMZKULUMAULLUTUHZFVAZFVDZ
          VBZOVCZVUBAVUDEUGZUHZMUHZJUGZUOUPZEHVEZDOUQZVULVFZJULVEVUMAVVDJULAVUT
          ULVFZVGZVVDOVULVFZHUJHUJVCZVVCOVULVVHOVVCVVHVVBDOVEOVVCVCVVHVVBDOVVHV
          VBVUDOVFVVAEHVHVIVJVVBDOVKVLVMVNVVFHUJUKZVGEHVVADOUQZVPZVVCVULVVIVVKV
          VCVCVVFVVAEDHOVOVQVVIVVIVVJVULVFZEHVEZVVKVULVFZVVFVVIWJVVFVVLEHVVFVUQ
          HVFZVGZVVJVUQVRZVUCMUHZVUTUOUPZCNVSUHZUQZVTZVULVVPBVVJVWBVVPBUGZOVFZV
          WCVUQUHZMUHZVUTUOUPZVGZVWDVWEVWAVFZVGZVWCVVJVFZVWCVWBVFZVVPVWDVWGVWIV
          VPVWDVGZVWGVWEVVTVFZVWGVGVWIVWMVWNVWGVVPOVVTVUQVAZVWDVWNAVVOVWOVVEAVV
          OVGZVWOVVQVWCVTZVULVFZBNWAUHZWBUHZUTUHZVEZVWPVUQINWCUNZVFZVWOVXBVGZAH
          VXCURVVOVXDUDHVXCVUQWDWEVXDVUQLVWTWFUNVFZVXEVXCGVWSVUQILVWTNTVWSWGZUA
          VWTWGZVXCWGIWHVFZIWIVFZUBIWKWLZUCWMLWNVFZVWTWNVFZVXFVXEXBGWOVFZVXLGVX
          IGWPVFZUBGITWQWLZWRZGLUAWSWLZVWSWOVFZVXMNWIVFZVXSUCVWSNVXGWTWLZVWSVWT
          VXHWSWLBVUQLVWTOVVTVXNOLVBZVCVXQGLOGIORTVXKXCZUAXAWLZVXSVVTVWTVBVCVYA
          VWSVWTVVTVWSNVVTVVTWGZVXGUCXCZVXHXAWLXDUUAXEXFZUUBZXGZOVVTVWCVUQXHZWE
          XIVVSVWGCVWEVVTVUCVWEVCVVRVWFVUTUOVUCVWEMXJXKXMUUHUUCVWKVWHXBVVPVVAVW
          GDVWCOVUDVWCVCZVUSVWFVUTUOVYKVURVWEMVUDVWCVUQXJUUDXKZXMXLVVPVWOVUQOXN
          VWLVWJXBVYIOVVTVUQXOOVWCVWAVUQUUEXPUUIUUFVVPVWAVXAVFZVXBVWBVULVFZVVPV
          UTXQVFZVYMVVEVYOAVVOVUTUUGZUURVXSNXRUHZVVTVFZVYOVYMVYAVXTVYRUCNVVTVYQ
          VYEVYQWGZXSWLZCVWSVYQVUTVWAVWTVVTVYFVXHVVSVYQVUCVWSUNZVUTUOUPCVVTVUCV
          VTVFZVVRWUAVUTUOWUBVVRVUCVYQVWSUNZWUAVXTWUBVVRWUCVCUCVUCVWSNMVVTVYQVY
          EVYSSVXGUUJXTVXSVYRWUBWUAWUCVCVYAVYTVYQVUCVWSVVTVYFUUKYAUULXKUUMUUNYA
          XFAVVOVXBVVEVWPVWOVXBVYGUUOXGVWRVYNBVWAVXAVWCVWAVCVWQVWBVULVWCVWAVVQU
          UPVNUUQUUSUUTYBVXLVVIVVMVVNVXREHVVJLUVAYCUVBUVCVVGVVFVXLVVGVXRLOVYDUV
          DWLXLUVEYBJULVULVVCFUFUVHXEZAVUOOAVUNOUVFZURVUOOURAVUNVULWUEAVUMVUNVU
          LURWUDULVULFUVGXFVXLVULWUEURVXRLOVYDUVIWLZUVJVUNOUVKXEAVWCVUOVFZBOVEZ
          OVUOURAVWFQUGZUOUPZEHVEZQXQUMZBOVEWUHUEAWULWUGBOAVWDVGZWUKWUGQXQWUMWU
          IXQVFZVGZWUIVUTYDUPZJULUMZWUKWUGYEZWUNWUQWUMWUIJUVLVQWUOWUPWURJULWUOV
          VEVGWUPWUKVWGEHVEZWUGWUOVVEWUPWUKWUSYEWUOVVEWUPVGZVGZWUJVWGEHWVAVVOVG
          ZWUJWUIVUTUOUPZVWGWVAWVCVVOWUOVVEWUPWVCWUOWUNVYOWUPWVCYEVVEWUMWUNYFVY
          PWUIVUTUVMYGUVNYHWVBVWFXQVFZWUNVYOWUJWVCVGVWGYEWUOVVOWVDWUTWUMVVOWVDW
          UNWUMVVOVGVWNWVDAVVOVWDVWNVWPVWOVWDVWNVYHVYJWEUVSVXTVWNWVDUCVWENMVVTV
          YESUVOXTXFXGXGWUMWUNWUTVVOUVPWVBVVEVYOWUOVVEWUPVVOUVTVYPXFVWFWUIVUTUV
          QUVRUWAYQUWBWUMVVEWUSWUGYEWUNWUMVVEVGWUSVWCVUTFUHZVFZWUGVVEWVFVWDWUSV
          GZWUMWUSVVEWVFVWCVVCVFWVGVVEWVEVVCVWCVVEVVCYIVFWVEVVCVCVVBDOOIVSUHYIR
          IVSUWIUWCUWJJULVVCYIFUFUWDYJUWEVVBWUSDVWCOVYKVVAVWGEHVYLUWFXMUWGWUMWU
          SWVGWUMVWDWUSAVWDYFXIUWHUWKWUMFULXNZVVEWVFWUGYEAWVHVWDAVUMWVHWUDULVUL
          FXOXFYHWVHVVEVGZWVEVUOVWCWVIWVEVUNVFWVEVUOURULVUTFUWLWVEVUNYKXFYLWEUW
          MXGUWNYMYNYMYQYNBOVUOUWOVLUWPVXOOUJUKZVUMVUPVGVUBVXPVXJIXRUHZOVFWVJVX
          KIOWVKRWVKWGXSOWVKUWSUWTGKLFOVYCUAUWQUWRUXAAVUAVUKKULAUYRULVFZVGZVUCU
          YTVFZCYOVUCOVFZVUJVGZCYOVUAVUKWVMWVNWVPCWVMWVNWVOVUJWVMUYTOVUCWVMVXLU
          YSOURZUYTOURZVXRAVUMWVLWVQWUDVUMWVLVGUYSVULVFUYSWUEVFWVQULVULUYRFXHVU
          LWUEUYSWUFUXBUYSOUXCXPWEZUYSLOVYDUXDYPYLWVMWVNVUJWVMWVNVGVUCVWCGUXEUH
          UNZUYTURZBUSUMZVUJWVMUYTLVFZWVNWWBWVMVXLWVQWWCVXRWVSUYSLOVYDUXFYPZVXN
          WWCWVNWWBVXQBUYTGVUCLUAUXGYCWEWVMWVNWVOWWBVUJYEWVMWVRWVNWVOWVMWWCWVRW
          WDWWCUYTVYBOUYTLYKVYDUXHXFUYTOVUCWDWEWVMWVOVGZWWAVUJBUSWWEVWCUSVFZVGZ
          WWAWVTUYSURZVUEVWCYRUXIUNZUOUPZDOUQZUYSURZVUJWVMWWAWWHYEWVOWWFWVMUYTU
          YSURZWWAWWHWVMVXLWVQWWMVXRWVSUYSLOVYDUXJYPWVTUYTUYSYSUXKUXLWWGWWKWVTU
          RZWWHWWLYEWWEVXNWVOVGWWIUSVFZWWFWWIVWCYDUPZUXMWWNWWFWWEWVOVXNWVMWVOYF
          VXQUXNWWFWWOWWFWWPWWFYRUSVFWWOYRUXOUXPUXQVWCYRUXRYJZWWFWJWWFVWCXQVFZU
          XSVWCYDUPZVGWWPVWCUYAWWRWWSWWPVWCUXTUYBUYCUYDDGVUCWWIWWKVWCLOVYCUAWWK
          WGUYLYGWWKWVTUYSYSXFWWGWWOWWLVUJYEWWFWWOWWEWWQVQWWOWWLVUJVUIWWLPWWIUS
          VUFWWIVCZVUHWWKUYSWWTVUGWWJDOVUFWWIVUEUOUYEUYFUYGUYHYTXFUYIYMUYJYNYTU
          YKUYMCUYTUYNVUJCOUYOUYPUYQYN $.
          $( [10-Jan-2014] $)

        ubthlem.10 $e |- ( ph -> K e. NN ) $.
        ubthlem.11 $e |- ( ph -> P e. X ) $.
        ubthlem.12 $e |- ( ph -> R e. RR+ ) $.
        ubthlem.13 $e |- ( ph -> { z e. X | ( P D z ) <_ R } C_ ( A ` K ) ) $.
        $( Lemma for ~ ubth .  Given that there is a closed ball
           ` B ( P , R ) ` in ` A `` K ` , for any ` x e. B ( 0 , 1 ) ` , we
           have ` P + R x. x e. B ( P , R ) ` and ` P e. B ( P , R ) ` , so
           both of these have ` norm ( t ( z ) ) <_ K ` and so
           ` norm ( t ( x ) ) <_ ( norm ( t ( P ) ) + norm ( t ( P + R x. x ) )
            ) / R <_ ( K + K ) / R ` , which is our desired uniform bound.
           (Contributed by Mario Carneiro, 11-Jan-2014.) $)
        ubthlem2 $p |- ( ph -> E. d e. RR A. t e. T
                         ( ( U normOp W ) ` t ) <_ d ) $=
          ( caddc co cdiv cr wcel cv cnmo cfv cle wbr wral wrex crp cn nnrp syl
          rpaddcl syl11anc rpdivcl rpre wa cba wf cxr c1 wi ccnv cima ccld cims
          cnm copn cblo wss ssel2 sylan ccn eqid cbn cnv bnnv ax-mp blocn2 ctop
          wb cme cms bncms cmsmeti opntop imsmet cuni wceq imsbai uniopn2 mp2an
          iscncl sylib simpld rexr adantr cns cpv crab rabss ad2antrr syl111anc
          a1i rpcn opreq2 breq1d cmul cnsb fveq2d cc0 nvsge0 eqtrd nvcl ralbidv
          cc breq2 fveq2 elrab ra4 sylancr ffvelrn syl32anc syld ralrimiva sylc
          simpr nvscl nvgcl eleq1 imbi12d rcla4cv metsym imsdval nvpncan2 rpge0
          3eqtrd jca mulid1 eqcomd breq12d clt adantl 1re lemul2 bitr4d rabbidv
          mpan elrp cvv fvex eqeltri rabex fvmpt eleq2d syl6bb 3imtr3d ad2antlr
          com12 met0 eqbrtrd sylanbrc sseldd eleqtrd simprd imp le2add syl22anc
          nnre mpan2d clno bloln lnosub eqtr3d lnomul nvmtri eqbrtrrd ax-mulrcl
          mp3an12 ax-addrcl letr mpand lemuldiv2 sylibd adantld nmoubi rcla4ev
          ) AMMULUMZHUNUMZUOUPZDUQZJOURUMZUSZUXDUTVAZDIVBZUXHRUQZUTVAZDIVBZRUOV
          CAUXDVDUPZUXEAUXCVDUPZHVDUPZUXNAMVDUPZUXQUXOAMVEUPZUXQUHMVFVGZUXSMMVH
          VIZUJUXCHVJVIUXDVKVGZAUXIDIAUXFIUPZVLZPOVMUSZUXFVNZUXDVOUPZBUQZJWBUSZ
          USZVPUTVAZUYGUXFUSZNUSZUXDUTVAZVQZBPVBUXIUYCUYEUXFVRUYGVSLVTUSUPBOWAU
          SZWCUSZVTUSVBZUYCUXFJOWDUMZUPZUYEUYQVLZAIUYRWEUYBUYSUEIUYRUXFWFWGZUYS
          UXFLUYPWHUMUPZUYTUYRFUYOUXFJLUYPOUAUYOWIZUBUYPWIZUYRWIZJWJUPZJWKUPZUC
          JWLWMZUDWNLWOUPZUYPWOUPZVUBUYTWPFWQUPZVUIFVUFFWRUPUCFJUAWSWMWTZFLUBXA
          WMUYOWQUPZVUJOWKUPZVUMUDUYOOVUCXBWMZUYOUYPVUDXAWMBUXFLUYPPUYDVUKPLXCX
          DVULFLPFJPSUAVUHXEZUBXFWMVUMUYDUYPXCXDVUOUYOUYPUYDUYOOUYDUYDWIZVUCUDX
          EVUDXFWMXHXGXIVGXJZAUYFUYBAUXEUYFUYAUXDXKVGXLUYCUYNBPUYCUYGPUPZVLZUYJ
          GHUYGJXMUSZUMZJXNUSZUMZPUPZVVDUXFUSZNUSZMUTVAZDIVBZVLZUYMVUTGVVDFUMZH
          UTVAZVVDMEUSZUPZUYJVVJVUTGCUQZFUMZHUTVAZVVOVVMUPZVQZCPVBZVVEVVLVVNVQZ
          AVVTUYBVUSAVVQCPXOZVVMWEVVTUKVVQCPVVMXPXIXQVUTVUGGPUPZVVBPUPZVVEVUGVU
          TVUHXSZAVWCUYBVUSUIXQZVUTVUGHYKUPZVUSVWDVWEVUTUXPVWGAUXPUYBVUSUJXQZHX
          TVGZUYCVUSUUBZHUYGVVAJPSVVAWIZUUCXRZGVVBJVVCPSVVCWIZUUDXRZVVSVWACVVDP
          VVOVVDXDZVVQVVLVVRVVNVWOVVPVVKHUTVVOVVDGFYAYBVVOVVDVVMUUEUUFUUGUUAVUT
          VVLHUYIYCUMZHVPYCUMZUTVAZUYJVUTVVKVWPHVWQUTVUTVVKVVBUYHUSZVWPVUTVVKVV
          DGFUMZVVDGJYDUSZUMZUYHUSZVWSVUTVUKVWCVVEVVKVWTXDVUKVUTVULXSVWFVWNGVVD
          FPVUPUUHXRVUTVUGVVEVWCVWTVXCXDVWEVWNVWFVVDGFJVXAUYHPSVXAWIZUYHWIZUAUU
          IXRVUTVXBVVBUYHVUTVUGVWCVWDVXBVVBXDVWEVWFVWLGVVBJVVCVXAPSVWMVXDUUJXRZ
          YEUULVUTVUGHUOUPZYFHUTVAZVLZVUSVWSVWPXDVWEVUTUXPVXIVWHUXPVXGVXHHVKZHU
          UKZUUMVGZVWJHUYGVVAJUYHPSVWKVXEYGXRYHVUTVWQHVUTVWGVWQHXDVWIHUUNVGUUOU
          UPVUTUYIUOUPZVPUOUPZVXGYFHUUQVAVLZUYJVWRWPVUSVXMUYCVUGVUSVXMVUHUYGJUY
          HPSVXEYIUVCUURVXNVUTUUSXSVUTUXPVXOVWHHUVDXIZUYIVPHUUTXRUVAAVVNVVJWPUY
          BVUSAVVNVVDVVOUXFUSZNUSZMUTVAZDIVBZCPXOZUPVVJAVVMVYAVVDAUXRVVMVYAXDUH
          KMVXRKUQZUTVAZDIVBZCPXOVYAVEEVYBMXDZVYDVXTCPVYEVYCVXSDIVYBMVXRUTYLYJU
          VBUGVXTCPPJVMUSUVESJVMUVFUVGUVHUVIVGZUVJVXTVVICVVDPVWOVXSVVHDIVWOVXRV
          VGMUTVWOVXQVVFNVVOVVDUXFYMYEYBYJYNUVKXQUVLVUTVVIUYMVVEVUTVVIVVHUYMUYB
          VVIVVHVQAVUSVVIUYBVVHVVHDIYOUVNUVMVUTVVHHUYLYCUMZUXCUTVAZUYMVUTVVHVVG
          GUXFUSZNUSZULUMZUXCUTVAZVYHVUTVVHVYJMUTVAZVYLUYCVYMVUSAUYBVYMAVYMDIVB
          ZUYBVYMVQAVWCVYNAGVYAUPVWCVYNVLAGVVMVYAAVWBVVMGUKAVWCGGFUMZHUTVAZGVWB
          UPUIAVYOYFHUTAVUKVWCVYOYFXDVULUIGFPVUPUVOYPAUXPVXHUJVXKVGUVPVVQVYPCGP
          VVOGXDZVVPVYOHUTVVOGGFYAYBYNUVQUVRVYFUVSVXTVYNCGPVYQVXSVYMDIVYQVXRVYJ
          MUTVYQVXQVYINVVOGUXFYMYEYBYJYNXIUVTVYMDIYOVGUWAXLVUTVVGUOUPZVYJUOUPZM
          UOUPZVYTVVHVYMVLVYLVQVUTVUNVVFUYDUPZVYRUDVUTUYEVVEWUAUYCUYEVUSVURXLZV
          WNPUYDVVDUXFYQVIZVVFONUYDVUQTYIYPZVUTVUNVYIUYDUPZVYSUDVUTUYEVWCWUEWUB
          VWFPUYDGUXFYQVIZVYIONUYDVUQTYIYPZAVYTUYBVUSAUXRVYTUHMUWDVGXQZWUHVVGVY
          JMMUWBUWCUWEVUTVYGVYKUTVAZVYLVYHVUTVVFVYIOYDUSZUMZNUSZVYGVYKUTVUTWULH
          UYKOXMUSZUMZNUSZVYGVUTWUKWUNNVUTWUKVVBUXFUSZWUNVUTVXBUXFUSZWUKWUPVUTV
          UGVUNUXFJOUWFUMZUPZVVEVWCWUQWUKXDVWEVUNVUTUDXSZUYCWUSVUSUYCUYSWUSVUAV
          UGVUNUYSWUSVUHUDUYRUXFJWUROWURWIZVUEUWGUWNVGXLZVWNVWFVVDGUXFJWURVXAWU
          JOPSVXDWUJWIZWVAUWHYRVUTVXBVVBUXFVXFYEUWIVUTVUGVUNWUSVWGVUSWUPWUNXDVW
          EWUTWVBVWIVWJHUYGVVAWUMUXFJWUROPSVWKWUMWIZWVAUWJYRYHYEVUTVUNVXIUYKUYD
          UPZWUOVYGXDWUTVXLUYCUYEVUSWVEVURPUYDUYGUXFYQWGZHUYKWUMONUYDVUQWVDTYGX
          RYHVUTVUNWUAWUEWULVYKUTVAWUTWUCWUFVVFVYIOWUJNUYDVUQWVCTUWKXRUWLVUTVYG
          UOUPZVYKUOUPZUXCUOUPZWUIVYLVLVYHVQVUTVXGUYLUOUPZWVGVUTUXPVXGVWHVXJVGV
          UTVUNWVEWVJUDWVFUYKONUYDVUQTYIYPZHUYLUWMVIVUTVYRVYSWVHWUDWUGVVGVYJUWO
          VIAWVIUYBVUSAUXOWVIUXTUXCVKVGXQZVYGVYKUXCUWPXRUWQYSVUTWVJWVIVXOVYHUYM
          WPWVKWVLVXPUYLUXCHUWRXRUWSYSUWTYSYTBUXDUXFJUYHNUXGOPUYDSVUQVXETUXGWIV
          UHUDUXAXRYTUXMUXJRUXDUOUXKUXDXDUXLUXIDIUXKUXDUXHUTYLYJUXBVI $.
          $( [11-Jan-2014] $)
      $}

      $d d ph $.
      $( Lemma for ~ ubth .  Prove the reverse implication, using ~ nmblolbi .
         (Contributed by Mario Carneiro, 11-Jan-2014.) $)
      ubthlem3 $p |- ( ph ->
                ( A. x e. X E. c e. RR A. t e. T ( N ` ( t ` x ) ) <_ c <->
                  E. d e. RR A. t e. T ( ( U normOp W ) ` t ) <_ d ) ) $=
        ( cle vz vu vy vr vn vm vk cv cfv wbr wral cr wrex cnmo co fveq1 fveq2d
        wceq breq1d cbvralv breq2 ralbidv cbvrexv fveq2 rexbidv wa crab cn cmpt
        syl5bb wss crp cblo adantr simpr sylib cbvrabv rabbidv cbvmptv ubthlem1
        syl5eq wcel ad3antrrr ad2antrr simplrl simprl simprr ubthlem2 rexlimdva
        wi simplrr expr ex rexlimdvv mpd syl5bir cnm cba wf ssel2 sylan adantlr
        cmul ad2ant2r cnv cbn bnnv ax-mp eqid blof mp3an12 syl ffvelrn syl11anc
        simplr nvcl mpan cxr cmnf nmoxr simpllr nmogtmnf xrre syl22anc ad2antlr
        clt ax-mulrcl syl2an nmblolbi cc0 nvge0 lemul1a syl31anc letrd ralimdva
        jca rcla4ev syld ralrimdva impbid ) ABUHZCUHZUIZHUIZKUHZTUJZCEUKZKULUMZ
        BJUKZUUBFIUNUOZUIZLUHZTUJZCEUKZLULUMZUUIUAUHZUBUHZUIZHUIZUULTUJZUBEUKZL
        ULUMZUAJUKZAUUOUVBUUHUABJUVBUUPUUBUIZHUIZUUETUJZCEUKZKULUMUUPUUAURZUUHU
        VAUVGLKULUVAUVEUULTUJZCEUKUULUUEURZUVGUUTUVIUBCEUUQUUBURZUUSUVEUULTUVKU
        URUVDHUUPUUQUUBUPUQUSUTUVJUVIUVFCEUULUUEUVETVAVBVJVCUVHUVGUUGKULUVHUVFU
        UFCEUVHUVEUUDUUETUVHUVDUUCHUUPUUAUUBVDUQUSVBVEVJUTZAUVCUUOAUVCVFZUCUHZU
        UPDUOUDUHZTUJUAJVGUEUHZUFVHUULUUQUIZHUIZUFUHZTUJZUBEUKZLJVGZVIZUIVKZUDV
        LUMZUCJUMUEVHUMUUOUVMBUCUACUWCDEFUGUEGHIJUDKMNOPQRAEFIVMUOZVKZUVCSVNUVM
        UVCUUIAUVCVOUVLVPZUFUGVHUWBUVEUGUHZTUJZCEUKZUAJVGZUVSUWIURZUWBUVEUVSTUJ
        ZCEUKZUAJVGUWLUWAUWOLUAJUWAUULUUBUIZHUIZUVSTUJZCEUKUULUUPURZUWOUVTUWRUB
        CEUVKUVRUWQUVSTUVKUVQUWPHUULUUQUUBUPUQUSUTUWSUWRUWNCEUWSUWQUVEUVSTUWSUW
        PUVDHUULUUPUUBVDUQUSVBVJVQUWMUWOUWKUAJUWMUWNUWJCEUVSUWIUVETVAVBVRWAVSZV
        TUVMUWEUUOUEUCVHJUVMUVPVHWBZUVNJWBZVFZUWEUUOWJUVMUXCVFZUWDUUOUDVLUXDUVO
        VLWBZUWDUUOUXDUXEUWDVFZVFBUACUWCDUVNUVOEFUGGUVPHIJKLMNOPQRAUWGUVCUXCUXF
        SWCUVMUUIUXCUXFUWHWDUWTUVMUXAUXBUXFWEUVMUXAUXBUXFWKUXDUXEUWDWFUXDUXEUWD
        WGWHWLWIWMWNWOWMWPAUUNUUILULAUULULWBZVFZUUNUUHBJUXHUUAJWBZVFZUUNUUDUULU
        UAFWQUIZUIZXCUOZTUJZCEUKZUUHUXJUUMUXNCEUXJUUBEWBZUUMUXNUXJUXPUUMVFZVFZU
        UDUUKUXLXCUOZUXMUXRUUCIWRUIZWBZUUDULWBZUXRJUXTUUBWSZUXIUYAUXRUUBUWFWBZU
        YCUXHUXPUYDUXIUUMAUXPUYDUXGAUWGUXPUYDSEUWFUUBWTXAXBXDZFXEWBZIXEWBZUYDUY
        CFXFWBUYFQFXGXHZRUWFUUBFIJUXTMUXTXIZUWFXIZXJXKXLZUXHUXIUXQXOZJUXTUUAUUB
        XMXNUYGUYAUYBRUUCIHUXTUYINXPXQXLUXRUUKULWBZUXLULWBZUXSULWBUXRUUKXRWBZUX
        GXSUUKYFUJZUUMUYMUXRUYCUYOUYKUYFUYGUYCUYOUYHRUUBFUUJIJUXTMUYIUUJXIZXTXK
        XLAUXGUXIUXQYAZUXRUYCUYPUYKUYFUYGUYCUYPUYHRUUBFUUJIJUXTMUYIUYQYBXKXLUXJ
        UXPUUMWGZUUKUULYCYDZUXIUYNUXHUXQUYFUXIUYNUYHUUAFUXKJMUXKXIZXPXQZYEUUKUX
        LYGXNUXJUXMULWBZUXQUXHUXGUYNVUCUXIAUXGVOVUBUULUXLYGYHZVNUXRUYDUXIUUDUXS
        TUJUYEUYLUUAUWFUUBFUXKHUUJIJMVUANUYQUYJUYHRYIXNUXRUYMUXGUYNYJUXLTUJZVFZ
        UUMUXSUXMTUJUYTUYRUXIVUFUXHUXQUXIUYNVUEVUBUYFUXIVUEUYHUUAFUXKJMVUAYKXQY
        PYEUYSUUKUULUXLYLYMYNWLYOUXJVUCUXOUUHWJVUDVUCUXOUUHUUGUXOKUXMULUUEUXMUR
        UUFUXNCEUUEUXMUUDTVAVBYQWMXLYRYSWIYT $.
        $( [11-Jan-2014] $)
    $}

    ubth.3 $e |- M = ( U normOp W ) $.
    $( Uniform Boundedness Theorem, also called the Banach-Steinhaus Theorem.
       Let ` T ` be a collection of bounded linear operators on a Banach
       space.  If, for every vector ` x ` , the norms of the operators' values
       are bounded, then the operators' norms are also bounded.  Theorem 4.7-3
       of [Kreyszig] p. 249.  See also
       ~ http://en.wikipedia.org/wiki/Uniform_boundedness_principle .  (The
       proof was shortened by Mario Carneiro, 11-Jan-2014.) $)
    ubth $p |- ( ( U e. CBan /\ W e. NrmCVec /\ T C_ ( U BLnOp W ) ) ->
                 ( A. x e. X E. c e. RR A. t e. T ( N ` ( t ` x ) ) <_ c <->
                   E. d e. RR A. t e. T ( M ` t ) <_ d ) ) $=
      ( cblo co cfv cle wbr wral cr cbn wcel cnv wss cv wrex wb caddc cmul cabs
      wi cop cif cba cnmo wceq opreq1 sseq2d fveq2 syl5eq raleqdv fveq1d breq1d
      cnm ralbidv rexbidv bibi12d imbi12d opreq2 cims copn eqid cnbn bnnv ax-mp
      elimel id ubthlem3 dedth2h 3impia ) DUAUBZGUCUBZCDGNOZUDZAUEBUEZPZFPZIUEZ
      QRZBCSZITUFZAHSZWEEPZJUEZQRZBCSZJTUFZUGZWAWBWDWRUKCWADUHUIULUJULZUMZGNOZU
      DZWKAWTUNPZSZWEWTGUOOZPZWNQRZBCSZJTUFZUGZUKCWTWBGWSUMZNOZUDZWFXKVDPZPZWHQ
      RZBCSZITUFZAXCSZWEWTXKUOOZPZWNQRZBCSZJTUFZUGZUKDGWSWSDWTUPZWDXBWRXJYFWCXA
      CDWTGNUQURYFWLXDWQXIYFWKAHXCYFHDUNPXCKDWTUNUSUTVAYFWPXHJTYFWOXGBCYFWMXFWN
      QYFWEEXEYFEDGUOOXEMDWTGUOUQUTVBVCVEVFVGVHGXKUPZXBXMXJYEYGXAXLCGXKWTNVIURY
      GXDXSXIYDYGWKXRAXCYGWJXQITYGWIXPBCYGWGXOWHQYGWFFXNYGFGVDPXNLGXKVDUSUTVBVC
      VEVFVEYGXHYCJTYGXGYBBCYGXFYAWNQYGWEXEXTGXKWTUOVIVBVCVEVFVGVHXMABWTVJPZCWT
      YHVKPZXNXKXCIJXCVLXNVLYHVLYIVLDWSUAWSWSVLVMZVPGWSUCWSUAUBWSUCUBYJWSVNVOVP
      XMVQVRVSVT $.
      $( [11-Jan-2014] $) $( [7-Nov-2007] $)
  $}


